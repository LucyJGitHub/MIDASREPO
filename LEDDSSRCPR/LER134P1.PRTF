      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60J                                                    *
/*EXI *  TEXT('Midas Fee Payments Due A/C Keys Detail Rpt')
/*ADD *  CHGPRTF FILE(LER134P1) SAVE(*YES)                            *
     A******************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*  PRTF/LER134P1 - Print File for Fee Payments Due A/C Key      *
     A*                  Generation Report                            *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      *  Prev Amend No. CLE042             Date 06Sep05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 101842             Date 22May96               *
     A*                 100374             Date 22May96               *
     A*                 BH3479             DATE 17AUG92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
     A*  101842 - Change the heading of the report to avoid confusion *
     A*  100374 - Include fee sequence on the report                  *
     A*  BH3479 - New Report. No explanation here. Try looking in     *
     A*           RPG/LER134's header box, or call Ruth.              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R LER134A1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(2)
     A                                     6'REFERENCE'
     A                                    16'LER134P1'
     A            BJURPT        53        30
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
     A*                                              Begin /101842/
     A************                        40'FEE PAYMENTS DUE A/C KEYS GENERATI-
     A************                          ON'
     A                                    36'ACCOUNT KEYS GENERATED FOR +
     A                                       CALCULATED FEES DUE'
     A*                                              End   /101842/
     A  24                                  SPACEA(2)
     A N24                                +1'BRANCH'
     A N24        STRBRC         3        +1
     A N24                                +1'-'
     A N24        BRNM          30A       +1
     A N24                                  SPACEA(2)
     A
     A          R LER134A2
     A                                     5'CUSTOMER'
     A                                    +3'FACILITY'
     A*                                              Begin /100374/
     A                                    +3'FEE SEQ'
     A*                                              End   /100374/
     A                                    +3'ACCOUNT KEY'
     A                                    +8'AMOUNT'
     A                                   +12'CURRENCY'
     A                                      SPACEA(1)
     A                                     5'--------'
     A                                    +3'--------'
     A*                                              Begin /100374/
     A                                    +3'-------'
     A*                                              End   /100374/
     A                                    +3'-----------'
     A                                    +8'------'
     A                                   +12'--------'
     A                                      SPACEA(2)
     A
     A          R LER134A3                  SPACEA(1)
      * Begin /CSD027/
     A**********  LKCNUM         6  0      6                                                  CSD027
     A            LKCNUM         6A        6
      * End /CSD027/
     A            FTYP           3  0     17
     A                                    20'/'
     A            FSEQ           2  0     21
     A*                                              Begin /100374/
     A************LKAKEY        10        27
     A**30********EAMT0         13  0     38EDTCDE(J)
     A**31********EAMT1         13  1     38EDTCDE(J)
     A**32********EAMT2         13  2     38EDTCDE(J)
     A**33********EAMT3         13  3     38EDTCDE(J)
     A************LKECCY         3        66
     A            LKFSEQ         2  0     29
     A*                                              Begin /CLE042/
     A************LKAKEY********10********37                                                  CLE042
     A            LKAKEY        14        36                                                  CLE042
     A*                                              End   /CLE042/
     A  30        EAMT0         13  0     50EDTCDE(J)
     A  31        EAMT1         13  1     50EDTCDE(J)
     A  32        EAMT2         13  2     50EDTCDE(J)
     A  33        EAMT3         13  3     50EDTCDE(J)
     A            LKECCY         3        76
     A*                                              End   /100374/
     A
     A          R LER134A4                  SPACEB(3)
     A                                     5'TOTAL ACCOUNT KEYS GENERATED'
     A            NKEY           5  0     +3EDTCDE(Z)
     A                                      SPACEA(3)
     A
     A  24                                50'***  END OF REPORT  ***'
     A
     A N24                                46'***  END OF REPORT FOR +
     A                                      BRANCH'
     A N24        STRBRC         3        76
     A N24                                81'***'
