      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Fee Accruals - Gen.d Keys - Audit')              *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*  PRTF/LER135AU - Print File for Calculated fee                *
     A*                  accruals account keys generated              *
     A*                  report - Audit.                              *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE134             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 XNNNNN             Date DDMmmYY               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A* CLE134 - Past Due Call Loan Processing                        *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*
     A          R LER135C1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE LER135AU'
     A            BJURPT        53        30
     A                                   100'DATE'
     A            BJMRDT         7       105
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z) SPACEA(2)
     A                                    43'FEE ACCRUALS ACCOUNT KEYS GENERATE-
     A                                      D - AUDIT' SPACEA(1)
     A                                    43'-----------------------------------
     A                                      ---------' SPACEA(2)
     A
     A          R LER135C2
     A                                      SPACEB(3)
     A                                     5'FILE CONTROL TOTALS:'
     A                                      SPACEA(1)
     A                                     5'--------------------'
     A
     A                                    12'ACCOUNT KEYS FILE'
     A                                      SPACEB(3)
     A                                      SPACEA(1)
     A                                    12'-----------------'
     A*
     A                                    12'RECORDS ON FILE - BEFORE'
     A                                      SPACEB(2)
     A            NORE           5  0     50EDTCDE(Z)
     A            VLRF          15  0     60EDTCDE(Z)
     A            VLRL           3  0     75
     A                                    12'RECORDS ON FILE - AFTER'
     A                                      SPACEB(2)
     A            LKNORE         5  0     50EDTCDE(Z)
     A            LKVLRF        15  0     60EDTCDE(Z)
     A            LKVLRL         3  0     75
      ** Start of Change CLE134
     A                                    12'RECORDS ON PDCL FILE - AFTER'
     A                                      SPACEB(3)
     A            LPNORE         5  0     50EDTCDE(Z)
     A            LPVLRF        15  0     60EDTCDE(Z)
     A            LPVLRL         3  0     75
     A                                      SPACEA(1)
      ** End of Change CLE134
     A
     A                                    12'EXTRACT FILE FOR FACILITIES'
     A                                      SPACEB(3)
     A                                      SPACEA(1)
     A                                    12'---------------------------'
     A
     A                                    12'RECORDS ON FILE - BEFORE'
     A                                      SPACEB(2)
     A            NPREV          5  0     50EDTCDE(Z)
     A                                    12'RECORDS ON FILE - AFTER'
     A                                      SPACEB(2)
     A            FFDNOR         5  0     50EDTCDE(Z)
     A
     A                                    45'***  END OF REPORT  ***'
     A                                      SPACEB(4)
     A
     A          R LER135C3
     A                                      SPACEB(4)
     A                                    43'NO ACCOUNT KEYS HAVE BEEN +
     A                                      GENERATED TODAY'
     A*    *********************
     A*    REPORT DATABASE ERROR
     A*    *********************
     A*
     A          R ERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8        30
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10        30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
