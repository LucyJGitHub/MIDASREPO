      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Fee Accruals - Fee Calculation Rpt.')            *
     A******************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*  PRTF/LER135P2 - Print File showing fee calculations.         *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. 261205A            Date 14Jan20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE134             Date 01Aug12               *
      *                 AR787620           Date 10Jun11               *
      *                 CLE073             Date 12May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 BUG23174           Date 24Mar09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 153736             Date 29Jan99               *
     A*                 101321             DATE 22MAY96               *
     A*                 100516             DATE 22MAY96               *
     A*                 BH3484             DATE 19AUG92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  261205A - Incorrect lending fee accrual for calc basis 6.    *
      *            Rename WDAYS to WDAYSP.                            *
      *          - Applied for MD054703                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loans                                 *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,LEH00072                                 *
     A*  CLE073 - Calculated Loan Based Fees                          *
     A*  BUG23174 - Fees Details are showing incorrect details in     *
     A*             "amount due/Past Due" when FECALC = 6.            *
     A*  153736 - Add new record format for adjustments to date       *
     A*  101321 - Change the output of the report such that it will   *
     A*           easier to understand the amounts.                   *
     A*  100516 - Past due amount should be shown on the report       *
     A*  BH3484  -  Shows 'Start Date' on Last Payment Date.          *
     A*             Also, put out a Total Outstanding line, showing   *
     A*             total past due fees now.                          *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A          R LER135B1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'LER135P2'
     A            BJURPT        53        30
     A                                   100'DATE'
     A            BJMRDT         7       105
     A*
     A                                   119'PAGE'
     A            PAGE1          4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    35'FEE ACCRUALS - FEE +
     A                                      CALCULATION REPORT'
     A  24                                  SPACEA(2)
     A N24                                73'BRANCH'
     A N24        STRBRA         3A       80
     A N24                                84'-'
     A N24        BRNM          30A       86
     A N24                                  SPACEA(2)
     A
     A
     A          R LER135B2
     A                                     2'CUSTOMER NAME:'
     A            BBCRNM        20        17
     A                                    40'CUSTOMER NUMBER:'
     A            BBCUST         6        57
     A*/COPY WNCPYSRC,LEH00633
      **Start of Change CLE073
     A************                        65'FACILITY:'                                       CLE073
     A N35                                65'FACILITY:'
     A  35                                65'FAC/LOAN'
     A*********** FHFTYP         3  0     75                                                  CLE073
     A***********                         78'/'                                               CLE073
     A*********** FHFSEQ         2  0     79                                                  CLE073
     A            FACLOA         6        75
      **End of Change CLE073
     A*/COPY WNCPYSRC,LEH00072
     A                                      SPACEA(1)
     A                                     2'ACCOUNT OFFICER'
     A            AZACON        30        +2
     A  44                                +2'MULTI CUSTOMER'
     A  45                                +2'MULTI CURRENCY'
     A                                     2'FEE TYPE'
     A                                      SPACEB(1)
     A            FECALT         2        +2
     A                                    +2'FEE SEQ.'
     A            FEFSEQ         2  0     +2
     A                                    +2'FEE CCY'
     A            FEFCCY         3        +2
     A                                    47'ORIGINAL FACILITY AMOUNT'
     A            FHFCCY         3        73
     A  40        OFAM0         13  0     78EDTCDE(J)
     A  41        OFAM1         13  1     78EDTCDE(J)
     A  42        OFAM2         13  2     78EDTCDE(J)
     A  43        OFAM3         13  3     78EDTCDE(J)
     A            FTYPE         43         2
     A                                      SPACEB(1)
     A                                     7'DATE'
     A                                      SPACEB(2)
     A                                    15'ACTION'
     A                                    43'AMOUNT (FAC. CCY)'
     A                                    63'%AGE'
     A                                    71'RATE'
     A                                    81'NO.DAYS'
     A                                    92'AVERAGE (FAC.CCY)'
     A                                   110'FEE AMOUNT (FEE CCY)'
     A                                      SPACEA(2)
     A
     A          R LER135B3
     A                                      SPACEA(1)
     A            FADAT          7         6
     A            ACTN          26        15
     A  40        AAMT0         13  0     43EDTCDE(J)
     A  41        AAMT1         13  1     43EDTCDE(J)
     A  42        AAMT2         13  2     43EDTCDE(J)
     A  43        AAMT3         13  3     43EDTCDE(J)
     A            DAYS           5  0     81EDTCDE(Z)
     A  40N70     WAVG0         13  0     90EDTCDE(J)
     A  41N70     WAVG1         13  1     90EDTCDE(J)
     A  42N70     WAVG2         13  2     90EDTCDE(J)
     A  43N70     WAVG3         13  3     90EDTCDE(J)
     A
     A          R LER135B4
     A                                      SPACEA(1)
     A N73                                15'CALCULATED (IN FEE CCY) AS:'
     A  30 72     AAMT0         13  0     43EDTCDE(J)
     A  31 72     AAMT1         13  1     43EDTCDE(J)
     A  32 72     AAMT2         13  2     43EDTCDE(J)
     A  33 72     AAMT3         13  3     43EDTCDE(J)
     A    N72     PCNT           6  3     62EDTCDE(3)
     A            RATE           7  5     70EDTCDE(3)
     A  30        WTOT0         13  0    110EDTCDE(J)
     A  31        WTOT1         13  1    110EDTCDE(J)
     A  32        WTOT2         13  2    110EDTCDE(J)
     A  33        WTOT3         13  3    110EDTCDE(J)
     A
     A          R LER135B5
     A                                      SPACEA(1)
     A                                    15'TOTAL FOR'
     A** Start of change BUG23174
     A**********  WDAYS          5  0     +1EDTCDE(Z)
     A** Start of change 261205A
     A**********  WDAYS         15  9     +1EDTCDE(Z)
     A            WDAYSP         6  0     +1EDTCDE(Z)
     A** End of change 261205A
     A** End of change BUG23174
     A                                    +1'DAYS AT CALCULATION BASIS'
     A            FECALC         1  0     +2
     A  30        WTOT0         13  0    110EDTCDE(J)
     A  31        WTOT1         13  1    110EDTCDE(J)
     A  32        WTOT2         13  2    110EDTCDE(J)
     A  33        WTOT3         13  3    110EDTCDE(J)
     A
     A          R LER135B6
     A                                      SPACEA(1)
     A                                    15'TOTAL FEE CALCULATED'
     A  30        WTOT0         13  0    110EDTCDE(J)
     A  31        WTOT1         13  1    110EDTCDE(J)
     A  32        WTOT2         13  2    110EDTCDE(J)
     A  33        WTOT3         13  3    110EDTCDE(J)
     A
     A*                                                      /*BH3484*/
     A          R LER135BO
     A                                      SPACEA(1)
     A                                    15'TOTAL OUTSTANDING   '
     A  30        WOSA0         13  0    110EDTCDE(J)
     A  31        WOSA1         13  1    110EDTCDE(J)
     A  32        WOSA2         13  2    110EDTCDE(J)
     A  33        WOSA3         13  3    110EDTCDE(J)
     A*                                                      /*BH3484*/
      ** Start of Change CLE134
     A          R LER135BS
     A                                      SPACEA(1)
     A                                    15'TOTAL SUSPENDED     '
     A  30        WSUS0         13  0    110EDTCDE(J)
     A  31        WSUS1         13  1    110EDTCDE(J)
     A  32        WSUS2         13  2    110EDTCDE(J)
     A  33        WSUS3         13  3    110EDTCDE(J)
      ** End of Change CLE134
     A
     A          R LER135B7
     A                                      SPACEA(1)
     A            STDAT          7         6
     A************************************15'FEE START'
     A            STNARR        16        15
     A*                                              Begin /100516/
     A          R LER135BA
     A                                      SPACEA(1)
     A                                    15'PREVIOUS OUTSTANDING'
     A  30        WPOA0         13  0    110EDTCDE(J)
     A  31        WPOA1         13  1    110EDTCDE(J)
     A  32        WPOA2         13  2    110EDTCDE(J)
     A  33        WPOA3         13  3    110EDTCDE(J)
     A*                                              End   /100516/
     A*                                              Begin /101321/
     A          R LER135BB
     A                                      SPACEA(1)
     A            FADAT          7         6
     A                                    15'PAYMENT DUE DATE'
     A*                                              End   /101321/
     A*/COPY WNCPYSRC,LER135P201
     A
     A          R LER135B8
     A                                      SPACEB(2)
     A                                    41'*** WARNING - FACILITY +
     A                                      AMOUNT HAS CHANGED DURING +
     A                                      CALCULATION PERIOD ***'
     A          R LER135B9
     A                                      SPACEB(3)
     A N79                                41'NO DETAILS TO REPORT'
     A N79                                  SPACEA(3)
     A                                    40'*** END OF REPORT FOR THIS FEE ***'
     A
     A          R LER135BR
     A                                      SPACEB(3)
     A N24                                38'***  END OF REPORT FOR BRANCH'
     A N24        STRBRA         3        68
     A N24                                73'***'
     A  24                                45'***  END OF REPORT  ***'
     A
     A*/Start 153736/*
     A          R LER135BC
     A                                      SPACEA(1)
     A                                    15'TOTAL ADJUSTMENTS TO DATE'
     A  30        WTOT0         13  0    110EDTCDE(J)
     A  31        WTOT1         13  1    110EDTCDE(J)
     A  32        WTOT2         13  2    110EDTCDE(J)
     A  33        WTOT3         13  3    110EDTCDE(J)
     A*/End 153736/*
