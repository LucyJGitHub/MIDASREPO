      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas LE Fee recalculated amount due to BV action')    *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LER135P3 - Fee recalculated amount due to BV action     *
      *                                                               *
      *  (c) Finastra International Limited 2019                      *
      *                                                               *
      *  Last Amend No. MD026969 *CREATE   Date 23Oct19               *
      *  Prev Amend No. XXxxxxxx           Date ddMmmyy               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD026969 - New Processing for Fee Settlement.                *
      *           - Applied for MD054354                              *
      *                                                               *
      *****************************************************************
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP3                    TEXT('(c) Finastra International +
     A                                      Limited 2019')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LER135P3'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    45'CALCULATED FEES WITH BACK+
     A                                      VALUED TRANSACTIONS TODAY'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    45'-------------------------+
     A                                      --------------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     5'BRANCH'
     A                                    14'CUSTOMER NO.'
     A                                    29'FACILITY'
     A                                    40'FEE SEQ'
     A                                    50'FEE CCY'
     A                                    60'LAST PAYMENT'
     A                                    75'SETTLEMENT'
     A                                    90'BACKVALUED'
     A                                      SPACEA(1)
     A                                    64'DATE'
     A                                    75'VALUE DATE'
     A                                    90'ADJUSTMENT AMOUNT'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL3
     A                                      SPACEA(1)
      *
     A            FZBRCA         3A  O     6
     A            FZCNUM         6A  O    16
     A            FZFACL         5S 0O    30
     A            FZFSEQ         2S 0O    42
     A            FZFCCY         3A  O    52
     A            RLPDT          7A  O    62
     A            RSDAT          7A  O    77
      *
     A  50        RAMTP0        13  0O    91EDTCDE(J)
     A  51        RAMTP1        13  1O    91EDTCDE(J)
     A  52        RAMTP2        13  2O    91EDTCDE(J)
     A  53        RAMTP3        13  3O    91EDTCDE(J)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP3
     A N54                                56'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
     A  54                                56'***  NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *