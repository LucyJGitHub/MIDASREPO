      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas LE Outstanding fees today report')               *
     A******************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*  PRTF/LER150P1 - Print File showing Outstanding Fees          *
     A*                                                               **
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  Last Amend No. CLE134             Date 01Aug12               *
      *  Prev Amend No. CLE139             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CAS009             Date 04May04               *
      *                 CGL029             Date 01Sep03               *
      *                 CLE034             Date 05May03               *
      *                 CLE031             Date 03Feb03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 157537             Date 29Mar99               *
      *                 CLE005             Date 22May97               *
      *                 BH3515             Date 07Sep92               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  CLE139 - Lending Fee Capitalisation                          *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE034 - Lending Enhancements Phase 1 Priority 1A            *
      *  CLE031 - Lending Enhancements.                               *
      *  157537 - Include branch code in Our Nostro field.            *
      *  CLE005 - Customer Lending Enhancements - Others (T3)         *
     A*  BH3515 - UNDERLINE keyword prevents viewing of header on outq*
     A*                                                               *
     A******************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R LER150H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'LER150P1'
     A*
     A            BJURPT        53        30
     A                                   100'DATE'
     A            BJMRDT         7       105
     A*
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    37'LENDING FEES OUTSTANDING'
     A**************************************UNDERLINE          /BH3515/
     A*                                                        /BH3515/
     A                                      SPACEA(1)
     A                                    37'------------------------'
     A                                      SPACEA(1)
     A*                                                        /BH3515/
     A
     A          R LER150H2                  SPACEA(2)
     A                                     1'BRANCH NAME'
     A            BRNM      R             13
     A                                   117'BRANCH'
     A            @AORL          3       124
      *                                              Begin /CLE005/
     A***********                           SPACEA(3)
     A N40                               123' '
     A                                      SPACEA(3)
      *                                              End   /CLE005/
     A          R LER150H3                  SPACEB(2)
     A
     A                                     5'A/C OFFICER'
     A*
     A            AZACON        30        +2
     A
     A                                      SPACEA(2)
     A
     A                                    13'LOAN/'
     A                                    74'FEE PERIOD'
     A                                    89'PAYMENT'
      **Start of Change CLE139
     A                                    98'CAPITALISE'
     A**********                          98'ADVICE/'                                         CLE139
     A                                   110'ADVICE/'
      **End of Change CLE139
      *                                              Begin /CLE005/
      **Start of Change CLE139
     A*N40******                         111' '                                               CLE139
     A N40                               122' '
      **End of Change CLE139
     A                                      SPACEA(1)
      **Start of Change CLE139
     A**40******                         111'FEE'                                             CLE139
     A  40                               122'FEE'
      **End of Change CLE139
      *                                              End   /CLE005/
     A                                      SPACEA(1)
     A                                    12'FACILITY'
     A                                    21'SEQ'
     A                                    25'FEE'
     A                                    29'CODE'
     A                                    49'CCY'
     A                                    60'AMOUNT'
     A                                    73'START'
     A                                    82'END'
     A                                    91'ON'
      **Start of Change CLE139
     A                                   101'FEE'
     A**********                          98'CONFIRM'                                         CLE139
     A                                   110'CONFIRM'
      **End of Change CLE139
      *                                              Begin /CLE005/
      **Start of Change CLE139
     A*N40******                         108' '                                               CLE139
     A N40                               119' '
      **End of Change CLE139
     A                                      SPACEA(2)
      **Start of Change CLE139
     A**40******                         108'INDICATOR'                                       CLE139
     A  40                               119'INDICATOR'
      **End of Change CLE139
      *                                              End   /CLE005/
     A                                      SPACEA(2)
     A
     A          R LER150D1                  SPACEA(1)
     A
      * Start of change CLE034
     A  32                                 2' '
     A  32                                  SPACEA(1)
      * End of change CLE034
     A  50        @BBA           6         5
     A  50        @RN           20        +1
     A  50        @RT           10        +1
     A                                      SPACEA(2)
     A            @LORF          6        12
     A            FEFSEQ    R             +3
     A            FEFCOD    R             +2
     A            AUFENV        20        +1
     A*
     A            FEFCCY    R             +1REFFLD(CCY)
     A     60     @DSAMT        13  0     53EDTWRD(' ,   ,   ,   , 0 -')
     A     61     @DSAM1        13  1     53EDTWRD('   ,   ,   , 0 . -')
     A     62     @DSAM2        13  2     53EDTWRD('  ,   ,   , 0 .  -')
     A     63     @DSAM3        13  3     53EDTWRD(' ,   ,   , 0 .   -')
     A            @STRT          7        72
     A            @END           7        +1
     A            @PAYON         5        +4
      **Start of Change CLE139
     A            @CAPI          3        +5
     A**********  @AUTST         3        +3                                                  CLE139
     A            @AUTST         3        +8
      **End of Change CLE139
      *                                              Begin /CLE005/
      **Start of Change CLE139
     A*N40******                         108' '                                               CLE139
     A N40                               119' '
      **End of Change CLE139
     A                                      SPACEA(1)
      **Start of Change CLE139
     A**40******  RPTFI     R            108REFFLD(CTOW)                                      CLE139
     A  40        RPTFI     R            119REFFLD(CTOW)
      **End of Change CLE139
      *                                              End   /CLE005/
     A                                      SPACEA(1)
     A                                    25'ACCRUED/AMORTISED TO DATE'
     A     60     @DTAMT        13  0     53EDTWRD(' ,   ,   ,   , 0 -')
     A     61     @DTAM1        13  1     53EDTWRD('   ,   ,   , 0 . -')
     A     62     @DTAM2        13  2     53EDTWRD('  ,   ,   , 0 .  -')
     A     63     @DTAM3        13  3     53EDTWRD(' ,   ,   , 0 .   -')
     A                                   120'    '
     A                                      SPACEA(1)
     A  51                                25'ADJUSTMENTS TO DATE'
     A  51 60     @DUAMT        13  0     53EDTWRD(' ,   ,   ,   , 0 -')
     A  51 61     @DUAM1        13  1     53EDTWRD('   ,   ,   , 0 . -')
     A  51 62     @DUAM2        13  2     53EDTWRD('  ,   ,   , 0 .  -')
     A  51 63     @DUAM3        13  3     53EDTWRD(' ,   ,   , 0 .   -')
     A  51                               120'    '
     A                                      SPACEA(1)
     A  51                                25'AMOUNT O/S '
     A  51        @DDAT          7        +1
     A  51 60     @DVAMT        13  0     53EDTWRD(' ,   ,   ,   , 0 -')
     A  51 61     @DVAM1        13  1     53EDTWRD('   ,   ,   , 0 . -')
     A  51 62     @DVAM2        13  2     53EDTWRD('  ,   ,   , 0 .  -')
     A  51 63     @DVAM3        13  3     53EDTWRD(' ,   ,   , 0 .   -')
     A  51                               120'    '
     A                                      SPACEA(1)
      **Start of change CAS009
     A  03                                25'NEXT PAYMENT DATE'
     A  03        R1NPYD         7        +1
     A  03                                +5'FREQUENCY'
     A  03        R1FREQ         1        +1
     A  03                                +5'DAY IN MONTH'
     A  03        R1DYIM         2        +1
     A                                      SPACEA(1)
      **End of change CAS009
      *                                              Begin /CLE005/
     A***********                         34'SETTLEMENT METHOD'
     A*********** FESTTL    R             +1REFFLD(SETT)
     A***********                         55'OURS'
     A*********** FEOURS    R             +1REFFLD(OURS)
     A*********** @OURN         20        75
     A***********                           SPACEA(1)
     A*********** @OURT         10        75
     A***********                           SPACEA(1)
     A***********                         53'THEIRS'
     A*********** FETHER    R             +1REFFLD(THEIRS)
     A*********** @THRN         35        75
     A***********                           SPACEA(1)
     A*********** @THRT         10        75
     A*N51*******                        120'    '
     A***********                           SPACEA(2)
     A
     A N40                                34'SETTLEMENT METHOD'
     A N40        FESTTL    R             +1REFFLD(SETT)
     A N40                                55'OURS'
     A**Start of change CGL029
     A*N40******  FEOURS    R             +1REFFLD(OURS)                                      CGL029
     A*N40******  @OURN         20        75                                                  CGL029
     A N40        FEOURS        18        +1
     A N40        @OURN         20        81
     A**End of change CGL029
     A                                      SPACEA(1)
     A N40        @OURT         10        75
     A                                      SPACEA(1)
     A N40                                53'THEIRS'
     A N40        FETHER    R             +1REFFLD(THEIRS)
     A N40        @THRN         35        75
     A                                      SPACEA(1)
     A N40        @THRT         10        75
      **Start of change CAS009
     A*N40N51***                         120'    '                                            CAS009
     A**********                            SPACEA(2)                                         CAS009
     A N40N51N03                         120'    '
     A                                      SPACEA(2)
     A N40N51 03                         120'    '
     A                                      SPACEA(1)
      **End of change CAS009
      *                                              Begin /CLE031/
     A  47                                20'NEXT PAYMENT DATE'
     A  47                                  SPACEB(1)
     A  47        RNXTDT         7A       39
     A  47                                58'FREQUENCY'
     A  47        RFREQU         1A       69
     A  47                                88'DAY IN MONTH'
     A  47        RDYIMN         2A      102
     A  47                                  SPACEA(1)
      *                                              End   /CLE031/
     A
      *                                              End   /CLE005/
     A          R LER150D2                  SPACEA(1)
     A
     A                                    48'CALCULATION TYPE'
     A            FECALT    R             +2
     A            @PORA          7        87
     A                                   100'RATE'
     A                                      SPACEA(1)
     A                                    20'NEXT PAYMENT DATE'
     A            @NPD           7        +1
     A                                    48'BASIS'
     A            FECALC    R             67REFFLD(CALC)
     A    N53     FEAMT1    R             77EDTCDE(1)
     A     53     @AMT13        13  3     77EDTWRD(' ,   ,   , 0 .   ')
     A            FEFRT1    R            100EDTCDE(1)
     A                                      SPACEA(1)
     A                                    20'FREQUENCY'
     A            FEFREQ    R             38REFFLD(FREQ)
     A N53        FEAMT2    R             77EDTCDE(1)
     A  53        @AMT23        13  3     77EDTWRD(' ,   ,   , 0 .   ')
     A            FEFRT2    R            100EDTCDE(1)
     A                                      SPACEA(1)
     A                                    20'DAY IN MONTH'
     A            FEDYIM    R             38REFFLD(DYIM)
     A                                    48'EXCHANGE RATE'
     A            FEERAT    R             +1EDTWRD('  , 0 .        ') +
     A                                      REFFLD(EXRT)
     A N53        FEAMT3    R             77EDTCDE(1)
     A  53        @AMT33        13  3     77EDTWRD(' ,   ,   , 0 .   ')
     A            FEFRT3    R            100EDTCDE(1)
     A                                      SPACEA(1)
     A N53        FEAMT4    R             77EDTCDE(1)
     A  53        @AMT43        13  3     77EDTWRD(' ,   ,   , 0 .   ')
     A            FEFRT4    R            100EDTCDE(1)
     A                                      SPACEA(1)
     A N53        FEAMT5    R             77EDTCDE(1)
     A  53        @AMT53        13  3     77EDTWRD(' ,   ,   , 0 .   ')
     A            FEFRT5    R            100EDTCDE(1)
      ** Start of Change CLE134
     A  49                                 8'SUSPEND FEE ALLOWED'
     A  49                                  SPACEB(2)
     A  49        FEFSFA         1        29
     A  49                                54'SUSPENDED FEE PAYMENTS'
     A  49        FESFP          3  0     79 EDTCDE(1)
     A  49                                89'PARTIAL PAYMENT ALLOWED'
 
     A  49        FEFPPA         1       113
     A  49                                 8'SUSPENDED FEE AMOUNT'
     A  49                                  SPACEB(1)
     A  49 60     FESFA         13  0     29 EDTWRD(' ,   ,   ,   , 0 -')
     A  49 61     FESFA1        13  0     29 EDTWRD('   ,   ,   , 0 . -')
     A  49 62     FESFA2        13  0     29 EDTWRD('  ,   ,   , 0 .  -')
     A  49 63     FESFA3        13  0     29 EDTWRD(' ,   ,   , 0 .   -')
     A  49                                54'               MAXIMUM'
     A  49        FESFPM         3  0     79 EDTCDE(1)
     A  49                                89'PARTIAL INDICATOR'
     A  49        FEPSIN         1       107
 
     A  49                                 8'             MAXIMUM'
     A  49                                  SPACEB(1)
     A  49 60     FESFAM        13  0     29 EDTWRD(' ,   ,   ,   , 0 -')
     A  49 61     FESFM1        13  0     29 EDTWRD('   ,   ,   , 0 . -')
     A  49 62     FESFM2        13  0     29 EDTWRD('  ,   ,   , 0 .  -')
     A  49 63     FESFM3        13  0     29 EDTWRD(' ,   ,   , 0 .   -')
     A  49                                54'EARLY MATURITY CONDITION'
     A  49        FEFEMC         1        79
     A  49                                89'PARTIAL LIMIT'
     A  49 60     FEPSLM        13  0    103 EDTWRD(' ,   ,   ,   , 0 -')
     A  49 61     FEPSL1        13  0    103 EDTWRD('   ,   ,   , 0 . -')
     A  49 62     FEPSL2        13  0    103 EDTWRD('  ,   ,   , 0 .  -')
     A  49 63     FEPSL3        13  0    103 EDTWRD(' ,   ,   , 0 .   -')
      ** End of Change CLE134
      *                                              Begin /CLE005/
     A**********                            SPACEA(2)
     C N40                               120' '
     A                                      SPACEA(2)
     C  40                               120' '
     A                                      SPACEA(1)
     A          R LER150D3
     A                                     5'RECEIPT SETTLEMENT INST+
     A                                      RUCTIONS'
     A                                      SPACEA(1)
      *                                              Begin /CLE031/
     A  48                                 5'SETTLEMENT CCY / RATE / INDICATOR'
     A  48        RRSCY          3A       43
     A  48                                +1'/'
     A  48        RREXR     R             49REFFLD(EXRT)
     A                                      EDTWRD('  , 0 .        ')
     A  48                                +1'/'
     A  48        RREXI          1A       67
     A  48                                  SPACEA(1)
      *                                              End   /CLE031/
     A                                     5'SETTLEMENT METHOD'
     A            RRSTM     R             33REFFLD(CRTP)
     A                                      SPACEA(1)
     A                                     5'OUR NOSTRO'
      *                                              Begin /157537/
     A*********** RRONS     R             23REFFLD(IPAC)
     A**Start of change CGL029
     A**********  RRONS         15        23                                                  CGL029
     A            RRONS         21        23
     A**End of change CGL029
      *                                              End   /157537/
     A                                      SPACEA(1)
     A                                     5'ORDERING CUSTOMER'
     A            RROCN     R             29REFFLD(RINO)
     A                                      SPACEA(1)
     A                                     5'ORDERING BANK'
     A            RROBN     R             29REFFLD(RINO)
     A                                      SPACEA(1)
     A                                     5'INTERMEDIARY BANK NO'
     A            RRIBN     R             27REFFLD(SDRN)
     A                                      SPACEA(1)
     A                                     8'A/C LINE'
     A            RRIBA     R             17REFFLD(ACB1)
     A                                      SPACEA(2)
     A
     A          R LER150D4
     A                                    53'PAYMENT SETTLEMENT INST+
     A                                      RUCTIONS'
     A                                      SPACEA(1)
      *                                              Begin /CLE031/
     A  48                                53'SETTLEMENT CCY / RATE / INDICATOR'
     A  48        RPSCY          3A       91
     A  48                                +1'/'
     A  48        RPEXR     R             97REFFLD(EXRT)
     A                                      EDTWRD('  , 0 .        ')
     A  48                                +1'/'
     A  48        RPEXI          1A      115
     A  48                                  SPACEA(1)
      *                                              End   /CLE031/
     A                                    53'SETTLEMENT METHOD'
     A            RPSTM     R             82REFFLD(CRTP)
     A                                      SPACEA(1)
     A                                    53'OUR NOSTRO'
      *                                              Begin /157537/
     A*********** RPONS     R             82REFFLD(IPAC)
     A**Start of change CGL029
     A**********  RPONS         15        82                                                  CGL029
     A            RPONS         21        82
     A**End of change CGL029
      *                                              End   /157537/
     A                                      SPACEA(1)
     A                                    53'MT202 SUPPLEMENTARY MESS+
     A                                      AGE'
     A            RCVMR     R             82REFFLD(ATYP)
     A                                      SPACEA(1)
     A                                    53'RECEIVER'
     A            RRVNO     R             82REFFLD(SDRN)
     A                                      SPACEA(1)
     A                                    53'ORDERING CUSTOMER'
     A            RPOCN     R             84REFFLD(RINO)
     A                                      SPACEA(1)
     A                                    53'ORDERING BANK'
     A            RPOBN     R             84REFFLD(RINO)
     A                                      SPACEA(1)
      *
     A                                    53'RECEIVER''S CORRESPONDEN+
     A                                      T NO'
     A            RRCRN     R             82REFFLD(SDRN)
     A                                      SPACEA(1)
     A                                    72'A/C LINE'
     A            RRCRA     R             82REFFLD(ACB1)
     A                                      SPACEA(1)
     A                                    53'INTERMEDIARY BANK NO'
     A            RPIBN     R             82REFFLD(SDRN)
     A                                      SPACEA(1)
     A                                    72'A/C LINE'
     A            RPIBA     R             82REFFLD(ACB1)
     A                                      SPACEA(1)
     A                                    53'ACCOUNT WITH BANK NO'
     A            RAWBN     R             82REFFLD(SDRN)
     A                                      SPACEA(1)
     A                                    72'A/C LINE'
     A            RAWBA     R             82REFFLD(ACB1)
     A                                      SPACEA(1)
     A                                    53'BENEFICIARY NO'
     A            RBENN     R             82REFFLD(SDRN)
     A                                      SPACEA(1)
     A                                    72'A/C LINE'
     A            RBENA     R             82REFFLD(ACB1)
     A                                      SPACEA(1)
     A                                    53'DETAILS OF PAYMENT 1'
     A            RDTP1     R             74REFFLD(ACB1)
     A                                      SPACEA(1)
     A                                    72'2'
     A            RDTP2     R             74REFFLD(ACB1)
     A                                      SPACEA(1)
     A                                    72'3'
     A            RDTP3     R             74REFFLD(ACB1)
     A                                      SPACEA(1)
     A                                    72'4'
     A            RDTP4     R             74REFFLD(ACB1)
     A                                      SPACEA(1)
     A                                    53'DETAILS OF CHARGES'
     A            RDCHG     R             74REFFLD(BRCA)
     A                                      SPACEA(1)
     A                                    53'BANK TO BANK INFO'
     A                                      SPACEA(1)
     A                                    53'1'
     A            RBTB1     R             55REFFLD(ACB1)
     A                                    91'4'
     A            RBTB4     R             93REFFLD(ACB1)
     A                                      SPACEA(1)
     A                                    53'2'
     A            RBTB2     R             55REFFLD(ACB1)
     A                                    91'5'
     A            RBTB5     R             93REFFLD(ACB1)
     A                                      SPACEA(1)
     A                                    53'3'
     A            RBTB3     R             55REFFLD(ACB1)
     A                                    91'6'
     A            RBTB6     R             93REFFLD(ACB1)
     A                                      SPACEA(2)
      *                                              End   /CLE005/
      * Start of change CLE034
     A          R LER150H4
     A  30                                 5'RECEIPT'
     A N30                                 5'PAYMENT'
     A                                    13'SETTLEMENT INSTRUCTION: +
     A                                      (METHOD/CURRENCY/ACCOUNT/+
     A                                      PERCENTAGE)'
     A                                      SPACEA(1)
      *
     A          R LER150D5
     A                                     5'SETTLEMENT ALLOCATION'
     A            RSTLA     R             28REFFLD(BRCA)
     A                                      SPACEA(1)
      *
     A          R LER150D6
     A            RSETM     R             13REFFLD(SEME)
     A                                    16'/'
     A            RSETC     R             18REFFLD(SETC)
     A                                    22'/'
     A**Start of change CGL029
     A**********  RSETA     R             24REFFLD(SETA)                                      CGL029
     A**********                          37'/'                                               CGL029
     A**********  RSTLP     R             39REFFLD(SPTR)                                      CGL029
     A            RSETA         18        24
     A                                    43'/'
     A            RSTLP     R             45REFFLD(SPTR)
     A**End of change CGL029
     A                                      SPACEA(1)
      * End of change CLE034
     A
     A          R LER150T2
     A                                    60'***  END OF REPORT  ***'
     A                                      SPACEB(3)
     A          R LER150T3
     A                                    60'* END OF REPORT FOR THIS BRANCH *'
     A                                      SPACEB(3)
