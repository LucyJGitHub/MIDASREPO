      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Fees Settled/Adjusted Today Summary report')     *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  PRTF/LER170P2 - Print File Showing Fees Settled/Adjusted     *
      *                  Today Summary report.                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CLE139             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 152181  *Create    Date 07Jan99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE139 - Lending Fee Capitalisation                          *
      *  152181 - Addition of new printer file to show report prior   *
      *           to inclusion of the settlement details, i.e. one    *
      *           line per fee settlement. Used if CLE005 is On.      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A          R HEADP2                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE LER170P2'
     A            BJURPT    R             30REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    29'FEES SETTLED/ADJUSTED +
     A                                      TODAY SUMMARY REPORT'
      *
     A                                      SPACEA(1)
     A                                    29'----------------------+
     A                                      --------------------'
     A                                      SPACEA(1)
      *
     A  37                                 1'BRANCH NAME'
     A  37        BRNM      R             13
     A  37                               117'BRANCH'
     A  37        @AORL          3       124
     A  37                                  SPACEA(3)
      *
      ** Sub-Heading:
      ** ------------
      *
     A                                    49'ORIGINAL'
     A                                       SPACEA(1)
      *
     A                                    38'ACCOUNT'
     A                                    48'SETTLEMENT'
     A                                    67'FEE'
      **Start of change CLE139
     A                                    71'CAP'
      **End of change CLE139
     A                                    80'ORIGINAL'
     A                                    98'AMOUNT SETTLED'
     A                                       SPACEA(1)
     A
     A                                     1'FEE REFERENCE'
     A                                    19'CUSTOMER NAME'
     A                                    38'OFFICER'
     A                                    51'DATE'
     A                                    62'CCY'
     A                                    67'IND'
      **Start of change CLE139
     A                                    71'FEE'
      **End of change CLE139
     A                                    80'AMOUNT DUE'
     A                                   100'/ADJUSTED'
     A                                   115'ACTION TYPE'
     A                                      SPACEA(1)
      *
     A                                     1'-------------'
     A                                    19'-------------'
     A                                    38'-------'
     A                                    51'----'
     A                                    62'---'
     A                                    67'---'
      **Start of change CLE139
     A                                    71'---'
      **End of change CLE139
     A                                    80'----------'
     A                                   100'---------'
     A                                   115'-----------'
     A                                      SPACEA(1)
      *
      *-----------------------*
      ** Detail Record Format *
      *-----------------------*
      *
     A          R DETLP2
     A                                      SPACEA(1)
      *
     A            @FR           16         1
     A            BBCRNM        20        19
     A            WKACOC         2        41
     A            @DD            7        49
     A            FEFCCY    R             62REFFLD(CCY)
     A            RPTFI     R             68REFFLD(PTFI)
      **Start of change CLE139
     A     64                             71'YES'
     A    N64                             71'NO'
      **End of change CLE139
     A     60     WOOAM0        13  0     74EDTCDE(K)
     A     61     WOOAM1        13  1     74EDTCDE(K)
     A     62     WOOAM2        13  2     74EDTCDE(K)
     A     63     WOOAM3        13  3     74EDTCDE(K)
     A     60     @DSAMT        13  0     94EDTWRD(' ,   ,   ,   , 0 -')
     A     61     @DSAM1        13  1     94EDTWRD('   ,   ,   , 0 . -')
     A     62     @DSAM2        13  2     94EDTWRD('  ,   ,   , 0 .  -')
     A     63     @DSAM3        13  3     94EDTWRD(' ,   ,   , 0 .   -')
     A            @NAR          18       115
      *
      *-----------------*
      ** Report Trailer *
      *-----------------*
      *
     A          R TRAILP2
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
