      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Loan Profitability Extract Audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*  PRTF/LER220AU - Facilities Extract - Audit                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. Xnnnnn             DATE ddmmmyy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R LER220A1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'LER220AU'
     A            BJURPT        53        30
     A                                   100'DATE'
     A            RUNDAX         7       105
     A                                   119'PAGE'
     A            PAGE1          4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    42'LOAN EXTRACT AUDIT REPORT'
     A                                      SPACEA(1)
     A                                    42'-------------------------'
     A                                      SPACEA(2)
     A
     A          R LER220A2                  SPACEB(3)
     A                                     5'EXTRACT FILE CONTROL TOTALS:'
     A                                      SPACEA(1)
     A                                     5'----------------------------'
     A                                      SPACEA(2)
     A                                    12'EXTRACT RECORDS BEFORE UPDATE'
     A            NPREV          5  0     50EDTCDE(1)
     A                                      SPACEA(1)
     A                                    12'EXTRACT RECORDS ADDED'
     A            NADD           5  0     50EDTCDE(1)
     A                                      SPACEA(1)
     A                                    12'EXTRACT RECORDS DELETED'
     A            NDEL           5  0     50EDTCDE(1)
     A                                      SPACEA(1)
     A                                    12'EXTRACT RECORDS AFTER UPDATE'
     A            LZNREC         5  0     50EDTCDE(1)
     A                                      SPACEA(3)
     A
     A          R DBFMT                     SPACEB(3)
     A                                    37'DATABASE ERROR'
     A            DBASE          3  0     52
     A                                    62'IN PROGRAM'
     A            DBPGM     R             73
     A                                    37'FILE'
     A                                      SPACEB(2)
     A            DBFILE    R             46
     A                                    62'KEY'
     A            DBKEY     R             73
     A
