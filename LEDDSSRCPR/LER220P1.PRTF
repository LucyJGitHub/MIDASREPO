      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Loan Profitability Extract Details')             *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*  PRTF/LER220P1 - Loan Extract for Profitability Reporting     *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 Xnnnnn             Date ddmmmyy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R LER220H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'LER220P1'
     A            BJURPT        53        30
     A                                   100'DATE'
     A            RUNDAX         7       105
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    42'LOAN EXTRACT AUDIT REPORT'
     A                                      SPACEA(1)
     A                                    42'-------------------------'
     A                                      SPACEA(2)
     A
     A          R LER220H2                  SPACEA(2)
     A
     A                                    25'CUSTOMER'
     A                                    +3'LOAN NO.'
     A                                    +3'FACILITY'
     A                                    +3'VALUE DATE'
     A                                    +3'MATURITY DATE'
     A                                    +3'STATUS'
     A                                      SPACEA(1)
     A                                    25'--------'
     A                                    +3'--------'
     A                                    +3'--------'
     A                                    +3'----------'
     A                                    +3'-------------'
     A                                    +3'------'
     A
     A          R LER220D1                  SPACEA(1)
     A
     A                                     5'* DUPLICATE *'
      * Begin /CSD027/
     A**********  CNUM           6  0     26                                                  CSD027
     A            CNUM           6A       26
      * End /CSD027/
      **Start of change CLE148
     A**********  LNRF           6  0     37                                                  CLE148
     A            LNRF           6A       37
      **End of change CLE148
     A            FTYP           3  0     48
     A                                    51'/'
     A            FSEQ           2  0     52
     A            VALUA          7        59
     A            MATUA          7        74
     A            STATUS         8        87
     A
     A          R LER220T1                  SPACEB(3)
     A
     A                                    25'** EXTRACT RECORDS WERE NOT +
     A                                      MAINTAINED FOR THE ABOVE RECORDS **'
     A
     A          R LER220T2                  SPACEB(3)
     A                                    60'***  END OF REPORT  ***'
     A
     A          R DBFMTP                    SPACEB(3)
     A                                    29'*** REFERENCE'
     A            DBPGM     R             43
     A                                    61'DATABASE ERROR'
     A                                      SPACEA(2)
     A
