      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Margin A/c Key Gen. & Prftblty Extr.')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module                      *
     A*                                                               *
     A*  PRTF/LER310AU - Margin Account Keys Generated - Audit        *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 057399             Date 10Mar94               *
      *                 XXXXXX             Date DDMmmYY               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     F*  057399 - Report LER310P1 shows incorrect rundate.            *
     F*           (The date is not wrong at all, but it takes into    *
     F*           consideration EOM, ACCRUAL, & NON-WORKING DAYS,     *
     F*           the actual processing date is not always shown.)    *
     F*           To fix, amend prtfs LER310AU and LER310P1 to show   *
     F*           both dates, and recompile RPG pgm LER310.           *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R LER310A1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'LER310AU'
     A            BJURPT        53        30
     A                                   100'DATE'
     A*********              Start of Change               /057399/
     A*********   RUNDAX         7       105
     A            AGMRDT         7       105
     A*                      End of Change                 /057399/
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    42'MARGIN ACCOUNT KEYS GENERATED'
     A*********              Start of Change               /057399/
     A                                    72'- AS OF'
     A            RUNDAX         7        80
     A*                      End of Change                 /057399/
     A                                      SPACEA(1)
     A                                    42'-----------------------------'
     A                                      SPACEA(2)
     A
     A          R LER310T3                  SPACEB(3)
     A
     A                                     5'EXTRACT FILE CONTROL TOTALS:'
     A                                      SPACEA(1)
     A                                     5'----------------------------'
     A                                      SPACEA(2)
     A                                    12'EXTRACT RECORDS BEFORE UPDATE'
     A            NPREV          5  0     50EDTCDE(1)
     A                                      SPACEA(1)
     A                                    12'EXTRACT RECORDS ADDED'
     A            NADD           5  0     50EDTCDE(1)
     A                                      SPACEA(1)
     A                                    12'EXTRACT RECORDS DELETED'
     A            NDEL           5  0     50EDTCDE(1)
     A                                      SPACEA(1)
     A                                    12'EXTRACT RECORDS AFTER UPDATE'
     A            MZNREC         5  0     50EDTCDE(1)
     A                                      SPACEA(3)
     A
     A          R DBFMT                     SPACEB(2)
     A                                    37'DATABASE ERROR'
     A            DBASE          3  0     52
     A                                    62'IN PROGRAM'
     A            DBPGM     R             73
     A                                    37'FILE'
     A                                      SPACEB(2)
     A            DBFILE    R             46
     A                                    62'KEY'
     A            DBKEY     R             73
     A
