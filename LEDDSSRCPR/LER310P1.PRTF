      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Margin A/c Key Gen. & Prftblty Extr.')           *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module                      *
     A*                                                               *
     A*  PRTF/LER310P1 - Margin Account Keys Generated                *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      *                 CLE042             Date 06Sep05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 057399             Date 10Mar94               *
     A*                 060109             Date 23Feb94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
     F*  057399 - Report LER310P1 shows incorrect rundate.            *
     F*           (The date is not wrong at all, but it takes into    *
     F*           consideration EOM, ACCRUAL, & NON-WORKING DAYS,     *
     F*           the actual processing date is not always shown.)    *
     F*           To fix, amend prtfs LER310AU and LER310P1 to show   *
     F*           both dates, and recompile RPG pgm LER310.           *
     A*  060109 - Split report Margin A/C Keys Generated by branch.   *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R LER310H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'LER310P1'
     A            BJURPT        53        30
     A                                   100'DATE'
     A*********              Start of Change               /057399/
     A*********   RUNDAX         7       105
     A            AGMRDT         7       105
     A*                      End of Change                 /057399/
     A                                   119'PAGE'
     A            PAGE1          4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    42'MARGIN ACCOUNT KEYS GENERATED'
     A*********              Start of Change               /057399/
     A                                    72'- AS OF'
     A            RUNDAX         7        80
     A*                      End of Change                 /057399/
     A*********              Start of Change               /060109/
     A*********                             SPACEA(1)
     A  70                               105'BRANCH'
     A  70        BRCA      R            112
     A*                      End of Change                 /060109/
     A                                    42'-----------------------------'
     A*********              Start of Change               /060109/
     A                                      SPACEB(1)
     A*                      End of Change                 /060109/
     A                                      SPACEA(2)
     A
     A          R LER310H2                  SPACEA(2)
     A
     A                                     5'CUSTOMER'
     A                                    +3'LOAN NO.'
     A                                    +3'ACCOUNT KEY'
     A                                   +14'AMOUNT'
     A                                    +3'CURRENCY'
     A                                    +3'REV.IND.'
     A                                      SPACEA(1)
     A                                     5'--------'
     A                                    +3'--------'
     A                                    +3'-----------'
     A                                   +14'------'
     A                                    +3'--------'
     A                                    +3'--------'
     A
     A          R LER310D1                  SPACEA(1)
     A
      * Begin /CSD027/
     A**********  CNUM           6  0      6                                                  CSD027
     A            CNUM           6A        6
      * End /CSD027/
      **Start of change CLE148
     A**********  LNNO           6  0     17                                                  CLE148
     A            LNNO           6A       17
      **End of change CLE148
     A*                                              Begin /CLE042/
     A************AKEY**********10********27                                                  CLE042
     A            AKEY          14        26                                                  CLE042
     A*                                              End   /CLE042/
     A  30        EAMT0         13  0     41EDTCDE(J)
     A  31        EAMT1         13  1     41EDTCDE(J)
     A  32        EAMT2         13  2     41EDTCDE(J)
     A  33        EAMT3         13  3     41EDTCDE(J)
     A            ECCY           3        63
     A            REVI           1  0     75
     A  40                                81'FOR ACCRUAL REPORT - MANUAL ADJ.'
     A  41                                81'FOR ACCRUAL REPORT - GENERATED'
     A  41                               112'ADJ.'
     A
     A          R DBFMTP                    SPACEB(3)
     A                                    29'*** REFERENCE'
     A            DBPGM     R             43
     A                                    61'DATABASE ERROR'
     A                                      SPACEA(2)
     A
     A          R LER310T1                  SPACEB(3)
     A
     A                                     5'TOTAL ACCOUNT KEYS GENERATED'
     A*********              Start of Change               /060109/
     A*********   NORE           5  0     +3EDTCDE(1)
     A N70        NORE           5  0     +3EDTCDE(1)
     A  70        SNORE          5  0     +3EDTCDE(1)
     A*                      End of Change                 /060109/
     A
     A          R LER310T2                  SPACEB(3)
     A
     A*********              Start of Change               /060109/
     A*********                           60'***  END OF REPORT  ***'
     A N70                                60'***  END OF REPORT  ***'
     A  70                                56'***  END OF REPORT FOR BRANCH'
     A  70        SAVBR          3A       86
     A  70                                91'***'
     A*                      End of Change                 /060109/
     A
