      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Profitability Rpt. by Business Group')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Customer Lending Module
     A*                                                               *
     A*  PRTF/LER470P1 - Profitability Report by Business Group       *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 163668             Date 14Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 ALIEN1             Date 12Jul92               *
      *                 E01378             Date 21Apr92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  163668 - Profitability showing on facility customer not on   *
      *           agent customer                                      *
     A*  ALIEN1 - For GL A/Cs, (Other Charges/Fees) account info. is  *
     A*           output by account (multiple records) or             *
     A*           consolidated (single record) for each customer.     *
     A*           In addition E01378 allowed no space between         *
     A*           M-T-D Average Balance and M-T-D Fees.               *
     A*  E01378 - Total facility figure field not large enough.       *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R LER470H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'LER470P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   100'DATE'
     A            BJMRDT    R            105REFFLD(RUNA)
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    37'CUSTOMER PROFITABILITY REPORT +
     A                                      - BY BUSINESS GROUP CONSOLIDATED'
     A  60                               100'FOR:'
     A  60        @USAC          2       106
     A  60                               108'/'
     A  60        @WSID         10       109
     A  61                               100'* WEEKLY *'
     A  62                               100'* END OF MONTH *'
     A                                    37'------------------------------+
     A                                      -------------------'
     A                                      SPACEB(1)
     A                                      SPACEA(1)
     A*/COPY WNCPYSRC,LER470P101
     A
     A  43                                01'DEPARTMENT REF.'
     A  43        NQDEPT         3        17
     A  43        DNAME         30        22
     A  41                                17'TOTAL FOR DEPARTMENT'
     A  41        DTOT          30        38
     A  40                                17'TOTAL FOR ALL DEPARTMENTS'
     A
     A                                    82'PERIOD'
     A            SMTH           7        89
     A                                    97'TO'
     A            PDAT           7       100
     A            PDAY           2  0    110
     A                                   113'DAYS'
     A                                      SPACEA(1)
     A  44                                01'ACCOUNT OFFICER'
     A  44        NQACOF         2        17
     A  44        ANAME         30        22SPACEA(1)
     A  42                                17'TOTAL FOR ACCOUNT OFFICER'
     A  42        ATOT          30        43SPACEA(1)
     A
     A          R LER470H2
     A  45                                  SPACEB(2)
     A  44                                01'CUSTOMER'
      * Begin /CSD027/
     A**44******  NQCNUM         6  0     10                                                  CSD027
     A  44        NQCNUM         6A       10
      * End /CSD027/
     A  44        CNAME         30        22
     A  44                                82'INDUSTRY:'
     A  44        INAME         40        92SPACEA(1)
     A
     A                                    01'FACILITY OR' SPACEB(1)
     A                                    24'* - - - - - - - -
     A                                      MONTH TO DATE - - - - - - - - *'
     A                                    78'* - - - - - - - -
     A                                      YEAR TO DATE - - - - - - - - *'
     A                                      SPACEA(1)
 
     A                                    01'LOAN TYPE'
     A                                    26'AVERAGE'
     A**********       START OF CHANGE                     /ALIEN1/
     A**********                          42'FEES'
     A                                    43'FEES'
     A**********       END OF CHANGE                       /ALIEN1/
     A                                    51'MARGIN'
     A                                    61'YIELD'
     A                                    69'MARGIN'
     A                                    81'AVERAGE'
     A                                    96'FEES'
     A                                   105'MARGIN'
     A                                   114'YIELD'
     A                                   122'MARGIN'
     A                                      SPACEA(1)
     A                                    26'BALANCE'
     A                                    62'RATE'
     A                                    70'RATE'
     A                                    81'BALANCE'
     A                                   115'RATE'
     A                                   123'RATE'
     A                                      SPACEA(2)
     A
     A          R LER470FC                  SPACEA(1)
     A
     A  01        FNAME         15         1
     A  01                                16'/'
     A  01        NQFSEQ         2  0     17
     A  02                                 1'TOTAL FACILITIES'
     A*                                                      E01378
     A************MAVV**********10**0*****22EDTCDE(J)
     A            MAVV          12  0     22EDTCDE(J)
     A*                                                      E01378
     A**********       START OF CHANGE                     /ALIEN1/
     A**********  MFEE           7  0     37EDTCDE(J)
     A            MFEE           7  0     38EDTCDE(J)
     A**********       END OF CHANGE                       /ALIEN1/
     A            MAYR           6  4     59EDTCDE(J)
     A*                                                      E01378
     A************YAVV**********10**0*****77EDTCDE(J)
     A            YAVV          11  0     77EDTCDE(J)
     A*                                                      E01378
     A            YFEE           7  0     92EDTCDE(J)
     A            YAYR           6  4    114EDTCDE(J)
     A
      *                                              Begin /163668/
     A          R LER470FL                  SPACEA(1)
      *
     A N22        RNAME         20         1
     A N22        RTOWN         10        30
      * Begin /CSD027/
     A**22******  RFCLT          6  0      1                                                  CSD027
     A  22        RFCLT          6A        1
      * End /CSD027/
     A  22        RFTYP          3  0      9
     A  22                                12'/'
     A  22        RFSEQ          2  0     13
     A  01 22     RMAVV         11  0     23EDTCDE(K)
     A  22        RMFEE          7  0     38EDTCDE(K)
     A  02 22     RMMGN          7  0     48EDTCDE(K)
     A  01 22     RMAYR          6  4     59EDTCDE(K)
     A  01 22     RYAVV         11  0     77EDTCDE(K)
     A  22        RYFEE          7  0     92EDTCDE(K)
     A  02 22     RYMGN          7  0    103EDTCDE(K)
     A  01 22     RYAYR          6  4    114EDTCDE(K)
      *                                              End   /163668/
     A          R LER470LN                  SPACEA(1)
     A
     A N03        NQGPNA        20         1
     A  03                                 1'TOTAL LOANS'
     A*                                                      E01378
     A************MAVV**********10**0*****22EDTCDE(J)
     A            MAVV          12  0     22EDTCDE(J)
     A*                                                      E01378
     A**********       START OF CHANGE                     /ALIEN1/
     A**********  MFEE           7  0     37EDTCDE(J)
     A            MFEE           7  0     38EDTCDE(J)
     A**********       END OF CHANGE                       /ALIEN1/
     A            MMGN           7  0     48EDTCDE(J)
     A            MAYR           6  4     59EDTCDE(J)
     A            MAMR           6  4     68EDTCDE(J)
     A*                                                      E01378
     A************YAVV**********10**0*****77EDTCDE(J)
     A            YAVV          11  0     77EDTCDE(J)
     A*                                                      E01378
     A            YFEE           7  0     92EDTCDE(J)
     A            YMGN           7  0    103EDTCDE(J)
     A            YAYR           6  4    114EDTCDE(J)
     A            YAMR           6  4    123EDTCDE(J)
     A
     A          R LER470RT                  SPACEA(1)
     A
     A                                    01'RETAIL ACCOUNTS -'
     A  04                                19'DR'
     A  05                                19'CR'
     A*                                                      E01378
     A************MAVV**********10**0*****22EDTCDE(J)
     A            MAVV          12  0     22EDTCDE(J)
     A*                                                      E01378
     A**********       START OF CHANGE                     /ALIEN1/
     A**********  MFEE           7  0     37EDTCDE(J)
     A            MFEE           7  0     38EDTCDE(J)
     A**********       END OF CHANGE                       /ALIEN1/
     A            MMGN           7  0     48EDTCDE(J)
     A            MAYR           6  4     59EDTCDE(J)
     A            MAMR           6  4     68EDTCDE(J)
     A*                                                      E01378
     A************YAVV**********10**0*****77EDTCDE(J)
     A            YAVV          11  0     77EDTCDE(J)
     A*                                                      E01378
     A            YFEE           7  0     92EDTCDE(J)
     A            YMGN           7  0    103EDTCDE(J)
     A            YAYR           6  4    114EDTCDE(J)
     A            YAMR           6  4    123EDTCDE(J)
     A
     A**********       START OF CHANGE                     /ALIEN1/
     A**********R LER470GL                  SPACEA(2)
     A
     A**********                          01'OTHER FEES/CHARGES'
     A**********  MFEE           7  0     37EDTCDE(J)
     A          R LER470GL                  SPACEA(1)
     A
     A N50                                01'OTHER FEES/CHARGES'
     A  50        NQBRCA         3         1
     A  50                                 4'/'
     A  50        NQACCY         3         5
     A  50                                 8'/'
     A  50        NQACNA        25         9
     A  50                                34'/'
     A  50        NQACSQ         2  0     35
     A            MFEE           7  0     38EDTCDE(J)
     A**********       END OF CHANGE                       /ALIEN1/
     A            YFEE           7  0     92EDTCDE(J)
     A
     A          R LER470DF                  SPACEA(1)
     A
     A  31                                01'MM - LOANS'
     A  32                                01'MM - DEPOSITS'
     A  33                                01'MM - NAs INT.BEARING'
     A  34                                01'MM - NAs NON-INT.BRG'
     A**35******************              01'FX - FX'
     A  35                                01'FX - FX VOLUMES'
     A  36                                01'FX - FRA'
     A  37                                01'FX - IRS'
     A  38                                01'FX - FUTURES'
     A  39                                01'FX - OPTIONS'
     A*                                                      E01378
     A************MAVV**********10**0*****22EDTCDE(J)
     A************YAVV**********10**0*****77EDTCDE(J)
     A            MAVV          12  0     22EDTCDE(J)
     A            YAVV          11  0     77EDTCDE(J)
     A*                                                      E01378
     A
     A** Format for extra spacing.
     A
     A          R LER470XX                  SPACEA(1)
     A                                     1' '
     A
     A          R LER470TT                  SPACEB(3)
     A                                    60'***  END OF REPORT  ***'
     A
