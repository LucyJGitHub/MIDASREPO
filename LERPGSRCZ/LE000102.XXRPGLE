     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*S*D****RPGBASEBNC****************************************************                      CCB023A
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE000102 - DLDLDGQD Reconciliation                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CCB023A *REDUNDANT Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL049             Date 07Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 BUG10315           Date 07Feb06               *
      *                 CDL038             Date 10May05               *
      *                 CDL028             Date 07Feb05               *
      *                 BUG3937            Date 10Sep04               *
      *                 BUG3830 *CREATE    Date 17Jun04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCB023A - COB Restructure - Input Cycle Termination          *
      *            Restructuring                                      *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG10315 - LDNI: Amend System allowed amendment of matured   *
      *             deal                                              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  BUG3937 - Do not fail the component when an entry is missing *
      *            in main file but present in extension file.        *
      *  BUG3830 - Credit Lines Extension Files Reconciliation        *
      *                                                               *
      *****************************************************************
 
     FDLDEALL5  IP  AE           K DISK    INCLUDE(DLDLDGD0)
     FDEALS     IS  AE           K DISK    INCLUDE(DEALSDGF)
     FDEALSH    IF   E           K DISK    INCLUDE(DLGHISD0)                                BUG10315
     FQSYSPRT   O    F  132        PRINTER USROPN
 
     D SDBANK        E DS                  EXTNAME(SDBANKPD)
     D DSFDY         E DS                  EXTNAME(DSFDY)
 
     IDLDLDGD0      01
     I                                          F1DLNO        L1M1
     IDEALSDGF      02
     I                                          DLNO          L1M1
 
     C     *INMR         IFEQ      *OFF
     C                   EXSR      OPNPRTF
     C     *IN01         IFEQ      *ON
     C                   MOVE      F1DLNO        KDLNO                                      BUG10315
     C     KDLNO         CHAIN     DLGHISD0                           03                    BUG10315
     C     *IN03         IFEQ      *ON                                                      BUG10315
     C     F1BRCA        ORNE      BRCA                                                     BUG10315
     C                   EXCEPT    ERR1
     C                   ENDIF                                                              BUG10315
     C                   ELSE
     C                   EXCEPT    ERR2
     C                   SETON                                          U7U8                 BUG3937
     C                   ENDIF
     C                   ELSE
     C   02F1BRCA        IFNE      BRCA
     C                   EXSR      OPNPRTF
     C                   EXCEPT    ERR3
     C                   SETON                                          U7U8                 BUG3937
     C                   ENDIF
     C                   ENDIF
     C/SPACE 5
      *********************************************************************
      * OPNPRTF - OPEN PRINTER FILE
      *********************************************************************
     C     OPNPRTF       BEGSR
     C     PrintOpen     IFNE      'Y'
     C                   MOVE      'Y'           PrintOpen         1
     C**********         SETON                                          U7U8                 BUG3937
     C                   OPEN      QSYSPRT
     C                   EXCEPT    HEADINGS
     C                   ENDIF
     C                   ENDSR
      *********************************************************************
     C/SPACE 5
      *********************************************************************
      * *PSSR  --- ABNORMAL ERROR CONDITIONS
      *********************************************************************
     C     *PSSR         BEGSR
     C     RunBefore     IFEQ      *BLANKS
     C                   MOVE      'Y'           RunBefore         1
     C                   DUMP
     C                   SETON                                        LRU7U8
     C                   ENDIF
     C                   RETURN
     C                   ENDSR
      *********************************************************************
     C/SPACE 5
      *********************************************************************
      * *INZSR --- INITIAL PROCESSING
      *********************************************************************
     C     *INZSR        BEGSR
     C                   CALL      'AOBANKR0'
     C                   PARM      *BLANKS       PRTCD             7
     C                   PARM      '*FIRST '     POPTN             7
     C     SDBANK        PARM      SDBANK        DSFDY
                                                                                            BUG10315
     C                   Z-ADD     0             KDLNO             6 0                      BUG10315
                                                                                            BUG10315
     C                   ENDSR
      *********************************************************************
     OQSYSPRT   E            HEADINGS       1 03
     O                                           12 'REF:LE000102'
     O                                           70 'DLDLDGQD RECONCILIATION'
     O                                           77 'REPORT'
     O                                          120 'DATE:'
     O                       BJMRDT             128
     O          E            ERR1        1
     O                                           11 'Deal number'
     O                       F1DLNO              18
     O                                           43 'on DLDLDGQD but missing '
     O**********                                 56 'from DEALSDG '                         BUG10315
     O                                           65 'from DEALSDG/DLGHISPD'                 BUG10315
     O          E            ERR2        1
     O                                           11 'Deal number'
     O                       DLNO                18
     O**********                                 43 'on DEALSDG  but missing '              BUG10315
     O**********                                 56 'from DLDLDGQD'                         BUG10315
     O                                           43 'on DEALSDG/DLGHISPD but '              BUG10315
     O                                           64 'missing from DLDLDGQD'                 BUG10315
     O          E            ERR3        1
     O                                           11 'Deal number'
     O                       DLNO                18
     O                                           43 'has an invalid branch on'
     O                                           52 'DLDLDGQD'
