     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*S*D****RPGBASEBNC****************************************************                      CCB023A
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE000103 - GLACNTQD Reconciliation                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CCB023A *REDUNDANT Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CLE040             Date 05Jul04               *
      *                 BUG3937            Date 10Sep04               *
      *                 BUG3830 *CREATE    Date 17Jun04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCB023A - COB Restructure - Input Cycle Termination          *
      *            Restructuring                                      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE040 - Collateralised Lending Phase 2 (Recompile)          *
      *  BUG3937 - Do not fail the component when an entry is missing *
      *            in main file but present in extension file.        *
      *  BUG3830 - Credit Lines Extension Files Reconciliation        *
      *                                                               *
      *****************************************************************
 
     FGLACNTL3  IP  AE           K DISK    INCLUDE(GLACNTD0)
     FACCNT     IS  AE           K DISK    INCLUDE(ACCNTABF)
     FQSYSPRT   O    F  132        PRINTER USROPN
 
     D SDBANK        E DS                  EXTNAME(SDBANKPD)
     D DSFDY         E DS                  EXTNAME(DSFDY)
 
     IGLACNTD0      01
     I                                          F1CNUM        L1M5
     I                                          F1CCY         L1M4
     I                                          F1ACOD        L1M3
     I                                          F1ACSQ        L1M2
     I                                          F1BRCA        L1M1
     IACCNTABF      02
     I                                          CNUM          L1M5
     I                                          CCY           L1M4
     I                                          ACOD          L1M3
     I                                          ACSQ          L1M2
     I                                          BRCA          L1M1
 
     C     *INMR         IFEQ      *OFF
     C                   EXSR      OPNPRTF
     C     *IN01         IFEQ      *ON
     C                   MOVE      F1CNUM        A_CNUM            6
     C                   MOVE      F1ACOD        A_ACOD            4
     C                   MOVE      F1ACSQ        A_ACSQ            2
     C                   MOVE      *BLANK        Account          28
     C                   EVAL      Account = A_CNUM + '/' + F1CCY  + '/' +
     C                                       A_ACOD + '/' + A_ACSQ + '/' +
     C                                       F1BRCA
     C                   EXCEPT    ERR1
     C                   ELSE
     C                   MOVE      CNUM          A_CNUM            6
     C                   MOVE      ACOD          A_ACOD            4
     C                   MOVE      ACSQ          A_ACSQ            2
     C                   MOVE      *BLANK        Account          28
     C                   EVAL      Account = A_CNUM + '/' + CCY    + '/' +
     C                                       A_ACOD + '/' + A_ACSQ + '/' +
     C                                       BRCA
     C                   EXCEPT    ERR2
     C                   SETON                                          U7U8                 BUG3937
     C                   ENDIF
     C                   ENDIF
     C/SPACE 5
      *********************************************************************
      * OPNPRTF - OPEN PRINTER FILE
      *********************************************************************
     C     OPNPRTF       BEGSR
     C     PrintOpen     IFNE      'Y'
     C                   MOVE      'Y'           PrintOpen         1
     C**********         SETON                                          U7U8                 BUG3937
     C                   OPEN      QSYSPRT
     C                   EXCEPT    HEADINGS
     C                   ENDIF
     C                   ENDSR
      *********************************************************************
     C/SPACE 5
      *********************************************************************
      * *PSSR  --- ABNORMAL ERROR CONDITIONS
      *********************************************************************
     C     *PSSR         BEGSR
     C     RunBefore     IFEQ      *BLANKS
     C                   MOVE      'Y'           RunBefore         1
     C                   DUMP
     C                   SETON                                        LRU7U8
     C                   ENDIF
     C                   RETURN
     C                   ENDSR
      *********************************************************************
     C/SPACE 5
      *********************************************************************
      * *INZSR --- INITIAL PROCESSING
      *********************************************************************
     C     *INZSR        BEGSR
     C                   CALL      'AOBANKR0'
     C                   PARM      *BLANKS       PRTCD             7
     C                   PARM      '*FIRST '     POPTN             7
     C     SDBANK        PARM      SDBANK        DSFDY
     C                   ENDSR
      *********************************************************************
     OQSYSPRT   E            HEADINGS       1 03
     O                                           12 'REF:LE000103'
     O                                           70 'GLACNTQD RECONCILIATION'
     O                                           77 'REPORT'
     O                                          120 'DATE:'
     O                       BJMRDT             128
     O          E            ERR1        1
     O                                            7 'Account'
     O                       Account             36
     O                                           55 'on GLACNTQD but missing '
     O                                           68 'from ACCNTAB '
     O          E            ERR2        1
     O                                            7 'Account'
     O                       Account             36
     O                                           55 'on ACCNTAB  but missing '
     O                                           68 'from GLACNTQD'
