     H DEBUG
     H COPYRIGHT('(c) Misys International Banking Systems Ltd. 2003')
      *****************************************************************
/*S*D****RPGBASEBNC****************************************************                      CCB023A
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE000104 - LEFCLTQD Reconciliation                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. CCB023A *REDUNDANT Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 BUG7185            Date 19May05               *
      *                 BUG3937            Date 10Sep04               *
      *                 BUG3830 *CREATE    Date 17Jun04
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCB023A - COB Restructure - Input Cycle Termination          *
      *            Restructuring                                      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  BUG7185 - LEC000100 timestamp value not valid (Recompile)    *
      *  BUG3937 - Do not fail the component when an entry is missing *
      *            in main file but present in extension file.        *
      *  BUG3830 - Credit Lines Extension Files Reconciliation        *
      *                                                               *
      *****************************************************************
 
     FLEFCLTLH  IP  AE           K DISK    INCLUDE(LEFCLTD0)
     FFCLTY     IS  AE           K DISK    INCLUDE(FCLTYFMF)
     FQSYSPRT   O    F  132        PRINTER USROPN
 
     D SDBANK        E DS                  EXTNAME(SDBANKPD)
     D DSFDY         E DS                  EXTNAME(DSFDY)
 
     ILEFCLTD0      01
     I                                          FCXFCU        L1M3
     I                                          FCXFTP        L1M2
     I                                          FCXFNO        L1M1
     IFCLTYFMF      02
     I                                          CNUM          L1M3
     I                                          FACT          L1M2
     I                                          FCNO          L1M1
 
     C     *INMR         IFEQ      *OFF
     C                   EXSR      OPNPRTF
     C     *IN01         IFEQ      *ON
     C                   MOVE      *BLANK        Facilty          13
     C                   EVAL      Facilty = FCXFCU + '/' + FCXFTP  + '/' +
     C                                       FCXFNO
     C                   EXCEPT    ERR1
     C                   ELSE
     C                   MOVE      CNUM          A_CNUM            6
     C                   MOVE      FACT          A_FACT            3
     C                   MOVE      FCNO          A_FCNO            2
     C                   MOVE      *BLANK        Facilty          13
     C                   EVAL      Facilty = A_CNUM + '/' + A_FACT + '/' +
     C                                       A_FCNO
     C                   EXCEPT    ERR2
     C                   SETON                                          U7U8                 BUG3937
     C                   ENDIF
     C                   ELSE
     C   02FCXBRC        IFNE      BRCA
     C                   EVAL      Facilty = FCXFCU + '/' + FCXFTP  + '/' +
     C                                       FCXFNO
     C                   EXSR      OPNPRTF
     C                   EXCEPT    ERR3
     C                   SETON                                          U7U8                 BUG3937
     C                   ENDIF
     C                   ENDIF
     C/SPACE 5
      *********************************************************************
      * OPNPRTF - OPEN PRINTER FILE
      *********************************************************************
     C     OPNPRTF       BEGSR
     C     PrintOpen     IFNE      'Y'
     C                   MOVE      'Y'           PrintOpen         1
     C**********         SETON                                          U7U8                 BUG3937
     C                   OPEN      QSYSPRT
     C                   EXCEPT    HEADINGS
     C                   ENDIF
     C                   ENDSR
      *********************************************************************
     C/SPACE 5
      *********************************************************************
      * *PSSR  --- ABNORMAL ERROR CONDITIONS
      *********************************************************************
     C     *PSSR         BEGSR
     C     RunBefore     IFEQ      *BLANKS
     C                   MOVE      'Y'           RunBefore         1
     C                   DUMP
     C                   SETON                                        LRU7U8
     C                   ENDIF
     C                   RETURN
     C                   ENDSR
      *********************************************************************
     C/SPACE 5
      *********************************************************************
      * *INZSR --- INITIAL PROCESSING
      *********************************************************************
     C     *INZSR        BEGSR
     C                   CALL      'AOBANKR0'
     C                   PARM      *BLANKS       PRTCD             7
     C                   PARM      '*FIRST '     POPTN             7
     C     SDBANK        PARM      SDBANK        DSFDY
     C                   ENDSR
      *********************************************************************
     OQSYSPRT   E            HEADINGS       1 03
     O                                           12 'REF:LE000104'
     O                                           70 'LEFCLTQD RECONCILIATION'
     O                                           77 'REPORT'
     O                                          120 'DATE:'
     O                       BJMRDT             128
     O          E            ERR1        1
     O                                            8 'Facility'
     O                       Facilty             22
     O                                           47 'on LEFCLTQD but missing '
     O                                           61 'from FCLTYFM '
     O          E            ERR2        1
     O                                            8 'Facility'
     O                       Facilty             22
     O                                           47 'on FCLTYFM  but missing '
     O                                           61 'from LEFCLTQD'
     O          E            ERR3        1
     O                                            8 'Facility'
     O                       Facilty             22
     O                                           47 'has an invalid branch on'
     O                                           56 'LEFCLTQD'
