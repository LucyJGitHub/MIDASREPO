     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Initialisation program (tranche 1/2)')        *
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Utility                                      *
      *                                                               *
      *  LE0004 - LE Initialisation Program (Tranche 1/2)             *
      *                                                               *
      *  Function:  This program initialise new fields of amended     *
      *             Lending Files for Tranche 1/2 system test         *
      *                                                               *
      *  Called By: LEC0004I - CL LE Initialisation Program           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CLE134             Date 01Aug12               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      *                 CRE073             Date 06Dec10               *
      *                 CLE143             Date 06Dec10               *
      *                 CLE064             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CAS009             Date 16May05               *
      *                 CGL029             Date 01Sep03               *
      *                 CAP084             Date 09Dec03               *
      *                 CLE029             Date 04Sep02               *
      *                 CAP075             Date 08Aug02               *
      *                 CAP074             Date 08Aug02               *
      *                 CAP073             Date 08Aug02               *
      *                 CAS005             Date 16Dec02               *
      *                 CLE028             Date 27Jun02               *
      *                 CAS004             Date 26Jun02               *
      *                 217461             Date 29May03               *
      *                 206369             Date 29May03               *
      *                 215633             Date 31Mar03               *
      * Midas Release 4.01.03 ----------------------------------------*
      *                 210371             Date 29Nov02               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 206537             Date 31Jul02               *
      *                 205778             Date 31May02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP068             Date 25JUN02               *
      *                 197084             Date 11Sep01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CLE011             Date 07Nov00               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 175129             Date 16Feb00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CLE004 *CREATE     Date 06Jan98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference                                *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CLE143 - Original Loan Contract Date & Effective Interest    *
      *           Rate (Recompile)                                    *
      *  CLE064 - Enhancement of Lending Frequencies                  *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus (Recompile)                              *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CAS009 - EIR/AC Accounting (Recompile)                       *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP084 - API wrapper project                                 *
      *         - New ICD setting for Authorising of Fee Adjustments. *
      *  CLE029 - Addition of Severity code and Limit ID to FCLTYFM   *
      *  CAP075 - Lending API enhancements - Loan Amendment.          *
      *           (amendment in common with CAP079)                   *
      *  CAP074 - Lending API enhancements - Loans input.             *
      *  CAP073 - Conversion of MN inputs into modular structure to   *
      *           use as APIs. Facility input Transaction Details     *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *           (Recompile)                                         *
      *  CLE028 - Flat Rate Personal Loans (Rule of 78s)              *
      *           (Recompile)                                         *
      *  CAS004 - Hedge Accounting Phase A (Recompile)                *
      *  217461 - Default the field GASS on CLOANCL or MCLONCL with a *
      *           'P' if the loan is a Participation Sold.            *
      *  206369 - Initialise ACSQ in FCLTYFM.                         *
      *  215633 - Initialise field RTPS in FCLTYFM correctly.         *
      *  210371 - Initialise Consolidated Confirmations Required to   *
      *           'N'.                                                *
      *  206537 - Fields ACDA and COFD are set to blank in PF/CLOANCL *
      *           after running pgm LE0004. Rename the said fields in *
      *           LF/LEHISTL1 to avoid getting mixed up with the same *
      *           field names in PF/CLOANCL.                          *
      *  205778 - Initialise 'Authorisations Active for' fields       *
      *           to 'N'.                                             *
      *  CAP068 - Recompiled for changes to FCLTYFM and FCLTYFN.      *
      *  197084 - Initialise field BPAURV to 'N'.                     *
      *  CLE011 - Rate Fixing Days for Lending Rollover Processing    *
      *           Recompile                                           *
      *  175129 - PCRF should be initialised with branch code plus 12 *
      *           digits. The PCRF intialisation is now done by LE0011*
      *           when CLE010 is switched on.                         *
      *  CLE004 - Customer Lending Enhancements - Syndications        *
      *                                                               *
      *****************************************************************
     FCLOANCK UF  E                    DISK
     FCLOAN   UF  E           K        DISK
     F            CLOANHHF                          KIGNORE
     F            CLOANCKF                          KIGNORE
     F            CLOANZ1F                          KIGNORE
     FLEHISTL1IF  E           K        DISK
     FFCLTYFM UF  E                    DISK
     FGTEESGT UF  E                    DISK
     FLOAMSDK UF  E                    DISK
     FSDCLNDPDUF  E                    DISK                                                   197084
     FMLOAN   UF  E           K        DISK                                                   217461
     F            CLOANHHF                          KIGNORE                                   217461
     F            CLOANZ1F                          KIGNORE                                   217461
     F            CLOANCKF                          KIGNORE                                   217461
     F            CLOANCLF                          KRENAMEMCLONCLF                           217461
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   50  - General read indicator                                *
      *   60  - Read LEHISTL1                                         *
      *   61  - Read SDCLNDPD                                         *                       197084
      *   88  - Chain indicator for CLOAN                             *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      *****************************************************************
      *
     ICLOANCLF
      ** Rename fields on CLOANCL
     I              RECI                            CLRECI
     I              RCDT                            CLRCDT
     I              MRIN                            CLMRIN
     I              ORED                            CLORED
     I              LCD                             CLLCD
     I              CHTP                            CLCHTP
     I              TNLU                            CLTNLU
      *****************************************************************
     IMCLONCLF                                                                                217461
      ** Rename fields on MCLONCL                                                             217461
     I              RECI                            MLRECI                                    217461
     I              RCDT                            MLRCDT                                    217461
     I              MRIN                            MLMRIN                                    217461
     I              ORED                            MLORED                                    217461
     I              LCD                             MLLCD                                     217461
     I              CHTP                            MLCHTP                                    217461
     I              TNLU                            MLTNLU                                    217461
      *****************************************************************                       217461
      *
     IHISTSHQF
      ** Rename fields on LEHISTL1
     I              RECI                            HIRECI
     I              RCDT                            HIRCDT
     I              PTYP                            HIPTYP
     I              CNUM                            HICNUM
     I              BRCA                            HIBRCA
     I              VDAT                            HIVDAT
     I              FCUS                            HIFCUS
     I              FTYP                            HIFTYP
     I              FSEQ                            HIFSEQ
     I              CCY                             HICCY
     I              CPAM                            HICPAM
     I              INTR                            HIINTR
     I              BRTT                            HIBRTT
     I              BRTE                            HIBRTE
     I              RTSP                            HIRTSP
     I              SPIN                            HISPIN
     I              AITC                            HIAITC
     I              AIAP                            HIAIAP
     I              IPRD                            HIIPRD
     I              LCD                             HILCD
     I              FSRP                            HIFSRP
     I              FSGN                            HIFSGN
     I              FPRC                            HIFPRC
     I              MNSG                            HIMNSG                                    206537
     I              ACDA                            HIACDA                                    206537
     I              ACAP                            HIACAP                                    206537
     I              COFD                            HICOFD                                    206537
      *****************************************************************
      *
     ISDBRCH    E DSSDBRCHPD
      **  External DS for Branch Details
      *
     ISDCURR    E DSSDCURRPD
      **  External DS for Currency Details
      *
     IDSSDY     E DSDSSDY
      **  DS for access programs, long data structure
      *
      *****************************************************************
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *                                                                                       217461
      * Set up Key Fields for MCLONCL                                                         217461
      *                                                                                       217461
     C           MLONKY    KLIST                                                              217461
     C                     KFLD           LNRF                                                217461
     C                     KFLD           MLRCDT                                              217461
      *
      * Set up fields for CLOANCK
      *
     C           LOANCL    KLIST
     C                     KFLD           LNRF
     C                     KFLD           CLRCDT
     C                     MOVE 'A'       CLRCDT
      *
     C                     READ CLOANCK                  50
      *
     C           *IN50     DOWEQ'0'
      *
      *  Set Rollover Status Indicator to 'A' (Authorised)
      *
     C                     MOVE 'A'       RSTS
      *
      *  Access CLOANCL file for Transaction Branch (BRCA)
      *
     C           LOANCL    CHAINCLOAN                88
      *
     C           *IN88     IFEQ '1'
     C                     READ CLOANCK                  50
     C                     ITER
     C                     ENDIF
      *
      ** Access the SDBRCHPD for the details of Transaction Branch
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM BRCA      PBRCA   3
     C           SDBRCH    PARM SDBRCH    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C                     READ CLOANCK                  50
     C                     ITER
     C                     ENDIF
      *
      ***If*Rollover*Rransaction*Reference*is*blank*set*it*to*the*first                  175129
      ***three*chars*of*the*MQ*for*Sent*Messages*(A8MQSM)**************                  175129
      *                                                                                  175129
     C***********RPCR      IFEQ *BLANK                                                   175129
     C***********          MOVELA8MQSM    CH03    3                                      175129
     C***********          MOVELCH03      RPCR                                           175129
     C***********          ENDIF                                                         175129
      *
     C                     UPDATCLOANCKF
     C                     READ CLOANCK                  50
     C                     ENDDO
      *
      *****************************************************************
      *
      * Set up fields for CLOANCL
      *
     C**********           Z-ADD1         LNRF                                                CLE148
     C                     MOVEL'AAAAAA'  LNRF                                                CLE148
     C                     MOVE 'A'       CLRCDT
      *
     C           LOANCL    SETLLCLOAN
     C                     READ CLOAN                    50
      *
     C           *IN50     DOWEQ'0'
      *
      * If the alternative name and address field is blank default to
      * B.
      *
     C           NACD      IFEQ *BLANKS
     C                     MOVE 'B'       NACD
     C                     ENDIF
      *
      *  Set Loan Status to 'A' (Authorised)
      *
     C                     MOVE 'A'       LSTS
      *
      *  Set Last M/C Rollover Date to Loan Start date
      *
     C                     MOVE VDAT      LMCD
      *
      *  Set Multi-currency aloowed flag to 'N'
      *
     C                     MOVE 'N'       MCRA
      *                                                                                       217461
      **  Set Participation Indicator to 'P'                                                  217461
     C           PTYP      IFEQ 66                                                            217461
     C           PTYP      OREQ 67                                                            217461
     C           PTYP      OREQ 69                                                            217461
     C           PTYP      OREQ 72                                                            217461
     C                     MOVE 'P'       GASS                                                217461
     C                     ENDIF                                                              217461
      *
      *  Access the history file for history Value Date
      *
     C           LNRF      SETGTLEHISTL1
     C           LNRF      REDPELEHISTL1                 60
      *
     C           *IN60     IFEQ '0'
     C                     MOVE HIVDAT    LRLD
     C                     ELSE
     C                     MOVE VDAT      LRLD
     C                     ENDIF
      *
      ** Access the SDBRCHPD for the details of Transaction Branch
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM BRCA      PBRCA
     C           SDBRCH    PARM SDBRCH    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C                     READ CLOAN                    50
     C                     ITER
     C                     ENDIF
      *
      ***If*Rollover*Rransaction*Reference*is*blank*set*it*to*the*first                  175129
      ***three*chars*of*the*MQ*for*Sent*Messages*(A8MQSM)**************                  175129
      *
     C***********PCRF      IFEQ *BLANK                                                   175129
     C***********          MOVELA8MQSM    CH03    3                                      175129
     C***********          MOVELCH03      PCRF                                           175129
     C***********          ENDIF                                                         175129
      *
     C                     UPDATCLOANCLF
     C                     READ CLOAN                    50
     C                     ENDDO
      *
      *****************************************************************
      *
      * Set up fields for MCLONCL                                                             217461
      *                                                                                       217461
     C**********           Z-ADD1         LNRF                                         217461 CLE148
     C                     MOVEL'AAAAAA'  LNRF                                                CLE148
     C                     MOVE 'A'       MLRCDT                                              217461
      *                                                                                       217461
     C           MLONKY    SETLLMLOAN                                                         217461
     C                     READ MLOAN                    50                                   217461
      *                                                                                       217461
     C           *IN50     DOWEQ'0'                                                           217461
      *                                                                                       217461
      * Default GASS on MCLONCL with a 'P' if the loan is a Part Sold,                        217461
      * as matured loans can still affect the facility history.                               217461
      *                                                                                       217461
     C           PTYP      IFEQ 66                                                            217461
     C           PTYP      OREQ 67                                                            217461
     C           PTYP      OREQ 69                                                            217461
     C           PTYP      OREQ 72                                                            217461
     C                     MOVE 'P'       GASS                                                217461
     C                     ENDIF                                                              217461
      *                                                                                       217461
     C                     UPDATMCLONCLF                                                      217461
     C                     READ MLOAN                    50                                   217461
      *                                                                                       217461
     C                     ENDDO                                                              217461
      *                                                                                       217461
      *****************************************************************                       217461
      *                                                                                       217461
      * Set up fields for FCLTYFM
      *
     C                     READ FCLTYFM                  50
      *
     C           *IN50     DOWEQ'0'
      *
      *  Set Syndication Indicator
      *
     C                     MOVE 'N'       SYIN
      *
      *  Set Syndication Complete flag
      *
     C                     MOVE 'N'       SYCP
      *
      *  Set Drawing Types
      *
     C                     MOVEL'CTDRM'   DTPS      P
      *
      *  Set Rollover Types
      *
     C**********           MOVEL'MTQXY'   RTPS      P                                         215633
     C                     MOVEL'MTQXYZ'  RTPS      P                                         215633
     C                     MOVEL'DWF45789'RTS15                                               CLE064
     C                     MOVE 'VECIJNU' RTS15                                               CLE064
      *
      *  Set Currency Selection
      *
     C                     MOVE 'N'       CSEL
      *
      *  Set Facility Status
      *
     C                     MOVE 'A'       FSTS
      *
      *  Set Committed Date to Approved date
      *
     C                     Z-ADDDTAP      COMD
      *
      *  Set Closing Date to Start date
      *
     C                     Z-ADDDTAP      CDTE
      *
      *  Set Last Availability Date to Expiry Date
      *
     C                     Z-ADDDTEX      AVLD
      *                                                                                       206369
      ** Set Account Sequence Number                                                          206369
      *                                                                                       206369
     C           ACSQ      IFEQ 0                                                             206369
     C                     Z-ADD1         ACSQ                                                206369
     C                     ENDIF                                                              206369
      *
      **  Retrieve the currency details.
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD            B:Return Cd
     C                     PARM '*KEY'    POPTN            I:Option
     C                     PARM FCCY      ##CCY   3        I:Currency Code
     C           SDCURR    PARM *BLANK    DSSDY            O:Format
      *
     C           PRTCD     IFNE *BLANKS
     C                     READ FCLTYFM                  50
     C                     ITER
     C                     ENDIF
      *
      *  Set Default Calculation Basis to the currency calc basis
      *
     C                     MOVE A6DICB    DICB
      *
      ** Access the SDBRCHPD for the details of Transaction Branch
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM BRCA      PBRCA
     C           SDBRCH    PARM *BLANK    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C                     READ FCLTYFM                  50
     C                     ITER
     C                     ENDIF
      *
      ***If*Rollover*Rransaction*Reference*is*blank*set*it*to*the*first                  175129
      ***three*chars*of*the*MQ*for*Sent*Messages*(A8MQSM)**************                  175129
      *
     C***********PCRF      IFEQ *BLANK                                                   175129
     C***********          MOVELA8MQSM    CH03    3                                      175129
     C***********          MOVELCH03      PCRF                                           175129
     C***********          ENDIF                                                         175129
      *
     C                     UPDATFCLTYFMF
     C                     READ FCLTYFM                  50
     C                     ENDDO
      *
      *****************************************************************
      *
      * Set up fields for GTEESGT
      *
     C                     READ GTEESGT                  50
      *
     C           *IN50     DOWEQ'0'
      *
      *  Set Guarantee Status
      *
     C                     MOVE 'A'       GSTS
      *
     C                     UPDATGTEESGTF
     C                     READ GTEESGT                  50
     C                     ENDDO
      *
      *****************************************************************
      *
      * Set up fields for LOAMSDK
      *
     C                     READ LOAMSDK                  50
      *
     C           *IN50     DOWEQ'0'
      *
      *  Set Loan Amendment Status
      *
     C                     MOVE 'A'       ASTS
      *
      ** Access the SDBRCHPD for the details of Transaction Branch
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM BRCA      PBRCA
     C           SDBRCH    PARM *BLANK    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C                     READ LOAMSDK                  50
     C                     ITER
     C                     ENDIF
      *
      ***If*Rollover*Rransaction*Reference*is*blank*set*it*to*the*first                  175129
      ***three*chars*of*the*MQ*for*Sent*Messages*(A8MQSM)**************                  175129
      *
     C***********PCRF      IFEQ *BLANK                                                   175129
     C***********          MOVELA8MQSM    CH03    3                                      175129
     C***********          MOVELCH03      PCRF                                           175129
     C***********          ENDIF                                                         175129
      *
     C                     UPDATLOAMSDKF
     C                     READ LOAMSDK                  50
     C                     ENDDO
      *                                                                                       197084
      ** Initialse field BPAURV                                                               197084
      *                                                                                       197084
     C                     READ SDCLNDPD                 61                                   197084
     C           BPAURV    IFNE 'Y'                                                           197084
     C                     MOVE 'N'       BPAURV                                              197084
     C                     MOVE 'N'       BPCONR                                              210371
     C                     MOVE 'N'       BPFAAU                                              205778
     C                     MOVE 'N'       BPLOAU                                              205778
     C                     MOVE 'N'       BPPPAU                                              205778
     C                     MOVE 'N'       BPPSAU                                              205778
     C                     MOVE 'N'       BPROAU                                              205778
     C                     MOVE 'N'       BPLMAU                                              205778
     C                     MOVE 'N'       BPMRAU                                              205778
     C                     MOVE 'N'       BPREAU                                              205778
     C                     MOVE 'N'       BPTPAU                                              205778
     C                     MOVE 'N'       BPFEAU                                              205778
     C                     MOVE 'N'       BPFSAU                                              205778
     C                     MOVE 'N'       BPGUAU                                              205778
     C                     MOVE 'N'       BPFAAM                                              205778
     C                     MOVE 'N'       BPFAUT                                              205778
     C                     MOVE 'N'       BPCAAU                                              205778
     C                     MOVE 'N'       BPFJAU                                              CAP084
     C                     UPDATSDCLNDD0                                                      197084
     C                     ENDIF                                                              197084
      *
     C                     SETON                     LR
      *
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
