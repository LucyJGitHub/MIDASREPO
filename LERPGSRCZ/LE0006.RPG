     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Initialisation program (tranche 3)')          *
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Utility                                      *
      *                                                               *
      *  LE0006 - LE Initialisation Program (Tranche 3)               *
      *                                                               *
      *  Function:  This program initialise new fields of amended     *
      *             Lending Files for Tranche 3 system test           *
      *                                                               *
      *  Called By: LEC0005I - CL LE Initialisation Program           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CLE141             Date 08Feb16               *
      *  Prev Amend No. CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      *                 CRE073             Date 06Dec10               *
      *                 CLE143             Date 06Dec10               *
      *                 CLE139             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CAS019             Date 24Mar07               *
      *                 CAS016             Date 28Feb06               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 225591             Date 08Jul04               *
      *                 CAS009             Date 16May05               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 CAP074             Date 08Aug02               *
      *                 CAS005             Date 16Dec02               *
      *                 CLE028             Date 27Jun02               *
      *                 CAS004             Date 26Jun02               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 204452             Date 02May02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CLE023             Date 19Nov01               *
      *                 193473             Date 07Feb02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 193012             Date 11Jun01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CLE011             Date 07Nov00               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 175129             Date 16Feb00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CLE009             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 144089             Date 16Oct98               *
      *                 CLE005 *CREATE     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE141 - Currency and Location Business Day Convention       *
      *           (Recompile)                                         *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CLE143 - Original Loan Contract Date & Effective Interest    *
      *           Rate (Recompile)                                    *
      *  CLE139 - Lending Fee Capitalisation (Recompile)              *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus (Recompile)                              *
      *  CAS019 - Upgrade of CAS016 to Midas Plus (Recompile)         *
      *  CAS016 - IAS18 EIR Recalculation (Fee Amortisation Over      *
      *           Whole Period) -Recompile.                           *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  225591 - Facility history changes for repayments due to      *
      *           rollover (recompile)                                *
      *  CAS009 - EIR/AC Accounting (Recompile)                       *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CAP074 - Lending API enhancements - Loans input.             *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *           (Recompile)                                         *
      *  CLE028 - Flat Rate Personal Loans (Rule of 78s)              *
      *           (Recompile)                                         *
      *  CAS004 - Hedge Accounting Phase A (Recompile)                *
      *  204452 - Recompiled due to changed PF/LEFEED.                *
      *  CLE023 - Lending Facility History Improvements. (Recompiled) *
      *  193473 - LEFEED: Write off field FEWOFF should be 0.         *
      *  193012 - Initialised several fields in LEFEED and LEFEEAD.   *
      *  CLE011 - Rate Fixing Days for Lending Rollover Processing    *
      *           Recompile                                           *
      *  175129 - PCRF should be initialised with branch code plus 12 *
      *           digits. The PCRF intialisation is now done by LE0011*
      *           when CLE010 is switched on.                         *
      *  CLE009 - LE I/C Swift Message Generation for Fees            *
      *           Recompiled due to changes in LEFEED                 *
      *  144089 - Initialisation of Payment Due/Taken fields on       *
      *           CLOANCL.                                            *
      *  CLE005 - Customer Lending Enhancements - Tranche 3           *
      *                                                               *
      *****************************************************************
     FLEFEED  UF  E                    DISK
     FLEFEEAD UF  E                    DISK
     FLEMNFUZZUF  E                    DISK                      A
     FLEFCAMZZUF  E                    DISK                      A
     FFACHISA UF  E           K        DISK
     FCLOANCL UF  E                    DISK                               144089
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   01  - Read LEFEED                                           *
      *   02  - Read LEFEEAD                                          *
      *   50  - Read CLOANCL                                          *   144089
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   8 80
      *
      ** Array containing Copyright statement
      *
      *****************************************************************
     ISDBRCH    E DSSDBRCHPD
      **  External DS for Branch Details
      *
     IDSSDY     E DSDSSDY
      **  DS for access programs, long data structure
      *
      *****************************************************************
      *
     C                     MOVEACPY@      CPY2@  80
     C/COPY WNCPYSRC,LE0006C001
      *
      **  Set-up fields for LEFEED records
      *
     C                     READ LEFEED                   01
     C           *IN01     DOWEQ'0'
      *
     C           FESTTL    IFGT 0
     C                     Z-ADDFESTTL    RSTM
     C                     MOVE FEOURS    RONS
     C                     MOVELFETHER    RIBN
     C                     MOVE '   '     OMDB                                                193012
     C                     MOVE FEOSBR    OSDB                                                193012
     C                     ENDIF
      *
     C                     MOVE 'A'       FSTS
     C                     MOVEL'INITINS 'IUSR
     C                     MOVEL'INITAUT 'XUSR
      *
     C                     MOVE 'Y'       FEINDC                                              193012
     C                     MOVE 'N'       SHFI                                                193012
      *                                                                                       193012
      ** Initialise FEWOFF to 0.                                                              193473
      *                                                                                       193473
     C                     Z-ADD0         FEWOFF                                              193473
      *                                                                                       193473
      ** Access the SDBRCHPD for the details of Fee Transaction Branch
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM FEBRCA    PBRCA   3
     C           SDBRCH    PARM *BLANK    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C                     READ LEFEED                   01
     C                     ITER
     C                     ENDIF
      *
      ***If*Fee*Transaction*Reference*is*blank*set*it*to*the*first                       175129
      ***three*chars*of*the*MQ*for*Sent*Messages*(A8MQSM)*********                       175129
      *                                                                                  175129
     C***********PCRF      IFEQ *BLANK                                                   175129
     C***********          MOVELA8MQSM    CH03    3                                      175129
     C***********          MOVELCH03      PCRF                                           175129
     C***********          ENDIF                                                         175129
      *
     C                     UPDATLEFEEDF
     C                     READ LEFEED                   01
     C                     ENDDO
      *
      *****************************************************************
      *
      **  Set-up fields for LEFEEAD records
      *
     C                     READ LEFEEAD                  02
     C           *IN02     DOWEQ'0'
     C           FAMETH    IFGT 0
     C                     Z-ADDFAMETH    RSTM
     C                     MOVELFAOURS    RONS
     C                     MOVELFATHER    RIBN
     C                     MOVE '   '     OMDB                                                193012
     C                     MOVE FAOSBR    OSDB                                                193012
     C                     ENDIF
     C                     MOVE 'A'       SSTS
     C                     MOVEL'INITINS 'IUSR
     C                     MOVEL'INITAUT 'XUSR
      *
     C                     MOVE FASADD    FAVDAT                                              193012
      *                                                                                       193012
      ** Access the SDBRCHPD for the details of Fee Settlement Branch
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM FABRCA    PBRCA   3
     C           SDBRCH    PARM *BLANK    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C                     READ LEFEEAD                  02
     C                     ITER
     C                     ENDIF
      *
      ***If*Fee*Settlement*Reference*is*blank*set*it*to*the*first                        175129
      ***three*chars*of*the*MQ*for*Sent*Messages*(A8MQSM)********                        175129
      *                                                                                  175129
     C***********PCRF      IFEQ *BLANK                                                   175129
     C***********          MOVELA8MQSM    CH03    3                                      175129
     C***********          MOVELCH03      PCRF                                           175129
     C***********          ENDIF                                                         175129
     C                     UPDATLEFEEADF
     C                     READ LEFEEAD                  02
     C                     ENDDO
      *
      *****************************************************************
      *
     C                     READ LEMNFUZZ                 03
     C           *IN03     IFEQ '1'
      *
     C                     MOVE 'T'       RECI
     C                     Z-ADD0         NORE
     C                     Z-ADD0         NINS
     C                     Z-ADD0         NAMD
     C                     Z-ADD0         NDEL
     C                     Z-ADD0         NLRB
     C                     Z-ADD0         NLRA
     C                     Z-ADD0         VIRF
     C                     Z-ADD0         VIRL
     C                     Z-ADD0         VARF
     C                     Z-ADD0         VARL
     C                     Z-ADD0         VDRF
     C                     Z-ADD0         VDRL
     C                     Z-ADD0         VLBF
     C                     Z-ADD0         VLBL
     C                     Z-ADD0         VLAF
     C                     Z-ADD0         VLAL
     C                     WRITELEMNFUZF
      *
     C                     ENDIF
      *
     C                     READ LEFCAMZZ                 03
     C           *IN03     IFEQ '1'
      *
     C                     MOVE 'T'       RECI
     C                     Z-ADD0         NORE
     C                     Z-ADD0         NINS
     C                     Z-ADD0         NAMD
     C                     Z-ADD0         NDEL
     C                     Z-ADD0         NLRB
     C                     Z-ADD0         NLRA
     C                     Z-ADD0         VIRF
     C                     Z-ADD0         VIRL
     C                     Z-ADD0         VARF
     C                     Z-ADD0         VARL
     C                     Z-ADD0         VDRF
     C                     Z-ADD0         VDRL
     C                     Z-ADD0         VLBF
     C                     Z-ADD0         VLBL
     C                     Z-ADD0         VLAF
     C                     Z-ADD0         VLAL
     C                     WRITELEFCAMZF
      *
     C                     ENDIF
      *
     C           *LOVAL    SETLLFACHISA
      *
     C                     READ FACHISAF                 60
     C           *IN60     DOWEQ'0'
     C                     Z-ADD0         FAGNDT
     C                     UPDATFACHISAF
     C                     READ FACHISAF                 60
     C                     ENDDO
      *
      *****************************************************************   144089
      *                                                                   144089
     C                     READ CLOANCL                  50               144089
      *                                                                   144089
     C           *IN50     DOWEQ'0'                                       144089
      *                                                                   144089
      *  Set Payment Due to 'M' (Modified)                                144089
      *                                                                   144089
     C                     MOVE 'M'       PDDI                            144089
      *                                                                   144089
      *  Set Payment Taken to 'M' (Modified)                              144089
      *                                                                   144089
     C                     MOVE 'M'       PTDI                            144089
     C/COPY WNCPYSRC,LE0006C002
      *                                                                   144089
     C                     UPDATCLOANCLF                                  144089
     C                     READ CLOANCL                  50               144089
     C                     ENDDO                                          144089
      *
     C                     MOVE '1'       *INLR
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
