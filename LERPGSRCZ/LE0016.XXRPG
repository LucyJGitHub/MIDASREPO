     H
      *****************************************************************
/*S*D****RPGBASE*******************************************************                     AR322732
     F********************************************************************
     F*                                                                  *
     F*  Midas Customer Lending Module                                LE0010
     F*                                                                  *
     F*       LE0016 - LTRIX FILE SIZE CHANGE                            *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      *  Last Amend No. AR322732*REDUNDANT Date 23Jul12               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 000000             Date 00/00/00              *
     F*                                                                  *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR322732 - The usage of LTRIX RRN and file size fields from  *
      *             the header record will be removed from all input  *
      *             and confirmation print programs.                  *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *                                                               *
     F*****************************************************************
     FLTRIX   UF  F      40            DISK
     FMFLSZFM CF  E                    WORKSTN
     F*
     F**  FUNCTION OF INDICATORS
     F* 01       LTRIX FILE
     F* 03       INCREMENT FILE SIZE BY 100 RECORDS
     F* 11       ERROR IN SCREEN INPUT - TWO INPUTS,ONLY ONE ALLOWED
     F* 13       ERROR IN SCREEN INPUT - NON NUMERIC FILE SIZE
     F* 14       ERROR IN SCREEN INPUT - NO INPUT
     F* 21       EXCEPTION OUTPUT TO LTRIX
     F* 38       SCREEN INPUT ERROR
     F* 39       FAILED CHAIN TO LTRIX
     F*
     F*    U7    SET ON BY F3 NO INCREASE
     F*    U8    DATABASE ERROR - LTRIX HEADER NOT FOUND
     F/SPACE 3
     E                    ZA1        16  1               ZALIGN SR. ARRAY
     E                    ZA2        16  1               ZALIGN SR. ARRAY
     E                    CPY@    1   1 80
     F/SPACE 3
     I**
     I**  LTRIX FILE
     I**
     ILTRIX   NS  01
     I                                        1   1 RECI
     I                                        2   2 RCDT
     I**********                              3   80LNNO                                      CLE148
     I                                        3   8 LNNO                                      CLE148
     I                                    P   9  110NORE
     I                                    P  12  140FLSZ
     I**
     I** Customer Lending Module STATUS DATA STRUCTURE
     I**
     ILESTAT     UDS                            256
     I                                        1   50NLTRIX
     I**
     I** PROGRAM STATUS DATA STRUCTURE
     I**
     IPSDS       SDS
     I                                      244 246 WSID
     I/EJECT
     C                     MOVEACPY@      BIS@   80
     C           *ENTRY    PLIST
     C                     PARM           FLSZ    50
     C                     PARM           INCR    1
     C*
     C                     MOVE 'LTRIX'   FLNAM   5
     C*
     C           1         CHAINLTRIX                39
     C*
     C           *IN39     IFEQ '1'
     C                     SETON                     LRU8
     C                     GOTO END
     C                     END
     C*
     C                     Z-ADDFLSZ      FLSIZ   50
     C*
     C           *IN38     DOUEQ'0'
     C                     EXFMTMFLSZF1
     C                     SETOF                     031138
     C                     SETOF                     13
     C*
     C           *INKC     IFEQ '1'
     C                     SETON                     LRU7
     C                     GOTO END
     C                     END
     C*
     C*
     C** NUMERIC CHECK FOR FILE SIZE ENTRY
     C                     MOVEL*BLANK    ZFIELD 16
     C                     MOVELNEWSIZ    ZFIELD
     C                     Z-ADD0         ZADEC   10
     C                     Z-ADD5         ZADIG   20
     C                     EXSR ZALIGN
     C   99                SETON                     3813
     C                     MOVE ZFIELD    WORKN   50
     C*
     C* CHECK FOR SCREEN INPUT
     C           INCMT     COMP 'Y'                      03
     C           *IN03     IFEQ '0'
     C           NEWSIZ    IFEQ '     '
     C                     SETON                     1438
     C                     END
     C                     ELSE
     C*
     C* CHECK ONLY ONE SCREEN INPUT
     C           WORKN     IFGT 0
     C                     SETON                     3814
     C                     END
     C                     END
     C                     END
     C*
     C** INCREMENT FILE SIZE BY 100 RECORDS
     C           *IN03     IFEQ '1'
     C           FLSZ      ADD  100       FLSZ
     C                     MOVE 'Y'       INCR    1
     C                     ELSE
     C*
     C** UPDATE LESTAT & HEADER WITH NEW SIZE
     C           WORKN     IFGT NLTRIX
     C                     MOVE 'Y'       INCR    1
     C                     END
     C*
     C                     Z-ADDWORKN     NLTRIX
     C*
     C                     Z-ADDWORKN     FLSZ
     C                     END
     C*
     C                     SETON                     21
     C                     EXCPT
     C                     SETOF                     21
     C*
     C                     SETON                     LR
     C           END       TAG                             ***  END    ***
     C*
     C********************************************************************
     C**
     C**   ZALIGN SR. TO VALIDATE AND RIGHT-ALIGN NUMERIC FIELDS.
     C**
     C           ZALIGN    BEGSR                           *** ZALIGN ***
     C**
     C                     SETOF                     929399
     C**
     C**   SAVE INPUT FIELD IN ARRAY, ZA1.
     C                     MOVEAZFIELD    ZA1
     C**
     C**   CALCULATION TO DEFINE NUMBER STRUCTURE CONTROL FIELDS.
     C                     Z-ADDZADIG     ZADIG   20
     C                     Z-ADDZADEC     ZADEC   10
     C**
     C**   CALCULATIONS TO DEFINE/CLEAR FIELDS.
     C                     MOVE ' '       ZA2
     C                     MOVEAZA2       ZFIELD 16
     C                     Z-ADD0         ZX      20
     C                     Z-ADD0         ZY      20
     C**
     C**   ENSURE REQUIRED STRUCTURE OF FIELD IS VALID.
     C           ZADIG     ADD  ZADEC     ZZ      20
     C           ZZ        COMP 15                   99
     C   99                GOTO ZAEND
     C**
     C**   LOOP TO FIND DECIMAL POINT, BLANKS AND CHARACTERS.
     C           ZATAG1    TAG                             ** ZATAG1 TAG *
     C           ZX        ADD  1         ZX
     C**
     C**   CHECK FOR DECIMAL POINT. ERROR IF SECOND DECIMAL POINT.
     C           ZA1,ZX    COMP '.'                      90
     C   90 93             SETON                     99
     C   99                GOTO ZAEND
     C**
     C**
     C**   CHECK FOR 'M' CONSTANT. ERROR IF NOT LAST CHARACTER ENTERED
     C           ZA1,ZX    COMP 'M'                      96
     C   96                DO
     C           ZX        COMP 15                   99  99
     C   99                GOTO ZAEND
     C           ZX        ADD  1         ZX
     C           ZA1,ZX    COMP ' '                  9999
     C   99                GOTO ZAEND
     C           ZX        SUB  1         ZX
     C           ZX        COMP 9                    99
     C   99                GOTO ZAEND
     C                     END
     C**
     C**   CHECK FOR 'T' CONSTANT. ERROR IF NOT LAST CHARACTER ENTERED
     C           ZA1,ZX    COMP 'T'                      97
     C   97                DO
     C           ZX        COMP 15                   99  99
     C   99                GOTO ZAEND
     C           ZX        ADD  1         ZX
     C           ZA1,ZX    COMP ' '                  9999
     C   99                GOTO ZAEND
     C           ZX        SUB  1         ZX
     C           ZX        COMP 12                   99
     C   99                GOTO ZAEND
     C                     END
     C**
     C**   CLEAR ALPHAMERIC CONSTANT FROM ZA1
     C   96
     COR 97                MOVE ' '       ZA1,ZX
     C**
     C**   CHECK FOR BLANKS. BYPASS FOR FIRST BLANK AFTER A DIGIT.
     C           ZA1,ZX    COMP ' '                      91
     C   91 92             GOTO ZATAG2
     C**
     C**   CHECK FOR NUMERIC, IF NOT '.' OR ' '.
     C  N90N91   ZA1,ZX    COMP '0'                    99
     C  N90N91N99ZA1,ZX    COMP '9'                  99
     C   99                GOTO ZAEND                      NOT NUMERIC
     C**
     C**   STORE DIGITS IN ARRAY AND HOW MANY.
     C**   ZY, TOTAL OF DIGITS IN THE INPUT FIELD.
     C**   ZZ, TOTAL OF DIGITS TO THE LEFT OF DECIMAL POINT.
     C  N90N91   ZY        ADD  1         ZY         92
     C  N90N91             MOVE ZA1,ZX    ZA2,ZY
     C   90                Z-ADDZY        ZZ         93  93
     C**
     C           ZX        COMP 16                     94  CHECK IF ALL
     C   94                GOTO ZATAG1
     C**
     C           ZATAG2    TAG                             ** ZATAG2 TAG *
     C**
     C**   IF 'M' SPECIFIED, MULTIPLY ZA1 BY 1 MILLION
     C   96      ZZ        ADD  6         ZZ
     C**
     C**   IF 'T' SPECIFIED, MULTIPLY ZA1 BY 1 THOUSAND
     C   97      ZZ        ADD  3         ZZ
     C**
     C**   FILL IN ZEROS IN ANY BLANKS LEFT OF DECIMAL POINT
     C   96 93
     COR 97 93   ZZ        DOWGTZY
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C                     END
     C**
     C**   IF CONSTANT SPECIFIED WITH NO DECIMAL POINT ZEROISE BLANKS
     C   96N93             DO   6
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C                     END
     C**
     C   97N93             DO   3
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C                     END
     C**
     C**   IF NO DECIMAL POINT FOUND, SET TOTAL FIELD ZZ.
     C  N93                Z-ADDZY        ZZ
     C**
     C**   CHECK FOR EMBEDDED BLANKS.
     C   91 92             MOVEAZA1,ZX    ZFIELD
     C   91 92   ZFIELD    COMP ' '                  9999
     C   99                GOTO ZAEND                      EMBEDDED BLANK
     C**
     C**   ENSURE THAT NUMBER OF DIGITS ENTERED EITHER SIDE
     C**   OF DECIMAL POINT ARE NOT MORE THAN ALLOWED.
     C           ZZ        COMP ZADIG                99
     C           ZY        SUB  ZZ        ZX
     C  N99      ZADEC     SUB  ZX        ZX           9995
     C   99                GOTO ZAEND
     C   95                GOTO ZATAG4                     NO TRAILING
     C**                                                   BLANKS.
     C**   FILL THE TRAILING BLANKS WITH ZEROS.
     C           ZY        ADD  ZX        ZZ
     C           ZATAG3    TAG                             ** ZATAG3 TAG *
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C           ZY        COMP ZZ                     94
     C   94                GOTO ZATAG3
     C**
     C**   RIGHT-ALIGN THE VALUE BY MOVING BACK TO ARRAY, ZA1.
     C           ZATAG4    TAG                             ** ZATAG4 TAG *
     C                     MOVE '0'       ZA1
     C                     Z-ADD16        ZX
     C           ZATAG5    TAG                             ** ZATAG5 TAG *
     C           ZY        COMP 0                        94
     C   94                GOTO ZATAG6
     C                     MOVE ZA2,ZY    ZA1,ZX
     C           ZY        SUB  1         ZY
     C           ZX        SUB  1         ZX
     C                     GOTO ZATAG5
     C           ZATAG6    TAG                             ** ZATAG6 TAG *
     C**
     C**   MOVE VALIDATED AND RIGHT-ALIGNED NUMBER BACK INTO ZFIELD.
     C                     MOVEAZA1       ZFIELD
     C**
     C                     SETOF                     9697
     C**
     C           ZAEND     ENDSR                           * ZAEND ENDSR *
     C**
     C**
     C********************************************************************
     O/SPACE 3
     O**
     O** LTRIX FILE
     O**
     OLTRIX   E        21
     O                         FLSZ      14P
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
