      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Facility History Update Utility')             *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE0017 - Facility History Update Utility                     *
      *                                                               *
      *  Function:  This program drops records from the facility      *
      *             history file which have no matching records on    *
      *             the facility master files.                        *
      *                                                               *
      *  Called By: LEC0017 - Remove unmatched records from facility  *
      *                       history.                                *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CLE106             Date 01Aug04               *
      *                 CLE025             Date 20Oct03               *
      *                 216822  *CREATE    Date 29May03               *
      *                                    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CLE106 - Syndications Manager (Recompile)                    *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  216822 - Drop records from FACHISH/FACHISA if there are no   *
      *           corresponding records on FCLTYFM.                   *
      *                                                               *
      *****************************************************************
      /EJECT
     FFCLTYL1 IF  E           K        DISK
     FFACHISH UF  E           K        DISK
     FFACHISA UF  E           K        DISK
     FLE0017P1O   E             65     PRINTER
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      * 65    Page overflow                                           *
      * 90    READ  fail on FACHISH                                   *
      * 91    CHAIN fail on FCLTYFM                                   *
      * 92    CHAIN fail on FACHISA                                   *
      * 98    Used by Standard Subroutine ZDATE2                      *
      *****************************************************************
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZEDITZ1
     E                    CPY@    1   1 80
      *
     IRUNDAT    E DS
     I              AGMBIN                          MBIN
     I              AGDFF                           DATF
     I              AGRDNB                          RUND
     I*
     I            DS
     I* Datastructure for FACHISA  key
     I                                        1   50@FAC
     I                                        1   30FFTYP
     I                                        4   50FFSEQ
     I*
     I            DS
     I                                    P   1   70OFAM0
     I                                    P   1   71OFAM1
     I                                    P   1   72OFAM2
     I                                    P   1   73OFAM3
      *
     ISDBANK    E DSSDBANKPD
      ** Bank details
      *
     ISDCURR    E DSSDCURRPD
      ** Currency Type
      *
     IDSSDY     E DSDSSDY
      ** Second DS for access programs, long data structure
      *
     IDSFDY     E DSDSFDY
      ** DS for access programs, short data structure
      *
      ** Array containing Copyright statement
     C                     MOVEACPY@      CPY2@  80
     C                     Z-ADD0         FHTOT  130
     C                     Z-ADD0         FHDROP 130
     C                     Z-ADD0         FADTOT 130
      *
     C           KEY001    KLIST                           Key to FCLTYL1
     C                     KFLD           FHBRCH
     C                     KFLD           FHCNUM
     C                     KFLD           FHFTYP
     C                     KFLD           FHFSEQ
      *
     C           KEY002    KLIST                           Key to FACHISA
     C                     KFLD           FHBRCH
     C                     KFLD           FHCNUM
     C                     KFLD           @FAC
      *
     C                     EXSR DATE
     C                     WRITEHEADER1
     C                     WRITEHEADER2
     C                     READ FACHISH                  90
     C           *IN90     DOWEQ*OFF
     C                     ADD  1         FHTOT            Total FACHISH records read
     C   65                WRITEHEADER1
     C   65                WRITEHEADER2
     C   65                SETOF                         65
     C           KEY001    CHAINFCLTYL1              91
      *
     C           *IN91     IFEQ *ON
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM FHFCCY    K101    3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           @RTCD     IFNE *BLANKS
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVE A6NBDP    DNUM    10
     C                     Z-ADDFHOFAM    OFAM0  130
     C                     MOVE OFAM0     ZFIELD 16
     C                     MOVE DNUM      ZADEC   10
     C                     EXSR ZEDIT
      *
     C                     Z-ADDFHEXDT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    EXPDAT  7
      *
     C                     DELETFACHISHF
     C                     ADD  1         FHDROP
     C                     EXSR FACAMT
     C                     WRITEDETAIL1
     C                     ENDIF
      *
     C                     READ FACHISH                  90
     C                     ENDDO
      *
     C                     WRITEHEADER1
     C                     WRITETRAILER1
     C                     MOVE *OFF      *IN98
     C           DATF      COMP 'M'                      98
     C                     SETON                         LR
     C                     RETRN
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZEDITZ2
      ********************************************************************
      *                                                                  *
      *  FACAMT - To delete records in FACHISA that have no              *
      *           corresponding records on the facility file.            *
      *                                                                  *
      *  Called from : Main processing                                   *
      *                                                                  *
      *  Calls : *none                                                   *
      *                                                                  *
      ********************************************************************
     C           FACAMT    BEGSR
      *
     C                     Z-ADDFHFTYP    FFTYP
     C                     Z-ADDFHFSEQ    FFSEQ
     C                     Z-ADD0         FADROP 130
      *
     C           KEY002    CHAINFACHISA              92
      *
     C           *IN92     DOWEQ*OFF
     C                     DELETFACHISA
     C                     ADD  1         FADROP
     C                     ADD  1         FADTOT
     C           KEY002    CHAINFACHISA              92
     C                     ENDDO
      *
     C                     ENDSR
      ********************************************************************
      *                                                                  *
      *  DATE - To determine Midas system date.                          *
      *                                                                  *
      *  Called from : Main processing                                   *
      *                                                                  *
      *  Calls : *none                                                   *
      *                                                                  *
      ********************************************************************
     C           DATE      BEGSR
      * Access bank details
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C           @RTCD     IFNE *BLANKS
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      ********************************************************************
      *                                                                  *
      *  *PSSR - Error handling                                          *
      *                                                                  *
      *  Called from : Automatic error handling                          *
      *                                                                  *
      *  Calls : *none                                                   *
      *                                                                  *
      ********************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
      *
     C                     ENDSR                           ** *PSSR **
     C********************************************************************
      /COPY ZSRSRC,ZDATE2Z3
**  CPY@
(c) Finastra International Limited
