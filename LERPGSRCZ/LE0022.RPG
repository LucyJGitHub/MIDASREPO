     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE History of Margin File')                      *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE0022 - History of Margin File                              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. CLE134             Date 01Aug12               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CAS005  *CREATE    Date 16Dec02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *                                                               *
      *****************************************************************
     FCLOAN   IF  E           K        DISK
     F            CLOANHHF                          KIGNORE
     F            CLOANCKF                          KIGNORE
     F            CLOANZ1F                          KIGNORE
     FMLOAN   IF  E           K        DISK
     F            CLOANHHF                          KIGNORE
     F            CLOANCKF                          KIGNORE
     F            CLOANZ1F                          KIGNORE
     F            CLOANCLF                          KRENAMECLOANCLX
     FMRGHISL0UF  E           K        DISK                      A
      /EJECT
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      /SPACE 3
      *
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
     ISDBANK    E DSSDBANKPD
      ** External data structures for Bank Details
      *
     IDSSDY     E DSDSSDY
      ** Long data structure for access objects
      *
      *****************************************************************
      *                                                               *
      *  Main Processing                                              *
      *                                                               *
      *****************************************************************
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read in Installation Data
      *
     C           *NAMVAR   DEFN           LDA
      *
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     Z-ADD*ZEROS    DBASE
     C                     MOVEL'LE0022'  DBPGM
      *
      ** Access Bank Details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSSDY
      *
      ** Database Error Processing
      *
     C           PRTCD     IFNE *BLANKS
     C                     MOVE 'SDBANKPD'DBFILE
     C                     MOVELPOPTN     DBKEY
     C                     Z-ADD1         DBASE
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           KMHIST    KLIST
     C                     KFLD           LNRF
     C                     KFLD           VDATE   50
      *
     C                     Z-ADD*HIVAL    VDATE
      *
      ** Read through CLOAN File
      *
     C           *LOVAL    SETLLCLOANCLF
     C                     READ CLOAN                    31
      *
     C           *IN31     DOWEQ*OFF
      *
     C           RECI      IFEQ 'D'
      *
     C           KMHIST    SETGTMRGHISD0
     C                     READPMRGHISD0                 30
     C           LNRF      IFEQ MHLNRF
     C           MARG      IFNE MHMARG
     C                     MOVE 'N'       WNEW    1
     C                     EXSR SRUPDT
     C                     ENDIF
     C                     ELSE
     C                     MOVE 'Y'       WNEW
     C                     EXSR SRUPDT
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     READ CLOAN                    31
     C                     ENDDO
      *
      ** Delete records not found in PF/CLOANCL
      *
     C           *LOVAL    SETLLMRGHISD0
     C                     READ MRGHISL0                 31
      *
     C           *IN31     DOWEQ*OFF
      *
     C           MHLNRF    CHAINCLOAN                30
     C           *IN30     IFEQ *ON
     C           MHLNRF    CHAINMLOAN                30
     C                     ENDIF
     C           *IN30     IFEQ *ON
     C                     DELETMRGHISD0
     C                     ENDIF
      *
     C                     READ MRGHISL0                 31
     C                     ENDDO
      *
     C                     MOVE *ON       *INLR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRUPDT - Subroutine to Update MRGHISPD                       *
      *                                                               *
      *****************************************************************
      *
     C           SRUPDT    BEGSR
      *
     C**********           Z-ADDLNRF      MHLNRF                                              CLE148
     C                     MOVELLNRF      MHLNRF                                              CLE148
     C           WNEW      IFEQ 'Y'
     C                     Z-ADDVDAT      MHVDAT
     C                     ELSE
     C                     Z-ADDBJRDNB    MHVDAT
     C                     ENDIF
     C                     Z-ADDMARG      MHMARG
     C                     WRITEMRGHISD0
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program Exception Error Routine                      *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
      *
     C                     CALL 'DBERRCTL'
     C                     ENDIF
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2002
