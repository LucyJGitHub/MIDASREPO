     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Facility Currency Amount Initialisation')     *
      *****************************************************************
      *                                                               *
      *  Midas - Upgrade Utility                                      *
      *                                                               *
      ***LE0017*-*LE*Facility*Currency*Amount*Initialisation***********                       CLE106
      *  LE0027 - LE Facility Currency Amount Initialisation          *                       CLE106
      *                                                               *
      *  Function:  This program initialise the new of facility       *
      *             currency amount brought in by CLE031              *
      *                                                               *
      *  Called By: LEC0011I - CL LE Initialisation Program           *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. CER050             Date 16Jun19               *
      *                 MD046248           Date 27Oct17               *
      *                 CLE141             Date 08Feb16               *
      *                 CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      *                 CRE073             Date 06Dec10               *
      *                 CLE143             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027A            Date 10May06               *
      *                 CLE042             Date 06Sep05               *
      *                 CLE106             Date 01Aug04               *
      *                 223803  *CREATE    Date 23Dec03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *           (Recompile)                                         *
      *  CER050 - Annualised Percentage Rate (Recompile)              *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE141 - Currency and Location Business Day Convention       *
      *           (Recompile)                                         *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CLE143 - Original Loan Contract Date & Effective Interest    *
      *           Rate (Recompile)                                    *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus (Recompile)                              *
      *  CSD027A - Conversion Of Customer Number to Alpha             *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CLE106 - Renamed LEC0017 and LE0017 to LEC0027 and LE0027    *
      *           respectively since LEC0017 and LE0017 aready exist  *
      *           in core library.                                    *
      *  223803 - Exclude various fields from FCLTY logical           *
      *           Initialise facility currency principal amount       *
      *           for main loans against non-revolving credit         *
      *           facilities                                          *
      *                                                               *
      *****************************************************************
     FCLOAN   UF  E           K        DISK         KCOMIT
     F            CLOANHHF                          KIGNORE
     F            CLOANCKF                          KIGNORE
     F            CLOANZ1F                          KIGNORE
     FFCLTYTMPIF  E           K        DISK
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   50  - General read indicator                                *
      *   88  - Chain indicator for CLOAN                             *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     E/COPY ZSRSRC,ZPOWERZ1
      *****************************************************************
      *
     ICLOANCLF
      ** Rename fields on CLOANCL
     I              RECI                            CLRECI
     I              RCDT                            CLRCDT
     I              MRIN                            CLMRIN
     I              ORED                            CLORED
     I              LCD                             CLLCD
     I              CHTP                            CLCHTP
     I              TNLU                            CLTNLU
      *****************************************************************
      *
      *
     ISDCURR    E DSSDCURRPD
      **  External DS for Currency Details
      *
     IDSSDY     E DSDSSDY
      **  DS for access programs, long data structure
      *
      *****************************************************************
      *
      ** Entry parameters
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      * Set up fields for CLOANCL
      *
     C           KFCL      KLIST
     C**********           KFLD           KFCUS   60                                         CSD027A
     C                     KFLD           KFCUS   6                                          CSD027A
     C                     KFLD           KFTYP   30
     C                     KFLD           KFSEQ   20
      *
     C                     READ CLOAN                    50
      *
     C           *IN50     DOWEQ'0'
      *
      ** If a main loan and loan currency is different to facility
      ** currency and facility currency principal amount is zero.
      *
     C           LPFI      IFNE 'I'
     C           LPFI      ANDNE'P'
     C           LPFI      ANDNE'R'
     C           FCCY      ANDNECCY
     C           FPAM      ANDEQ0
      *
     C**********           Z-ADDFCUS      KFCUS                                              CSD027A
     C                     MOVE FCUS      KFCUS                                              CSD027A
     C                     Z-ADDFTYP      KFTYP
     C                     Z-ADDFSEQ      KFSEQ
     C           KFCL      CHAINFCLTYTMP             88
      *
      ** If non-revolving
      *
     C           RVCR      IFEQ 'N'
      *
      **  Retrieve the currency details.
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD   7        B:Return Cd
     C                     PARM '*KEY'    POPTN   7        I:Option
     C                     PARM CCY       ##CCY   3        I:Currency Code
     C           SDCURR    PARM *BLANK    DSSDY            O:Format
     C                     Z-ADDA6NBDP    ZCDPI
      *
      **  Retrieve the facility currency details.
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD            B:Return Cd
     C                     PARM '*KEY'    POPTN            I:Option
     C                     PARM FCCY      ##CCY            I:Currency Code
     C           SDCURR    PARM *BLANK    DSSDY            O:Format
     C                     Z-ADDA6NBDP    ZCDPO
      *
     C                     Z-ADDFCXR      ZEXCH
     C                     MOVE FMDI      ZMD
     C                     Z-ADDCPAM      ZAMTCI
     C                     MOVE CCY       ZCCYI
     C                     MOVE FCCY      ZCCYO
     C                     EXSR ZCONV
     C                     Z-ADDZAMTCO    FPAM
      *
     C                     UPDATCLOANCLF
     C                     ENDIF
     C                     ENDIF
     C                     READ CLOAN                    50
     C                     ENDDO
      *
     C                     COMIT
     C                     SETON                     LR
      *
      *****************************************************************
      /TITLE SR/ZCONV
     C/COPY ZSRSRC,ZCONVZ1
**  CPY@
(c) Finastra International Limited 2003
**  POWER - ARRAY OF POWERS FOR CURRENCY CONVERSION
0000001
0000010
0000100
0001000
0010000
0100000
1000000
