     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Italy-Guarantees PUMA display controller')    *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  RPG/LE0081 - Midas Italy - Guarantees Puma Display Control   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. LUC139             Date 07Jan21               *
      *  Prev Amend No. CER009  *CREATE    Date 16Aug06               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  LUC139 - UCI Italian Returns Upgrade to FM2.1                *
      *  CER009 - Puma II Upgrade to MidasPlus                        *
      *                                                               *
      *****************************************************************
     FLEGTT1L0UF  E           K        DISK         KCOMIT       A    UC
      ** Guarantees extension file - Update
      *
     F            LEGTT1D0                          KRENAMEUPDIDX
     FLE0081DFCF  E                    WORKSTN
     F            LE0081S0                          KRENAMESCREEN
      *
     E                    CPY@    1   1 80
      *
     IA@CPY       DS
     I                                        1  80 CPY@
      *
     IPSDS       SDS
      ** Get program name from PSDS
      *
     I                                        1  10 #0PGM
     I                                      244 253 #0JOB
     I                                      254 263 #0USER
      *
     ILDA         DS                            256
      ** Data structure for data-base processing
      *
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
     INWRCD     E DSLEGTT1L0
      ** Current/previous master file fields
      *
     ISVRCD       DS                             21
      ** Stored file fields for update mode
      *
     IIRFXTB    E DSIRFXTBPD
      *
     IDSFDY     E DSDSFDY
      ** First DS for Access Programs, Short Data Structure
      *
     IDSSDY     E DSDSSDY
      ** Second DS to retrieve file details, Long format
      *
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
     IPDATA       DS                           1024
     I                                        1  15 WKEY
     I                                        1   6 GCUS
     I                                        1   6 WCUSN
     I                                        7  12 WWFCLN
     I                                       13  15 SEQR
     I                                       13  150WSEQN
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  Main Pprocessing                                             *
      *                                                               *
      *****************************************************************
     C                     EXSR SRINIT
      *
      ** Execute specific routine depending on action
      *
     C                     SELEC
      *
      ** Enquiry
      *
     C           ACTION    WHEQ 'E'
     C                     EXSR SRENQ
      *
      ** Delete
      *
     C           ACTION    WHEQ 'D'
     C                     EXSR SRDEL
      *
      ** Insert
      *
     C           ACTION    WHEQ 'I'
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
     C           *IN89     IFEQ *ON
     C                     EXSR SRINS
     C                     ELSE
     C                     EXSR SRAMD
     C                     ENDIF
      *
      ** Amend
      *
     C           ACTION    WHEQ 'A'
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
     C           *IN89     IFEQ *OFF
     C                     EXSR SRAMD
     C                     ELSE
     C                     EXSR SRINS
     C                     ENDIF
      *
     C                     ENDSL
      *
      ** Execute routine to setup return code and exit program
      *
     C                     EXSR SRRTRN
      *****************************************************************
      *                                                               *
      *  SRINS - Routine to handle 'INSERT' action                    *
      *                                                               *
      *****************************************************************
     C           SRINS     BEGSR
      *
      ** Initialize fields
      *
     C                     EXSR SRINZ
      *
     C           *IN68     DOWEQ*OFF
     C           *IN69     ANDEQ*OFF
     C           *IN70     ANDEQ*OFF
      *
      ** Display and handle screen
      *
     C                     EXSR SRSCRN
      *
      ** Clear messages
      *
     C                     EXSR SRCMSG
      *
      ** Refresh
      *
     C           *INKE     IFEQ *ON
      *
      ** Initialize fields
      *
     C                     EXSR SRINZ
      *
      ** Initialize error condition indicators
      *
     C                     MOVE *OFF      *IN75
     C                     MOVE *OFF      *IN32
     C                     MOVE *OFF      *IN33
     C                     MOVE *OFF      *IN37
     C                     MOVE *OFF      *IN38
     C                     MOVE *OFF      *IN39
     C                     MOVE *OFF      *IN40
     C                     MOVE *OFF      *IN41
     C                     ITER
     C                     ENDIF
      *
      ** Previous or exit
      *
     C           *INKC     IFEQ *ON
     C           *INKL     OREQ *ON
      *
     C                     LEAVE
      *
     C                     ENDIF
      *
      ** Validate input
      *
     C                     EXSR SRVAL
      *
      ** If no errors,
      *
     C           *IN75     IFEQ *OFF
      *
      **  Move screen fields to file fields
      *
     C                     EXSR SRSTOF
      *
      ** Write a new record to the file
      *
     C                     EXSR SRWRT
      *
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRAMD - Routine to handle 'AMEND' action                     *
      *                                                               *
      *****************************************************************
     C           SRAMD     BEGSR
      *
      ** Save before image
      *
     C                     EXSR SRSAVE
      *
      ** Set file fields to screen fields
      *
     C                     EXSR SRFTOS
      *
     C           *IN68     DOWEQ*OFF
     C           *IN69     ANDEQ*OFF
     C           *IN70     ANDEQ*OFF
      *
      ** Display and handle screen
      *
     C                     EXSR SRSCRN
      *
      ** Clear messages
      *
     C                     EXSR SRCMSG
      *
      ** Refresh
      *
     C           *INKE     IFEQ *ON
      *
      ** Set file fields to screen fields
      *
     C                     EXSR SRFTOS
      *
      ** Initialize error condition indicators
      *
     C                     MOVE *OFF      *IN75
     C                     MOVE *OFF      *IN32
     C                     MOVE *OFF      *IN33
      *
     C                     ITER
      *
     C                     ENDIF
      *
      ** Previous or exit
      *
     C           *INKC     IFEQ *ON
     C           *INKL     OREQ *ON
      *
     C                     LEAVE
      *
     C                     ENDIF
      *
      ** Validate input
      *
     C                     EXSR SRVAL
      *
      ** If no errors,
      *
     C           *IN75     IFEQ *OFF
      *
      ** Allocate record via update index
      *
     C                     EXSR SRALOC
      *
      ** If record on file and allocated, compare record images,
      ** else, screen will be redisplayed
      *
     C           *IN60     IFEQ *OFF
     C           *IN61     ANDEQ*OFF
      *
     C                     EXSR SRCOMP
      *
      ** If images do not match, screen will be redisplayed with
      ** relevant message
      *
     C           WWMTCH    IFEQ 'Y'
      *
      ** Images match, move screen values to file fields
      *
     C                     EXSR SRSTOF
      *
      ** Update record
      *
     C                     EXSR SRUPD
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRDEL - Routine to handle 'DELETE' action                    *
      *                                                               *
      *****************************************************************
     C           SRDEL     BEGSR
      *
      ** Set indicators on for 'DELETE' mode to protect fields
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
      ** If record not found,
      ** set DB error indicator, setup message, display screen, exit
      *
     C           *IN89     IFEQ *OFF
      *
      ** Record found, save before image
      *
     C                     EXSR SRSAVE
      *
      ** Allocate record via update index
      *
     C                     EXSR SRALOC
      *
      ** If record on file and allocated, compare record images,
      ** else, screen will be redisplayed
      *
     C           *IN60     IFEQ *OFF
     C           *IN61     ANDEQ*OFF
      *
     C                     EXSR SRCOMP
      *
      ** If images do not match, screen will be redisplayed with
      ** relevant message
      *
     C           WWMTCH    IFEQ 'Y'
      *
      ** Images match, delete record
      *
     C                     EXSR SRFTOS
     C                     MOVE *ON       *IN30
     C                     MOVE *OFF      *IN31
     C                     EXSR SRSCRN
     C           *INKL     IFEQ *OFF
     C                     EXSR SRDELR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRENQ - Routine to handle 'ENQUIRY' action                   *
      *                                                               *
      *****************************************************************
     C           SRENQ     BEGSR
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
      ** If record not found,
      ** set DB error indicator, setup message, display screen, exit
      *
     C           *IN89     IFEQ *ON
     C                     MOVE *ON       *IN69
     C                     MOVEL'ITX0004' ZAMSID
     C                     EXSR SRSMSG
      *
      ** Display and handle screen
      *
     C                     EXSR SRSCRN
      *
     C                     ELSE
      *
      ** Record found, set file fields to screen fields
      *
     C                     EXSR SRFTOS
      *
      ** Display and handle screen
      *
     C                     MOVE *ON       *IN30
     C                     MOVE *OFF      *IN31
     C                     EXSR SRSCRN
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRSMSG - Routine to send messages to message subfile         *
      *                                                               *
      *****************************************************************
     C           SRSMSG    BEGSR
      *
     C                     MOVE *ON       *IN25
      *
     C           ZAMSGF    IFEQ *BLANKS
     C                     MOVEL'LERMSGF' ZAMSGF
     C                     ENDIF
     C                     CALL 'SNDERMSG'
     C                     PARM #0PGM     ZAPGM  10
     C                     PARM           ZAPGRL  5
     C                     PARM           ZAMSID  7
     C                     PARM           ZAMSGF 10
     C                     PARM           ZAMSDA132
     C                     PARM           ZAMSTP  7
      *
      ** Clear all fields for default mechanism next time
      *
     C                     MOVEL*BLANK    ZAPGRL
     C                     MOVEL*BLANK    ZAMSID
     C                     MOVEL*BLANK    ZAMSDA
     C                     MOVEL*BLANK    ZAMSTP
     C                     MOVEL*BLANK    ZAMSGF
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRCMSG - Routine to clear program's message queue            *
      *                                                               *
      *****************************************************************
     C           SRCMSG    BEGSR
      *
     C                     CALL 'CLRERMSG'
     C                     PARM #0PGM     ZAPGM
     C                     PARM '*SAME'   ZAPGRL
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRREC - Routine to access file via retrieve index            *
      *                                                               *
      *****************************************************************
     C           SRREC     BEGSR
      *
     C           GTEKEY    CHAINUPDIDX               89
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRALOC - Routine to access record via update index           *
      *                                                               *
      *****************************************************************
     C           SRALOC    BEGSR
      *
     C           GTEKEY    CHAINUPDIDX               6061
      *
      ** If record not on file, setup message 'Record deleted'
      *
     C           *IN60     IFEQ *ON
     C                     MOVEL'ITT4008' ZAMSID
     C                     EXSR SRSMSG
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRUPD - Routine to update file                               *
      *                                                               *
      *****************************************************************
     C           SRUPD     BEGSR
      *
     C                     UPDATUPDIDX                 68
     C           *IN68     IFEQ *OFF
     C                     MOVEL*ON       *IN70
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRWRT - Routine to write a new record to the file            *
      *                                                               *
      *****************************************************************
     C           SRWRT     BEGSR
      *
     C                     WRITEUPDIDX                 68
     C           *IN68     IFEQ *OFF
     C                     MOVEL*ON       *IN70
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRDELR - Routine to delete a record                          *
      *                                                               *
      *****************************************************************
     C           SRDELR    BEGSR
      *
     C                     DELETUPDIDX                 68
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRCOMP - Routine to compare before/after image of records    *
      *                                                               *
      *****************************************************************
     C           SRCOMP    BEGSR
      *
     C           SVRCD     IFEQ NWRCD
     C                     MOVE 'Y'       WWMTCH  1
     C                     ELSE
     C                     MOVE 'N'       WWMTCH
     C                     MOVEL'ITX0002' ZAMSID
     C                     EXSR SRSMSG
      *
      ** Use SETLL to release record lock
      *
     C           GTEKEY    SETLLUPDIDX
      *
      ** Set condition to redisplay screen
      *
     C                     MOVE *ON       *IN60
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRSAVE - Routine to save before image of record via DS       *
      *                                                               *
      *****************************************************************
     C           SRSAVE    BEGSR
      *
     C                     MOVELNWRCD     SVRCD
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRRTRN - Routine to set up return code for calling program   *
      *                                                               *
      *****************************************************************
     C           SRRTRN    BEGSR
      *
      ** DBF update error
      *
     C           *IN69     IFEQ *ON
     C                     MOVE 'Y2U0004' WWRTRN
     C                     ELSE
      *
      ** Database error
      *
     C           *IN68     IFEQ *ON
     C                     MOVE 'USR0563' WWRTRN
     C                     ELSE
      *
      ** F12 pressed
      *
     C           *INKL     IFEQ *ON
     C                     MOVE 'USR0790' WWRTRN
     C                     ELSE
      ** F3 pressed
     C           *INKC     IFEQ *ON
     C                     MOVE 'Y2U9999' WWRTRN
     C                     ELSE
      *
      ** No errors
      *
     C                     MOVE *BLANKS   WWRTRN
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     CLOSELEGTT1L0               99
      *
      ** Exit program
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRDBER - Routine to handle database errors                   *
      *                                                               *
      *****************************************************************
     C           SRDBER    BEGSR
      *
      ** Set on data-base error indicators
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *IN69
      *
      ** Dump program
      *
     C                     DUMP
      *
      ** Call standard DB error handler
      *
     C                     CALL 'DBERRCTL'             83
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRSCRN - Routine to handle screen and validation             *
      *                                                               *
      *****************************************************************
     C           SRSCRN    BEGSR
      *
      ** Display messages
      *
     C                     WRITE#MSGCTL
      *
      ** Display main screen
      *
     C                     EXFMTSCREEN
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRVAL - Routine to control validation of each field          *
      *                                                               *
      *****************************************************************
     C           SRVAL     BEGSR
      *
      ** Initialize error condition indicators
      *
     C                     MOVE *OFF      *IN75
     C                     MOVE *OFF      *IN32
     C                     MOVE *OFF      *IN33
      *
      ** Validate fields
      **   - Codice Aziendale
      **     Must exist on the COL Italian Returns table
      *
     C                     MOVELLICOLC    KEY       P
      *
     C                     CALL 'AOFXTBR0'
     C                     PARM '*BLANKS' RTCD    7
     C                     PARM '*KEY   ' OPTN    7
     C                     PARM 'COL'     TABLE   3
     C                     PARM           KEY    24
     C           IRFXTB    PARM IRFXTB    DSSDY
      *
     C           RTCD      IFNE *BLANK
     C                     MOVEL'ITU0001' ZAMSID
     C                     EXSR SRSMSG
     C                     MOVE *ON       *IN32
     C                     MOVE *ON       *IN75
     C                     ELSE
     C                     MOVELKEY       LICOLC
     C                     ENDIF
      *
      **  - Stato Rapporto
      **     Must exist on the BAV Italian Returns table
      *
     C                     MOVELLIX563    KEY       P
      *
     C                     CALL 'AOFXTBR0'
     C                     PARM '*BLANKS' RTCD
     C                     PARM '*KEY   ' OPTN
     C                     PARM 'BAV'     TABLE
     C                     PARM           KEY
     C           IRFXTB    PARM IRFXTB    DSSDY
      *
     C           RTCD      IFNE *BLANK
     C                     MOVEL'ITU0002' ZAMSID
     C                     EXSR SRSMSG
     C                     MOVE *ON       *IN33
     C                     MOVE *ON       *IN75
     C                     ELSE
     C                     MOVELKEY       LIX563
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRINZ - Routine to initialize screen fields with defaults    *
      *                                                               *
      *****************************************************************
     C           SRINZ     BEGSR
      *
      ** Reset Working fields to blanks
      *
     C                     EXSR SRBTOS
      *
      ** Default value for 'Stato Rapporto' is 061
      *
     C                     MOVE '061'     LIX563
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRFTOS - Routine to move file fields to screen fields        *
      *                                                               *
      *****************************************************************
     C           SRFTOS    BEGSR
      *
      ** Reset Working fields to blanks
      *
     C                     EXSR SRBTOS
      *
      ** Populate display file from file
      *
     C                     MOVE XPCOLC    LICOLC
     C           XP563     IFNE *ZERO
     C                     MOVE XP563     LIX563
     C                     ENDIF
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRSTOF - Routine to move screen fields to file fields        *
      *                                                               *
      *****************************************************************
     C           SRSTOF    BEGSR
      *
      ** Move key field to file field
      *
     C                     MOVE GCUS      XPCNUM
     C                     MOVE WWFCLN    XPFCLN
     C                     Z-ADDWSEQN     XPGUSN
      *
      ** Move data fields to file fields
      *
     C                     MOVE LICOLC    XPCOLC
     C                     MOVE LIX563    XP563
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRINIT - Initialisation routine                              *
      *                                                               *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      ** Get parameters from calling program
      *
     C           *ENTRY    PLIST
     C                     PARM           ACTION  1
     C                     PARM           PDATA
     C                     PARM           WWRTRN  7
      *
      ** Redefine data-base error fields for program
      *
     C           *LIKE     DEFN DBFILE    WWBFIL
     C           *LIKE     DEFN DBKEY     WWBKEY
     C           *LIKE     DEFN DBASE     WWBASE
      *
      ** Setup file value used in database error during access to
      ** retrieval index
      *
     C           *LIKE     DEFN DBFILE    WWEXTF
     C                     MOVEL'LEGTT1L0'WWEXTF
      *
      ** Access Bank Details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Initialise display mode indicators
      *
     C           ACTION    IFEQ 'I'
     C           ACTION    OREQ 'A'
     C                     MOVEL*ON       *IN31
     C                     ELSE
     C                     MOVEL*OFF      *IN31
     C                     ENDIF
     C           ACTION    IFEQ 'E'
     C                     MOVEL*ON       *IN30
     C                     ENDIF
      *
      ** Initialise error indicators
      *
     C                     MOVE *OFF      *IN32
     C                     MOVE *OFF      *IN33
     C                     MOVEL*OFF      *IN75
      *
     C                     OPEN LEGTT1L0               99
      *
      ** Key to access gurantee file
      *
     C           GTEKEY    KLIST
     C                     KFLD           GCUS
     C                     KFLD           WWFCLN
     C                     KFLD           WSEQN
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SRBTOS - Routine to move blanks to Screen fields             *
      *                                                               *
      *****************************************************************
     C           SRBTOS    BEGSR
      *
      ** Reset fields blank
      *
     C                     MOVEL*BLANKS   LICOLC
     C                     MOVEL*BLANKS   LIX563
      *
     C                     ENDSR
      *
**  CPY@
(c) Misys Internation Banking Systems Ltd. 2006
