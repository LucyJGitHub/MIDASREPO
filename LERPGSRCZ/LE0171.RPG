     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Facilities maintenance - LU window')          *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE0171 - LE Facilities - Display File                        *
      *                                                               *
      *  Function:  This program allows to maintain new fields        *
      *             on LE Facilities                                  *
      *                                                               *
      *  Called By: LE0170  - Facility Details Enquiry                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      *  Last Amend No. CLE134             Date 01Aug12               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
      *
      *   The following routines CANNOT be changed :
      *   ========================================
      *
      *   (Main processing)
      *   SRENQ
      *   SRRTRN
      *   SRSCRN
      *   SRREC
      *
      *   The following routines can be changed :
      *   =====================================
      *
      *   SRINIT for specific initialization
      *   SRDBER to handle database errors
      *   SRVAL  to initialize error indicators and control validation
      *          of more/less fields
      *   SRINZ  to setup fields with defaults
      *   SRFTOS to move additional fields to the screen fields
      *
      *
      * - Changes to the existing code should be reduced to a minimum,
      *   using separate subroutines in order to preserve the program
      *   structure as defined in the skeleton, thus reducing
      *   maintenance efforts.
      *
      * - Moreover, no functionality should be added unless
      *   specifically required. In this case, it should be very well
      *   documented in the header box.
      *
      * - The data structure used to save the before image of the
      *   record is called SVRCD.
      *   Only the length should be changed.
      *
      * - The data structure used to access the current record
      *   via the DS name is called NWRCD.
      *   Only the file name should be changed.
      *
      * - The files must have their record formats renamed to:
      *      RTVIDX for the retrieve index
      *      SCREEN for the screen format
      *   Any file/screen access will be done through the renamed
      *   format so that these routines remain unchanged.
      *
      *-------------------------------------------------------------------------
      *
      *   Naming conventions
      *   ==================
      *
      * - Work fields used in the program should start with WW or WU
      *   i.e. WWPLEX or WUPLEX
      *
      * - Screen fields should start with #0 (for 1st screen format),
      *   i.e. #0PLEX
      *
      * - Key fields should start with K ,i.e. KCNUM
      *   (Also for fields used in KLIST's)
      *
      * - Subroutines should start with SR, i.e. SRVAL for validation,
      *   SRINIT for initial routine , etc...
      *
      *-------------------------------------------------------------------------
      *  Use of indicators:
      *
      *  (Screen field error indicators should start with 20 in
      *   ascending order)
      *
      *  *IN15  -  On=Delete/Enquiry, Protect field.
      *            Off=Insert/Amend, Underline field.
      *  *IN69  -  SFLEND control
      *  *IN75  -  General error indicator. Used to condition output
      *            of error messages and redisplay screen.
      *  *IN81  -  Call to DBERRCTL ended in error
      *  *IN82  -  Plexus capital currency in error
      *  *IN89  -  Extension record not found via retrieval index
      *
      *  *INKL  -  F12 pressed, exit
      *  *INLR  -  End processing
      *
      *  *INU7  -  :    Data-base
      *  *INU8  -  :  error control
      *
      *-------------------------------------------------------------------------
     FSDCURRL1IF  E           K        DISK
      *
     FLEFCX2L0IF  E           K        DISK
     F            FCLTYMF6                          KRENAMERTVIDX
      ** Extended Customer Loans  Retrieval index        Prefix VE.
      *
     FLE0171DFCF  E                    WORKSTN
     F            LE0171F6                          KRENAMESCREEN
      **                     Display file               Prefix #0.
      *
      /EJECT
      *-------------------------------------------------------------------------
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
      /EJECT
      *-------------------------------------------------------------------------
     I           SDS
      *
      ** Get program name from PSDS
      *
     I                                     *PROGRAM WWPGM
     I                                      244 253 SWSID
     I                                      254 263 SUSER
      *
     IRUNDAT    E DSRUNDAT
      *
      /EJECT
      *-------------------------------------------------------------------------
      *
     IA@CPY       DS
     I* Copyright array
     I                                        1  80 CPY@
      *
     IDLDA        DS                            256
      *
      ** Data structure for data-base processing
      *
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      /EJECT
      *-------------------------------------------------------------------------
     IDSFDY     E DSDSFDY
      *
      ** First DS for access programs, short data structure
      *
     I@MMOD     E DSSDMMODPD
      *
      ** Dummy record formats for access to Midas modules details
      *
     INWRCD     E DSLEFCX2PD
      *
      ** Current/previous master file fields
      *
      /EJECT
      *-------------------------------------------------------------------------
     ISVRCD       DS                             26
      *
      ** Stored master file fields
      *
      /EJECT
      *-------------------------------------------------------------------------
      *
      ** Get the data structure passed from calling program
      *
     IDATA        DS                           1024
      *
     I                                        1   6 #1CNUM
     I                                        7   9 #1FACT
     I                                       10  11 #1FCNO
     I                                       12  12 #1RCTP
     I                                       13  15 #1CCY
     I                                       16  32 #1AMNT
      /EJECT
      *-------------------------------------------------------------------------
      * Main processing
      *-------------------------------------------------------------------------
     C           KEY       KLIST
     C                     KFLD           KCNUM
     C                     KFLD           KFACT
     C                     KFLD           KFCNO
      *
      ** Execute initial routine
      *
     C                     EXSR SRINIT
      *
     C           ACTION    CASEQ'E'       SRENQ
     C                     ENDCS
      *
      ** Execute routine to setup return code and exit program
      *
     C                     EXSR SRRTRN
      *
      /EJECT
      *-------------------------------------------------------------------------
      * SRENQ - Routine to handle 'ENQUIRY' action
      *-------------------------------------------------------------------------
      *
     C           SRENQ     BEGSR
      *
      ** Set indicators on for 'ENQUIRY' mode to protect fields
      *
     C                     MOVE '1'       *IN15
      *
      ** Check whether record exists
      *
     C                     EXSR SRREC
      *
      ** If record not found,
      ** set DB error indicator, setup message, display screen, exit
      *
     C           *IN89     IFEQ '1'
      *
     C                     ELSE
      *
      ** Record found, set file fields to screen fields
      *
     C                     EXSR SRFTOS
      *
     C                     MOVE '0'       *IN75
      *
      ** Display and handle screen
      *
      ** do while *in75 is *OFF or F12
      *
     C           *IN75     DOUEQ*OFF                       No Error
     C           *INKL     OREQ *ON                        or F12
      *
     C                     EXSR SRSCRN
     C                     ENDDO
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *-------------------------------------------------------------------------
      * SRREC - Routine to access file via retrieve index
      *-------------------------------------------------------------------------
      *
     C           SRREC     BEGSR
      *
     C           KEY       CHAINRTVIDX               89
      *
     C                     ENDSR
      /EJECT
      *-------------------------------------------------------------------------
      * SRRTRN - Routine to set up return code for calling program
      *-------------------------------------------------------------------------
      *
     C           SRRTRN    BEGSR
      *
      ** DBF update error
      *
     C           *IN69     IFEQ '1'
     C                     MOVE 'Y2U0004' WWRTRN
     C                     ELSE
      *
      ** F12 pressed
      *
     C           *INKL     IFEQ '1'
     C                     MOVE 'USR0790' WWRTRN
     C                     ELSE
      *
      ** No errors
      *
     C                     MOVE *BLANKS   WWRTRN
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Exit program
      *
     C                     MOVE '1'       *INLR
      *
     C                     ENDSR
      /EJECT
      *-------------------------------------------------------------------------
      * SRDBER - Routine to handle database errors
      *-------------------------------------------------------------------------
      *
     C           SRDBER    BEGSR
      *
      ** Update data area LDA
      *
     C           *NAMVAR   DEFN LDA       DLDA
     C           *LOCK     IN   DLDA
     C                     MOVEL'LE0171  'DBPGM
     C                     MOVE WWBFIL    DBFILE
     C                     MOVE WWBKEY    DBKEY
     C                     MOVE WWBASE    DBASE
     C                     OUT  DLDA
      *
      ** Set on data-base error indicators
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *IN69
      *
      ** Dump program
      *
     C                     DUMP
      *
      ** Call standard DB error handler
      *
     C                     CALL 'DBERRCTL'             81
      *
     C                     ENDSR
      /EJECT
      *-------------------------------------------------------------------------
      * SRSCRN - Routine to handle screen and validation
      *-------------------------------------------------------------------------
      *
     C           SRSCRN    BEGSR
      *
      ** Display messages
      *
     C                     WRITE#MSGCTL
      *
      ** Display main screen
      *
     C                     EXFMTSCREEN
      *
     C                     ENDSR
      /EJECT
      *-------------------------------------------------------------------------
      /EJECT
      *-------------------------------------------------------------------------
      * SRINZ - Routine to initialize screen fields with defaults
      *-------------------------------------------------------------------------
      *
     C           SRINZ     BEGSR
      *
     C                     Z-ADD0         #0DGRI
     C                     MOVE *BLANKS   #0SUBR
     C                     MOVE *BLANKS   #0MOBL
      *
     C                     ENDSR
      /EJECT
      *-------------------------------------------------------------------------
      * SRFTOS - Routine to move file fields to screen fields
      *-------------------------------------------------------------------------
      *
     C           SRFTOS    BEGSR
      *
     C                     MOVELVNDGRI    #0DGRI
     C                     MOVE VNSUBR    #0SUBR
     C                     MOVELVNMOBL    #0MOBL
      *
     C                     ENDSR
      /EJECT
      *-------------------------------------------------------------------------
      * SRINIT - Routine to handle initial processing
      *-------------------------------------------------------------------------
      *
     C           SRINIT    BEGSR
      *
      ** Get parameters from calling program
      *
     C           *ENTRY    PLIST
     C                     PARM           ACTION  1
     C                     PARM           DATA
     C                     PARM           WWRTRN  7
      *
      ** Setup key values using transaction data passed from caller
      *
     C           *LIKE     DEFN VNCNUM    KCNUM
     C           *LIKE     DEFN VNFACT    KFACT
     C           *LIKE     DEFN VNFCNO    KFCNO
     C                     MOVEL#1CNUM    KCNUM
     C                     MOVEL#1FACT    KFACT
     C                     MOVEL#1FCNO    KFCNO
      *
      ** Redefine data-base error fields for program
      *
     C           *LIKE     DEFN DBFILE    WWBFIL
     C           *LIKE     DEFN DBKEY     WWBKEY
     C           *LIKE     DEFN DBASE     WWBASE
      *
      ** Setup file value used in database error during access to
      ** retrieval index
      *
     C           *LIKE     DEFN DBFILE    WWEXTF
     C                     MOVEL'LEFCX2PD'WWEXTF
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
      ** Initialise error indicators
      *
      ** Call access program for Midas Modules Details
      *
     C                     CALL 'AOMMODR0'
     C                     PARM '*MSG    '@RTCD   7
     C                     PARM '*FIRST  '@OPTN   7
     C           @MMOD     PARM @MMOD     DSFDY
      *
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDMMODPD'WWBFIL
     C                     MOVEL'99'      WWBASE
     C                     MOVEL@OPTN     WWBKEY
     C                     EXSR SRDBER
     C                     ENDIF
      *
     C           BGLRIN    IFNE 'Y'
     C                     MOVE *BLANKS   WWRTRN
     C                     MOVE '1'       *INLR
     C                     RETRN
     C                     ENDIF
      *
      ** Access Currency file to retrieve decimal position
      ** for Correction Amount
      *
     C           #1CCY     CHAINSDCURRL1             88
     C           *IN88     IFEQ '1'
     C                     MOVEL'SDCURRL1'WWBFIL
     C                     Z-ADD10        WWBASE
     C                     MOVEL#1CCY     WWBKEY
     C                     EXSR SRDBER
     C                     ENDIF
      *
     C                     ENDSR
** CPY@
(c) Misys International Banking Systems Ltd. 2005
