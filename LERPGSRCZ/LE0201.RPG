     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Customer portfolio enquiry')                  *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Customer Lending Module                              *
     F*                                                               *
     F*     LE0201 - CUSTOMER PORTFOLIO ENQUIRY                       *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD036924           Date 06Nov19               *
      *  Prev Amend No. MD053295           Date 13Apr19               *
      *                 MD050139           Date 23Mar18               *
      *                 MD046248           Date 27Oct17               *
      *                 MD047993           Date 23Sep17               *
      *                 MD035545           Date 11Sep15               *
      *                 CGL184             Date 07Dec16               *
      *                 AR830785           Date 07May13               *
      *                 AR996895           Date 25Nov12               *
      *                 CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      *                 CLE064             Date 06Dec10               *
      *                 263666             Date 01Feb10               *
      *                 CER059             Date 19Jul10               *
      *                 CER016A            Date 19May08               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 246141             Date 26Mar07               *
      *                 245082             Date 30Jan07               *
      *                 164847             Date 30Nov06               *
      *                 BUG12990           Date 05Jan07               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 242286             Date 25Sep06               *
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CLE106             Date 27Apr05               *
      *                 CSW037A            Date 02May05               *
      *                 CIR013             Date 25Apr05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 BUG2620            Date 12May04               *
      *                 CGL029             Date 01Sep03               *
      *                 CLE028             Date 27Jun02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CLE023             Date 19Nov01               *
      *                 199973             Date 20Dec01               *
      *                 199124             Date 11Dec01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CLE011             Date 07Nov00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CIR004             Date 20Jan00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 156778             Date 12Mar99               *
      *                 155147             Date 18Feb99               *
      *                 147830             Date 17Dec98               *
      *                 147221             Date 20Nov98               *
      *                 143510             Date 29Sep98               *
     F*                 121768             DATE 19Feb98               *
     F*                 126017             DATE 17Nov97               *
     F*                 128954             Date 17Feb98               *
     F*                 CLE005             Date 22May97               *
     F*                 097885             DATE 31JAN97               *
     F*                 095244             DATE 01NOV95               *
     F*                 095238             DATE 31OCT95               *
     F*                    088131             DATE 20JUN95            *
     F*                    072584             DATE 07OCT94            *
     F*                    066550             DATE 04FEB94            *
     F*                    054104 (BH4505)    DATE 27APR93            *
     F*                    S01392             DATE 14SEP92            *
     F*                    E27023             DATE 02JUN92 (19SEP91)  *
     F*                    S01311             DATE 07FEB91            *
     F*                                                               *
     F*---------------------------------------------------------------*
      *                                                               *
      *  MD036924 - Correct error message is displayed when customer  *
      *             is closed                                         *
      *  MD053295 - Lending loan with Interest Calculation Basis      *
      *             method 3 (30/360) has wrong interest payment      *
      *             amount for month of March (Recompile)             *
      *  MD050139- AOSARDR0 called every time causing performance     *
      *            issue. Recompile due to changes in /COPY           *
      *            ZFREQ/ZFRQ*.                                       *
      *  MD046248 - Finastra Rebranding                               *
      *  MD047993 - Modified MD035545. Removed dependency on Accrue   *
      *             on Last Day indicator.                            *
      *  MD035545 - LE: Interest Calculation in Leap year. Consider   *
      *             Accrue on Last Day Indicator for method 6.        *
      *  CGL184 - New Frequencies for Account Statements (Recompile)  *
      *  AR830785 - Recompiled due to changes in core hook RULE78Z1.  *
      *             (Child: AR830786)                                 *
      *  AR996895 - Incorrect acrrual interest calculation            *
      *             (Child: AR996897) (Recompile)                     *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference                                *
      *  CLE064 - Enhancement of Lending Frequencies (Recompile)      *
      *  263666 - Recompiled due to changes in ZINTDYZ2               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER016A - German Interest Calculation: Upgarde of FGE059     *
      *            to Midas Plus (Recompile)                          *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  246141 - Recompiled due to changes in ZINTDYZ2               *
      *  245082 - Loans with first and last day accrual should be     *
      *           treated as first day accruals until maturity        *
      *           date.  Then add one day on the maturity date.       *
      *           Patterned after fix 235784 from Release 4.          *
      *  164847 - Recompile over changes in /COPY ZINTDYZ2.           *
      *  BUG12990 - Recompiled due to changes in ZINTDYZ2/ZINTDYZ2LE  *
      *  242286 - Recompile over changes in /COPY ZINTDYZ2.           *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CLE106 - Syndication Manager                                 *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CIR013 - Additional Calculation Method for FRA/IRS/CCF       *
      *           Recompile over GLINTC and ZINTDY.                   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  BUG2620- Re-compiled due to changed LE0201FM.                *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE028 - Flat Rate Personal Loans (Rule of 78s)              *
      *  CLE023 - Lending Facilty History Improvements.               *
      *  199973 - Seton indicator 46 for rolldown even if guarantee   *
      *           sequence no. are all the same.                      *
      *  199124 - 'Synd Loan' column still being displayed eventhough *
      *           CLE006 (Syndication) is off.                        *
      *  CLE011 - Rate Fixing Days for Lending Rollover Processing    *
      *           Recompile                                           *
      *  CIR004 - Dealing Calculation Method Change.                  *
      *         - Recompiled due to GLINTC and ZINTDY changes         *
      *  156778 - Guarantees should only be displayed if the value    *
      *           date has been reached.                              *
      *  155147 - 'Prime' should be set to 'Prmry' to avoid confusion.*
      *  147830 - New narrative on sfl display. Loans identified as   *
      *           Prime,Intnl,Part or Risk.                           *
     F*  147221 - Credit Agreement extra processing                   *
      *  143510 - Shows extra one day in interest amount computation. *
      *           Applied fix 065277 to ignore First Day Accrual ind. *
     F*  121768 - Recompile over changed GLINTCZ1                     *
     F*  126017 - Correction to interest calculation for calculation  *
     F*           basis 6 in SR/GLINTC. Add SR/ZDAT10                 *
     F*  128954 - Call new pgm LE0212 for CLE004 instead of LE0210    *
     F*  CLE005 - Customer Lending Enhancements - Others (T3)         *
     F*  097885 - Recompiled for wrong calculation of number of day   *
     F*           interest (ZINTDYZ2).                                *
     F*  095244 - Recompiled for Calculation method 3                 *
     F*  095238 - Recompiled for Calculation method 3                 *
     F*  088131 - For Guarantees Issued, screen should say 'Interest' *
     F*           instead of 'Fee' as this enquiry shows only the     *
     F*           interest rate and interest accrued, and does not    *
     F*           show details of any fees - recompile over amended   *
     F*           display file.                                       *
     F*    072584 - Initialize indicators 31-36 so as not to          *
     F*             highlight the related fields based on previous    *
     F*             validation.                                       *
     F*    066550 - GLINTC has had historical problems / inaccuracies *
     F*             and is now externaly descibed and changed to use  *
     F*             ZINTDYZ1. See document atached to error no on GEMS*
     F*     054104  -  JAPANESE SUB MODULE ENHANCEMENTS HAVE BEEN     *
     F*    (BH4505)    APPLIED SUCH THAT THEY SCREW UP THE INTEREST   *
     F*                CALCULATION                                    *
     A*     S01392  -  JAPANESE SUB MODULE ENHANCEMENTS               *
     F*     E27023 (BH1133) - Show participations purchased under     *
     F*                       original borrower as well as selling    *
     F*                       customer                                *
     F*     S01311  -  EXPOSURE MANAGEMENT ENQUIRIES                  *
     F*                New program created to replace LE0200          *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FLE0201FMCF  E                    WORKSTN
     F                                        RRN   KSFILE LE0201S1
     F                                        RRN   KSFILE LE0201S2
     F                                        RRN   KSFILE LE0201S3
     F                                        RRN   KSFILE LE0201S4
     F                                        RRN   KSFILE LE0201S5
     F/COPY WNCPYSRC,LE0201F002
     FLEREF   IF  E           K        DISK         KINFSR *PSSR
     F/COPY WNCPYSRC,LE0201F003
     FCLOAN   IF  E           K        DISK         KINFSR *PSSR
     F            CLOANHHF                          KIGNORE
     F************CLOANCKF                          KIGNORE                0120
     F            CLOANZ1F                          KIGNORE
     F/COPY WNCPYSRC,LE0201F001
     FFCLTY   IF  E           K        DISK         KINFSR *PSSR
     F            FCLTYHHF                          KIGNORE
     F            FCLTYZZF                          KIGNORE
     FGTREF   IF  E           K        DISK         KINFSR *PSSR
     F            GTREFHGF                          KIGNORE
     FGTEES   IF  E           K        DISK         KINFSR *PSSR
     F            GTEESHHF                          KIGNORE
     F            GTEESZZF                          KIGNORE
     FSDCURRPDIF  E                    DISK         KINFSR *PSSR      UC
     F/COPY WNCPYSRC,LE0201F004
      /EJECT
     F********************************************************************
     F*                                                                  *
     F* INDICATORS                                                       *
     F*                                                                  *
     F* ID F  C  H  L    FUNCTION OF INDICATORS                          *
     F*       06      LINKED ENQUIRY                                     *
     F*       11      F1 TAKEN FROM FIRST SCREEN                         *
     F*       12      F12 TAKEN FROM FIRST SCREEN                        *
     F*       18      ROLLUP                                             *
     F*       19      ROLLDOWN                                           *
     F*       20      MULTI-BRANCHING (DISPLAYS BRANCH ON SCREEN)        *
     F*       21      F1 TAKEN FROM SECOND SCREEN                        *
     F*       22      F12 TAKEN FROM SECOND SCREEN                       *
     F*       23      Display current principal as *'s if greater        *147830
     F*               than max size on sfl.                              *147830
     F*       24      Display daily interest amount as *'s if greater    *147830
     F*               than max size on sfl.                              *147830
     F*       25      Display accrued interest to date as *'s if         *147830
     F*               greater than max size on sfl.                      *147830
      *       26      CLE006 (Syndicated Lending) is on                  *                    199124
     F*       28      STOP ACCRUALS INDICATOR ON                         *
     F*       30      GENERAL ERROR IN VALIDATION                        *
     F*       31      ERROR IN VALIDATION OF LOANS MENU ENTRY            *
     F*       32      ERROR IN VALIDATION OF PARTS. PURCH. MENU ENTRY    *
     F*       33      ERROR IN VALIDATION OF PARTS. SOLD MENU ENTRY      *
     F*       34      ERROR IN VALIDATION OF GUARANTEES BY MENU ENTRY    *
     F*       35      ERROR IN VALIDATION OF G'TEES ISSUED MENU ENTRY    *
     F*       36      ERROR IN VALIDATION OF FACILITIES MENU ENTRY       *
     F*       37      ERROR IN VALIDATION OF BRANCH CODE                 *
     F*       38      ERROR IN VALIDATION OF COMPANY CODE                *
     F*       39      ERROR IN VALIDATION OF CUSTOMER                    *
     F*       41-43   SUBFILE INDICATORS                                 *
     F*       45      ROLLUP ENABLED                                     *
     F*       46      ROLLDOWN ENABLED                                   *
     F*       51      LOANS OPTION TAKEN ON FIRST SCREEN                 *
     F*       52      PARTS. PURCHASED OPTION TAKEN ON FIRST SCREEN      *
     F*       53      PARTICIPATIONS SOLD OPTION TAKEN ON FIRST SCREEN   *
     F*       54      GUARANTEES BY OPTION TAKEN ON FIRST SCREEN         *
     F*       55      GUARANTEES ISSUED OPTION TAKEN ON FIRST SCREEN     *
     F*       56      FACILITIES OPTION TAKEN ON FIRST SCREEN            *
      *       57      SAR CLE023 is installed                         *                       CLE023
     F*       60      END OF FILE                                        *
     F*       61      DATABASE ERROR                                     *
     F*       62      READC INDICATOR ON SUBFILE                         *
     F*       70      CHAIN FAIL                                         *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
     F* SUBROUTINES                                                      *
     F*                                                                  *
     F*    BRCCOM - CHECK BRANCH AND COMPANY CODES                       *
     F*    CCYARR - SET UP CURRENCY ARRAYS                               *
     F*    CCYDEP - ACCESS CURRENCY DETAILS                              *
     F*    CMDSR  - PROCESS COMMAND KEYS                                 *
     F*    DKLIST - SET UP KEY LISTS                                     *
     F*    FILGSF - FILL GUARANTEES SUBFILE                              *
     F*    FILLSF - FILL SUBFILE                                         *
     F*    FIRSCR - PROCESS FIRST SCREEN                                 *
     F*    INIT   - INITIAL PROCESSING                                   *
     F*    PROFAC - PROCESS FACILITIES OPTION                            *
     F*    PROGIP - PROCESS GUARANTEES SCREEN INPUT                      *
     F*    PROINP - PROCESS SCREEN INPUT                                 *
     F*    PROSEL - PROCESS SELECTION                                    *
     F*    READGT - READ LIVE RECORD FROM GTREF                          *
     F*    READLE - READ LIVE RECORD FROM LEREF                          *
     F*    RETSR  - END OF PROGRAM PROCESSING                            *
     F*    ROLDWG - PROCESS ROLLDOWN FOR GUARANTEES                      *
     F*    ROLDWN - PROCESS ROLLDOWN                                     *
     F*    SECSCR - PROCESS SECOND SCREENS                               *
     F*    SELECG - DETERMINE IF GUARANTEES RECORD TO BE SELECTED        *
     F*    SELECT - DETERMINE IF RECORD TO BE SELECTED                   *
     F*    SFREC  - SET UP SUBFILE RECORD                                *
     F*    SFRECG - SET UP GUARANTEES SUBFILE RECORD                     *
     F*    VALIDD - VALIDATE CUSTOMER/BRANCH/COMPANY                     *
     F*    VALIDF - VALIDATE FIRST SCREEN                                *
     F*    VALIDM - VALIDATE MENU ENTRIES                                *
     F*    VALIDS - VALIDATE SECOND SCREEN                               *
     F*    *PSSR  - ERROR PROCESSING                                     *
     F*                                                                  *
     F*    GLINTC - CALCULATE INTEREST                                   *
     F*    ZCONV  - CONVERT AN AMOUNT FROM ONE CURRENCY TO ANOTHER       *
     F*    ZDATE2 - CONVERT DAY NUMBER TO DDMMMYY FORMAT                 *
     F*    ZRSTOR - RESTORE INDICATORS                                   *
     F*    ZSAVE  - SAVE BIT SETTINGS AND SET OFF INDICATORS             *
     F*    ZSEDIT - EDIT NUMERIC FIELD FOR OUTPUT                        *
     F*    ZXRATE - OBTAIN CROSS-EXCHANGE RATE AND MULTIPLY/DIVIDE       *
     F*             INDICATOR BETWEEN TWO CURRENCIES                     *
     F*                                                                  *
     F********************************************************************
     E                    XCYA      150  3               CURRENCY CODES
     E                    XDPA      150  1 0             CURRENCY DP
     E                    XSPA      150 13 8             SPOT RATES
     E                    XMDA      150  1               MULT/DIV INDICATOR
     E*
      /COPY ZSRSRC,ZSEDITZ1
      /COPY ZSRSRC,ZPOWERZ1
      /COPY ZSRSRC,ZDATE2Z1
     E**********          MES     1  25 79               MESSAGES                           MD036924
     E                    MES     1  26 79               MESSAGES                           MD036924
     E                    BRTTLA  1   2 40               BR TITLE (ALL)
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZDATE9Z1                                               066550
     E/COPY ZSRSRC,ZFREQBZ1                                                                   CLE028
     E/COPY ZSRSRC,ZHOLE                                                                      CLE028
     E/COPY WNCPYSRC,LE0201E001
     I*
     ILEREFD1F
     I* CUSTOMER/FACILITY/LOAN REFERENCE
     I              RECI                            RECIR
     I              CNUM                            CNUMR
     I/COPY WNCPYSRC,LE0201I002
     I*
     IGTREFGRF
     I* GUARANTORS REFERENCE
     I              RECI                            RECIG
     I*
     IGTEESGTF
     I* GUARANTEES
     I              CCY                             CCYG
     I*
     ICLOANCLF
     I* CUSTOMER LOANS
     I              BRCA                            BRCAX
     I*
     IFCLTYFMF
     I* FACILITIES
     I              BRCA                            BRCAX
     I*
     ILDA        UDS                            256
     I* LOCAL DATA AREA
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I/COPY QWINDSRC,LE0201GDTA
     I*
     IRUNDT       DS
     I* DATA AREA RUNDAT
     I                                        1   7 RUNA
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
     I*
     IPSDS       SDS
     I                                      244 253 WSID
     I                                      254 263 USER
     I*
     IZMUSER      DS                             17
     I* DATA AREA OF MENU USER
     I                                       11  13 DFTBR
     I                                       17  17 BNKAUT
     I*
     ISDGELR    E DSSDGELRPD
     I* GENERAL LEDGER DETAILS
     I*
     ISDBANK    E DSSDBANKPD
     I* BANK DETAILS
     I*
     ISDBRCH    E DSSDBRCHPD
     I*  EXTERNAL DS FOR BRANCH CODE DETAILS
     I*
     ISDCUST    E DSSDCUSTPD
     I              QQDFAC                          DFAC01                                    CGL029
     I* CUSTOMER DETAILS
     I*
     ISDCOMP    E DSSDCOMPPD
     I* COMPANY CODES
     I*
     ISCSARD    E DSSCSARDPD                                              CLE005
      ** Switchable Features File                                         CLE005
      *                                                                   CLE005
     IDSFDY     E DSDSFDY
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
     I*
     IDSSDY     E DSDSSDY
     I* SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE
     I*
     IEMSDS     E DSEMDTA
     I* LINK ENQUIRY DATA AREA
     I                                      141 146 CUSTNO
     I*
     I/COPY WNCPYSRC,LE0201I003
     IMNUOPT      DS                              6
     I/COPY WNCPYSRC,LE0201I004
     I* MENU OPTIONS DATA AREA
     I                                        1   1 SMNULN
     I                                        2   2 SMNUPP
     I                                        3   3 SMNUPS
     I                                        4   4 SMNUGB
     I                                        5   5 SMNUGI
     I                                        6   6 SMNUFC
     I/COPY WNCPYSRC,LE0201I005
     I*
     IPARMS       DS                             12
     I* PARAMETERS TO PASS TO LOWER LEVEL PROGRAM
     I                                        1   6 PCNUM
     I                                        7   9 PBRCD
     I                                       10  12 PCOMP
     I*
     I            DS                              6
     I* FACILITY/LOAN REFERENCE
     I                                        1   6 FCLN
     I                                        1   1 FLFLAG
     I                                        2   6 FLREF
     I*
     IGTEESD      DS                             15
     I* KEY FIELDS TO GTEES
     I                                        1   6 DSGUCN
     I                                        7  12 DSFCLN
     I                                       13  15 DSGUSN
     I*
     IFCLTYD      DS                             12
     I* KEY FIELDS TO FCLTY
     I                                        1   6 DSCNUM
     I                                        7   9 DSFACT
     I                                       10  11 DSFCNO
     I                                       12  12 DSRECT
      *                                                                   147830
      ** Max size for sfl display of values                               147830
      *                                                                   147830
     I              99999999              C         WMAX1                 147830
     I              99999999999           C         WMAX2                 147830
      *                                                                   147830
     I              'LECalcBasis6LeapYear'C         WPARML                                  MD035545
     I*
      /COPY ZSRSRC,ZSEDITZ2
     I/COPY ZSRSRC,ZINTDYZ1                                               066550
     I/COPY ZSRSRC,ZDATE9Z2                                               066550
     I/COPY ZSRSRC,ZDAT10Z1                                               126017
     I/COPY ZSRSRC,ZHOLI                                                                      CLE028
      /EJECT
     I/COPY WNCPYSRC,LE0201I001
     C********************************************************************
     C*                                                                  *
     C*    MAIN - MAIN PROCESSING                                        *
     C*                                                                  *
     C*    CALLS: INIT   - INITIAL PROCESSING                            *
     C*           FIRSCR - PROCESS FIRST SCREEN                          *
     C*           PROFAC - PROCESS FACILITIES OPTION                     *
     C*           SECSCR - PROCESS SECOND SCREENS                        *
     C*                                                                  *
     C********************************************************************
     C*
     C* FIRST CYCLE
     C*
     C           FIRSTC    IFNE 'Y'
     C                     EXSR INIT
     C                     END
     C*
     C* IF FIRST CYCLE OR F1 OR F12 TAKEN FROM SECOND SCREEN, PROCESS
     C* FIRST SCREEN
     C*
     C           FIRSTC    IFNE 'Y'
     C           *IN21     OREQ '1'
     C           *IN22     OREQ '1'
     C                     EXSR FIRSCR
     C                     END
     C*
     C                     MOVE 'Y'       FIRSTC  1
     C/COPY WNCPYSRC,LE0201C017
     C*
     C* IF FACILITIES OPTION TAKEN ON FIRST SCREEN, PROCESS CALL TO
     C* LE0161 - OUTSTANDING FACILITIES BY CUSTOMER ENQUIRY
     C*
     C           *IN56     IFEQ '1'
     C                     EXSR PROFAC
     C*
     C* OTHERWISE, PROCESS NEXT SCREEN WITHIN THIS PROGRAM
     C*
     C                     ELSE
     C                     EXSR SECSCR
     C                     END
     C/COPY WNCPYSRC,LE0201C018
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    INIT - INITIAL PROCESSING                                     *
     C*                                                                  *
     C*    CALLED BY: MAIN   - MAIN PROCESSING                           *
     C*    CALLS:     DKLIST - SET UP KEY LISTS                          *
     C*               CCYARR - SET UP CURRENCY ARRAYS                    *
     C*                                                                  *
     C********************************************************************
     C*
     C           INIT      BEGSR
      *                                                                   147830
     C           *LIKE     DEFN ZECODE    WECODE                          147830
      *                                                                   147830
     C           *LIKE     DEFN #CNUM     W#CNUM                          147830
     C*
     C                     MOVEACPY@      BIS@   80
     C/COPY WNCPYSRC,LE0201C006
     C*
     C* KEY LISTS
     C*
     C                     EXSR DKLIST
     C*
     C                     Z-ADD11        #COUNT  20
     C*
     C* ACCESS THE DATA AREAS RUNDAT & ZMUSER
     C*
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C           *NAMVAR   DEFN           ZMUSER
     C                     IN   RUNDT
     C                     IN   ZMUSER
     C           MBIN      IFEQ 'Y'
     C                     SETON                     20
     C                     END
     C*
     C* SETUP CURRENCY ARRAYS
     C*
     C                     EXSR CCYARR
     C*
     C* ACCESS GENERAL LEDGER DETAILS
     C*
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
     C*
     C* ACCESS BANK DETAILS
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD
     C                     PARM '*FIRST ' @OPTN
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C* CHECK SYSTEM DATE FORMAT
     C*
     C           DATF      COMP 'M'                      98
     C*
     C* SET UP EDIT CODE FOR ZSEDIT
     C*
     C                     MOVE 'J'       ZECODE
     C*
     C* ACCESS EM DATA AREA AND LDA
     C*
     C           *NAMVAR   DEFN           EMSDS
     C           *NAMVAR   DEFN           LDA
     C*
     C           *LOCK     IN   EMSDS
     C                     MOVE RQDTD     MODID   2
     C                     OUT  EMSDS
     C*
     C* INITIALIZE RETURN CODE AND FIRST RECORD READ FLAGS
     C*
     C                     MOVE *BLANK    RTNDWF
     C                     MOVE *BLANK    FIRSTG  1
     C                     MOVE *BLANK    FIRSTL  1
     C*
     C* IF PROGRAM HAS BEEN CALLED AS A LINKED ENQUIRY, SET UP CUSTOMER
     C* NUMBER AND BRANCH SCREEN FIELDS
     C*
     C           MODID     IFEQ 'LE'
     C                     MOVELCUSTNO    SCUST
     C                     MOVEL'ALL'     SBRCD
     C                     SETON                     06
     C                     END
     C/COPY WNCPYSRC,LE0201C019
      *                                                                   128954
      ** Check if CLE004 is switched *ON.                                 128954
      *                                                                   128954
     C                     MOVE 'N'       CLE004  1                       128954
     C                     CALL 'AOSARDR0'                                128954
     C                     PARM *BLANKS   @RTCD                           128954
     C                     PARM '*VERIFY' @OPTN                           128954
     C                     PARM 'CLE004'  @SARD                           128954
     C           SCSARD    PARM SCSARD    DSFDY                           128954
      *                                                                   128954
     C           @RTCD     IFEQ *BLANKS                                   128954
     C                     MOVE 'Y'       CLE004                          128954
     C                     END                                            128954
      *                                                                   128954
     C           @RTCD     IFNE *BLANKS                                   128954
     C           @RTCD     ANDNE'*NRF   '                                 128954
      *                                                                   128954
      ** Call to program ended in error                                   128954
      *                                                                   128954
     C                     MOVE *BLANKS   DBKEY                           128954
     C                     MOVEL'CLE004'  DBKEY            ************   128954
     C                     MOVE 'SCSARDPD'DBFILE           * DBERR 07 *   128954
     C                     Z-ADD007       DBASE            ************   128954
     C                     MOVEL'LE0201'  DBPGM                           128954
     C                     EXSR *PSSR                                     128954
     C                     ENDIF                                          128954
      *                                                                   CLE005
      ** Check if CLE005 is switched *ON.                                 CLE005
      *                                                                   CLE005
     C                     MOVE 'N'       CLE005  1                       CLE005
     C                     CALL 'AOSARDR0'                                CLE005
     C                     PARM *BLANKS   @RTCD   7                       CLE005
     C                     PARM '*VERIFY' @OPTN   7                       CLE005
     C                     PARM 'CLE005'  @SARD   6                       CLE005
     C           SCSARD    PARM SCSARD    DSFDY                           CLE005
      *                                                                   CLE005
     C           @RTCD     IFEQ *BLANKS                                   CLE005
     C                     MOVE 'Y'       CLE005                          CLE005
     C                     END                                            CLE005
      *                                                                   CLE005
     C           @RTCD     IFNE *BLANKS                                   CLE005
     C           @RTCD     ANDNE'*NRF   '                                 CLE005
      *                                                                   CLE005
      ** Call to program ended in error                                   CLE005
      *                                                                   CLE005
     C                     MOVE *BLANKS   DBKEY                           CLE005
     C                     MOVEL'CLE005'  DBKEY            ************   CLE005
     C                     MOVE 'SCSARDPD'DBFILE           * DBERR 06 *   CLE005
     C                     Z-ADD006       DBASE            ************   CLE005
     C                     MOVEL'LER032'  DBPGM                           CLE005
     C                     EXSR *PSSR                                     CLE005
     C                     ENDIF                                          CLE005
      *                                                                                       199124
      ** Check if CLE006 is switched *ON.                                                     199124
      *                                                                                       199124
     C                     CALL 'AOSARDR0'                                                    199124
     C                     PARM *BLANKS   @RTCD                                               199124
     C                     PARM '*VERIFY' @OPTN                                               199124
     C                     PARM 'CLE006'  @SARD                                               199124
     C           SCSARD    PARM SCSARD    DSFDY                                               199124
      *                                                                                       199124
     C                     MOVE '0'       *IN26                                               199124
      *                                                                                       199124
      ** Display 'Synd Loan' column if CLE006 is On.                                          199124
      *                                                                                       199124
     C           @RTCD     IFEQ *BLANKS                                                       199124
     C                     MOVE '1'       *IN26                                               199124
     C                     END                                                                199124
      *                                                                                       CLE023
      ** Access SAR details to see if CLE023 is installed                                     CLE023
      *                                                                                       CLE023
     C                     MOVE 'N'       CLE023  1                                           CLE023
     C                     MOVE *OFF      *IN57                                               CLE023
      *                                                                                       CLE023
     C                     CALL 'AOSARDR0'                                                    CLE023
     C                     PARM *BLANKS   @RTCD                                               CLE023
     C                     PARM '*VERIFY' @OPTN                                               CLE023
     C                     PARM 'CLE023'  @SARD                                               CLE023
     C           SCSARD    PARM SCSARD    DSFDY                                               CLE023
      *                                                                                       CLE023
     C           @RTCD     IFEQ *BLANKS                                                       CLE023
     C                     MOVE 'Y'       CLE023                                              CLE023
     C                     MOVE *ON       *IN57                                               CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C           @RTCD     IFNE *BLANKS                                                       CLE023
     C           @RTCD     ANDNE'*NRF   '                                                     CLE023
      *                                                                                       CLE023
      ** Call to program ended in error                                                       CLE023
      *                                                                                       CLE023
     C                     MOVE *BLANKS   DBKEY                                               CLE023
     C                     MOVEL'CLE023'  DBKEY                                               CLE023
     C                     MOVE 'SCSARDPD'DBFILE                                              CLE023
     C                     Z-ADD008       DBASE                                               CLE023
     C                     MOVEL'LE0201'  DBPGM                                               CLE023
     C                     EXSR *PSSR                                                         CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE028
      ** Determine if Flat Rate Personal Loans (Rule of 78s) is                               CLE028
      ** installed                                                                            CLE028
      *                                                                                       CLE028
     C                     CALL 'AOSARDR0'                                                    CLE028
     C                     PARM *BLANKS   PRTCD   7                                           CLE028
     C                     PARM '*VERIFY' POPTN   7                                           CLE028
     C                     PARM 'CLE028'  PSARD   6                                           CLE028
     C           SCSARD    PARM SCSARD    DSFDY                                               CLE028
      *                                                                                       CLE028
     C           PRTCD     IFNE *BLANKS                                                       CLE028
     C           PRTCD     ANDNE'*NRF   '                                                     CLE028
     C           *LOCK     IN   LDA                                                           CLE028
     C                     MOVEL'SCSARDPD'DBFILE                                              CLE028
     C                     Z-ADD8         DBASE                                               CLE028
     C                     MOVELPSARD     DBKEY                                               CLE028
     C                     OUT  LDA                                                           CLE028
     C                     EXSR *PSSR                                                         CLE028
     C                     ENDIF                                                              CLE028
      *                                                                                       CLE028
     C           PRTCD     IFEQ *BLANKS                                                       CLE028
     C                     MOVE 'Y'       CLE028  1                                           CLE028
     C                     ELSE                                                               CLE028
     C                     MOVE 'N'       CLE028                                              CLE028
     C                     ENDIF                                                              CLE028
      *                                                                                     MD035545
      ** Retrieve system value setting.                                                     MD035545
      *                                                                                     MD035545
     C                     MOVELWPARML    P@OP01                                            MD035545
     C                     CALL 'AOSVALR0'                                                  MD035545
     C                     PARM *BLANKS   @RTCD                                             MD035545
     C                     PARM           P@OP01 20                                         MD035545
     C                     PARM           P@VL01200                                         MD035545
     C                     PARM           P@OP02 20                                         MD035545
     C                     PARM           P@VL02200                                         MD035545
     C                     PARM           P@OP03 20                                         MD035545
     C                     PARM           P@VL03200                                         MD035545
     C                     PARM           P@OP04 20                                         MD035545
     C                     PARM           P@VL04200                                         MD035545
     C                     PARM           P@OP05 20                                         MD035545
     C                     PARM           P@VL05200                                         MD035545
     C                     PARM           P@OP06 20                                         MD035545
     C                     PARM           P@VL06200                                         MD035545
     C                     PARM           P@OP07 20                                         MD035545
     C                     PARM           P@VL07200                                         MD035545
     C                     PARM           P@OP08 20                                         MD035545
     C                     PARM           P@VL08200                                         MD035545
     C                     PARM           P@OP09 20                                         MD035545
     C                     PARM           P@VL09200                                         MD035545
     C                     PARM           P@OP10 20                                         MD035545
     C                     PARM           P@VL10200                                         MD035545
      *                                                                                     MD035545
      ** If system value is not found then default value to 'N'.                            MD035545
      *                                                                                     MD035545
     C           @RTCD     IFNE '       '                                                   MD035545
     C                     MOVE 'N'       WCALC6                                            MD035545
     C                     ELSE                                                             MD035545
     C                     MOVELP@VL01    WCALC6                                            MD035545
     C********** WCALC6    IFEQ 'Y'                                                MD035545 MD047993
     C********** BKALDI    ANDEQ'Y'                                                MD035545 MD047993
     C**********           MOVE 'Y'       WCALC6                                   MD035545 MD047993
     C**********           ELSE                                                    MD035545 MD047993
     C**********           MOVE 'N'       WCALC6                                   MD035545 MD047993
     C**********           ENDIF                                                   MD035545 MD047993
     C                     ENDIF                                                            MD035545
     C*
     C/COPY WNCPYSRC,LE0201C011
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    DKLIST - SET UP KEY LISTS                                     *
     C*                                                                  *
     C*    CALLED BY: INIT - INITIAL PROCESSING                          *
     C*    CALLS:                                                        *
     C*                                                                  *
     C********************************************************************
     C*
     C           DKLIST    BEGSR
     C*
     C* PARAMETER LIST FOR PROGRAM 'AOCUSTR0'
     C*
     C           PLIST1    PLIST
     C                     PARM '*BLANKS' @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM           CKEY   10
     C                     PARM           KEYSTS  7
     C           SDCUST    PARM SDCUST    DSSDY
     C*
     C* KEY LIST FOR FCLTY
     C*
     C           FCLTYK    KLIST
     C**********           KFLD           #CNUM   60                                          CSD027
     C                     KFLD           #CNUM   6                                           CSD027
     C                     KFLD           #FACT   30
     C                     KFLD           #FCNO   20
     C                     KFLD           RECTF   1
     C*
     C* KEY LISTS FOR LEREF
     C*
     C           LEREFK    KLIST
     C                     KFLD           #CNUM
     C                     KFLD           #FACT
     C                     KFLD           #FCNO
     C                     KFLD           #LNNO
     C*
     C           LEREFP    KLIST
     C                     KFLD           #CNUM
     C*
     C* KEY LISTS FOR GTREF
     C*
     C           GTREFK    KLIST
     C**********           KFLD           #GOCN   60                                          CSD027
     C**********           KFLD           #GUCN   60                                          CSD027
     C                     KFLD           #GOCN   6                                           CSD027
     C                     KFLD           #GUCN   6                                           CSD027
     C                     KFLD           #FCLN   6
     C                     KFLD           #GUSN   30
     C*
     C           GTREFP    KLIST
     C                     KFLD           #GOCN
     C*
     C* KEY LIST FOR CLOAN
     C*
     C           CLOANK    KLIST
     C                     KFLD           LNNO
     C                     KFLD           RECTL   1
     C                     MOVE 'A'       RECTL
     C*
     C* KEY LIST FOR GTEES
     C*
     C           GTEESK    KLIST
     C                     KFLD           GUCN
     C                     KFLD           FCLN
     C                     KFLD           GUSN
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    FIRSCR - PROCESS FIRST SCREEN                                 *
     C*                                                                  *
     C*    CALLED BY: MAIN   - MAIN PROCESSING                           *
     C*    CALLS:     VALIDF - VALIDATE FIRST SCREEN                     *
     C*                                                                  *
     C********************************************************************
     C*
     C           FIRSCR    BEGSR
     C*
     C* IF FIRST CYCLE AND PROGRAM CALLED AS LINKED ENQUIRY, ACCESS
     C* CUSTOMER DETAILS FOR CUSTOMER NAME AND TOWN
     C*
     C           FIRSTC    IFNE 'Y'
     C           MODID     ANDEQ'LE'
     C                     MOVELSCUST     CKEY
     C                     CALL 'AOCUSTR0'PLIST1
      ***********          MOVELCUSTNO    SCNUM
     C                     MOVELBBCUST    SCNUM
     C                     MOVELBBCRNM    SCRNM
     C                     MOVELBBCRTN    SCRTN
     C                     END
     C*
     C* IF FIRST SCREEN BEING REDISPLAYED AFTER RETURN FROM SECOND
     C* SCREEN, RESET SCREEN FIELDS
     C*
     C           *IN21     IFEQ '1'
     C           *IN22     OREQ '1'
     C                     MOVE *BLANKS   MNUOPT
     C                     MOVE *BLANKS   MSG
     C                     SETOF                     2122
     C*
     C* IF PROGRAM CALLED AS LINKED ENQUIRY, RESET CUSTOMER TO INITIAL
     C* CUSTOMER AND BRANCH TO 'ALL'
     C*
     C           MODID     IFEQ 'LE'
      ***********          MOVELCUSTNO    SCUST
     C                     MOVELSCNUM     SCUST
     C                     MOVEL'ALL'     SBRCD
     C                     MOVE *BLANKS   SCOMP
     C                     END
     C                     END
     C*
     C* DISPLAY FIRST SCREEN
     C*
     C                     EXFMTLE0201F0
     C*
     C* VALIDATE SCREEN INPUT
     C*
     C                     EXSR VALIDF
     C*
     C* DO WHILE INPUT INVALID
     C*
     C           *IN30     DOWEQ'1'
     C*
     C* WRITE FIRST SCREEN
     C*
     C                     EXFMTLE0201F0
     C*
     C* VALIDATE INPUT
     C*
     C                     EXSR VALIDF
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    SECSCR - PROCESS SECOND SCREENS                               *
     C*                                                                  *
     C*    CALLED BY: MAIN   - MAIN PROCESSING                           *
     C*    CALLS:     VALIDS - VALIDATE SECOND SCREEN                    *
     C*               FILGSF - FILL GUARANTEES SUBFILE                   *
     C*               FILLSF - FILL SUBFILE                              *
     C*               PROGIP - PROCESS GUARANTEES SCREEN INPUT           *
     C*               PROINP - PROCESS SCREEN INPUT                      *
     C*                                                                  *
     C********************************************************************
     C*
     C           SECSCR    BEGSR
     C*
     C* WRITE HEADINGS
     C*
     C   51                WRITELE0201F1
     C   52                WRITELE0201F2
     C   53                WRITELE0201F3
     C   54                WRITELE0201F4
     C   55                WRITELE0201F5
     C*
     C* CLEAR SUBFILE IN PREPARATION FOR NEW CUSTOMER/BRANCH ETC
     C*
     C                     MOVEA'001'     *IN,41
     C                     Z-ADD*ZERO     RRN
     C*
     C   51                WRITELE0201C1
     C   52                WRITELE0201C2
     C   53                WRITELE0201C3
     C   54                WRITELE0201C4
     C   55                WRITELE0201C5
     C*
     C                     MOVEA'011'     *IN,41
     C*
     C* IF PROCESSING GUARANTEES BY, RESET TOP OF THIS PAGE GUARANTEED
     C* CUSTOMER NUMBER, FACILITY/LOAN REFERENCE AND GUARANTEE SEQUENCE
     C* NUMBER
     C*
     C           *IN54     IFEQ '1'
     C**********           Z-ADD*ZERO     #GUCN                                               CSD027
     C                     MOVE *BLANKS   #GUCN                                               CSD027
     C                     MOVE *BLANKS   #FCLN
     C                     Z-ADD*ZERO     #GUSN
     C*
     C* OTHERWISE, PROCESSING LOANS, PARTICIPATIONS PURCHASED,
     C* PARTICIPATIONS SOLD OR GUARANTEES ISSUED, RESET TOP OF THIS
     C* PAGE FACILITY TYPE, FACILITY SEQUENCE AND LOAN NUMBER
     C*
     C                     ELSE
     C                     Z-ADD*ZERO     #FACT
     C                     Z-ADD*ZERO     #FCNO
     C**********           Z-ADD*ZERO     #LNNO   60                                          CLE148
     C                     MOVEL*BLANKS   #LNNO   6                                           CLE148
     C                     END
     C*
     C*
     C* VALIDATE SECOND SCREEN INPUT
     C*
     C                     EXSR VALIDS
     C*
     C* DO WHILE INPUT INVALID
     C*
     C           *IN30     DOWEQ'1'
     C*
     C* WRITE SUBFILE SCREEN
     C*
     C   51                EXFMTLE0201C1
     C   52                EXFMTLE0201C2
     C   53                EXFMTLE0201C3
     C   54                EXFMTLE0201C4
     C   55                EXFMTLE0201C5
     C*
     C* VALIDATE INPUT
     C*
     C                     EXSR VALIDS
     C*
     C                     END
     C*
     C* SAVE SCREEN INPUT FIELDS
     C*
     C                     MOVE SCUST     SSCUST 10
     C                     MOVE SBRCD     SSBRCD  3
     C                     MOVE SCOMP     SSCOMP  3
     C*
     C* WRITE HEADINGS
     C*
     C   51                WRITELE0201F1
     C   52                WRITELE0201F2
     C   53                WRITELE0201F3
     C   54                WRITELE0201F4
     C   55                WRITELE0201F5
     C*
     C* FILL NEXT PAGE OF SUBFILE
     C*
     C           *IN54     IFEQ '1'
     C                     EXSR FILGSF
     C                     ELSE
     C                     EXSR FILLSF
     C                     END
     C*
     C* DO WHILE F1, F3, F12 NOT TAKEN
     C*
     C           RTNDWF    DOWEQ*BLANK
     C*
     C* WRITE SUBFILE SCREEN
     C*
     C   51                EXFMTLE0201C1
     C   52                EXFMTLE0201C2
     C   53                EXFMTLE0201C3
     C   54                EXFMTLE0201C4
     C   55                EXFMTLE0201C5
     C*
     C/COPY WNCPYSRC,LE0201MOV1
     C                     SETOF                     30
     C*
     C* PROCESS SCREEN INPUT
     C*
     C           *IN54     IFEQ '1'
     C                     EXSR PROGIP
     C                     ELSE
     C                     EXSR PROINP
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    VALIDF - VALIDATE FIRST SCREEN                                *
     C*                                                                  *
     C*    CALLED BY: FIRSCR - PROCESS FIRST SCREEN                      *
     C*    CALLS:     CMDSR  - PROCESS COMMAND KEYS                      *
     C*               VALIDD - VALIDATE CUSTOMER/BRANCH/COMPANY          *
     C*               VALIDM - VALIDATE MENU ENTRIES                     *
     C*                                                                  *
     C********************************************************************
     C*
     C           VALIDF    BEGSR
     C*
     C* SETOF GENERAL ERROR INDICATOR
     C*
     C                     SETOF                     30
     C*
     C* BLANK MESSAGE FIELD
     C*
     C                     MOVE *BLANKS   MSG
     C*
     C* CHECK COMMAND KEYS
     C*
     C                     EXSR CMDSR
     C*
     C* VALIDATE CUSTOMER, BRANCH AND COMPANY
     C*
     C                     EXSR VALIDD
     C*
     C* IF CUSTOMER, BRANCH AND COMPANY VALID, VALIDATE MENU ENTRIES
     C*
     C           MSG       IFEQ *BLANKS
     C                     EXSR VALIDM
     C                     ELSE                                           072584
     C/COPY WNCPYSRC,LEH00915
     C                     MOVEA'000000'  *IN,31                          072584
     C/COPY WNCPYSRC,LE0201C020
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    VALIDS - VALIDATE SECOND SCREEN                               *
     C*                                                                  *
     C*    CALLED BY: SECSCR - PROCESS SECOND SCREENS                    *
     C*    CALLS:     CMDSR  - PROCESS COMMAND KEYS                      *
     C*               VALIDD - VALIDATE CUSTOMER/BRANCH/COMPANY          *
     C*                                                                  *
     C********************************************************************
     C*
     C           VALIDS    BEGSR
     C*
     C* SETOF GENERAL ERROR INDICATOR
     C*
     C                     SETOF                     30
     C*
     C* BLANK MESSAGE FIELD
     C*
     C                     MOVE *BLANKS   MSG
     C*
     C* CHECK COMMAND KEYS
     C*
     C                     EXSR CMDSR
     C*
     C* VALIDATE CUSTOMER, BRANCH AND COMPANY
     C*
     C                     EXSR VALIDD
     C/COPY WNCPYSRC,LEH00916
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    VALIDM - VALIDATE MENU ENTRIES                                *
     C*                                                                  *
     C*    CALLED BY: VALIDF - VALIDATE FIRST SCREEN                     *
     C*    CALLS:                                                        *
     C*                                                                  *
     C********************************************************************
     C*
     C           VALIDM    BEGSR
     C*
     C* SET OFF ERROR INDICATORS
     C*
     C                     SETOF                     313233
     C                     SETOF                     343536
     C*
     C* SET OFF MENU ENTRY INDICATORS
     C*
     C                     SETOF                     515253
     C                     SETOF                     545556
     C/COPY WNCPYSRC,LE0201C021
     C*
     C* INITIALIZE COUNT OF ENTRIES MADE
     C*
     C                     Z-ADD*ZERO     X
     C*
     C* CHECK THAT AT LEAST ONE ENTRY HAS BEEN MADE
     C*
     C           MNUOPT    IFEQ *BLANKS
     C                     MOVE MES,1     MSG
     C                     SETON                     30
     C*
     C* OTHERWISE, CHECK INDIVIDUAL MENU ENTRIES
     C*
     C                     ELSE
     C*
     C* VALIDATE LOANS MENU ENTRY:
     C*
     C* IF AN ENTRY HAS BEEN MADE, MUST BE 'Y' OR '1'
     C*
     C           SMNULN    IFNE *BLANK
     C           SMNULN    IFEQ 'Y'
     C           SMNULN    OREQ '1'
     C*
     C* ADD 1 TO COUNT OF ENTRIES MADE AND SET ON INDICATOR
     C*
     C                     ADD  1         X
     C                     SETON                       51
     C*
     C* OTHERWISE, INVALID ENTRY MADE, SET ON ERROR INDICATORS
     C*
     C                     ELSE
     C                     SETON                     3031
     C                     END
     C                     END
     C*
     C* VALIDATE PARTICIPATIONS PURCHASED MENU ENTRY:
     C*
     C* IF AN ENTRY HAS BEEN MADE, MUST BE 'Y' OR '1'
     C*
     C           SMNUPP    IFNE *BLANK
     C           SMNUPP    IFEQ 'Y'
     C           SMNUPP    OREQ '1'
     C*
     C* ADD 1 TO COUNT OF ENTRIES MADE AND SET ON INDICATOR
     C*
     C                     ADD  1         X
     C                     SETON                       52
     C*
     C* OTHERWISE, INVALID ENTRY MADE, SET ON ERROR INDICATORS
     C*
     C                     ELSE
     C                     SETON                     3032
     C                     END
     C                     END
     C*
     C* VALIDATE PARTICIPATIONS SOLD MENU ENTRY:
     C*
     C* IF AN ENTRY HAS BEEN MADE, MUST BE 'Y' OR '1'
     C*
     C           SMNUPS    IFNE *BLANK
     C           SMNUPS    IFEQ 'Y'
     C           SMNUPS    OREQ '1'
     C*
     C* ADD 1 TO COUNT OF ENTRIES MADE AND SET ON INDICATOR
     C*
     C                     ADD  1         X
     C                     SETON                       53
     C*
     C* OTHERWISE, INVALID ENTRY MADE, SET ON ERROR INDICATORS
     C*
     C                     ELSE
     C                     SETON                     3033
     C                     END
     C                     END
     C*
     C* VALIDATE GUARANTEES BY MENU ENTRY:
     C*
     C* IF AN ENTRY HAS BEEN MADE, MUST BE 'Y' OR '1'
     C*
     C           SMNUGB    IFNE *BLANK
     C           SMNUGB    IFEQ 'Y'
     C           SMNUGB    OREQ '1'
     C*
     C* ADD 1 TO COUNT OF ENTRIES MADE AND SET ON INDICATOR
     C*
     C                     ADD  1         X
     C                     SETON                       54
     C*
     C* OTHERWISE, INVALID ENTRY MADE, SET ON ERROR INDICATORS
     C*
     C                     ELSE
     C                     SETON                     3034
     C                     END
     C                     END
     C*
     C* VALIDATE GUARANTEES ISSUED MENU ENTRY:
     C*
     C* IF AN ENTRY HAS BEEN MADE, MUST BE 'Y' OR '1'
     C*
     C           SMNUGI    IFNE *BLANK
     C           SMNUGI    IFEQ 'Y'
     C           SMNUGI    OREQ '1'
     C*
     C* ADD 1 TO COUNT OF ENTRIES MADE AND SET ON INDICATOR
     C*
     C                     ADD  1         X
     C                     SETON                       55
     C*
     C* OTHERWISE, INVALID ENTRY MADE, SET ON ERROR INDICATORS
     C*
     C                     ELSE
     C                     SETON                     3035
     C                     END
     C                     END
     C*
     C* VALIDATE FACILITIES MENU ENTRY:
     C*
     C* IF AN ENTRY HAS BEEN MADE, MUST BE 'Y' OR '1'
     C*
     C           SMNUFC    IFNE *BLANK
     C           SMNUFC    IFEQ 'Y'
     C           SMNUFC    OREQ '1'
     C*
     C* ADD 1 TO COUNT OF ENTRIES MADE AND SET ON INDICATOR
     C*
     C                     ADD  1         X
     C                     SETON                       56
     C*
     C* OTHERWISE, INVALID ENTRY MADE, SET ON ERROR INDICATORS
     C*
     C                     ELSE
     C                     SETON                     3036
     C                     END
     C                     END
     C/COPY WNCPYSRC,LE0201C022
     C*
     C* IF ANY MENU ENTRIES ARE INVALID
     C*
     C           *IN30     IFEQ '1'
     C                     MOVE MES,2     MSG
     C*
     C* OTHERWISE, ALL MENU ENTRIES VALID
     C*
     C                     ELSE
     C*
     C* CHECK THAT ONE, AND ONLY ONE, ENTRY HAS BEEN MADE
     C*
     C           X         IFNE 1
     C                     MOVE MES,3     MSG
     C                     SETON                     30
     C                     SETON                     313233
     C                     SETON                     343536
     C/COPY WNCPYSRC,LE0201C023
     C                     END
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    VALIDD - VALIDATE CUSTOMER/BRANCH/COMPANY                     *
     C*                                                                  *
     C*    CALLED BY: VALIDF - VALIDATE FIRST SCREEN                     *
     C*               VALIDS - VALIDATE SECOND SCREEN                    *
     C*    CALLS:                                                        *
     C*                                                                  *
     C********************************************************************
     C*
     C           VALIDD    BEGSR
     C*
     C* SETOF OFF ERROR INDICATORS
     C*
     C                     SETOF                     373839
     C*
     C* VALIDATE CUSTOMER
     C*
     C* CUSTOMER NOT ENTERED
     C*
     C           SCUST     IFEQ *BLANKS
     C                     MOVE MES,4     MSG
     C                     SETON                       3039
     C                     ELSE
     C*
     C* CUSTOMER OR SHORTNAME ENTERED
     C*
     C                     MOVELSCUST     CKEY
     C                     CALL 'AOCUSTR0'PLIST1
     C*
     C* INVALID CUSTOMER
     C*
     C           @RTCD     IFNE *BLANKS
      *                                                                                     MD036924
      * If Customer is Closed                                                               MD036924
      *                                                                                     MD036924
     C           @RTCD     IFEQ '*CUSCLS'                                                   MD036924
     C                     MOVE MES,26    MSG                                               MD036924
     C                     ELSE                                                             MD036924
     C*
     C* IF CUSTOMER NUMBER ENTERED, DISPLAY 'CUSTOMER NUMBER NOT ON
     C* FILE' MESSAGE
     C*
     C           KEYSTS    IFEQ '*CNUM'
     C                     MOVE MES,5     MSG
     C*
     C* OTHERWISE, DISPLAY 'CUSTOMER SHORTNAME NOT ON FILE' MESSAGE
     C*
     C                     ELSE
     C                     MOVE MES,6     MSG
     C                     END
     C                     ENDIF                                                            MD036924
     C                     SETON                     3039
     C*
     C* OTHERWISE, VALID CUSTOMER ENTERED
     C*
     C                     ELSE
     C                     MOVE *BLANKS   SCUST
     C                     MOVELBBCUST    SCUST
     C                     MOVELBBCUST    #CNUM
     C                     MOVELBBCUST    #GOCN
     C                     MOVELBBCRNM    SCRNM                                               CSD027
     C                     MOVELBBCRTN    SCRTN                                               CSD027
     C                     END
     C                     END
     C*
     C* IF CUSTOMER VALID, VALIDATE BRANCH AND COMPANY
     C*
     C           MSG       IFEQ *BLANKS
     C*
     C* IF MULTIBRANCHING = Y, VALIDATE THAT BOTH BRANCH AND COMPANY
     C* NOT ENTERED
     C*
     C           MBIN      IFEQ 'Y'
     C*
     C           SBRCD     IFNE *BLANKS
     C           SCOMP     ANDNE*BLANKS
     C                     MOVE MES,7     MSG
     C                     SETON                     303738
     C                     END
     C*
     C* IF EVERYTHING STILL VALID
     C*
     C           MSG       IFEQ *BLANKS
     C*
     C* IF BOTH BRANCH AND COMPANY ARE LEFT BLANK, USE DEFAULT BRANCH
     C*
     C           SBRCD     IFEQ *BLANKS
     C           SCOMP     ANDEQ*BLANKS
     C                     MOVE DFTBR     SBRCD
     C                     END
     C*
     C* VALIDATE USER'S BANK LEVEL AUTHORITY
     C*
     C           SBRCD     IFEQ 'ALL'
     C           SCOMP     OREQ 'ALL'
     C*
     C           BNKAUT    IFNE 'Y'
     C                     MOVE MES,8     MSG
     C           SBRCD     IFEQ 'ALL'
     C                     SETON                     3037
     C                     ELSE
     C                     SETON                     3038
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C* IF EVERYTHING STILL VALID
     C*
     C           MSG       IFEQ *BLANKS
     C*
     C* VALIDATE BRANCH
     C*
     C           SBRCD     IFNE *BLANKS
     C           SBRCD     ANDNE'ALL'
     C*
     C* DETERMINE IF BRANCH CODE EXISTS ON SD
     C*
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*BLANKS' @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM SBRCD     @DSNB   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
     C           @RTCD     IFNE *BLANKS
     C                     MOVE MES,9     MSG
     C                     SETON                     3037
     C                     ELSE
     C                     MOVELA8BRCD    SBRCD
     C*
     C* CHECK USER'S ACCESS TO SELECTED BRANCH
     C*
     C***********          CALL 'ZVBBU'                                   147221
     C                     CALL 'ZVACTBU'                                 147221
     C                     PARM 'E'       ZACTN   1                       147221
     C                     PARM SBRCD     @ZBR    3
     C                     PARM           @ERR    10
     C           @ERR      IFNE *ZERO
     C           @ERR      IFEQ 1
     C                     MOVE MES,10    MSG
     C                     END
     C           @ERR      IFEQ 2
     C                     MOVE MES,11    MSG
     C                     END
     C                     SETON                     3037
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C* IF EVERYTHING STILL VALID
     C*
     C           MSG       IFEQ *BLANKS
     C*
     C* FORMAT SCREEN HEADER
     C*
     C           SBRCD     IFEQ 'ALL'
     C                     MOVEABRTTLA,1  BRTTL  40
     C                     ELSE
     C           SBRCD     IFNE *BLANKS
     C                     MOVE *BLANKS   BRTTL
     C                     MOVE *BLANKS   WRK9    9
     C                     MOVEL' Br -   'WRK9
     C                     MOVE SBRCD     WRK9
     C                     MOVELWRK9      BRTTL
     C                     MOVE A8BRNM    BRTTL
     C                     END
     C                     END
     C                     END
     C*
     C* IF BRANCH VALID, VALIDATE COMPANY
     C*
     C           MSG       IFEQ *BLANKS
     C*
     C           SCOMP     IFNE *BLANKS
     C           SCOMP     ANDNE'ALL'
     C*
     C* DETERMINE IF COMPANY CODE EXISTS ON SD
     C*
     C                     CALL 'AOCOMPR0'
     C                     PARM '*BLANKS' @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM SCOMP     @CENB   3
     C           SDCOMP    PARM SDCOMP    DSFDY
     C           @RTCD     IFNE *BLANKS
     C                     MOVE MES,12    MSG
     C                     SETON                     3038
     C                     ELSE
     C                     MOVE APCMCD    SCOMP
     C*
     C* CHECK USER'S ACCESS TO SELECTED COMPANY
     C*
     C                     CALL 'ZVCOU'
     C                     PARM SCOMP     @ZBR
     C                     PARM           @ERR
     C           @ERR      IFNE *ZERO
     C           @ERR      IFEQ 1
     C                     MOVE MES,13    MSG
     C                     END
     C           @ERR      IFEQ 2
     C                     MOVE MES,14    MSG
     C                     END
     C                     SETON                     3038
     C                     END
     C*
     C* IF EVERYTHING STILL VALID
     C*
     C           MSG       IFEQ *BLANKS
     C*
     C* FORMAT SCREEN HEADER
     C*
     C                     MOVE *BLANKS   BRTTL
     C                     MOVE *BLANKS   WRK9
     C                     MOVEL' Co -   'WRK9
     C                     MOVE SCOMP     WRK9
     C                     MOVELWRK9      BRTTL
     C                     MOVE APCMNM    BRTTL
     C                     END
     C                     END
     C*
     C* OTHERWISE, IF ALL COMPANIES REQUESTED, SET UP SCREEN HEADER FOR
     C* ALL COMPANIES
     C*
     C                     ELSE
     C           SCOMP     IFEQ 'ALL'
     C                     MOVEABRTTLA,2  BRTTL
     C                     END
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    PROGIP - PROCESS GUARANTEES SCREEN INPUT                      *
     C*                                                                  *
     C*    CALLED BY: SECSCR - PROCESS SECOND SCREENS                    *
     C*    CALLS:     CMDSR  - PROCESS COMMAND KEYS                      *
     C*               ROLDWG - PROCESS ROLLDOWN FOR GUARANTEES           *
     C*               FILGSF - FILL GUARANTEES SUBFILE                   *
     C*                                                                  *
     C********************************************************************
     C*
     C           PROGIP    BEGSR
     C*
     C* CHECK COMMAND KEYS
     C*
     C                     EXSR CMDSR
     C*
     C* CHECK FOR INPUT OF SCREEN FIELDS (CUSTOMER ETC)
     C*
     C           SCUST     IFNE SSCUST
     C           SBRCD     ORNE SSBRCD
     C           SCOMP     ORNE SSCOMP
     C                     MOVE 'R'       RTNDWF
     C                     MOVE *BLANK    FIRSTG
     C                     MOVE *BLANK    FIRSTL
     C                     END
     C*
     C* CHECK FOR ROLLDOWN
     C*
     C           *IN19     IFEQ '1'
     C                     EXSR ROLDWG
     C                     END
     C*
     C* CHECK FOR ROLLUP
     C*
     C           *IN18     IFEQ '1'
     C                     EXSR FILGSF
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    PROINP - PROCESS SCREEN INPUT                                 *
     C*                                                                  *
     C*    CALLED BY: SECSCR - PROCESS SECOND SCREENS                    *
     C*    CALLS:     CMDSR  - PROCESS COMMAND KEYS                      *
     C*               ROLDWN - PROCESS ROLLDOWN                          *
     C*               FILLSF - FILL SUBFILE                              *
     C*               PROSEL - PROCESS SELECTION                         *
     C*                                                                  *
     C********************************************************************
     C*
     C           PROINP    BEGSR
     C*
     C* CHECK COMMAND KEYS
     C*
     C                     EXSR CMDSR
     C*
     C* CHECK FOR INPUT OF SCREEN FIELDS (CUSTOMER ETC)
     C*
     C           SCUST     IFNE SSCUST
     C           SBRCD     ORNE SSBRCD
     C           SCOMP     ORNE SSCOMP
     C                     MOVE 'R'       RTNDWF
     C                     END
     C*
     C* CHECK FOR ROLLDOWN
     C*
     C           *IN19     IFEQ '1'
     C                     EXSR ROLDWN
     C*
     C* OTHERWISE, CHECK FOR ROLLUP
     C*
     C                     ELSE
     C           *IN18     IFEQ '1'
     C                     EXSR FILLSF
     C*
     C* OTHERWISE, IF RECORDS ARE DISPLAYED ON SUBFILE, CHECK IF ANY
     C* OPTIONS HAVE BEEN INPUT
     C*
     C                     ELSE
     C           *IN43     IFEQ '0'
     C                     MOVE *BLANK    RETND
     C           *IN62     DOUEQ'1'
     C           RETND     OREQ 'U'
     C           RETND     OREQ 'S'
     C*
     C   51                READCLE0201S1                 62
     C   52                READCLE0201S2                 62
     C   53                READCLE0201S3                 62
     C   55                READCLE0201S5                 62
     C*
     C           *IN62     IFEQ '0'
     C*
     C* IF OPTION ENTERED IS NOT '1' OR '2', DISPLAY ERROR MESSAGE
     C*
     C           OPTN      IFNE '1'
     C           OPTN      ANDNE'2'
     C                     MOVE *BLANK    OPTN
     C*
     C   51                UPDATLE0201S1
     C   52                UPDATLE0201S2
     C   53                UPDATLE0201S3
     C   55                UPDATLE0201S5
     C*
     C                     MOVE MES,25    MSG
     C                     SETON                     30
     C*
     C* OTHERWISE, PROCESS OPTION ENTERED
     C*
     C                     ELSE
     C                     EXSR PROSEL
     C                     MOVE *BLANK    OPTN
     C*
     C   51                UPDATLE0201S1
     C   52                UPDATLE0201S2
     C   53                UPDATLE0201S3
     C   55                UPDATLE0201S5
     C*
     C                     END
     C                     END
     C                     END
     C                     END
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    PROSEL - PROCESS SELECTION                                    *
     C*                                                                  *
     C*    CALLED BY: PROINP - PROCESS SCREEN INPUT                      *
     C*    CALLS:     LE0190 - LOAN DETAILS ENQUIRY                      *
     C*               LE0210 - LOAN ACTIVITY ENQUIRY                     *
     C*               RETSR  - END OF PROGRAM PROCESSING                 *
     C*                                                                  *
     C********************************************************************
     C*
     C           PROSEL    BEGSR
     C*
     C* ACCESS DATA AREA EMSDS AND SET UP LOAN NUMBER AS PARAMETER
     C* TO PASS TO LOWER LEVEL PROGRAMS
     C*
     C           *LOCK     IN   EMSDS
     C                     MOVE *BLANKS   RQDTD
     C                     MOVELLNNO      RQDTD
     C                     MOVE 'LE'      RQDTD
     C                     MOVE *BLANK    RETND
     C                     OUT  EMSDS
     C*
     C* IF OPTION ENTERED IS '1', CALL LE0190 - LOAN DETAILS ENQUIRY
     C*
     C                     OUT  LDA
     C           OPTN      IFEQ '1'
     C                     CALL 'LE0190'
     C*
     C* OTHERWISE, OPTION ENTERED IS '2', CALL LE0210 - LOAN ACTIVITY
     C* ENQUIRY
     C** But if CLE004 is installed, call LE0212 instead                  128954
     C*
     C                     ELSE
     C           CLE004    IFEQ 'Y'                                       128954
     C                     CALL 'LE0212'                                  128954
     C                     ELSE                                           128954
     C                     CALL 'LE0210'
     C                     ENDIF                                          128954
     C                     END
     C           *LOCK     IN   LDA
     C*
     C* CHECK DATA AREA EMSDS FOR COMMAND KEY TAKEN IN LOWER LEVEL
     C* PROGRAMS
     C*
     C           *LOCK     IN   EMSDS
     C*
     C* IF F1 TAKEN IN LOWER LEVEL PROGRAM (RETURN CODE = 'S') AND THIS
     C* PROGRAM CALLED AS A LINKED ENQUIRY (MODID = 'LE')
     C* OR F3 TAKEN IN LOWER LEVEL PROGRAM (RETURN CODE = 'Q')
     C* OR DATABASE ERROR HAS OCCURRED IN LOWER LEVEL PROGRAM (RETURN
     C* CODE = 'E')
     C*
     C           RETND     IFEQ 'S'
     C           MODID     ANDEQ'LE'
     C           RETND     OREQ 'Q'
     C           RETND     OREQ 'E'
     C*
     C* STORE RETURN CODE TO PASS TO HIGHER LEVEL PROGRAM AND EXIT THIS
     C* PROGRAM
     C*
     C                     MOVE RETND     RTNDWF
     C                     EXSR RETSR
     C                     END
     C*
     C* IF F1 TAKEN IN LOWER LEVEL PROGRAM (RETURN CODE = 'S') AND THIS
     C* PROGRAM CALLED FROM MENU (MODID NOT = 'LE')
     C*
     C           RETND     IFEQ 'S'
     C           MODID     ANDNE'LE'
     C*
     C* SET ON INDICATOR TO REDISPLAY FIRST SCREEN
     C*
     C                     MOVE RETND     RTNDWF
     C                     SETON                     21
     C                     END
     C*
     C                     OUT  EMSDS
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    PROFAC - PROCESS FACILITIES OPTION                            *
     C*                                                                  *
     C*    CALLED BY: MAIN   - MAIN PROCESSING                           *
     C*    CALLS:     LE0161 - OUTSTANDING FACILITIES BY CUSTOMER        *
     C*                        ENQUIRY                                   *
     C*               RETSR  - END OF PROGRAM PROCESSING                 *
     C*                                                                  *
     C********************************************************************
     C*
     C           PROFAC    BEGSR
     C*
     C* ACCESS DATA AREA EMSDS AND SET UP CUSTOMER NUMBER, BRANCH CODE
     C* AND COMPANY CODE AS PARAMETERS TO PASS TO LOWER LEVEL PROGRAM
     C*
     C           *LOCK     IN   EMSDS
     C*
     C                     MOVE *BLANKS   PARMS
     C                     MOVE *BLANKS   RQDTD
     C*
     C                     MOVELSCUST     PCNUM
     C                     MOVE SBRCD     PBRCD
     C                     MOVE SCOMP     PCOMP
     C                     MOVELPARMS     RQDTD
     C                     MOVE 'LE'      RQDTD
     C                     MOVE *BLANK    RETND
     C*
     C                     OUT  EMSDS
     C*
     C* CALL LE0161 - OUTSTANDING FACILITIES BY CUSTOMER ENQUIRY
     C*
     C                     OUT  LDA
     C                     CALL 'LE0161'
     C           *LOCK     IN   LDA
     C*
     C* CHECK DATA AREA EMSDS FOR COMMAND KEY TAKEN IN LOWER LEVEL
     C* PROGRAM
     C*
     C           *LOCK     IN   EMSDS
     C*
     C* IF F1 TAKEN IN LOWER LEVEL PROGRAM (RETURN CODE = 'S') AND THIS
     C* PROGRAM CALLED AS A LINKED ENQUIRY (MODID = 'LE')
     C* OR F3 TAKEN IN LOWER LEVEL PROGRAM (RETURN CODE = 'Q')
     C* OR DATABASE ERROR HAS OCCURRED IN LOWER LEVEL PROGRAM (RETURN
     C* CODE = 'E')
     C*
     C           RETND     IFEQ 'S'
     C           MODID     ANDEQ'LE'
     C           RETND     OREQ 'Q'
     C           RETND     OREQ 'E'
     C*
     C* STORE RETURN CODE TO PASS TO HIGHER LEVEL PROGRAM AND EXIT THIS
     C* PROGRAM
     C*
     C                     MOVE RETND     RTNDWF
     C                     EXSR RETSR
     C                     END
     C*
     C* IF F1 TAKEN IN LOWER LEVEL PROGRAM (RETURN CODE = 'S') AND THIS
     C* PROGRAM CALLED FROM MENU (MODID NOT = 'LE')
     C* OR IF F12 TAKEN IN LOWER LEVEL PROGRAM (RETURN CODE = 'U')
     C*
     C           RETND     IFEQ 'S'
     C           MODID     ANDNE'LE'
     C           RETND     OREQ 'U'
     C*
     C* SET ON INDICATOR TO REDISPLAY FIRST SCREEN
     C*
     C                     SETON                     21
     C                     END
     C*
     C                     OUT  EMSDS
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    FILGSF - FILL GUARANTEES SUBFILE                              *
     C*                                                                  *
     C*    CALLED BY: SECSCR - PROCESS SECOND SCREENS                    *
     C*               PROGIP - PROCESS GUARANTEES SCREEN INPUT           *
     C*               ROLDWG - PROCESS ROLLDOWN FOR GUARANTEES           *
     C*    CALLS:     READGT - READ LIVE RECORD FROM GTREF               *
     C*               SFRECG - SET UP GUARANTEES SUBFILE RECORD          *
     C*                                                                  *
     C********************************************************************
     C*
     C           FILGSF    BEGSR
     C*
     C* SETON ROLLUP/ROLLDOWN INDICATORS
     C*
     C                     SETON                     4546
     C*
     C* SET SUBFILE INDICATORS
     C*
     C                     MOVEA'001'     *IN,41
     C                     Z-ADD*ZERO     RRN
     C**********           Z-ADD*ZERO     $GUCN   60                                          CSD027
     C                     MOVE *BLANKS   $GUCN   6                                           CSD027
     C                     MOVE *BLANKS   $FCLN   6
     C                     Z-ADD*ZERO     $GUSN   30
     C*
     C* IF TOP OF THIS PAGE GUARANTEE SEQUENCE NUMBER IS EQUAL TO ZERO
     C* OR EQUAL TO THAT ON FIRST RECORD READ FROM GTREF, DISABLE
     C* ROLLDOWN
     C*
     C           #GUSN     IFEQ *ZERO
     C********** #GUSN     OREQ FGUSN                                                         199973
     C                     SETOF                     46
     C                     END
     C*
     C                     WRITELE0201C4
     C*
     C           GTREFK    SETLLGTREF
     C*
     C* READ FIRST LIVE RECORD FROM GTREF
     C*
     C                     EXSR READGT
     C*
     C* IF RECORD FOUND ON GTREF, STORE GUARANTEED CUSTOMER NUMBER,
     C* FACILTY/LOAN REFERENCE AND GUARANTEE SEQUENCE NUMBER IN TOP OF
     C* THIS PAGE FIELDS
     C*
     C           *IN60     IFEQ '0'
     C**********           Z-ADDGUCN      $GUCN            *TOP OF THIS PAGE                  CSD027
     C                     MOVE GUCN      $GUCN            *TOP OF THIS PAGE                  CSD027
     C                     MOVE *BLANKS   $FCLN            *TOP OF THIS PAGE
     C                     Z-ADDGUSN      $GUSN            *TOP OF THIS PAGE
     C*
     C* OTHERWISE, SET TOP OF THIS PAGE FIELDS TO ZERO/BLANK
     C*
     C                     ELSE
     C                     Z-ADD*ZERO     #GUSN
     C                     MOVE *BLANKS   #FCLN
     C                     Z-ADD*ZERO     #GUSN
     C                     END
     C*
     C           *IN60     DOWEQ'0'
     C           RRN       ANDNE#COUNT
     C*
     C* FORMAT SUBFILE RECORD
     C*
     C                     EXSR SFRECG
     C*
     C* OUTPUT RECORD TO SUBFILE
     C*
     C                     ADD  1         RRN            43*
     C                     WRITELE0201S4
     C*
     C* IF THIS IS FIRST RECORD READ FROM GTREF, STORE GUARANTEE
     C* SEQUENCE NUMBER OF THIS RECORD
     C*
     C           FIRSTG    IFNE 'Y'
     C           *IN60     ANDEQ'0'
     C                     Z-ADDGUSN      FGUSN   30
     C                     MOVE 'Y'       FIRSTG
     C                     END
     C*
     C* AT END OF PAGE READ NEXT LIVE RECORD FROM GTREF
     C*
     C           RRN       IFEQ #COUNT
     C                     EXSR READGT
     C*
     C* IF RECORD FOUND ON GTREF, STORE GUARANTEED CUSTOMER NUMBER,
     C* FACILTY/LOAN REFERENCE AND GUARANTEE SEQUENCE NUMBER IN TOP OF
     C* NEXS PAGE FIELDS
     C*
     C           *IN60     IFEQ '0'
     C**********           Z-ADDGUCN      #GUCN                                               CSD027
     C                     MOVE GUCN      #GUCN                                               CSD027
     C                     MOVE FCLN      #FCLN
     C                     Z-ADDGUSN      #GUSN            *TOP OF NEXT PAGE
     C*
     C* OTHERWISE, SET TOP OF NEXT PAGE FIELDS TO ZERO/BLANK
     C*
     C                     ELSE
     C**********           Z-ADD*ZERO     #GUCN                                               CSD027
     C                     MOVE *BLANKS   #GUCN                                               CSD027
     C                     MOVE *BLANKS   #FCLN
     C                     Z-ADD*ZERO     #GUSN            *TOP OF NEXT PAGE
     C                     END
     C*
     C* OTHERWISE, NOT END OF PAGE READ NEXT LIVE RECORD FROM GTREF
     C*
     C                     ELSE
     C                     EXSR READGT
     C***60*****           Z-ADD*ZERO     #GUCN                                               CSD027
     C   60                MOVE *BLANKS   #GUCN                                               CSD027
     C   60                MOVE *BLANKS   #FCLN
     C   60                Z-ADD*ZERO     #GUSN
     C                     END
     C                     END
     C*
     C* IF TOP OF NEXT PAGE GUARANTEE SEQUENCE NUMBER IS ZERO, DISABLE
     C* ROLLUP
     C*
     C           #GUSN     IFEQ *ZERO
     C                     SETOF                     45
     C                     END
     C*
     C* 43 ON - NO RECORDS ON SUBFILE
     C*
     C           *IN43     IFEQ '1'
     C                     MOVEA'01'      *IN,41
     C*
     C* IF MULTI-BRANCHING ON AND ALL BRANCHES REQUESTED
     C* OR MULTI-BRANCHING OFF
     C*
     C           MBIN      IFEQ 'Y'
     C           SBRCD     ANDEQ'ALL'
     C           MBIN      OREQ 'N'
     C*
     C* DISPLAY 'NO OUTSTANDING GUARANTEES GIVEN BY THIS CUSTOMER'
     C* MESSAGE
     C*
     C                     MOVE MES,15    MSG
     C                     END
     C*
     C* IF MULTI-BRANCHING ON AND INDIVIDUAL BRANCH REQUESTED
     C*
     C           MBIN      IFEQ 'Y'
     C           SBRCD     ANDNE'ALL'
     C*
     C* DISPLAY 'NO OUTSTANDING GUARANTEES GIVEN BY THIS CUSTOMER IN
     C* THE BRANCH' MESSAGE
     C*
     C                     MOVE MES,16    MSG
     C                     END
     C*
     C                     SETON                     30
     C                     ELSE
     C                     MOVEA'11'      *IN,41
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    FILLSF - FILL SUBFILE                                         *
     C*                                                                  *
     C*    CALLED BY: SECSCR - PROCESS SECOND SCREENS                    *
     C*               PROINP - PROCESS SCREEN INPUT                      *
     C*               ROLDWN - PROCESS ROLLDOWN                          *
     C*    CALLS:     READLE - READ LIVE RECORD FROM LEREF               *
     C*               SFREC  - SET UP SUBFILE RECORD                     *
     C*                                                                  *
     C********************************************************************
     C*
     C           FILLSF    BEGSR
     C*
     C* SETON ROLLUP/ROLLDOWN INDICATORS
     C*
     C                     SETON                     4546
     C*
     C* SET SUBFILE INDICATORS
     C*
     C                     MOVEA'001'     *IN,41
     C                     Z-ADD*ZERO     RRN
     C                     Z-ADD*ZERO     $FACT   30
     C                     Z-ADD*ZERO     $FCNO   20
     C**********           Z-ADD*ZERO     $LNNO   60                                          CLE148
     C                     MOVEL*BLANKS   $LNNO   6                                           CLE148
     C*
     C* IF TOP OF THIS PAGE LOAN NUMBER IS EQUAL TO ZERO OR EQUAL TO
     C* THAT ON FIRST RECORD READ FROM LEREF, DISABLE ROLLDOWN
     C*
     C********** #LNNO     IFEQ *ZERO                                                         CLE148
     C           #LNNO     IFEQ *BLANKS                                                       CLE148
     C           #LNNO     OREQ FLNNO
     C                     SETOF                     46
     C                     END
     C*
     C   51                WRITELE0201C1
     C   52                WRITELE0201C2
     C   53                WRITELE0201C3
     C   55                WRITELE0201C5
     C*
     C/COPY WNCPYSRC,LE0201C001
     C           LEREFK    SETLLLEREF
     C*
     C* READ FIRST LIVE RECORD FROM LEREF
     C*
     C                     EXSR READLE
     C*
     C* IF RECORD FOUND ON LEREF, STORE FACILITY TYPE, FACILITY
     C* SEQUENCE AND LOAN NUMBER IN TOP OF THIS PAGE FIELDS
     C*
     C           *IN60     IFEQ '0'
     C                     Z-ADDFACT      $FACT            *TOP OF THIS PAGE
     C                     Z-ADDFCNO      $FCNO            *TOP OF THIS PAGE
     C**********           Z-ADDLNNO      $LNNO            *TOP OF THIS PAGE                  CLE148
     C                     MOVELLNNO      $LNNO                                               CLE148
     C*
     C* OTHERWISE, SET TOP OF THIS PAGE FIELDS TO ZERO
     C*
     C                     ELSE
     C                     Z-ADD*ZERO     #FACT
     C                     Z-ADD*ZERO     #FCNO
     C**********           Z-ADD*ZERO     #LNNO                                               CLE148
     C                     MOVEL*BLANKS   #LNNO                                               CLE148
     C                     END
     C*
     C           *IN60     DOWEQ'0'
     C           RRN       ANDNE#COUNT
     C*
     C* FORMAT SUBFILE RECORD
     C*
     C                     EXSR SFREC
     C*
     C* OUTPUT RECORD TO SUBFILE
     C*
     C                     ADD  1         RRN     40     43*
     C*
     C   51                WRITELE0201S1
     C   52                WRITELE0201S2
     C   53                WRITELE0201S3
     C   55                WRITELE0201S5
     C*
     C* IF THIS IS FIRST RECORD READ FROM LEREF, STORE LOAN NUMBER OF
     C* THIS RECORD
     C*
     C           FIRSTL    IFNE 'Y'
     C           *IN60     ANDEQ'0'
     C**********           Z-ADDLNNO      FLNNO   60                                          CLE148
     C                     MOVELLNNO      FLNNO   6                                           CLE148
     C                     MOVE 'Y'       FIRSTL
     C                     END
     C*
     C* AT END OF PAGE READ NEXT LIVE RECORD FROM LEREF
     C*
     C           RRN       IFEQ #COUNT
     C                     EXSR READLE
     C*
     C* IF RECORD FOUND ON LEREF, STORE FACILITY TYPE, FACILITY
     C* SEQUENCE AND LOAN NUMBER IN TOP OF NEXT PAGE FIELDS
     C*
     C           *IN60     IFEQ '0'
     C                     Z-ADDFACT      #FACT
     C                     Z-ADDFCNO      #FCNO
     C**********           Z-ADDLNNO      #LNNO            *TOP OF NEXT PAGE                  CLE148
     C                     MOVELLNNO      #LNNO                                               CLE148
     C*
     C* OTHERWISE, SET TOP OF NEXT PAGE FIELDS TO ZERO
     C*
     C                     ELSE
     C                     Z-ADD*ZERO     #FACT
     C                     Z-ADD*ZERO     #FCNO
     C**********           Z-ADD*ZERO     #LNNO            *TOP OF NEXT PAGE                  CLE148
     C                     MOVEL*BLANKS   #LNNO                                               CLE148
     C                     END
     C*
     C* OTHERWISE, NOT END OF PAGE READ NEXT LIVE RECORD FROM LEREF
     C*
     C                     ELSE
     C                     EXSR READLE
     C   60                Z-ADD*ZERO     #FACT
     C   60                Z-ADD*ZERO     #FCNO
     C***60*****           Z-ADD*ZERO     #LNNO                                               CLE148
     C   60                MOVEL*BLANKS   #LNNO                                               CLE148
     C                     END
     C                     END
     C*
     C* IF TOP OF NEXT PAGE LOAN NUMBER IS ZERO, DISABLE ROLLUP
     C*
     C********** #LNNO     IFEQ *ZERO                                                         CLE148
     C           #LNNO     IFEQ *BLANKS                                                       CLE148
     C                     SETOF                     45
     C                     END
     C*
     C* 43 ON - NO RECORDS ON SUBFILE
     C*
     C           *IN43     IFEQ '1'
     C                     MOVEA'01'      *IN,41
     C*
     C* IF MULTI-BRANCHING ON AND ALL BRANCHES REQUESTED
     C* OR MULTI-BRANCHING OFF
     C*
     C           MBIN      IFEQ 'Y'
     C           SBRCD     ANDEQ'ALL'
     C           MBIN      OREQ 'N'
     C*
     C* DISPLAY 'NO LOANS/PARTICIPATIONS PURCHASED/PARTICIPATIONS SOLD/
     C* GUARANTEES ISSUED FOR THIS CUSTOMER' MESSAGE
     C*
     C   51                MOVE MES,17    MSG
     C   52                MOVE MES,18    MSG
     C   53                MOVE MES,19    MSG
     C   55                MOVE MES,20    MSG
     C                     END
     C*
     C* IF MULTI-BRANCHING ON AND INDIVIDUAL BRANCH REQUESTED
     C*
     C           MBIN      IFEQ 'Y'
     C           SBRCD     ANDNE'ALL'
     C*
     C* DISPLAY 'NO LOANS/PARTICIPATIONS PURCHASED/PARTICIPATIONS SOLD/
     C* GUARANTEES ISSUED FOR CUSTOMER IN THE BRANCH' MESSAGE
     C*
     C   51                MOVE MES,21    MSG
     C   52                MOVE MES,22    MSG
     C   53                MOVE MES,23    MSG
     C   55                MOVE MES,24    MSG
     C                     END
     C*
     C                     SETON                     30
     C                     ELSE
     C                     MOVEA'11'      *IN,41
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    READGT - READ LIVE RECORD FROM GTREF                          *
     C*                                                                  *
     C*    CALLED BY: FILGSF - FILL GUARANTEES SUBFILE                   *
     C*    CALLS:     SELECG - DETERMINE IF GUARANTEES RECORD TO BE      *
     C*                        SELECTED                                  *
     C*                                                                  *
     C********************************************************************
     C*
     C           READGT    BEGSR
     C*
     C* READ A LIVE RECORD FROM GTREF
     C*
     C                     MOVE '0'       *IN60
     C                     MOVE 'N'       SELCT   1
     C*
     C* ONLY PROCESS RECORDS FROM GTREF WITH RECORD ID EQUAL TO 'D'
     C*
     C           *IN60     DOWEQ'0'
     C           SELCT     ANDEQ'N'
     C           GTREFP    READEGTREF                    60*
     C           *IN60     IFEQ '0'
     C                     EXSR SELECG
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    READLE - READ LIVE RECORD FROM LEREF                          *
     C*                                                                  *
     C*    CALLED BY: FILLSF - FILL SUBFILE                              *
     C*    CALLS:     SELECT - DETERMINE IF RECORD TO BE SELECTED        *
     C*                                                                  *
     C********************************************************************
     C*
     C           READLE    BEGSR
     C*
     C* READ A LIVE RECORD FROM LEREF
     C*
     C                     MOVE '0'       *IN60
     C                     MOVE 'N'       SELCT
     C*
     C/COPY WNCPYSRC,LE0201C002
     C           *IN60     DOWEQ'0'
     C           SELCT     ANDEQ'N'
     C           LEREFP    READELEREF                    60*
     C           *IN60     IFEQ '0'
     C                     EXSR SELECT
     C                     END
     C                     END
     C/COPY WNCPYSRC,LE0201C003
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    SFRECG - SET UP GUARANTEES SUBFILE RECORD                     *
     C*                                                                  *
     C*    CALLED BY: FILGSF - FILL GUARANTEES SUBFILE                   *
     C*    CALLS:     CCYDEP - ACCESS CURRENCY DETAILS                   *
     C*               ZSEDIT - EDIT NUMERIC FIELD FOR OUTPUT             *
     C*               ZXRATE - OBTAIN CROSS-EXCHANGE RATE AND MULTIPLY/  *
     C*                        DIVIDE INDICATOR BETWEEN TWO CURRENCIES   *
     C*               ZCONV  - CONVERT AMOUNT FROM ONE CURRENCY TO       *
     C*                        ANOTHER                                   *
     C*                                                                  *
     C********************************************************************
     C*
     C           SFRECG    BEGSR
     C*
     C* ACCESS CURRENCY ARRAYS TO PICK UP DECIMAL PLACES, SPOT RATE AND
     C* MULTIPLY/DIVIDE INDICATOR FOR GUARANTEE CURRENCY
     C*
     C                     MOVE CCYG      XCCY
     C                     EXSR CCYDEP
     C*
     C* INITIALIZE SCREEN AMOUNT FIELDS
     C*
     C                     MOVE *BLANKS   STAMT
     C                     MOVE *BLANKS   SOSAMT
     C*
     C* EDIT TOTAL AMOUNT FOR OUTPUT TO SCREEN
     C*
     C                     Z-ADD*ZERO     ZFLD15
     C                     MOVE TAMT      ZFLD15
     C                     Z-ADDXDECP     ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    STAMT
     C*
     C* IF FACILITY/LOAN RECORD FOUND FLAG NOT BLANK (IE. FACILITY OR
     C* LOAN RECORD WAS FOUND)
     C*
     C           FCLNRF    IFNE *BLANK
     C*
     C* CALCULATE CROSS-EXCHANGE RATE AND MULTIPLY/DIVIDE INDICATOR
     C* BETWEEN GUARANTEE CURRENCY AND FACILITY/LOAN CURRENCY
     C*
     C* SET UP GUARANTEE CURRENCY FIELDS
     C*
     C                     Z-ADDXSPOT     ZRATE2 138
     C                     MOVE XMDIN     ZMDI2   1
     C                     MOVE CCYG      ZCCYO
     C                     Z-ADDXDECP     ZCDPO
     C*
     C* ACCESS CURRENCY ARRAYS FOR FACILITY/LOAN CURRENCY DETAILS
     C*
     C           FCLNRF    IFEQ 'F'
     C                     MOVE FCCY      XCCY
     C                     ELSE
     C                     MOVE CCY       XCCY
     C                     END
     C                     EXSR CCYDEP
     C*
     C* SET UP FACILITY/LOAN CURRENCY FIELDS
     C*
     C                     Z-ADDXSPOT     ZRATE1 138
     C                     MOVE XMDIN     ZMDI1   1
     C                     MOVE XCCY      ZCCYI
     C                     Z-ADDXDECP     ZCDPI
     C*
     C* IF BOTH MULTIPLY/DIVIDE INDICATORS EQUAL TO 'D' AND 'FROM'
     C* CURRENCY IS BASE CURRENCY (IE. ITS SPOT RATE IS 1), CHANGE
     C* 'FROM' MULTIPLY/DIVIDE INDICATOR TO 'M' TO AVOID UNNECESSARY
     C* CALCULATION OF RECIPROCAL OF 'TO' SPOT RATE AND HENCE
     C* TRUNCATION OF CROSS EXCHANGE RATE LEADING TO ROUNDING ERRORS
     C* (IE. IF ZMDI1 = ZMDI2, ZXRATE CALCULATES CROSS EXCHANGE RATE,
     C* ZRATEX, AS ZRATE1/ZRATE2 WHICH = RECIPROCAL OF ZRATE2 IF ZRATE1
     C* IS 1;
     C* IF ZMDI1 = 'D', ZCONV CALCULATES 'TO' AMOUNT AS ZAMTCI/ZRATEX
     C* WHICH = ZAMTCI/(1/ZRATE2) = ZAMTCI X ZRATE2 IF ZRATE1 IS 1)
     C*
     C           ZMDI1     IFEQ 'D'
     C           ZMDI2     ANDEQ'D'
     C           ZRATE1    ANDEQ1
     C                     MOVE 'M'       ZMDI1
     C                     END
     C                     EXSR ZXRATE
     C*
     C* CONVERT FACILITY/LOAN AMOUNT TO GUARANTEE CURRENCY
     C*
     C                     Z-ADDZRATEX    ZEXCH
     C                     MOVE ZMDIX     ZMD
     C                     Z-ADDOSAMT     ZAMTCI
     C                     EXSR ZCONV
     C                     Z-ADDZAMTCO    OSAMT
     C                     END
     C*
     C* EDIT OUTSTANDING AMOUNT FOR OUTPUT TO SCREEN
     C*
     C                     Z-ADD*ZERO     ZFLD15
     C                     MOVE OSAMT     ZFLD15
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    SOSAMT
     C*
     C* FORMAT LOAN/FACILITY GUARANTEED FOR OUTPUT TO SCREEN
     C*
     C********** FLFLAG    IFEQ 'F'                                                           CLE148
     C           FLFLAG    IFEQ *BLANK                                                        CLE148
     C                     MOVELFLREF     SFCLN
     C                     MOVE FLREF     FACRF
     C                     MOVEL'/'       FACRF   3
     C                     MOVE FACRF     SFCLN
     C                     ELSE
     C                     MOVE FCLN      SFCLN
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    SFREC - SET UP SUBFILE RECORD                                 *
     C*                                                                  *
     C*    CALLED BY: FILLSF - FILL SUBFILE                              *
     C*    CALLS:     CCYDEP - ACCESS CURRENCY DETAILS                   *
     C*               ZSEDIT - EDIT NUMERIC FIELD FOR OUTPUT             *
     C*               GLINTC - CALCULATE INTEREST                        *
     C*               ZDATE2 - CONVERT DAY NUMBER TO DDMMMYY FORMAT      *
     C*                                                                  *
     C********************************************************************
     C*
     C           SFREC     BEGSR
     C*
     C/COPY WNCPYSRC,LE0201C012
     C* FORMAT LOAN TYPE/SUB TYPE FOR OUTPUT TO LOANS AND GUARANTEES
     C* ISSUED SCREENS
     C*
     C           *IN51     IFEQ '1'
     C           *IN55     OREQ '1'
     C                     MOVELLTYP      SLTST
     C**********           MOVEL'/'       SUBA    3                                           CLE042
     C**********           MOVE SUTP      SUBA                                                CLE042
     C                     MOVEL'/'       SUBA    7                                           CLE042
     C                     MOVELSUTP      WKSTCL  6                                           CLE042
     C                     MOVE CLAS      WKSTCL                                              CLE042
     C                     MOVE WKSTCL    SUBA                                                CLE042
     C                     MOVE SUBA      SLTST
     C                     END
     C*
     C* ACCESS CURRENCY ARRAYS TO PICK UP DECIMAL PLACES.
     C*
     C                     MOVE CCY       XCCY    3
     C                     EXSR CCYDEP
      *                                                                   147830
      * Get Syndicated loan narrative for sfl display                     147830
      *                                                                   147830
     C                     CLEARSLNAR                                     147830
      *                                                                   147830
     C                     SELEC                                          147830
      *                                                                   147830
     C           LPFI      WHEQ 'I'                                       147830
     C**********           MOVE 'Intnl'   SLNAR                                        147830 CLE042
     C                     MOVEL'Intl'    SLNAR                                               CLE042
      *                                                                   147830
     C           LPFI      WHEQ 'P'                                       147830
     C**********           MOVE 'Part.'   SLNAR                                        147830 CLE042
     C                     MOVEL'Part'    SLNAR                                               CLE042
      *                                                                   147830
     C           LPFI      WHEQ 'R'                                       147830
     C**********           MOVE 'Risk '   SLNAR                                        147830 CLE042
     C                     MOVEL'Risk'    SLNAR                                               CLE042
      *                                                                   147830
     C           LPFI      WHEQ 'S'                                                           CLE106
     C**********           MOVE 'SubPt'   SLNAR                                        CLE106 CLE042
     C                     MOVEL'SubP'    SLNAR                                               CLE042
      *                                                                                       CLE106
     C                     OTHER                                          147830
      *                                                                   147830
      ** First save customer number                                       147830
      *                                                                   147830
     C**********           Z-ADD#CNUM     W#CNUM                                       147830 CSD027
     C                     MOVE #CNUM     W#CNUM                                              CSD027
      *                                                                   147830
     C**********           Z-ADDFCUS      #CNUM                                        147830 CSD027
     C**********           Z-ADDFCUS      #CNUM                                        147830 CSD027
     C                     MOVE FCUS      #CNUM                                               CSD027
     C                     Z-ADDFTYP      #FACT                           147830
     C                     Z-ADDFSEQ      #FCNO                           147830
     C                     MOVE 'A'       RECTF                           147830
     C           FCLTYK    CHAINFCLTY                70                   147830
     C           *IN70     IFEQ *OFF                                      147830
     C           SYIN      ANDEQ'Y'                                       147830
     C***********          MOVE 'Prime'   SLNAR                    147830 155147
     C                     MOVE 'Prmry'   SLNAR                           155147
     C**********           MOVE 'Prmry'   SLNAR                                        155147 CLE042
     C                     MOVEL'Prmy'    SLNAR                                               CLE042
     C                     ENDIF                                          147830
      *                                                                   147830
      ** Restore customer number                                          147830
      *                                                                   147830
     C**********           Z-ADDW#CNUM    #CNUM                                        147830 CSD027
     C                     MOVE W#CNUM    #CNUM                                               CSD027
      *                                                                   147830
     C                     ENDSL                                          147830
      *                                                                   147830
     C*
     C* INITIALIZE SCREEN AMOUNT FIELDS
     C*
     C                     MOVE *BLANKS   SCPAM
     C                     MOVE *BLANKS   SDINT
     C                     MOVE *BLANKS   SAINT
     C*
     C* EDIT CURRENT PRINCIPAL FOR OUTPUT TO SCREEN
     C*
      *                                                                   147830
      ** If current principal is greater than size allowed on sfl         147830
      ** display then show as ***************.                            147830
      *                                                                   147830
     C           CPAM      IFGT WMAX2                                     147830
     C                     MOVE *ON       *IN23                           147830
     C                     ELSE                                           147830
     C                     MOVE *OFF      *IN23                           147830
     C                     ENDIF                                          147830
      *                                                                   147830
     C           *IN23     IFEQ *OFF                                      147830
      *                                                                   147830
     C                     Z-ADD*ZERO     ZFLD15
     C                     MOVE CPAM      ZFLD15
     C                     Z-ADDXDECP     ZDECS
      *                                                                                       CLE028
      ** For flat rate loans, if the loan hasn't started or just input                        CLE028
      ** today, add interest and/or charges if these were added on                            CLE028
      *                                                                                       CLE028
     C           CLE028    IFEQ 'Y'                                                           CLE028
     C           PTYP      ANDEQ80                                                            CLE028
     C           VDAT      IFGE BJRDNB                                                        CLE028
     C           ORED      OREQ BJRDNB                                                        CLE028
     C           ADIF      IFEQ 'Y'                                                           CLE028
     C                     ADD  FLTI      ZFLD15                                              CLE028
     C                     ENDIF                                                              CLE028
     C           ADCF      IFEQ 'Y'                                                           CLE028
     C                     ADD  CHGA      ZFLD15                                              CLE028
     C                     ENDIF                                                              CLE028
     C                     ENDIF                                                              CLE028
     C                     ENDIF                                                              CLE028
      *                                                                                       CLE028
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    SCPAM
      *                                                                   147830
     C                     ENDIF                                          147830
     C*
     C* CALCULATE ACCRUED INTEREST:
     C* IF STOP ACCRUALS INDICATOR IS OFF
     C* AND VALUE DATE IS BEFORE OR EQUAL TO TODAY IE. LOAN HAS STARTED
     C*
     C                     TESTB'3'       LONI           28
     C           *IN28     IFEQ '0'
     C           VDAT      ANDLERUND
     C*
     C* IF LOAN HAS GONE PAST DUE, CALCULATE ACCRUED INTEREST AS SUM OF
     C* ACCRUED INTEREST ADJUSTMENT POSTED, ACCRUED INTEREST MANUAL
     C* ADJUSTMENT POSTED AND PAST DUE INTEREST AMOUNT
     C*
     C           RECI      IFEQ 'D'
     C           MDAT      ANDLTIACD
     C           MDAT      ANDNE*ZERO
     C           AIAP      ADD  AIMN      AINT   130
     C                     ADD  PDIN      AINT
     C*
     C* OTHERWISE, CALCULATE ACCRUED INTEREST AS SUM OF
     C* ACCRUED INTEREST ADJUSTMENT POSTED, ACCRUED INTEREST MANUAL
     C* ADJUSTMENT POSTED AND ACCRUED INTEREST TO CONTROL DATE
     C*
     C                     ELSE
     C           AIAP      ADD  AIMN      AINT
     C                     ADD  AITC      AINT
     C*
     C* SET STARTING DATE EQUAL TO INTEREST ACCRUAL CONTROL DATE
     C*
     C                     MOVE IACD      ZIBEG
     C*
     C* DETERMINE END DATE:
     C* IF MATURITY DATE EQUAL TO ZERO OR MATURITY DATE IS AFTER TODAY,
     C* SET END DATE EQUAL TO RUN DATE
     C*
     C           MDAT      IFEQ *ZERO
     C           RUND      ORLT MDAT
     C                     MOVE RUND      ZIEND
     C*
     C* OTHERWISE, SET END DATE EQUAL TO MATURITY DATE
     C*
     C                     ELSE
     C                     MOVE MDAT      ZIEND
     C                     END
     C*
     C**CHECK*IF*FIRST*DAY*ACCRUALS:                                      S01392
     C**IF*ACCRUALS LAST*DAY*INDCIATOR*IS*NOT*'Y'                         S01392
     C***********                                                         S01392
     C***********BKALDI    IFNE 'Y'                                       S01392
      *                                                                   S01392
      * adjust end date according to 'additional day indicator'           S01392
     C                     Z-ADDMDAT      ZMDAT                           S01392
     C***********          EXSR ZFLDC                              S01392 143510
     C**********           Z-ADDVDAT      ZVDAT                                        S01392 245082
     C                     Z-ADDMDAT      ZMDAT                                               245082
     C                     EXSR ZFLAC                                     S01392
      *                                                                   S01392
     C*****                                                               S01392
     C**IF*MATURITY DATE EQUAL TO ZERO IE. CALL LOAN OR MATURITY DATE     S01392
     C**IS*AFTER TODAY, ADD 1 TO END DATE IE. ADD EXTRA DAY'S INTEREST    S01392
     C***********                                                         S01392
     C***********MDAT      IFEQ *ZERO                                     S01392
     C***********RUND      ORLT MDAT                                      S01392
     C***********          ADD  1         ZIEND                           S01392
     C***********          END                                            S01392
     C***********          END                                            S01392
     C*
     C* IF CURRENT PRINCIPAL IS POSITIVE, EXECUTE GLINTC TO CALCULATE
     C* INTEREST
     C*
     C           CPAM      IFGT *ZERO
     C*
     C* CALCULATION BASIS:
     C*
     C                     MOVE ICBS      ZICALC
     C*
     C* INTEREST RATE:
     C*
     C                     Z-ADDINTR      ZIRATE
     C*
     C* PRINCIPAL:
     C*
     C                     Z-ADDCPAM      ZIAMT
     C*
     C/COPY WNCPYSRC,LE0201C013
     C                     EXSR GLINTC
     C/COPY WNCPYSRC,LE0201C014
     C*
     C** ROUND DOWN INTEREST IF REQUIRED.                                 S01392
     C*                                                                   S01392
     C           RDFC      IFEQ 'Y'                                       S01392
     C***********          Z-ADDZINTR     AINT                     S01392 054104
     C                     ADD  ZINTR     AINT                            054104
     C*********************ADD  ZINTR     AINT      H                     S01392
     C                     ELSE                                           S01392
     C***********          Z-ADDZINTR     AINT      H              S01392 054104
     C                     ADD  ZINTR     AINT      H                     054104
     C                     END                                            S01392
     C*
     C                     END
     C*
     C* IF PROCESSING A LOAN, A PARTICIPATION PURCHASED OR A
     C* PARTICIPATION SOLD, CALCULATE DAILY INTEREST:
     C*
     C           *IN51     IFEQ '1'
     C           *IN52     OREQ '1'
     C           *IN53     OREQ '1'
     C*
     C* IF CURRENT PRINCIPAL IS POSITIVE, EXECUTE GLINTC TO CALCULATE
     C* INTEREST
     C*
     C           CPAM      IFGT *ZERO
     C           IACD      ADD  1         ZIEND
     C*
     C/COPY WNCPYSRC,LE0201C015
     C                     EXSR GLINTC
     C/COPY WNCPYSRC,LE0201C016
     C*                                                                   S01392
     C** ROUND DOWN INTEREST IF REQUIRED.                                 S01392
     C*                                                                   S01392
     C           RDFC      IFEQ 'Y'                                       S01392
     C                     Z-ADDZINTR     DINT                            S01392
     C                     ELSE                                           S01392
     C                     Z-ADDZINTR     DINT   120H
     C                     END                                            S01392
     C*
     C* OTHERWISE, INTEREST IS ZERO
     C*
     C                     ELSE
     C                     Z-ADD*ZERO     DINT
     C                     END
     C                     END
     C                     END
      *                                                                                       CLE028
      ** For flat rate personal loans, execute RULE78 to compute for                          CLE028
      ** outstanding interest                                                                 CLE028
      *                                                                                       CLE028
     C           CLE028    IFEQ 'Y'                                                           CLE028
     C           PTYP      ANDEQ80                                                            CLE028
      *                                                                                       CLE028
     C                     EXSR SROSIN                                                        CLE028
     C                     Z-ADDZINTR     AINT      H                                         CLE028
     C                     Z-ADD*ZERO     DINT                                                CLE028
     C                     ENDIF                                                              CLE028
     C*
     C* OTHERWISE, NOT ACCRUING INTEREST, SET ACCRUED INTEREST TO SUM
     C* OF ACCRUED INTEREST ADJUSTMENT POSTED, ACCRUED INTEREST MANUAL
     C* ADJUSTMENT POSTED AND ACCRUED INTEREST TO CONTROL DATE AND SET
     C* DAILY INTEREST TO ZERO
     C*
     C                     ELSE
     C           AIAP      ADD  AIMN      AINT
     C                     ADD  AITC      AINT
     C                     Z-ADD*ZERO     DINT
     C                     END
     C*
     C* CALCULATE ACCRUED INTEREST TO DATE:
     C* SUBTRACT INTEREST PAID/RECEIVED TO DATE
     C*
     C           CLE028    IFEQ 'N'                                                           CLE028
     C           CLE028    OREQ 'Y'                                                           CLE028
     C           PTYP      ANDNE80                                                            CLE028
     C           CLE028    OREQ 'Y'                                                           CLE028
     C           PTYP      ANDEQ80                                                            CLE028
     C           ADIF      ANDNE'Y'                                                           CLE028
     C                     SUB  IPRD      AINT
     C                     ENDIF                                                              CLE028
     C*
     C* SUBTRACT INTEREST CAPITALIZED TO DATE
     C*
     C                     SUB  ICTD      AINT
     C*
     C* SUBTRACT INTEREST WRITTEN OFF TO DATE
     C*
     C                     SUB  IWOD      AINT
     C/COPY WNCPYSRC,LE0201C024
     C*
     C* EDIT ACCRUED INTEREST FOR OUTPUT TO SCREEN
     C*
      *                                                                   147830
      ** If accrued interest to date is greater than size allowed in      147830
      ** sfl display then show as ************.                           147830
      *                                                                   147830
     C           AINT      IFGT WMAX2                                     147830
     C                     MOVE *ON       *IN25                           147830
     C                     ELSE                                           147830
     C                     MOVE *OFF      *IN25                           147830
     C                     ENDIF                                          147830
      *                                                                   147830
      ** Save standard edit code for this program and set new edit        147830
      ** code for daily interest amount in sfl display only.              147830
      *                                                                   147830
     C           *IN25     IFEQ *OFF                                      147830
      *                                                                   147830
     C                     MOVE ZECODE    WECODE                          147830
     C                     MOVE 'L'       ZECODE                          147830
      *                                                                   147830
     C                     Z-ADD*ZERO     ZFLD15
     C                     MOVE AINT      ZFLD15
     C                     Z-ADDXDECP     ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    SAINT
      *                                                                   147830
      ** Restore standard edit code for this program                      147830
      *                                                                   147830
     C                     MOVE WECODE    ZECODE                          147830
      *                                                                   147830
     C                     ENDIF                                          147830
      *                                                                   147830
     C*
     C* EDIT DAILY INTEREST FOR OUTPUT TO LOANS, PARTICIPATIONS
     C* PURCHASED AND PARTICIPATIONS SOLD SCREENS
     C*
      *                                                                   147830
      ** If daily interest is greater than size allowed in sfl display    147830
      ** then show as *********.                                          147830
      *                                                                   147830
     C           DINT      IFGT WMAX1                                     147830
     C                     MOVE *ON       *IN24                           147830
     C                     ELSE                                           147830
     C                     MOVE *OFF      *IN24                           147830
     C                     ENDIF                                          147830
      *                                                                   147830
     C           *IN51     IFEQ '1'
     C           *IN52     OREQ '1'
     C           *IN53     OREQ '1'
     C           *IN24     IFEQ *OFF                                      147830
      *                                                                   147830
      ** Save standard edit code for this program and set new edit        147830
      ** code for daily interest amount in sfl display only.              147830
      *                                                                   147830
     C                     MOVE ZECODE    WECODE                          147830
     C                     MOVE 'L'       ZECODE                          147830
      *                                                                   147830
     C                     Z-ADD*ZERO     ZFLD15
     C                     MOVE DINT      ZFLD15
     C                     Z-ADDXDECP     ZDECS
     C                     EXSR ZSEDIT
     C           CLE028    IFEQ 'Y'                                                           CLE028
     C           PTYP      ANDEQ80                                                            CLE028
     C                     MOVE *BLANKS   SDINT                                               CLE028
     C                     ELSE                                                               CLE028
     C                     MOVE ZOUT21    SDINT
     C                     ENDIF                                                              CLE028
      *                                                                   147830
      ** Restore standard edit code for this program                      147830
      *                                                                   147830
     C                     MOVE WECODE    ZECODE                          147830
      *                                                                   147830
     C                     ENDIF                                          147830
     C                     END
     C*
     C* IF PROCESSING A GUARANTEE ISSUED, FORMAT FEE RATE FOR OUTPUT TO
     C* SCREEN
     C*
     C           *IN55     IFEQ '1'
     C                     Z-ADDINTR      FRATE   75
     C                     Z-ADD*ZERO     ZFLD15
     C                     MOVE FRATE     ZFLD15
     C                     Z-ADD5         ZDECS
     C                     MOVE *BLANK    ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    SFRATE
     C                     MOVE 'J'       ZECODE
     C                     END
     C*
     C* IF PROCESSING A LOAN OR A PARTICIPATION PURCHASED, CHECK PART.
     C* SOLD:
     C*
     C           *IN51     IFEQ '1'
     C           *IN52     OREQ '1'
     C*
     C* IF NUMBER OF PARTICPANTS GREATER THAN ZERO, SET PART. SOLD
     C* TO 'Y', OTHERWISE SET TO 'N'
     C*
     C                     MOVE *BLANK    SPART
     C           NOPS      IFGT *ZERO
     C                     MOVE 'Y'       SPART
     C                     ELSE
     C                     MOVE 'N'       SPART
     C                     END
     C                     END
     C*
     C* CONVERT MATURITY DATE
     C*
     C                     MOVE *BLANKS   MDATE
     C           MDAT      IFNE *ZERO
     C                     MOVE MDAT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    MDATE
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT                                                                                  CLE028
      *****************************************************************                       CLE028
      *  SROSIN - Compute for outstanding interest via Rule of 78     *                       CLE028
      *****************************************************************                       CLE028
     C           SROSIN    BEGSR                                                              CLE028
      *                                                                                       CLE028
     C                     Z-ADDVDAT      W78VDT                                              CLE028
     C                     Z-ADDMDAT      W78MDT                                              CLE028
     C                     Z-ADDFLTI      W78FLT                                              CLE028
     C                     MOVE RFRQ      W78RPF                                              CLE028
     C                     Z-ADDFRPD      W78FRD                                              CLE028
     C                     Z-ADDRDYN      W78RPM                                              CLE028
     C                     MOVE CCY       W78CCY                                              CLE028
     C                     MOVE *BLANKS   W78LCN                                              CLE028
     C                     Z-ADDVDAT      W78BEG                                              CLE028
     C                     MOVE 'A'       W78ROA                                              CLE028
     C           RUND      IFLT MDAT                                                          CLE028
     C                     Z-ADDRUND      W78END                                              CLE028
     C                     ELSE                                                               CLE028
     C                     Z-ADDMDAT      W78END                                              CLE028
     C                     ENDIF                                                              CLE028
      *                                                                                       CLE028
     C                     EXSR RULE78                                                        CLE028
      *                                                                                       CLE028
     C                     Z-ADDW78INT    ZINTR                                               CLE028
      *                                                                                       CLE028
     C                     ENDSR                                                              CLE028
      *****************************************************************                       CLE028
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    ROLDWG - PROCESS ROLLDOWN FOR GUARANTEES                      *
     C*                                                                  *
     C*    CALLED BY: PROGIP - PROCESS GUARANTEES SCREEN INPUT           *
     C*    CALLS:     SELECG - DETERMINE IF GUARANTEES RECORD TO BE      *
     C*                        SELECTED                                  *
     C*               FILGSF - FILL GUARANTEES SUBFILE                   *
     C*                                                                  *
     C********************************************************************
     C*
     C           ROLDWG    BEGSR
     C*
     C                     Z-ADD*ZERO     RRN
     C**********           Z-ADD$GUCN     #GUCN                                               CSD027
     C                     MOVE $GUCN     #GUCN                                               CSD027
     C                     MOVE $FCLN     #FCLN
     C                     Z-ADD$GUSN     #GUSN
     C*
     C* SET FILE POINTER AFTER TOP OF THIS PAGE GUARANTEE SEQUENCE
     C* NUMBER AND READ PREVIOUS RECORD
     C*
     C           GTREFK    SETGTGTREF
     C                     READPGTREF                    60*
     C*
     C* DO WHILE NOT START OF FILE AND SUBFILE PAGE NOT FULL
     C*
     C           *IN60     DOWEQ'0'
     C           RRN       ANDLT#COUNT
     C*
     C* IF CUSTOMER NUMBER HAS CHANGED, TREAT AS START OF FILE
     C*
     C           GOCN      IFNE #CNUM
     C                     SETON                         60*
     C*
     C* OTHERWISE, CONTINUE READING BACKWARDS THROUGH FILE
     C*
     C                     ELSE
     C                     READPGTREF                    60*
     C*
     C* DETERMINE IF RECORD TO BE SELECTED
     C*
     C                     EXSR SELECG
     C*
     C* IF RECORD SELECTED FOR OUTPUT
     C*
     C           SELCT     IFEQ 'Y'
     C*
     C* ADD 1 TO COUNT OF RECORDS READ
     C*
     C                     ADD  1         RRN
     C                     END
     C                     END
     C*
     C                     END
     C*
     C* RESET TOP OF THIS PAGE GUARANTEED CUSTOMER NUMBER, FACILITY/
     C* LOAN REFERENCE AND GUARANTEE SEQUENCE NUMBER
     C*
     C           *IN60     IFEQ '0'
     C**********           Z-ADDGUCN      #GUCN            *TOP OF PAGE                       CSD027
     C                     MOVE GUCN      #GUCN            *TOP OF PAGE                       CSD027
     C                     MOVE FCLN      #FCLN            *TOP OF PAGE
     C                     Z-ADDGUSN      #GUSN            *TOP OF PAGE
     C                     ELSE
     C**********           Z-ADD*ZERO     #GUCN                                               CSD027
     C                     MOVE *BLANKS   #GUCN                                               CSD027
     C                     MOVE *BLANKS   #FCLN
     C                     Z-ADD*ZERO     #GUSN
     C                     END
     C*
     C* FILL NEXT PAGE OF SUBFILE
     C*
     C                     EXSR FILGSF
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    ROLDWN - PROCESS ROLLDOWN                                     *
     C*                                                                  *
     C*    CALLED BY: PROINP - PROCESS SCREEN INPUT                      *
     C*    CALLS:     SELECT - DETERMINE IF RECORD TO BE SELECTED        *
     C*               FILLSF - FILL SUBFILE                              *
     C*                                                                  *
     C********************************************************************
     C*
     C           ROLDWN    BEGSR
     C*
     C                     Z-ADD*ZERO     RRN
     C                     Z-ADD$FACT     #FACT
     C                     Z-ADD$FCNO     #FCNO
     C**********           Z-ADD$LNNO     #LNNO                                               CLE148
     C                     MOVEL$LNNO     #LNNO                                               CLE148
     C*
     C* SET FILE POINTER AFTER TOP OF THIS PAGE LOAN NUMBER AND READ
     C* PREVIOUS RECORD
     C*
     C           LEREFK    SETGTLEREF
     C                     READPLEREF                    60*
     C*
     C* DO WHILE NOT START OF FILE AND SUBFILE PAGE NOT FULL
     C*
     C           *IN60     DOWEQ'0'
     C/COPY WNCPYSRC,LE0201C007
     C           RRN       ANDLT#COUNT
     C/COPY WNCPYSRC,LE0201C008
     C*
     C* IF CUSTOMER NUMBER HAS CHANGED, TREAT AS START OF FILE
     C*
     C           CNUMR     IFNE #CNUM
     C                     SETON                         60*
     C*
     C* OTHERWISE, CONTINUE READING BACKWARDS THROUGH FILE
     C*
     C                     ELSE
     C                     READPLEREF                    60*
     C*
     C* DETERMINE IF RECORD TO BE SELECTED
     C*
     C/COPY WNCPYSRC,LE0201C009
     C                     EXSR SELECT
     C/COPY WNCPYSRC,LE0201C010
     C*
     C* IF RECORD SELECTED FOR OUTPUT
     C*
     C           SELCT     IFEQ 'Y'
     C*
     C* ADD 1 TO COUNT OF RECORDS READ
     C*
     C                     ADD  1         RRN
     C                     END
     C                     END
     C*
     C                     END
     C*
     C* RESET TOP OF THIS PAGE FACILITY TYPE, FACILITY SEQUENCE AND
     C* LOAN NUMBER
     C*
     C           *IN60     IFEQ '0'
     C                     Z-ADDFACT      #FACT            *TOP OF PAGE
     C                     Z-ADDFCNO      #FCNO            *TOP OF PAGE
     C**********           Z-ADDLNNO      #LNNO            *TOP OF PAGE                       CLE148
     C                     MOVELLNNO      #LNNO                                               CLE148
     C                     ELSE
     C                     Z-ADD*ZERO     #FACT
     C                     Z-ADD*ZERO     #FCNO
     C**********           Z-ADD*ZERO     #LNNO                                               CLE148
     C                     MOVEL*BLANKS   #LNNO                                               CLE148
     C                     END
     C*
     C* FILL NEXT PAGE OF SUBFILE
     C*
     C                     EXSR FILLSF
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    SELECG - DETERMINE IF GUARANTEES RECORD TO BE SELECTED        *
     C*                                                                  *
     C*    CALLED BY: READGT - READ LIVE RECORD FROM GTREF               *
     C*               ROLDWG - PROCESS ROLLDOWN FOR GUARANTEES           *
     C*    CALLS:     *PSSR  - ERROR PROCESSING                          *
     C*               BRCCOM - CHECK BRANCH AND COMPANY CODES            *
     C*                                                                  *
     C********************************************************************
     C*
     C           SELECG    BEGSR
     C*
     C* ONLY SELECT RECORDS FROM GTREF WITH RECORD ID EQUAL TO 'D'
     C*
     C           RECIG     IFEQ 'D'
     C                     MOVE 'Y'       SELCT
     C                     ELSE
     C                     MOVE 'N'       SELCT
     C                     END
     C*
     C* ACCESS GUARANTEES FILE USING GUARANTEED CUSTOMER NUMBER,
     C* FACILITY/LOAN REFERENCE AND GUARANTEE SEQUENCE NUMBER FROM
     C* GTREF
     C*
     C           SELCT     IFEQ 'Y'
     C           GTEESK    CHAINGTEES                61
     C*
     C* DATABASE ERROR PROCESSING
     C*
     C           *IN61     IFEQ '1'
     C           RECI      ORNE 'D'
     C           RECI      ANDNE'E'
     C                     MOVEL'GTEES'   DBFILE
     C                     MOVE GUCN      DSGUCN
     C                     MOVE FCLN      DSFCLN
     C                     MOVE GUSN      DSGUSN
     C                     MOVELGTEESD    DBKEY
     C                     Z-ADD1         DBASE
     C                     EXSR *PSSR
     C                     END
      *                                                                   156778
      ** Skip this guarantee if value date has not been reached.          156778
      *                                                                   156778
     C           STTD      IFLE BJRDNB                                    156778
     C*
     C* SET FACILITY/LOAN RECORD FOUND FLAG TO BLANK
     C*
     C                     MOVE *BLANK    FCLNRF  1
     C*
     C**IF*PROCESSING*A*FACILITY*(FIRST*CHARACTER*OF*FCLN*IS*'F'),*****                       CLE148
      ** If processing a facility (first character of FCLN is ' '),                           CLE148
     C* ACCESS FACILITIES FILE RECORD TYPE 'A'
     C*
     C********** FLFLAG    IFEQ 'F'                                                           CLE148
     C           FLFLAG    IFEQ *BLANK                                                        CLE148
     C**********           Z-ADDGUCN      #CNUM                                               CSD027
     C                     MOVE GUCN      #CNUM                                               CSD027
     C                     MOVELFLREF     #FACT
     C                     MOVE FLREF     #FCNO
     C                     MOVE 'A'       RECTF
     C           FCLTYK    CHAINFCLTY                70
     C*
     C* IF 'A' RECORD FOUND, SET FACILITY/LOAN RECORD FOUND FLAG TO 'F'
     C*
     C           *IN70     IFEQ '0'
     C           RECI      ANDEQ'D'
     C           *IN70     OREQ '0'
     C           RECI      ANDEQ'E'
     C                     MOVE 'F'       FCLNRF
     C*
     C* ACCESS FACILITY 'B' RECORD
     C*
     C                     MOVE 'B'       RECTF
     C           FCLTYK    CHAINFCLTY                61
     C*
     C* IF 'B' RECORD NOT FOUND, DATABASE ERROR HAS OCCURRED
     C*
     C           *IN61     IFEQ '1'
     C           RECI      ORNE 'D'
     C           RECI      ANDNE'E'
     C                     MOVEL'FCLTY'   DBFILE
     C                     MOVE #CNUM     DSCNUM
     C                     MOVE #FACT     DSFACT
     C                     MOVE #FCNO     DSFCNO
     C                     MOVE RECTF     DSRECT
     C                     MOVELFCLTYD    DBKEY
     C                     Z-ADD2         DBASE
     C                     EXSR *PSSR
     C*
     C* OTHERWISE, 'B' RECORD FOUND, STORE CURRENT EXPOSURE AMOUNT
     C*
     C                     ELSE
      *                                                                                       CLE023
      ** If CLE023 is installed, use the field CEXP for current                               CLE023
      ** exposure instead of CAMD                                                             CLE023
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
     C                     Z-ADDCEXP      OSAMT                                               CLE023
     C                     ELSE                                                               CLE023
     C                     Z-ADDCAMD      OSAMT  130
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C                     END
     C*
     C* OTHERWISE, FACILITY 'A' RECORD NOT FOUND (NOT DATABASE ERROR
     C* SINCE FACILITY MAY BE REVERSED WITHOUT REVERSING GUARANTEE),
     C* SET OUTSTANDING AMOUNT TO ZERO
     C*
     C                     ELSE
     C                     Z-ADD*ZERO     OSAMT
     C                     END
     C*
     C* OTHERWISE, PROCESSING A LOAN, ACCESS LOANS FILE
     C*
     C                     ELSE
     C                     MOVE FCLN      LNNO
     C           CLOANK    CHAINCLOAN                70
     C*
     C* IF LOAN RECORD FOUND, SET FACILITY/LOAN RECORD FOUND FLAG TO
     C* 'L' AND STORE CURRENT PRINCIPAL AMOUNT
     C*
     C           *IN70     IFEQ '0'
     C           RECI      ANDEQ'D'
     C           *IN70     OREQ '0'
     C           RECI      ANDEQ'M'
     C                     MOVE 'L'       FCLNRF
     C                     Z-ADDCPAM      OSAMT
     C*
     C* OTHERWISE, LOAN RECORD NOT FOUND, SET OUTSTANDING AMOUNT TO
     C* ZERO
     C*
     C                     ELSE
     C                     Z-ADD*ZERO     OSAMT
     C                     END
     C                     END
     C*
     C                     MOVE 'Y'       SELCT
     C*
     C* IF MULTI-BRANCHING AND A FACILITY OR LOAN RECORD FOUND, CHECK
     C* BRANCH AND COMPANY CODES
     C*
     C           MBIN      IFEQ 'Y'
     C           FCLNRF    ANDNE*BLANK
     C                     EXSR BRCCOM
     C                     END
      *                                                                   156778
     C                     ELSE                                           156778
     C                     MOVE 'N'       SELCT                           156778
     C                     ENDIF                                          156778
      *                                                                   156778
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    SELECT - DETERMINE IF RECORD TO BE SELECTED                   *
     C*                                                                  *
     C*    CALLED BY: READLE - READ LIVE RECORD FROM LEREF               *
     C*               ROLDWN - PROCESS ROLLDOWN                          *
     C*    CALLS:     *PSSR  - ERROR PROCESSING                          *
     C*               BRCCOM - CHECK BRANCH AND COMPANY CODES            *
     C*                                                                  *
     C********************************************************************
     C*
     C           SELECT    BEGSR
     C*
     C* ONLY SELECT RECORDS FROM LEREF WITH RECORD ID EQUAL TO 'D'
     C*
     C           RECIR     IFEQ 'D'
     C                     MOVE 'Y'       SELCT
     C                     ELSE
     C                     MOVE 'N'       SELCT
     C                     END
     C*
     C* ONLY SELECT RECORDS FROM LEREF WITH FACILITY/LOAN INDICATOR
     C* NOT EQUAL TO 'F'
     C* (unless processing for participations purchased)                  E27023
     C*
     C           SELCT     IFEQ 'Y'
     C           FLIN      IFEQ 'F'
     C           *IN52     ANDNE'1'                                       E27023
     C                     MOVE 'N'       SELCT
     C                     END
     C                     END
     C*
     C* ACCESS LOANS FILE USING LOAN NUMBER FROM LEREF
     C*
     C           SELCT     IFEQ 'Y'
     C           CLOANK    CHAINCLOAN                61
     C*
     C* DATABASE ERROR PROCESSING
     C*
     C           *IN61     IFEQ '1'
     C           RECI      ORNE 'D'
     C           RECI      ANDNE'M'
     C                     MOVEL'CLOAN'   DBFILE
     C                     MOVELLNNO      LKEY    7
     C                     MOVE RECTL     LKEY
     C                     MOVELLKEY      DBKEY
     C                     Z-ADD4         DBASE
     C                     EXSR *PSSR
     C                     END
     C*
     C* IF MULTI-BRANCHING, CHECK BRANCH AND COMPANY CODES
     C*
     C           MBIN      IFEQ 'Y'
     C                     EXSR BRCCOM
     C                     END
     C                     END
     C*
     C* ONLY SELECT RECORDS WITH CORRECT PROCESSING TYPE
     C* IE. IF MENU OPTION FOR LOANS ENTERED, ONLY SELECT RECORDS FROM
     C* CLOAN WITH PROCESSING TYPE 61, 62, 63
      ** and 80 (Flat rate personal loans)                                                    CLE028
     C*
     C           *IN51     IFEQ '1'
     C           PTYP      IFNE 61
     C           PTYP      ANDNE62
     C           PTYP      ANDNE63
     C           PTYP      ANDNE80                                                            CLE028
     C                     MOVE 'N'       SELCT
     C/COPY WNCPYSRC,LE0201C004
     C                     END
     C                     END
     C*
     C* IF MENU OPTION FOR PARTICIPATIONS PURCHASED ENTERED, ONLY
     C* SELECT RECORDS FROM CLOAN WITH PROCESSING TYPE 64, 65
     C*
     C           *IN52     IFEQ '1'
     C           PTYP      IFNE 64
     C           PTYP      ANDNE65
     C           CLE005    ANDEQ'N'                                       CLE005
      *                                                                   CLE005
      ** If SAR CLE005 is Present, include PTYP 68 and 71                 CLE005
      *                                                                   CLE005
     C           CLE005    OREQ 'Y'                                       CLE005
     C           PTYP      ANDNE64                                        CLE005
     C           PTYP      ANDNE65                                        CLE005
     C           PTYP      ANDNE68                                        CLE005
     C           PTYP      ANDNE71                                        CLE005
      *                                                                   CLE005
     C                     MOVE 'N'       SELCT
     C                     END
     C                     END
     C*
     C* IF MENU OPTION FOR PARTICIPATIONS SOLD ENTERED, ONLY SELECT
     C* RECORDS FROM CLOAN WITH PROCESSING TYPE 66, 67
     C*
     C           *IN53     IFEQ '1'
     C           PTYP      IFNE 66
     C           PTYP      ANDNE67
     C           CLE005    ANDEQ'N'                                       CLE005
      *                                                                   CLE005
      ** If SAR CLE005 is Present, include PTYP 69 and 72                 CLE005
      *                                                                   CLE005
     C           CLE005    OREQ 'Y'                                       CLE005
     C           PTYP      ANDNE66                                        CLE005
     C           PTYP      ANDNE67                                        CLE005
     C           PTYP      ANDNE69                                        CLE005
     C           PTYP      ANDNE72                                        CLE005
      *                                                                   CLE005
     C                     MOVE 'N'       SELCT
     C                     END
     C                     END
     C*
     C* IF MENU OPTION FOR GUARANTEES ISSUED ENTERED, ONLY SELECT
     C* RECORDS FROM CLOAN WITH PROCESSING TYPE 70
     C*
     C           *IN55     IFEQ '1'
     C           PTYP      IFNE 70
     C                     MOVE 'N'       SELCT
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*
     C/COPY WNCPYSRC,LE0201C005
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    BRCCOM - CHECK BRANCH AND COMPANY CODES                       *
     C*                                                                  *
     C*    CALLED BY: SELECG - DETERMINE IF GUARANTEES RECORD TO BE      *
     C*                        SELECTED                                  *
     C*               SELECT - DETERMINE IF RECORD TO BE SELECTED        *
     C*    CALLS:     *PSSR  - ERROR PROCESSING                          *
     C*                                                                  *
     C********************************************************************
     C*
     C           BRCCOM    BEGSR
     C*
     C* SELECT FACILITY RECORDS BY BRANCH CODE FOR BRANCH REPORT
     C*
     C           SBRCD     IFNE *BLANKS
     C           SBRCD     ANDNE'ALL'
     C           SBRCD     ANDNEBRCAX
     C                     MOVE 'N'       SELCT
     C                     END
     C*
     C* SELECT FACILITY RECORDS BY COMPANY FOR COMPANY REPORT
     C*
     C           SCOMP     IFNE *BLANKS
     C           SCOMP     ANDNE'ALL'
     C*
     C* ACCESS SDBRCHPD TO DETERMINE COMPANY FOR BRANCH OF FACILITY/
     C* LOAN
     C*
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG    '@RTCD
     C                     PARM '*KEY    '@OPTN
     C                     PARM BRCAX     @DSNB
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
     C*
     C* DATABASE ERROR PROCESSING
     C*
     C           @RTCD     IFNE *BLANKS
     C                     MOVEL'SDBRCHPD'DBFILE
     C                     MOVEL@DSNB     DBKEY
     C                     Z-ADD3         DBASE
     C                     MOVEL@OPTN     DBOPTN  7
     C                     EXSR *PSSR
     C*
     C* OTHERWISE, COMPARE COMPANY INPUT ON SCREEN WITH COMPANY ON
     C* BRANCH CODE RECORD
     C*
     C                     ELSE
     C           SCOMP     IFNE A8CMCD
     C                     MOVE 'N'       SELCT
     C                     END
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    CCYARR - SET UP CURRENCY ARRAYS                               *
     C*                                                                  *
     C*    CALLED BY: INIT - INITIAL PROCESSING                          *
     C*    CALLS:                                                        *
     C*                                                                  *
     C********************************************************************
     C*
     C           CCYARR    BEGSR
     C*
     C* READ ALL RECORDS FROM CURRENCY DETAILS FILE AND SET UP CURRENCY
     C* CODE, DECIMAL PLACES, SPOT RATES AND MULTIPLY/DIVIDE INDICATORS
     C* ARRAYS
     C*
     C                     OPEN SDCURRPD
     C*
     C                     Z-ADD1         X       30
     C                     READ SDCURRPD                 61
     C*
     C           *IN61     DOWEQ'0'
     C                     MOVE A6CYCD    XCYA,X
     C                     MOVE A6NBDP    XDPA,X
     C                     Z-ADDA6SPRT    XSPA,X
     C                     MOVE A6MDIN    XMDA,X
     C                     ADD  1         X
     C                     READ SDCURRPD                 61
     C                     END
     C*
     C                     CLOSESDCURRPD
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    CCYDEP - ACCESS CURRENCY DETAILS                              *
     C*                                                                  *
     C*    CALLED BY: SFRECG - SET UP GUARANTEES SUBFILE RECORD          *
     C*               SFREC  - SET UP SUBFILE RECORD                     *
     C*    CALLS:     *PSSR  - ERROR PROCESSING                          *
     C*                                                                  *
     C********************************************************************
     C*
     C           CCYDEP    BEGSR
     C*
     C* IF CURRENCY IS BLANK, IT IS A DATABASE ERROR ON SDCURRPD
     C*
     C           XCCY      IFEQ *BLANKS
     C                     MOVEL'SDCURRPD'DBFILE
     C                     MOVELXCCY      DBKEY
     C                     Z-ADD5         DBASE
     C                     EXSR *PSSR
     C*
     C* OTHERWISE, LOOK UP CURRENCY IN CURRENCY ARRAY
     C*
     C                     ELSE
     C                     Z-ADD1         X
     C           XCCY      LOKUPXCYA,X                   61
     C*
     C* ARRAY XCYA HAS BEEN FILLED WITH RECORDS FROM SDCURRPD IN
     C* SR. CCYARR; THEREFORE, IF A CURRENCY IS NOT FOUND IN THE ARRAY,
     C* IT IS A DATABASE ERROR ON SDCURRPD
     C*
     C           *IN61     IFEQ '0'
     C                     MOVEL'SDCURRPD'DBFILE
     C                     MOVELXCCY      DBKEY
     C                     Z-ADD6         DBASE
     C                     EXSR *PSSR
     C*
     C* OTHERWISE, STORE CURRENCY DECIMAL PLACES, SPOT RATE AND
     C* MULTIPLY/DIVIDE INDICATOR
     C*
     C                     ELSE
     C                     Z-ADDXDPA,X    XDECP   10
     C                     Z-ADDXSPA,X    XSPOT  138
     C                     MOVE XMDA,X    XMDIN   1
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    CMDSR - PROCESS COMMAND KEYS                                  *
     C*                                                                  *
     C*    CALLED BY: VALIDF - VALIDATE FIRST SCREEN                     *
     C*               VALIDS - VALIDATE SECOND SCREEN                    *
     C*               PROGIP - PROCESS GUARANTEES SCREEN INPUT           *
     C*               PROINP - PROCESS SCREEN INPUT                      *
     C*    CALLS:     RETSR  - END OF PROGRAM PROCESSING                 *
     C*                                                                  *
     C*    THIS IS A STANDARD LINK ENQUIRY SUBROUTINE FOR PROCESSING     *
     C*    THE COMMAND KEYS                                              *
     C*                                                                  *
     C********************************************************************
     C*
     C           CMDSR     BEGSR
     C*
     C* SET DATA AREA WORK FIELD RTNDWF TO BLANKS
     C*
     C                     MOVE *BLANK    RTNDWF  1
     C*
     C* IF F3 PRESSED, RETURN TO MENU
     C*
     C           *INKC     IFEQ '1'
     C                     MOVE 'Q'       RTNDWF
     C                     EXSR RETSR
     C                     END
     C*
     C* IF F1 PRESSED, RETURN TO INITIAL ENQUIRY
     C*
     C           *IN11     IFEQ '1'
     C           *IN21     OREQ '1'
     C                     MOVE 'S'       RTNDWF
     C*
     C* IF F1 TAKEN FROM FIRST SCREEN OR F1 TAKEN FROM SECOND SCREEN
     C* AND THIS PROGRAM CALLED AS A LINKED ENQUIRY, EXIT THIS PROGRAM
     C*
     C           *IN11     IFEQ '1'
     C           *IN21     OREQ '1'
     C           MODID     ANDEQ'LE'
     C                     EXSR RETSR
     C                     END
     C                     END
     C*
     C* IF F12 PRESSED, RETURN TO PREVIOUS ENQUIRY
     C*
     C           *IN12     IFEQ '1'
     C           *IN22     OREQ '1'
     C                     MOVE 'U'       RTNDWF
     C*
     C* IF F12 TAKEN FROM FIRST SCREEN, EXIT THIS PROGRAM
     C* (IF F12 TAKEN FROM SECOND SCREEN, FIRST SCREEN REDISPLAYED)
     C*
     C           *IN12     IFEQ '1'
     C                     EXSR RETSR
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
      /EJECT
     C/COPY WNCPYSRC,LEH00917
     C********************************************************************
     C*                                                                  *
     C*    RETSR - END OF PROGRAM PROCESSING                             *
     C*                                                                  *
     C*    CALLED BY: PROSEL - PROCESS SELECTION                         *
     C*               PROFAC - PROCESS FACILITIES OPTION                 *
     C*               CMDSR  - PROCESS COMMAND KEYS                      *
     C*               *PSSR  - ERROR PROCESSING                          *
     C*    CALLS:                                                        *
     C*                                                                  *
     C*    THIS IS A STANDARD LINK ENQUIRY SUBROUTINE FOR ENDING THE     *
     C*    PROGRAM                                                       *
     C*                                                                  *
     C********************************************************************
     C*
     C           RETSR     BEGSR
     C*
     C* SET ON LR
     C*
     C                     SETON                     LR
     C*
     C* ACCESS EM DATA AREA
     C*
     C           *LOCK     IN   EMSDS
     C                     MOVE RTNDWF    RETND
     C                     OUT  EMSDS
     C*
     C                     RETRN
     C*
     C                     ENDSR
     C********************************************************************
      /EJECT
     C********************************************************************
     C*                                                                  *
     C*    *PSSR - ERROR PROCESSING                                      *
     C*                                                                  *
     C*    CALLED BY: SELECG - DETERMINE IF GUARANTEES RECORD TO BE      *
     C*                        SELECTED                                  *
     C*               SELECT - DETERMINE IF RECORD TO BE SELECTED        *
     C*               BRCCOM - CHECK BRANCH AND COMPANY CODES            *
     C*               CCYDEP - ACCESS CURRENCY DETAILS                   *
     C*    CALLS:     RETSR  - END OF PROGRAM PROCESSING                 *
     C*                                                                  *
     C********************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'LE0201'  DBPGM
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     SETON                     U7U8LR
     C*
     C           MODID     IFEQ 'LE'
     C                     MOVE 'E'       RTNDWF
     C                     EXSR RETSR
     C                     END
     C*
     C                     END
     C*
     C                     RETRN
     C*
     C                     ENDSR
     C*                                                                  *
     C********************************************************************
      /EJECT
      ***** ZSRSRC,ZDATE2Z2                                               S01392
      /COPY ZSRSRC,ZDATE2Z4                                               S01932
      /EJECT
      /COPY ZSRSRC,ZSEDITZ3
      /EJECT
      /COPY ZSRSRC,GLINTCZ1
      /EJECT
      /COPY ZSRSRC,ZSAVEZ1
      /EJECT
      /COPY ZSRSRC,ZRSTORZ1
      /EJECT
      /COPY ZSRSRC,ZXRATEZ1
      /EJECT
      /COPY ZSRSRC,ZCONVZ1
      /EJECT
      /COPY ZSRSRC,ZFLACZ2                                                S01392
      /EJECT                                                              066550
     C/COPY ZSRSRC,ZINTDYZ2                                               066550
      /EJECT                                                              066550
     C/COPY ZSRSRC,ZDATE9Z3                                               066550
     C/COPY ZSRSRC,ZDAT10Z2                                               126017
      /EJECT                                                              066550
     C/COPY ZSRSRC,ZFREQBZ2                                                                   CLE028
     C/COPY ZSRSRC,ZDATE1Z2                                                                   CLE028
     C/COPY ZSRSRC,ZACCH                                                                      CLE028
     C/COPY ZSRSRC,ZCHKH                                                                      CLE028
     C/COPY ZSRSRC,RULE78Z1                                                                   CLE028
      /EJECT                                                                                  CLE028
     C********************************************************************
**  POWER - ARRAY OF POWERS FOR CURRENCY CONVERSION
0000001
0000010
0000100
0001000
0010000
0100000
1000000
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**   MES - SCREEN MESSAGES
ENTER '1' OR 'Y' AGAINST REQUIRED OPTION
INVALID OPTION - ENTER '1' OR 'Y' AGAINST REQUIRED OPTION
INVALID OPTION - ONLY ONE OPTION MAY BE SELECTED
ENTER CUSTOMER NUMBER OR SHORTNAME
CUSTOMER NUMBER NOT ON FILE
CUSTOMER SHORTNAME NOT ON FILE
ENTER EITHER BRANCH OR COMPANY
NO BANK AUTHORIZATION FOR USER
INVALID BRANCH CODE
USER IS NOT AUTHORISED TO ANY BOOKING BRANCHES
USER IS NOT AUTHORISED TO THE BOOKING BRANCH
INVALID COMPANY CODE
USER IS NOT AUTHORISED TO ANY COMPANIES
USER IS NOT AUTHORISED TO THE COMPANY
NO OUTSTANDING GUARANTEES GIVEN BY THIS CUSTOMER
NO OUTSTANDING GUARANTEES GIVEN BY THIS CUSTOMER IN THE BRANCH/COMPANY
NO LOANS FOR THIS CUSTOMER
NO OUTSTANDING PARTICIPATIONS PURCHASED FOR THIS CUSTOMER
NO OUTSTANDING PARTICIPATIONS SOLD FOR THIS CUSTOMER
NO OUTSTANDING GUARANTEES ISSUED FOR THIS CUSTOMER
NO LOANS FOR CUSTOMER IN THE BRANCH/COMPANY
NO OUTSTANDING PARTICIPATIONS PURCHASED FOR CUSTOMER IN THE BRANCH/COMPANY
NO OUTSTANDING PARTICIPATIONS SOLD FOR CUSTOMER IN THE BRANCH/COMPANY
NO OUTSTANDING GUARANTEES ISSUED FOR CUSTOMER IN THE BRANCH/COMPANY
ENTER '1' FOR LOAN DETAILS, '2' FOR LOAN ACTIVITY
CUSTOMER IS CLOSED                                                                          MD036924
**   BRTTLA - BRANCH TITLE
             All Branches
             All Companies
** CPY@   **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
** @YD  USED BY SR. ZDATE9 - YEARS, IN DAYS, CUMULATIVE IN 4 YEAR SPAN
00366007310109601461
** @MD  USED BY SR. ZDATE9 - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
00000000310005900090001200015100181002120024300273003040033400365
**   ZFMD  -  NUMBER OF DAYS IN THE MONTH                                                     CLE028
312831303130313130313031                                                                      CLE028
