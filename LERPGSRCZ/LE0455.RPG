     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Loan increases and repayments ccy conv.')     *
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  LE0455 - Loan Increases and Repayments Currency Conversion   *
      *                                                               *
      *  Function:  This program produces a report for all outstan    *
      *  (COB)      ding repayment schedules, manual repayments and   *
      *             loan amendments that are converted from old cur   *
      *             rency to new loan currency.                       *
      *                                                               *
      *  Called By: LEC0455 - Loan Increases and Repayments Ccy       *
      *                       Conversion.                             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 240072             Date 30Jun06               *
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 18May04               *
      *                 CGL029             Date 01Sep03               *
      *                 CAP075             Date 08Aug02               *
      *                 CLE031             Date 05Feb03               *
      *                 CAS005             Date 16Dec02               *
      *                 CLE028             Date 27Jun02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 194952             Date 01Aug01               *
      *                 194933             Date 01Jul01               *
      *                 CLE014  *CREATE    Date 20Mar01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *           (Recompile)                                         *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference                                *
      *  240072 - OSAC not being updated during multicurrency rollover*
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL039 - Increase Account Code to 10 digits                  *
      *  CAP075 - Lending API enhancements - Loan Amendment.          *
      *           (amendment in common with CAP079)                   *
      *  CLE031 - Lending Enhancements.                               *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *           (Recompile)                                         *
      *  CLE028 - Flat Rate Personal Loans (Rule of 78s)              *
      *           (Recompile)                                         *
      *  194952 - Defaulting of settlement details.                   *
      *  194933 - Error in coverting currency amounts. Uses spot rate *
      *           instead of the rate defined in multi-ccy rollover.  *
      *  CLE014 - Customer Driven Lending Enhancements                *
      *                                                               *
      *****************************************************************
     FLEACT5L1IF  E           K        DISK
     F            LOAMSDKF                          KRENAMEACTN5DKF
     FLOAMSL1 UF  E           K        DISK         KINFSR *PSSR
     F            LOAMSDHF                          KIGNORE
     F            LOAMSZ1F                          KIGNORE
     FCLOAN   IF  E           K        DISK                                                   194933
     F            CLOANHHF                          KIGNORE                                   194933
     F            CLOANZ1F                          KIGNORE                                   194933
     F            CLOANCKF                          KIGNORE                                   194933
     FPETEMPL0IF  E           K        DISK                                                   194933
     F            CLOANCLF                          KRENAMEPETEMPLF                           194933
     FLOAMSZ1 UF  E                    DISK         KINFSR *PSSR
     FLE0455P1O   E             11     PRINTER                        UC
     F                                              KINFDS SPOOL1
     FLE0455AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   30  - Printer file indicator                                *
      *   37  - Multibranching ON                                     *
      *   60  - End of File                                           *
      *   61  - End of File                                           *
      *   62  - End of File                                           *
      *                                                               *
      *                                                               *
      *   89  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRINIT - Program Initialisation                              *
      *  SRPROC - Detail Processing                                   *
      *  SRTERM - Termination Processing                              *
      *  SRREPT - Process Report Lines                                *
      *                                                               *
      *  SRCONV - Convert Amount                                      *
      *  SRUPTR - Update Trailer File                                 *
      *  SRFRMT - Format Loan Details                                 *
      *  SRPREC - Format and Write a Detail Record to the Report      *
      *  SRREND - Write End of Report                                 *
      *                                                               *
      *  SRAUDT - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMYY/MMDDYY)  *
      *  ZSEDIT - Edit an Amount                                      *
      *  GLZSUB - Subtract Amount to the Total                        *
      *  GLZADD - Add Amount to the Total                             *
      *  GLZSUM - Carry out the addition for subroutine               *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
     E                    POWER   1   7  7 3
      *
      ** Array containing Copyright statement
      *
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZSEDITZ1
      /SPACE 3
      *
     IACTN5DKF
     I              LNRF                            RALNRF
     I              CNUM                            RACNUM
     I              CCY                             RACCY
     I              BRCA                            RABRCA
     I              VDAT                            RAVDAT
     I              AMTP                            RAAMTP
     I              PRAM                            RAPRAM
     I              AUTO                            RAAUTO
     I              RLCY                            RARLCY
      *                                                                                       194933
     ICLOANCLF                                                                                194933
     I              RECI                            CLRECI                                    194933
     I              LNRF                            CLLNRF                                    194933
     I              VDAT                            CLVDAT                                    194933
     I              MRIN                            CLMRIN                                    194933
     I              LTYP                            CLLTYP                                    194933
     I              SUTP                            CLSUTP                                    194933
     I              PTYP                            CLPTYP                                    194933
     I              BRTT                            CLBRTT                                    194933
     I              RTSP                            CLRTSP                                    194933
     I              CCY                             CLCCY                                     194933
     I              BRCA                            CLBRCA                                    194933
     I              CNUM                            CLCNUM                                    194933
     I              REPT                            CLREPT                                    194933
     I              AUTO                            CLAUTO                                    194933
     I              FCXR                            CLFCXR                                    194933
     I              FMDI                            CLFMDI                                    194933
     I              FACO                            CLFACO                                    194933
     I              BRTE                            CLBRTE                                    194933
     I              SPIN                            CLSPIN                                    194933
     I              WTIN                            CLWTIN                                    194933
     I              FCUS                            CLFCUS                                    194933
     I              FTYP                            CLFTYP                                    194933
     I              FSEQ                            CLFSEQ                                    194933
     I              INTC                            CLINTC                                    194933
     I              NACD                            CLNACD                                    194933
     I              FCLB                            CLFCLB                                    194933
     I              ORED                            CLORED                                    194933
     I              LCD                             CLLCD                                     194933
     I              CHTP                            CLCHTP                                    194933
     I              TNLU                            CLTNLU                                    194933
     I              RSTM                            CLRSTM                                    194933
     I              RONS                            CLRONS                                    194933
     I              RIBN                            CLRIBN                                    194933
     I              RIBA                            CLRIBA                                    194933
     I              ROBN                            CLROBN                                    194933
     I              ROCN                            CLROCN                                    194933
     I              PSTM                            CLPSTM                                    194933
     I              PONS                            CLPONS                                    194933
     I              PIBN                            CLPIBN                                    194933
     I              PIBA                            CLPIBA                                    194933
     I              POBN                            CLPOBN                                    194933
     I              POCN                            CLPOCN                                    194933
     I              RCRN                            CLRCRN                                    194933
     I              RCRA                            CLRCRA                                    194933
     I              RVNO                            CLRVNO                                    194933
     I              AWBN                            CLAWBN                                    194933
     I              AWBA                            CLAWBA                                    194933
     I              BENN                            CLBENN                                    194933
     I              BENA                            CLBENA                                    194933
     I              DTP1                            CLDTP1                                    194933
     I              DTP2                            CLDTP2                                    194933
     I              DTP3                            CLDTP3                                    194933
     I              DTP4                            CLDTP4                                    194933
     I              DCHG                            CLDCHG                                    194933
     I              BTB1                            CLBTB1                                    194933
     I              BTB2                            CLBTB2                                    194933
     I              BTB3                            CLBTB3                                    194933
     I              BTB4                            CLBTB4                                    194933
     I              BTB5                            CLBTB5                                    194933
     I              BTB6                            CLBTB6                                    194933
     I              CVMR                            CLCVMR                                    194933
     I              FSRP                            CLFSRP                                    194933
     I              FSGN                            CLFSGN                                    194933
     I              FPRC                            CLFPRC                                    194933
     I              DFTP                            CLDFTP                                    194933
     I              DFST                            CLDFST                                    194933
     I              MNSG                            CLMNSG                                    194933
     I              GASS                            CLGASS                                    194933
     I              GPRT                            CLGPRT                                    194933
     I              IUSR                            CLIUSR                                    194933
     I              AUSR                            CLAUSR                                    194933
     I              XUSR                            CLXUSR                                    194933
     I              PCRF                            CLPCRF                                    194933
     I              PCOB                            CLPCOB                                    194933
     I              PDDI                            CLPDDI                                    194933
     I              PTDI                            CLPTDI                                    194933
     I              SCCY                            CLSCCY                                    194933
     I              ICCY                            CLICCY                                    194933
     I              REPI                            CLREPI                                    194933
     I              PSCY                            CLPSCY                                    CLE031
     I              PEXR                            CLPEXR                                    CLE031
     I              PEXI                            CLPEXI                                    CLE031
     I              REXR                            CLREXR                                    CLE031
     I              REXI                            CLREXI                                    CLE031
     I              CLAS                            CLCLAS                                    CLE042
     I              DFCL                            CLDFCL                                    CLE042
      *                                                                                       194933
     IPETEMPLF                                                                                194933
     I              RECI                            PERECI                                    194933
     I              LNRF                            PELNRF                                    194933
     I              VDAT                            PEVDAT                                    194933
     I              MRIN                            PEMRIN                                    194933
     I              LTYP                            PELTYP                                    194933
     I              SUTP                            PESUTP                                    194933
     I              PTYP                            PEPTYP                                    194933
     I              BRTT                            PEBRTT                                    194933
     I              RTSP                            PERTSP                                    194933
     I              CCY                             PECCY                                     194933
     I              BRCA                            PEBRCA                                    194933
     I              CNUM                            PECNUM                                    194933
     I              REPT                            PEREPT                                    194933
     I              AUTO                            PEAUTO                                    194933
     I              FCXR                            PEFCXR                                    194933
     I              FMDI                            PEFMDI                                    194933
     I              FACO                            PEFACO                                    194933
     I              BRTE                            PEBRTE                                    194933
     I              SPIN                            PESPIN                                    194933
     I              WTIN                            PEWTIN                                    194933
     I              FCUS                            PEFCUS                                    194933
     I              FTYP                            PEFTYP                                    194933
     I              FSEQ                            PEFSEQ                                    194933
     I              INTC                            PEINTC                                    194933
     I              NACD                            PENACD                                    194933
     I              FCLB                            PEFCLB                                    194933
     I              ORED                            PEORED                                    194933
     I              LCD                             PELCD                                     194933
     I              CHTP                            PECHTP                                    194933
     I              TNLU                            PETNLU                                    194933
     I              RSTM                            PERSTM                                    194933
     I              RONS                            PERONS                                    194933
     I              RIBN                            PERIBN                                    194933
     I              RIBA                            PERIBA                                    194933
     I              ROBN                            PEROBN                                    194933
     I              ROCN                            PEROCN                                    194933
     I              PSTM                            PEPSTM                                    194933
     I              PONS                            PEPONS                                    194933
     I              PIBN                            PEPIBN                                    194933
     I              PIBA                            PEPIBA                                    194933
     I              POBN                            PEPOBN                                    194933
     I              POCN                            PEPOCN                                    194933
     I              RCRN                            PERCRN                                    194933
     I              RCRA                            PERCRA                                    194933
     I              RVNO                            PERVNO                                    194933
     I              AWBN                            PEAWBN                                    194933
     I              AWBA                            PEAWBA                                    194933
     I              BENN                            PEBENN                                    194933
     I              BENA                            PEBENA                                    194933
     I              DTP1                            PEDTP1                                    194933
     I              DTP2                            PEDTP2                                    194933
     I              DTP3                            PEDTP3                                    194933
     I              DTP4                            PEDTP4                                    194933
     I              DCHG                            PEDCHG                                    194933
     I              BTB1                            PEBTB1                                    194933
     I              BTB2                            PEBTB2                                    194933
     I              BTB3                            PEBTB3                                    194933
     I              BTB4                            PEBTB4                                    194933
     I              BTB5                            PEBTB5                                    194933
     I              BTB6                            PEBTB6                                    194933
     I              CVMR                            PECVMR                                    194933
     I              FSRP                            PEFSRP                                    194933
     I              FSGN                            PEFSGN                                    194933
     I              FPRC                            PEFPRC                                    194933
     I              DFTP                            PEDFTP                                    194933
     I              DFST                            PEDFST                                    194933
     I              MNSG                            PEMNSG                                    194933
     I              GASS                            PEGASS                                    194933
     I              GPRT                            PEGPRT                                    194933
     I              IUSR                            PEIUSR                                    194933
     I              AUSR                            PEAUSR                                    194933
     I              XUSR                            PEXUSR                                    194933
     I              PCRF                            PEPCRF                                    194933
     I              PCOB                            PEPCOB                                    194933
     I              PDDI                            PEPDDI                                    194933
     I              PTDI                            PEPTDI                                    194933
     I              SCCY                            PESCCY                                    194933
     I              ICCY                            PEICCY                                    194933
     I              REPI                            PEREPI                                    194933
     I              CLAS                            PECLAS                                    CLE042
     I              DFCL                            PEDFCL                                    CLE042
      *
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     I/COPY ZSRSRC,ZSEDITZ2
      *
     IRUNDAT    E DSRUNDAT
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      ** File Information Data Structure for LE0455P1.
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ** File Information Data Structure for LE0455AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     I            DS
     I                                        1   3 RBRCD
     I                                        1   3 WPBRCA
      *
     IDSSDY     E DSDSSDY
      ** Long data structure for access objects
      *
     IDSFDY     E DSDSFDY
      ** Dummy Data Structures used by Access Programs.
      *
     ISDBANK    E DSSDBANKPD
      ** External data structures for Bank Details
      *
     ISDCURR    E DSSDCURRPD
      ** External data structures for Currency Details
      *
     ISDBRCH    E DSSDBRCHPD
      ** External data structures for branch details
      *
     ISDCUST    E DSSDCUSTPD
      ** External data structures for customer details
     I              QQDFAC                          DFACBK                                    CGL029
      *
     ISCSARD    E DSSCSARDPD                                                                  CLE031
      **  External data structure for Switchable Features                                     CLE031
      *                                                                                       CLE031
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ** Perform Initialisation.
      *
     C                     EXSR SRINIT
      *
      ** Perform Detail Processing.
      *
     C                     EXSR SRPROC
      *
      ** Terminate Program
      *
     C                     EXSR SRTERM
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:                                                       *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  AOBANKR0 Access Program                                      *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR                           *** SRINIT ***
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           *LIKE     DEFN BRCA      KBRCA
     C           *LIKE     DEFN LNRF      KLNRF
     C           *LIKE     DEFN VDAT      KVDAT
     C           *LIKE     DEFN LASN      KLASN
     C           *LIKE     DEFN LNRF      KLOAN                                               194933
     C           *LIKE     DEFN RCDT      KRTYP                                               194933
      *
      ** Keylist for loan amendments file
      *
     C           KLOAM1    KLIST
     C                     KFLD           KBRCA
     C                     KFLD           KLNRF
     C                     KFLD           KVDAT
      *                                                                                       194933
      ** Keylist for loan details                                                             194933
      *                                                                                       194933
     C           KLOAN1    KLIST                                                              194933
     C                     KFLD           KLOAN                                               194933
     C                     KFLD           KRTYP                                               194933
      *
      ** Initialise LDA field.
      *
     C           *LOCK     IN   LDA
     C                     MOVE *BLANK    DBFILE
     C                     MOVE *BLANK    DBKEY
     C                     Z-ADD*ZERO     DBASE
     C                     MOVEL'LE0455'  DBPGM
     C                     OUT  LDA
      *
      ** Call Access Program for Bank Details - Title, Run Date and
      ** Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Handle Database Error.
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBANKPD'DBFILE
     C                     MOVELPOPTN     DBKEY
     C                     Z-ADD1         DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *                                                                                       CLE031
      ** Check whether SAR CLE031 is installed or not.                                        CLE031
      *                                                                                       CLE031
     C                     CALL 'AOSARDR0'                                                    CLE031
     C                     PARM *BLANK    @RTCD   7                                           CLE031
     C                     PARM '*VERIFY' @OPTN   7                                           CLE031
     C                     PARM 'CLE031'  WSARD   6                                           CLE031
     C           SCSARD    PARM SCSARD    DSFDY                                               CLE031
      *                                                                                       CLE031
     C           @RTCD     IFEQ *BLANK                                                        CLE031
     C                     MOVE 'Y'       CLE031  1                                           CLE031
     C                     ELSE                                                               CLE031
     C                     MOVE 'N'       CLE031                                              CLE031
     C           @RTCD     IFNE '*NRF   '                                                     CLE031
     C           *LOCK     IN   LDA                                                           CLE031
     C                     MOVEL'LE0455  'DBPGM                                               CLE031
     C                     MOVEL'SCSARDPD'DBFILE                                              CLE031
     C                     Z-ADD7         DBASE                                               CLE031
     C                     MOVEL'CLE031'  DBKEY                                               CLE031
     C                     OUT  LDA                                                           CLE031
     C                     EXSR *PSSR                                                         CLE031
     C                     ENDIF                                                              CLE031
      *                                                                                       CLE031
     C                     ENDIF                                                              CLE031
      *
      ** Check System Date Format, DDMMYY (*IN98 off)
      **                           MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ** Access RUNDAT for multibranching indicator
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
     C           AGMBIN    IFEQ 'Y'
     C                     MOVE *ON       *IN37
     C                     ENDIF
      *
      ** RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ** Report Work fields.
      *
     C                     Z-ADD*ZERO     RQDLN1  30
     C                     Z-ADD*ZERO     DIFLN1  30
      *
     C                     MOVE 'Y'       WFIRST  1
     C                     MOVE *BLANK    WPBRCA  3
     C                     MOVE *BLANK    WPBRNM 30
     C                     MOVE *BLANK    WPRBRC  3
     C                     MOVE *BLANK    WPRBNM 30
     C**********           Z-ADD*ZERO     WPLNRF  60                                          CLE148
     C                     MOVEL*BLANK    WPLNRF  6                                           CLE148
     C                     Z-ADD*ZERO     RCOUNT  50
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRPROC
      *****************************************************************
      *                                                               *
      *  SRPROC - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:                                                       *
      *  SRCONV - Convert Amount                                      *
      *  SRAUDT - Produce Audit Report and End Program                *
      *  SRREPT - Process Report Lines                                *
      *  SRREND - Write End of Report                                 *
      *  SRUPTR - Update Trailer File                                 *
      *                                                               *
      *****************************************************************
      *
     C           SRPROC    BEGSR                           *** SRPROC ***
      *
      ** Access action file for multi-currency rollover record
      *
     C           *LOVAL    SETLLLEACT5L1             60
      *
     C                     READ LEACT5L1                 60
      *
      ** If End of Records, (*IN60), Perform: Audit Reporting (No
      ** Records).
      *
     C           *IN60     IFEQ *ON
     C                     EXSR SRAUDT
     C                     ENDIF
      *
      ** Process rollover records
      *
     C           *IN60     DOWEQ*OFF
      *
      ** Check loan amendments file for action type 'PI','RE','MR','PF' or 'PT'
      *
     C                     MOVE RABRCA    KBRCA
     C**********           Z-ADDRALNRF    KLNRF                                               CLE148
     C                     MOVELRALNRF    KLNRF                                               CLE148
     C                     Z-ADDRAVDAT    KVDAT
      *
     C           KLOAM1    SETLLLOAMSDKF
     C                     READ LOAMSDKF                 61
      *
      ** Process loan amendment with value date greater than multi-cur
      ** rency rollover date
      *
     C           *IN61     DOWEQ*OFF
     C           VDAT      ANDGTRAVDAT
     C           LNRF      ANDEQRALNRF
      *
     C           AMTP      IFEQ 'PI'
     C           AMTP      OREQ 'RE'
     C           AMTP      OREQ 'MR'
     C           AMTP      OREQ 'PF'
     C           AMTP      OREQ 'PT'
      *
     C           RARLCY    IFNE *BLANK
      *
      ** Convert old currency amount to new currency
      *
     C           CCY       IFNE RARLCY
      *
     C                     EXSR SRCONV
      *
     C                     ADD  1         RCOUNT
      *
     C           LNRF      IFEQ WPLNRF
     C********** WPLNRF    ANDNE*ZERO                                                         CLE148
     C           WPLNRF    ANDNE*BLANK                                                        CLE148
     C                     MOVE *ON       *IN30
     C                     ELSE
     C                     MOVE *OFF      *IN30
     C                     ENDIF
      *
      ** Save previous loan number
      *
     C**********           Z-ADDLNRF      WPLNRF                                              CLE148
     C                     MOVELLNRF      WPLNRF                                              CLE148
      *
      ** Process report lines
      *
     C                     EXSR SRREPT
      *
      ** Update loan amendments prior to the new currency
      *
     C                     MOVE RARLCY    CCY
     C                     Z-ADDWNPRAM    PRAM
     C                     Z-ADDWNINAM    INTA
     C                     Z-ADDWNWTAM    WTXA
     C                     Z-ADDWNFAAM    FEAM
     C                     Z-ADDWNTOAM    TAMT
     C                     UPDATLOAMSDKF
      *
      ** Update trailer file
      *
     C                     EXSR SRUPTR
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** Save previous branch
      *
     C           *IN37     IFEQ *ON
     C                     MOVELRBRCA     WPBRCA
     C                     MOVELRBRNM     WPBRNM
     C                     ENDIF
      *
      ** Read next loan amendment for the loan
      *
     C                     READ LOAMSDKF                 61
     C                     ENDDO
      *
      ** Read next multi-currency rollover event
      *
     C                     READ LEACT5L1                 60
      *
      ** Write trailer record for the branch
      *
     C           *IN60     IFEQ *ON
     C           *IN37     ANDEQ*ON
     C           WPBRCA    ANDNE*BLANK
     C                     EXSR SRREND
     C                     ENDIF
      *
     C                     ENDDO
      *
      ** Write '*** END OF REPORT ***'
      *
     C                     MOVE *OFF      *IN37
      *
      ** Write report trailer
      *
     C           RCOUNT    IFNE *ZERO
     C                     EXSR SRREND
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRCONV
      *****************************************************************
      *                                                               *
      *  SRCONV - Converts old currency amendment amount to new       *
      *           currency.                                           *
      *                                                               *
      *  Called By: SRPROC subroutine                                 *
      *                                                               *
      *  Calls:                                                       *
      *  AOCURRR0 Access Object                                       *
      *  ZXRATE   - Gets the cross exchange rate & mult/div ind.      *
      *  ZCONV    - Converts amount from 1 currency to another.       *
      *  SRDFLT   - Default Settlement Details.                       *                       194952
      *                                                               *
      *****************************************************************
      *
     C           SRCONV    BEGSR                           *** SRCONV ***
      *
      ** Initialise old and new amount work fields
      *
     C                     Z-ADD*ZERO     WOPRAM 130
     C                     Z-ADD*ZERO     WNPRAM 130
     C                     Z-ADD*ZERO     WOINAM 130
     C                     Z-ADD*ZERO     WNINAM 130
     C                     Z-ADD*ZERO     WOWTAM 130
     C                     Z-ADD*ZERO     WNWTAM 130
     C                     Z-ADD*ZERO     WOFAAM 130
     C                     Z-ADD*ZERO     WNFAAM 130
     C                     Z-ADD*ZERO     WOTOAM 130
     C                     Z-ADD*ZERO     WNTOAM 130
      *                                                                                       194933
      ** Get the old and new facility xrates.                                                 194933
      *                                                                                       194933
     C                     EXSR SRFXRT                                                        194933
      *
      ** Get old currency details
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM CCY       PCURR   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE
     C                     MOVELPCURR     DBKEY
     C                     MOVEL'SDCURRPD'DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           PRTCD     IFEQ *BLANK
     C**********           MOVE A6MDIN    ZMDI1   1                                           194933
     C**********           Z-ADDA6SPRT    ZRATE1 138                                          194933
     C                     Z-ADDA6NBDP    WNBDP1  10
     C                     ENDIF
      *
      ** Get new currency details
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM RARLCY    PCURR
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE
     C                     MOVELPCURR     DBKEY
     C                     MOVEL'SDCURRPD'DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           PRTCD     IFEQ *BLANK
     C**********           MOVE A6MDIN    ZMDI2   1                                           194933
     C**********           Z-ADDA6SPRT    ZRATE2 138                                          194933
     C                     Z-ADDA6NBDP    WNBDP2  10
     C                     ENDIF
      *
      ** Obtain cross exchange rate & cross multiply/divide indicator
      *
     C                     EXSR ZXRATE
      *
     C                     Z-ADDZRATEX    ZEXCH  138
     C                     MOVE ZMDIX     ZMD     1
     C                     MOVE CCY       ZCCYI   3
     C                     MOVE RARLCY    ZCCYO   3
     C                     Z-ADDWNBDP1    ZCDPI   10
     C                     Z-ADDWNBDP2    ZCDPO   10
      *
      ** Set up input fields for amount conversion (Principal Amount)
      *
     C           PRAM      IFNE *ZERO
     C                     Z-ADDPRAM      ZAMTCI 150
      *
     C                     EXSR ZCONV
      *
     C                     Z-ADDZAMTCO    WNPRAM    H
     C                     Z-ADDPRAM      WOPRAM
     C                     ENDIF
      *
      ** Set up input fields for amount conversion (Interest Amount)
      *
     C           INTA      IFNE *ZERO
     C                     Z-ADDINTA      ZAMTCI
      *
     C                     EXSR ZCONV
      *
     C                     Z-ADDZAMTCO    WNINAM    H
     C                     Z-ADDINTA      WOINAM
     C                     ENDIF
      *
      ** Set up input fields for amount conversion (With-holding tax)
      *
     C           WTXA      IFNE *ZERO
     C                     Z-ADDWTXA      ZAMTCI 150
      *
     C                     EXSR ZCONV
      *
     C                     Z-ADDZAMTCO    WNWTAM    H
     C                     Z-ADDWTXA      WOWTAM
     C                     ENDIF
      *
      ** Set up input fields for amount conversion (Fee amount)
      *
     C           FEAM      IFNE *ZERO
     C                     Z-ADDFEAM      ZAMTCI 150
      *
     C                     EXSR ZCONV
      *
     C                     Z-ADDZAMTCO    WNFAAM    H
     C                     Z-ADDFEAM      WOFAAM
     C                     ENDIF
      *
      ** Set up input fields for amount conversion (Total amount)
      *
     C           TAMT      IFNE *ZERO
     C                     Z-ADDTAMT      ZAMTCI 150
      *
     C                     EXSR ZCONV
      *
     C                     Z-ADDZAMTCO    WNTOAM    H
     C                     Z-ADDTAMT      WOTOAM
     C                     ENDIF
      *                                                                                       194952
      ** Default settlement details                                                           194952
      *                                                                                       194952
     C                     EXSR SRDFLT                                                        194952
      *
     C                     ENDSR
      *                                                                                       194952
      *****************************************************************                       194952
      /TITLE SR/SRDFLT                                                                        194952
      *****************************************************************                       194952
      *                                                               *                       194952
      *  SRDFLT - Default Settlement Details                          *                       194952
      *                                                               *                       194952
      *  Called By: SRCONV subroutine                                 *                       194952
      *                                                               *                       194952
      *  Calls: None                                                  *                       194952
      *                                                               *                       194952
      *****************************************************************                       194952
      *                                                                                       194952
     C           SRDFLT    BEGSR                           *** SRDFLT ***                     194952
      *                                                                                       194952
     C                     MOVE *BLANK    WDFPAY  1                                           194952
     C                     MOVE *BLANK    WDFRCV  1                                           194952
      *                                                                                       194952
     C                     SELEC                                                              194952
      *                                                                                       194952
     C           AMTP      WHEQ 'PI'                                                          194952
     C           PTYP      IFEQ 66                                                            194952
     C           PTYP      OREQ 67                                                            194952
     C           PTYP      OREQ 69                                                            194952
     C           PTYP      OREQ 72                                                            194952
     C                     MOVE 'Y'       WDFRCV                                              194952
     C                     ELSE                                                               194952
     C                     MOVE 'Y'       WDFPAY                                              194952
     C                     ENDIF                                                              194952
      *                                                                                       194952
     C           AMTP      WHEQ 'RE'                                                          194952
     C           AMTP      OREQ 'MR'                                                          194952
     C           PTYP      IFEQ 66                                                            194952
     C           PTYP      OREQ 69                                                            194952
     C           PTYP      OREQ 72                                                            194952
     C                     MOVE 'Y'       WDFPAY                                              194952
     C                     ELSE                                                               194952
     C                     MOVE 'Y'       WDFRCV                                              194952
     C                     ENDIF                                                              194952
      *                                                                                       194952
     C                     ENDSL                                                              194952
      *                                                                                       194952
      ** Default receipt settlement side from loans details                                   194952
      *                                                                                       194952
     C           WDFRCV    IFEQ 'Y'                                                           194952
      *                                                                                       194952
     C                     MOVELCLRSTM    RSTM                                                194952
     C                     MOVEL*BLANKS   SETP                                                240072
     C                     MOVELCLRSTM    SETP                                                240072
     C                     MOVELCLRONS    RONS                                                194952
     C                     MOVEL*BLANKS   OSAC                                                240072
     C                     MOVELCLRONS    OSAC                                                240072
     C                     MOVELCLROCN    ROCN                                                194952
     C                     MOVELCLROBN    ROBN                                                194952
     C                     MOVELCLRIBN    RIBN                                                194952
     C                     MOVELCLRIBA    RIBA                                                194952
      *                                                                                       CLE031
     C           CLE031    IFEQ 'Y'                                                           CLE031
     C                     MOVE CLSCCY    SCCY                                                CLE031
     C                     MOVE CLREXR    REXR                                                CLE031
     C                     MOVE CLREXI    REXI                                                CLE031
     C                     MOVE *BLANKS   PSCY                                                CLE031
     C                     MOVE *BLANKS   PEXR                                                CLE031
     C                     MOVE *BLANKS   PEXI                                                CLE031
     C                     ENDIF                                                              CLE031
      *                                                                                       194952
      ** Clear pay settlement side                                                            194952
      *                                                                                       194952
     C                     MOVE *BLANKS   PSTM                                                194952
     C                     MOVE *BLANKS   PONS                                                194952
     C                     MOVE *BLANKS   RVNO                                                194952
     C                     MOVE *BLANKS   CVMR                                                194952
     C                     MOVE *BLANKS   POCN                                                194952
     C                     MOVE *BLANKS   POBN                                                194952
     C                     MOVE *BLANKS   RCRN                                                194952
     C                     MOVE *BLANKS   RCRA                                                194952
     C                     MOVE *BLANKS   PIBN                                                194952
     C                     MOVE *BLANKS   PIBA                                                194952
     C                     MOVE *BLANKS   AWBN                                                194952
     C                     MOVE *BLANKS   AWBA                                                194952
     C                     MOVE *BLANKS   BENN                                                194952
     C                     MOVE *BLANKS   BENA                                                194952
     C                     MOVE *BLANKS   DTP1                                                194952
     C                     MOVE *BLANKS   DTP2                                                194952
     C                     MOVE *BLANKS   DTP3                                                194952
     C                     MOVE *BLANKS   DTP4                                                194952
     C                     MOVE *BLANKS   DCHG                                                194952
     C                     MOVE *BLANKS   BTB1                                                194952
     C                     MOVE *BLANKS   BTB2                                                194952
     C                     MOVE *BLANKS   BTB3                                                194952
     C                     MOVE *BLANKS   BTB4                                                194952
     C                     MOVE *BLANKS   BTB5                                                194952
     C                     MOVE *BLANKS   BTB6                                                194952
     C                     ENDIF                                                              194952
      *                                                                                       194952
      ** Default pay settlement side from loans details                                       194952
      *                                                                                       194952
     C           WDFPAY    IFEQ 'Y'                                                           194952
      *                                                                                       194952
     C                     MOVELCLPSTM    PSTM                                                194952
     C                     MOVEL*BLANKS   SETP                                                240072
     C                     MOVELCLPSTM    SETP                                                240072
     C                     MOVELCLPONS    PONS                                                194952
     C                     MOVEL*BLANKS   OSAC                                                240072
     C                     MOVELCLPONS    OSAC                                                240072
     C                     MOVELCLRVNO    RVNO                                                194952
     C                     MOVELCLCVMR    CVMR                                                194952
     C                     MOVELCLPOCN    POCN                                                194952
     C                     MOVELCLPOBN    POBN                                                194952
     C                     MOVELCLRCRN    RCRN                                                194952
     C                     MOVELCLRCRA    RCRA                                                194952
     C                     MOVELCLPIBN    PIBN                                                194952
     C                     MOVELCLPIBA    PIBA                                                194952
     C                     MOVELCLAWBN    AWBN                                                194952
     C                     MOVELCLAWBA    AWBA                                                194952
     C                     MOVELCLBENN    BENN                                                194952
     C                     MOVELCLBENA    BENA                                                194952
     C                     MOVELCLDTP1    DTP1                                                194952
     C                     MOVELCLDTP2    DTP2                                                194952
     C                     MOVELCLDTP3    DTP3                                                194952
     C                     MOVELCLDTP4    DTP4                                                194952
     C                     MOVELCLDCHG    DCHG                                                194952
     C                     MOVELCLBTB1    BTB1                                                194952
     C                     MOVELCLBTB2    BTB2                                                194952
     C                     MOVELCLBTB3    BTB3                                                194952
     C                     MOVELCLBTB4    BTB4                                                194952
     C                     MOVELCLBTB5    BTB5                                                194952
     C                     MOVELCLBTB6    BTB6                                                194952
      *
     C           CLE031    IFEQ 'Y'                                                           CLE031
     C                     MOVE CLPSCY    PSCY                                                CLE031
     C                     MOVE CLPEXR    PEXR                                                CLE031
     C                     MOVE CLPEXI    PEXI                                                CLE031
     C                     MOVE *BLANKS   SCCY                                                CLE031
     C                     MOVE *BLANKS   REXR                                                CLE031
     C                     MOVE *BLANKS   REXI                                                CLE031
     C                     ENDIF                                                              CLE031
      *                                                                                       194952
      ** Clear receipt settlement side                                                        194952
      *                                                                                       194952
     C                     MOVE *BLANKS   RSTM                                                194952
     C                     MOVE *BLANKS   RONS                                                194952
     C                     MOVE *BLANKS   ROCN                                                194952
     C                     MOVE *BLANKS   ROBN                                                194952
     C                     MOVE *BLANKS   RIBN                                                194952
     C                     MOVE *BLANKS   RIBA                                                194952
     C                     ENDIF                                                              194952
      *                                                                                       194952
     C                     ENDSR                                                              194952
      *                                                                                       194933
      *****************************************************************                       194933
      /TITLE SR/SRFXRT                                                                        194933
      *****************************************************************                       194933
      *                                                               *                       194933
      *  SRFXRT - Get Facility XRates                                 *                       194933
      *                                                               *                       194933
      *  Called By: SRCONV subroutine                                 *                       194933
      *                                                               *                       194933
      *  Calls: None                                                  *                       194933
      *                                                               *                       194933
      *****************************************************************                       194933
      *                                                                                       194933
     C           SRFXRT    BEGSR                           *** SRFXRT ***                     194933
      *                                                                                       194933
     C**********           Z-ADDLNRF      KLOAN                                        194933 CLE148
     C                     MOVELLNRF      KLOAN                                               CLE148
     C                     MOVE 'A'       KRTYP                                               194933
     C           KLOAN1    CHAINCLOAN                63                                       194933
      *                                                                                       194933
     C           *IN63     IFEQ *ON                                                           194933
     C           *LOCK     IN   LDA                                                           194933
     C                     MOVE 'CLOANCL 'DBFILE                                              194933
     C                     MOVELLNRF      DBKEY                                               194933
     C                     Z-ADD8         DBASE                                               194933
     C                     OUT  LDA                                                           194933
     C                     EXSR *PSSR                                                         194933
     C                     ENDIF                                                              194933
      *                                                                                       194933
     C           LNRF      CHAINPETEMPL0             63                                       194933
      *                                                                                       194933
     C           *IN63     IFEQ *ON                                                           194933
     C           *LOCK     IN   LDA                                                           194933
     C                     MOVE 'PETEMPL0'DBFILE                                              194933
     C                     MOVELLNRF      DBKEY                                               194933
     C                     Z-ADD9         DBASE                                               194933
     C                     OUT  LDA                                                           194933
     C                     EXSR *PSSR                                                         194933
     C                     ENDIF                                                              194933
      *                                                                                       194933
     C                     MOVE PEFMDI    ZMDI1   1                                           194933
     C                     Z-ADDPEFCXR    ZRATE1 138                                          194933
     C                     MOVE CLFMDI    ZMDI2   1                                           194933
     C                     Z-ADDCLFCXR    ZRATE2 138                                          194933
      *                                                                                       194933
     C                     ENDSR                                                              194933
      *
      *****************************************************************
      /TITLE SR/SRFRMT
      *****************************************************************
      *                                                               *
      *  SRFRMT - Format and output report details.                   *
      *                                                               *
      *  Called By: SRPROC subroutine                                 *
      *                                                               *
      *  Calls:                                                       *
      *  AOBRCHR0 Access Object                                       *
      *  AOCUSTR0 Access Object                                       *
      *  RCFP1    - Subroutine to register the P1 Printer File via    *
      *             RCF.                                              *
      *  ZDATE2   - Formats Date                                      *
      *  ZSEDIT   - Formats Amount                                    *
      *                                                               *
      *****************************************************************
      *
     C           SRFRMT    BEGSR                           *** SRFRMT ***
      *
      ** Format branch code
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM RABRCA    PBRCH   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE
     C                     MOVELPBRCH     DBKEY
     C                     MOVEL'SDBRCHPD'DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELA8BRCD    RBRCA
     C                     MOVELA8BRNM    RBRNM
      *
      ** Format Loan, Customer and Old/New currency
      *
     C                     MOVELLNRF      RLNRF
     C                     MOVELCNUM      RCNUM
      *
      ** Get customer name
      *
     C                     MOVELCNUM      PCUST
     C                     CALL 'AOCUSTR0'
     C                     PARM '*MSG    'PRTCD
     C                     PARM '*KEY    'POPTN
     C                     PARM           PCUST  10
     C                     PARM *BLANKS   PKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDCUSTPD'DBFILE
     C                     MOVELPCUST     DBKEY
     C                     Z-ADD5         DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBBCSSN    RCRNM
     C                     MOVELCCY       ROCCY
     C                     MOVELRARLCY    RNCCY
      *
      ** Format value date
      *
     C                     Z-ADDVDAT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    RVDAT
      *
      ** Format amendment type, old/new amount and auto-settle ind.
      *
     C                     MOVELAMTP      RATYP
      *
     C           REPT      IFNE 3
     C                     Z-ADDWOPRAM    ZFLD15
     C                     ELSE
     C                     Z-ADDWOTOAM    ZFLD15
     C                     ENDIF
     C                     Z-ADDWNBDP1    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    ROAMT
      *
     C           REPT      IFNE 3
     C                     Z-ADDWNPRAM    ZFLD15
     C                     ELSE
     C                     Z-ADDWNTOAM    ZFLD15
     C                     ENDIF
     C                     Z-ADDWNBDP2    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    RNAMT
      *
     C                     MOVE AUTO      RAUTO
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRREPT
      *****************************************************************
      *                                                               *
      *  SRREPT - Process Report Lines.                               *
      *                                                               *
      *  Called By: SRPROC subroutine                                 *
      *                                                               *
      *  Calls:                                                       *
      *  SRFRMT - Format Detail Record                                *
      *  SRPREC - Format and Write a Detail Record to the Report      *
      *                                                               *
      *****************************************************************
      *
     C           SRREPT    BEGSR                           *** SRREPT ***
      *
      ** Format detail record
      *
     C                     EXSR SRFRMT
      *
      ** Write out the record.
      *
     C                     EXSR SRPREC
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRPREC
      *****************************************************************
      *                                                               *
      *  SRPREC - Subroutine to Write a Detail record to the Report.  *
      *                                                               *
      *  Called By: SRREPT subroutine                                 *
      *                                                               *
      *  Calls:                                                       *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *  SRREND - Write Trailer                                       *
      *                                                               *
      *****************************************************************
      *
     C           SRPREC    BEGSR                           *** SRPREC ***
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
      *
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C           *IN37     OREQ *ON
     C           RABRCA    ANDNEWPBRCA
     C           WPBRCA    ANDNE*BLANK
      *
     C           WFIRST    IFEQ 'Y'
      *
      ** RCF Processing for P1 File.
      *
     C                     MOVE 'N'       WFIRST
     C                     EXSR RCFP1
     C                     ENDIF
      *
      ** Write trailer record for the branch
      *
     C           *IN37     IFEQ *ON
     C           RABRCA    ANDNEWPBRCA
     C           WPBRCA    ANDNE*BLANK
     C                     EXSR SRREND
     C                     ENDIF
      *
     C                     MOVE *OFF      *IN30
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ** Write out Detail Record.
      *
     C                     WRITEDETAIL
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRREND
      *****************************************************************
      *                                                               *
      *  SRREND - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: SRPROC and SRPREC subroutine                      *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRREND    BEGSR                           *** SRREND ***
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
      *
      ** Save current branch header narrative to workfield
      *
     C                     MOVELRBRCA     WPRBRC
     C                     MOVELRBRNM     WPRBNM
      *
      ** When there is a need to write the header before the branch
      ** trailer narrative, move the previous branch to header
      *
     C                     MOVELWPBRCA    RBRCA
     C                     MOVELWPBRNM    RBRNM
     C                     WRITEHEADP1
      *
      ** Move back the current branch header narrative
      *
     C                     MOVELWPRBRC    RBRCA
     C                     MOVELWPRBNM    RBRNM
     C                     ENDIF
      *
      ** Write out Total Format.
      *
     C                     WRITETRAILP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRUPTR
      *****************************************************************
      *                                                               *
      *  SRUPTR - Update Trailer File.                                *
      *                                                               *
      *  Called By: SRPROC subroutine                                 *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRUPTR    BEGSR                           *** SRUPTR ***
      *
     C           1         CHAINLOAMSZ1              62
      *
     C           *IN62     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'LOAMSZ1' DBFILE
     C                     MOVEL'*FIRST'  DBKEY
     C                     Z-ADD6         DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Subtract old amount
      *
     C                     Z-ADD*ZERO     ZZAMT
     C                     MOVE WOTOAM    ZZAMT
     C                     Z-ADDVRRF      ZZTOTI
     C                     Z-ADDVRRL      ZZTOTD
     C                     EXSR GLZSUB
     C                     Z-ADDZZTOTI    VRRF
     C                     Z-ADDZZTOTD    VRRL
     C                     Z-ADDVRIF      ZZTOTI
     C                     Z-ADDVRIL      ZZTOTD
     C                     EXSR GLZSUB
     C                     Z-ADDZZTOTI    VRIF
     C                     Z-ADDZZTOTD    VRIL
      *
      ** Add new amount
      *
     C                     Z-ADD*ZERO     ZZAMT
     C                     MOVE WNTOAM    ZZAMT
     C                     Z-ADDVRRF      ZZTOTI
     C                     Z-ADDVRRL      ZZTOTD
     C                     EXSR GLZADD
     C                     Z-ADDZZTOTI    VRRF
     C                     Z-ADDZZTOTD    VRRL
     C                     Z-ADDVRIF      ZZTOTI
     C                     Z-ADDVRIL      ZZTOTD
     C                     EXSR GLZADD
     C                     Z-ADDZZTOTI    VRIF
     C                     Z-ADDZZTOTD    VRIL
      *
     C                     UPDATLOAMSZ1F
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRTERM
      *****************************************************************
      *                                                               *
      *  SRTERM - Termination process                                 *
      *                                                               *
      *  Called By: Main processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRTERM    BEGSR                           *** SRTERM ***
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRAUDT
      *****************************************************************
      *                                                               *
      *  SRAUDT - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, SRPROC, *PSSR                    *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRAUDT    BEGSR                           *** SRAUDT ***
      *
      ** Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
      *
      ** If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ** Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ENDIF
     C                     ENDIF
      *
      ** End Program and Return.
      *
     C                     EXSR SRTERM
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ** Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     DUMP
     C                     EXSR SRAUDT
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ** Ensure Report Spool File recorded by RCF.
      *
     C                     OPEN LE0455P1
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM *BLANK    ZSERR   1
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ** Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  GLZADD - Subroutine to add an amount to the total            *
      *                                                               *
      *  Called by: SRTRAL - Update the trailer record                *
      *                                                               *
      *  Calls    : GLZSUM - Subr. to carry out the addition          *
      *                                                               *
      *****************************************************************
      *
     C           GLZADD    BEGSR                           ***  GLZADD ***
      *
     C                     Z-ADDZZAMT     ZZAMT  153     91-DEFINE ZZAMT
     C   91                GOTO ZZAEND                     AMT = 0.BYPASS
      *
      ** Split ZZAMT into integer and decimal fields
      *
     C                     Z-ADDZZAMT     ZZAMTI 150       Integer Field
     C                     MOVE ZZAMT     ZZAMTD  30       Decimal Field
      *
      ** Both ZZAMTI and ZZAMTD contain a 'SIGN' zone now
      *
     C                     EXSR GLZSUM
     C           ZZAEND    ENDSR                           ***  ZZAEND ***
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  GLZSUB - Subroutine to subtract an amount to the total       *
      *                                                               *
      *  Called by: SRTRAL - Update the trailer record                *
      *                                                               *
      *  Calls    : GLZADD - Subroutine to add an amount to the total *
      *                                                               *
      *****************************************************************
      *
     C           GLZSUB    BEGSR                           ***  GLZSUB ***
      *
      ** Processing. Just Reverse the 'SIGN' and add
      *
     C                     Z-SUBZZAMT     ZZAMT  153       Reverse 'SIGN'
     C                     EXSR GLZADD                       and 'ADD'
     C                     Z-SUBZZAMT     ZZAMT            Restore 'SIGN'
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  GLZSUM - Subroutine to carry out the additon for subroutine  *
      *                                                               *
      *  Called by: GLZADD - Subroutine to add an amount to the total *
      *                                                               *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           GLZSUM    BEGSR                           ***  GLZSUM ***
      *
     C                     Z-ADDZZTOTI    ZZTOTI 150       Define ZZTOTI
     C                     Z-ADDZZTOTD    ZZTOTD  30       Define ZZTOTD
     C                     SETOF                     919293
     C                     SETOF                     949599
      *
      ** Determine sign of ZZAMTI & D, 92 if neg.
      *
     C           ZZAMTI    COMP 0                      9293
     C   93      ZZAMTD    COMP 0                      9293
     C   93                GOTO ZZSEND                     Zero Bypass
      *
      ** Determine sign of ZZTOTI & D, 91 if neg.
      *
     C           ZZTOTI    COMP 0                      9193
     C   93      ZZTOTD    COMP 0                      9193
      *
      ** If ZZTOTAL is zero, return ZZAMOUNT.
      *
     C   93                Z-ADDZZAMTI    ZZTOTI
     C   93                Z-ADDZZAMTD    ZZTOTD
     C   93                GOTO ZZSEND                     Zero Bypass
      *
      ** If signs differ bypass overflow checks.
      *
     C   91N92
     CORN91 92             GOTO ZZOFPS
      *
     C           ZZAMTD    ADD  ZZTOTD    ZZWK2   40
     C           ZZWK2     COMP 999                  93    '93' = CARRY
     C  N93      ZZWK2     COMP -999                   93    Into Integer.
      *
     C   93N92   ZZAMTI    ADD  1         ZZWK3  150       ADD 'CARRY' +VE
     C      93 92ZZAMTI    SUB  1         ZZWK3            SUB 'CARRY' -VE
     C   93      ZZTOTI    ADD  ZZWK3     ZZWK3
     C  N93      ZZTOTI    ADD  ZZAMTI    ZZWK3
      *
      ** If the modulus of ZZWK3 is lt mod. ZZTOTI then O/F has occurred
      *
     C  N92      ZZWK3     COMP ZZTOTI                 99  -92 MEANS NOS.
     C   92      ZZWK3     COMP ZZTOTI               99     Negative
     C  N99                Z-ADDZZWK2     ZZTOTD
     C  N99                Z-ADDZZWK3     ZZTOTI
      *
      ** If O/F zeroise ZZAMT, ind '99' set and ZZTOT fields left intact
      *
     C   99                Z-ADD0         ZZAMT  153
     C                     GOTO ZZSEND
      *
      ** The 'SIGNS' are different
      *
     C           ZZOFPS    TAG                             ***  ZZOFPS ***
      *
      ** If ZZAMT was negative, make it pos. to comp with ZZTOT
      *
     C   92                Z-SUBZZAMTI    ZZAMTI 150
     C   92                Z-SUBZZAMTD    ZZAMTD  30
      *
      ** If ZZTOT was negative, make it pos. to comp with ZZAMT.
      *
     C   91                Z-SUBZZTOTI    ZZTOTI
     C   91                Z-SUBZZTOTD    ZZTOTD
      *
      ** Both ZZAMT and ZZTOT are noe positive
      *
     C           ZZTOTI    COMP ZZAMTI               93  95
     C   95      ZZTOTD    COMP ZZAMTD               93  95
      *
      ** If ZZTOT eq. ZZAMT return zero.
      *
     C   95                Z-ADD0         ZZTOTI
     C   95                Z-ADD0         ZZTOTD
     C   95                GOTO ZZSEND
      *
      ** If ZZTOT gt. ZZAMT.
      *
     C   93      ZZAMTD    COMP ZZTOTD               94
     C   93 94   ZZTOTI    SUB  1         ZZTOTI
     C   93 94   ZZTOTD    ADD  1000      ZZWK2
     C   93      ZZTOTI    SUB  ZZAMTI    ZZTOTI
     C   93 94   ZZWK2     SUB  ZZAMTD    ZZTOTD
     C   93N94   ZZTOTD    SUB  ZZAMTD    ZZTOTD
      *
      ** If ZZAMT gt. ZZTOT.
      *
     C  N93      ZZTOTD    COMP ZZAMTD               94
     C  N93 94   ZZAMTI    SUB  1         ZZWK3  150
     C  N93 94   ZZAMTD    ADD  1000      ZZWK2
     C  N93 94   ZZWK3     SUB  ZZTOTI    ZZTOTI
     C  N93N94   ZZAMTI    SUB  ZZTOTI    ZZTOTI
     C  N93 94   ZZWK2     SUB  ZZTOTD    ZZTOTD
     C  N93N94   ZZAMTD    SUB  ZZTOTD    ZZTOTD
      *
      ** Reverse sign of ZZTOT if larger I/P fields were negative
      *
     C                     SETOF                     94
     C   93 91
     CORN93 92             SETON                     94
     C   94                Z-SUBZZTOTI    ZZTOTI
     C   94                Z-SUBZZTOTD    ZZTOTD
      *
      ** Restore sign of ZZAMTI & ZZAMTD If It was reversed.
      *
     C   92                Z-SUBZZAMTI    ZZAMTI
     C   92                Z-SUBZZAMTD    ZZAMTD
     C           ZZSEND    TAG                             ***  ZZSEND ***
      *
      ** If ZZTOTD is zero, and ZZTOTI is neg. set up ZZNEGD.
      *
     C                     SETOF                     96
     C           ZZTOTD    COMP 0                        91
     C   91      ZZTOTI    COMP 0                      96
     C   96                MOVE '.000-'   ZZNEGD  5
     C                     ENDSR                             ** ENDSR **
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      /TITLE SR/ZXRATE
     C/COPY ZSRSRC,ZXRATEZ1
      /TITLE SR/ZCONV
     C/COPY ZSRSRC,ZCONVZ1
      /TITLE SR/ZDATE2
     C/COPY ZSRSRC,ZDATE2Z2
      /TITLE SR/ZSEDIT
     C/COPY ZSRSRC,ZSEDITZ3
** CPY@
(c) Finastra International Limited 2001
**  POWER - ARRAY OF POWERS FOR CURRENCY CONVERSION
0000001
0000010
0000100
0001000
0010000
0100000
1000000
** ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
** ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
** ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
