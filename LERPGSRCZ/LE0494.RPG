     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Dropped collateral records')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE0494 - Dropped Collateral Records                          *
      *                                                               *
      *  Function:  This program should be able to remove all         *
      *  (COB)      guarantee and collateral records on file linked   *
      *             to all Facilities which have been dropped from    *
      *             the file and linked to all loans which have been  *
      *             dropped from the Loans and Loans Maturity File.   *
      *                                                               *
      *  Called By: LEC0494 - Dropped Collateral Records              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR812812           Date 16Nov12               *
      *                 CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 199662             Date 20Dec01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CLE011             Date 07Nov00               *
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 147685             Date 03Mar99               *
      *                 BT1137             Date 15Sep98               *
      *                 134015             Date 03Mar99               *
      *                 127734             Date 08Dec97               *
      *                 CLE005 *Create     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR812812 - Guarantees not dropped. Reversed part of fix      *
      *             134015 so that reversed guarantee will be dropped *
      *             from the system after COB. Also, all expired      *
      *             gurantees will only be dropped when the facility  *
      *             or loan attached to it is also dropped.           *
      *             (Child: AR812813)                                 *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  199662 - Review date on guarantee is overwritten by review   *
      *           date of facility. Rename fields from FCLTYL1.       *
      *  CLE011 - Rate Fixing Days for Lending Rollover Processing    *
      *           Recompile                                           *
      *  147685 - Reversed records are being processed causing a File *
      *           Out of Balance in LE0960.                           *
      *  BT1137 - Only adjust 'live' trailer fields when processing   *
      *           live records, ie reci of 'D'.                       *
      *  134015 - Guarantees and Collateral should be dropped for     *
      *            closed facility                                    *
      *  127734 - Incorrect update of trailer causing an OOB in LE0960*
      *  CLE005 - Customer Lending Enhancements - Others (T3)         *
      *                                                               *
      *****************************************************************
     FLE0494AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
     FLE0494P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
     F***FCLTYL0*IF  E           K        DISK                            134015
     FFCLTYL1 IF  E           K        DISK                               134015
     FCLOAN   IF  E           K        DISK
     F            CLOANZ1F                          KIGNORE
     FMLOAN   IF  E           K        DISK
     F            CLOANHHF                          KRENAMEMLOANHHF
     F            CLOANCLF                          KRENAMEMLOANCLF
     F            CLOANCKF                          KRENAMEMLOANCKF
     F            CLOANZ1F                          KIGNORE
     FGTEESL1 UF  E           K        DISK         KINFSR *PSSR
     F            GTEESHHF                          KIGNORE
     F            GTEESZZF                          KIGNORE
     FGTEESZZ UF  E           K        DISK         KINFSR *PSSR
     F/COPY WNCPYSRC,LE0494F001
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   01  - Change in Facility/Loan Number                        *
      *   02  - Change in Customer                                    *
      *   03  - Change in Branch                                      *
      *   04  - Guarantee is for Facility, else for Loan              *
      *                                                               *
      *   89  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  INIT   - Program Initialisation                              *
      *  PROCES - Detail Processing                                   *
      *  REPORT - Process Report Lines                                *
      *  LEVEL  - Subroutine to Check Level Breaks                    *
      *  UPDATE - Subroutine to Update File                           *
      *                                                               *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *  GLZADD - Add an amount to the total                          *
      *  GLZSUB - Subtract an amount to the total                     *
      *  GLZSUB - Carry out the addition                              *
      *                                                               *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMYY/MMDDYY)  *
      *  ZFRPED - Edit an Amount                                      *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZFRPEDZ1
      /SPACE 3
     IFCLTYFMF                                                                                199662
      ** Rename fields which are similar to GTEESL1 fields.                                   199662
     I              RVDT                            FRVDT                                     199662
     I              RVFR                            FRVFR                                     199662
     I              RVDY                            FRVDY                                     199662
     I              ORED                            FORED                                     199662
     I              LCD                             FLCD                                      199662
     I              CHTP                            FCHTP                                     199662
     I              TNLU                            FTNLU                                     199662
     I              IUSR                            FIUSR                                     199662
     I              AUSR                            FAUSR                                     199662
     I              XUSR                            FXUSR                                     199662
      *                                                                                       199662
     ICLOANCKF
     I              NLRA                            NLRAX
      *
     IMLOANCKF
     I              NLRA                            NLRAY
      *                                                                   BT1137
     IGTEESGTF                                                            BT1137
     I              RECI                            RECI#G                BT1137
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      ***  File Information Data Structure for LE0494P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ***  File Information Data Structure for LE0494AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     IDSSDY     E DSDSSDY
      ***  Dummy Data Structures used by Access Programs.
      *
     IDSFDY     E DSDSFDY
      ***  Dummy Data Structures used by Access Programs
      *
     ISDBANK    E DSSDBANKPD
      ***  External data structures for Bank Details
      *
     ISDCURR    E DSSDCURRPD
      ***  External data structures for Currency Details
      *
     ISDBRCH    E DSSDBRCHPD
      ***  External data structures for Branch Details
      *
     I/COPY ZSRSRC,ZFRPEDZ2
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      ***  Output Audit Report and End Program.
      *
     C                     EXSR AUDIT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  AOBANKR0 Access Program                                      *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR                           ***  INIT  ***
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Facility Key.
      *
     C           FACKY     KLIST
     C                     KFLD           WBRCA   3                       134015
     C**********           KFLD           WCNUM   60                                          CSD027
     C                     KFLD           WCNUM   6                                           CSD027
     C                     KFLD           WFACT   30
     C                     KFLD           WFCNO   20
      *
      ***  Loan Key.
      *
     C           LONKY     KLIST
     C**********           KFLD           WLNRF   60                                          CLE148
     C                     KFLD           WLNRF   6                                           CLE148
     C                     KFLD           WRCDT   1
      *
     C                     MOVEL'A'       WRCDT
      *
      ***  Initialise LDA field.
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'LE0494'  DBPGM
     C                     OUT  LDA
      *
      ***  Call Access Program for Bank Details - Title, Run Date and
      ***  Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBANKPD'DBFILE
     C                     MOVEL'*FIRST  'DBKEY     P      ************
     C                     Z-ADD001       DBASE            * DBERR 01 *
     C                     OUT  LDA                        ************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Check System Date Format, DDMMYY (*IN98 off)
      ***                            MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      ***  RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ***  Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
     C/COPY WNCPYSRC,LE0494C002
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  REPORT - Process Report Lines                                *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR                           *** PROCES ***
      *
      ***  Read first record from File.
      *
     C                     READ GTEESL1                  89
      *
      ***  If End of Records, (*IN89), Perform: Audit Reporting (No
      ***  Records).
      *
     C           *IN89     IFEQ *ON
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  Do While not End of File.
      *
     C           *IN89     DOWEQ*OFF
      *
      ***  Process Report Lines.
      ***  only the non-reversed ones                                     127734
      *
     C***********RECI      IFNE '*'                                127734 147685
     C           RECI#G    IFNE '*'                                       147685
     C                     EXSR REPORT
     C                     ELSE                                                             AR812812
     C                     EXSR UPDATE                                                      AR812812
     C                     ENDIF                                          127734
      *
      ***  Read next record.
      *
     C                     READ GTEESL1                  89
      *
      ***  End of Do While not End of Valid Records.
      *
     C                     ENDDO
      *
      ***  Write final records and Totals to Report.
      *
     C           WOPEN     IFEQ 'Y'
     C                     EXSR WP1END
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/REPORT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  LEVEL  - Subroutine to Check Level Breaks                    *
      *  UPDATE - Subroutine to Update File                           *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *                                                               *
      *****************************************************************
      *
     C           REPORT    BEGSR                           *** REPORT ***
      *
      *****If*record is Facility, chain to FCLTYL0                        134015
      ***  If record is Facility, chain to FCLTYL1                        134015
      *
     C                     MOVELFCLN      WFCLN1  1
     C                     MOVE *OFF      *IN04
      *
     C********** WFCLN1    IFEQ 'F'                                                           CLE148
     C           WFCLN1    IFEQ ' '                                                           CLE148
     C                     MOVE *ON       *IN04
     C                     MOVE BRCA      WBRCA                           134015
     C                     MOVE CNUM      WCNUM
     C                     MOVE FCLN      WFCTY   50
     C                     MOVELWFCTY     WFACT
     C                     MOVE WFCTY     WFCNO
     C***********FACKY     CHAINFCLTYL0              89                   134015
     C           FACKY     CHAINFCLTYL1              89                   134015
     C                     ELSE
      *
      ***  Record is Loan, chain to CLOAN or MLOAN
      *
     C                     MOVE FCLN      WLNRF
     C           LONKY     CHAINCLOAN                89
      *
     C           *IN89     IFEQ *ON
     C           LONKY     CHAINMLOAN                89
     C                     ENDIF
      *
     C                     ENDIF
      *
      ***  If not found in any of the files
      *
     C           *IN89     IFEQ *ON
     C********** *IN89     OREQ *OFF                                                 134015 AR812812
     C********** WFCLN1    ANDEQ'F'                                                  134015 AR812812
     C********** RECI      ANDEQ'C'                                                  134015 AR812812
      *
      ***  Open Printer file.
      *
     C           WOPEN     IFEQ *BLANKS
     C                     OPEN LE0494P1
     C                     MOVE 'Y'       WOPEN   1
     C                     EXSR RCFP1
     C                     ENDIF
      *
     C                     EXSR LEVEL
     C                     EXSR UPDATE
      *
      ***  Move appropriate narrative for type of record.
      *
     C                     MOVE *BLANKS   PGSCI
      *
     C           GSCI      IFEQ 'G'
     C                     MOVEL'GUARANTE'PGSCI
     C                     MOVE 'E '      PGSCI
     C                     ENDIF
      *
     C           GSCI      IFEQ 'S'
     C                     MOVEL'SECURITY'PGSCI
     C                     ENDIF
      *
     C           GSCI      IFEQ 'C'
     C                     MOVEL'COLLATER'PGSCI
     C                     MOVE 'AL'      PGSCI
     C                     ENDIF
      *
      ***  Write out the record.
      *
     C                     EXSR WP1REC
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1REC
      *****************************************************************
      *                                                               *
      *  WP1REC - Subroutine to Write a Detail record to the Report.  *
      *                                                               *
      *  Called By: REPORT                                            *
      *  Calls:                                                       *
      *  AOCURRR0 Access Program                                      *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMYY/MMDDYY)  *
      *  ZFRPED - Edit an Amount                                      *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *                                                               *
      *****************************************************************
      *
     C           WP1REC    BEGSR                           *** WP1REC ***
      *
      ***  Format Dates.
      *
     C                     Z-ADDSTTD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    PSTTD
      *
     C                     Z-ADDEDDT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    PEDDT
      *
      ***  Format Amounts.
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*KEY    'WOPTN   7
     C                     PARM           CCY
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ***  Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDCURRPD'DBFILE
     C                     MOVELCCY       DBKEY     P      ************
     C                     Z-ADD002       DBASE            * DBERR 02 *
     C                     OUT  LDA                        ************
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     Z-ADDUAMT      ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT25    PUAMT
      *
     C                     Z-ADDTAMT      ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT25    PTAMT
      *
      ***  Move File fields to Print fields.
      *
     C                     MOVE GUSN      PGUSN
     C                     MOVE CCY       PCCY
     C                     Z-ADDNOUN      PNOUN
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C           *IN03     OREQ *ON
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Detail Record.
      *
     C                     WRITEDETAIL
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/LEVEL
      *****************************************************************
      *                                                               *
      *  LEVEL  - Subroutine to check level breaks.                   *
      *                                                               *
      *  Called By: REPORT                                            *
      *  Calls:                                                       *
      *  AOBRCHR0 Access Program                                      *
      *  WP1END - Write End of Report                                 *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *                                                               *
      *****************************************************************
      *
     C           LEVEL     BEGSR                           *** LEVEL  ***
      *
      ***  Change in Facility or Loan number
      *
     C           FCLN      IFEQ WPFCLN
     C                     MOVE *OFF      *IN01
     C                     ELSE
     C                     MOVE *ON       *IN01
     C                     MOVE FCLN      WPFCLN  6
     C                     MOVE FCLN      PLNRF
     C                     MOVE FCLN      PFCTY3  3
     C                     MOVEL'/'       PFCTY3
     C                     MOVE PFCTY3    PFCTY
     C                     MOVELFCLN      PFCTY4  4
     C                     MOVE PFCTY4    PFCTY3
     C                     MOVELPFCTY3    PFCTY
     C                     ENDIF
      *
      ***  Change in Customer.
      *
     C           CNUM      IFEQ WPCNUM
     C                     MOVE *OFF      *IN02
     C                     ELSE
     C                     MOVEA'11'      *IN,01
     C**********           MOVE CNUM      WPCNUM  60                                          CSD027
     C                     MOVE CNUM      WPCNUM  6                                           CSD027
     C                     MOVE CNUM      PCNUM
     C                     ENDIF
      *
      ***  Change in Branch.
      *
     C           BRCA      IFEQ WPBRCA
     C                     MOVE *OFF      *IN03
     C                     ELSE
     C                     MOVEA'111'     *IN,01
      *
      ***  Write End of Report.
      *
     C           WFIRST    IFEQ *BLANKS
     C                     MOVE 'Y'       WFIRST  1
     C                     ELSE
     C                     EXSR WP1END
     C                     ENDIF
      *
      ***  Get Branch details.
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*KEY    'WOPTN   7
     C                     PARM           BRCA
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
      ***  Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBRCHPD'DBFILE
     C                     MOVELBRCA      DBKEY     P      ************
     C                     Z-ADD003       DBASE            * DBERR 03 *
     C                     OUT  LDA                        ************
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVE BRCA      WPBRCA  3
     C                     MOVE BRCA      PBRCA
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/UPDATE
      *****************************************************************
      *                                                               *
      *  UPDATE - Subroutine to Update file.                          *
      *                                                               *
      *  Called By: REPORT                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           UPDATE    BEGSR                           *** UPDATE ***
      *
     C/COPY WNCPYSRC,LE0494C001
      ***  Delete GTEESL1 record
      *
     C                     DELETGTEESL1
      *
      ***  Increment Trailer fields.
      *
     C                     ADD  1         RCOUNT
     C           RECI#G    IFEQ 'D'                                       BT1137
     C                     ADD  1         LIVCNT  50                      BT1137
     C***********          ADD  TAMT      RAMNT  200                      127734
     C                     ADD  UAMT      RAMNT  200                      127734
     C                     ENDIF                                          BT1137
      *
     C/COPY WNCPYSRC,LE0494C003
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: PROCES, LEVEL                                     *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           WP1END    BEGSR                           *** WP1END ***
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Total Format.
      *
     C                     WRITETRAILP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, PROCES, *PSSR                    *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ***  Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
     C                     WRITECONTROL
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ENDIF
      *
      ***  Update Trailer file.
      *
     C                     READ GTEESZZ                  89
      *
      ***  Handle Database Error.
      *
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVE 'GTEESZZ 'DBFILE
     C                     MOVEL'*FIRST  'DBKEY     P      ************
     C                     Z-ADD004       DBASE            * DBERR 04 *
     C                     OUT  LDA                        ************
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     SUB  RCOUNT    NORE
     C***********          SUB  RCOUNT    NLRB                            BT1137
     C***********          SUB  RCOUNT    NLRA                      127734BT1137
     C                     SUB  LIVCNT    NLRB                            BT1137
     C                     SUB  LIVCNT    NLRA                            BT1137
     C           RAMNT     DIV  1000      ZZAMT
     C                     Z-ADDVLBF      ZZTOTI
     C                     Z-ADDVLBL      ZZTOTD
     C                     EXSR GLZSUB
     C                     Z-ADDZZTOTI    VLBF
     C                     Z-ADDZZTOTD    VLBL
     C           RAMNT     DIV  1000      ZZAMT                           127734
     C                     Z-ADDVLAF      ZZTOTI                          127734
     C                     Z-ADDVLAL      ZZTOTD                          127734
     C                     EXSR GLZSUB                                    127734
     C                     Z-ADDZZTOTI    VLAF                            127734
     C                     Z-ADDZZTOTD    VLAL                            127734
     C                     UPDATGTEESZZF
     C                     ENDIF
      *
      ***  End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM *BLANKS   SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/GLZADD
      *****************************************************************
      *                                                               *
      *  GLZADD  -  Add an amount to the total                        *
      *                                                               *
      *  Called by: GLZSUB                                            *
      *  Calls    :                                                   *
      *  GLZSUM  -  Carry out the additon for subroutine              *
      *                                                               *
      *****************************************************************
      *
     C           GLZADD    BEGSR                                       ***
      *
     C           ZZAMT     IFNE 0
      *
      ** Split ZZAMT into integer and decimal fields
      *
     C                     Z-ADDZZAMT     ZZAMTI 150       Integer
     C                     MOVE ZZAMT     ZZAMTD  30       Decimal
      *
      ** Both ZZAMTI and ZZAMTD contain a 'sign' zone now
      *
     C                     EXSR GLZSUM
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/GLZSUB
      *****************************************************************
      *                                                               *
      *  GLZSUB  -  Subroutine to subtract an amount to the total     *
      *                                                               *
      *  Called by: AUDIT                                             *
      *  Calls    :                                                   *
      *  GLZADD  -  Add an amount to the total                        *
      *                                                               *
      *****************************************************************
      *
     C           GLZSUB    BEGSR
      *
      ** Processing - Just reverse the 'sign' and add
      *
     C                     Z-SUBZZAMT     ZZAMT  153       Reverse 'sign'
     C                     EXSR GLZADD                       and 'add'
     C                     Z-SUBZZAMT     ZZAMT            Restore 'sign'
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/GLZSUM
      *****************************************************************
      *                                                               *
      *  GLZSUM  -  Carry out the additon for subroutine              *
      *                                                               *
      *  Called by: GLZADD                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           GLZSUM    BEGSR
      *
     C                     Z-ADDZZTOTI    ZZTOTI 150       DEFINE ZZTOTI
     C                     Z-ADDZZTOTD    ZZTOTD  30       DEFINE ZZTOTD
     C                     SETOF                     919293
     C                     SETOF                     949599
      *
      ** Determine sign of ZZAMTI & D,  92 if neg
      *
     C           ZZAMTI    COMP 0                      9293
     C   93      ZZAMTD    COMP 0                      9293
     C   93                GOTO ZZSEND                     ZERO BYPASS
      *
      ** Determine sign of ZZTOTI & D, 91 if neg
      *
     C           ZZTOTI    COMP 0                      9193
     C   93      ZZTOTD    COMP 0                      9193
      *
      ** If ZZTOTAL is zero, return ZZAMOUNT
      *
     C   93                Z-ADDZZAMTI    ZZTOTI
     C   93                Z-ADDZZAMTD    ZZTOTD
     C   93                GOTO ZZSEND                     ZERO BYPASS
      *
      ** If signs differ, bypass overflow checks
      *
     C   91N92
     CORN91 92             GOTO ZZOFPS
      *
     C           ZZAMTD    ADD  ZZTOTD    ZZWK2   40
     C           ZZWK2     COMP 999                  93    '93' = CARRY
     C  N93      ZZWK2     COMP -999                   93    INTO INTEGER.
      *
     C   93N92   ZZAMTI    ADD  1         ZZWK3  150       ADD 'CARRY' +VE
     C      93 92ZZAMTI    SUB  1         ZZWK3            SUB 'CARRY' -VE
     C   93      ZZTOTI    ADD  ZZWK3     ZZWK3
     C  N93      ZZTOTI    ADD  ZZAMTI    ZZWK3
      *
      ** If the modulus of ZZWK3 is lt mod. ZZTOTI then O/F has occured
      *
     C  N92      ZZWK3     COMP ZZTOTI                 99  -92 MEANS NOS.
     C   92      ZZWK3     COMP ZZTOTI               99     NEGATIVE
     C  N99                Z-ADDZZWK2     ZZTOTD
     C  N99                Z-ADDZZWK3     ZZTOTI
      *
      ** If O/F zeroise ZZAMT, Ind '99' set and ZZTOT fields left intact
      *
     C   99                Z-ADD0         ZZAMT  153
     C                     GOTO ZZSEND
      *
      ** The 'signs' are different
      *
     C           ZZOFPS    TAG                             ***  ZZOFPS ***
      *
      * If ZZAMT was negative, make it pos. to como with ZZTOT
      *
     C   92                Z-SUBZZAMTI    ZZAMTI 150
     C   92                Z-SUBZZAMTD    ZZAMTD  30
      *
      ** If ZZTOT was negative, make it pos. to comp with ZZAMT.
      *
     C   91                Z-SUBZZTOTI    ZZTOTI
     C   91                Z-SUBZZTOTD    ZZTOTD
      *
      ** Both ZZAMT and ZZTOT are now positive
      *
     C           ZZTOTI    COMP ZZAMTI               93  95
     C   95      ZZTOTD    COMP ZZAMTD               93  95
      *
      ** If ZZTOT eq. ZZAMT return ZERO.
      *
     C   95                Z-ADD0         ZZTOTI
     C   95                Z-ADD0         ZZTOTD
     C   95                GOTO ZZSEND
      *
      ** If ZZTOT gt. ZZAMT.
      *
     C   93      ZZAMTD    COMP ZZTOTD               94
     C   93 94   ZZTOTI    SUB  1         ZZTOTI
     C   93 94   ZZTOTD    ADD  1000      ZZWK2
     C   93      ZZTOTI    SUB  ZZAMTI    ZZTOTI
     C   93 94   ZZWK2     SUB  ZZAMTD    ZZTOTD
     C   93N94   ZZTOTD    SUB  ZZAMTD    ZZTOTD
      *
      ** If ZZAMT gt. ZZTOT.
      *
     C  N93      ZZTOTD    COMP ZZAMTD               94
     C  N93 94   ZZAMTI    SUB  1         ZZWK3  150
     C  N93 94   ZZAMTD    ADD  1000      ZZWK2
     C  N93 94   ZZWK3     SUB  ZZTOTI    ZZTOTI
     C  N93N94   ZZAMTI    SUB  ZZTOTI    ZZTOTI
     C  N93 94   ZZWK2     SUB  ZZTOTD    ZZTOTD
     C  N93N94   ZZAMTD    SUB  ZZTOTD    ZZTOTD
      *
      ** Reverse sign of ZZTOT if larger I/P fields were negative
      *
     C                     SETOF                     94
     C   93 91
     CORN93 92             SETON                     94
     C   94                Z-SUBZZTOTI    ZZTOTI
     C   94                Z-SUBZZTOTD    ZZTOTD
      *
      ** Restore sign of ZZAMTI & ZZAMTD if it was reversed
      *
     C   92                Z-SUBZZAMTI    ZZAMTI
     C   92                Z-SUBZZAMTD    ZZAMTD
     C           ZZSEND    TAG
      *
      **   If ZZTOTD is zero, and ZZTOTI is neg. set up ZZNEGD.
      *
     C                     SETOF                     96
     C           ZZTOTD    COMP 0                        91
     C   91      ZZTOTI    COMP 0                      96
     C   96                MOVE '.000-'   ZZNEGD  5
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/ZDATE2
     C/COPY ZSRSRC,ZDATE2Z2
      /TITLE SR/ZFRPED
     C/COPY ZSRSRC,ZFRPEDZ3
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
