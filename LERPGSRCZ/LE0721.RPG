     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Leo LE Facility Availability Update')            *
      *****************************************************************
      *                                                               *
      *  Midas - LEO/Midas Lending Integration                        *
      *                                                               *
      *  LE0721 - LEO LE Facility Availability Update                 *
      *                                                               *
      *  Function: This program allows the file FACEXLC to be         *
      *  (COB)     updated with parameters passed to it.              *
      *                                                               *
      *  Called By: RPG/CMIU45                                        *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Last Amend No. CLE124             Date 02Nov21               *
      *  Prev Amend No. CLE123             Date 02Nov21               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01473             Date 23AUG94               *
      *                 999999             Date  99XXX99              *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CLE124 - Commitment and Outstanding Report by Customer Name  *
      *  CLE123 - Commitment and Outstanding Report by Account Officer*
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *     CLE042 - Extended Loan Sub Type                           *
      *     S01473 - New program created for Midas/Leo Integration    *
      *                                                               *
      *****************************************************************
     F/EJECT
     FFACEXLC O   E                    DISK
      *
     F/EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*    01  - Currency array set up                                *
     F*                                                               *
     F*****************************************************************
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  INIT   - Program Initialisation                              *
     F*  UPDATE - Update File                                         *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E                    CPY@    1   1 80               COPYRIGHT
      *
     I/EJECT
     I*                                                                                       CLE123
     ISCSARD    E DSSCSARDPD                                                                  CLE123
      **  Switchable Features File                                                            CLE123
     IDSFDY     E DSDSFDY                                                                     CLE123
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                                    CLE123
     I*                                                                                       CLE123
     I*DETL********DS*****************************80                                          CLE042
     I*DETL********DS*****************************84                                   CLE042 CLE123
     IDETL        DS                            206                                           CLE123
      ** DS for parameters DETL passed to program
     I                                        1   1 @RECI
     I                                        2   4 @BRCA
     I                                        5  10 @ZZ006
     I**********                             11  160@FCUS                                     CSD027
     I                                       11  16 @FCUS                                     CSD027
     I                                       17  220@CFNO
     I                                       23  23 @RCDT
     I                                       24  26 @CCY
     I                                    P  27  330@AMNT
     I                                       34  35 @CRSK
     I                                       36  37 @CLOC
     I                                    P  38  400@MDAT
     I                                       41  43 @FCLB
     I                                       44  59 @ZZ016
     I                                       60  62 @CMPYM
     I                                       63  640@PTYP
     I                                       65  66 @LTYP
     I                                       67  68 @SUTP
     I                                       69  80 @LCREF
     I                                       81  84 @CLAS                                     CLE042
     I                                       85  99 @PCRF                                     244459
     I                                      100 100 @LPFI                                     244459
     I                                      101 101 @INOI                                     244459
     I                                      102 103 @ANACC                                    CLE123
     I                                      104 133 @ANACN                                    CLE123
     I                                      134 136 @ANDPD                                    CLE123
     I                                      137 166 @ANDPM                                    CLE123
     I                                      167 186 @ANCRM                                    CLE123
     I                                      187 206 @CRNM                                     CLE124
     I*
     ILDA       E DSLDA
      ** Database error Data Structure
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ***  Perform Update File  Processing.
      *
     C                     EXSR UPDATE
      *
      ***  End Program.
      *
     C                     SETON                     LR
     C                     RETRN
      *
      *****************************************************************
      /TITLE SR/INIT
      *****************************************************************
      *                                                               *
      *  INIT   - SUBROUTINE TO DO PROGRAM INITIALISATION.            *
      *                                                               *
      *  Called By: MAIN PROCESSING                                   *
      *                                                               *
      *  Calls:     NONE                                              *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           RTCD    7        RETURN CODE
     C                     PARM           DETL             UPDAT DETAIL
     C*
     C** Set up copyright statement
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C** Define LDA
     C*
     C           *NAMVAR   DEFN           LDA
      *                                                                                       CLE123
      ** Check if CLE123 is switched *ON                                                      CLE123
      *                                                                                       CLE123
     C                     MOVE 'N'       CLE123  1                                           CLE123
     C                     CALL 'AOSARDR0'                                                    CLE123
     C                     PARM *BLANKS   WRTCD   7                                           CLE123
     C                     PARM '*VERIFY' WOPTN   7                                           CLE123
     C                     PARM 'CLE123'  WSARD   6                                           CLE123
     C           SCSARD    PARM SCSARD    DSFDY                                               CLE123
      *                                                                                       CLE123
     C           WRTCD     IFEQ *BLANKS                                                       CLE123
     C                     MOVE 'Y'       CLE123                                              CLE123
     C                     ENDIF                                                              CLE123
      *                                                                                       CLE123
      ** Check if CLE124 is switched *ON                                                      CLE124
      *                                                                                       CLE124
     C                     MOVE 'N'       CLE124  1                                           CLE124
     C                     CALL 'AOSARDR0'                                                    CLE124
     C                     PARM *BLANKS   WRTCD   7                                           CLE124
     C                     PARM '*VERIFY' WOPTN   7                                           CLE124
     C                     PARM 'CLE124'  WSARD   6                                           CLE124
     C           SCSARD    PARM SCSARD    DSFDY                                               CLE123
      *                                                                                       CLE124
     C           WRTCD     IFEQ *BLANKS                                                       CLE124
     C                     MOVE 'Y'       CLE124                                              CLE124
     C                     ENDIF                                                              CLE124
     C*
     C** Initialise update fields
     C*
     C                     MOVE *BLANKS   RECI
     C                     MOVE *BLANKS   BRCA
     C                     MOVE *BLANKS   ZZ006
     C**********           Z-ADD0         FCUS                                                CSD027
     C                     MOVE *BLANKS   FCUS                                                CSD027
     C                     Z-ADD0         CFNO
     C                     MOVE *BLANKS   RCDT
     C                     MOVE *BLANKS   CCY
     C                     Z-ADD0         AMNT
     C                     MOVE *BLANKS   CRSK
     C                     MOVE *BLANKS   CLOC
     C                     Z-ADD0         MDAT
     C                     MOVE *BLANKS   FCLB
     C                     MOVE *BLANKS   ZZ016
     C                     MOVE *BLANKS   CMPYM
     C                     Z-ADD0         PTYP
     C                     MOVE *BLANKS   LTYP
     C                     MOVE *BLANKS   SUTP
     C                     MOVE *BLANKS   CLAS                                                CLE042
     C                     MOVE *BLANKS   LCREF
     C                     MOVE *BLANKS   PCRF                                                244459
     C                     MOVE *BLANKS   LPFI                                                244459
     C                     MOVE *BLANKS   INOI                                                244459
      *                                                                                       CLE123
     C           CLE123    IFEQ 'Y'                                                           CLE123
     C                     MOVE *BLANKS   ANACOC                                              CLE123
     C                     MOVE *BLANKS   ANACON                                              CLE123
     C                     MOVE *BLANKS   ANDPCD                                              CLE123
     C                     MOVE *BLANKS   ANDPNM                                              CLE123
     C                     MOVE *BLANKS   ANCRNM                                              CLE123
     C                     ENDIF                                                              CLE123
      *                                                                                       CLE124
     C           CLE124    IFEQ 'Y'                                                           CLE124
     C                     MOVE *BLANKS   CRNM                                                CLE124
     C                     ENDIF                                                              CLE124
     C*
     C                     ENDSR
     C*
      *****************************************************************
      /TITLE SR/UPDATE
      *****************************************************************
      *                                                               *
      *  UPDATE - SUBROUTINE TO UPDATE FILE FACEXLC                   *
      *                                                               *
      *  Called By: MAIN PROCESSING                                   *
      *                                                               *
      *  Calls:     NONE                                              *
      *                                                               *
      *****************************************************************
      *
     C           UPDATE    BEGSR
      *
      ** Move input L/C parameters to output fields
      *
     C                     MOVE @RECI     RECI
     C                     MOVE @BRCA     BRCA
     C                     MOVE @ZZ006    ZZ006
     C                     MOVE @FCUS     FCUS
     C                     Z-ADD@CFNO     CFNO
     C                     MOVE @RCDT     RCDT
     C                     MOVE @CCY      CCY
     C                     Z-ADD@AMNT     AMNT
     C                     MOVE @CRSK     CRSK
     C                     MOVE @CLOC     CLOC
     C                     Z-ADD@MDAT     MDAT
     C                     MOVE @FCLB     FCLB
     C                     MOVE @ZZ016    ZZ016
     C                     MOVE @CMPYM    CMPYM
     C                     Z-ADD@PTYP     PTYP
     C                     MOVE @LTYP     LTYP
     C                     MOVE @SUTP     SUTP
     C                     MOVE @CLAS     CLAS                                                CLE042
     C                     MOVE @LCREF    LCREF
     C                     MOVE @PCRF     PCRF                                                244459
     C                     MOVE @LPFI     LPFI                                                244459
     C                     MOVE @INOI     INOI                                                244459
     C           CLE123    IFEQ 'Y'                                                           CLE123
     C                     MOVE @ANACC    ANACOC                                              CLE123
     C                     MOVE @ANACN    ANACON                                              CLE123
     C                     MOVE @ANDPD    ANDPCD                                              CLE123
     C                     MOVE @ANDPM    ANDPNM                                              CLE123
     C                     MOVE @ANCRM    ANCRNM                                              CLE123
     C                     ENDIF                                                              CLE123
      *                                                                                       CLE124
     C           CLE124    IFEQ 'Y'                                                           CLE124
     C                     MOVE @CRNM     CRNM                                                CLE124
     C                     ENDIF                                                              CLE124
     C*
     C** Update file FACEXLC
     C*
     C                     WRITEFACEXLCF               01
     C*
     C** If error setup return code
     C*
     C           *IN01     IFEQ '1'
     C                     MOVE '*ERROR'  RTCD
     C                     END
     C*
     C                     ENDSR
     C*
      *****************************************************************
      /EJECT
** CPY@
(c) Finastra International Limited 2001
