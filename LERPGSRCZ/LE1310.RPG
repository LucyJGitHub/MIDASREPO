     H        1
      *****************************************************************
/*S*D****RPGBASE*******************************************************                       CCB023
/*STD *  RPGSQL                                                       *                       CCB023
/*EXI *  TEXT('Midas LE Released transactions report')                *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE1310 - Midas LE Released Transactions Report               *
      *                                                               *
      *  Function:  This program will process all LE Released         *
      *  (I/C)      transactions performed during the day.            *
      *                                                               *
      *  Called By: LEC1310 - Released Transactions Print Control     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. AR1089839          Date 19Feb13               *
      *  Prev Amend No. CCB023             Date 06Aug12               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CLE030  *CREATE    Date 20Sep02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR1089839 - COBOP and 2.1 source clashes                     *
      *  CCB023 - COB Restructure - Input Cycle Termination           *
      *           Restructuring                                       *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      * CLE042 - Extended Loan Sub Type (Recompile)                 on*
      * CLE030 - Customer Lending Enhancements - Release Authorisation*
      *                                                               *
      *****************************************************************
     FLE1310AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
     FLE1310P1O   E                    PRINTER
     F                                              KINFDS SPOOL1
     F***LERELEL0IF**E           K        DISK         KINFSR *PSSR                           CCB023
     F/COPY WNCPYSRC,LE1310F001
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   37  - Multibranching ON                                     *
      *                                                               *
      *   89  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  INIT   - Program Initialisation                              *
      *  PROCES - Detail Processing                                   *
      *  REP1   - Process Released transactions                       *
      *                                                               *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMMYY)        *
      *  ZFRPED - Edit an Amount                                      *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     E/COPY WNCPYSRC,LE1310E001
      *
      ** Array containing Label for Audit Report
      *
     E                    TABTYP  1  13  2   TABDES 40   DESCRIPTION
      *
      ** Array containing Description of Unauthorised Loans/Parts
      *
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZFRPEDZ1
     E/COPY ZSRSRC,ZEDITZ1
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      ***  File Information Data Structure for MMNNNNPY.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ***  File Information Data Structure for MMNNNNAU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
     I                                    B 188 1890OFLLNU
     I                                    B 367 3680PRTLNU
      *
      ***  Dummy Data Structures used by Access Programs.
      *
     I***SDBANK*   E DSSDBANKPD                                                               CCB023
     ISDSBNK    E DSSDSBNKTD                                                                  CCB023
      ***  External data structures for Bank Details
      *
     ISDBRCH    E DSSDBRCHPD
      ***  External data structures for Branch Details
      *
     ISDCURR    E DSSDCURRPD
      ***  External data structures for Currency Details
      *
     IDSFDY     E DSDSFDY
      ***  Short data structures for Access Objects
      *
     IDSSDY     E DSDSSDY
      *                                                                                       CCB023
      ** External DS for LERELEPD                                                             CCB023
      *                                                                                       CCB023
     ILERELE    E DSLERELEL0                                                                  CCB023
      *                                                                                       CCB023
      ** Long data structure for access objects
     I/COPY WNCPYSRC,LE1310I001
      *
     I/COPY ZSRSRC,ZFRPEDZ2
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      ***  End Program.
      *
     C                     SETON                     LR
     C                     RETRN
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  AOBANKR0 Access Program                                      *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR                           ***  INIT  ***
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
     C                     PARM           PSEQ    5
      *
      ***  Initialise LDA field.
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'LE1310'  DBPGM
     C                     OUT  LDA
      *
      ***  Call Access Program for Bank Details - Title, Run Date and
      ***  Run Day Number.
      *
     C**********           CALL 'AOBANKR0'                                                    CCB023
     C**********           PARM '*MSG   ' PRTCD   7                                           CCB023
     C**********           PARM '*FIRST ' POPTN   7                                           CCB023
     C********** SDBANK    PARM SDBANK    DSFDY                                               CCB023
     C                     CALL 'AOSBNKR0'                                                    CCB023
     C                     PARM           PRTCD   7                                           CCB023
     C                     PARM '*FIRST ' POPTN   7                                           CCB023
     C           SDSBNK    PARM SDSBNK    DSFDY                                               CCB023
      *
      ***  Handle Database Error.
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C**********           MOVE 'SDBANKPD'DBFILE           ***************                    CCB023
     C                     MOVE 'SDSBNKTD'DBFILE                                              CCB023
     C                     Z-ADD001       DBASE            * DB ERROR 01 *
     C                     OUT  LDA                        ***************
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Check System Date Format, DDMMYY (*IN98 off)
      ***                            MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      ***  Check Mode to print a different title on the report
      ***  if it is not 'R', report runs automatically during EOD => Setof 10
      ***  if it is 'R', report runs on request during IC => Seton 10
      *
     C           MODE      IFEQ 'R'
     C                     SETON                     10
     C                     ELSE
     C                     SETOF                     10
     C                     ENDIF
      *
     C/COPY WNCPYSRC,LE1310C006
      *
      ***  RCF Processing for Printer File
      *
     C                     EXSR RCFP1
      *
      ***  RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ***  Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
      ***  Audit Work fields.
      *
     C                     Z-ADD0         RQDLNU  30
     C                     Z-ADD0         DIFLNU  30
      *
      ***  Program Work fields
      *
     C                     Z-ADD0         I       10
     C                     MOVE *OFF      HAUIND  1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  REP1   - Process Released transactions                       *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR                           *** PROCES ***
      *
      ***  Do While Not EOF LERELEL0
      *
     C/EXEC SQL                                                                               CCB023
     C+ Declare RCursor Insensitive scroll cursor for                                         CCB023
     C+ SELECT * FROM LERELEPD                                                                CCB023
     C+ ORDER BY RELNRF, RELCD, RETIME                                                        CCB023
     C/END-EXEC                                                                               CCB023
      *                                                                                       CCB023
     C/EXEC SQL                                                                               CCB023
     C+ Open RCursor                                                                          CCB023
     C/END-EXEC                                                                               CCB023
      *                                                                                       CCB023
     C                     Z-ADD1         I                *** Index
     C**********           MOVE *OFF      *IN89                                               CCB023
     C********** *IN89     DOWEQ*OFF                                                          CCB023
     C           SQLCOD    DOWEQ0                                                             CCB023
      *
     C**********           READ LERELEL0                 89                                   CCB023
      *                                                                                       CCB023
     C/EXEC SQL                                                                               CCB023
     C+ FETCH NEXT FROM RCursor INTO :LERELE                                                  CCB023
     C/END-EXEC                                                                               CCB023
      *
     C********** *IN89     IFEQ *OFF                                                          CCB023
     C           SQLCOD    IFEQ 0                                                             CCB023
      *
      ***  In AUDIT mode (i.e MODE # 'R') record are printed only if Last change date
      ***  is equal to the system Run date
      *
     C           *IN10     IFEQ *OFF
     C           RELCD     ANDEQBJRDNB
     C           *IN10     OREQ *ON
      *
      ***  Process Released transactions
      *
     C                     EXSR REP1
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDDO
      *                                                                                       CCB023
     C/EXEC SQL                                                                               CCB023
     C+ Close RCursor                                                                         CCB023
     C/END-EXEC                                                                               CCB023
      *
      ***  Process Audit Report
      *
     C                     EXSR AUDIT
      *
      ***  Write End of Report
      *
     C                     EXSR WP1END
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/REP1
      *****************************************************************
      *                                                               *
      *  REP1   - Process Released transactions                       *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMMYY)        *
      *  ZFRPED - Edit an Amount                                      *
      *                                                               *
      *****************************************************************
      *
     C           REP1      BEGSR                           *** REP1   ***
      *
      ***  Move File Fields to Report Fields
      *
     C                     MOVELRELNRF    PLNRF
     C                     MOVELRECCY     PCCY
      *
     C           PCCY      IFNE *BLANKS
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM PCCY      PK101   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ***  Handle Database Error.
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDCURRPD'DBFILE           ***************
     C                     MOVELPCCY      DBKEY            * DB ERROR 02 *
     C                     Z-ADD002       DBASE            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDIF
      *
      ***  Format Amount
      *
     C                     Z-ADDA6NBDP    ZDECS
     C                     Z-ADDRECPAM    ZFLD15
     C                     EXSR ZFRPED
     C                     MOVELZOUT25    PCPAM
      *
     C                     MOVELREXUSR    PXUSR
     C                     MOVELRERUSR    PRUSR
      *
      ***  Determine Description of Released transactions
      *
     C                     MOVE REPTYP    PTYP    2
     C           PTYP      LOKUPTABTYP    TABDES         11
     C           *IN11     IFEQ *ON
     C                     MOVE TABDES    PDESC
     C                     ELSE
      *
     C                     MOVE REAMTP    PTYP
     C           PTYP      LOKUPTABTYP    TABDES         11
     C           *IN11     IFEQ *ON
     C                     MOVE TABDES    PDESC
     C                     ELSE
     C                     MOVEL'NOT DEF' PDESC
      *
     C                     ENDIF
     C                     ENDIF
      *
      ***  Format Release Date
      *
     C                     Z-ADDRELCD     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    PLCD
      *
      ***  Format Release Time
      *
     C                     Z-ADDRETIME    PTIME
      *
      ***  Write out the record.
      *
     C                     EXSR WP1REC
      *
      ***  Increment Record Count
      *
     C                     ADD  1         RCOUNT  50
      *
      ***  Initialise Detail Record
      *
     C                     MOVE *BLANKS   PLNRF
     C                     MOVE *BLANKS   PDESC
     C                     MOVE *BLANKS   PCPAM
     C                     MOVE *BLANKS   PCCY
     C                     MOVE *BLANKS   PXUSR
     C                     MOVE *BLANKS   PRUSR
     C                     MOVE *BLANKS   PLCD
     C                     Z-ADD0         PTIME
      *
     C                     ENDSR
      *****************************************************************
     C/COPY WNCPYSRC,LE1310C002
      *****************************************************************
      /TITLE SR/WP1REC
      *****************************************************************
      *                                                               *
      *  WP1REC - Subroutine to Write Detail Record                   *
      *                                                               *
      *  Called By: REP1                                              *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           WP1REC    BEGSR                           *** WP1REC ***
      *
      ***  Write out Sub-Header If New Transaction
      *
     C           RCOUNT    IFEQ 0
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings.
      *
     C                     Z-ADD5         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
     C           I         IFEQ 1
     C                     WRITESUB1
     C                     ENDIF
      *
     C/COPY WNCPYSRC,LE1310C003
      *
     C                     ENDIF
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings and Sub Headings on a new page.
      *
     C                     Z-ADD2         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
      *
     C           I         IFEQ 1
     C                     WRITESUB1
     C                     ENDIF
      *
     C/COPY WNCPYSRC,LE1310C004
      *
     C                     ENDIF
      *
      ***  Write out Detail Record.
      *
     C           I         IFEQ 1
     C                     WRITEDTL1
     C                     ENDIF
      *
     C/COPY WNCPYSRC,LE1310C005
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           WP1END    BEGSR                           *** WP1END ***
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Trail Format.
      *
     C                     WRITETRAILP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report                   *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ***  Output Header and CLC Record
      *
     C           HAUIND    IFEQ *OFF
     C                     WRITEHEADAU
      *
     C           RCOUNT    IFNE 0
     C                     WRITECLC
     C                     ELSE
     C                     WRITENOT
     C                     END
      *
     C                     MOVE *ON       HAUIND
     C                     ENDIF
      *
      ***  Output File Controls.
      *
     C           RCOUNT    IFNE 0
     C                     WRITECONTROL
     C                     ENDIF
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD12        RQDLNU
     C           OFLLNU    SUB  PRTLNU    DIFLNU
     C           DIFLNU    IFLE RQDLNU
     C                     WRITEHEADAU
     C                     ENDIF
      *
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C                     ENDIF
      *
      ***  Initialize RCOUNT
      *
     C                     Z-ADD*ZERO     RCOUNT
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/ZDATE2
     C/COPY ZSRSRC,ZDATE2Z2
      /TITLE SR/ZFRPED
     C/COPY ZSRSRC,ZFRPEDZ3
     C/COPY ZSRSRC,ZEDITZ2
**  CPY@
(c) Misys International Banking Systems Ltd. 2002
     X/COPY WNCPYSRC,LE1310X001
**  DESC
61LOAN - CALL
62LOAN - TIME
63LOAN - DISCOUNTED
64PARTICIPATIONS PURCHASED - TIME
65PARTICIPATIONS PURCHASED - DISCOUNTED
66PARTICIPATIONS SOLD - TIME
67PARTICIPATIONS SOLD - DISCOUNTED
68PARTICIPATIONS PURCHASED - CALL
69PARTICIPATIONS SOLD - CALL
70GUARANTEE ISSUED/RISK LOAN - TIME
71PARTICIPATIONS PURCHASED - RISK
72PARTICIPATIONS SOLD - RISK
PIPRINCIPAL INCREASE
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
