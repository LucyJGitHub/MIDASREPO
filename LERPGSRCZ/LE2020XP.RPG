     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Facilities extension PF setup')               *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE2020XP - Facilities Extension File Setup                   *
      *                                                               *
      *  (c) Misys International Banking Systems, Ltd. 2006           *
      *                                                               *
      *  Last Amend No. LUC139             Date 07Jan21               *
      *  Prev Amend No. CER009  *CREATE    Date 16Aug06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian Returns upgrade to FM2.1                *
      *  CER009 - Puma II Upgrade to MidasPlus                        *
      *                                                               *
      *****************************************************************
     FFCLTYFM IF  E                    DISK
      ** Facilities master file
      *
     FLEFCT1L0UF  E           K        DISK                      A
      ** Extended Facilities file
      *
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** Program Start
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      A@CPY  80
      *
      ** Receive Parameter List
      *
     C           *ENTRY    PLIST
     C                     PARM           PPRTR   7
     C                     PARM           PMODE   1
      *
     C           *LIKE     DEFN CNUM      KCNUM
     C           *LIKE     DEFN FACT      KFACT
     C           *LIKE     DEFN FCNO      KFCNO
      *
     C           KAFCTY    KLIST
     C                     KFLD           KCNUM
     C                     KFLD           KFACT
     C                     KFLD           KFCNO
      *
      ** Take on, set up fields
      *
     C                     READ FCLTYFM                  31
     C           *IN31     DOWEQ*OFF
      *
     C                     MOVE CNUM      KCNUM
     C                     Z-ADDFACT      KFACT
     C                     Z-ADDFCNO      KFCNO
      *
     C           KAFCTY    CHAINLEFCT1L0             31
      *
      ** Set file field values to appropriate default
      *
     C           *IN31     IFEQ *ON
     C                     MOVE CNUM      PRCNUM
     C                     Z-ADDFACT      PRFACT
     C                     Z-ADDFCNO      PRFCNO
     C                     Z-ADD*ZERO     PRSBCL
     C                     MOVE *BLANKS   PROPPL
     C                     Z-ADD*ZERO     PREXPO
     C                     MOVE *BLANKS   PRDECD
     C                     MOVE *BLANKS   PRSACO
     C                     Z-ADD*ZERO     PRSAPE
      *
     C                     WRITELEFCT1D0
      *
     C                     ENDIF
      *
     C                     READ FCLTYFM                  31
     C                     ENDDO
      *
      ** End Program and Return
      *
     C                     MOVEL*ON       *INLR
     C                     RETRN
      *****************************************************************
** CPY@
(c) Misys International Banking Systems Ltd. 2006
