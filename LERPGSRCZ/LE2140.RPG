     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Manual facility utilisation')                 *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE2140 - Manual Facility Utilisation                         *
      *                                                               *
      *  Function:  This is a new program that could be run in any    *
      *             of the three (3) modes available, depending on    *
      *             the parameter passed to it.                       *
      *               1. Interactive ('I') - called from the Midas    *
      *                    Transaction Input menu                     *
      *               2. Record ('R') - Run interactively but updates *
      *                    are done to a new 'test harness' file      *
      *               3. Play ('P') - Run interactively but data is   *
      *                    read from the 'test harness' file          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. AR674226           Date 04Dec19               *
      *  Prev Amend No. CLE071             Date 18Jul18               *
      *                 MD046248           Date 27Oct17               *
      *                 MD020359           Date 07Mar14               *
      *                 CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      *                 CLE139             Date 06Dec10               *
      *                 CLE064             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 BUG23133B          Date 17Mar09               *
      *                 BUG23133A          Date 16Mar09               *
      *                 BUG23133           Date 05Mar09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CAS016             Date 28Feb06               *
      * Midas Plus 1.4 Base ------------------------------------------*
      *                 244314             Date 12Dec06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 240968             Date 10Jul06               *
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CAS011             Date 16May05               *
      *                 CAS009             Date 16May05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 CAP073             Date 08Aug02               *
      *                 CLE029             Date 04Sep02               *
      *                 CLE031             Date 04Feb03               *
      *                 CLE028             Date 27Jun02               *
      * Midas Release 4.01.03 ----------------------------------------*
      *                 190571             Date 13Jan03               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 197515             Date 09May02               *
      *                 204452             Date 03May02               *
      *                 203082             Date 25Apr02               *
      *                 204658             Date 25Apr02               *
      *                 204392             Date 25Apr02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CLE042             Date 06Sep05               *
      *                 CAP068             Date 25JUN02               *
      *                 203094             Date 18Feb02               *
      *                 CLE023             Date 19Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 195125             Date 11Jul01               *
      *                 191779             Date 02Jul01               *
      *                 194509             Date 19Jun01               *
      *                 CLE014             Date 20Mar01               *
      *                 189904             Date 05Jun01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 190570             Date 19Mar01               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CLE009             Date 03Jun99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 158845             Date 23Apr99               *
      *                 157050             Date 20Mar99               *
      *                 154799             Date 24Feb99               *
      *                 151295             Date 18Feb99               *
      *                 150746             Date 15Dec98               *
      *                 150215             Date 11Dec98               *
      *                 148360             Date 25Nov98               *
      *                 139313             Date 04Aug98               *
      *                 138870             Date 15Jul98               *
      *                 133916             Date 29Apr98               *
      *                 128953             Date 20Jan98               *
      *                 CLE005 *Create     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR674226 - Manual transaction reached 999. Increase the size *
      *             of utility sequence no. to accomodate 9999.       *
      *             (Child: AR674227)                                 *
      *           - Applied for MD054782.                             *
      *  CLE071 - Value Dating of Rate Changes to Fees (Recompile)    *
      *  MD046248 - Finastra Rebranding                               *
      *  MD020359 - Manual Facility Utilisation unable to proceed     *
      *             due to field exchange rate indicator              *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  CLE148 - Alpha Loan Reference                                *
      *  CLE139 - Lending Fee Capitalisation (Recompile)              *
      *  CLE064 - Enhancement of Lending Frequencies (Recompile)      *
      *  BUG23133B - Facility Exchange rate field is not picking value*
      *              from the system if left as BLANK. (Reopened)     *
      *  BUG23133A - Facility Exchange rate field is not picking value*
      *              from the system if left as BLANK. (Reopened)     *
      *  BUG23133 - Facility Exchange rate field is not picking value *
      *             from the system if left as BLANK.                 *
      *  CAS016 - IAS18 EIR Recalculation (Fee Amortisation Over      *
      *           Whole Period) -Recompile.                           *
      *  244314 - If system value CLAuthSameORED = 'Y', then allow    *
      *           original input user to re-authorise a transaction   *
      *           that was previously authorised and has been amended *
      *           by a second user.                                   *
      *  240968 - If system value CLAuthORED = 'Y', when authorising  *
      *           do not check insert user is different to authorise  *
      *           user if status is 'R'                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CAS011 - EIR/AC Accounting Upgrade to MP1                    *
      *  CAS009 - EIR/AC Accounting (Recompile)                       *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP073 - Lending API Enhancement - Facility Input (Recompile)*
      *  CLE029 - Addition of Severity code and Limit ID to FCLTYFM   *
      *  CLE031 - Lending Enhancements.                               *
      *  CLE028 - Flat Rate Personal Loans (Rule of 78s)              *
      *  190571 - Correct hash totalling to use absolute value (not   *
      *           negative amounts). MFUs should appear on LE0720P1.  *
      *  197515 - Incorrect allocation of utilisation sequence number.*
      *           Initialise WKRSQ key field to zero in SRUPUI.       *
      *  204452 - Recompiled due to changed PF/LEFEED.                *
      *  203082 - Correct validation of MFU amount for 'A'mend and    *
      *           'X'authorise. Do not let warning message LEU0046    *
      *           appear when facility available amount has not been  *
      *           exceeded.                                           *
      *  204658 - Current exposure updated for reversal even though   *
      *           expiry date is earlier than run date.               *
      *  204392 - Utilisation start date/expiry date should not be    *
      *           greater than facility expiry date.                  *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CAP068 - Recompiled for changes to FCLTYFM and FCLTYFN.      *
      *  203094 - Amount drawn array 1 not being updated when Manual  *
      *           Facility Utilisation was amended or deleted.        *
      *           The field UORED should not be used to compare for   *
      *           WRUND, use ORED instead.                            *
      *  CLE023 - Lending Facilty History Improvements.               *
      *  195125 - Record not changed after amendment of utilisation   *
      *  191779 - If MFU is in Credit Agreement ccy but Tranche ccy   *
      *           is different, and CA exchange rate and MFU exchange *
      *           rate are the same, use the screen amount rather     *
      *           than converting the amount twice.                   *
      *  194509 - Amended Transactions go to Re-auth and then complete*
      *  CLE014 - Customer Driven Lending Enhancements                *
      *  189904 - Drawn amounts (OAM fields in FCLTYFN) were not      *
      *           updated correctly because SRUPFA was being called   *
      *           before SRUPUI has set up the date fields. Amendment *
      *           to CLE009. (189094)                                 *
      *           Consider revolving credit indicator when updating   *
      *           facility availability.                              *
      *  190570 - MFU must either be in facility currency, or in one  *
      *           of the allowable currencies on the facility.        *
      *  CLE009 - LE I/C Swift Message Generation for Fees            *
      *  158845 - Only update facility exposure where the utilisation *
      *           expiry is not today or earlier.                     *
      *  157050 - Prevent update of previous utilisation amount as    *
      *           this corrupt the data for manual utilisation        *
      *           processing in COB.                                  *
      *  154799 - Recompiled due to changes in /COPY,ZACCH            *
      *  151295 - Problems authorising repayment schedules            *
      *           Recompiled due to changes in ZCHKH                  *
      *  150746 - Cannot blank out expiry date field once entered.    *
      *  150215 - Include drilldown for Manual Facility Utilisation.  *
      *  148360 - Could not enter a negative utilisation amount.      *
      *  139313 - Various fields incorrectly initialised in srinwd,   *
      *           causing zero values on file.                        *
      *  138870 - Could not amend narrative for facility utilisation  *
      *  133916 - It should not be possible to enter a manual facility*
      *           Utilization against a Credit Agreement Facility but *
      *           the system allows it.                               *
      *  128953 - Transaction should be validated against facility    *
      *  CLE005 - Customer Lending Enhancements - Others.             *
      *                                                               *
      *****************************************************************
     FLEMNFUL3IF  E           K        DISK         KINFSR *PSSR
     F            LEMNFUPF                          KRENAMELEMNFUP3
     FLEMNFUL4UF  E           K        DISK         KINFSR *PSSR A
     F            LEMNFUPF                          KRENAMELEMNFUP4
     F                                              KCOMIT
     FLEMNFUL5IF  E           K        DISK         KINFSR *PSSR
     F            LEMNFUPF                          KRENAMELEMNFUP5
     FLEMNFUL6IF  E           K        DISK         KINFSR *PSSR
     F            LEMNFUPF                          KRENAMELEMNFUP6
     FLEMNFUZZUF  E                    DISK         KINFSR *PSSR
     FLE2140DFCF  E                    WORKSTN                        UC
     F                                        SRRN  KSFILE LE2140S0
     FLE2140PDUF  E                    DISK         KINFSR *PSSR A    UC
     FFCLTY   UF  E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
     F            FCLTYHHF                          KIGNORE
     F            FCLTYZZF                          KIGNORE
     FLEFCAML7IF  E           K        DISK         KINFSR *PSSR
      *
     FLEFEEDL1UF  E           K        DISK         KINFSR *PSSR          CLE009
     F                                              KCOMIT                CLE009
      *                                                                   CLE009
      /SPACE 3
      *****************************************************************
      *                                                               *
      *    F U N C T I O N   O F   I N D I C A T O R S                *
      *                                                               *
      *  05 - Linked Enquiry                                          *   150215
      *    11           SFLCLR                                        *
      *    12           SFLDSPCTL                                     *
      *    13           Display 'Page Keys'                           *
      *    14           READE to LEFEEDL1                             *   CLE009
      *    15           LOKUP currency array                          *   CLE009
      *    16           LOKUP Allowable currencies array              *   190570
      *    19           SFLEND at Loan Subfile Control                *
      *    20           SFLNXTCHG                                     *
      *    21           ROLLUP                                        *
      *    24           Display sequence number allocated             *
      *    25           F9 pressed from subfile screen                *
      *    26           Authorisation - F11                           *
      *    27           Deletion - F10                                *
      *    29           Authorisation required                        *
      *    30           Invalid action code                           *
      *    31           Invalid customer number                       *
      *    32           Invalid facility type                         *
      *    33           Invalid facility number                       *
      *    34           Invalid sequence number                       *
      *    35           Invalid customer number - review from         *
      *    36           Invalid facility type   - review from         *
      *    37           Invalid facility number - review from         *
      *    38           Invalid authorisation value                   *
      *    41           Indicator for Narrative                       *   138870
      *    43           Invalid currency                              *
      *    44           Invalid amount                                *
      *    45           Invalid start date                            *
      *    46           Invalid expiry date                           *
      *    47           Invalid exchange rate                         *
      *    48           Invalid exchange rate indicator               *
      *    49           Invalid subfile selection code                *
      *    40           Protect detail fields except EXDT and UAMT    *
      *    50           Chain to FCLTY                                *
      *    51           Invalid utilisation amount sign               *   148360
      *    55           Protect EXDT and UAMT fields                  *
      *    79           Subfile read-change                           *
      *    80           End of file - LEMNFUL5/LEMNFUL6               *
      *    81           Chain to LEMNFUL3                             *
      *    83           End of test-harness file                      *
      *    85           End of file - LEMNFUL3                        *
      *    88           Chain to LEMNFUL4                             *
      *    86           Chain to LEMNFUZZ                             *
      *    KC           F3  pressed                                   *
      *    KL           F12 pressed                                   *
      *    KK           F11 pressed                                   *
      *    KJ           F10 pressed                                   *
      *    92           Divide indicator                              *
      *    98           MMDDYY format                                 *
      *    99           Error in ZALIGN                               *
      *    LR           End program processing                        *
      *                                                               *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
     E                    RUNS       10  5 0A
      ** Facility run dates
      *
     E                    POWR    1   7  7 3             CCY CONVERSIONS
     E                    OAM        10 13 0
      ** Facility drawn amounts
      *
     E                    CYA       150  3                                CLE009
      ** Currencies array                                                 CLE009
     E                    TND       150  1 0                              CLE009
      ** Telex Notice Days array                                          CLE009
      *                                                                   CLE009
     E/COPY ZSRSRC,ZALIGNZ1
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZHOLE
     E**********          DRAW        5  1                                             128953 CLE028
     E                    DRAW        6  1                                                    CLE028
      ** Work Array for Drawing Types                                     128953
      *
     E                    WCY         9  3                                190570
      ** Work Allowable currencies array                                  190570
      *
      /EJECT
     ILEMNFUP4
      ** Rename fields for manual utilisations update file.
     I              RECI                            URECI
     I              BRCA                            UBRCA
     I              CNUM                            UCNUM
     I              FACT                            UFACT
     I              FCNO                            UFCNO
     I              SQNO                            USQNO
     I              STDT                            USTDT
     I              EXDT                            UEXDT
     I              UCCY                            UUCCY
     I              UAMT                            UUAMT
     I              UXRT                            UUXRT
     I              UXID                            UUXID
     I              UNAR                            UUNAR
     I              UADT                            UUADT
     I              PUAM                            UPUAM
     I              USTS                            UUSTS
     I              IUSR                            UIUSR
     I              AUSR                            UAUSR
     I              XUSR                            UXUSR
     I              ORED                            UORED
     I              TLCD                            UTLCD
     I              CHTP                            UCHTP
     IFCLTYFMF
      ** Rename fields for facility file - A
      *
     I              RECI                            MRECI
     I              BRCA                            MBRCA
     I              CNUM                            MCNUM
     I              FACT                            MFACT
     I              FCNO                            MFCNO
     I              FCCY                            MFCCY
     I              IUSR                            MIUSR
     I              AUSR                            MAUSR
     I              XUSR                            MXUSR
     I              ORED                            MORED
     I              ORBR                            MORBR
     I              MCCY                            MMCCY
     I              CSEL                            MCSEL                 190570
     I              ALCY                            MALCY
     I              FAMT                            MFAMT
     I              CANM                            MCANM
     I              CAFT                            MCAFT
     I              CAFN                            MCAFN
     I              TRCA                            MTRCA
     I              CHTP                            MCHTP
     I              CACY                            MCACY
     I              CAXR                            MCAXR
     I              CMDI                            MCMDI
     I              DTAP                            MDTAP
     I              RVCR                            MRVCR                 CLE014
     IFCLTYFNF
      ** Rename fields for facility file - B
      *
     I              RECI                            NRECI
     I              CNUM                            NCNUM
     I              FACT                            NFACT
     I              FCNO                            NFCNO
      *
     I              STDT                            NSTDT
     I              ORED                            NORED
     I              CHTP                            NCHTP
     I              CAMD                            NCAMD
     I              CEXP                            NCEXP                                     CLE023
     ILEFCAMPF
      ** Rename fields for facility amendments file.
      *
     I              RECI                            ARECI
     I              BRCA                            ABRCA
     I              CNUM                            ACNUM
     I              FACT                            AFACT
     I              FCNO                            AFCNO
     I              SQNO                            ASQNO
     I              FATP                            AFATP
     I              AAMT                            AAAMT
     I              VLDT                            AVLDT
     I              RVCR                            ARVCR
     I              ASTS                            AASTS
     I              IUSR                            AIUSR
     I              AUSR                            AAUSR
     I              XUSR                            AXUSR
     I              ORED                            AORED
     I              TLCD                            ATLCD
     I              CHTP                            ACHTP
     I              PCRF                            APCRF
     I              PCOB                            APCOB
      *
     I            DS
     I                                        1   6 WDATE
     I                                        1   2 WDD
     I                                        3   4 WMM
     I                                        5   6 WYY
     I            DS
     I                                        1   6 WTIMEA
     I                                        1   2 WHH
     I                                        3   4 WNN
     I                                        5   6 WSS
     I            DS
      ** Data structure of rundates
     I                                    P   1   30RUN0
     I                                    P   4   60RUN1
     I                                    P   7   90RUN2
     I                                    P  10  120RUN3
     I                                    P  13  150RUN4
     I                                    P  16  180RUN5
     I                                    P  19  210RUN6
     I                                    P  22  240RUN7
     I                                    P  25  270RUN8
     I                                    P  28  300RUN9
     I                                    P   1  30 RUNS
     I            DS
      ** Data structure of drawn amounts
     I                                    P   1   70OAM1
     I                                    P   8  140OAM2
     I                                    P  15  210OAM3
     I                                    P  22  280OAM4
     I                                    P  29  350OAM5
     I                                    P  36  420OAM6
     I                                    P  43  490OAM7
     I                                    P  50  560OAM8
     I                                    P  57  630OAM9
     I                                    P  64  700OA10
     I                                    P   1  70 OAM
     IDMNFUT      DS
     I**********                              1   60WUCNO                                     CSD027
     I                                        1   6 WUCNO                                     CSD027
     I                                        7   90WUFTP
     I                                       10  110WUFNO
     I**********                             12  140WURSQ                                   AR674226
     I                                       12  150WURSQ                                   AR674226
     IDFCLTY      DS
     I**********                              1   60WKCNO                                     CSD027
     I                                        1   6 WKCNO                                     CSD027
     I                                        7   90WKFTP
     I                                       10  110WKFNO
     I                                        1  11 DMFUTB
     I                                       12  12 WKRCT
     IDMFUTA      DS
     I**********                              1   60CNUM                                      CSD027
     I                                        1   6 CNUM                                      CSD027
     I                                        7   90FACT
     I                                       10  110FCNO
     IRUNDAT    E DS
      ** Rundat data area for system information
      *
     IA@CPY       DS
      * Copyright array
     I                                        1  80 CPY@
      *
     ILDA         DS                            256
      ** Local data area for database error details
      *
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     IPSDS       SDS
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
      *
     I                                     *PROGRAM ##PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     IDREVW       DS
     I                                        1   6 SRCUS
     I                                        7   9 SRTYP
     I                                       10  11 SRNUM
     I                                       12  12 SRAUT
     IDSCRNA      DS
     I                                        1   3 SUCCY
     I                                        4  17 SUAMT
     I                                       18  23 SSTDT
     I                                       24  29 SEXDT
     I                                       30  41 SUXRT
     I                                       42  42 SUXID
     I                                       43  72 SUNAR
     I                                       73  73 SAMTS                 148360
      *
     IL2140F    E DSLE2140PD
      *
     IL2140S      DS                            133
      *
     ISCSARD    E DSSCSARDPD
      ** SAR details
     I              LCD                             LCDX
      *
     ISDBANK    E DSSDBANKPD
      ** Bank details
      *
     ISDCLND    E DSSDCLNDPD
      ** Customer lending ICD
      *
     ISDMMOD    E DSSDMMODPD
      ** Midas modules details
      *
     ISDCURR    E DSSDCURRPD
      ** Currency Details
      *
     ISDCUST    E DSSDCUSTPD
      ** Customer Details
      *
     ISDBRCH    E DSSDBRCHPD
     I              QQDFAC                          Q1DFAC                                    CGL029
      ** Branch Details
      *
     IDSFDY     E DSDSFDY
      ** Short data structure for access objects
      *
     IDSSDY     E DSDSSDY
      ** Long data structure for access objects
      *
     IEMSDS     E DSEMDTA                                                 150215
      ** Link Enquiry data area                                           150215
      *                                                                   150215
     I                                      141 146 CUSTNO                150215
     I                                      147 149 FACTYP                150215
     I                                      150 151 FACSEQ                150215
      *                                                                   CLE009
     ISDNOST    E DSSDNOSTPD                                              CLE009
      *                                                                   CLE009
      ** Nostro details                                                   CLE009
      *                                                                   CLE009
     ISVAL1       DS                            200                                           240968
     I                                        1   1 SVAL11                                    240968
     ISVAL2       DS                            200                                           244314
     I                                        1   1 SVAL21                                    244314
      ** DS for AOSVALR0                                                                      240968
      *                                                                                       240968
     I              'CLAuthORED'          C         SVALKK                                    240968
     I              'CLAuthSameORED'      C         SVALKJ                                    244314
     I              'MFUExchRateTolerance'C         SVALKI                                  BUG23133
     I/COPY QWINDSRC,LE2140GDTA
     I/COPY ZSRSRC,ZHOLI
      /EJECT
      *****************************************************************
      *                   Index to subroutines                        *
      *  Main routine                                                 *
      *  SRINIT  -  Initial processing                                *
      *  SRINRC  -  Input and record mode processing                  *
      *  SRINS1  -  Initialise main screen fields                     *
      *  SRINWI  -  Initialise main work fields                       *
      *  SRINWS  -  Initialise subfile work fields                    *
      *  SRINSS  -  Initialise subfile screen fields                  *
      *  SRSUBF  -  Subfile screen                                    *
      *  SRSINS  -  Initialise subfile fields                         *
      *  SRSCLR  -  Subfile clear                                     *
      *  SRREVW  -  Set utilisation from 'Review From'                *
      *  SRREVS  -  Select records from Review fields                 *
      *  SRLOAD  -  Load subfile records                              *
      *  SRSFMT  -  Format Subfile fields                             *
      *  SRDETL  -  Detail processing                                 *
      *  SRINSD  -  Initialise detail screen fields and indicators    *
      *  SRINWD  -  Initialise detail work variables                  *
      *  SRENDP  -  Close-down the program                            *
      *  SRPLAY  -  Play mode processing                              *
      *  SRMOVK  -  Move test-harness key to screen format            *
      *  SRMOVD  -  Move test-harness file format to screen format    *
      *  SRUPUD  -  Update test-harness file - DELETE                 *
      *  SRUPUX  -  Update test-harness file - AUTHORISE              *
      *  SRUPUA  -  Update test-harness file - AMEND                  *
      *  SRUPUI  -  Update test-harness file - INSERT                 *
      *  SRUPTH  -  Write to test-harness file                        *
      *  SRDETS  -  Initial screen called from subfile                *
      *  SRVALI  -  Initial validation                                *
      *  SRVALD  -  Detail validation                                 *
      *  SRVALR  -  'Review From' fields validation                   *
      *  SRDHDR  -  Move fields to detail screen header fields        *
      *  SRRTRV  -  Retrieve details from utilisation file            *
      *  SRVALA  -  Validate detail fields                            *
      *  SRUPMF  -  Update the manual utilisation file                *
      *  SRFEES  -  Update Fees File                                  *   CLE009
      *  SRUPFA  -  Update the facility file                          *
      *  SRCONF  -  Convert facility currency to C/A currency         *
      *  SRUPFS  -  Update the facility's superfacility               *
      *  SRUPTR  -  Update utilisation trailer file                   *
      *  SRVALS  -  Subfile validation                                *
      *  SRPROC  -  Subfile detail processing                         *
      *  SRAUTH  -  Validate authorisation action                     *
      *  SRWIND  -  Window processing                                 *
      *  SRUAUT  -  Check if user is authorised to the action         *
      *  SRPERR  -  Send error message for play records               *
      *  SRGETD  -  Get the currency's number of decimal places       *
      *  CLEAR   -  Clear message from the program message queue      *
      *  GLZADD  -  Subroutine to add an amount to the total          *
      *  GLZSUB  -  Subroutine to subtract an amount to the total     *
      *  GLZSUM  -  Subroutine to carry out the additon for subroutine*
      *  ZASNMS  -  Send message to the program message queue         *
      *  *PSSR   -  Error handling subroutine                         *
      *****************************************************************
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ** Initial processing.
      *
     C                     EXSR SRINIT
      *
      ** Detail processings.
      *
     C           *INKC     DOWNE'1'
     C           *IN83     ANDNE'1'
     C           PMODE     CASEQ'I'       SRINRC
     C           PMODE     CASEQ'R'       SRINRC
     C           PMODE     CASEQ'P'       SRPLAY
     C                     ENDCS
     C                     ENDDO
      *
      ** Final processing.
      *
     C                     EXSR SRENDP
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      /EJECT
      /TITLE SR/SRINIT
      *****************************************************************
      *  SRINIT  -  Initial Processing                                *
      *  Called by: Main routine                                      *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter.
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Receive entry parameters.
      *
     C           *ENTRY    PLIST
     C                     PARM           PMODE   1
      *
      ** Access RUNDAT for multibranching indicator.
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
      ** Define and initialise LDA.
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'LE2140'  DBPGM
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     Z-ADD*ZEROS    DBASE
     C                     OUT  LDA
      *                                                                   150215
      ** Access EM Data Area                                              150215
     C           *NAMVAR   DEFN           EMSDS                           150215
      *                                                                   150215
     C           *LOCK     IN   EMSDS                                     150215
     C                     MOVE RQDTD     MODID   2                       150215
     C                     OUT  EMSDS                                     150215
      *                                                                   150215
     C           MODID     IFEQ 'LE'                                      150215
     C           RETND     ANDEQ' '                                       150215
     C                     MOVE *ON       *IN05                           150215
     C                     ENDIF                                          150215
      *
      ** Retrieve bank details.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE           ************
     C                     Z-ADD1         DBASE            * DBERR 01 *
     C                     MOVEL'*FIRST'  DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVELBJRDNB    WRUND   50
     C                     Z-ADDBJBVPD    BVLP    30
     C           BJDFIN    IFEQ 'M'
     C                     MOVE '1'       *IN98            MMDDYY
     C                     ENDIF
      *
      ** Retrieve midas module details
      *
     C                     CALL 'AOMMODR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*FIRST ' POPTN
     C           SDMMOD    PARM SDMMOD    DSFDY
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDMMODPD'DBFILE           ************
     C                     Z-ADD16        DBASE            * DBERR 16 *
     C                     MOVEL'*FIRST'  DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *                                                                                       240968
     C                     CALL 'AOSVALR0'                                                    240968
     C                     PARM           PRTCD                                               240968
     C                     PARM SVALKK    SVALK1 20                                           240968
     C                     PARM           SVAL1 200                                           240968
     C**********           PARM *BLANK    SVALK2 20                                    240968 244314
     C                     PARM SVALKJ    SVALK2 20                                           244314
     C                     PARM           SVAL2 200                                           240968
     C                     PARM *BLANK    SVALK3 20                                           240968
     C                     PARM           SVAL3 200                                           240968
     C                     PARM *BLANK    SVALK4 20                                           240968
     C                     PARM           SVAL4 200                                           240968
     C                     PARM *BLANK    SVALK5 20                                           240968
     C                     PARM           SVAL5 200                                           240968
     C                     PARM *BLANK    SVALK6 20                                           240968
     C                     PARM           SVAL6 200                                           240968
     C                     PARM *BLANK    SVALK7 20                                           240968
     C                     PARM           SVAL7 200                                           240968
     C                     PARM *BLANK    SVALK8 20                                           240968
     C                     PARM           SVAL8 200                                           240968
     C                     PARM *BLANK    SVALK9 20                                           240968
     C                     PARM           SVAL9 200                                           240968
     C                     PARM *BLANK    SVALK0 20                                           240968
     C                     PARM           SVAL10200                                           240968
      *                                                                                       240968
     C           PRTCD     IFNE *BLANK                                                        240968
     C           *LOCK     IN   LDA                                                           240968
     C                     MOVEL'*KEY    'DBKEY                                               240968
     C                     MOVEL'SDSVALPD'DBFILE           ***************                    240968
     C                     Z-ADD033       DBASE            * DBERROR 033 *                    240968
     C                     OUT  LDA                        ************* *                    240968
     C                     EXSR *PSSR                                                         240968
     C                     ENDIF                                                              240968
     C                     MOVE SVAL11    ATHR    1                                           240968
     C                     MOVE SVAL21    ATHS    1                                           244314
      *                                                                   CLE014
      ** Determine if CLE014 is installed                                 CLE014
      *                                                                   CLE014
     C                     CALL 'AOSARDR0'                                CLE014
     C                     PARM '       ' PRTCD                           CLE014
     C                     PARM '*VERIFY' POPTN                           CLE014
     C                     PARM 'CLE014'  PSARD                           CLE014
     C           SCSARD    PARM SCSARD    DSSDY                           CLE014
      *                                                                   CLE014
      ** Handle Database Error                                            CLE014
      *                                                                   CLE014
     C           PRTCD     IFNE *BLANKS                                   CLE014
     C           PRTCD     ANDNE'*NRF   '                                 CLE014
     C           *LOCK     IN   LDA                                       CLE014
     C                     MOVE 'SCSARDPD'DBFILE                          CLE014
     C                     MOVELPSARD     DBKEY                           CLE014
     C                     Z-ADD30        DBASE                           CLE014
     C                     OUT  LDA                                       CLE014
     C                     EXSR *PSSR                                     CLE014
     C                     ENDIF                                          CLE014
      *                                                                   CLE014
      ** CLE014 is installed                                              CLE014
      *                                                                   CLE014
     C           PRTCD     IFEQ *BLANKS                                   CLE014
     C                     MOVE 'Y'       CLE014  1                       CLE014
     C                     ELSE                                           CLE014
     C                     MOVE 'N'       CLE014                          CLE014
     C                     ENDIF                                          CLE014
      *                                                                                       CLE028
      ** Determine if CLE028 is installed                                                     CLE028
      *                                                                                       CLE028
     C                     CALL 'AOSARDR0'                                                    CLE028
     C                     PARM '       ' PRTCD                                               CLE028
     C                     PARM '*VERIFY' POPTN                                               CLE028
     C                     PARM 'CLE028'  PSARD                                               CLE028
     C           SCSARD    PARM SCSARD    DSSDY                                               CLE028
      *                                                                                       CLE028
      ** Handle Database Error                                                                CLE028
      *                                                                                       CLE028
     C           PRTCD     IFNE *BLANKS                                                       CLE028
     C           PRTCD     ANDNE'*NRF   '                                                     CLE028
     C           *LOCK     IN   LDA                                                           CLE028
     C                     MOVE 'SCSARDPD'DBFILE                                              CLE028
     C                     MOVELPSARD     DBKEY                                               CLE028
     C                     Z-ADD31        DBASE                                               CLE028
     C                     OUT  LDA                                                           CLE028
     C                     EXSR *PSSR                                                         CLE028
     C                     ENDIF                                                              CLE028
      *                                                                                       CLE028
      ** CLE028 is installed                                                                  CLE028
      *                                                                                       CLE028
     C           PRTCD     IFEQ *BLANKS                                                       CLE028
     C                     MOVE 'Y'       CLE028  1                                           CLE028
     C                     ELSE                                                               CLE028
     C                     MOVE 'N'       CLE028                                              CLE028
     C                     ENDIF                                                              CLE028
      *
      ** Determine if Customer Lending Enhancements - Authorisations
      ** feature is installed.
      *
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM 'CLE002'  PSARD   6
     C           SCSARD    PARM SCSARD    DSFDY
     C           PRTCD     IFEQ *BLANKS
     C                     MOVE 'Y'       CLE002  1
     C                     ELSE
     C                     MOVE 'N'       CLE002
     C           PRTCD     IFNE '*NRF    '
     C           *LOCK     IN   LDA
     C                     MOVEL'SCSARDPD'DBFILE           ************
     C                     Z-ADD2         DBASE            * DBERR 02 *
     C                     MOVEL'CLE002'  DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *                                                                   CLE009
      ** Access SAR details file to see if Customer Lending               CLE009
      ** Enhancements is installed                                        CLE009
      *                                                                   CLE009
     C                     CALL 'AOSARDR0'                                CLE009
     C                     PARM *BLANK    PRTCD                           CLE009
     C                     PARM '*KEY   ' POPTN                           CLE009
     C                     PARM 'CLE007'  PSARD   6                       CLE009
     C           SCSARD    PARM SCSARD    DSFDY                           CLE009
      *                                                                   CLE009
     C           PRTCD     IFEQ *BLANK                                    CLE009
     C                     MOVE 'Y'       CLE007  1                       CLE009
     C                     CALL 'AOSARDR0'                                CLE009
     C                     PARM *BLANKS   PRTCD                           CLE009
     C                     PARM '*KEY   ' POPTN                           CLE009
     C                     PARM 'CLE009'  PSARD   6                       CLE009
     C           SCSARD    PARM SCSARD    DSFDY                           CLE009
      *                                                                   CLE009
      ** If CLE009 is installed, fill currency and their corresponding    CLE009
      ** telex notice days arrays                                         CLE009
      *                                                                   CLE009
     C           PRTCD     IFEQ *BLANKS                                   CLE009
     C                     MOVE 'Y'       CLE009  1                       CLE009
     C                     Z-ADD1         Y       30                      CLE009
      *                                                                   CLE009
     C                     CALL 'AOCURRR0'                                CLE009
     C                     PARM *BLANKS   PRTCD                           CLE009
     C                     PARM '*FIRST ' POPTN                           CLE009
     C                     PARM           PCURR   3                       CLE009
     C           SDCURR    PARM SDCURR    DSSDY                           CLE009
      *                                                                   CLE009
     C                     MOVE A6CYCD    CYA,Y                           CLE009
     C                     MOVE A6TXND    TND,Y                           CLE009
      *                                                                   CLE009
     C           PRTCD     DOWNE'*EOF    '                                CLE009
     C                     ADD  1         Y                               CLE009
      *                                                                   CLE009
     C                     CALL 'AOCURRR0'                                CLE009
     C                     PARM *BLANKS   PRTCD                           CLE009
     C                     PARM '*NEXT  ' POPTN                           CLE009
     C                     PARM           PCURR   3                       CLE009
     C           SDCURR    PARM SDCURR    DSSDY                           CLE009
      *                                                                   CLE009
     C           PRTCD     IFNE '*EOF    '                                CLE009
     C                     MOVE A6CYCD    CYA,Y                           CLE009
     C                     MOVE A6TXND    TND,Y                           CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C                     ENDDO                                          CLE009
      *                                                                   CLE009
     C                     ELSE                                           CLE009
     C                     MOVE 'N'       CLE009                          CLE009
      *                                                                   CLE009
     C           PRTCD     IFNE '*NRF    '                                CLE009
     C           *LOCK     IN   LDA                                       CLE009
     C                     MOVEL'SCSARDPD'DBFILE           ************   CLE009
     C                     Z-ADD27        DBASE            * DBERR 27 *   CLE009
     C                     MOVEL'CLE009'  DBKEY            ************   CLE009
     C                     OUT  LDA                                       CLE009
     C                     EXSR *PSSR                                     CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C                     ENDIF                                          CLE009
     C                     ELSE                                           CLE009
     C                     MOVE 'N'       CLE007                          CLE009
     C           PRTCD     IFNE '*NRF   '                                 CLE009
     C           *LOCK     IN   LDA                                       CLE009
     C                     MOVEL'SCSARDPD'DBFILE           ************   CLE009
     C                     Z-ADD28        DBASE            * DBERR 28 *   CLE009
     C                     MOVEL'CLE007'  DBKEY            ************   CLE009
     C                     OUT  LDA                                       CLE009
     C                     EXSR *PSSR                                     CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF                                          CLE009
      *                                                                                       CLE023
      ** Determine if Lending Enhancement CLE023 is installed                                 CLE023
      *                                                                                       CLE023
     C                     CALL 'AOSARDR0'                                                    CLE023
     C                     PARM '       ' PRTCD                                               CLE023
     C                     PARM '*VERIFY' POPTN                                               CLE023
     C                     PARM 'CLE023'  PSARD                                               CLE023
     C           SCSARD    PARM SCSARD    DSSDY                                               CLE023
      *                                                                                       CLE023
      ** Handle Database Error                                                                CLE023
      *                                                                                       CLE023
     C           PRTCD     IFNE *BLANKS                                                       CLE023
     C           PRTCD     ANDNE'*NRF   '                                                     CLE023
     C           *LOCK     IN   LDA                                                           CLE023
     C                     MOVE 'SCSARDPD'DBFILE                                              CLE023
     C                     MOVELPSARD     DBKEY                                               CLE023
     C                     Z-ADD31        DBASE                                               CLE023
     C                     OUT  LDA                                                           CLE023
     C                     EXSR *PSSR                                                         CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
      ** CLE023 is installed                                                                  CLE023
      *                                                                                       CLE023
     C           PRTCD     IFEQ *BLANKS                                                       CLE023
     C                     MOVE 'Y'       CLE023  1                                           CLE023
     C                     ELSE                                                               CLE023
     C                     MOVE 'N'       CLE023                                              CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE031
      ** Check whether SAR CLE031 is installed or not.                                        CLE031
      *                                                                                       CLE031
     C                     CALL 'AOSARDR0'                                                    CLE031
     C                     PARM *BLANK    PRTCD   7                                           CLE031
     C                     PARM '*VERIFY' POPTN   7                                           CLE031
     C                     PARM 'CLE031'  PSARD   6                                           CLE031
     C           SCSARD    PARM SCSARD    DSFDY                                               CLE031
      *                                                                                       CLE031
     C           PRTCD     IFEQ *BLANK                                                        CLE031
     C                     MOVE 'Y'       CLE031  1                                           CLE031
      *                                                                                       CLE031
     C                     ELSE                                                               CLE031
      *                                                                                       CLE031
     C                     MOVE 'N'       CLE031                                              CLE031
     C           PRTCD     IFNE '*NRF   '                                                     CLE031
     C           *LOCK     IN   LDA                                                           CLE031
     C                     MOVEL'LE2140  'DBPGM                                               CLE031
     C                     MOVEL'SCSARDPD'DBFILE                                              CLE031
     C                     Z-ADD32        DBASE                                               CLE031
     C                     MOVEL'CLE031'  DBKEY                                               CLE031
     C                     OUT  LDA                                                           CLE031
     C                     EXSR *PSSR                                                         CLE031
     C                     ENDIF                                                              CLE031
      *                                                                                       CLE031
     C                     ENDIF                                                              CLE031
      *                                                                                       CLE134
     C                     CALL 'AOSARDR0'                                                    CLE134
     C                     PARM *BLANKS   PRTCD                                               CLE134
     C                     PARM '*KEY   ' POPTN                                               CLE134
     C                     PARM 'CLE134'  PSARD   6                                           CLE134
     C           SCSARD    PARM SCSARD    DSFDY                                               CLE134
     C           PRTCD     IFEQ *BLANKS                                                       CLE134
     C                     MOVE 'Y'       CLE134  1                                           CLE134
     C                     ELSE                                                               CLE134
     C                     MOVE 'N'       CLE134                                              CLE134
     C                     ENDIF                                                              CLE134
      *
      ** If CLE002 is installed access the Lending ICD to check
      ** whether Authorisation for Manual Facility Utilisation
      ** is required.
      *
     C           CLE002    IFEQ 'Y'
     C                     CALL 'AOCLNDR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*FIRST ' POPTN
     C********** SDCLND    PARM SDCLND    DSFDY                                               CLE134
     C           SDCLND    PARM SDCLND    DSSDY                                               CLE134
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCLNDPD'DBFILE           ************
     C                     Z-ADD3         DBASE            * DBERR 03 *
     C                     MOVEL'*FIRST'  DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Set authorisation flag accordingly. If CLE002 is not
      ** installed, move 'N' to flag.
      *
     C           BPFAUT    IFEQ 'Y'
     C                     MOVE 'Y'       WMFAU   1
     C                     ELSE
     C                     MOVE 'N'       WMFAU
     C                     ENDIF
     C                     ELSE
     C                     MOVE 'N'       WMFAU
     C                     ENDIF
      *
      ** Open display file for Interactive or Record mode.
      *
     C           PMODE     IFEQ 'I'
     C           PMODE     OREQ 'R'
     C                     OPEN LE2140DF
     C                     ENDIF
      *
      ** Open the Manual Facility Utilisation Play/Record file.
      *
     C                     MOVE 'N'       WOPNT   1
     C           PMODE     IFEQ 'P'
     C           PMODE     OREQ 'R'
     C                     OPEN LE2140PD
     C                     MOVE 'Y'       WOPNT
     C                     ENDIF
      *
     C           WMFAU     IFEQ 'Y'
     C                     MOVE '1'       *IN29
     C                     ENDIF
     C/COPY WNCPYSRC,LE2140C004
      *
      ** Key to FCLTY file.
      *
     C           KEYFAC    KLIST
     C**********           KFLD           WKCNO   60                                          CSD027
     C                     KFLD           WKCNO   6                                           CSD027
     C                     KFLD           WKFTP   30
     C                     KFLD           WKFNO   20
     C                     KFLD           WKRCT   1
      *
      ** Key to LEMNFUL4/LEMNFUL5 file.
      *
     C           KEYREV    KLIST
     C**********           KFLD           WKRCN   60                                          CSD027
     C                     KFLD           WKRCN   6                                           CSD027
     C                     KFLD           WKRFT   30
     C                     KFLD           WKRFN   20
      *
      ** Key to LEMNFUL3
      *
     C           KEYFU3    KLIST
     C                     KFLD           WKCNO
     C                     KFLD           WKFTP
     C                     KFLD           WKFNO
     C**********           KFLD           WKRSQ   30                                        AR674226
     C                     KFLD           WKRSQ   40                                        AR674226
      *
      ** Key to LEMNFUL3/LEMNFUL4
      *
     C           KEYUTI    KLIST
     C                     KFLD           WUCNO
     C                     KFLD           WUFTP
     C                     KFLD           WUFNO
     C                     KFLD           WURSQ
      *
     C           KEYFAM    KLIST
     C                     KFLD           MBRCA
     C                     KFLD           WKCNO
     C                     KFLD           WKFTP
     C                     KFLD           WKFNO
      *                                                                   CLE009
      ** Key list for Fees file                                           CLE009
      *                                                                   CLE009
     C           WKFEE     KLIST                                          CLE009
     C**********           KFLD           WKCNO   60                                   CLE009 CSD027
     C                     KFLD           WKCNO   6                                           CSD027
     C                     KFLD           WFFACL  50                      CLE009
     C**********           KFLD           WFLOAN  60                                   CLE009 CLE148
     C                     KFLD           WFLOAN  6                                           CLE148
      *
     C                     MOVE 'N'       WSUBF   1
     C                     MOVE 'N'       WACPT   1
     C                     MOVE 'N'       WIERR   1
     C                     MOVE 'N'       WVERR   1
     C                     MOVE 'N'       WERRF   1
     C                     MOVE 'N'       WWFLG   1
     C                     MOVE 'N'       WWERR   1
      *
     C                     MOVEL'LERMSGF 'ZAMSGF
     C                     MOVE USER      SUSER
     C                     MOVE BJMRDT    SRUNA
     C                     MOVE WSID      SWSID
      *
     C                     ENDSR
      /TITLE SR/SRINRC
      *****************************************************************
      *  SRINRC  -  Input and Record Mode Processing                  *
      *  Called by: Main routine                                      *
      *  Calls    :                                                   *
      *  SRINS1  -  Initialise main screen fields                     *
      *  CLEAR   -  Clear message from the program message queue      *
      *  SRINWI  -  Initialise main work fields                       *
      *  SRDETL  -  Detail processing                                 *
      *  SRVALR  -  'Review From' fields validation                   *
      *  SRSUBF  -  Subfile screen                                    *
      *****************************************************************
     C           SRINRC    BEGSR
      *
      ** Write Main screen and footer.
      *
     C                     WRITELE2140F0
      *
      ** Initialise screen fields
      *
     C                     EXSR SRINS1
      *
     C           *INKC     DOUEQ'1'
      *
     C           *IN05     IFEQ *ON                                       150215
     C                     MOVELCUSTNO    SRCUS                           150215
     C                     MOVELFACTYP    SRTYP                           150215
     C                     MOVELFACSEQ    SRNUM                           150215
     C                     ELSE                                           150215
     C                     WRITELE2140C1
     C                     EXSR CLEAR
     C                     EXFMTLE2140F1
     C                     ENDIF                                          150215
      *
      ** Initialise work fields
      *
     C                     EXSR SRINWI
      *
      ** Exit this subroutine if F3 pressed
      *
     C           *INKC     IFEQ '1'
     C                     LEAVE
     C                     ENDIF
      *
     C           SACTN     IFNE *BLANK
     C           SCNUM     ORNE *BLANKS
     C           SFTYP     ORNE *BLANKS
     C           SFNUM     ORNE *BLANKS
     C           SFSEQ     ORNE *BLANKS
      *
      ** Validate Facility details. If error exists, redisplay initial
      ** screen. Otherwise go to Detail Screen.
      *
     C                     EXSR SRVALI
     C           WIERR     IFEQ 'N'
     C                     EXSR SRDETL
     C                     EXSR SRINS1
     C                     ENDIF
      *
      ** Validate Review From and Req. Reauth. fields. If error exists,
      ** redisplay initial screen. Otherwise got to Subfile Screen.
      *
     C                     ELSE
     C                     EXSR SRVALR
     C           WVERR     IFEQ 'N'
     C                     EXSR SRSUBF
     C                     EXSR SRINS1
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
      *
     C                     ENDSR
      /TITLE SR/SRINS1
      *****************************************************************
      *  SRINS1  -  Initialise main screen fields.                    *
      *  Called by: SRINRC, SRPLAY, SRDETS                            *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRINS1    BEGSR
      *
     C           WSUBF     IFEQ 'N'
     C                     MOVE *BLANK    SACTN
     C                     ELSE
     C                     MOVE 'I'       SACTN
     C                     ENDIF
     C                     MOVE *BLANKS   SCNUM
     C                     MOVE *BLANKS   SFTYP
     C                     MOVE *BLANKS   SFNUM
     C                     MOVE *BLANKS   SFSEQ
     C                     MOVE *BLANKS   SRCUS
     C                     MOVE *BLANKS   SRTYP
     C                     MOVE *BLANKS   SRNUM
     C                     MOVE 'N'       SRAUT
      *
     C                     ENDSR
      /TITLE SR/SRINWI
      *****************************************************************
      *  SRINWI  -  Initialise main work fields.                      *
      *  Called by: SRINRC, SRPLAY, SRDETS                            *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRINWI    BEGSR
      *
     C                     MOVE 'N'       WIERR
     C                     MOVE 'N'       WVERR
     C                     MOVEA'00000'   *IN,30
     C                     MOVEA'0000'    *IN,35
     C                     MOVE '0'       *IN24
      *
     C                     ENDSR
      /TITLE SR/SRINWS
      *****************************************************************
      *  SRINWS  -  Initialise subfile work fields                    *
      *  Called by: SRSUBF                                            *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRINWS    BEGSR
      *
     C                     MOVE 'N'       WSFER
     C                     MOVE 'N'       WVERR
     C                     MOVEA'0000'    *IN,35
     C                     MOVE '0'       *IN49
      *
     C                     ENDSR
      /TITLE SR/SRINSS
      *****************************************************************
      *  SRINSS  -  Initialise subfile screen fields                  *
      *  Called by: SRSUBF                                            *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRINSS    BEGSR
      *
     C                     MOVE *BLANKS   SRCUS
     C                     MOVE *BLANKS   SRTYP
     C                     MOVE *BLANKS   SRNUM
     C                     MOVE 'N'       SRAUT
      *                                                                   150215
     C           *IN05     IFEQ *ON                                       150215
     C                     MOVELCUSTNO    SRCUS                           150215
     C                     MOVELFACTYP    SRTYP                           150215
     C                     MOVELFACSEQ    SRNUM                           150215
     C                     ENDIF                                          150215
      *
     C                     ENDSR
      /TITLE SR/SRSUBF
      *****************************************************************
      *  SRSUBF  -  Subfile Screen                                    *
      *  Called by: SRINRC                                            *
      *  Calls    :                                                   *
      *  SRSINS  -  Initialise subfile fields                         *
      *  SRSCLR  -  Subfile clear                                     *
      *  CLEAR   -  Clear message from the program message queue      *
      *  SRREVW  -  Set utilisation from 'Review From'                *
      *  SRREVS  -  Select records from Review fields                 *
      *  SRINWS  -  Initialise subfile work fields                    *
      *  SRINSS  -  Initialise subfile screen fields                  *
      *  SRDETS  -  Initial screen called from subfile                *
      *  SRLOAD  -  Load subfile records                              *
      *  SRVALS  -  Subfile validation                                *
      *****************************************************************
     C           SRSUBF    BEGSR
      *
     C                     MOVE 'N'       WSEXT   1
     C                     MOVE 'Y'       WSUBF   1
     C                     MOVE DREVW     WSAVR  12
     C                     MOVE *BLANK    RTNDWF  1                       150215
      *
      ** Initialise and clear the subfile/message queue, set the
      ** Review From facility then load a subfile page.
      *
     C                     EXSR SRSINS
     C                     EXSR SRSCLR
     C                     EXSR CLEAR
     C                     EXSR SRREVW
     C                     EXSR SRLOAD
      *
      ** Do until user wants to exit the subfile screen.
      *
     C           WSEXT     DOUEQ'Y'
     C           *INKC     OREQ '1'
      *
     C                     WRITELE2140F2
     C                     WRITELE2140C1
     C                     EXSR CLEAR
     C                     EXFMTLE2140C0
      *
      ** Reintialise work fields.
      *
     C                     EXSR SRINWS
      *
      ** F3 pressed.
      *
     C                     SELEC
     C           *INKC     WHEQ '1'
     C                     MOVE 'Q'       RTNDWF                          150215
     C                     LEAVE
      *
      ** F12 pressed.
      *
     C           *INKL     WHEQ '1'
     C                     MOVE '0'       *IN25
     C                     MOVE 'U'       RTNDWF                          150215
     C                     LEAVE
      *
      ** F5 pressed.
      *
     C           *INKE     WHEQ '1'
     C                     EXSR SRSINS
     C                     EXSR SRSCLR
     C                     EXSR SRINSS
     C                     EXSR SRINWS
     C                     EXSR SRREVW
     C                     EXSR SRLOAD
      *
      ** F9 pressed.
      *
     C           *INKI     WHEQ '1'
     C                     MOVE '1'       *IN25
     C                     EXSR SRDETS
      *
      ** Rollup key pressed.
      *
     C           *IN21     WHEQ '1'
     C                     EXSR SRSINS
     C                     EXSR SRLOAD
      *
      ** Priority on Review from fields: if this is entered, process
      ** this request.
      *
     C                     OTHER
     C           DREVW     IFNE WSAVR
     C                     EXSR SRREVS
     C                     ELSE
     C                     EXSR SRVALS
     C                     ENDIF
      *
     C                     ENDSL
     C                     ENDDO
      *
     C                     MOVE 'N'       WSUBF
     C                     MOVEA'0000'    *IN,35
      *                                                                   150215
     C           *IN05     IFEQ *ON                                       150215
     C           RTNDWF    ANDEQ'U'                                       150215
     C                     MOVE *ON       *INKC                           150215
     C                     ENDIF                                          150215
      *
     C                     ENDSR
      /TITLE SR/SRSINS
      *****************************************************************
      *  SRSINS  -  Initialise Subfile fields                         *
      *  Called by: SRINRC, SRSUBF, SRREVS                            *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRSINS    BEGSR
      *
     C                     MOVE *BLANKS   SLSEL
     C                     MOVE *BLANKS   SSCUS
     C                     MOVE *BLANKS   SSTYP
     C                     MOVE *BLANKS   SSNUM
     C                     MOVE *BLANKS   SSUSQ
     C                     MOVE *BLANKS   SSCCY
     C                     MOVE *BLANKS   SSSTS
     C                     MOVE '1'       *IN13
     C                     MOVE '0'       *IN19
      *
     C                     ENDSR
      /TITLE SR/SRSCLR
      *****************************************************************
      *  SRSCLR  -  Subfile Clear                                     *
      *  Called by: SRSUBF, SRREVS                                    *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRSCLR    BEGSR
      *
     C                     MOVE '1'       *IN11
     C                     WRITELE2140C0
     C                     MOVE '00'      *IN,11
     C                     Z-ADD1         SRRN    40
     C                     WRITELE2140F0
      *
     C                     ENDSR
      /TITLE SR/SRREVW
      *****************************************************************
      *  SRREVW  -  Set Utilisation from 'Review From'                *
      *  Called by: SRSUBF, SRREVS                                    *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRREVW    BEGSR
      *
     C           SRCUS     IFEQ *BLANK
     C**********           Z-ADD0         WKRCN                                               CSD027
     C                     MOVE *BLANKS   WKRCN                                               CSD027
     C                     ELSE
     C                     MOVE SRCUS     WKRCN
     C                     ENDIF
      *
     C           SRTYP     IFEQ *BLANK
     C                     Z-ADD0         WKRFT
     C                     ELSE
     C                     MOVE SRTYP     WKRFT
     C                     ENDIF
      *
     C           SRNUM     IFEQ *BLANK
     C                     Z-ADD0         WKRFN
     C                     ELSE
     C                     MOVE SRNUM     WKRFN
     C                     ENDIF
      *
     C           SRAUT     IFEQ 'Y'
     C           KEYREV    SETLLLEMNFUL5
     C                     READ LEMNFUL5                 80
     C                     ELSE
     C           KEYREV    SETLLLEMNFUL6
     C                     READ LEMNFUL6                 80
     C                     ENDIF
      *                                                                   150215
     C           *IN05     IFEQ *ON                                       150215
     C           *IN80     ANDEQ*OFF                                      150215
     C                     MOVE CNUM      WWCUS   6                       150215
     C                     MOVE FACT      WWTYP   3                       150215
     C                     MOVE FCNO      WWNUM   2                       150215
     C           SRCUS     IFNE WWCUS                                     150215
     C           SRTYP     ORNE WWTYP                                     150215
     C           SRNUM     ORNE WWNUM                                     150215
     C                     MOVE *ON       *IN80                           150215
     C                     ENDIF                                          150215
     C                     ENDIF                                          150215
      *
     C                     ENDSR
      /TITLE SR/SRREVS
      *****************************************************************
      *  SRREVS  -  Select records from Review fields                 *
      *  Called by: SRSUBF, SRPROC                                    *
      *  Calls    :                                                   *
      *  SRSINS  -  Initialise subfile fields                         *
      *  SRSCLR  -  Subfile clear                                     *
      *  SRREVW  -  Set utilisation from 'Review From'                *
      *  SRLOAD  -  Load subfile records                              *
      *****************************************************************
     C           SRREVS    BEGSR
      *
      ** Validate Review From fields. If no error is found, Initialise
      ** and Clear subfile for reload
      *
     C                     EXSR SRVALR
     C           WVERR     IFEQ 'N'
     C                     EXSR SRSINS
     C                     EXSR SRSCLR
     C                     EXSR SRREVW
     C                     EXSR SRLOAD
     C                     MOVE DREVW     WSAVR
     C                     Z-ADD1         SRECN
     C                     ENDIF
      *
     C                     ENDSR
      /TITLE SR/SRLOAD
      *****************************************************************
      *  SRLOAD  -  Load subfile records                              *
      *  Called by: SRSUBF, SRREVS                                    *
      *  Calls    :                                                   *
      *  *PSSR   -  Error handling subroutine                         *
      *  SRSFMT  -  Format Subfile fields                             *
      *  ZASNMS  -  Sends message to the program message queue        *
      *****************************************************************
     C           SRLOAD    BEGSR
      *
      ** Send message if no records to display in subfile.
      *
     C           *IN80     IFEQ '1'
     C                     MOVEL'LEU0017' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN12
      *
     C                     ELSE
     C                     MOVE '0'       *IN12
     C                     Z-ADD1         WCTR    20
     C                     Z-ADDSRRN      SRECN
      *
     C           *IN80     DOWEQ'0'
     C           WCTR      ANDLE10
      *
      ** Get the utilisation's branch and check if the user has
      ** access to the branch of the utilisations on display.
      *
     C                     MOVE CNUM      WKCNO
     C                     MOVE FACT      WKFTP
     C                     MOVE FCNO      WKFNO
     C                     MOVE 'A'       WKRCT
     C           KEYFAC    CHAINFCLTY               N50
     C           *IN50     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'FCLTYFM 'DBFILE           ************
     C                     Z-ADD4         DBASE            * DBERR 04 *
     C                     MOVELDFCLTY    DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     Z-ADD*ZERO     @ERR    10
     C           AGMBIN    IFEQ 'Y'
     C                     CALL 'ZVBBU'
     C                     PARM MBRCA     @ZBR    3
     C                     PARM           @ERR
     C                     ENDIF
      *
      ** If user is authorised to the branch format for display and
      ** write to subfile.
      *
     C           @ERR      IFEQ *ZERO
     C                     EXSR SRSFMT
     C                     WRITELE2140S0
     C                     ADD  1         SRRN
     C                     ADD  1         WCTR
     C                     ENDIF
      *
      ** Read next record.
      *
     C           SRAUT     IFEQ 'Y'
     C                     READ LEMNFUL5                 80
     C                     ELSE
     C                     READ LEMNFUL6                 80
     C                     ENDIF
      *                                                                   150215
     C           *IN05     IFEQ *ON                                       150215
     C           *IN80     ANDEQ*OFF                                      150215
     C                     MOVE CNUM      WWCUS                           150215
     C                     MOVE FACT      WWTYP                           150215
     C                     MOVE FCNO      WWNUM                           150215
     C           SRCUS     IFNE WWCUS                                     150215
     C           SRTYP     ORNE WWTYP                                     150215
     C           SRNUM     ORNE WWNUM                                     150215
     C                     MOVE *ON       *IN80                           150215
     C                     ENDIF                                          150215
     C                     ENDIF                                          150215
      *
     C                     ENDDO
     C                     ENDIF
      *
      ** Check if no records selected.
      *
     C           WCTR      IFEQ 1
     C                     MOVEL'LEU0017' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN12
     C                     ENDIF
      *
      ** Check if next record is valid for display, this is to
      ** condition enabling/disabling ROLLUP.
      *
     C           *IN80     DOWEQ'0'
     C           @ERR      ANDNE*ZERO
      *
     C           SRAUT     IFEQ 'Y'
     C                     READ LEMNFUL5                 80
     C                     ELSE
     C                     READ LEMNFUL6                 80
     C                     ENDIF
      *
      ** User must have access to the branch of the utilisation.
      *
     C           *IN80     IFEQ '0'
     C           AGMBIN    ANDEQ'Y'
     C                     CALL 'ZVBBU'
     C                     PARM BRCA      @ZBR
     C                     PARM           @ERR
     C                     ENDIF
     C                     ENDDO
      *
      ** Set on Subfile End indicator and do not display 'Page Key'
      ** for one-page subfile.
      *
     C           *IN80     IFEQ '1'
     C                     MOVE '1'       *IN19
     C           SRRN      IFLE 11
     C                     MOVE '0'       *IN13
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      /TITLE SR/SRSFMT
      *****************************************************************
      *  SRSFMT  -  Format Subfile fields                             *
      *  Called by: SRLOAD                                            *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRSFMT    BEGSR
      *
     C                     MOVE CNUM      SSCUS
     C                     MOVE FACT      SSTYP
     C                     MOVE FCNO      SSNUM
     C                     MOVE SQNO      SSUSQ
     C                     MOVE UCCY      SSCCY
     C                     SELEC
     C           USTS      WHEQ 'C'
     C                     MOVEL'LEU0054' ZAMSID
     C           USTS      WHEQ 'R'
     C                     MOVEL'LEU0055' ZAMSID
     C           USTS      WHEQ 'A'
     C                     MOVEL'LEU0057' ZAMSID
     C                     ENDSL
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    SSSTS
     C                     MOVE *BLANKS   ZAMSID
      *
     C                     ENDSR
      /TITLE SR/SRDETL
      *****************************************************************
      *  SRDETL  -  Detail Processing                                 *
      *  Called by: SRINRC, SRDETS, SRPROC                            *
      *  Calls    :                                                   *
      *  SRINSD  -  Initialise detail screen fields and indicators    *
      *  SRINWD  -  Initialise detail work variables                  *
      *  SRDHDR  -  Move fields to detail screen header fields        *
      *  SRRTRV  -  Retrieve details from utilisation file            *
      *  SRVALD  -  Detail validation                                 *
      *  CLEAR   -  Clears message from the program message queue     *
      *  SRWIND  -  Window processing                                 *
      *  SRUPMF  -  Update the manual utilisation file                *
      *****************************************************************
     C           SRDETL    BEGSR
      *
      ** Initialise screen and work fields and write the detail
      ** header. For non-input action code, retrieve the utilisation
      ** details.
      *
     C                     EXSR SRINSD
     C                     EXSR SRINWD
     C                     EXSR SRDHDR
      *
     C           SACTN     IFNE 'I'
     C                     EXSR SRRTRV
     C                     ENDIF
      *
      ** Protect fields for Enquire, Authorise and Reauthorise
      ** actions. For Amend, do not protect Utilisation amount and
      ** Expiry date fields.
      *
     C                     SELEC
     C           SACTN     WHEQ 'E'
     C           SACTN     OREQ 'X'
     C           SACTN     OREQ 'D'
     C                     MOVE *ON       *IN40
     C                     MOVE *ON       *IN55
     C                     MOVE '1'       *IN41                           138870
     C           SACTN     WHEQ 'A'
     C                     MOVE *ON       *IN40
     C                     ENDSL
      *
      ** Display F10 for action code Delete or F11 for action code
      ** Authorise.
      *
     C                     SELEC
     C           SACTN     WHEQ 'D'
     C                     MOVE '1'       *IN27
     C           SACTN     WHEQ 'X'
     C           BPAURV    IFNE 'Y'
     C                     MOVE '1'       *IN26
     C                     ENDIF
     C                     ENDSL
      *
      ** Validate to see if allowed to be authorised.
      *
     C           SACTN     IFEQ 'X'
     C                     EXSR SRVALD
     C                     ENDIF
      *
      ** Do until action is accepted.
      *
     C           WACPT     DOUEQ'Y'
     C           *INKC     OREQ '1'
     C           *INKL     OREQ '1'
      *
     C                     WRITELE2140C1
     C           SACTN     IFNE 'X'
     C                     EXSR CLEAR
     C                     ENDIF
     C                     WRITELE2140F3
     C                     EXFMTLE2140F4
      *
      **  Enable *INKK if BPAURV is activated
      *
     C           SACTN     IFEQ 'X'
     C           BPAURV    ANDEQ'Y'
     C                     MOVE '1'       *INKK
     C                     ENDIF
      *
      ** Reinitialise work fields.
      *
     C           SACTN     IFNE 'X'
     C                     EXSR SRINWD
     C                     ENDIF
      *
      ** F3/F12 key pressed.
      *
     C           *INKC     IFEQ '1'
     C           *INKL     OREQ '1'
     C                     LEAVE
     C                     ENDIF
      *
     C           SACTN     IFEQ 'E'
     C                     MOVE 'Y'       WACPT
     C                     ELSE
      *
      ** Validate for Insert and Amend action codes. Do not validate
      ** if warning has been previously found.
      *
     C           SACTN     IFEQ 'I'
     C           SACTN     OREQ 'A'
     C           SACTN     OREQ 'D'
     C           WSKIP     IFEQ 'Y'
     C           DSCRNB    ANDNEDSCRNA
     C           WERRF     ANDEQ'N'
     C           WSKIP     OREQ 'N'
     C                     MOVE 'N'       WSKIP
     C***********          MOVE DSCRNA    DSCRNB 72                       148360
     C                     MOVE DSCRNA    DSCRNB 73                       148360
     C                     EXSR SRVALD
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** If no errors and action is 'I', 'A', or 'X' and F11 is
      ** pressed, or action 'D' and F10 pressed.
      *
     C           WDERR     IFEQ 'N'
     C           SACTN     IFEQ 'I'
     C           SACTN     OREQ 'A'
     C           SACTN     OREQ 'X'
     C           *INKK     ANDEQ'1'
     C           SACTN     OREQ 'D'
     C           *INKJ     ANDEQ'1'
     C           WWFLG     IFEQ 'Y'
     C           SACTN     ANDNE'X'
     C                     MOVE 'N'       WWFLG
     C                     ELSE
      *
      ** Perform additional window processing before updating file.
      *
     C                     EXSR SRWIND
     C                     EXSR SRUPMF
     C                     MOVE 'Y'       WACPT
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDDO
      *
      ** Clear warning messages (if any).
      *
     C           SACTN     IFEQ 'X'
     C                     EXSR CLEAR
     C                     ENDIF
      *
     C                     ENDSR
      /TITLE SR/SRINSD
      *****************************************************************
      *  SRINSD  -  Initialise details screen fields and indicators   *
      *  Called by: SRDETL, SRPLAY                                    *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRINSD    BEGSR
      *
     C                     MOVE *BLANKS   SDACT
     C                     MOVE *BLANKS   SDCNO
     C                     MOVE *BLANKS   SDCNM
     C                     MOVE *BLANKS   SDFTP
     C                     MOVE *BLANKS   SDFNO
     C                     MOVE *BLANKS   SDFSQ
     C                     MOVE *BLANKS   SDSTS
     C                     MOVE *BLANKS   SUCCY
     C                     MOVE *BLANKS   SUAMT
     C                     MOVE *BLANKS   SAMTS                           148360
     C                     MOVE *BLANKS   SSTDT
     C                     MOVE *BLANKS   SEXDT
     C                     MOVE *BLANKS   SUXRT
     C                     MOVE *BLANKS   SUXID
     C                     MOVE *BLANKS   SUNAR
      *
     C                     Z-ADD0         WPSDT
     C                     Z-ADD0         WPEDT
     C                     Z-ADD0         WPCAM
     C                     MOVE 'N'       WSKIP   1
     C                     MOVE *BLANKS   SGSQN
     C                     MOVEA'00'      *IN,26
     C                     MOVE '0'       *IN40
     C                     MOVE '0'       *IN41                           138870
     C                     MOVE '0'       *IN55
      *
     C                     ENDSR
      /TITLE SR/SRINWD
      *****************************************************************
      *  SRINWD  -  Intialise detail work variables                   *
      *  Called by: SRDETL, SRPLAY                                    *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRINWD    BEGSR
      *
     C***********          Z-ADD0         WOEXD                           139313
     C***********          Z-ADD0         WEXDT                           139313
     C***********          Z-ADD0         WUXRT                           139313
     C                     MOVE 'N'       WACPT   1
     C                     MOVE 'N'       WDERR
     C                     MOVE 'N'       WWERR
     C                     MOVEA'000'     *IN,42
     C                     MOVEA'0000'    *IN,45
     C                     MOVEA'0'       *IN,51                          148360
      *
     C                     ENDSR
      /TITLE SR/SRENDP
      *****************************************************************
      *  SRENDP  -  Close-down the program                            *
      *  Called by: Main Routine                                      *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRENDP    BEGSR
      *
     C           *IN05     IFEQ *ON                                       150215
     C                     EXSR SRRET                                     150215
     C                     ENDIF                                          150215
      *                                                                   150215
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      /TITLE SR/SRRET                                                     150215
      *****************************************************************   150215
      *  SRRET - End of program processing                            *   150215
      *  Called by: SRENDP - Close-down the program                   *   150215
      *  Calls    : None                                              *   150215
      *****************************************************************   150215
     C           SRRET     BEGSR                                          150215
      *                                                                   150215
      ** Access EM DATA AREA                                              150215
      *                                                                   150215
     C           *LOCK     IN   EMSDS                                     150215
     C                     MOVE RTNDWF    RETND                           150215
     C                     OUT  EMSDS                                     150215
      *                                                                   150215
     C                     ENDSR                                          150215
      /TITLE SR/SRPLAY
      *****************************************************************
      *  SRPLAY  -  Play Mode Processing                              *
      *  Called by: Main routine                                      *
      *  Calls    :                                                   *
      *  SRINS1  -  Initialise main screen fields                     *
      *  SRINWI  -  Initialise main work fields                       *
      *  SRMOVK  -  Move test-harness key to screen format            *
      *  SRVALI  -  Initial validation                                *
      *  SRINSD  -  Initialise detail screen fields and indicators    *
      *  SRINWD  -  Initialise detail work variables                  *
      *  SRRTRV  -  Retrieve details from utilisation file            *
      *  SRMOVD  -  Move test-harness file format to screen format    *
      *  SRVALD  -  Detail validation                                 *
      *  SRUPMF  -  Update the manual utilisation file                *
      *****************************************************************
     C           SRPLAY    BEGSR
      *
     C                     READ LE2140PD                 83
     C           *IN83     DOWEQ*OFF
      *
      ** Save the current test-harness record read then move the
      ** fields to screen fields.
      *
     C                     MOVELL2140F    L2140S
      *
      ** Initialise initial screen and work fields.
      *
     C                     EXSR SRINS1
     C                     EXSR SRINWI
      *
      ** Move records in test harnest file to screen fields.
      *
     C                     EXSR SRMOVK
      *
      ** Validate screen fields. If no errors are found, update
      ** manual facility utilisation  and facility files.
      *
     C                     EXSR SRVALI
     C           WIERR     IFEQ 'N'
      *
     C                     EXSR SRINSD
     C                     EXSR SRINWD
     C                     EXSR SRDHDR
      *
     C           SACTN     IFNE 'I'
     C                     EXSR SRRTRV
     C                     ENDIF
     C                     EXSR SRMOVD
      *
     C                     EXSR SRVALD
     C           WDERR     IFEQ 'N'
     C                     EXSR SRUPMF
     C                     ENDIF
     C                     ENDIF
      *
      ** Update test-harness file for warnings.
      *
     C           TMSVR     IFNE 'E'
     C           *INLR     ANDNE'1'
     C                     UPDATLE2140D0
     C                     ENDIF
      *
      ** Read next record.
      *
     C                     READ LE2140PD                 83
     C                     ENDDO
      *
     C                     ENDSR
      /TITLE SR/SRMOVK
      *****************************************************************
      *  SRMOVK  -  Move Test-Harness key fields to screen format     *
      *  Called by: SRPLAY                                            *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRMOVK    BEGSR
      *
      ** Initialise detail screen fields.
      *
     C                     MOVE *BLANKS   DSCRNA
      *
     C                     MOVE TACTN     SACTN
     C                     MOVE TCNUM     SCNUM
     C                     MOVE TFTYP     SFTYP
     C                     MOVE TFNUM     SFNUM
     C                     MOVE TFSEQ     SFSEQ
      *
     C                     ENDSR
      /TITLE SR/SRMOVD
      *****************************************************************
      *  SRMOVD  -  Move Test-Harness-file format to screen format    *
      *  Called by: SRPLAY                                            *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRMOVD    BEGSR
      *
     C                     SELEC
     C           SACTN     WHEQ 'I'
     C                     MOVE TUCCY     SUCCY
     C                     MOVE TUAMT     SUAMT
     C                     MOVE TAMTS     SAMTS                           148360
     C                     MOVE TSTDT     SSTDT
     C                     MOVE TEXDT     SEXDT
     C                     MOVE TUXRT     SUXRT
     C                     MOVE TUXID     SUXID
     C                     MOVE TUNAR     SUNAR
     C           SACTN     WHEQ 'A'
     C                     MOVE TUAMT     SUAMT
     C                     MOVE TAMTS     SAMTS                           148360
     C                     MOVE TEXDT     SEXDT
     C                     MOVE TUNAR     SUNAR                           138870
     C                     ENDSL
      *
     C                     ENDSR
      /TITLE SR/SRUPUD
      *****************************************************************
      *  SRUPUD  -  Update test-harness file - DELETE                 *
      *  Called by: SRUPMF                                            *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRUPUD    BEGSR
      *
     C                     MOVE SDACT     UCHTP
     C                     MOVE WRUND     UTLCD
     C                     MOVE 'R'       URECI
     C                     MOVE 'D'       UUSTS
     C                     UPDATLEMNFUP4
      *
     C                     ENDSR
      /TITLE SR/SRUPUX
      *****************************************************************
      *  SRUPUX  -  Update test-harness file - AUTHORISE              *
      *  Called by: SRUPMF                                            *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRUPUX    BEGSR
      *
     C                     MOVE WRUND     UTLCD
     C                     MOVE 'A'       UUSTS
     C                     MOVE USER      UXUSR
     C                     UPDATLEMNFUP4
      *
     C                     ENDSR
      /TITLE SR/SRUPUA
      *****************************************************************
      *  SRUPUA  -  Update test-harness file - AMEND                  *
      *  Called by: SRUPMF                                            *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRUPUA    BEGSR
      *
     C                     MOVE USER      UAUSR
     C***********          MOVE UUAMT     WPUAM 130                       157050
      *                                                                                       195125
      ** Before this subroutine is being called in SRUPMF, utilisation                        195125
      ** detail file is being access again to get the record for up                           195125
      ** date thus, utilisation detail fields are reinitialised and no                        195125
      ** longer the ones being updated in SRUPMF.                                             195125
      **                                                                                      195125
      ***If*CLE009*is*on*then*fields*have*already*been*set*up*in*SRUPMF                189904 195125
     C********** CLE009    IFEQ 'N'                                                    189904 195125
     C**********           MOVE UUAMT     WPUAM  130               157050 189904
      * Fields prefixed with U are for update of file.                    189904
     C                     MOVE UAMT      WPUAM  130                      189904
      *                                                                   157050
      ** Do not update UPUAM as this interfere with the manual            157050
      ** utilisation processing in COB.                                   157050
      *                                                                   157050
     C***********          MOVE UUAMT     UPUAM                           157050
     C                     MOVE WUAMT     UUAMT
     C**********           MOVE UEXDT     WOEXD   50                      189904
     C                     MOVE EXDT      WOEXD   50                      189904
     C                     MOVE WEXDT     UEXDT
     C                     MOVE STDT      USTDT                           189904
     C**********           ENDIF                                                       189904 195125
     C                     MOVE WRUND     UUADT
     C                     MOVE WRUND     UTLCD
     C                     MOVE SDACT     UCHTP
     C                     MOVE SUNAR     UUNAR                           138870
     C           WMFAU     IFEQ 'Y'
     C           USTS      IFEQ 'A'
     C           USTS      OREQ 'R'                                       194509
     C                     MOVE 'R'       UUSTS
     C                     ELSE
     C                     MOVE 'C'       UUSTS
     C                     ENDIF
     C                     ELSE
     C                     MOVE 'A'       UUSTS
     C                     ENDIF
     C                     UPDATLEMNFUP4
      *
     C                     ENDSR
      /TITLE SR/SRUPUI
      *****************************************************************
      *  SRUPUI  -  Update test-harness file - INSERT                 *
      *  Called by: SRUPMF                                            *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRUPUI    BEGSR
      *
     C                     MOVE 'D'       URECI
     C                     MOVE MBRCA     UBRCA
     C                     MOVE WKCNO     UCNUM
     C                     MOVE SDFTP     UFACT
     C                     MOVE SDFNO     UFCNO
     C                     MOVE SUCCY     UUCCY
     C                     MOVE WSTDT     USTDT
     C                     MOVE WEXDT     UEXDT
     C                     MOVE WUAMT     UUAMT
     C                     MOVE WUXRT     UUXRT
     C                     MOVE SUXID     UUXID
     C                     MOVE SUNAR     UUNAR
     C                     MOVE USER      UIUSR
     C                     Z-ADDWRUND     UORED
     C                     MOVE SDACT     UCHTP
      *
     C                     Z-ADD0         UUADT
     C                     Z-ADD0         UPUAM
     C                     MOVE *BLANKS   UAUSR
     C                     MOVE *BLANKS   UXUSR
     C                     MOVE WRUND     UTLCD
      *
     C           WMFAU     IFEQ 'Y'
     C                     MOVE 'C'       UUSTS
     C                     ELSE
     C                     MOVE 'A'       UUSTS
     C                     ENDIF
      *                                                                                       197515
      ** Initialise WKRSQ field to zero.                                                      197515
      *                                                                                       197515
     C                     Z-ADD0         WKRSQ                                               197515
      *
      ** Automatically generate a new sequence number.
      *
     C           KEYFU3    SETLLLEMNFUL3
     C                     READ LEMNFUL3                 85
     C           *IN85     IFEQ *ON
     C           SQNO      ANDGT1
     C                     Z-ADD1         USQNO
      *
     C                     ELSE
     C**********           Z-ADD0         WTSQN   30                                        AR674226
     C**********           Z-ADD1         WDIFF   30                                        AR674226
     C                     Z-ADD0         WTSQN   40                                        AR674226
     C                     Z-ADD1         WDIFF   40                                        AR674226
     C           *IN85     DOWEQ*OFF
     C           DMFUTB    ANDEQDMFUTA
     C           WDIFF     ANDEQ1
     C           SQNO      SUB  WTSQN     WDIFF
     C           WDIFF     IFGT 1
     C                     LEAVE
     C                     ENDIF
     C                     Z-ADDSQNO      WTSQN
     C                     READ LEMNFUL3                 85
     C                     ENDDO
     C           WTSQN     ADD  1         USQNO
     C                     MOVE USQNO     SGSQN
     C                     MOVE '1'       *IN24
     C/COPY WNCPYSRC,LE2140C005
     C                     ENDIF
      *
      ** Write to utilisation file.
      *
     C                     WRITELEMNFUP4
      *
     C                     ENDSR
      /TITLE SR/SRUPTH
      *****************************************************************
      *  SRUPTH  -  Write to test-harness file                        *
      *  Called by: SRUPMF                                            *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRUPTH    BEGSR
      *
      ** Initialise test-harness file fields to blanks.
      *
     C                     MOVE *BLANKS   L2140F
      *
      ** Get date and time stamps.
      *
     C                     Z-ADDWRUND     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     WDATE
     C                     MOVELWDD       WFLD3A  3
     C                     MOVE '/'       WFLD3A
     C                     MOVELWMM       WFLD3B  3
     C                     MOVE '/'       WFLD3B
     C                     MOVELWFLD3A    WFLD6A  6
     C                     MOVE WFLD3B    WFLD6A
     C                     MOVELWFLD6A    WDTST   8
     C                     MOVE WYY       WDTST
     C                     MOVELWDTST     TDTST
      *
     C                     TIME           WTIME   60
     C                     MOVELWTIME     WTIMEA  6
     C                     MOVELWHH       WFLD3A  3
     C                     MOVE ':'       WFLD3A
     C                     MOVELWNN       WFLD3B  3
     C                     MOVE ':'       WFLD3B
     C                     MOVELWFLD3A    WFLD6A  6
     C                     MOVE WFLD3B    WFLD6A
     C                     MOVELWFLD6A    WTMST   8
     C                     MOVE WSS       WTMST
     C                     MOVELWTMST     TTMST
      *
      ** Get common details.
      *
     C                     MOVE SDACT     TACTN
     C                     MOVE SDCNO     TCNUM
     C                     MOVE SDFTP     TFTYP
     C                     MOVE SDFNO     TFNUM
     C                     MOVE SDFSQ     TFSEQ
      *
     C                     SELEC
     C           SDACT     WHEQ 'I'
     C           SDACT     OREQ 'A'
     C                     MOVE SUCCY     TUCCY
     C                     MOVE SUAMT     TUAMT
     C                     MOVE SAMTS     TAMTS                           148360
     C                     MOVE SSTDT     TSTDT
     C                     MOVE SEXDT     TEXDT
     C                     MOVE SUXRT     TUXRT
     C                     MOVE SUXID     TUXID
     C                     MOVE SUNAR     TUNAR
     C                     MOVE USER      TIUSR
      *
     C           SDACT     WHEQ 'X'
     C                     MOVE USER      TXUSR
     C                     ENDSL
      *
      ** Write to the test-harness file.
      *
     C                     WRITELE2140D0
      *
     C                     ENDSR
      /TITLE SR/SRDETS
      *****************************************************************
      *  SRDETS  -  Initial screen called from subfile                *
      *  Called by: SRSUBF                                            *
      *  Calls    :                                                   *
      *  SRINS1  -  Initialise main screen fields                     *
      *  CLEAR   -  Clears message from the program message queue     *
      *  SRINWI  -  Initialise main work fields                       *
      *  SRVALI  -  Initial validation                                *
      *  SRDETL  -  Detail processing                                 *
      *****************************************************************
     C           SRDETS    BEGSR
      *
     C                     EXSR SRINS1
     C           WSEXT     DOUEQ'Y'
      *
     C                     WRITELE2140C1
     C                     EXSR CLEAR
     C                     EXFMTLE2140F1
      *
      ** Initialise work fields
      *
     C                     EXSR SRINWI
      *
      ** F3/F12 pressed.
      *
     C           *INKC     IFEQ '1'
     C           *INKL     OREQ '1'
     C                     LEAVE
     C                     ENDIF
      *
      ** Validate Facility details. If error exists, redisplay intial
      ** screen. Otherwise go to Detail Screen.
      *
     C                     EXSR SRVALI
     C           WIERR     IFEQ 'N'
     C                     EXSR SRDETL
     C                     EXSR SRINS1
     C                     MOVE 'N'       WSUBF
     C                     MOVE 'Y'       WSEXT
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     MOVE '0'       *IN25
     C                     MOVEA'0000'    *IN,35
      *
     C                     ENDSR
      /TITLE SR/SRVALI
      *****************************************************************
      *  SRVALI  -  Initial Validation                                *
      *  Called by: SRINRC, SRPLAY, SRDETS                            *
      *  Calls    :                                                   *
      *  ZASNMS  -  Sends message to the program message queue        *
      *  SRAUTH  -  Validate authorisation action                     *
      *  SRUAUT  -  Check if user is authorised to the action         *
      *****************************************************************
     C           SRVALI    BEGSR
      *
      ** Action code must be E, A, D, I or X.
      *
     C           SACTN     IFNE 'E'
     C           SACTN     ANDNE'A'
     C           SACTN     ANDNE'D'
     C           SACTN     ANDNE'I'
     C           SACTN     ANDNE'X'
     C                     MOVE *ON       *IN30
     C                     MOVEL'LEU0001' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
      *
      ** Validate Customer Number
      *
      ** (1) Customer must not be blank
      *
     C           SCNUM     IFEQ *BLANK
     C                     MOVE '1'       *IN31
     C                     MOVEL'LEU0003' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
      *
      ** (2) Customer must exist Customer File
      *
     C                     MOVELSCNUM     PCUST
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM           PCUST  10
     C                     PARM *BLANKS   PKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           PRTCD     IFEQ *BLANK
     C                     MOVE *BLANKS   SCNUM
     C                     MOVELBBCUST    SCNUM
      *
     C                     ELSE
     C                     MOVE '1'       *IN31
     C                     MOVEL'LEU0004' ZAMSID
     C                     EXSR ZASNMS
     C           PRTCD     IFEQ '*NOSEL  '
     C                     MOVE *BLANKS   SCNUM
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** Check Facility type if numeric
      *
     C           SFTYP     IFEQ *BLANKS
     C                     MOVE '1'       *IN32
     C                     MOVEL'LEU0005' ZAMSID
     C                     EXSR ZASNMS
      *
     C                     ELSE
     C                     MOVELSFTYP     WNUM4   40
     C                     MOVELWNUM4     WALF3   3
      *
     C           SFTYP     IFNE WALF3
     C                     MOVE '1'       *IN32
     C                     MOVEL'LEU0006' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
      *
      ** Validate Facility Number.
      *
     C           SFNUM     IFEQ *BLANKS
     C                     MOVE '1'       *IN33
     C                     MOVEL'LEU0007' ZAMSID
     C                     EXSR ZASNMS
      *
     C                     ELSE
     C                     MOVELSFNUM     WNUM3   30
     C                     MOVELWNUM3     WALF2   2
      *
     C           SFNUM     IFNE WALF2
     C                     MOVE '1'       *IN33
     C                     MOVEL'LEU0008' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
      *
      ** Check if input references a valid facility. If it does,
      ** save the facility details. Otherwise, send an error message.
      *
     C           *IN31     IFEQ '0'
     C           *IN32     ANDEQ'0'
     C           *IN33     ANDEQ'0'
      *
     C                     MOVE BBCUST    WKCNO
     C                     MOVE SFTYP     WKFTP
     C                     MOVE SFNUM     WKFNO
     C                     MOVE 'A'       WKRCT
     C           KEYFAC    CHAINFCLTY               N50
     C           *IN50     IFEQ *ON
     C                     MOVEA'111'     *IN,31
     C                     MOVEL'LEU0012' ZAMSID
     C                     EXSR ZASNMS
      *                                                                   128953
      **  Determine if Facility allows Manual Utilisation                 128953
      *                                                                   128953
     C                     ELSE                                           128953
      *                                                                   133916
      **  Restrict Manual Utilisation if Credit Agreement Facility        133916
      *                                                                   133916
     C           MTRCA     IFEQ 'CA'                                      133916
     C                     MOVEA'111'     *IN,31                          133916
     C                     MOVEL'LEU0060' ZAMSID                          133916
     C                     EXSR ZASNMS                                    133916
      *                                                                   133916
     C                     ELSE                                           133916
      *                                                                   133916
     C                     MOVEA*BLANKS   DRAW                            128953
     C           CLE028    IFEQ 'Y'                                                           CLE028
     C                     MOVEADTPX      DRAW                                                CLE028
     C                     Z-ADD6         WLIMIT  10                                          CLE028
     C                     ELSE                                                               CLE028
     C                     MOVEADTPS      DRAW                            128953
     C                     Z-ADD5         WLIMIT                                              CLE028
     C                     ENDIF                                                              CLE028
      *                                                                   128953
     C                     Z-ADD1         W#NO    10                      128953
     C                     MOVE 'N'       W#FLAG  1                       128953
      *                                                                   128953
     C********** W#NO      DOWLE5                                                      128953 CLE028
     C           W#NO      DOWLEWLIMIT                                                        CLE028
     C           W#FLAG    ANDEQ'N'                                       128953
     C           DRAW,W#NO IFEQ 'M'                                       128953
     C                     MOVE 'Y'       W#FLAG                          128953
     C                     ENDIF                                          128953
      *                                                                   128953
     C                     ADD  1         W#NO                            128953
      *                                                                   128953
     C                     ENDDO                                          128953
      *                                                                   128953
      **  Generate Message if Manual Utilisation is restricted            128953
      *                                                                   128953
     C           W#FLAG    IFEQ 'N'                                       128953
     C           DTPS      ANDNE*BLANKS                                   128953
     C           CLE028    ANDEQ'N'                                                           CLE028
     C           W#FLAG    OREQ 'N'                                                           CLE028
     C           DTPX      ANDNE*BLANKS                                                       CLE028
     C           CLE028    ANDEQ'Y'                                                           CLE028
     C                     MOVEA'111'     *IN,31                          128953
     C                     MOVEL'LEU0059' ZAMSID                          128953
     C                     EXSR ZASNMS                                    128953
     C                     ENDIF                                          128953
     C                     ENDIF                                          133916
     C                     ENDIF
     C                     ENDIF
      *
      ** Validate Sequence Number.
      *
     C           SACTN     IFEQ 'I'
     C           SFSEQ     IFNE *BLANKS
     C                     MOVE '1'       *IN34
     C                     MOVEL'LEU0009' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
      *
     C                     ELSE
     C           SFSEQ     IFEQ *BLANKS
     C                     MOVE '1'       *IN34
     C                     MOVEL'LEU0010' ZAMSID
     C                     EXSR ZASNMS
      *
     C                     ELSE
     C                     MOVELSFSEQ     WNUM4
     C                     MOVELWNUM4     WALF3     P
     C           SFSEQ     IFNE WALF3
     C                     MOVE '1'       *IN34
     C                     MOVEL'LEU0011' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** Check if facility utilisation entered exists.
      *
     C           WERRF     IFEQ 'N'
     C           SACTN     ANDNE'I'
     C                     MOVE SFSEQ     WKRSQ
     C           KEYFU3    CHAINLEMNFUL3             81
     C           *IN81     IFEQ *ON
     C                     MOVEA'1111'    *IN,31
     C                     MOVEL'LEU0018' ZAMSID
     C                     EXSR ZASNMS
      *
      ** Check if utilisation cannot be amended.
      *
     C                     ELSE
     C           SACTN     IFEQ 'A'
     C           USTS      ANDEQ'D'
     C                     MOVEA'1111'    *IN,31
     C                     MOVEL'LEU0019' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
      *
      ** Check if utilisation cannot be authorised.
      *
     C           SACTN     IFEQ 'X'
     C                     EXSR SRAUTH
     C                     ENDIF
      *
      ** Check if utilisation cannot be deleted.
      *
     C           SACTN     IFEQ 'D'
     C           USTS      ANDEQ'D'
     C                     MOVEA'1111'    *IN,31
     C                     MOVEL'LEU0020' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** Check user-authority.
      *
     C           WERRF     IFEQ 'N'
     C           PMODE     ANDNE'P'
     C                     MOVE SACTN     WACTN   1
     C                     EXSR SRUAUT
     C                     ENDIF
      *
     C           WERRF     IFEQ 'Y'
     C                     MOVE 'Y'       WIERR   1
     C                     ENDIF
      *
     C                     ENDSR
      /TITLE SR/SRVALR
      *****************************************************************
      *  SRVALR  -  Validate 'Review From' fields                     *
      *  Called by: SRINRC, SRREVS                                    *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRVALR    BEGSR
      *
      ** Check Customer number if numeric.
      *
     C********** SRCUS     IFNE *BLANKS                                                       CSD027
     C**********           MOVELSRCUS     WNUM7   70                                          CSD027
     C**********           MOVELWNUM7     WALF6   6                                           CSD027
     C********** SRCUS     IFNE WALF6                                                         CSD027
     C**********           MOVE '1'       *IN35                                               CSD027
     C**********           MOVEL'LEU0013' ZAMSID                                              CSD027
     C**********           EXSR ZASNMS                                                        CSD027
     C**********           ENDIF                                                              CSD027
     C**********           ENDIF                                                              CSD027
      *
      ** Check Facility Type if numeric.
      *
     C           SRTYP     IFNE *BLANKS
     C                     MOVELSRTYP     WNUM4
     C                     MOVELWNUM4     WALF3
     C           SRTYP     IFNE WALF3
     C                     MOVE '1'       *IN36
     C                     MOVEL'LEU0014' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
      *
      ** Check Facility Number if numeric.
      *
     C           SRNUM     IFNE *BLANKS
     C                     MOVELSRNUM     WNUM3
     C                     MOVE *BLANKS   WALF2
     C                     MOVELWNUM3     WALF2
     C           SRNUM     IFNE WALF2
     C                     MOVE '1'       *IN37
     C                     MOVEL'LEU0015' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
      *
      ** Requiring Authorisation field must be 'Y' or 'N' only
      *
     C           SRAUT     IFNE 'Y'
     C           SRAUT     ANDNE'N'
     C                     MOVE '1'       *IN38
     C                     MOVEL'LEU0016' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
      *
     C           WERRF     IFEQ 'Y'
     C                     MOVE 'Y'       WVERR   1
     C                     ENDIF
      *
     C                     ENDSR
      /TITLE SR/SRDHDR
      *****************************************************************
      *  SRDHDR  -  Move fields to detail screen header fields        *
      *  Called by: SRDETL, SRPLAY                                    *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRDHDR    BEGSR
      *
     C           WSUBF     IFEQ 'Y'
     C           *IN25     ANDEQ'0'
     C                     MOVE SLSEL     SDACT
     C                     MOVE SSCUS     SDCNO
     C                     MOVE SSTYP     SDFTP
     C                     MOVE SSNUM     SDFNO
     C                     MOVE SSUSQ     SDFSQ
      *
     C                     ELSE
     C                     MOVE SACTN     SDACT
     C                     MOVE WKCNO     SDCNO
     C                     MOVE SFTYP     SDFTP
     C                     MOVE SFNUM     SDFNO
     C                     MOVE SFSEQ     SDFSQ
     C                     ENDIF
      *
      ** Get facility utilisation status
      *
     C           SACTN     IFEQ 'I'
     C                     MOVE *BLANKS   SDSTS
      *
     C                     ELSE
     C                     MOVE SDCNO     WUCNO
     C                     MOVE SDFTP     WUFTP
     C                     MOVE SDFNO     WUFNO
     C                     MOVE SDFSQ     WURSQ
     C           KEYUTI    CHAINLEMNFUL3             81
     C           *IN81     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'LEMNFUPD'DBFILE           ************
     C                     Z-ADD5         DBASE            * DBERR 05 *
     C                     MOVELDMNFUT    DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     SELEC
     C           USTS      WHEQ 'C'
     C                     MOVEL'LEU0054' ZAMSID
     C           USTS      WHEQ 'R'
     C                     MOVEL'LEU0056' ZAMSID
     C           USTS      WHEQ 'A'
     C                     MOVEL'LEU0057' ZAMSID
     C           USTS      WHEQ 'D'
     C                     MOVEL'LEU0058' ZAMSID
     C                     ENDSL
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    SDSTS
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
      ** Get customer name.
      *
     C                     MOVE *BLANKS   PCUST
     C           WSUBF     IFEQ 'Y'
     C           *IN25     ANDEQ'0'
     C                     MOVELSSCUS     PCUST
     C                     ELSE
     C                     MOVE SCNUM     PCUST
     C                     ENDIF
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM           PCUST
     C                     PARM *BLANKS   PKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
     C           PRTCD     IFEQ *BLANKS
     C                     MOVE BBCSSN    SDCNM
     C                     ELSE
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCUSTPD'DBFILE           ************
     C                     Z-ADD6         DBASE            * DBERR 06 *
     C                     MOVEL'*FIRST'  DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      /TITLE SR/SRRTRV
      *****************************************************************
      *  SRRTRV  -  Retrieve details from utilisation file            *
      *  Called by: SRDETL, SRPLAY                                    *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRRTRV    BEGSR
      *
      ** Check if facility exists for the manual facility utilisation.
      *
     C           WSUBF     IFEQ 'Y'
     C                     MOVE SDCNO     WKCNO
     C                     MOVE SDFTP     WKFTP
     C                     MOVE SDFNO     WKFNO
     C                     MOVE 'A'       WKRCT
     C           KEYFAC    CHAINFCLTY               N50
     C           *IN50     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'FCLTYFM 'DBFILE           ************
     C                     Z-ADD7         DBASE            * DBERR 07 *
     C                     MOVELDFCLTY    DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
      *                                                                   128953
      **  Determine if Facility allows Manual Utilisation                 128953
      *                                                                   128953
     C                     ELSE                                           128953
     C                     MOVEA*BLANKS   DRAW                            128953
     C           CLE028    IFEQ 'Y'                                                           CLE028
     C                     MOVEADTPX      DRAW                                                CLE028
     C                     Z-ADD6         WLIMIT                                              CLE028
     C                     ELSE                                                               CLE028
     C                     MOVEADTPS      DRAW                            128953
     C                     Z-ADD5         WLIMIT                                              CLE028
     C                     ENDIF                                                              CLE028
      *                                                                   128953
     C                     Z-ADD1         W#NO    10                      128953
     C                     MOVE 'N'       W#FLAG  1                       128953
      *                                                                   128953
     C********** W#NO      DOWLE5                                                      128953 CLE028
     C           W#NO      DOWLEWLIMIT                                                        CLE028
     C           W#FLAG    ANDEQ'N'                                       128953
     C           DRAW,W#NO IFEQ 'M'                                       128953
     C                     MOVE 'Y'       W#FLAG                          128953
     C                     ENDIF                                          128953
      *                                                                   128953
     C                     ADD  1         W#NO                            128953
      *                                                                   128953
     C                     ENDDO                                          128953
      *                                                                   128953
      **  Generate DBERROR if Manual Utilisation is restricted            128953
      *                                                                   128953
     C           W#FLAG    IFEQ 'N'                                       128953
     C           DTPS      ANDNE*BLANKS                                   128953
     C           CLE028    ANDEQ'N'                                                           CLE028
     C           W#FLAG    OREQ 'N'                                                           CLE028
     C           DTPX      ANDNE*BLANKS                                                       CLE028
     C           CLE028    ANDEQ'Y'                                                           CLE028
     C           *LOCK     IN   LDA                                       128953
     C                     MOVEL'FCLTYFM 'DBFILE           ************   128953
     C                     Z-ADD25        DBASE            * DBERR 25 *   128953
     C                     MOVELDFCLTY    DBKEY            ************   128953
     C                     OUT  LDA                                       128953
     C                     EXSR *PSSR                                     128953
     C                     ENDIF                                          128953
     C                     ENDIF
      *
      ** Get the manual facility utilisation details
      *
     C                     MOVE SDCNO     WUCNO
     C                     MOVE SDFTP     WUFTP
     C                     MOVE SDFNO     WUFNO
     C                     MOVE SDFSQ     WURSQ
     C           KEYUTI    CHAINLEMNFUL3             81
     C           *IN81     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'LEMNFUPD'DBFILE           ************
     C                     Z-ADD8         DBASE            * DBERR 08 *
     C                     MOVELDMNFUT    DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     ENDIF
      *
      ** Move fields from file to screen fields.
      *
     C                     MOVE UCCY      SUCCY
      *
      ** Format utilisation amount
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*BLANK  'PRTCD
     C                     PARM '*KEY    'POPTN
     C                     PARM SUCCY     PCURR   3
     C           SDCURR    PARM SDCURR    DSSDY
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCURRPD'DBFILE           ************
     C                     Z-ADD9         DBASE            * DBERR 09 *
     C                     MOVELSUCCY     DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     Z-ADDA6NBDP    ZADEC
     C                     MOVE *BLANKS   ZFIELD
      *                                                                   148360
     C           UAMT      IFLT *ZERO                                     148360
     C                     MOVE '-'       SAMTS                           148360
     C           UAMT      MULT -1        ZUAMT  130                      148360
     C                     MOVE ZUAMT     ZFIELD                          148360
     C                     ELSE                                           148360
      *                                                                   148360
     C                     MOVE UAMT      ZFIELD
     C                     ENDIF                                          148360
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    SUAMT
      *                                                                                       203082
      ** Save Utilisation Amount for 'A' and 'X' validation later                             203082
      *                                                                                       203082
     C                     Z-ADDUAMT      OOUAMT 130                                          203082
      *
      ** Format the Start date.
      *
     C                     Z-ADDSTDT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SSTDT
      *
      ** Format the Expiry date.
      *
     C           EXDT      IFEQ 0
     C                     MOVE *BLANKS   SEXDT
     C                     ELSE
     C                     MOVE EXDT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SEXDT
     C                     ENDIF
      *
      ** Format the Exchange rate.
      *
     C           UXRT      IFEQ 0
     C                     MOVE *BLANKS   SUXRT
     C                     ELSE
     C                     Z-ADD7         ZADEC
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE UXRT      ZFIELD
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    SUXRT
     C                     ENDIF
      *
     C                     MOVE UXID      SUXID
     C                     MOVELUNAR      SUNAR
      *
     C                     ENDSR
      /TITLE SR/SRVALD
      *****************************************************************
      ***SRVALA**-**Validate*detail*fields*****************************   190570
      *  SRVALD  -  Validate detail fields                            *   190570
      *  Called by: SRDETL, SRPLAY                                    *
      *  Calls    :                                                   *
      *  ZASNMS  -  Sends message to the program message queue        *
      *****************************************************************
     C           SRVALD    BEGSR
      *
      ** (1) Check if currency is blank.
      *
     C           SUCCY     IFEQ *BLANKS
     C                     MOVEL'LEU0026' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN43
      *
      ** (2) Check if currency exists in currency file.
      *
     C                     ELSE
     C                     CALL 'AOCURRR0'
     C                     PARM '*BLANK  'PRTCD
     C                     PARM '*KEY    'POPTN
     C                     PARM SUCCY     PCURR   3
     C           SDCURR    PARM SDCURR    DSSDY
     C           PRTCD     IFNE *BLANKS
     C                     MOVEL'LEU0027' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN43
      *
      ** (3) For single currency facility, check if currency is
      **  equal to facility currency.
      *
     C                     ELSE
      *
     C                     MOVE *BLANKS   SUCCY
     C                     MOVE A6CYCD    SUCCY
      *
     C           MMCCY     IFEQ 'N'
     C           SUCCY     ANDNEMFCCY
     C                     MOVEL'LEU0028' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN43
     C                     ENDIF
      *
      ** (4) For multi-currency facility, check if currency is equal
      ** to facility or to any allowable currencies.
      *
      * Save Permitted currencies against the multi-currency facility     190570
      *                                                                   190570
     C           MCSEL     IFEQ 'Y'                                       190570
     C                     MOVEAMALCY     WCY                             190570
      *                                                                   190570
     C           MMCCY     IFEQ 'Y'
     C           SUCCY     ANDNEMFCCY                                     190570
      * If MFU is not in facility currency, check if ccy is allowed       190570
     C********** PRTCD     ANDNE*BLANKS                                   190570
     C                     Z-ADD0         X1      20                      190570
      *                                                                   190570
     C                     DO   9                                         190570
     C           SUCCY     LOKUPWCY                      16               190570
     C                     ENDDO                                          190570
     C           *IN16     IFEQ *ON                                       190570
     C                     MOVE *OFF      *IN16                           190570
     C                     ELSE                                           190570
     C                     MOVE '1'       *IN16                           190570
     C                     MOVEL'LEU0029' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN43
     C                     ENDIF                                          190570
     C                     ENDIF
     C                     ENDIF                                          190570
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** (1) Check if amount is blank.
      *
     C           SUAMT     IFEQ *BLANKS
     C                     MOVEL'LEU0030' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN44
      *
      ** (2) Check if amount is valid.
      *
     C                     ELSE
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELSUAMT     ZFIELD
     C                     Z-ADDA6NBDP    ZADEC
     C           13        SUB  ZADEC     ZADIG
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    WUAMT  130
      *
     C           *IN99     IFEQ '1'
     C           WUAMT     OREQ 0
     C                     MOVE '1'       *IN44
     C                     MOVEL'LEU0031' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
      *                                                                   148360
      ** (1) Check if utilisation amount sign is '+' or '-'               148360
      *                                                                   148360
     C           SAMTS     IFNE *BLANK                                    148360
     C           SAMTS     ANDNE'+'                                       148360
     C           SAMTS     ANDNE'-'                                       148360
     C                     MOVE '1'       *IN51                           148360
     C                     MOVEL'LEU0061' ZAMSID                          148360
     C                     EXSR ZASNMS                                    148360
     C                     ENDIF                                          148360
      *                                                                   148360
      ** Change the sign of the amount if necessary                       148360
      *                                                                   148360
     C           *IN44     IFEQ *OFF                                      148360
     C           *IN51     ANDEQ*OFF                                      148360
     C           SAMTS     ANDEQ'-'                                       148360
     C                     Z-SUBWUAMT     WUAMT                           148360
     C                     ENDIF                                          148360
      *
      ** (1) Check if start date is blank.
      *
     C           SSTDT     IFEQ *BLANKS
     C                     MOVEL'LEU0032' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN45
      *
      ** (2) Check if start date is in valid date format.
      *
     C                     ELSE
      *
     C                     Z-ADD0         NUM7    70
     C                     MOVE *BLANKS   ALF6    6
     C                     MOVELSSTDT     NUM7
     C                     MOVELNUM7      ALF6
      *
     C           SSTDT     IFNE ALF6
     C                     MOVEL'LEU0035' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN45
      *
     C                     ELSE
      *
     C                     MOVE SSTDT     ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    WSTDT   50
      *
     C           *IN99     IFEQ '1'
     C                     MOVEL'LEU0035' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN45
      *
     C                     ELSE
      *
      ** (3) Check if start date is earlier than approval date.
      *
     C           WSTDT     IFLT MDTAP
     C                     MOVEL'LEU0033' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN45
      *
      ** (4) Check if start date is a holiday.
      *
     C                     ENDIF                                                              204392
     C**********           ELSE                                                               204392
      *                                                                                       204392
     C           WSTDT     IFGT DTEX                                                          204392
     C                     MOVEL'LEU0064' ZAMSID                                              204392
     C                     EXSR ZASNMS                                                        204392
     C                     MOVE '1'       *IN45                                               204392
     C                     ENDIF                                                              204392
      *
     C                     MOVE WSTDT     ZDAYNO
     C                     MOVE SUCCY     ZCCY
     C                     EXSR ZCHKH
     C           ZIND      IFEQ 'H'
     C           *IN43     ANDEQ*OFF
     C           SACTN     ANDNE'D'
     C           WSTDT     ANDNEWPSDT
     C                     MOVE 'Y'       WWERR
     C                     MOVE '1'       *IN45
     C                     MOVEL'LEU0034' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C**********           ENDIF                                                              204392
     C                     ENDIF
      *
     C                     ENDIF
     C                     Z-ADDWSTDT     WPSDT   50
     C                     ENDIF
      *
     C                     Z-ADD0         WEXDT                           139313
      ** (1) On amend, check if expiry date is blanked when expiry
      ** date had already been entered before.
      *
     C           SACTN     IFEQ 'A'
     C           SEXDT     ANDEQ*BLANKS
     C           EXDT      ANDNE0
     C           ORED      IFEQ BJRDNB                                    150746
     C                     Z-ADD0         WEXDT                           150746
     C                     ELSE                                           150746
     C                     MOVE '1'       *IN46
     C                     MOVEL'LEU0036' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF                                          150746
      *
      ** (2) Check if expiry date is in valid date format.
      *
     C                     ELSE
     C           SEXDT     IFNE *BLANKS
      *
     C                     Z-ADD0         NUM7
     C                     MOVE *BLANKS   ALF6
     C                     MOVELSEXDT     NUM7
     C                     MOVELNUM7      ALF6
      *
     C           SEXDT     IFNE ALF6
      *
     C                     MOVEL'LEU0037' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN46
      *
     C                     ELSE
      *
     C                     MOVE SEXDT     ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    WEXDT   50
      *
     C           *IN99     IFEQ '1'
     C                     MOVEL'LEU0037' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN46
     C                     ELSE
      *
      ** (3) Check if expiry date is earlier than start date.
      *
     C           WEXDT     IFLT WSTDT
     C                     MOVEL'LEU0038' ZAMSID
     C                     EXSR ZASNMS
     C                     MOVE '1'       *IN46
      *
      ** (4) Check if expiry date is a holiday.
      *
     C                     ELSE
     C                     MOVE WEXDT     ZDAYNO
     C                     MOVE SUCCY     ZCCY
     C                     EXSR ZCHKH
     C           ZIND      IFEQ 'H'
     C           *IN43     ANDEQ*OFF
     C           SACTN     ANDNE'D'
     C           WEXDT     ANDNEWPEDT
     C                     MOVE '1'       *IN46
     C                     MOVE 'Y'       WWERR
     C                     MOVEL'LEU0039' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     Z-ADDWEXDT     WPEDT   50
     C                     ENDIF
      *                                                                                       204392
     C           WEXDT     IFGT DTEX                                                          204392
     C                     MOVEL'LEU0065' ZAMSID                                              204392
     C                     EXSR ZASNMS                                                        204392
     C                     MOVE '1'       *IN46                                               204392
     C                     ENDIF                                                              204392
      *
     C                     Z-ADD0         WUXRT                           139313
      ** (1) Check if exchange rate is not blank for single currency
      ** facility.
      *
     C           *IN43     IFEQ '0'
     C           MMCCY     IFEQ 'N'
     C           SUXRT     ANDNE*BLANKS
     C                     MOVE '1'       *IN47
     C                     MOVEL'LEU0040' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
      *
      ** (2) Check if exchange rate is not blank for multi currency
      ** facility and where facility currency is equal to utilisation
      ** currency
      *
     C           MMCCY     IFEQ 'Y'
      *
     C           SUCCY     IFEQ MFCCY
     C           SUXRT     ANDNE*BLANKS
     C                     MOVE '1'       *IN47
     C                     MOVEL'LEU0041' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
      *
      ** (3) Check if rate is blank when utilisation and facility
      ** currencies are different.
      ** Extract spot rate if blank.                                                        BUG23133
      *
     C           SUCCY     IFNE MFCCY
     C           SUXRT     ANDEQ*BLANKS
     C**********           MOVE '1'       *IN47                                             BUG23133
     C**********           MOVEL'LEU0049' ZAMSID                                            BUG23133
     C**********           EXSR ZASNMS                                                      BUG23133
     C                     EXSR SREXCH                                                      BUG23133
     C                     ENDIF
      *
      ** (4) Check if rate is not valid.
      ** If valid, check if it is within range of tolerance rate.                           BUG23133
      *
     C           SUCCY     IFNE MFCCY
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELSUXRT     ZFIELD
     C                     Z-ADD7         ZADEC
     C                     Z-ADD4         ZADIG
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    WUXRT  117
      *
     C           *IN99     IFEQ '1'
     C           WUXRT     OREQ 0
     C                     MOVE '1'       *IN47
     C                     MOVEL'LEU0042' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE                                                             BUG23133
     C                     EXSR SRTOLR                                                      BUG23133
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** (1) Check if mult/div indicator is not blank for single
      ** currency facility.
      *
     C           MMCCY     IFEQ 'N'
     C           SUXID     ANDNE*BLANKS
     C                     MOVE '1'       *IN48
     C                     MOVEL'LEU0043' ZAMSID
     C                     EXSR ZASNMS
     C                     ELSE
      *
      ** (2) Check if mult/div indicator is not blank of multi-currency
      ** facility and where facility currency is equal to utilisation
      ** currency
      *
     C           MMCCY     IFEQ 'Y'
      *
     C           SUCCY     IFEQ MFCCY
     C           SUXID     ANDNE*BLANKS
     C                     MOVE '1'       *IN48
     C                     MOVEL'LEU0044' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
      *
      ** (3) Check if mult/div indicator is blank when utilisation
      ** and facility currencies are different.
      *
     C           SUCCY     IFNE MFCCY
     C           SUXID     ANDEQ*BLANKS
     C                     MOVE '1'       *IN48
     C                     MOVEL'LEU0050' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
      *
      ** (4) Check if mult/div indicator is invalid.
      *
     C           SUCCY     IFNE MFCCY
     C           SUXID     ANDNE'M'
     C           SUXID     ANDNE'D'
     C********** SUXID     ORNE A6MDIN                                             BUG23133 MD020359
     C                     MOVE '1'       *IN48
     C                     MOVEL'LEU0045' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** (1) Check if amount entered is greater than the available
      ** facility amount at the start date of the utilisation.
      ** If yes, issue a warning message.
      *
     C           WERRF     IFEQ 'N'
     C           MMCCY     IFEQ 'Y'
     C           SUCCY     ANDNEMFCCY
      *
      ** Get the number of decimal places of the two currencies.
      *
     C                     MOVE MFCCY     PCCY    3
     C                     Z-ADD17        PDBASE
     C                     EXSR SRGETD
     C                     MOVE WDECN     CDPF    10
      *                                                                                       CLE023
     C                     MOVE A6MDIN    WFMDI   1                                           CLE023
     C                     Z-ADDA6SPRT    WFSPT  138                                          CLE023
      *                                                                                       CLE023
     C                     MOVE SUCCY     PCCY    3
     C                     Z-ADD18        PDBASE  30
     C                     EXSR SRGETD
     C                     MOVE WDECN     CDPU    10
      *                                                                                       CLE023
     C                     MOVE A6MDIN    WSMDI   1                                           CLE023
     C                     Z-ADDA6SPRT    WSSPT  138                                          CLE023
     C                     MOVE SUCCY     WCCYX   3                                           CLE023
      *
     C           CDPF      SUB  CDPU      CX      10
     C           CX        ADD  4         CX
     C           SUXID     COMP 'D'                      92
     C      92   WUXRT     DIV  POWR,CX   OFCXRT    H
     C      92   1         DIV  OFCXRT    OFCXRT    H
     C     N92   WUXRT     MULT POWR,CX   OFCXRT 159H
      *
      ** When Lending Enhancement CLE023 is switched on                                       CLE023
      ** compute utilisation amount with spot rate                                            CLE023
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
      *                                                                                       CLE023
     C                     MOVE WSMDI     ZMDI1   1                                           CLE023
     C                     Z-ADDWSSPT     ZRATE1 138                                          CLE023
     C                     MOVE WFMDI     ZMDI2   1                                           CLE023
     C                     Z-ADDWFSPT     ZRATE2 138                                          CLE023
     C                     EXSR ZXRATE                                                        CLE023
      *                                                                                       CLE023
     C           ZMDIX     IFEQ 'D'                                                           CLE023
     C           ZRATEX    DIV  POWR,CX   WFXRT     H                                         CLE023
     C           1         DIV  WFXRT     WFXRT     H                                         CLE023
     C                     ELSE                                                               CLE023
     C           ZRATEX    MULT POWR,CX   WFXRT  159H                                         CLE023
     C                     ENDIF                                                              CLE023
     C           WUAMT     MULT WFXRT     WSCAMT 130H                                         CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C           WUAMT     MULT OFCXRT    WCAMT  130H
      *
     C                     ELSE
     C                     MOVE WUAMT     WCAMT
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
     C                     Z-ADDWUAMT     WSCAMT                                              CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C                     ENDIF
      *
      ** Get the drawn amounts from FCLTYFN file.
      *
     C                     MOVE 'B'       WKRCT
     C           KEYFAC    CHAINFCLTY               N50
     C           *IN50     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'FCLTYFN 'DBFILE           ************
     C                     Z-ADD10        DBASE            * DBERR 10 *
     C                     MOVELDFCLTY    DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** If utilisation start date is within next 8 working days
      ** retrieve Drawn Amount Day (N+1) for Run Date + N working days.
      ** The available facility amount will be the difference between
      ** the Facility Amount and the Drawn Amount Day (N+1). Otherwise,
      ** available facility amount will be the difference between
      ** the Facility Amount and the Current Exposure Amount Drawn.
      *
     C           WSTDT     IFLE RUN9
     C           WSTDT     ANDGERUN0
     C                     DO   10        S       20
     C                     MOVE OAM,S     WOAMT  130
     C           RUNS,S    IFGE WSTDT
     C                     LEAVE
     C                     ENDIF
     C                     ENDDO
     C           MFAMT     SUB  WOAMT     WAAMT  130
      *
     C                     ELSE
      *
     C                     SELEC
     C           WSTDT     WHLT RUN0
     C           MFAMT     SUB  OAM1      WAAMT
     C           WSTDT     WHGT RUN9
     C           MFAMT     SUB  OA10      WAAMT
     C                     ENDSL
      *
     C                     ENDIF
      *
      ** Further check if there have been any increase and/or decrease
      ** in the facility amount.
      *
     C           KEYFAM    SETLLLEFCAML7
     C           KEYFAM    READELEFCAML7                 89
      *
     C           AVLDT     DOWLEWSTDT
     C           *IN89     ANDEQ'0'
      *
     C                     SELEC
     C           AFATP     WHEQ 'FI'
     C                     ADD  AAAMT     WAAMT
     C           AFATP     WHEQ 'FD'
     C                     SUB  AAAMT     WAAMT
     C                     ENDSL
      *
     C           KEYFAM    READELEFCAML7                 89
      *
     C                     ENDDO
      *                                                                                       203082
      ** For 'A'mend and 'X'authorise, validate against correct                               203082
      ** facility amount available.                                                           203082
      *                                                                                       203082
     C           SACTN     IFEQ 'A'                                                           203082
     C           SACTN     OREQ 'X'                                                           203082
     C                     ADD  OOUAMT    WAAMT                                               203082
     C                     ENDIF                                                              203082
      *
      ** Send warning message if converted amount is greater than
      ** the available facility amount.
      *
     C           WCAMT     IFGT WAAMT
     C           SACTN     ANDNE'D'
     C           WCAMT     ANDNEWPCAM
     C                     MOVE 'Y'       WWERR
     C                     MOVE '1'       *IN44
     C                     MOVEL'LEU0046' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     Z-ADDWCAMT     WPCAM  130
      *
      ** On amendment, convert the existing utilisation amount to
      ** facility currency for future reference.
      *
     C           SACTN     IFEQ 'A'
     C           MMCCY     IFEQ 'Y'
     C           SUCCY     ANDNEMFCCY
      *
      ** Get the number of decimal places of the two currencies.
      *
     C                     MOVE MFCCY     PCCY
     C                     Z-ADD19        PDBASE
     C                     EXSR SRGETD
     C                     MOVE WDECN     CDPF
      *                                                                                       CLE023
     C                     MOVE A6MDIN    WFMDI                                               CLE023
     C                     Z-ADDA6SPRT    WFSPT                                               CLE023
      *                                                                                       CLE023
     C                     MOVE SUCCY     PCCY
     C                     Z-ADD20        PDBASE
     C                     EXSR SRGETD
     C                     MOVE WDECN     CDPU
      *                                                                                       CLE023
     C                     MOVE A6MDIN    WSMDI                                               CLE023
     C                     Z-ADDA6SPRT    WSSPT                                               CLE023
     C                     MOVE SUCCY     WCCYX                                               CLE023
      *
     C           CDPF      SUB  CDPU      CX
     C           CX        ADD  4         CX
     C           SUXID     COMP 'D'                      92
     C      92   WUXRT     DIV  POWR,CX   OFCXRT    H
     C      92   1         DIV  OFCXRT    OFCXRT    H
     C     N92   WUXRT     MULT POWR,CX   OFCXRT    H
      *
     C           UAMT      MULT OFCXRT    WPAMT  130H
      *
      ** When Lending Enhancement CLE023 is switched on                                       CLE023
      ** compute utilisation amount with spot rate                                            CLE023
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
      *                                                                                       CLE023
     C                     MOVE WSMDI     ZMDI1                                               CLE023
     C                     Z-ADDWSSPT     ZRATE1                                              CLE023
     C                     MOVE WFMDI     ZMDI2                                               CLE023
     C                     Z-ADDWFSPT     ZRATE2                                              CLE023
     C                     EXSR ZXRATE                                                        CLE023
      *                                                                                       CLE023
     C           ZMDIX     IFEQ 'D'                                                           CLE023
     C           ZRATEX    DIV  POWR,CX   WFXRT     H                                         CLE023
     C           1         DIV  WFXRT     WFXRT     H                                         CLE023
     C                     ELSE                                                               CLE023
     C           ZRATEX    MULT POWR,CX   WFXRT     H                                         CLE023
     C                     ENDIF                                                              CLE023
     C           UAMT      MULT WFXRT     WSPAMT 130H                                         CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C                     ELSE
     C                     MOVE UAMT      WPAMT
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
     C                     Z-ADDUAMT      WSPAMT                                              CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** If error(s) exist(s), move 'Y' to detail error flag.
      *
     C           WERRF     IFEQ 'Y'
     C                     MOVE 'Y'       WDERR   1
     C                     MOVE 'N'       WSKIP
     C                     ENDIF
      *
     C                     ENDSR
      /TITLE SR/SRUPMF
      *****************************************************************
      *  SRUPMF  -  Update the Manual Utilisation file                *
      *  Called by: SRDETL, SRPLAY                                    *
      *  Calls    :                                                   *
      *  SRUPTH  -  Write to test-harness file                        *
      *  SRUPUI  -  Update test-harness file - INPUT                  *
      *  SRUPUA  -  Update test-harness file - AMEND                  *
      *  SRUPUX  -  Update test-harness file - AUTHORISE              *
      *  SRUPUD  -  Update test-harness file - DELETE                 *
      *  SRUPTR  -  Update utilisation trailer file                   *
      *  SRUPFA  -  Update the facility file                          *
      *  SRUPFS  -  Update the facility's superfacility               *
      *  SRFEES  -  Update Fees File                                  *   CLE009
      *****************************************************************
     C           SRUPMF    BEGSR
      *
      ** Write to test-harness file for "Record" mode.
      *
     C           PMODE     IFEQ 'R'
     C                     EXSR SRUPTH
     C                     ENDIF
      *                                                                   CLE009
      ** If CLE009 is installed, update fees file                         CLE009
      *                                                                   CLE009
     C           CLE009    IFEQ 'Y'                                       CLE009
     C           SACTN     IFEQ 'I'                                       CLE009
     C           SACTN     OREQ 'A'                                       CLE009
     C           SACTN     OREQ 'D'                                       CLE009
      ** Set up fields required for subroutine SRUPFA first:              189904
     C           SACTN     IFEQ 'I'                                       189904
     C                     MOVE WSTDT     USTDT                           189904
     C                     MOVE WEXDT     UEXDT                           189904
     C                     Z-ADDWRUND     UORED                           189904
     C                     ENDIF                                          189904
     C           SACTN     IFEQ 'A'                                       189904
     C                     MOVE UAMT      WPUAM                           189904
     C                     MOVE WUAMT     UUAMT                           189904
     C                     MOVE EXDT      WOEXD                           189904
     C                     MOVE WEXDT     UEXDT                           189904
     C                     MOVE STDT      USTDT                           189904
     C                     ENDIF                                          189904
     C                     EXSR SRUPFA                                    CLE009
     C                     ENDIF                                          CLE009
     C                     EXSR SRFEES                                    CLE009
     C                     ELSE                                           CLE009
     C                     MOVE *BLANKS   AUTH                            CLE009
     C                     MOVE *BLANKS   SWRI                            CLE009
     C                     ENDIF                                          CLE009
      *
      ** Update the Manual Facility Utilisation file.
      *
     C           SACTN     IFEQ 'I'
     C                     EXSR SRUPUI
     C                     ELSE
      *
     C                     MOVE SDCNO     WUCNO
     C                     MOVE SDFTP     WUFTP
     C                     MOVE SDFNO     WUFNO
     C                     MOVE SDFSQ     WURSQ
     C           KEYUTI    CHAINLEMNFUL4             88
     C           *IN88     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'LEMNFUPD'DBFILE           ************
     C                     Z-ADD11        DBASE            * DBERR 11 *
     C                     MOVELDMNFUT    DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     SELEC
     C           SACTN     WHEQ 'A'
     C                     EXSR SRUPUA
     C           SACTN     WHEQ 'X'
     C                     EXSR SRUPUX
     C           SACTN     WHEQ 'D'
     C                     EXSR SRUPUD
     C                     ENDSL
     C                     ENDIF
      *
      ** Update the Facility and Utilisation trailer files.
      *
     C           SACTN     IFEQ 'I'
     C           SACTN     OREQ 'A'
     C           SACTN     OREQ 'D'
     C                     EXSR SRUPTR
     C           CLE009    IFEQ 'N'                                       CLE009
     C                     EXSR SRUPFA
     C                     ENDIF                                          CLE009
      *
      ** If the facility has credit agreement/superfacility, update
      ** the superfacility in the same way as the facility.
      *
     C           MTRCA     IFEQ 'TR'
     C                     EXSR SRUPFS
     C                     ENDIF
     C                     ENDIF
      *
      ** Comit the changes.
      *
     C                     COMIT
      *
     C                     ENDSR
      *****************************************************************   CLE009
      * SRFEES  -  Update Fees File                                   *   CLE009
      * Called by: SRUPMF - Update the manual utilisation file        *   CLE009
      * Calls    : None                                               *   CLE009
      *****************************************************************   CLE009
     C           SRFEES    BEGSR                                          CLE009
      *                                                                   CLE009
      ** If authorisation switchable feature is on and action code        CLE009
      ** is insert or amend and manual facility utilisations              CLE009
      ** authorisation is required                                        CLE009
      *                                                                   CLE009
     C           CLE002    IFEQ 'Y'                                       CLE009
     C           SACTN     ANDNE'X'                                       CLE009
     C           SACTN     ANDNE'D'                                       CLE009
     C           BPFAUT    ANDEQ'Y'                                       CLE009
      *                                                                   CLE009
      ** If action code is insert                                         CLE009
      *                                                                   CLE009
     C           SACTN     IFEQ 'I'                                       CLE009
     C                     MOVE WKSWRI    SWRI                            CLE009
     C                     MOVE *BLANKS   AUTH                            CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
      ** If action code is amend                                          CLE009
      *                                                                   CLE009
     C           SACTN     IFEQ 'A'                                       CLE009
     C           ORED      IFLT BJRDNB                                    CLE009
     C                     MOVE WKSWRI    SWRI                            CLE009
     C                     MOVE 'Y'       AUTH                            CLE009
     C                     ELSE                                           CLE009
     C           WKSWRI    IFEQ 'C'                                       CLE009
     C           SWRI      ANDEQ*BLANKS                                   CLE009
     C                     MOVE WKSWRI    SWRI                            CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C                     ELSE                                           CLE009
      *                                                                   CLE009
      ** If action code is authorise                                      CLE009
      *                                                                   CLE009
     C           SACTN     IFEQ 'X'                                       CLE009
     C                     MOVE SWRI      WKSWRI                          CLE009
     C                     MOVE 'Y'       AUTH                            CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
      ** If action code is reverse                                        CLE009
      *                                                                   CLE009
     C           SACTN     IFEQ 'D'                                       CLE009
     C           AUTH      IFEQ *BLANKS                                   CLE009
     C                     MOVE *BLANKS   WKSWRI                          CLE009
     C                     ELSE                                           CLE009
      *                                                                   CLE009
      ** If work regeneration indicator is blank                          CLE009
      *                                                                   CLE009
     C           WKSWRI    IFEQ *BLANKS                                   CLE009
     C                     MOVE SWRI      WKSWRI                          CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
      ** If work regeneration indicator is changed                        CLE009
      *                                                                   CLE009
     C           WKSWRI    IFEQ 'C'                                       CLE009
      *                                                                   CLE009
      ** Check if the facility has an associated fee                      CLE009
      *                                                                   CLE009
     C                     MOVE WKFTP     CHFACT  3                       CLE009
     C                     MOVE WKFNO     CHFCNO  2                       CLE009
     C           CHFACT    CAT  CHFCNO    CHFACL  5                       CLE009
     C                     MOVE CHFACL    WFFACL                          CLE009
     C**********           Z-ADD0         WFLOAN                                       CLE009 CLE148
     C                     MOVEL*BLANKS   WFLOAN                                              CLE148
     C           WKFEE     SETLLLEFEEDL1                                  CLE009
     C           WKFEE     READELEFEEDL1                 14               CLE009
      *                                                                   CLE009
      ** Do while an associated fee exists                                CLE009
      *                                                                   CLE009
     C           *IN14     DOWEQ*OFF                                      CLE009
      *                                                                   CLE009
      ** If the fee is a confirmed calculated fee and swift               CLE009
      ** regeneration indicator is not 'I', 'B', 'H'                      CLE009
      *                                                                   CLE009
     C                     MOVE FEAUTO    WAUTO   1                                           CLE134
     C           WAUTO     IFEQ 'C'                                                           CLE134
     C           FEAUT2    OREQ 'C'                                                           CLE134
     C           CLE134    IFEQ 'Y'                                                           CLE134
     C                     MOVE 'Y'       WAUTO                                               CLE134
     C                     END                                                                CLE134
     C                     END                                                                CLE134
      *                                                                                       CLE134
     C********** FEAUTO    IFEQ 'Y'                                                    CLE009 CLE134
     C           WAUTO     IFEQ 'Y'                                                           CLE134
     C           FECALT    ANDNE*BLANKS                                   CLE009
     C           FESWRI    ANDNE'I'                                       CLE009
     C           FESWRI    ANDNE'B'                                       CLE009
     C           FESWRI    ANDNE'H'                                       CLE009
      *                                                                   CLE009
      ** Set currency to fee currency                                     CLE009
      ** Set day to run date                                              CLE009
      ** Set number of working days to telex notice days                  CLE009
      *                                                                   CLE009
     C                     MOVE FEFCCY    ZCCY                            CLE009
     C                     Z-ADDBJRDNB    ZDAYNO                          CLE009
     C                     Z-ADD1         Y                               CLE009
      *                                                                   CLE031
      ** If CLE031 is on then use correct currency to access AONOSTR0     CLE031
      *                                                                   CLE031
     C                     MOVE FEFCCY    WKCCY   3                       CLE031
      *                                                                   CLE031
     C           CLE031    IFEQ 'Y'                                       CLE031
      *                                                                   CLE031
     C           PTFI      IFEQ 'P'                                       CLE031
     C           FEPSCY    IFNE *BLANKS                                   CLE031
     C                     MOVE FEPSCY    WKCCY                           CLE031
     C                     ENDIF                                          CLE031
     C                     ELSE                                           CLE031
     C           FESCCY    IFNE *BLANKS                                   CLE031
     C                     MOVE FESCCY    WKCCY                           CLE031
     C                     ENDIF                                          CLE031
     C                     ENDIF                                          CLE031
      *                                                                   CLE031
     C                     ENDIF                                          CLE031
      *                                                                   CLE031
     C                     MOVE WKCCY     ZCCY                            CLE031
     C                     MOVE WKCCY     PCURR                           CLE031
      *                                                                   CLE031
     C********** FEFCCY    LOKUPCYA,Y                    15        CLE009 CLE031
     C           WKCCY     LOKUPCYA,Y                    15               CLE031
     C                     MOVE TND,Y     ZNRDYS                          CLE009
      *                                                                   CLE009
      ** If Fee Settlement type is '01', '02', '07', '08' or '12'         CLE009
      *                                                                   CLE009
     C           FESTTL    IFEQ 01                                        CLE009
     C           FESTTL    OREQ 02                                        CLE009
     C           FESTTL    OREQ 07                                        CLE009
     C           FESTTL    OREQ 08                                        CLE009
     C           FESTTL    OREQ 12                                        CLE009
      *                                                                   CLE009
     C                     CALL 'AONOSTR0'                                CLE009
     C                     PARM *BLANKS   PRTCD                           CLE009
     C                     PARM '*KEY   ' POPTN                           CLE009
     C                     PARM           PCUSN   6                       CLE009
     C                     PARM FEFCCY    PCURR                           CLE009
     C**********           PARM           PACCD   4                                    CLE009 CAS011
     C                     PARM           PACCD  10                                           CAS011
     C                     PARM           PACSN   2                       CLE009
     C                     PARM FEOURS    PNONB   2                       CLE009
     C                     PARM           PBRCA   3                       CLE009
     C                     PARM           PCSSN  10                       CLE009
     C                     PARM           PPNOI   1                       CLE009
     C           SDNOST    PARM SDNOST    DSFDY                           CLE009
      *                                                                   CLE009
     C           PRTCD     IFNE *BLANKS                                   CLE009
     C           *LOCK     IN   LDA                                       CLE009
     C                     MOVEL'AONOSTR0'DBFILE           ************   CLE009
     C                     Z-ADD29        DBASE            * DBERR 29 *   CLE009
     C                     MOVELFEFCCY    DBKEY            ************   CLE009
     C                     OUT  LDA                                       CLE009
     C                     EXSR *PSSR                                     CLE009
      *                                                                   CLE009
     C                     ELSE                                           CLE009
      *                                                                   CLE009
      ** Set Location to Nostro Location                                  CLE009
      *                                                                   CLE009
     C                     MOVE A7NOSN    ZLOC                            CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C                     ELSE                                           CLE009
      *                                                                   CLE009
     C                     MOVE *BLANKS   ZLOC                            CLE009
      *                                                                   CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
      ** Calculate number of working days forward                         CLE009
      *                                                                   CLE009
     C                     EXSR ZFWDT                                     CLE009
      *                                                                   CLE009
      ** Set Telex Notice date to day number                              CLE009
      *                                                                   CLE009
     C                     Z-ADDZDYNBR    TXDT    50                      CLE009
      *                                                                   CLE009
      ** If next payment date is before or equals to Telex Notice date    CLE009
      ** and Next Payment Telex indicator is set to 'Y'                   CLE009
      *                                                                   CLE009
     C           FENPYD    IFLE TXDT                                      CLE009
     C           FEPPTI    ANDEQ'Y'                                       CLE009
      *                                                                   CLE009
      ** Update Fees file                                                 CLE009
      *                                                                   CLE009
     C                     MOVE 'A'       FESWRI                          CLE009
     C                     UPDATLEFEEDF                                   CLE009
     C                     ELSE                                           CLE009
      *                                                                   CLE009
      ** If end date is before or equals to Telex Notice date and End     CLE009
      ** Telex indicator is set to 'Y'                                    CLE009
      *                                                                   CLE009
     C           FEPEND    IFLE TXDT                                      CLE009
     C           FEEPTI    ANDEQ'Y'                                       CLE009
      *                                                                   CLE009
      ** Update Fees file                                                 CLE009
      *                                                                   CLE009
     C                     MOVE 'A'       FESWRI                          CLE009
     C                     UPDATLEFEEDF                                   CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C           WKFEE     READELEFEEDL1                 14               CLE009
     C                     ENDDO                                          CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C                     MOVE *BLANKS   WKSWRI                          CLE009
      *                                                                   CLE009
     C                     ENDSR                                          CLE009
      /TITLE SR/SRUPFA
      *****************************************************************
      *  SRUPFA  -  Update the Facility file                          *
      *  Called by: SRUPMF, SRUPFS                                    *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRUPFA    BEGSR
      *
      *   Check revolving credit indicator in FCLTYFM.                    189904
     C                     MOVE 'A'       WKRCT                           189904
     C           KEYFAC    CHAINFCLTY                50                   189904
     C           *IN50     IFEQ *ON                                       189904
     C           *LOCK     IN   LDA                                       189904
     C                     MOVEL'FCLTYFM 'DBFILE           ************   189904
     C                     Z-ADD30        DBASE            * DBERR 30 *   189904
     C                     MOVELDFCLTY    DBKEY            ************   189904
     C                     OUT  LDA                                       189904
     C                     EXSR *PSSR                                     189904
     C                     ENDIF                                          189904
      *                                                                   189904
     C                     MOVE 'B'       WKRCT
     C           KEYFAC    CHAINFCLTY                50
     C           *IN50     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'FCLTYFN 'DBFILE           ************
     C                     Z-ADD12        DBASE            * DBERR 12 *
     C                     MOVELDFCLTY    DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *                                                                   CLE014
      ** If the facilty is a credit agreement and the revolving credit    CLE014
      ** indicator is 'T', tranche revolving credit must be used in       CLE014
      ** updating the amounts.                                            CLE014
      *                                                                   CLE014
     C           CLE014    IFEQ 'Y'                                       CLE014
     C           MTRCA     ANDEQ'CA'                                      CLE014
     C           MRVCR     ANDEQ'T'                                       CLE014
     C                     MOVE WRVCR     WWRVCR  1                       CLE014
     C                     ELSE                                           CLE014
     C                     MOVE MRVCR     WWRVCR                          CLE014
     C                     ENDIF                                          CLE014
      *
      ** Action code = INSERT
      *
     C                     SELEC
     C           SACTN     WHEQ 'I'
      *
      ** Update the drawn amounts that fall between the utilisation's
      ** Start and Expiry dates.
      ** However if facility is non-revolving, update all the drawn       189904
      ** amounts from the Start date onwards.                             189904
      *
     C           1         DO   10        S
     C           RUNS,S    IFGE USTDT
     C***********RUNS,S    ANDLEUEXDT                                     158845
     C           RUNS,S    ANDLTUEXDT                                     158845
     C           UEXDT     ANDNE0
     C           WWRVCR    ANDEQ'Y'                                       CLE014
     C           RUNS,S    ORGE USTDT
     C           UEXDT     ANDEQ0
     C           WWRVCR    ANDEQ'Y'                                       CLE014
     C           RUNS,S    ORGE USTDT                                     CLE014
     C           WWRVCR    ANDEQ'N'                                       CLE014
     C                     ADD  WCAMT     OAM,S
     C           S         IFEQ 2
      * If its Rundate & MFU has not yet expired, add to Current exposure.189904
     C           RUNS,S    IFLT UEXDT                                     189904
     C           UEXDT     OREQ 0                                         189904
     C                     ADD  WCAMT     NCAMD
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
     C                     ADD  WSCAMT    NCEXP                                               CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C                     ENDIF                                          189904
      *                                                                   189904
     C                     ENDIF
      *                                                                   CLE009
      ** If CLE009 is installed, set Work Regeneration Indicator to       CLE009
      ** changed                                                          CLE009
      *                                                                   CLE009
     C           CLE009    IFEQ 'Y'                                       CLE009
     C                     MOVE 'C'       WKSWRI  1                       CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF
     C                     ENDDO
     C                     UPDATFCLTYFNF
      *
      ** Action code = AMEND.
      *
     C           SACTN     WHEQ 'A'
      *
      ** Reverse the previous utilisation amount and add the new
      ** amount to the facility drawn amounts.
      *
     C***********UUAMT     IFNE UPUAM                                     157050
     C           UUAMT     IFNE WPUAM                                     157050
     C           1         DO   10        S
     C           RUNS,S    IFGE USTDT
     C***********RUNS,S    ANDLEWOEXD                                     158845
     C           RUNS,S    ANDLTWOEXD                                     158845
     C           WOEXD     ANDNE0
     C           WWRVCR    ANDEQ'Y'                                       CLE014
     C           RUNS,S    ORGE USTDT
     C           WOEXD     ANDEQ0
     C           WWRVCR    ANDEQ'Y'                                       CLE014
     C           RUNS,S    ORGE USTDT                                     CLE014
     C           WWRVCR    ANDEQ'N'                                       CLE014
     C           S         IFEQ 1
     C********** UORED     ANDEQWRUND                                                         203094
     C           ORED      ANDEQWRUND                                                         203094
     C           S         ORGE 2
     C                     SUB  WPAMT     OAM,S
     C                     ADD  WCAMT     OAM,S
      *                                                                   CLE009
      ** If CLE009 is installed, set Work Regeneration Indicator to       CLE009
      ** changed                                                          CLE009
      *                                                                   CLE009
     C           CLE009    IFEQ 'Y'                                       CLE009
     C                     MOVE 'C'       WKSWRI  1                       CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF
     C           S         IFEQ 2
     C                     SUB  WPAMT     NCAMD
     C                     ADD  WCAMT     NCAMD
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
     C                     SUB  WSPAMT    NCEXP                                               CLE023
     C                     ADD  WSCAMT    NCEXP                                               CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
     C                     ENDIF
      *
      ** If Expiry  date is amended, and New Expiry date is greater
      ** than the old Expiry date, add the utilisation amount from
      ** the drawn amounts which fell between the old and new expiry
      ** expiry dates.
      *
     C           UEXDT     IFNE WOEXD
     C           WOEXD     ANDNE0
     C           WWRVCR    ANDEQ'Y'                                       CLE014
      *
     C           UEXDT     IFGT WOEXD
     C           1         DO   10        S
     C***********RUNS,S    IFGT WOEXD                                     158845
     C***********RUNS,S    ANDLEUEXDT                                     158845
     C           RUNS,S    IFGE WOEXD                                     158845
     C           RUNS,S    ANDLTUEXDT                                     158845
     C           S         IFEQ 1
     C********** UORED     ANDEQWRUND                                                         203094
     C           ORED      ANDEQWRUND                                                         203094
     C           S         ORGE 2
     C                     ADD  WCAMT     OAM,S
      *                                                                   CLE009
      ** If CLE009 is installed, set Work Regeneration Indicator to       CLE009
      ** changed                                                          CLE009
      *                                                                   CLE009
     C           CLE009    IFEQ 'Y'                                       CLE009
     C                     MOVE 'C'       WKSWRI  1                       CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF
     C           S         IFEQ 2
     C                     ADD  WCAMT     NCAMD
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
     C                     ADD  WSCAMT    NCEXP                                               CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
     C                     ENDIF
      *
      ** If Expiry  date is amended, and New Expiry date is lesser
      ** than the old Expiry date, subtract the utilisation amount from
      ** the drawn amounts which fell between the new and old expiry
      ** expiry dates.
      *
     C           UEXDT     IFLT WOEXD
     C           1         DO   10        S
     C***********RUNS,S    IFGT UEXDT                                     158845
     C***********RUNS,S    ANDLEWOEXD                                     158845
     C           RUNS,S    IFGE UEXDT                                     158845
     C           RUNS,S    ANDLTWOEXD                                     158845
     C           S         IFEQ 1
     C********** UORED     ANDEQWRUND                                                         203094
     C           ORED      ANDEQWRUND                                                         203094
     C           S         ORGE 2
     C                     SUB  WCAMT     OAM,S
      *                                                                   CLE009
      ** If CLE009 is installed, set Work Regeneration Indicator to       CLE009
      ** changed                                                          CLE009
      *                                                                   CLE009
     C           CLE009    IFEQ 'Y'                                       CLE009
     C                     MOVE 'C'       WKSWRI  1                       CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF
     C           S         IFEQ 2
     C                     SUB  WCAMT     NCAMD
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
     C                     SUB  WSCAMT    NCEXP                                               CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
     C                     ENDIF
     C                     ENDIF
      *
      ** If Expiry  date is amended, and previous expiry date is not
      ** entered, reverse the previous utilisation amount that are
      ** added to the drawn and exposure amounts.
      *
     C           UEXDT     IFNE WOEXD
     C           WOEXD     ANDEQ0
     C***********UEXDT     ANDLTRUN9                                      158845
     C           UEXDT     ANDLERUN9                                      158845
     C           WWRVCR    ANDEQ'Y'                                       CLE014
     C***********UEXDT     IFLT RUN0                                      158845
     C           UEXDT     IFLE RUN0                                      158845
     C           1         DO   10        S
     C           S         IFEQ 1
     C********** UORED     ANDEQWRUND                                                         203094
     C           ORED      ANDEQWRUND                                                         203094
     C           S         ORGE 2
     C                     SUB  WCAMT     OAM,S
      *                                                                   CLE009
      ** If CLE009 is installed, set Work Regeneration Indicator to       CLE009
      ** changed                                                          CLE009
      *                                                                   CLE009
     C           CLE009    IFEQ 'Y'                                       CLE009
     C                     MOVE 'C'       WKSWRI  1                       CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF
     C           S         IFEQ 2
     C                     SUB  WCAMT     NCAMD
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
     C                     SUB  WSCAMT    NCEXP                                               CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C                     ENDIF
     C                     ENDDO
     C                     ENDIF
      *
      ** If Expiry  date is amended, and previous expiry date is not
      ** entered, reverse the utilisation amount previously added after
      ** the new Expiry date.
      *
     C***********UEXDT     IFGE RUN0                                      158845
     C***********UEXDT     ANDLTRUN9                                      158845
     C           UEXDT     IFGT RUN0                                      158845
     C           UEXDT     ANDLERUN9                                      158845
     C           1         DO   10        S
     C***********RUNS,S    IFGT UEXDT                                     158845
     C           RUNS,S    IFGE UEXDT                                     158845
     C           RUNS,S    ANDLERUN9
     C           S         IFEQ 1
     C********** UORED     ANDEQWRUND                                                         203094
     C           ORED      ANDEQWRUND                                                         203094
     C           S         ORGE 2
     C                     SUB  WCAMT     OAM,S
      *                                                                   CLE009
      ** If CLE009 is installed, set Work Regeneration Indicator to       CLE009
      ** changed                                                          CLE009
      *                                                                   CLE009
     C           CLE009    IFEQ 'Y'                                       CLE009
     C                     MOVE 'C'       WKSWRI  1                       CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF
     C           S         IFEQ 2
     C                     SUB  WCAMT     NCAMD
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
     C                     SUB  WSCAMT    NCEXP                                               CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
     C                     ENDIF
     C                     ENDIF
      *
     C***********UUAMT     IFNE UPUAM                                     157050
     C           UUAMT     IFNE WPUAM                                     157050
     C           UEXDT     ORNE WOEXD
     C                     UPDATFCLTYFNF
     C                     ENDIF
      *
      ** Action code = DELETE.
      *
     C           SACTN     WHEQ 'D'
      *
      ** Reverse the utilisation amount that are previously added to
      ** the drawn amounts and current exposure amount.
      *
     C           1         DO   10        S
     C           RUNS,S    IFGE USTDT
     C***********RUNS,S    ANDLEUEXDT                                     158845
     C           RUNS,S    ANDLTUEXDT                                     158845
     C           UEXDT     ANDNE0
     C           WWRVCR    ANDEQ'Y'                                       CLE014
     C           RUNS,S    ORGE USTDT
     C           UEXDT     ANDEQ0
     C           WWRVCR    ANDEQ'Y'                                       CLE014
     C           RUNS,S    ORGE USTDT                                     CLE014
     C           WWRVCR    ANDEQ'N'                                       CLE014
     C           S         IFEQ 1
     C********** UORED     ANDEQWRUND                                                         203094
     C           ORED      ANDEQWRUND                                                         203094
     C           S         ORGE 2
     C                     SUB  WCAMT     OAM,S
      *                                                                   CLE009
      ** If CLE009 is installed, set Work Regeneration Indicator to       CLE009
      ** changed                                                          CLE009
      *                                                                   CLE009
     C           CLE009    IFEQ 'Y'                                       CLE009
     C                     MOVE 'C'       WKSWRI  1                       CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF
     C           S         IFEQ 2
     C*                                                                                       204658
     C* Only back out of exposure if expiry date has not yet been                             204658
     C* reached.                                                                              204658
     C*                                                                                       204658
     C           UEXDT     ANDGTRUNS,2                                                        204658
     C*                                                                                       204658
     C                     SUB  WCAMT     NCAMD
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
     C                     SUB  WSCAMT    NCEXP                                               CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C                     ENDIF
     C                     ENDIF
     C                     ENDDO
     C                     UPDATFCLTYFNF
      *
     C                     ENDSL
      *
     C           ENDFA     ENDSR
      /TITLE SR/SRCONF
      *****************************************************************
      *  SRCONF  -  Convert Facility currency to C/A currency         *
      *  Called by: SRUPFS                                            *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRCONF    BEGSR
      *
      ** Convert the facility amount in credit agreement currency.
      *
     C           WSFCY     IFNE WSCCY
      *
     C           SUCCY     IFEQ WSCCY                                     191779
     C           WUXRT     ANDEQWSCXR                                     191779
     C           SUXID     ANDNEWSCMD                                     191779
     C                     MOVE WUAMT     WSAMT                           191779
     C           CLE023    IFEQ 'Y'                                                           CLE023
     C                     Z-ADDWUAMT     WSSAM                                               CLE023
     C                     ENDIF                                                              CLE023
     C                     ELSE                                           191779
      *                                                                   191779
      ** Get the number of decimal places of the two currencies.
      *
     C                     MOVE WSCCY     PCCY
     C                     Z-ADD21        PDBASE
     C                     EXSR SRGETD
     C                     MOVE WDECN     CDPS    10
      *                                                                                       CLE023
     C                     MOVE A6MDIN    WFMDI                                               CLE023
     C                     Z-ADDA6SPRT    WFSPT                                               CLE023
      *                                                                                       CLE023
     C                     MOVE WSFCY     PCCY
     C                     Z-ADD22        PDBASE
     C                     EXSR SRGETD
     C                     MOVE WDECN     CDPF2   10
      *                                                                                       CLE023
     C                     MOVE A6MDIN    WSMDI                                               CLE023
     C                     Z-ADDA6SPRT    WSSPT                                               CLE023
     C                     MOVE WSFCY     WCCYX                                               CLE023
      *
     C           CDPS      SUB  CDPF2     CX
     C           CX        ADD  4         CX
     C           WSCMD     COMP 'D'                      92
     C      92   WSCXR     DIV  POWR,CX   OFCXRT    H
     C      92   1         DIV  OFCXRT    OFCXRT    H
     C     N92   WSCXR     MULT POWR,CX   OFCXRT    H
      *
     C           WCAMT     MULT OFCXRT    WSAMT  130H
      *                                                                                       CLE023
      ** When Lending Enhancement CLE023 is switched on                                       CLE023
      ** compute utilisation amount with spot rate                                            CLE023
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
      *                                                                                       CLE023
     C                     MOVE WSMDI     ZMDI1                                               CLE023
     C                     Z-ADDWSSPT     ZRATE1                                              CLE023
     C                     MOVE WFMDI     ZMDI2                                               CLE023
     C                     Z-ADDWFSPT     ZRATE2                                              CLE023
     C                     EXSR ZXRATE                                                        CLE023
      *                                                                                       CLE023
     C           ZMDIX     IFEQ 'D'                                                           CLE023
     C           ZRATEX    DIV  POWR,CX   WFXRT     H                                         CLE023
     C           1         DIV  WFXRT     WFXRT     H                                         CLE023
     C                     ELSE                                                               CLE023
     C           ZRATEX    MULT POWR,CX   WFXRT     H                                         CLE023
     C                     ENDIF                                                              CLE023
     C           WSCAMT    MULT WFXRT     WSSAM  130H                                         CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C                     ENDIF                                          191779
      *
     C                     ELSE
     C                     MOVE WCAMT     WSAMT
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
     C                     Z-ADDWSCAMT    WSSAM                                               CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C                     ENDIF
      *
      ** Override the converted utilisation amount with the newly-
      ** converted amount in credit agreement currency.
      *
     C                     MOVE WSAMT     WCAMT
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
     C                     Z-ADDWSSAM     WSCAMT                                              CLE023
     C                     ENDIF                                                              CLE023
      *
      ** Convert the previous facility amount in credit agreement
      ** currency.
      *
     C           WSFCY     IFNE WSCCY
      *
      ** Get the number of decimal places of the two currencies.
      *
     C                     MOVE WSCCY     PCCY
     C                     Z-ADD23        PDBASE
     C                     EXSR SRGETD
     C                     MOVE WDECN     CDPS
      *                                                                                       CLE023
     C                     MOVE A6MDIN    WFMDI                                               CLE023
     C                     Z-ADDA6SPRT    WFSPT                                               CLE023
      *                                                                                       CLE023
     C                     MOVE WSFCY     PCCY
     C                     Z-ADD24        PDBASE
     C                     EXSR SRGETD
     C                     MOVE WDECN     CDPF2
      *                                                                                       CLE023
     C                     MOVE A6MDIN    WSMDI                                               CLE023
     C                     Z-ADDA6SPRT    WSSPT                                               CLE023
      *
     C           CDPS      SUB  CDPF2     CX
     C           CX        ADD  4         CX
     C           WSCMD     COMP 'D'                      92
     C      92   WSCXR     DIV  POWR,CX   OFCXRT    H
     C      92   1         DIV  OFCXRT    OFCXRT    H
     C     N92   WSCXR     MULT POWR,CX   OFCXRT    H
      *
     C           WPAMT     MULT OFCXRT    WSPAM  130H
      *
      ** When Lending Enhancement CLE023 is switched on                                       CLE023
      ** compute utilisation amount with spot rate                                            CLE023
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
      *                                                                                       CLE023
     C                     MOVE WSMDI     ZMDI1                                               CLE023
     C                     Z-ADDWSSPT     ZRATE1                                              CLE023
     C                     MOVE WFMDI     ZMDI2                                               CLE023
     C                     Z-ADDWFSPT     ZRATE2                                              CLE023
     C                     EXSR ZXRATE                                                        CLE023
      *                                                                                       CLE023
     C           ZMDIX     IFEQ 'D'                                                           CLE023
     C           ZRATEX    DIV  POWR,CX   WFXRT     H                                         CLE023
     C           1         DIV  WFXRT     WFXRT     H                                         CLE023
     C                     ELSE                                                               CLE023
     C           ZRATEX    MULT POWR,CX   WFXRT  159H                                         CLE023
     C                     ENDIF                                                              CLE023
     C           WSPAMT    MULT WFXRT     WSSPAM 130H                                         CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C                     ELSE
     C                     MOVE WPAMT     WSPAM
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
     C                     Z-ADDWSPAMT    WSSPAM                                              CLE023
     C                     ENDIF                                                              CLE023
      *                                                                                       CLE023
     C                     ENDIF
      *
      ** Override the previously converted utilisation amount with
      ** the newly converted amount in credit agreement currency.
      *
     C                     Z-ADDWSPAM     WPAMT
      *                                                                                       CLE023
     C           CLE023    IFEQ 'Y'                                                           CLE023
     C                     Z-ADDWSSPAM    WSPAMT                                              CLE023
     C                     ENDIF                                                              CLE023
      *
     C                     ENDSR
      /TITLE SR/SRUPFS
      *****************************************************************
      *  SRUPFS  -  Update the facility's Credit agreement facility   *
      *  Called by: SRUPMF                                            *
      *  Calls    :                                                   *
      *  SRCONF  -  Convert facility currency to C/A currency         *
      *  SRUPFA  -  Update the facility file                          *
      *                                                               *
      *****************************************************************
     C           SRUPFS    BEGSR
      *
      ** Save Facility Currency and Credit Agreement details.
      *
     C                     MOVE MFCCY     WSFCY   3
     C                     MOVE MCAXR     WSCXR  138
     C                     MOVE MCMDI     WSCMD   1
     C                     MOVE MCACY     WSCCY   3
     C           CLE014    IFEQ 'Y'                                       CLE014
     C                     MOVE MRVCR     WRVCR   1                       CLE014
     C                     ENDIF                                          CLE014
      *
      ** Access Credit Agreement facility record.
      *
     C                     MOVE MCANM     WKCNO
     C                     MOVE MCAFT     WKFTP
     C                     MOVE MCAFN     WKFNO
     C                     MOVE 'A'       WKRCT
     C           KEYFAC    CHAINFCLTY               N50
     C           *IN50     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'FCLTYFM 'DBFILE           ************
     C                     Z-ADD13        DBASE            * DBERR 13 *
     C                     MOVELDFCLTY    DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
     C                     ELSE
     C                     EXSR SRCONF
     C                     EXSR SRUPFA
     C                     ENDIF
      *
     C                     ENDSR
      /TITLE SR/SRUPTR
      *****************************************************************
      *  SRUPTR  -  Update Utilisation Trailer file                   *
      *  Called by: SRUPMF                                            *
      *  Calls   :                                                    *
      *  SRUPFA  -  Update the facility file                          *
      *****************************************************************
     C           SRUPTR    BEGSR
      *
     C           1         CHAINLEMNFUZF             86
     C           *IN86     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVEL'LEMNFUZZ'DBFILE           ************
     C                     Z-ADD14        DBASE            * DBERR 14 *
     C                     MOVE *BLANKS   DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
      *
      ** Action code = INSERT
      *
     C                     ELSE
     C                     SELEC
     C           SACTN     WHEQ 'I'
     C                     ADD  1         NORE
     C                     ADD  1         NINS
     C                     ADD  1         NLRA
     C                     MOVE UUAMT     ZZAMT
     C           ZZAMT     IFLT *ZEROS                                                        190571
     C                     Z-SUBZZAMT     ZZAMT                                               190571
     C                     ENDIF                                                              190571
      *                                                                                       190571
     C                     MOVE VIRF      ZZTOTI
     C                     MOVE VIRL      ZZTOTD
     C                     EXSR GLZADD
     C                     MOVE ZZTOTI    VIRF
     C                     MOVE ZZTOTD    VIRL
     C                     MOVE VLAF      ZZTOTI
     C                     MOVE VLAL      ZZTOTD
     C                     EXSR GLZADD
     C                     MOVE ZZTOTI    VLAF
     C                     MOVE ZZTOTD    VLAL
      *
      ** Action code = DELETE
      *
     C           SACTN     WHEQ 'D'
     C                     ADD  1         NDEL
     C                     SUB  1         NLRA
     C                     MOVE UUAMT     ZZAMT
     C           ZZAMT     IFLT *ZEROS                                                        190571
     C                     Z-SUBZZAMT     ZZAMT                                               190571
     C                     ENDIF                                                              190571
      *                                                                                       190571
     C                     MOVE VDRF      ZZTOTI
     C                     MOVE VDRL      ZZTOTD
     C                     EXSR GLZADD
     C                     MOVE ZZTOTI    VDRF
     C                     MOVE ZZTOTD    VDRL
     C                     MOVE VLAF      ZZTOTI
     C                     MOVE VLAL      ZZTOTD
     C                     EXSR GLZSUB
     C                     MOVE ZZTOTI    VLAF
     C                     MOVE ZZTOTD    VLAL
      *
      ** Action code = AMEND
      *
     C           SACTN     WHEQ 'A'
     C                     ADD  1         NAMD
     C***********UPUAM     SUB  UUAMT     WDAMT  130                      157050
     C           WPUAM     SUB  UUAMT     WDAMT  130                      157050
     C                     MOVE WDAMT     ZZAMT
     C           ZZAMT     IFLT *ZEROS                                                        190571
     C                     Z-SUBZZAMT     ZZAMT                                               190571
     C                     ENDIF                                                              190571
      *                                                                                       190571
     C                     MOVE VARF      ZZTOTI
     C                     MOVE VARL      ZZTOTD
     C                     EXSR GLZADD
     C                     MOVE ZZTOTI    VARF
     C                     MOVE ZZTOTD    VARL
     C                     ENDSL
      *
     C                     UPDATLEMNFUZF
     C                     ENDIF
      *
     C                     ENDSR
      /TITLE SR/SRVALS
      *****************************************************************
      *  SRVALS  -  Subfile Validation                                *
      *  Called by: SRSUBF                                            *
      *  Calls    :                                                   *
      *  SRAUTH  -  Validate authorisation action                     *
      *  SRUAUT  -  Check if user is authorised to the action         *
      *  SRPROC  -  Subfile detail processing                         *
      *****************************************************************
     C           SRVALS    BEGSR
      *
     C                     Z-ADDSRRN      WSRRN   40
      *
      ** Read changed records in subfile
      *
     C                     MOVE 'N'       WSFER   1
     C                     MOVE 'N'       WFRST   1
      *
     C           *IN12     IFEQ '0'
     C                     READCLE2140S0                 79
     C                     ELSE
     C                     MOVE '1'       *IN79
     C                     ENDIF
      *
     C           *IN79     DOWEQ'0'
     C                     MOVE '0'       *IN20
     C                     MOVE '0'       *IN49
      *
     C           SLSEL     IFNE *BLANK
     C                     MOVE '1'       *IN20
      *                                                                   150215
      ** If called from Linked Enquiry, only action 'E' is valid          150215
      *                                                                   150215
     C           *IN05     IFEQ *ON                                       150215
     C           SLSEL     IFNE 'E'                                       150215
     C                     MOVE '1'       *IN49                           150215
     C                     MOVE 'Y'       WSFER                           150215
     C                     MOVEL'LEU0062' ZAMSID                          150215
     C                     EXSR ZASNMS                                    150215
     C                     ENDIF                                          150215
     C                     ELSE                                           150215
      *
      ** Valid actions are E, A, D, and X (for unauthorised loan)
      *
     C           SLSEL     IFNE 'E'
     C           SLSEL     ANDNE'A'
     C           SLSEL     ANDNE'D'
     C           SLSEL     ANDNE'X'
     C                     MOVE '1'       *IN49
     C                     MOVE 'Y'       WSFER
     C                     MOVEL'LEU0048' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF                                          150215
      *
     C                     MOVELSSSTS     WUSTS   1
      *
      ** If action code is 'X', execute Authorisation Processing
      *
     C           SLSEL     IFEQ 'X'
     C           *IN49     ANDEQ'0'
      *
     C                     MOVE SSCUS     WUCNO
     C                     MOVE SSTYP     WUFTP
     C                     MOVE SSNUM     WUFNO
     C                     MOVE SSUSQ     WURSQ
     C           KEYUTI    CHAINLEMNFUL3             81
     C           *IN81     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     MOVEL'LEMNFUPD'DBFILE           ************
     C                     Z-ADD15        DBASE            * DBERR 15 *
     C                     MOVELDMNFUT    DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     EXSR SRAUTH
     C                     ENDIF
      *
      ** Check user/action/branch authority
      *
     C           *IN49     IFEQ '0'
     C           *IN05     ANDEQ*OFF                                      150215
     C                     MOVE SLSEL     WACTN
     C                     EXSR SRUAUT
     C                     ENDIF
      *
      ** Save rel. record no. of first record in error
      *
     C           WFRST     IFEQ 'N'
     C           *IN49     ANDEQ'1'
     C                     Z-ADDSRRN      SRECN
     C                     MOVE 'Y'       WFRST
     C                     ENDIF
     C                     ENDIF
      *
     C                     UPDATLE2140S0
     C                     READCLE2140S0                 79
     C                     ENDDO
      *
     C           WSFER     IFEQ 'N'
     C                     EXSR SRPROC
     C                     ENDIF
      *
      ** Restore next available RRN
      *
     C                     Z-ADDWSRRN     SRRN    40
      *
     C                     ENDSR
      /TITLE SR/SRPROC
      *****************************************************************
      *  SRPROC  -  Subfile Detail Processing                         *
      *  Called by: SRVALS                                            *
      *  Calls   :                                                    *
      *  SRDETL  -  Detail processing                                 *
      *  SRREVS  -  Select records from Review fields                 *
      *****************************************************************
     C           SRPROC    BEGSR
      *
      ** Read changed records in subfile.
      *
     C           *IN12     IFEQ '0'
     C                     READCLE2140S0                 79
     C                     ENDIF
      *
     C           *IN79     DOWEQ'0'
     C           *INKC     ANDEQ'0'
     C           *INKL     ANDEQ'0'
      *
     C                     MOVE SLSEL     SACTN
     C                     EXSR SRDETL
      *
      ** Blank out selection field.
      *
     C                     MOVE *BLANK    SLSEL
     C                     UPDATLE2140S0
      *
      ** Read next change record.
      *
     C           *INKL     IFEQ '0'
     C                     READCLE2140S0                 79
     C                     ENDIF
     C                     ENDDO
      *
      ** Reload subfile if no more changed record to read.
      *
     C           *IN79     IFEQ '1'
     C                     EXSR SRREVS
     C                     Z-ADDSRRN      WSRRN
     C                     ENDIF
     C                     Z-ADD1         SRECN
      *
     C                     ENDSR
      /TITLE SR/SRAUTH
      *****************************************************************
      *  SRAUTH  -  Validate authorisation action                     *
      *  Called by: SRVALI, SRVALS                                    *
      *  Calls    :                                                   *
      *  ZASNMS  -  Sends message to the program message queue        *
      *****************************************************************
     C           SRAUTH    BEGSR
      *
     C           CLE002    IFEQ 'N'
     C           WSUBF     IFEQ 'Y'
     C                     MOVE '1'       *IN49
     C                     MOVE 'Y'       WSFER
     C                     ELSE
     C                     MOVE '1'       *IN30
     C                     ENDIF
     C                     MOVEL'LEU0051' ZAMSID
     C                     EXSR ZASNMS
      *
      ** Authorising user must be different from the insert and
      ** amend users
      *
     C                     ELSE
      * If system value CLAuthORED = 'Y' and status is 'R' then allow authorise user          240968
      * to be same as orignal insert user                                                     240968
      * Or if CLAuthSameORED = 'Y' then allow authorise user to be same as original           244314
      * insert user as long as the last change type was an amendment and the                  244314
      * current status of transaction is 'Reauthorise Required'                               244314
     C           ATHR      IFEQ 'Y'                                                           240968
     C           USTS      ANDEQ'R'                                                           240968
     C           ATHS      OREQ 'Y'                                                           244314
     C           USTS      ANDEQ'R'                                                           244314
     C           USER      IFEQ AUSR                                                          240968
     C           WSUBF     IFEQ 'Y'                                                           240968
     C                     MOVE '1'       *IN49                                               240968
     C                     MOVE 'Y'       WSFER                                               240968
     C                     ELSE                                                               240968
     C                     MOVE '1'       *IN30                                               240968
     C                     ENDIF                                                              240968
     C                     MOVEL'LEU0047' ZAMSID                                              240968
     C                     EXSR ZASNMS                                                        240968
     C                     ENDIF                                                              240968
     C                     ELSE                                                               240968
     C           USER      IFEQ IUSR
     C           USER      OREQ AUSR
     C           WSUBF     IFEQ 'Y'
     C                     MOVE '1'       *IN49
     C                     MOVE 'Y'       WSFER
     C                     ELSE
     C                     MOVE '1'       *IN30
     C                     ENDIF
     C                     MOVEL'LEU0047' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF                                                              240968
      *
      ** Status of utilisation must be complete or requiring
      ** reauthorisation.
      *
     C           WERRF     IFEQ 'N'
     C           USTS      ANDNE'C'
     C           USTS      ANDNE'R'
     C           WSUBF     IFEQ 'Y'
     C                     MOVE '1'       *IN49
     C                     MOVE 'Y'       WSFER
     C                     ELSE
     C                     MOVEA'1111'    *IN,31
     C                     ENDIF
     C                     MOVEL'LEU0022' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      /TITLE SR/SRWIND
      *****************************************************************
      *  SRWIND  -  Window Processing                                 *
      *  Called by: SRDETL                                            *
      *  Calls    :                                                   *
      *  ZASNMS  -  Sends message to the program message queue        *
      *****************************************************************
     C           SRWIND    BEGSR
      *
     C           BGWDPR    IFEQ 'Y'
      *
      /COPY WNCPYSRC,LE2140MOV1
      *
     C                     MOVELSACTN     UACTCD
     C           SACTN     IFEQ 'X'
     C                     MOVE 'E'       UACTCD
     C                     ENDIF
      *
     C                     CALL 'WN0010'
     C                     PARM ##PGM     UPGM   10        Program
     C                     PARM *BLANK    UFKEY   2        Function Key
     C                     PARM           UACTCD  1        Action
     C                     PARM           DATA             Trans Data
     C                     PARM *BLANK    W0RTN   7        Return Code
     C                     PARM           SPARE 256        Spare Field
      *
     C                     SELEC
     C           W0RTN     WHEQ 'Y2U9999'
     C                     MOVE *ON       *INKC
      *
     C           W0RTN     WHEQ 'USR0790'
     C                     MOVE *ON       *INKL
      *
     C           W0RTN     WHNE *BLANKS
     C                     MOVELW0RTN     ZAMSID
     C                     MOVEL'WNMSGF  'ZAMSGF
     C                     MOVEL'LERMSGF 'ZAMSGF
     C                     EXSR ZASNMS
     C                     ENDSL
     C                     ENDIF
      *
     C                     ENDSR
      /TITLE SR/SRUAUT
      *****************************************************************
      *  SRUAUT  -  Check if user is authorised to the action         *
      *  Called by: SRVALI, SRVALS                                    *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRUAUT    BEGSR
      *
      ** For single branch system, check if user is authorised to the
      ** action
      *
     C                     MOVE WACTN     @ZACTN
     C                     MOVE MBRCA     @ZBR
      *
     C           AGMBIN    IFEQ 'N'
     C                     CALL 'ZVACTU'
     C                     PARM           @ZACTN
     C                     PARM           @ERR
     C           @ERR      IFNE 0
     C           WSUBF     IFEQ 'Y'
     C           *IN25     ANDEQ'0'
     C                     MOVE '1'       *IN49
     C                     MOVE 'Y'       WSFER
     C                     ELSE
     C                     MOVE '1'       *IN30
     C                     ENDIF
     C                     MOVEL'LEU0023' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
      *
      ** For multibranching system, and action codes I/A/E/D/X check ifde
      ** user is authorised to the action.
      *
     C                     ELSE
     C                     CALL 'ZVACTBU'
     C                     PARM           @ZACTN  1
     C                     PARM           @ZBR    3
     C                     PARM           @ERR    10
     C           @ERR      IFEQ 1
     C           WSUBF     IFEQ 'Y'
     C           *IN25     ANDEQ'0'
     C                     MOVE '1'       *IN49
     C                     MOVE 'Y'       WSFER
     C                     ELSE
     C                     MOVE '1'       *IN30
     C                     ENDIF
     C                     MOVEL'LEU0024' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C           @ERR      IFEQ 2
     C           WSUBF     IFEQ 'Y'
     C           *IN25     ANDEQ'0'
     C                     MOVE '1'       *IN49
     C                     MOVE 'Y'       WSFER
     C                     ELSE
     C                     MOVE '1'       *IN30
     C                     ENDIF
     C                     MOVEL'LEU0025' ZAMSID
     C                     EXSR ZASNMS
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      /TITLE SR/SRGETD
      *****************************************************************
      *  SRGETD  -  Get the number of decimal places                  *
      *  Called by: SRVALD, SRCONF                                    *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRGETD    BEGSR
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*BLANK  'PRTCD
     C                     PARM '*KEY    'POPTN
     C                     PARM PCCY      PCURR   3
     C           SDCURR    PARM SDCURR    DSSDY
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCURRPD'DBFILE           ************
     C                     Z-ADDPDBASE    DBASE            * DBERR XX *
     C                     MOVELPCCY      DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
     C                     Z-ADDA6NBDP    WDECN   10
      *
     C                     ENDSR
      /TITLE SR/CLEAR
      *****************************************************************
      *  CLEAR   -  Clear the message queue                           *
      *  Called by: SRINRC, SRSUBF, SRDETL, SRDETS                    *
      *  Calls    : Y2CLMSC                                           *
      *****************************************************************
     C           CLEAR     BEGSR
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM ##PGM     ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
     C                     MOVE 'N'       WERRF
      *
     C                     ENDSR
      /TITLE SR/ZASNMS
      *****************************************************************
      *  ZASNMS - Send messages to the program's message queue        *
      *  Called by: SRVALI, SRVALD                                    *
      *  Calls    :                                                   *
      *  SRPERR  -  Send error message for play records               *
      *****************************************************************
     C           ZASNMS    BEGSR
      *
      ** Send error to message queue when not in Play mode.
      *
     C           PMODE     IFNE 'P'
     C           ZAPGMQ    IFEQ *BLANK
     C                     MOVEL##PGM     ZAPGMQ
     C                     ENDIF
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGMQ 10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
      *
     C           WWERR     IFEQ 'Y'
     C                     MOVE 'Y'       WWFLG
     C                     MOVE 'Y'       WSKIP
     C                     MOVE 'N'       WWERR
     C                     ELSE
     C                     MOVE 'Y'       WERRF
     C                     ENDIF
      *
      ** ...else error in Play Mode.
      *
     C                     ELSE
     C                     EXSR SRPERR
     C                     ENDIF
      *
     C           ZAEXIT    ENDSR
      /TITLE SR/SRTEXT
      *****************************************************************
      * SRTEXT  -  Set up text from message file                      *
      * Called by: All subroutines setting of texts                   *
      * Calls    : None                                               *
      *****************************************************************
     C           SRTEXT    BEGSR
      *
     C                     CALL 'Y2RVMGC'
     C                     PARM *BLANKS   PRTCD
     C                     PARM           ZAMSID           Message Id.
     C                     PARM           ZAMSGF           Message file.
     C                     PARM           ZAMSDA           Message data.
     C                     PARM           ZAMSTX           Messge text
      *
     C                     ENDSR
      /TITLE SR/SRPERR
      *****************************************************************
      *  SRPERR - Send error messages for Play records                *
      *  Called by: ZASNMS                                            *
      *  Calls    : None                                              *
      *****************************************************************
     C           SRPERR    BEGSR
      *
     C                     CALL 'Y2RVMGC'
     C                     PARM           PRTCD
     C                     PARM           ZAMSID           Message Id.
     C                     PARM           ZAMSGF           Message file.
     C                     PARM           ZAMSDA           Message data.
     C                     PARM           ZAMSTX132        Messge text
      *
      ** Restore the saved record to ensure that no other fields
      ** will be updated aside from error msg id, text and severity
      *
     C                     MOVELL2140S    L2140F
      *
     C           WWERR     IFEQ 'Y'
     C                     MOVE 'W'       TMSVR
     C                     ELSE
     C                     MOVE 'E'       TMSVR
     C                     ENDIF
     C                     MOVELZAMSID    TMSID
     C                     MOVELZAMSTX    TMSTX
      *
      ** If not a warning error, stop validation and return
      *
     C           TMSVR     IFEQ 'E'
     C           *INLR     ANDNE'1'
     C                     ROLBK
     C                     UPDATLE2140D0
     C                     CLOSELE2140PD
     C                     MOVE '1'       *INLR
     C                     DUMP
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      /TITLE SR/GLZADD
      *****************************************************************
      *  GLZADD - Subroutine to add an amount to the total            *
      *  Called by: Main program                                      *
      *  Calls    : GLZSUM - Subr. to carry out the addition          *
      *****************************************************************
     CSR         GLZADD    BEGSR                           ***  GLZADD ***
      *
     CSR                   Z-ADDZZAMT     ZZAMT  153     91-DEFINE ZZAMT
     CSR 91                GOTO ZZAEND                     AMT = 0.BYPASS
      *
      * SPLIT ZZAMT INTO INTEGER AND DECIMAL FIELDS
     CSR                   Z-ADDZZAMT     ZZAMTI 150       INTEGER FIELD
     CSR                   MOVE ZZAMT     ZZAMTD  30       DECIMAL FIELD
      * BOTH ZZAMTI AND ZZAMTD CONTAIN A 'SIGN' ZONE NOW
      *
     CSR                   EXSR GLZSUM
     CSR         ZZAEND    ENDSR                           ***  ZZAEND ***
      /TITLE SR/GLZSUB
      *****************************************************************
      *  GLZSUB - Subroutine to subtract an amount to the total       *
      *  Called by: Main program                                      *
      *  Calls    : GLZADD - Subroutine to add an amount to the total *
      *****************************************************************
     CSR         GLZSUB    BEGSR                           ***  GLZSUB ***
      * PROCESSING. JUST REVERSE THE 'SIGN' AND ADD
     CSR                   Z-SUBZZAMT     ZZAMT  153       REVERSE 'SIGN'
     CSR                   EXSR GLZADD                       AND 'ADD'
     CSR                   Z-SUBZZAMT     ZZAMT            RESTORE 'SIGN'
     CSR                   ENDSR
      /TITLE SR/GLZSUM
      *****************************************************************
      *  GLZSUM - Subroutine to carry out the additon for subroutine  *
      *  Called by: Main program                                      *
      *  Calls    : None                                              *
      *****************************************************************
     CSR         GLZSUM    BEGSR                           ***  GLZSUM ***
      *
     CSR                   Z-ADDZZTOTI    ZZTOTI 150       DEFINE ZZTOTI
     CSR                   Z-ADDZZTOTD    ZZTOTD  30       DEFINE ZZTOTD
     CSR                   SETOF                     919293
     CSR                   SETOF                     949599
      *
      *    DETERMINE SIGN OF ZZAMTI & D,   92 IF NEG
     CSR         ZZAMTI    COMP 0                      9293
     CSR 93      ZZAMTD    COMP 0                      9293
     CSR 93                GOTO ZZSEND                     ZERO BYPASS
      *
      *    DETERMINE SIGN OF ZZTOTI & D, 91 IF NEG.
     CSR         ZZTOTI    COMP 0                      9193
     CSR 93      ZZTOTD    COMP 0                      9193
      *
      *    IF ZZTOTAL IS ZERO, RETURN ZZAMOUNT.
     CSR 93                Z-ADDZZAMTI    ZZTOTI
     CSR 93                Z-ADDZZAMTD    ZZTOTD
     CSR 93                GOTO ZZSEND                     ZERO BYPASS
      *    IF SIGNS DIFFER BYPASS OVERFLOW CHECKS.
     CSR 91N92
     CORN91 92             GOTO ZZOFPS
      *
     CSR         ZZAMTD    ADD  ZZTOTD    ZZWK2   40
     CSR         ZZWK2     COMP 999                  93    '93' = CARRY
     CSRN93      ZZWK2     COMP -999                   93    INTO INTEGER.
      *
     CSR 93N92   ZZAMTI    ADD  1         ZZWK3  150       ADD 'CARRY' +VE
     CSR    93 92ZZAMTI    SUB  1         ZZWK3            SUB 'CARRY' -VE
     CSR 93      ZZTOTI    ADD  ZZWK3     ZZWK3
     CSRN93      ZZTOTI    ADD  ZZAMTI    ZZWK3
      *
      * IF THE MODULUS OF ZZWK3 IS LT MOD. ZZTOTI THEN O/F HAS OCCURED
     CSRN92      ZZWK3     COMP ZZTOTI                 99  -92 MEANS NOS.
     CSR 92      ZZWK3     COMP ZZTOTI               99     NEGATIVE
     CSRN99                Z-ADDZZWK2     ZZTOTD
     CSRN99                Z-ADDZZWK3     ZZTOTI
      *
      * IF O/F ZEROIZE ZZAMT , IND '99' SET AND ZZTOT FIELDS LEFT INTACT.
     CSR 99                Z-ADD0         ZZAMT  153
     CSR                   GOTO ZZSEND
      *
      * THE 'SIGNS' ARE DIFFERENT
     CSR         ZZOFPS    TAG                             ***  ZZOFPS ***
      * IF ZZAMT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZTOT
      *
     CSR 92                Z-SUBZZAMTI    ZZAMTI 150
     CSR 92                Z-SUBZZAMTD    ZZAMTD  30
      *
      * IF ZZTOT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZAMT.
      *
     CSR 91                Z-SUBZZTOTI    ZZTOTI
     CSR 91                Z-SUBZZTOTD    ZZTOTD
      * BOTH ZZAMT AND ZZTOT ARE NOW POSITIVE.
      *
     CSR         ZZTOTI    COMP ZZAMTI               93  95
     CSR 95      ZZTOTD    COMP ZZAMTD               93  95
      *
      * IF ZZTOT EQ. ZZAMT RETURN ZERO.
     CSR 95                Z-ADD0         ZZTOTI
     CSR 95                Z-ADD0         ZZTOTD
     CSR 95                GOTO ZZSEND
      *
      * IF ZZTOT GT. ZZAMT.
     CSR 93      ZZAMTD    COMP ZZTOTD               94
     CSR 93 94   ZZTOTI    SUB  1         ZZTOTI
     CSR 93 94   ZZTOTD    ADD  1000      ZZWK2
     CSR 93      ZZTOTI    SUB  ZZAMTI    ZZTOTI
     CSR 93 94   ZZWK2     SUB  ZZAMTD    ZZTOTD
     CSR 93N94   ZZTOTD    SUB  ZZAMTD    ZZTOTD
      *
      *
      * IF ZZAMT GT. ZZTOT.
     CSRN93      ZZTOTD    COMP ZZAMTD               94
     CSRN93 94   ZZAMTI    SUB  1         ZZWK3  150
     CSRN93 94   ZZAMTD    ADD  1000      ZZWK2
     CSRN93 94   ZZWK3     SUB  ZZTOTI    ZZTOTI
     CSRN93N94   ZZAMTI    SUB  ZZTOTI    ZZTOTI
     CSRN93 94   ZZWK2     SUB  ZZTOTD    ZZTOTD
     CSRN93N94   ZZAMTD    SUB  ZZTOTD    ZZTOTD
      *
      * REVERSE SIGN OF ZZTOT IF LARGER I/P FIELDS WERE NEGATIVE
     CSR                   SETOF                     94
     CSR 93 91
     CORN93 92             SETON                     94
     CSR 94                Z-SUBZZTOTI    ZZTOTI
     CSR 94                Z-SUBZZTOTD    ZZTOTD
      **
      **   RESTORE SIGN OF ZZAMTI & ZZAMTD IF IT WAS REVERSED.
     CSR 92                Z-SUBZZAMTI    ZZAMTI
     CSR 92                Z-SUBZZAMTD    ZZAMTD
     CSR         ZZSEND    TAG                             ***  ZZSEND ***
      **
      **   IF ZZTOTD IS ZERO, AND ZZTOTI IS NEG. SET UP ZZNEGD.
     CSR                   SETOF                     96
     CSR         ZZTOTD    COMP 0                        91
     CSR 91      ZZTOTI    COMP 0                      96
     CSR 96                MOVE '.000-'   ZZNEGD  5
     CSR                   ENDSR                             ** ENDSR **
      /TITLE SR/SREXCH                                                                      BUG23133
      *****************************************************************                     BUG23133
      *  SREXCH - Subroutine to get exchange rate                     *                     BUG23133
      *  Called by: SRVALD                                            *                     BUG23133
      *  Calls    : AOCURR0, ZXRATE, ZEDIT                            *                     BUG23133
      *****************************************************************                     BUG23133
     C           SREXCH    BEGSR                                                            BUG23133
      *                                                                                     BUG23133
      ** If facility currency and MFU currency is non-base, get cross-rate.                 BUG23133
      ** Else, get spot rate from currencies file.                                          BUG23133
      *                                                                                     BUG23133
     C           MFCCY     IFNE BJCYCD                                                      BUG23133
     C           SUCCY     ANDNEBJCYCD                                                      BUG23133
     C                     CALL 'AOCURRR0'                                                  BUG23133
     C                     PARM *BLANKS   PRTCD                                             BUG23133
     C                     PARM '*KEY'    POPTN                                             BUG23133
     C                     PARM MFCCY     PCURR   3                                         BUG23133
     C           SDCURR    PARM SDCURR    DSSDY                                             BUG23133
     C           PRTCD     IFNE *BLANKS                                                     BUG23133
     C           *LOCK     IN   LDA                                                         BUG23133
     C                     MOVEL'SDCURRPD'DBFILE           ************                     BUG23133
     C                     Z-ADD34        DBASE            * DBERR 34 *                     BUG23133
     C                     MOVELMFCCY     DBKEY            ************                     BUG23133
     C                     OUT  LDA                                                         BUG23133
     C                     EXSR *PSSR                                                       BUG23133
     C                     ELSE                                                             BUG23133
     C                     Z-ADDA6SPRT    ZRATE1                                            BUG23133
     C                     MOVELA6MDIN    ZMDI1                                             BUG23133
     C                     ENDIF                                                            BUG23133
     C                     CALL 'AOCURRR0'                                                  BUG23133
     C                     PARM *BLANKS   PRTCD                                             BUG23133
     C                     PARM '*KEY'    POPTN                                             BUG23133
     C                     PARM SUCCY     PCURR                                             BUG23133
     C           SDCURR    PARM SDCURR    DSSDY                                             BUG23133
     C           PRTCD     IFNE *BLANKS                                                     BUG23133
     C           *LOCK     IN   LDA                                                         BUG23133
     C                     MOVEL'SDCURRPD'DBFILE           ************                     BUG23133
     C                     Z-ADD35        DBASE            * DBERR 35 *                     BUG23133
     C                     MOVELSUCCY     DBKEY            ************                     BUG23133
     C                     OUT  LDA                                                         BUG23133
     C                     EXSR *PSSR                                                       BUG23133
     C                     ELSE                                                             BUG23133
     C                     Z-ADDA6SPRT    ZRATE2                                            BUG23133
     C                     MOVE A6MDIN    ZMDI2                                             BUG23133
     C                     ENDIF                                                            BUG23133
     C                     CALL 'ZXRATE'                                                    BUG23133
     C                     PARM           ZRATE1 138                                        BUG23133
     C                     PARM           ZMDI1   1                                         BUG23133
     C                     PARM           ZRATE2 138                                        BUG23133
     C                     PARM           ZMDI2   1                                         BUG23133
     C                     PARM           ZRATEX 138                                        BUG23133
     C                     PARM           ZMDIX   1                                         BUG23133
     C                     Z-ADDZRATEX    WXRAT  167                                        BUG23133
     C                     MOVELWXRAT     ZFIELD                                            BUG23133
     C                     CALL 'ZEDIT'                                                     BUG23133
     C                     PARM           ZFIELD                                            BUG23133
     C                     PARM 7         ZADEC                                             BUG23133
     C                     MOVE ZFIELD    SUXRT                                             BUG23133
     C                     MOVE ZMDIX     SUXID                                             BUG23133
     C**********           ELSE                                                  BUG23133A BUG23133B
     C                     ENDIF                                                           BUG23133B
     C           MFCCY     IFEQ BJCYCD                                                     BUG23133B
     C           SUCCY     ANDNEBJCYCD                                                     BUG23133B
     C                     CALL 'AOCURRR0'                                                  BUG23133
     C                     PARM *BLANKS   PRTCD                                             BUG23133
     C                     PARM '*KEY'    POPTN                                             BUG23133
     C                     PARM SUCCY     PCURR                                             BUG23133
     C           SDCURR    PARM SDCURR    DSSDY                                             BUG23133
     C           PRTCD     IFNE *BLANKS                                                     BUG23133
     C           *LOCK     IN   LDA                                                         BUG23133
     C                     MOVEL'SDCURRPD'DBFILE           ************                     BUG23133
     C                     Z-ADD35        DBASE            * DBERR 35 *                     BUG23133
     C                     MOVELSUCCY     DBKEY            ************                     BUG23133
     C                     OUT  LDA                                                         BUG23133
     C                     EXSR *PSSR                                                       BUG23133
     C                     ELSE                                                             BUG23133
     C                     Z-ADDA6SPRT    WXRAT                                             BUG23133
     C                     MOVELWXRAT     ZFIELD                                            BUG23133
     C                     CALL 'ZEDIT'                                                     BUG23133
     C                     PARM           ZFIELD                                            BUG23133
     C                     PARM 7         ZADEC                                             BUG23133
     C                     MOVE ZFIELD    SUXRT                                             BUG23133
     C                     MOVELA6MDIN    SUXID                                             BUG23133
     C                     ENDIF                                                            BUG23133
     C                     ENDIF                                                            BUG23133
     C           MFCCY     IFNE BJCYCD                                                     BUG23133B
     C           SUCCY     ANDEQBJCYCD                                                     BUG23133B
     C                     CALL 'AOCURRR0'                                                 BUG23133B
     C                     PARM *BLANKS   PRTCD                                            BUG23133B
     C                     PARM '*KEY'    POPTN                                            BUG23133B
     C                     PARM MFCCY     PCURR                                            BUG23133B
     C           SDCURR    PARM SDCURR    DSSDY                                            BUG23133B
     C           PRTCD     IFNE *BLANKS                                                    BUG23133B
     C           *LOCK     IN   LDA                                                        BUG23133B
     C                     MOVEL'SDCURRPD'DBFILE           ************                    BUG23133B
     C                     Z-ADD35        DBASE            * DBERR 35 *                    BUG23133B
     C                     MOVELMFCCY     DBKEY            ************                    BUG23133B
     C                     OUT  LDA                                                        BUG23133B
     C                     EXSR *PSSR                                                      BUG23133B
     C                     ELSE                                                            BUG23133B
     C                     Z-ADDA6SPRT    WXRAT                                            BUG23133B
     C                     MOVELWXRAT     ZFIELD                                           BUG23133B
     C                     CALL 'ZEDIT'                                                    BUG23133B
     C                     PARM           ZFIELD                                           BUG23133B
     C                     PARM 7         ZADEC                                            BUG23133B
     C                     MOVE ZFIELD    SUXRT                                            BUG23133B
     C           A6MDIN    IFEQ 'M'                                                        BUG23133B
     C                     MOVEL'D'       SUXID                                            BUG23133B
     C                     ELSE                                                            BUG23133B
     C                     MOVEL'M'       SUXID                                            BUG23133B
     C                     ENDIF                                                           BUG23133B
     C                     ENDIF                                                           BUG23133B
     C                     ENDIF                                                           BUG23133B
     C                     MOVE 'Y'       WEXCH   1                                         BUG23133
     C                     ENDSR                                                            BUG23133
      /TITLE SR/SRTOLR                                                                      BUG23133
      *****************************************************************                     BUG23133
      *  SRTOLR - Subroutine to check if rate is within tolerance     *                     BUG23133
      *  Called by: SRVALD                                            *                     BUG23133
      *  Calls    : AOCURR0, AOSVALR0                                 *                     BUG23133
      *****************************************************************                     BUG23133
     C           SRTOLR    BEGSR                                                            BUG23133
     C           WEXCH     IFNE 'Y'                                                         BUG23133
     C********** SACTN     ANDNE'D'                                              BUG23133A BUG23133B
     C           SACTN     ANDNE'I'                                                        BUG23133B
     C                     MOVELSUXRT     WCRAT  12                                         BUG23133
     C           '.'       CHECKWCRAT     L       20                                        BUG23133
     C           L         SUBSTWCRAT:1   WDGT1   4                                         BUG23133
     C                     ADD  2         L                                                 BUG23133
     C                     SUBSTWCRAT:L   WDECS   7                                         BUG23133
     C                     MOVE WDGT1     WDGT2   40                                        BUG23133
     C                     MOVE WDECS     WDEC1   70                                        BUG23133
     C           WDEC1     DIV  10000000  WDEC2  117                                        BUG23133
     C           WDGT2     ADD  WDEC2     WSPR2  117                                        BUG23133
     C                     CALL 'AOCURRR0'                                                  BUG23133
     C                     PARM *BLANKS   PRTCD                                             BUG23133
     C                     PARM '*KEY'    POPTN                                             BUG23133
     C                     PARM SUCCY     PCURR                                             BUG23133
     C           SDCURR    PARM SDCURR    DSSDY                                             BUG23133
     C           PRTCD     IFNE *BLANKS                                                     BUG23133
     C           *LOCK     IN   LDA                                                         BUG23133
     C                     MOVEL'SDCURRPD'DBFILE           ************                     BUG23133
     C                     Z-ADD35        DBASE            * DBERR 35 *                     BUG23133
     C                     MOVELSUCCY     DBKEY            ************                     BUG23133
     C                     OUT  LDA                                                         BUG23133
     C                     EXSR *PSSR                                                       BUG23133
     C                     ELSE                                                             BUG23133
     C                     Z-ADDA6SPRT    WSPOT  117                                        BUG23133
     C                     ENDIF                                                            BUG23133
      *                                                                                     BUG23133
      ** Get system value exchange rate tolerance.                                          BUG23133
      *                                                                                     BUG23133
     C                     CALL 'AOSVALR0'                                                  BUG23133
     C                     PARM *BLANKS   PRTCD                                             BUG23133
     C                     PARM SVALKI    SVALK1                                            BUG23133
     C                     PARM           SVAL1                                             BUG23133
     C                     PARM *BLANKS   SVALK2                                            BUG23133
     C                     PARM           SVAL2                                             BUG23133
     C                     PARM *BLANKS   SVALK3                                            BUG23133
     C                     PARM           SVAL3                                             BUG23133
     C                     PARM *BLANKS   SVALK4                                            BUG23133
     C                     PARM           SVAL4                                             BUG23133
     C                     PARM *BLANKS   SVALK5                                            BUG23133
     C                     PARM           SVAL5                                             BUG23133
     C                     PARM *BLANKS   SVALK6                                            BUG23133
     C                     PARM           SVAL6                                             BUG23133
     C                     PARM *BLANKS   SVALK7                                            BUG23133
     C                     PARM           SVAL7                                             BUG23133
     C                     PARM *BLANKS   SVALK8                                            BUG23133
     C                     PARM           SVAL8                                             BUG23133
     C                     PARM *BLANKS   SVALK9                                            BUG23133
     C                     PARM           SVAL9                                             BUG23133
     C                     PARM *BLANKS   SVALK0                                            BUG23133
     C                     PARM           SVAL10                                            BUG23133
     C           PRTCD     IFNE *BLANK                                                      BUG23133
     C           *LOCK     IN   LDA                                                         BUG23133
     C                     MOVEL'*KEY    'DBKEY                                             BUG23133
     C                     MOVEL'SDSVALPD'DBFILE           ************                     BUG23133
     C                     Z-ADD036       DBASE            * DBERR 36 *                     BUG23133
     C                     OUT  LDA                        ************                     BUG23133
     C                     EXSR *PSSR                                                       BUG23133
     C                     ELSE                                                             BUG23133
     C                     MOVELSVAL1     WRTPR   20                                        BUG23133
     C                     ENDIF                                                            BUG23133
     C           WRTPR     DIV  100       WRTDC   32                                        BUG23133
     C           1.00      SUB  WRTDC     WLOWR   32                                        BUG23133
     C           1.00      ADD  WRTDC     WUPPR   32                                        BUG23133
     C           WSPOT     MULT WLOWR     WRTMN  117                                        BUG23133
     C           WSPOT     MULT WUPPR     WRMNX  117                                        BUG23133
     C           WSPR2     IFLT WRTMN                                                       BUG23133
     C           WSPR2     ORGT WRMNX                                                       BUG23133
     C                     MOVE '1'       *IN47                                             BUG23133
     C                     MOVEL'LEU0066' ZAMSID                                            BUG23133
     C                     EXSR ZASNMS                                                      BUG23133
     C                     ENDIF                                                            BUG23133
     C                     ENDIF                                                            BUG23133
     C                     MOVE 'N'       WEXCH                                             BUG23133
     C                     ENDSR                                                            BUG23133
      /TITLE SR/*PSSR
      *****************************************************************
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *  Calls  :                                                     *
      *****************************************************************
     C           *PSSR     BEGSR                                       **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      *                                                                   150215
      ** If called as linked enquiry, set return code to 'E' and execute  150215
      ** end of program processing                                        150215
      *                                                                   150215
     C           *IN05     IFEQ *ON                                       150215
     C                     MOVE 'E'       RTNDWF                          150215
     C                     EXSR SRENDP                                    150215
     C                     END                                            150215
      *
     C           PMODE     IFEQ 'I'
     C           PMODE     OREQ 'R'
     C                     CALL 'DBERRCTL'
      *
     C                     ELSE
     C                     ROLBK
     C                     MOVELL2140S    L2140F
     C                     MOVE 'E'       TMSVR
     C                     MOVE DBASE     TMSID
     C                     MOVELDBFILE    TMSTX
     C           WOPNT     IFEQ 'Y'
     C                     UPDATLE2140D0
     C                     CLOSELE2140PD
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      *
      /TITLE SR/ZALIGN
     C/COPY ZSRSRC,ZALIGNZ2
      /TITLE SR/ZEDIT
     C/COPY ZSRSRC,ZEDITZ2
      /TITLE SR/ZDATE1
     C/COPY ZSRSRC,ZDATE1Z2
      /TITLE SR/ZDATE2
     C/COPY ZSRSRC,ZDATE2Z2
      /TITLE SR/ZACCH
     C/COPY ZSRSRC,ZACCH
      /TITLE SR/ZCHKH
     C/COPY ZSRSRC,ZCHKH
      /EJECT
     C/COPY ZSRSRC,ZFWDT                                                  CLE009
     C/COPY ZSRSRC,ZXRATEZ1                                                                   CLE023
** CPY@
(c) Finastra International Limited 2001
**  POWR -- USED IN CURRENCY CONVERSION(FACILITY)
0000001
0000010
0000100
0001000
0010000
0100000
1000000
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
