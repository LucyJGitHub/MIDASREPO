     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Facility amendments maintenance')             *
/*XBIA*: OVRDBF FILE(LEFCAMSQ) TOFILE(LEFCAML3) SHARE(*NO)          : *                       190017
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE2150 - Facility Amendments Maintenance                     *
      *                                                               *
      *  Function:  This is the new Facilitites Amendments            *
      *             maintenance program.  It will be run in one of    *
      *             four modes, depending on the value of a parameter *
      *             (Mode) passed to it:                              *
      *               1. Mode = 'I', Interactive.                     *
      *                  The program is called from the Midas         *
      *                  Transaction Input menu.  Data is processed   *
      *                  through a screen in the standard way.        *
      *               2. Mode = 'R', Record.                          *
      *                  The program is run interactively through a   *
      *                  Midas test environment, and data is          *
      *                  processed through a screen as in 1.  At the  *
      *                  point when updates are performed a record is *
      *                  written to a new 'test harness' physical     *
      *                  file which will mirror exactly the contents  *
      *                  of the screen.                               *
      *               3. Mode = 'P', Play.                            *
      *                  The program is run interactively or in batch *
      *                  through a Midas test environment.  Data is   *
      *                  read in from the 'test harness' file         *
      *                  (written in 2, record mode).  Updates to the *
      *                  Midas database will be as done in 1.         *
      *               4. Mode 'B', Batch.                             *
      *                  The program is run in batch and called by a  *
      *                  communcations control program.  Data which   *
      *                  has been entered in a PC subsystem is        *
      *                  received from a message queue and passed to  *
      *                  the program as a parameter.  Updates to the  *
      *                  Midas database will be as done according to  *
      *                  the contents of the parameter and a returned *
      *                  message will be passed back to the control   *
      *                  program.                                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      *  Prev Amend No. CLE139             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CAS009             Date 16May05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 CSF002             Date 11Aug03               *
      *                 CLE031             Date 04Feb03               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 204452             Date 03May02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CPK015             Date 01Jul02               *
      *                 CAP071             Date 19Mar02               *
      *                 191838             Date 27Apr01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 195955             Date 20Jul01               *
      *                 165649             Date 20Jul01               *
      *                 186075             Date 29Jun01               *
      *                 194509             Date 19Jun01               *
      *                 CLE014             Date 20Mar01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006             Date 15May01               *
      *                 190017             Date 07Mar01               *
      *                 175536             Date 11Jan01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CRT004             DATE 04MAY00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 168022             Date 01Nov99               *
      *                 CLE009             Date 07Jun99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 159692             Date 11May99               *
      *                 157580             Date 03May99               *
      *                 158617 (BT0708)    Date 30Apr98               *
      *                 158616 (BT0933)    Date 03Jul98               *
      *                 157217 (BT1420)    Date 29Jan99               *
      *                 156676             Date 12Mar99               *
      *                 154797 (Reopen)    Date 03Mar99               *
      *                 154799             Date 24Feb99               *
      *                 151295             Date 18Feb99               *
      *                 153357             Date 22Jan99               *
      *                 149746             Date 11Dec98               *
      *                 142446             Date 14Sep98               *
      *                 135744             Date 30Jul98               *
      *                 129521             Date 26Jan98               *
      *                 129467             Date 26Jan98               *
      *                 127737             Date 10Dec97               *
      *                 CLE005 *Create     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CLE139 - Lending Fee Capitalisation (Recompile)              *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CAS009 - EIR/AC Accounting (Recompile)                       *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSF002 - Global layer.                                       *
      *           - Replace previous ZBACT* parameters with the new   *
      *             unique code.                                      *
      *  CLE031 - Lending Enhancements.                               *
      *  204452 - Recompiled due to changed PF/LEFEED.                *
      *  CPK015 - Release 4.01 Packaging:                             *
      *           - A default timestamp string is required for WRITE. *
      *  CAP071 - Conversion of LE inputs into modular structure to   *
      *           use as APIs. (Recompile)                            *
      *  191838 - Allow facility decrease as long as the facility     *
      *           amount will not be less than zero.                  *
      *  195955 - Negative facility amount is not properly defaulted  *
      *           on screen.                                          *
      *  165649 - Allow pgm to reactivate a facility with a negative  *
      *           balance.                                            *
      *  186075 - Last facility amount should always be the default   *
      *           used during reactivation.                           *
      *  194509 - Amended Transactions go to Re-auth and then complete*
      *  CLE014 - Customer Driven Lending Enhancements                *
      *  CSD006 - Use long DS with SDCURRPD.                          *
      *  190017 - Prevent same sequence number being allocated to     *
      *           transactions entered by 2 different users. Introduce*
      *           a 'dummy' LF which is overridden to LEFCAML3, SHARE(*
      *           *NO) for retrieval of last sequence number. Move    *
      *           the code to find the next sequence number to just   *
      *           before the write of the new record to file. Keep    *
      *           trying to write if error found on WRITE to file.   *
      *  175536 - Error message LEF0209 appears after user pressed    *
      *           'Enter' on detail screen after an action 'X'.       *
      *  CRT004 - Cashier Midas TCP/IP interface.                     *
      *             Recompiled over SDBRCHPD Changes                  *
      *  168022 - Check SDGELRPD to see if flag 'Originating Branches *
      *           - User Validation Required' = 'Y' before validation.*
      *  CLE009 - LE I/C Swift Message Generation for Fees            *
      *  159692 - Allow facility increase/decrease and revolving      *
      *           credit change even if facility has expired.         *
      *  157580 - Recompile due to changes in LE2150DF.               *
      *  158617 - Field ZAMSGF not always set up in time              *
      *  158616 - Need to set up PCRF when transaction input via      *
      *           AS/400 (same as BT0506 for other input functions)   *
      *  142446 - PCRF is that of the facility. 158616 should specify *
      *           PMODE not equal to 'B' to set up PCRF when input    *
      *           via AS/400.                                         *
      *  157217 - Facility amendments generated via assignment should *
      *           not be validated for exposure                       *
      *  156676 - Authorizing user is blank on most transactions.     *
      *           Insert and amend in batch mode does not update      *
      *           the authorizing user.                               *
      *  154797 - For facility reactivation of closed facility, value *
      *           date must be today if originally closed before      *
      *           expiry, otherwise must be expiry date. Will default *
      *           to today. Replace check against closing date.       *
      *  154799 - Recompiled due to changes in /COPY,ZACCH            *
      *  151295 - Problems authorising repayment schedules            *
      *           Recompiled due to changes in ZCHKH                  *
      *  153357 - New Expiry Date should not be less than the former  *
      *           expiry date if it is an expired facility or should  *
      *           not be less than the former closing date if it is a *
      *           closed facility.                                    *
      *  149746 - Facility amendment not allowed for reactivating     *
      *           facility.                                           *
      *  135744 - Branch field corrupted and then written to file     *
      *           because 'read' used; should be 'reade'.             *
      *           Causes facility amendments (increases/decreases)    *
      *           to be missed when entering a loan.                  *
      *  129521 - In batch mode, incorrect User validation. It should *
      *           be the user from the message.                       *
      *  129467 - Incorrect ROLBK causes same message to be processed *
      *           from QMQM several times.                            *
      *  127737 - 'FR' for a closed facility should check against     *
      *           closing date instead of expiry date                 *
      *  CLE005 - Customer Lending Enhancements - Others (T3)         *
      *                                                               *
      *****************************************************************
     FLEFCAML3IF  E           K        DISK
     F            LEFCAMPF                          KRENAMELEFCAMP3
     FLEFCAML5IF  E           K        DISK
     F            LEFCAMPF                          KRENAMELEFCAMP5
     FLEFCAMSQIF  E           K        DISK                               190017
     F            LEFCAMPF                          KRENAMELEFCAMQ1       190017
     FFCLTY   IF  E           K        DISK
     FLE2150DFCF  E                    WORKSTN                        UC
     F                                        SRRN  KSFILE LE2150S0
     FLE2150PDUF  E                    DISK         KINFSR *PSSR A    UC
     FLEFCAMZZUF  E                    DISK         KINFSR *PSSR
     FLEFCAML6UF  E           K        DISK         KINFSR *PSSR A
     F                                              KCOMIT
      *
     F/COPY WNCPYSRC,LEH00588
     FLEFEEDL1UF  E           K        DISK         KINFSR *PSSR          CLE009
     F                                              KCOMIT                CLE009
     F/COPY WNCPYSRC,LE2150F001
      *                                                                   CLE009
      /SPACE 3
      *****************************************************************
      *                                                               *
      *    F U N C T I O N   O F   I N D I C A T O R S                *
      *                                                               *
      *    11           Invalid action                                *
      *    12           Invalid customer                              *
      *    13           Invalid facility                              *
      *    14           Invalid amendment type                        *
      *    15           Invalid value date                            *
      *    16           Invalid sequence number                       *
      *    17           Invalid review from customer (first screen)   *
      *    18           Invalid review from facility (first screen)   *
      *    19           Invalid unauthorised field   (first screen)   *
      *    20           Insert action request on subfile              *
      *    21           Invalid currency                              *
      *    22           Invalid amount                                *
      *    23           Invalid revolving credit                      *
      *    24           Invalid new expiry date                       *
      *    25           Delete action                                 *
      *    26           Authorise action                              *
      *    27           Invalid review from customer                  *
      *    28           Invalid review from facility                  *
      *    29           Invalid unauthorised field                    *
      *    30           Protect details on enquire, delete and auth.  *
      *    31           Authorise is valid                            *
      *    32           More than 1 subfile page                      *
      *    33           Subfile clear                                 *
      *    34           Subfile end                                   *
      *    35           Rollup is pressed                             *
      *    36           Invalid select field                          *
      *    37           Subfile next page                             *
      *    38           No details to display                         *
      *    39           Output sequence number for insert             *
      *    40           Work indicator for TESTN                      *
      *    41           READE to LEFEEDL1                             *   CLE009
      *    42           LOKUP currency array                          *   CLE009
      *    43           Display sign for reactivate amount            *                       165649
      *    44           Invalid sign for reactivate amount            *                       165649
      *    80           File handling                                 *   190017
      *                                                               *
      *****************************************************************
      /EJECT
     E                    STAMP   1   1 26                                                    CPK015
      ** Array containing dummy timestamp                                                     CPK015
     E                    CPY@    1   1 80
     E                    @RUN       10  5 0
     E                    @OAM       10 13 0
      *
     E                    CYA       150  3                                CLE009
      ** Currencies array                                                 CLE009
     E                    TND       150  1 0                              CLE009
      ** Telex Notice Days array                                          CLE009
      *                                                                   CLE009
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZALIGNZ1
     E/COPY ZSRSRC,ZHOLE
      *
      /SPACE 3
      *
     ILEFCAMQ1                                                            190017
      ** Rename fields                                                    190017
     I              RECI                            RECIQ                 190017
     I              BRCA                            BRCAQ                 190017
     I              CNUM                            CNUMQ                 190017
     I              FACT                            FACTQ                 190017
     I              FCNO                            FCNOQ                 190017
     I              SQNO                            SQNOQ                 190017
     I              VLDT                            VLDTQ                 190017
     I              FATP                            FATPQ                 190017
     I              ACCY                            ACCYQ                 190017
     I              AAMT                            AAMTQ                 190017
     I              RVCR                            RVCRQ                 190017
     I              NDEX                            NDEXQ                 190017
     I              ASTS                            ASTSQ                 190017
     I              IUSR                            IUSRQ                 190017
     I              AUSR                            AUSRQ                 190017
     I              XUSR                            XUSRQ                 190017
     I              ORED                            OREDQ                 190017
     I              TLCD                            TLCDQ                 190017
     I              CHTP                            CHTPQ                 190017
     I              PCRF                            PCRFQ                 190017
     I              PCOB                            PCOBQ                 190017
     I              GASSA                           GASSAQ                190017
     I              AUTH                            AUTHQ                 190017
     I              SWRI                            SWRIQ                 190017
      *                                                                   190017
     IRUNDAT      DS
      ** Rundate data area
      *
     I                                       13  13 MBIN
      *
     ILDA         DS                            256
      ** Local data area for database error details
      *
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
     IPSDS       SDS
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
      *
     I                                     *PROGRAM SPGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     IPINPP       DS
      ** Transmitted message format
      *
     I                                        1   4 PIMTYP
     I                                        5  20 PITUSR
     I                                       21  36 PITWST
     I                                       37  51 PITREF
     I                                       52  55 PITSEQ
     I                                       56  56 PITACT
     I                                       57  64 PITDST
     I                                       65  72 PITTST
     I                                       73  76 PINORB
     I                                       77  80 PIFILL
     I                                       81  86 PICNUM
     I                                       87  89 PIFACT
     I                                       90  91 PIFCNO
     I                                       92  99 PIVLDT
     I                                      100 102 PISQNO
     I                                      103 105 PIACCY
     I                                      106 119 PIFAMT
     I                                      120 120 PIRVCR
     I                                      121 128 PINDEX
     I                                      129 138 PIIUSR
     I                                      139 148 PIAUSR
     I                                      149 158 PIXUSR
     I                                      159 161 PIORBR
     I                                      162 162 PIGASS                157217
      *
     IPOUTP       DS
      ** Returned message format
      *
     I                                        1   4 POMTYP
     I                                        5  20 POTUSR
     I                                       21  36 POTWST
     I                                       37  51 POTREF
     I                                       52  55 POTSEQ
     I                                       56  56 POTACT
     I                                       57  64 POTDST
     I                                       65  72 POTTST
     I                                       73  76 PONORB
     I                                       77  80 POFILL
     I                                       81  81 POMSEV
     I                                       82  88 POMSID
     I                                       89 166 POMSTX
     I                                      167 172 POCNUM
     I                                      173 175 POFACT
     I                                      176 177 POFCNO
     I                                      178 185 POVLDT
     I                                      186 188 POSQNO
      *                                                                   158616
      * DS for calculating PC reference                                   158616
     ILEALLO    E DSLEALLO                                                158616
      *
     ISDCLND    E DSSDCLNDPD
      ** Customer lending ICD
      *
     ISCSARD    E DSSCSARDPD
      ** SAR details
      *
     ISDBANK    E DSSDBANKPD
      ** Bank details
      *
     ISDBRCH    E DSSDBRCHPD
      ** Branch details
      *
     ISDCUST    E DSSDCUSTPD
      ** Customer details
     I              QQDFAC                          QQDFA1                                    CGL029
      *
     ISDCURR    E DSSDCURRPD
      ** Currency details
      *
     ISDNOST    E DSSDNOSTPD                                              CLE009
      *                                                                   CLE009
      ** Nostro details                                                   CLE009
      *                                                                   CLE009
     ISDGELR    E DSSDGELRPD                                              168022
      ** General Ledger Details                                           168022
     I              QQACCD                          QQACC1                                    CGL029
      *                                                                   168022
     IDSFDY     E DSDSFDY
      ** Short data structure for access objects
      *
     IDSSDY     E DSDSSDY
      ** Long data structure for access objects
      *
     IWRKFLD      DS
      ** Data structure for Workfields
      *
     I                                        1   8 WDTST
     I                                        9  16 WTMST
     I                                       17  17 WACTI
     I                                       18  20 WBRCA
     I                                       21  260WCNUM
     I                                       27  290WFACT
     I                                       30  310WFCNO
     I                                       32  340WSQNO
     I                                       35  390WVLDT
     I                                       40  41 WFATP
     I                                       42  44 WACCY
     I                                       45  570WAAMT
     I**********                             58  58 WRVCR                                     165649
     I**********                             59  630WNDEX                                     165649
     I**********                             64  64 WASTS                                     165649
     I**********                             65  74 WIUSR                                     165649
     I**********                             75  84 WAUSR                                     165649
     I**********                             85  94 WXUSR                                     165649
     I**********                             95  990WORED                                     165649
     I**********                            100 1040WTLCD                                     165649
     I**********                            105 105 WCHTP                                     165649
     I**********                            106 106 WMSEV                                     165649
     I**********                            107 113 WMSID                                     165649
     I**********                            114 191 WMSTX                                     165649
     I                                       58  58 WSIGN                                     165649
     I                                       59  59 WRVCR                                     165649
     I                                       60  640WNDEX                                     165649
     I                                       65  65 WASTS                                     165649
     I                                       66  75 WIUSR                                     165649
     I                                       76  85 WAUSR                                     165649
     I                                       86  95 WXUSR                                     165649
     I                                       96 1000WORED                                     165649
     I                                      101 1050WTLCD                                     165649
     I                                      106 106 WCHTP                                     165649
     I                                      107 107 WMSEV                                     165649
     I                                      108 114 WMSID                                     165649
     I                                      115 192 WMSTX                                     165649
      *
     I            DS
      ** Data structure for RUN Array
      *
     I                                    P   1   30RUN0
     I                                    P   4   60RUN1
     I                                    P   7   90RUN2
     I                                    P  10  120RUN3
     I                                    P  13  150RUN4
     I                                    P  16  180RUN5
     I                                    P  19  210RUN6
     I                                    P  22  240RUN7
     I                                    P  25  270RUN8
     I                                    P  28  300RUN9
     I                                    P   1  30 @RUN
      *
     I            DS
      ** Data structure of OAM Array
     I                                    P   1   70OAM1
     I                                    P   8  140OAM2
     I                                    P  15  210OAM3
     I                                    P  22  280OAM4
     I                                    P  29  350OAM5
     I                                    P  36  420OAM6
     I                                    P  43  490OAM7
     I                                    P  50  560OAM8
     I                                    P  57  630OAM9
     I                                    P  64  700OA10
     I                                    P   1  70 @OAM
      *
     I/COPY QWINDSRC,LE2150GDTA
     I/COPY ZSRSRC,ZHOLI
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *                   Index to subroutines                        *
      *                                                               *
      *  Main routine                                                 *
      *  SRINIT  -  Initial Processing                                *
      *  SRISCR  -  Initial Screen Processing                         *
      *  SRSELE  -  Select Screen Subfile Processing                  *
      *  SRLOAD  -  Load Subfile                                      *
      *  SRDETL  -  Detail Processing                                 *
      *  SRCLSE  -  Closedown                                         *
      *  SRBTCH  -  Batch Processing                                  *
      *  SRPLAY  -  Play Processing                                   *
      *  SRISSF  -  Initial Screen called from subfile                *
      *  SRSVAL  -  Subfile Validation                                *
      *  SRSPRC  -  Subfile Processing                                *
      *  SRUPDT  -  Update                                            *
      *  SRFEES  -  Update Fees File                                  *   CLE009
      *  SRVDET  -  Validate Detail Screen Input                      *
      *  SRVUSR  -  Validate User/Action/Branch                       *
      *  SRVISC  -  Validate Initial Screen Input                     *
      *  SRVRVW  -  Validate Review From                              *
      *  SRSQNO  -  Get Sequence No.                                  *
      *  SRWIND  -  Window Processing                                 *
      *  SREROR  -  Error Processing                                  *
      *  ZASNMS  -  Sends messages to the program's message queue     *
      *  SRCLRS  -  Clears the subfile message queue                  *
      *  GLZADD  -  Add an amount to the total                        *
      *  GLZSUB  -  Subroutine to subtract an amount to the total     *
      *  GLZSUM  -  Carry out the additon for subroutine              *
      *  *PSSR   -  Error handling subroutine                         *
      *                                                               *
      *  ZALIGN  -  Val/right aligned numberic fields                 *
      *  ZCHKH   -  Check if holiday for ccy/locn                     *
      *  ZDATE1  -  Validate & cvt date to a day nbr                  *
      *  ZDATE2  -  Convert day number to a date                      *
      *  ZEDIT   -  Ins dec point & 0s for numeric                    *
      *                                                               *
      *  AOBANKR0   Access program for Bank Details                   *
      *  AOCLNDR0   Access program for Module Details                 *
      *  AOCURRR0   Access program for Currency Details               *
      *  AOCUSTR0   Access program for Customer Details               *
      *  AOSARDR0   Access program for SAR Details                    *
      *  DBERRCTL   DB error ctl for interactive jobs                 *
      *  WN0010  -  Window manager                                    *
      *  Y2RVMGC -  Retrieve a message                                *
      *  Y2SNMGC -  Send a message                                    *
      *  Y2CLMSC -  Clear specified program message queue             *
      *  ZVACTBU -  Val Act Code for User/Pgm/Brch                    *
      *  ZVACTU  -  Validate Act Code for User/Pgm                    *
      *  ZVBBU   -  Validate Booking Branch/User                      *
      *  ZVOBU   -  Validate Originating Branch/User                  *
      *                                                               *
      *****************************************************************
      *
      ** Perform initial processing
      *
     C                     EXSR SRINIT
      *
      ** Perform MODE processing
      *
     C           PMODE     IFEQ 'I'
     C           PMODE     OREQ 'R'
     C                     EXSR SRISCR
     C                     ENDIF
      *
     C           PMODE     IFEQ 'B'
     C                     EXSR SRBTCH
     C                     ENDIF
      *
     C           PMODE     IFEQ 'P'
     C                     EXSR SRPLAY
     C                     ENDIF
      *
      ** Terminate program
      *
     C                     EXSR SRCLSE
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT  -  Initial Processing                                *
      *                                                               *
      *  Called by: Main routine                                      *
      *  Calls    :                                                   *
      *  AOBANKR0   Access program for Bank Details                   *
      *  AOCLNDR0   Access program for Module Details                 *
      *  AOSARDR0   Access program for SAR Details                    *
      *  *PSSR   -  Error handling subroutine                         *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Receive entry parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           PMODE   1
     C                     PARM           PINPP
     C                     PARM           POUTP
      *
      ** Key fields for Facility Amendment file
      *
     C           FCAMKY    KLIST
     C**********           KFLD           WKCNUM  60                                          CSD027
     C                     KFLD           WKCNUM  6                                           CSD027
     C                     KFLD           WKFACT  30
     C                     KFLD           WKFCNO  20
     C                     KFLD           WKVLDT  50
     C                     KFLD           WKSQNO  30
      *                                                                   135744
     C           FCAMK4    KLIST                                          135744
     C                     KFLD           WKCNUM                          135744
     C                     KFLD           WKFACT                          135744
     C                     KFLD           WKFCNO                          135744
     C                     KFLD           WKVLDT                          135744
      *                                                                   135744
     C           FCAMK3    KLIST                                          135744
     C                     KFLD           WKCNUM                          135744
     C                     KFLD           WKFACT                          135744
     C                     KFLD           WKFCNO                          135744
      *
      ** Key fields for Facility file
      *
     C           FCTYKY    KLIST
     C                     KFLD           WKCNUM
     C                     KFLD           WKFACT
     C                     KFLD           WKFCNO
     C                     KFLD           WKRCTP  1
      *                                                                   CLE009
     C           FCAM2     KLIST                                          CLE009
     C                     KFLD           CNUM                            CLE009
     C                     KFLD           FACT                            CLE009
     C                     KFLD           FCNO                            CLE009
     C                     KFLD           VLDT                            CLE009
     C                     KFLD           SQNO                            CLE009
      *                                                                   CLE009
      ** Key list for Fees file                                           CLE009
      *                                                                   CLE009
     C           WKFEE     KLIST                                          CLE009
     C**********           KFLD           CNUM    60                                   CLE009 CSD027
     C                     KFLD           CNUM    6                                           CSD027
     C                     KFLD           WFFACL  50                      CLE009
     C**********           KFLD           WFLOAN  60                                   CLE009 CLE148
     C                     KFLD           WFLOAN  6                                           CLE148
      *
      ** Define dataara LDA and RUNDAT
      *
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
      ** Initialise LDA
      *
     C           *LOCK     IN   LDA
     C                     MOVEL'LE2150'  DBPGM
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     Z-ADD*ZEROS    DBASE
     C                     OUT  LDA
      *
      ** Retrieve bank details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST  'POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE           ************
     C                     Z-ADD1         DBASE            * DBERR 01 *
     C                     MOVEL'*FIRST  'DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Set *IN98, depending on the value of Date Format INdicator
      *
     C                     MOVE *OFF      *IN98
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ** Initialise Header format
      *
     C                     MOVELUSER      SUSER
     C                     MOVELBJMRDT    SRUNA
     C                     MOVELWSID      SWSID
      *
      ** Check if CLE002 is Installed
      *
     C                     MOVEL'N'       CLE002  1
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   PRTCD   7                    e
     C                     PARM '*KEY    'POPTN   7
     C                     PARM 'CLE002'  PSARD   6
     C           SCSARD    PARM SCSARD    DSFDY
      *
     C           PRTCD     IFEQ *BLANKS
     C                     MOVEL'Y'       CLE002
     C                     ENDIF
      *
     C           PRTCD     IFNE *BLANKS
     C           PRTCD     ANDNE'*NRF   '                                 CLE004
     C           *LOCK     IN   LDA
     C                     MOVEL'SCSARDPD'DBFILE           ************
     C                     Z-ADD2         DBASE            * DBERR 02 *
     C                     MOVEL'CLE002  'DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Retrieve midas module details, If CLE002 is Installed
      *
     C           CLE002    IFEQ 'Y'
      *
     C                     CALL 'AOCLNDR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*FIRST ' POPTN
     C           SDCLND    PARM SDCLND    DSFDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCLNDPD'DBFILE           ************
     C                     Z-ADD3         DBASE            * DBERR 03 *
     C                     MOVEL'*FIRST'  DBKEY            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           BPFAAM    IFEQ 'Y'
     C                     MOVE *ON       *IN31
     C                     ELSE
     C                     MOVE *OFF      *IN31
     C                     ENDIF
      *
     C                     ELSE
      *
     C                     MOVE *OFF      *IN31
      *
     C                     ENDIF
      *                                                                   CLE009
      ** Access SAR details file to see if Customer Lending               CLE009
      ** Enhancements is installed                                        CLE009
      *                                                                   CLE009
     C                     CALL 'AOSARDR0'                                CLE009
     C                     PARM *BLANK    PRTCD                           CLE009
     C                     PARM '*KEY   ' POPTN                           CLE009
     C                     PARM 'CLE007'  PSARD   6                       CLE009
     C           SCSARD    PARM SCSARD    DSFDY                           CLE009
      *                                                                   CLE009
     C           PRTCD     IFEQ *BLANK                                    CLE009
     C                     MOVE 'Y'       CLE007  1                       CLE009
     C                     CALL 'AOSARDR0'                                CLE009
     C                     PARM *BLANKS   PRTCD                           CLE009
     C                     PARM '*KEY   ' POPTN                           CLE009
     C                     PARM 'CLE009'  PSARD   6                       CLE009
     C           SCSARD    PARM SCSARD    DSFDY                           CLE009
      *                                                                   CLE009
      ** If CLE009 is installed, fill currency and their corresponding    CLE009
      ** telex notice days arrays                                         CLE009
      *                                                                   CLE009
     C           PRTCD     IFEQ *BLANKS                                   CLE009
     C                     MOVE 'Y'       CLE009  1                       CLE009
     C                     Z-ADD1         Y       30                      CLE009
      *                                                                   CLE009
     C                     CALL 'AOCURRR0'                                CLE009
     C                     PARM *BLANKS   PRTCD                           CLE009
     C                     PARM '*FIRST ' POPTN                           CLE009
     C                     PARM           PCURR   3                       CLE009
     C           SDCURR    PARM SDCURR    DSSDY                           CLE009
      *                                                                   CLE009
     C                     MOVE A6CYCD    CYA,Y                           CLE009
     C                     MOVE A6TXND    TND,Y                           CLE009
      *                                                                   CLE009
     C           PRTCD     DOWNE'*EOF    '                                CLE009
     C                     ADD  1         Y                               CLE009
      *                                                                   CLE009
     C                     CALL 'AOCURRR0'                                CLE009
     C                     PARM *BLANKS   PRTCD                           CLE009
     C                     PARM '*NEXT  ' POPTN                           CLE009
     C                     PARM           PCURR   3                       CLE009
     C           SDCURR    PARM SDCURR    DSSDY                           CLE009
      *                                                                   CLE009
     C           PRTCD     IFNE '*EOF    '                                CLE009
     C                     MOVE A6CYCD    CYA,Y                           CLE009
     C                     MOVE A6TXND    TND,Y                           CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C                     ENDDO                                          CLE009
      *                                                                   CLE009
     C                     ELSE                                           CLE009
     C                     MOVE 'N'       CLE009                          CLE009
      *                                                                   CLE009
     C           PRTCD     IFNE '*NRF    '                                CLE009
     C           *LOCK     IN   LDA                                       CLE009
     C                     MOVEL'SCSARDPD'DBFILE           ************   CLE009
     C                     Z-ADD27        DBASE            * DBERR 27 *   CLE009
     C                     MOVEL'CLE009'  DBKEY            ************   CLE009
     C                     OUT  LDA                                       CLE009
     C                     EXSR *PSSR                                     CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C                     ENDIF                                          CLE009
     C                     ELSE                                           CLE009
     C                     MOVE 'N'       CLE007                          CLE009
     C           PRTCD     IFNE '*NRF   '                                 CLE009
     C           *LOCK     IN   LDA                                       CLE009
     C                     MOVEL'SCSARDPD'DBFILE           ************   CLE009
     C                     Z-ADD28        DBASE            * DBERR 28 *   CLE009
     C                     MOVEL'CLE007'  DBKEY            ************   CLE009
     C                     OUT  LDA                                       CLE009
     C                     EXSR *PSSR                                     CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE014
      ** Determine if CLE014 is installed                                 CLE014
      *                                                                   CLE014
     C                     CALL 'AOSARDR0'                                CLE014
     C                     PARM '       ' PRTCD                           CLE014
     C                     PARM '*VERIFY' POPTN                           CLE014
     C                     PARM 'CLE014'  PSARD                           CLE014
     C           SCSARD    PARM SCSARD    DSSDY                           CLE014
      *                                                                   CLE014
      ** Handle Database Error                                            CLE014
      *                                                                   CLE014
     C           PRTCD     IFNE *BLANKS                                   CLE014
     C           PRTCD     ANDNE'*NRF   '                                 CLE014
     C           *LOCK     IN   LDA                                       CLE014
     C                     MOVE 'SCSARDPD'DBFILE                          CLE014
     C                     MOVELPSARD     DBKEY                           CLE014
     C                     Z-ADD33        DBASE                           CLE014
     C                     OUT  LDA                                       CLE014
     C                     EXSR *PSSR                                     CLE014
     C                     ENDIF                                          CLE014
      *                                                                   CLE014
      ** CLE014 is installed                                              CLE014
      *                                                                   CLE014
     C           PRTCD     IFEQ *BLANKS                                   CLE014
     C                     MOVE 'Y'       CLE014  1                       CLE014
     C                     ELSE                                           CLE014
     C                     MOVE 'N'       CLE014                          CLE014
     C                     ENDIF                                          CLE014
      *                                                                   CLE031
      ** Check whether SAR CLE031 is installed or not.                    CLE031
      *                                                                   CLE031
     C                     CALL 'AOSARDR0'                                CLE031
     C                     PARM *BLANK    PRTCD   7                       CLE031
     C                     PARM '*VERIFY' POPTN   7                       CLE031
     C                     PARM 'CLE031'  PSARD   6                       CLE031
     C           SCSARD    PARM SCSARD    DSFDY                           CLE031
      *                                                                   CLE031
     C           PRTCD     IFEQ *BLANK                                    CLE031
     C                     MOVE 'Y'       CLE031  1                       CLE031
      *                                                                   CLE031
     C                     ELSE                                           CLE031
      *                                                                   CLE031
     C                     MOVE 'N'       CLE031                          CLE031
     C           PRTCD     IFNE '*NRF   '                                 CLE031
     C           *LOCK     IN   LDA                                       CLE031
     C                     MOVEL'LE2150  'DBPGM                           CLE031
     C                     MOVEL'SCSARDPD'DBFILE                          CLE031
     C                     Z-ADD34        DBASE                           CLE031
     C                     MOVEL'CLE031'  DBKEY                           CLE031
     C                     OUT  LDA                                       CLE031
     C                     EXSR *PSSR                                     CLE031
     C                     ENDIF                                          CLE031
      *                                                                   CLE031
     C                     ENDIF                                          CLE031
     C/COPY WNCPYSRC,LE2150C001
      *                                                                   168022
      ** Access General Ledger Details:                                   168022
     C**********           CALL 'AOGELRR0'                                             168022 CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*DBERR ' PRTCD                           168022
     C                     PARM '*FIRST ' POPTN                           168022
     C********** SDGELR    PARM SDGELR    DSFDY                                        168022 CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *
      ** Open files depending on MODE
      *
     C           PMODE     IFEQ 'I'
     C           PMODE     OREQ 'R'
     C                     OPEN LE2150DF
     C                     ENDIF
      *
     C           PMODE     IFEQ 'R'
     C           PMODE     OREQ 'P'
     C                     OPEN LE2150PD
     C                     ENDIF
      *
     C                     MOVEL'LERMSGF 'ZAMSGF                          158617
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRISCR  -  Initial Screen Processing                         *
      *                                                               *
      *  Called by: Main routine                                      *
      *  Calls    :                                                   *
      *  SRCLRS  -  Clears the subfile message queue                  *
      *  SRCLSE  -  Closedown                                         *
      *  SRDETL  -  Detail Processing                                 *
      *  SRSELE  -  Select Screen Subfile Processing                  *
      *  SRVISC  -  Validate Initial Screen Input                     *
      *  SRVRVW  -  Validate Review From                              *
      *                                                               *
      *****************************************************************
      *
     C           SRISCR    BEGSR
      *
      ** Do UNTIL F3 = Exit is pressed
      *
     C           *INKC     DOUEQ*ON
      *
      ** Initialise Initial Screen fields
      *
     C           WERR      IFEQ *ZERO
     C                     MOVE *BLANKS   F1ACTI
     C                     MOVE *BLANKS   F1CUST
     C                     MOVE *BLANKS   F1FACT
     C                     MOVE *BLANKS   F1FCNO
     C                     MOVE *BLANKS   F1FATP
     C                     MOVE *BLANKS   F1VLDT
     C                     MOVE *BLANKS   F1CNUM
     C                     MOVE *BLANKS   F1FAC2
     C                     MOVE *BLANKS   F1FCN2
     C                     MOVEL'N'       F1FAAM
     C                     MOVE *BLANKS   F1SQNO
     C                     MOVE *BLANKS   WPVLDT
     C                     ENDIF
      *
      ** Initialise all work fields
      *
     C                     CLEARWRKFLD
      *
      ** Display Initial screen
      *
     C                     WRITELE2150F0
     C                     WRITELE2150C1
     C                     EXFMTLE2150F1
      *
     C                     EXSR SRCLRS
     C                     Z-ADD*ZERO     WERR
     C                     MOVEA'00000000'*IN,11
     C                     MOVEA'00'      *IN,19
      *
      ** If F3 = Exit pressed
      *
     C           *INKC     IFEQ *ON
     C                     EXSR SRCLSE
     C                     ENDIF
      *
     C           F1ACTI    IFNE *BLANKS
     C           F1CUST    ORNE *BLANKS
     C           F1FACT    ORNE *BLANKS
     C           F1FCNO    ORNE *BLANKS
     C           F1FATP    ORNE *BLANKS
     C           F1VLDT    ORNE *BLANKS
     C           F1SQNO    ORNE *BLANKS
      *
      ** Initial validation
      *
     C                     EXSR SRVISC
      *
      ** If no error exist, display the detail screen
      *
     C           WERR      IFEQ *ZERO
      *
      ** Move Initial Screen fields to Detail Screen fields
      *
     C                     MOVE F1CUST    F2CNUM
     C                     MOVELBBCSSN    F2CUST
     C                     MOVELF1ACTI    F2ACTI
      *
     C                     MOVE *BLANKS   F2ASTS
      *
     C           F2ACTI    IFNE 'I'
      *
     C           ASTS      IFEQ 'R'
     C                     MOVEL'LEF0285' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    F2ASTS
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C           ASTS      IFEQ 'C'
     C                     MOVEL'LEF0286' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    F2ASTS
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C           ASTS      IFEQ 'A'
     C                     MOVEL'LEF0287' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    F2ASTS
     C                     MOVELZAMSTX    W#ASTS132
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C           RECI      IFNE 'D'
     C                     MOVEL'LEF0288' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    F2ASTS
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     MOVELF1FACT    F2FACT
     C                     MOVELF1FCNO    F2FCNO
     C                     MOVE F1VLDT    F2VLDT
     C                     MOVE F1SQNO    F2SQNO
      *
     C                     EXSR SRDETL
     C                     ENDIF
      *
      ** Else (if all input fields are blank), validate review fields
      *
     C                     ELSE
      *
     C                     MOVELF1CNUM    C0CNUM
     C                     MOVELF1FAC2    C0FACT
     C                     MOVELF1FCN2    C0FCNO
     C                     MOVELF1FAAM    C0FAAM
      *
     C                     EXSR SRVRVW
      *
      ** If no error exist, execute select screen processing
      *
     C           WERR      IFEQ *ZERO
     C                     EXSR SRSELE
     C                     Z-ADD*ZERO     WERR
     C                     MOVEA'00000000'*IN,11
     C                     MOVEA'00'      *IN,19
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRSELE  -  Select Screen Subfile Processing                  *
      *                                                               *
      *  Called by: SRISCR                                            *
      *  Calls    :                                                   *
      *  SRCLRS  -  Clears the subfile message queue                  *
      *  SRCLSE  -  Closedown                                         *
      *  SRISSF  -  Initial Screen called from subfile                *
      *  SRLOAD  -  Load Subfile                                      *
      *  SRSPRC  -  Subfile Processing                                *
      *  SRSVAL  -  Subfile Validation                                *
      *  SRVRVW  -  Validate Review From                              *
      *                                                               *
      *****************************************************************
      *
     C           SRSELE    BEGSR
      *
      ** Load subfile
      *
     C                     EXSR SRLOAD
      *
      ** Do until F3 or F12 is pressed
      *
     C           *INKC     DOUEQ*ON
      *
      ** Format Review From
      *
     C********** C0CNUM    IFEQ *ZERO                                                         CSD027
     C**********           MOVE *BLANKS   C0CNUM                                              CSD027
     C**********           ENDIF                                                              CSD027
      *
     C           C0FACT    IFEQ *ZERO
     C                     MOVE *BLANKS   C0FACT
     C                     ENDIF
      *
     C           C0FCNO    IFEQ *ZERO
     C                     MOVE *BLANKS   C0FCNO
     C                     ENDIF
      *
      ** Show 'Page keys' if more than 1 page
      *
     C                     MOVE '1'       *IN32
     C           *IN34     IFEQ *ON
     C           SRRN      ANDLE10
     C                     MOVE '0'       *IN32
     C                     ENDIF
      *
      ** Write/Read subfile
      *
     C                     WRITELE2150F3
     C                     WRITELE2150C1
     C                     EXFMTLE2150C0
      *
     C                     EXSR SRCLRS
     C                     Z-ADD*ZERO     WERR
     C                     Z-ADDWSREC     SSREC
     C                     MOVEA'000'     *IN,27
     C                     MOVE *OFF      *IN33
     C                     MOVEA'0000'    *IN,36
      *
      ** F3 = Exit pressed, exit program
      *
     C           *INKC     IFEQ *ON
     C                     EXSR SRCLSE
     C                     ENDIF
      *
      ** F5 = Refresh is pressed, re-load subfile
      *
     C           *INKE     IFEQ *ON
     C                     MOVE *OFF      *IN35
     C                     MOVE *BLANKS   C0CNUM
     C                     MOVE *BLANKS   C0FACT
     C                     MOVE *BLANKS   C0FCNO
     C                     EXSR SRLOAD
     C                     ITER
     C                     ENDIF
      *
      ** F9 = Insert is pressed, initial screen called from subfile
      *
     C           *INKI     IFEQ *ON
     C                     EXSR SRISSF
     C                     MOVE *OFF      *IN35
     C                     EXSR SRLOAD
     C                     ITER
     C                     ENDIF
      *
      ** F12 = Previous is pressed, exit process
      *
     C           *INKL     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
      *
      ** Rollup (*IN35) is pressed, write a further page of record
      *
     C           *IN35     IFEQ *ON
     C                     ADD  10        SSREC
     C                     EXSR SRLOAD
     C                     ITER
     C                     ENDIF
      *
      ** ENTER key is pressed, validate Review From fields
      *
     C           F1CNUM    IFNE C0CNUM
     C           F1FAC2    ORNE C0FACT
     C           F1FCN2    ORNE C0FCNO
     C           F1FAAM    ORNE C0FAAM
     C                     EXSR SRVRVW
     C                     ELSE
      *
      ** Validate Subfile
      *
     C                     EXSR SRSVAL
      *
      ** If no error exist, execute subfile processing
      *
     C           WERR      IFEQ *ZERO
     C                     EXSR SRSPRC
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Reload Subfile
      *
     C           WERR      IFEQ *ZERO
     C                     MOVE *OFF      *IN35
     C                     EXSR SRLOAD
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRLOAD  -  Load Subfile                                      *
      *                                                               *
      *  Called by: SRSELE                                            *
      *  Calls    :                                                   *
      *  SREROR  -  Error Processing                                  *
      *  ZDATE2  -  Convert day number to a date                      *
      *  ZVBBU   -  Validate Booking Branch/User                      *
      *                                                               *
      *****************************************************************
      *
     C           SRLOAD    BEGSR
      *
      ** If not Rollup, re-load subfile
      *
     C           *IN35     IFEQ *OFF
      *
     C                     MOVE *ON       *IN33
     C                     WRITELE2150C0
     C                     MOVE *OFF      *IN33
      *
     C                     MOVE C0CNUM    WKCNUM
     C                     MOVE C0FACT    WKFACT
     C                     MOVE C0FCNO    WKFCNO
     C                     Z-ADD*ZERO     WKVLDT
     C                     Z-ADD*ZERO     WKSQNO
     C                     Z-ADD*ZERO     SRRN    40
     C                     Z-ADD1         SSREC
     C                     Z-ADD1         WSREC
      *
     C           C0FAAM    IFEQ 'Y'
     C           FCAMKY    SETLLLEFCAML5
     C                     ELSE
     C           FCAMKY    SETLLLEFCAML6
     C                     ENDIF
      *
      ** Read LEFCAML5 if Requiring Authorisation = 'Y'
      ** Read LEFCAML6 otherwise
      *
     C           C0FAAM    IFEQ 'Y'
     C                     READ LEFCAML5                 34
     C                     ELSE
     C                     READ LEFCAML6            N    34
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     Z-ADD0         WRRN    40
      *
      ** Do while not EOF
      *
     C           *IN34     DOWEQ*OFF
     C           WRRN      ANDLT10
      *
      ** Move file fields to subfile fields
      *
     C                     MOVE *BLANKS   S0SEL
     C                     MOVE CNUM      S0CNUM
     C                     MOVE FACT      S0FACT
     C                     MOVE FCNO      S0FCNO
     C                     MOVE SQNO      S0SQNO
     C                     MOVELBRCA      S0BRCA
     C                     MOVELACCY      S0ACCY
     C                     MOVELRVCR      S0RVCR
     C                     Z-ADDAAMT      S0AAMT
     C                     Z-ADDNDEX      S0NDEX
     C                     MOVELAUSR      S0AUSR
     C                     MOVELIUSR      S0IUSR
     C                     MOVE *BLANKS   S0ORBR
      *
     C                     Z-ADDVLDT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    S0VLDT
     C                     MOVELZDATE     S0VLD2
      *
     C                     MOVEL'WNMSGF  'ZAMSGF
     C                     MOVEL'LERMSGF 'ZAMSGF
     C           FATP      IFEQ 'FI'
     C                     MOVEL'FI'      S0FAT2
     C                     MOVEL'LEF0281' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    S0FATP
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C           FATP      IFEQ 'FD'
     C                     MOVEL'FD'      S0FAT2
     C                     MOVEL'LEF0282' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    S0FATP
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C           FATP      IFEQ 'FR'
     C                     MOVEL'FR'      S0FAT2
     C                     MOVEL'LEF0283' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    S0FATP
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C           FATP      IFEQ 'RC'
     C                     MOVEL'RC'      S0FAT2
     C                     MOVEL'LEF0284' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    S0FATP
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C           ASTS      IFEQ 'R'
     C                     MOVEL'LEF0285' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    S0ASTS
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C           ASTS      IFEQ 'C'
     C                     MOVEL'LEF0286' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    S0ASTS
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C           ASTS      IFEQ 'A'
     C                     MOVEL'LEF0287' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    S0ASTS
     C                     MOVELZAMSTX    W#ASTS
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
      ** If Originating Branches User Validation is required:             168022
     C           BKOBUV    IFEQ 'Y'                                       168022
     C/COPY WNCPYSRC,LE2150C006
      ** Get Originating Branch
      *
     C                     MOVE S0CNUM    WKCNUM
     C                     MOVE S0FACT    WKFACT
     C                     MOVE S0FCNO    WKFCNO
     C                     MOVEL'A'       WKRCTP
     C           FCTYKY    CHAINFCLTY                80
      *
     C           *IN80     IFEQ *OFF
     C                     MOVELORBR      S0ORBR
     C/COPY WNCPYSRC,LE2150C007
     C                     ENDIF
     C                     ENDIF                                          168022
      *
      ** Perform check on branch authority
      *
     C           MBIN      IFEQ 'Y'
     C                     CALL 'ZVBBU'
     C                     PARM           S0BRCA
     C                     PARM           @ERR    10
     C                     ENDIF
      *
     C           @ERR      IFEQ *ZERO
     C                     ADD  1         SRRN
     C                     ADD  1         WRRN
     C                     WRITELE2150S0
     C                     ENDIF
      *
      ** Read Facility Amendments record
      *
     C           C0FAAM    IFEQ 'Y'
     C                     READ LEFCAML5                 34
     C                     ELSE
     C                     READ LEFCAML6            N    34
     C                     ENDIF
      *
     C                     ENDDO
      *
     C           SRRN      IFEQ *ZERO
     C                     Z-ADD1         SRRN
     C                     MOVE *ON       *IN38
     C                     MOVEL'E'       WERR
     C                     MOVEL'LEF0230' ZAMSID
     C                     EXSR SREROR
     C                     MOVE *BLANKS   S0SEL
     C                     MOVE *BLANKS   S0CNUM
     C                     MOVE *BLANKS   S0FACT
     C                     MOVE *BLANKS   S0FCNO
     C                     MOVE *BLANKS   S0VLDT
     C                     MOVE *BLANKS   S0SQNO
     C                     MOVE *BLANKS   S0FATP
     C                     MOVE *BLANKS   S0ASTS
     C                     WRITELE2150S0
     C                     MOVE *OFF      *IN38
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRDETL  -  Detail Processing                                 *
      *                                                               *
      *  Called by: SRISCR, SRISSF, SRSPRC                            *
      *  Calls    :                                                   *
      *  AOCURRR0   Access program for Currency Details               *
      *  SRCLRS  -  Clears the subfile message queue                  *
      *  SRCLSE  -  Closedown                                         *
      *  SREROR  -  Error Processing                                  *
      *  SRUPDT  -  Update                                            *
      *  SRVDET  -  Validate Detail Screen Input                      *
      *  SRWIND  -  Window Processing                                 *
      *  ZEDIT   -  Ins dec point & 0s for numeric                    *
      *  ZDATE2  -  Convert day number to a date                      *
      *                                                               *
      *****************************************************************
      *
     C           SRDETL    BEGSR
      *
      ** Initialise Details Screen fields
      *
     C                     MOVEA'000000'  *IN,21
     C                     MOVE *OFF      *IN30
      *                                                                                       165649
     C                     MOVE *OFF      *IN44                                               165649
     C           F1FATP    IFEQ 'FR'                                                          165649
     C                     MOVE *ON       *IN43                                               165649
     C                     ELSE                                                               165649
     C                     MOVE *OFF      *IN41                                               165649
     C                     END                                                                165649
      *
      ** Set on Function keys and indicators based on action
      *
     C           F2ACTI    IFEQ 'I'
     C                     MOVE *BLANKS   F2ACCY
     C                     MOVE *BLANKS   F2AAMT
     C                     MOVE *BLANKS   F2SIGN                                              165649
     C                     MOVE *BLANKS   F2RVCR
     C                     MOVE *BLANKS   F2NDEX
     C                     ELSE
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY    'POPTN   7
     C           F2ACCY    PARM WACCY     PK101   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C                     MOVE *BLANKS   F2AAMT
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE WAAMT     ZFIELD
     C                     Z-ADDA6NBDP    ZADEC
     C                     EXSR ZEDIT
      *
     C           WAAMT     IFNE *ZERO
     C                     MOVE ZFIELD    F2AAMT
     C                     ENDIF
      *
     C                     MOVELWRVCR     F2RVCR
     C                     MOVE WSIGN     F2SIGN                                              165649
      *
     C                     MOVE *BLANKS   F2NDEX
     C                     Z-ADDWNDEX     ZDAYNO
     C                     EXSR ZDATE2
      *
     C           ZDATE     IFNE *ZERO
     C                     MOVE ZDATE     F2NDEX
     C                     MOVE ZDATE     WPNDEX
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           F2ACTI    IFEQ 'E'
     C           F2ACTI    OREQ 'D'
     C           F2ACTI    OREQ 'X'
     C                     MOVE *ON       *IN30
      *
     C           F2ACTI    IFEQ 'D'
     C                     MOVEA'10'      *IN,25
     C                     ENDIF
      *
     C           F2ACTI    IFEQ 'X'
     C                     MOVEA'01'      *IN,25
      *
      **  Disable F11 if BPAURV is activated
      *
     C           BPAURV    IFEQ 'Y'
     C                     MOVE '0'       *IN26
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           F2ACTI    IFEQ 'A'
     C           F2ACTI    OREQ 'X'
     C                     EXSR SRVDET
     C                     ENDIF
      *
      ** Format Amendment type
      *
     C           F1FATP    IFEQ 'FI'
     C                     MOVEL'LEF0281' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    F2FATP
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C           F1FATP    IFEQ 'FD'
     C                     MOVEL'LEF0282' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    F2FATP
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C           F1FATP    IFEQ 'FR'
     C                     MOVEL'LEF0283' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    F2FATP
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C           F1FATP    IFEQ 'RC'
     C                     MOVEL'LEF0284' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    F2FATP
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
      ** Do until action is accepted, F3=Exit or F12=Previous
      *
     C           *INKC     DOUEQ*ON
     C           *INKL     OREQ *ON
     C           WERR      OREQ *ZERO
      *
      ** Write/Read Detail screen
      *
     C                     WRITELE2150C1
     C                     EXFMTLE2150F2
      *
      **  Enable *INKK is BPAURV is 'Y'
      *
     C           F2ACTI    IFEQ 'X'
     C           BPAURV    ANDEQ'Y'
     C                     MOVE '1'       *INKK
     C                     ENDIF
      *
     C                     EXSR SRCLRS
     C                     Z-ADD*ZERO     WERR
     C                     MOVEA'0000'    *IN,21
      *
      ** F3 = Exit pressed, exit program
      *
     C           *INKC     IFEQ *ON
     C                     EXSR SRCLSE
     C                     ENDIF
      *
      ** F12 = Previous pressed, return to previous screen
      *
     C           *INKL     IFEQ *ON
      *
     C           F2ACTI    IFNE 'I'
     C                     MOVE *BLANKS   F1FATP
     C                     ENDIF
      *
     C                     LEAVE
     C                     ENDIF
      *
      ** Validate Detail screen
      *
     C           F2ACTI    IFEQ 'A'
     C           F2ACTI    OREQ 'I'
     C           F2ACTI    OREQ 'X'
     C                     EXSR SRVDET
     C                     ENDIF
      *
      ** If no error, continue
      *
     C           WERR      IFEQ *ZERO
      *
     C           F2ACTI    IFEQ 'D'
     C           *INKJ     ANDEQ*OFF
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0232' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C           F2ACTI    IFEQ 'X'
     C           *INKK     ANDEQ*OFF
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0233' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
      ** Window Processing
      *
     C                     EXSR SRWIND
      *
     C           *INKC     IFEQ *ON
     C                     EXSR SRCLSE
     C                     ENDIF
      *
     C           *INKL     IFEQ *ON
     C                     ITER
     C                     ENDIF
      *
      ** If action is 'I', 'A' or action is 'X' and F11 pressed,
      ** or action 'D' and F10 pressed
      *
     C           F2ACTI    IFNE 'I'
     C           WERR      ANDEQ*ZERO                                     175536
     C                     MOVE *BLANKS   F1FATP
     C                     ENDIF
      *
     C           F2ACTI    IFEQ 'I'
     C           F2ACTI    OREQ 'A'
     C           F2ACTI    OREQ 'X'
     C           *INKK     ANDEQ*ON
     C           F2ACTI    OREQ 'D'
     C           *INKJ     ANDEQ*ON
      *
      ** Update Facility Amendment File
      *
     C                     EXSR SRUPDT
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRCLSE  -  Closedown                                         *
      *                                                               *
      *  Called by: Main routine, SRISCR, SRSELE, SRDETL, SRISSF,     *
      *             SREROR                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SRCLSE    BEGSR
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRBTCH  -  Batch Processing                                  *
      *                                                               *
      *  Called by: Main routine                                      *
      *  Calls    :                                                   *
      *  SRUPDT  -  Update                                            *
      *  SRVDET  -  Validate Detail Screen Input                      *
      *  SRVISC  -  Validate Initial Screen Input                     *
      *                                                               *
      *****************************************************************
      *
     C           SRBTCH    BEGSR
      *
      ** Initialise all work fields
      *
     C                     CLEARWRKFLD
     C                     CLEARPOUTP
     C                     MOVELPINPP     POUTP
     C                     MOVE *BLANKS   POMSEV
     C                     MOVE *BLANKS   POMSID
     C                     MOVE *BLANKS   POMSTX
     C                     MOVE PICNUM    POCNUM
     C                     MOVE PIFACT    POFACT
     C                     MOVE PIFCNO    POFCNO
     C                     MOVE PIVLDT    POVLDT
     C                     MOVE PISQNO    POSQNO
      *
      ** Move Transmitted_message_format to Initial screen
      *
     C                     MOVELPITACT    F1ACTI
     C                     MOVE PICNUM    F1CUST
     C                     MOVE PIFACT    F1FACT
     C                     MOVE PIFCNO    F1FCNO
     C                     MOVELPITUSR    USER                            129521
     C           F1ACTI    IFEQ 'I'
     C                     MOVE PIMTYP    F1FATP
     C                     ELSE
     C                     MOVE *BLANKS   F1FATP
     C                     ENDIF
     C                     MOVE PISQNO    F1SQNO
     C                     MOVE PIVLDT    F1VLDT
     C           4         SUBSTPIVLDT:1  F1VLDT
      *
      ** Do initial validation
      *
     C                     EXSR SRVISC
      *
      ** Move Transmitted_message_format to Detail screen
      *
     C                     MOVELPITACT    F2ACTI
     C                     MOVE PICNUM    F2CNUM
     C                     MOVE PIFACT    F2FACT
     C                     MOVE PIFCNO    F2FCNO
     C                     MOVE PISQNO    F2SQNO
     C                     MOVE PIVLDT    F2VLDT
     C           4         SUBSTPIVLDT:1  F2VLDT
     C                     MOVELPIACCY    F2ACCY
     C                     MOVE PIFAMT    F2AAMT
     C                     MOVELPIRVCR    F2RVCR
     C                     MOVE PINDEX    F2NDEX
     C           4         SUBSTPINDEX:1  F2NDEX
      *
      ** Process detail validation
      *
     C                     EXSR SRVDET
      *
      ** Update file
      *
     C                     EXSR SRUPDT
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRPLAY  -  Play Processing                                   *
      *                                                               *
      *  Called by: Main routine                                      *
      *  Calls    :                                                   *
      *  SRUPDT  -  Update                                            *
      *  SRVDET  -  Validate Detail Screen Input                      *
      *  SRVISC  -  Validate Initial Screen Input                     *
      *                                                               *
      *****************************************************************
      *
     C           SRPLAY    BEGSR
      *
      ** Read Test-harness-file
      *
     C                     READ LE2150PD                 80
      *
     C           *IN80     DOWEQ*OFF
      *
      ** Initialise all work fields
      *
     C                     CLEARWRKFLD
      *
      ** Move Test-Harness-record to Initial screen
      *
     C                     MOVELS1ACTI    F1ACTI
     C                     MOVELS1CUST    F1CUST
     C                     MOVELS1FACT    F1FACT
     C                     MOVELS1FCNO    F1FCNO
     C                     MOVELS1FATP    F1FATP
     C                     MOVELS1SQNO    F1SQNO
     C                     MOVELS1VLDT    F1VLDT
      *
      ** Do Initial Screen validation
      *
     C                     EXSR SRVISC
      *
      ** Move Test-Harness-record to Detail screen
      *
     C                     MOVELS1ACTI    F2ACTI
     C                     MOVELS1CUST    F2CNUM
     C                     MOVELS1FACT    F2FACT
     C                     MOVELS1FCNO    F2FCNO
     C                     MOVELS1SQNO    F2SQNO
     C                     MOVELS1VLDT    F2VLDT
     C                     MOVELS2ACCY    F2ACCY
     C                     MOVELS2AAMT    F2AAMT
     C                     MOVELS2RVCR    F2RVCR
     C                     MOVELS2NDEX    F2NDEX
      *
      ** Do Detail Screen validation
      *
     C                     EXSR SRVDET
      *
      ** Update file
      *
     C                     EXSR SRUPDT
      *
      ** Read Test-harness-file
      *
     C                     READ LE2150PD                 80
     C                     ENDDO
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRISSF  -  Initial Screen called from subfile                *
      *                                                               *
      *  Called by: SRSELE                                            *
      *  Calls    :                                                   *
      *  SRCLRS  -  Clears the subfile message queue                  *
      *  SRCLSE  -  Closedown                                         *
      *  SRDETL  -  Detail Processing                                 *
      *  SRVISC  -  Validate Initial Screen Input                     *
      *                                                               *
      *****************************************************************
      *
     C           SRISSF    BEGSR
      *
     C                     MOVE *OFF      *IN39
     C                     MOVE *ON       *IN20
      *
      ** Do until F3 is pressed, or entry is accepted
      *
     C           *INKC     DOUEQ*ON
      *
      ** Initialise Insert Screen fields
      *
     C           WERR      IFEQ *ZERO
     C                     MOVEL'I'       F1ACTI
     C                     MOVE *BLANKS   F1CUST
     C                     MOVE *BLANKS   F1FACT
     C                     MOVE *BLANKS   F1FCNO
     C                     MOVE *BLANKS   F1FATP
     C                     MOVE *BLANKS   F1VLDT
     C                     MOVE *BLANKS   F1CNUM
     C                     MOVE *BLANKS   F1FAC2
     C                     MOVE *BLANKS   F1FCN2
     C                     MOVEL'N'       F1FAAM
     C                     MOVE *BLANKS   F1SQNO
     C                     MOVE *BLANKS   WPVLDT
     C                     ENDIF
      *
      ** Initialise all work fields
      *
     C                     CLEARWRKFLD
      *
      ** Write/Read initial screen
      *
     C                     WRITELE2150C1
     C                     EXFMTLE2150F1
      *
     C                     EXSR SRCLRS
     C                     Z-ADD*ZERO     WERR
     C                     MOVEA'00000000'*IN,11
     C                     MOVE *OFF      *IN19
      *
      ** F3 = Exit pressed, exit program
      *
     C           *INKC     IFEQ *ON
     C                     EXSR SRCLSE
     C                     ENDIF
      *
      ** F12 = Previous pressed, return to subfile screen
      *
     C           *INKL     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
      *
      ** ENTER key is pressed, initial validation
      *
     C                     EXSR SRVISC
      *
      ** If no error exist, display the detail screen
      *
     C           WERR      IFEQ *ZERO
      *
      ** Move Initial Screen fields to Detail Screen fields
      *
     C                     MOVE F1CUST    F2CNUM
     C                     MOVELBBCSSN    F2CUST
     C                     MOVELF1ACTI    F2ACTI
      *
     C                     MOVE *BLANKS   F2ASTS
      *
     C           F2ACTI    IFNE 'I'
      *
     C           ASTS      IFEQ 'R'
     C                     MOVEL'LEF0285' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    F2ASTS
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C           ASTS      IFEQ 'C'
     C                     MOVEL'LEF0286' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    F2ASTS
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C           ASTS      IFEQ 'A'
     C                     MOVEL'LEF0287' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    F2ASTS
     C                     MOVELZAMSTX    W#ASTS
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C           RECI      IFNE 'D'
     C                     MOVEL'LEF0288' ZAMSID
     C                     EXSR SRTEXT
     C                     MOVELZAMSTX    F2ASTS
     C                     MOVE *BLANKS   ZAMSID
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     MOVE F1FACT    F2FACT
     C                     MOVE F1FCNO    F2FCNO
     C                     MOVE F1VLDT    F2VLDT
     C                     MOVE F1SQNO    F2SQNO
      *
     C                     EXSR SRDETL
      *
     C           *INKL     IFEQ *ON
     C                     LEAVE
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDDO
      *
     C                     MOVE *OFF      *INKL
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRSVAL  -  Subfile Validation                                *
      *                                                               *
      *  Called by: SRSELE                                            *
      *  Calls    :                                                   *
      *  SREROR  -  Error Processing                                  *
      *  SRVUSR  -  Validate User/Action/Branch                       *
      *                                                               *
      *****************************************************************
      *
     C           SRSVAL    BEGSR
      *
      ** Save SRRN, SSREC
      *
     C                     Z-ADDSRRN      WSRRN   40
     C                     Z-ADDSSREC     WSREC   40
      *
      ** Read 'changed' records
      *
     C                     READCLE2150S0                 80
      *
      ** Do while not EOF
      *
     C           *IN80     DOWEQ*OFF
      *
      ** Validate action codes, must be E, A, X, D
      *
     C           S0SEL     IFNE 'E'
     C           S0SEL     ANDNE'A'
     C           S0SEL     ANDNE'X'
     C           S0SEL     ANDNE'D'
     C           S0SEL     ANDNE*BLANKS
     C                     MOVE *ON       *IN36
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0016' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
      ** Check user/branch authorisation
      *
     C           S0SEL     IFNE *BLANKS
     C                     MOVELS0BRCA    WVBRCA
     C                     MOVELS0ORBR    WVORBR
     C                     MOVELS0SEL     WVACTI
     C                     EXSR SRVUSR
     C                     ENDIF
      *
     C           WVERR     IFEQ 'Y'
     C                     MOVE *ON       *IN36
     C                     ENDIF
      *
     C                     MOVEL'N'       WVERR
      *
      ** Check if user allowed to authorise
      *
     C           S0SEL     IFEQ 'X'
      *
     C           *IN31     IFEQ *OFF
     C           CLE002    OREQ 'N'
     C                     MOVE *ON       *IN36
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0213' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C           *IN36     IFEQ *OFF
      *
     C           S0ASTS    IFEQ W#ASTS
     C                     MOVE *ON       *IN36
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0228' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C/COPY WNCPYSRC,LE2150C008
     C                     ENDIF
      *
     C           *IN36     IFEQ *OFF
     C/COPY WNCPYSRC,LE2150C002
      *
     C           USER      IFEQ S0IUSR
     C           USER      OREQ S0AUSR
     C                     MOVE *ON       *IN36
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0231' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
     C/COPY WNCPYSRC,LE2150C003
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Update subfile record
      *
     C                     MOVE *ON       *IN37
      *
     C           WERR      IFEQ 1
     C           *IN36     ANDEQ*ON
     C                     Z-ADDSRRN      SSREC
     C                     ENDIF
      *
     C           S0SEL     IFEQ *BLANKS
     C                     MOVE *OFF      *IN37
     C                     ENDIF
      *
     C                     UPDATLE2150S0
     C                     MOVEA'00'      *IN,36
      *
      ** Read 'changed' records
      *
     C                     READCLE2150S0                 80
     C                     ENDDO
      *
      ** Restore SRRN
      *
     C                     Z-ADDWSRRN     SRRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRSPRC  -  Subfile Processing                                *
      *                                                               *
      *  Called by: SRSELE                                            *
      *  Calls    :                                                   *
      *  AOCUSTR0   Access program for Customer Details               *
      *  SRDETL  -  Detail Processing                                 *
      *  ZDATE1  -  Validate & cvt date to a day nbr                  *
      *                                                               *
      *****************************************************************
      *
     C           SRSPRC    BEGSR
      *
      ** Save SRRN
      *
     C                     Z-ADDSRRN      WSRRN   40
      *
      ** Read 'changed' records
      *
     C                     READCLE2150S0                 80
      *
      ** Do while not EOF, or F12 pressed
      *
     C           *IN80     DOWEQ*OFF
      *
      ** Move Subfile fields to Detail Screen fields
      *
     C                     MOVELS0SEL     F2ACTI
     C                     MOVELS0CNUM    F2CNUM
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY    'POPTN   7
     C                     PARM F2CNUM    PKEY1  10
     C                     PARM           PKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C                     MOVELBBCSSN    F2CUST
     C                     MOVELS0ASTS    F2ASTS
     C                     MOVELS0FACT    F2FACT
     C                     MOVELS0FCNO    F2FCNO
     C                     MOVELS0VLD2    F2VLDT
     C                     MOVE S0SQNO    F2SQNO
     C                     MOVELS0FAT2    F1FATP
      *
     C                     MOVE *BLANKS   ZDATE
     C                     MOVE S0VLD2    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    WVLDT
      *
     C                     MOVELS0ACCY    WACCY
     C                     MOVELS0RVCR    WRVCR
     C                     Z-ADDS0AAMT    WAAMT
      *                                                                                       165649
      ** Setup negative sign for facility reactivation amount                                 165649
      *                                                                                       165649
     C                     MOVE '0'       *IN43                                               165649
     C           S0FAT2    IFEQ 'FR'                                                          165649
     C                     MOVE '1'       *IN43                                               165649
     C                     MOVE '+'       WSIGN                                               165649
     C           WAAMT     IFLT *ZERO                                                         165649
     C                     MOVE '-'       WSIGN                                               165649
     C                     Z-SUBS0AAMT    WAAMT                                               165649
     C                     END                                                                165649
     C                     END                                                                165649
      *                                                                                       165649
     C                     Z-ADDS0NDEX    WNDEX
     C                     EXSR SRDETL
      *
      ** Update Subfile with *BLANKS selection
      *
     C                     MOVE *BLANKS   S0SEL
     C                     UPDATLE2150S0
      *
     C           WERR      IFNE *ZERO
     C           SRRN      ANDNEWSRRN
     C           SRRN      ADD  1         SSREC
     C                     LEAVE
     C                     ENDIF
      *
      ** Read 'changed' records
      *
     C                     READCLE2150S0                 80
      *
     C                     ENDDO
      *
      ** Restore SRRN
      *
     C                     Z-ADDWSRRN     SRRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRUPDT  -  Update                                            *
      *                                                               *
      *  Called by: SRDETL, SRBTCH, SRPLAY                            *
      *  Calls    :                                                   *
      *  GLZADD  -  Add an amount to the total                        *
      *  GLZSUB  -  Subroutine to subtract an amount to the total     *
      *  SRSQNO  -  Get Sequence No.                                  *
      *  ZDATE1  -  Validate & cvt date to a day nbr                  *
      *  ZDATE2  -  Convert day number to a date                      *
      *  SRFEES  -  Update Fees File                                  *   CLE009
      *                                                               *
      *****************************************************************
      *
     C           SRUPDT    BEGSR
      *
      ** If Mode is 'R', write a record to the test-harness-file
      *
     C           PMODE     IFEQ 'R'
      *
      ** Initialise Test-Harness-record
      *
     C                     MOVELF2CNUM    S1CUST
     C                     MOVELF2FACT    S1FACT
     C                     MOVELF2FCNO    S1FCNO
     C                     MOVELF1FATP    S1FATP
     C                     MOVELF2VLDT    S1VLDT
     C                     MOVELF2SQNO    S1SQNO
      *
     C                     MOVELF2ACTI    S1ACTI
      *
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     WDT6    6
     C           2         SUBSTWDT6:1    WDT21   2
     C           2         SUBSTWDT6:3    WDT23   2
     C           2         SUBSTWDT6:5    WDT27   2
     C                     MOVELWDT21     WDT3    3
     C                     MOVE '/'       WDT3
     C                     MOVELWDT23     WDT5    5
     C                     MOVE '/  '     WDT5
     C                     MOVE WDT27     WDT5
     C                     MOVELWDT3      SFDTST
     C                     MOVE WDT5      SFDTST
      *
     C                     TIME           WTIME   60
     C                     MOVE WTIME     WTM6    6
     C           2         SUBSTWTM6:1    WTM21   2
     C           2         SUBSTWTM6:3    WTM23   2
     C           2         SUBSTWTM6:5    WTM25   2
     C                     MOVELWTM21     WTM3    3
     C                     MOVE ':'       WTM3
     C                     MOVELWTM23     WTM5    5
     C                     MOVE ':  '     WTM5
     C                     MOVE WTM25     WTM5
     C                     MOVELWTM3      SFTMST
     C                     MOVE WTM5      SFTMST
      *
     C                     MOVE *BLANKS   SFAUSR
     C                     MOVE *BLANKS   SFIUSR
     C                     MOVE *BLANKS   SFXUSR
      *
      ** If Action is 'I' or 'A' include the contents of Detail Screen
      *
     C           F2ACTI    IFEQ 'I'
     C           F2ACTI    OREQ 'A'
      *
     C                     MOVELF2ACCY    S2ACCY
     C                     MOVELF2AAMT    S2AAMT
     C                     MOVELF2RVCR    S2RVCR
     C                     MOVELF2NDEX    S2NDEX
      *
     C           F2ACTI    IFEQ 'I'
     C                     MOVELUSER      SFIUSR
     C                     ELSE
     C                     MOVELUSER      SFAUSR
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** If Action is 'X'
      *
     C           F2ACTI    IFEQ 'X'
     C                     MOVELUSER      SFXUSR
     C                     ENDIF
      *
      ** Write Test-harness-file
      *
     C           F2ACTI    IFNE 'E'
     C                     WRITELE2150D0
     C                     ENDIF
      *
     C                     ENDIF
      *                                                                   CLE009
      ** If CLE009 is installed, set Work Regeneration Indicator to       CLE009
      ** changed                                                          CLE009
      *                                                                   CLE009
     C           CLE009    IFEQ 'Y'                                       CLE009
      *                                                                   CLE009
      ** If action code is insert, amend or delete                        CLE009
      *                                                                   CLE009
     C           F2ACTI    IFEQ 'I'                                       CLE009
     C           F2ACTI    OREQ 'A'                                       CLE009
     C           F2ACTI    OREQ 'D'                                       CLE009
      *                                                                   CLE009
      ** If action type is facility increase, facility decrease or        CLE009
      ** revolving credit                                                 CLE009
      *                                                                   CLE009
     C           FATP      IFEQ 'FI'                                      CLE009
     C           FATP      OREQ 'FD'                                      CLE009
     C           FATP      OREQ 'RC'                                      CLE009
     C                     MOVE 'C'       WKSWRI  1                       CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF                                          CLE009
      *
      ** If Action is 'I' write record to LF/LEFCAML6
      *
     C           F2ACTI    IFEQ 'I'
      *
      ***Get*the*next*available*sequence*no.***                           190017
      *
     C**********           EXSR SRSQNO                                    190017
     C**********           Z-ADDWSQNO     SQNO                            190017
     C**********           MOVE WSQNO     F1SQN2                          190017
     C**********           MOVE WSQNO     POSQNO                          190017
      *
      ** Detail screen fields
      *
     C                     MOVE F2CNUM    CNUM
     C                     MOVE F2FACT    FACT
     C                     MOVE F2FCNO    FCNO
      *
     C                     MOVE *BLANKS   ZDATE
     C                     MOVE F2VLDT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    VLDT
      *
     C                     MOVELF1FATP    FATP
     C                     MOVELF2ACCY    ACCY
     C           F1FATP    IFEQ 'FR'                                                          165649
     C           F2SIGN    ANDEQ'-'                                                           165649
     C                     Z-SUBWAAMT     AAMT                                                165649
     C                     ELSE                                                               165649
     C                     Z-ADDWAAMT     AAMT
     C                     END                                                                165649
     C                     MOVELF2RVCR    RVCR
     C                     Z-ADDWNDEX     NDEX
     C                     Z-ADDBJRDNB    ORED
     C                     Z-ADDBJRDNB    TLCD
     C                     MOVELF2ACTI    CHTP
      *
      ** Set Insert User to current user
      *
     C                     MOVE *BLANKS   IUSR
     C                     MOVE *BLANKS   AUSR
     C                     MOVE *BLANKS   XUSR
     C                     MOVELUSER      IUSR
      *
      ** Set Record id to 'D'
      *
     C                     MOVEL'D'       RECI
      *
      ** Set Record status to 'C' if Facility Amendment Authority 'Y'
      ** and CLE002 is present
      *
     C           *IN31     IFEQ *ON
     C           CLE002    ANDEQ'Y'
     C                     MOVEL'C'       ASTS
     C                     ELSE
     C                     MOVEL'A'       ASTS
     C                     ENDIF
      *
      ** For batch mode, set fields from interface file
      *
     C           PMODE     IFEQ 'B'
     C                     MOVELPIIUSR    IUSR
     C                     MOVELPIAUSR    AUSR                            156676
     C                     MOVELPIXUSR    XUSR                            156676
     C                     MOVELPITREF    PCRF
     C                     MOVELPIORBR    PCOB
     C                     MOVE PIGASS    GASSA                           157217
     C                     MOVEL'A'       ASTS
     C/COPY WNCPYSRC,LE2150C009
     C                     ENDIF
      *
     C***********PMODE     IFEQ 'I'                                158616 142446
     C           PMODE     IFNE 'B'                                158616 142446
     C                     MOVE BRCA      PBRCA                           158616
     C                     CALL 'AOBRCHR1'                                158616
     C                     PARM *BLANKS   PRTCD                           158616
     C                     PARM '*KEY   ' POPTN                           158616
     C                     PARM           PBRCA   3                       158616
     C           SDBRCH    PARM SDBRCH    DSSDY                           158616
     C           PRTCD     IFNE *BLANKS                                   158616
     C           *LOCK     IN   LDA                                       158616
     C                     MOVEL'SDBRCHPD'DBFILE           ************   158616
     C                     Z-ADD32        DBASE            * DBERR 32 *   158616
     C                     MOVELBRCA      DBKEY            ************   158616
     C                     OUT  LDA                                       158616
     C                     EXSR *PSSR                                     158616
     C                     ELSE                                           158616
     C                     MOVELA8MQSM    W#1ST3  3                       158616
     C********** *NAMVAR   DEFN           LEALLO                                       158616 CLE148
     C********** *LOCK     IN   LEALLO                                                 158616 CLE148
     C********** PCLAST    ADD  1         PCNEXT  80                                   158616 CLE148
     C                     CALL 'LEALLO'                                                      CLE148
     C                     PARM *BLANKS   PRTCD   7                                           CLE148
     C                     PARM 'Y'       PUPDT   1                                           CLE148
     C                     PARM *BLANKS   PCLAST                                              CLE148
     C                     PARM *BLANKS   PCNEXT  8                                           CLE148
     C                     MOVE PCNEXT    W1ST11 11                       158616
     C                     MOVELW#1ST3    W1ST11                          158616
     C                     MOVELW1ST11    PCRF                            158616
     C                     MOVE '0001'    PCRF                            158616
     C**********           Z-ADDPCNEXT    PCLAST                                       158616 CLE148
     C**********           OUT  LEALLO                                                 158616 CLE148
     C                     ENDIF                                          158616
     C                     ENDIF                                          158616
      *
     C           CLE009    IFNE 'Y'                                       CLE009
     C                     MOVE *BLANKS   SWRI                            CLE009
     C                     MOVE *BLANKS   AUTH                            CLE009
     C                     ELSE                                           CLE009
      *                                                                   CLE009
      ** If CLE009 is installed, update fees file                         CLE009
      *                                                                   CLE009
     C                     EXSR SRFEES                                    CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
      ** Get the next available sequence no.                              190017
      *                                                                   190017
     C                     EXSR SRSQNO                                    190017
     C                     Z-ADDWSQNO     SQNO                            190017
     C                     MOVE WSQNO     F1SQN2                          190017
     C                     MOVE WSQNO     POSQNO                          190017
      *                                                                   CLE009
     C**********           WRITELEFCAMPF                                  190017
      *                                                                   190017
      ** Set ON *IN80 if problem with writing to LEFCAML6, which is       190017
      ** now keyed UNIQUE. If 80 is ON, find next available sequence      190017
      ** number and try again to write record.                            190017
      *                                                                   190017
     C                     MOVEASTAMP,1   STMP             Default Timestamp                  CPK015
     C                     WRITELEFCAMPF               80                 190017
      *                                                                   190017
     C           *IN80     DOWEQ*ON                                       190017
     C                     EXSR SRCLRS                                    190017
     C                     EXSR SRSQNO                                    190017
     C                     Z-ADDWSQNO     SQNO                            190017
     C                     MOVE WSQNO     F1SQN2                          190017
     C                     MOVE WSQNO     POSQNO                          190017
     C                     WRITELEFCAMPF               80                 190017
     C                     ENDDO                                          190017
      *                                                                   190017
     C                     MOVE *ON       *IN39
      *
      ** Update trailer LEFCAMZZ
      *
     C           1         CHAINLEFCAMZZ             80
      *
     C           *IN80     IFEQ *OFF
     C                     ADD  1         NORE
     C                     ADD  1         NINS
     C                     ADD  1         NLRA
     C           WAAMT     DIV  1000      ZZAMT
     C                     Z-ADDVIRF      ZZTOTI
     C                     Z-ADDVIRL      ZZTOTD
     C                     EXSR GLZADD
     C                     Z-ADDZZTOTI    VIRF
     C                     Z-ADDZZTOTD    VIRL
     C                     Z-ADDVLAF      ZZTOTI
     C                     Z-ADDVLAL      ZZTOTD
     C                     EXSR GLZADD
     C                     Z-ADDZZTOTI    VLAF
     C                     Z-ADDZZTOTD    VLAL
     C                     UPDATLEFCAMZF
     C/COPY WNCPYSRC,LEH00589
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     MOVE F2CNUM    WKCNUM
     C                     MOVE F2FACT    WKFACT
     C                     MOVE F2FCNO    WKFCNO
     C                     MOVE F2SQNO    WKSQNO
      *
     C                     MOVE *BLANKS   ZDATE
     C                     MOVE F2VLDT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    WKVLDT
      *
      ** If Action is 'A' update record on LF/LEFCAML6
      *
     C           F2ACTI    IFEQ 'A'
      *
      ** Chain LEFCAML6
      *
     C           FCAMKY    CHAINLEFCAML6             80
      *
      ** Save Previous Amount
      *
     C                     Z-ADDAAMT      WPAMT  130
      *
      ** Detail screen fields
      *
     C                     MOVELF2ACCY    ACCY
     C                     MOVELF2RVCR    RVCR
     C           FATP      IFEQ 'FR'                                                          165649
     C           F2SIGN    ANDEQ'-'                                                           165649
     C                     Z-SUBWAAMT     AAMT                                                165649
     C                     ELSE                                                               165649
     C                     Z-ADDWAAMT     AAMT
     C                     END                                                                165649
      *
     C                     MOVE *BLANKS   ZDATE
     C                     MOVE F2NDEX    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    NDEX
      *
     C                     Z-ADDBJRDNB    TLCD
     C                     MOVELF2ACTI    CHTP
      *
      ** Set Amend User to current user
      *
     C                     MOVE *BLANKS   AUSR
     C                     MOVELUSER      AUSR
      *
      ** If Facility Amendment Authority 'Y' and CLE002 is present
      *
     C           *IN31     IFEQ *ON
     C           CLE002    ANDEQ'Y'
      *
      ** Set Record Status to 'R' if previous status was 'A',
      ** otherwise status 'C'
      *
     C           ASTS      IFEQ 'A'
     C           ASTS      OREQ 'R'                                       194509
     C                     MOVEL'R'       ASTS
     C                     ELSE
     C                     MOVEL'C'       ASTS
     C                     ENDIF
      *
     C                     ELSE
     C                     MOVEL'A'       ASTS
     C                     ENDIF
      *
      ** For batch mode, set fields from interface file
      *
     C           PMODE     IFEQ 'B'
     C                     MOVELPIAUSR    AUSR
     C                     MOVELPIXUSR    XUSR                            156676
     C                     MOVELPITREF    PCRF
     C                     MOVEL'A'       ASTS
     C                     ENDIF
      *                                                                   CLE009
      ** If CLE009 is installed, set Work Regeneration Indicator to       CLE009
      ** changed and update fees file                                     CLE009
      *                                                                   CLE009
     C           CLE009    IFEQ 'Y'                                       CLE009
     C                     EXSR SRFEES                                    CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
      *
     C                     UPDATLEFCAMPF
      *
      ** Update trailer LEFCAMZZ
      *
     C           1         CHAINLEFCAMZZ             80
      *
     C           *IN80     IFEQ *OFF
      *
     C                     ADD  1         NAMD
      *
     C           WPAMT     SUB  WAAMT     WDIFF  130
     C           WDIFF     DIV  1000      ZZAMT
     C                     Z-ADDVARF      ZZTOTI
     C                     Z-ADDVARL      ZZTOTD
     C                     EXSR GLZADD
     C                     Z-ADDZZTOTI    VARF
     C                     Z-ADDZZTOTD    VARL
     C                     UPDATLEFCAMZF
     C/COPY WNCPYSRC,LEH00590
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** If Action is 'X' update record on LF/LEFCAML6
      *
     C           F2ACTI    IFEQ 'X'
      *
      ** Chain LEFCAML6
      *
     C           FCAMKY    CHAINLEFCAML6             80
      *
     C                     Z-ADDBJRDNB    TLCD
      *
      ** Set Record Status to 'A'
      *
     C                     MOVEL'A'       ASTS
      *
      ** Set Authorising User to current user
      *
     C                     MOVE *BLANKS   XUSR
     C                     MOVELUSER      XUSR
      *
      ** For batch mode, set fields from interface file
      *
     C           PMODE     IFEQ 'B'
     C                     MOVELPIXUSR    XUSR
     C                     MOVELPITREF    PCRF
     C                     MOVEL'A'       ASTS
     C                     ENDIF
      *                                                                   CLE009
      ** If CLE009 is installed, set Work Regeneration Indicator to       CLE009
      ** changed and update fees file                                     CLE009
      *                                                                   CLE009
     C           CLE009    IFEQ 'Y'                                       CLE009
     C                     EXSR SRFEES                                    CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
      *
     C                     UPDATLEFCAMPF
      *
     C                     ENDIF
      *
      ** If Action is 'D' update record on LF/LEFCAML6
      *
     C           F2ACTI    IFEQ 'D'
      *
      ** Chain LEFCAML6
      *
     C           FCAMKY    CHAINLEFCAML6             80
      *
     C                     Z-ADDBJRDNB    TLCD
     C                     MOVELF2ACTI    CHTP
      *
      ** Set Record Status to 'D'
      *
     C                     MOVEL'D'       ASTS
      *
      ** Set Record id to 'R'
      *
     C                     MOVEL'R'       RECI
      *
     C           PMODE     IFEQ 'B'
     C                     MOVELPITREF    PCRF
     C                     ENDIF
      *                                                                   CLE009
      ** If CLE009 is installed, set Work Regeneration Indicator to       CLE009
      ** changed and update fees file                                     CLE009
      *                                                                   CLE009
     C           CLE009    IFEQ 'Y'                                       CLE009
     C                     EXSR SRFEES                                    CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
      *
     C                     UPDATLEFCAMPF
      *
      ** Update trailer LEFCAMZZ
      *
     C           1         CHAINLEFCAMZZ             80
      *
     C           *IN80     IFEQ *OFF
     C                     ADD  1         NDEL
     C                     SUB  1         NLRA
     C           WAAMT     DIV  1000      ZZAMT
     C                     Z-ADDVDRF      ZZTOTI
     C                     Z-ADDVDRL      ZZTOTD
     C                     EXSR GLZADD
     C                     Z-ADDZZTOTI    VDRF
     C                     Z-ADDZZTOTD    VDRL
     C                     Z-ADDVLAF      ZZTOTI
     C                     Z-ADDVLAL      ZZTOTD
     C                     EXSR GLZSUB
     C                     Z-ADDZZTOTI    VLAF
     C                     Z-ADDZZTOTD    VLAL
     C                     UPDATLEFCAMZF
     C                     ENDIF
      *
     C                     ENDIF
     C/COPY WNCPYSRC,LE2150C010
      *
      ** Commit changes when not in batch mode
      *
     C           PMODE     IFNE 'B'
     C                     COMIT
     C                     ENDIF
      *
     C                     ENDSR
     C/COPY WNCPYSRC,LE2150C011
      *****************************************************************   CLE009
      * SRFEES  -  Update Fees File                                   *   CLE009
      * Called by: SRUPDT - Update                                    *   CLE009
      * Calls    : None                                               *   CLE009
      *****************************************************************   CLE009
     C           SRFEES    BEGSR                                          CLE009
      *                                                                   CLE009
      ** If authorisation switchable feature is on and action code        CLE009
      ** is insert or amend and facility amendments authorisations        CLE009
      ** is required                                                      CLE009
      *                                                                   CLE009
     C           CLE002    IFEQ 'Y'                                       CLE009
     C           F2ACTI    ANDNE'X'                                       CLE009
     C           F2ACTI    ANDNE'D'                                       CLE009
     C           BPFAAM    ANDEQ'Y'                                       CLE009
      *                                                                   CLE009
      ** If action code is insert                                         CLE009
      *                                                                   CLE009
     C           F2ACTI    IFEQ 'I'                                       CLE009
     C                     MOVE WKSWRI    SWRI                            CLE009
     C                     MOVE *BLANKS   AUTH                            CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
      ** If action code is amend                                          CLE009
      *                                                                   CLE009
     C           F2ACTI    IFEQ 'A'                                       CLE009
     C           ORED      IFLT BJRDNB                                    CLE009
     C                     MOVE WKSWRI    SWRI                            CLE009
     C                     MOVE 'Y'       AUTH                            CLE009
     C                     ELSE                                           CLE009
     C           WKSWRI    IFEQ 'C'                                       CLE009
     C           SWRI      ANDEQ*BLANKS                                   CLE009
     C                     MOVE WKSWRI    SWRI                            CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C                     ELSE                                           CLE009
      *                                                                   CLE009
      ** If action code is authorise                                      CLE009
      *                                                                   CLE009
     C           F2ACTI    IFEQ 'X'                                       CLE009
     C                     MOVE SWRI      WKSWRI                          CLE009
     C                     MOVE 'Y'       AUTH                            CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
      ** If action code is reverse                                        CLE009
      *                                                                   CLE009
     C           F2ACTI    IFEQ 'D'                                       CLE009
     C           AUTH      IFEQ *BLANKS                                   CLE009
     C                     MOVE *BLANKS   WKSWRI                          CLE009
     C                     ELSE                                           CLE009
      *                                                                   CLE009
      ** If work regeneration indicator is blank                          CLE009
      *                                                                   CLE009
     C           WKSWRI    IFEQ *BLANKS                                   CLE009
     C                     MOVE SWRI      WKSWRI                          CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
      ** If work regeneration indicator is changed                        CLE009
      *                                                                   CLE009
     C           WKSWRI    IFEQ 'C'                                       CLE009
      *                                                                   CLE009
      ** Check if the facility has an associated fee                      CLE009
      *                                                                   CLE009
     C                     MOVE FACT      CHFACT  3                       CLE009
     C                     MOVE FCNO      CHFCNO  2                       CLE009
     C           CHFACT    CAT  CHFCNO    CHFACL  5                       CLE009
     C                     MOVE CHFACL    WFFACL                          CLE009
     C**********           Z-ADD0         WFLOAN                                       CLE009 CLE148
     C                     MOVEL*BLANKS   WFLOAN                                              CLE148
     C           WKFEE     SETLLLEFEEDL1                                  CLE009
     C           WKFEE     READELEFEEDL1                 41               CLE009
      *                                                                   CLE009
      ** Do while an associated fee exists                                CLE009
      *                                                                   CLE009
     C           *IN41     DOWEQ*OFF                                      CLE009
      *                                                                   CLE009
      ** If the fee is a confirmed calculated fee and swift               CLE009
      ** regeneration indicator is not 'I', 'B', 'H'                      CLE009
      *                                                                   CLE009
     C           FEAUTO    IFEQ 'Y'                                       CLE009
     C           FECALT    ANDNE*BLANKS                                   CLE009
     C           FESWRI    ANDNE'I'                                       CLE009
     C           FESWRI    ANDNE'B'                                       CLE009
     C           FESWRI    ANDNE'H'                                       CLE009
      *                                                                   CLE009
      ** Set currency to fee currency                                     CLE009
      ** Set day to run date                                              CLE009
      ** Set number of working days to telex notice days                  CLE009
      *                                                                   CLE009
     C                     MOVE FEFCCY    ZCCY                            CLE009
     C                     Z-ADDBJRDNB    ZDAYNO                          CLE009
     C                     Z-ADD1         Y                               CLE009
      *                                                                   CLE031
      ** If feature CLE031 is on                                          CLE031
      *                                                                   CLE031
     C                     MOVE FEFCCY    WKCCY   3                       CLE031
      *                                                                   CLE031
     C           CLE031    IFEQ 'Y'                                       CLE031
      *                                                                   CLE031
     C           PTFI      IFEQ 'P'                                       CLE031
     C           FEPSCY    IFNE *BLANKS                                   CLE031
     C                     MOVE FEPSCY    WKCCY                           CLE031
     C                     ENDIF                                          CLE031
     C                     ELSE                                           CLE031
     C           FESCCY    IFNE *BLANKS                                   CLE031
     C                     MOVE FESCCY    WKCCY                           CLE031
     C                     ENDIF                                          CLE031
     C                     ENDIF                                          CLE031
      *                                                                   CLE031
     C                     ENDIF                                          CLE031
      *                                                                   CLE031
     C                     MOVE WKCCY     ZCCY                            CLE031
     C                     MOVE WKCCY     PCURR                           CLE031
      *                                                                   CLE031
     C********** FEFCCY    LOKUPCYA,Y                    42        CLE009 CLE031
     C           WKCCY     LOKUPCYA,Y                    42               CLE031
     C                     MOVE TND,Y     ZNRDYS                          CLE009
      *                                                                   CLE009
      ** If Fee Settlement type is '01', '02', '07', '08' or '12'         CLE009
      *                                                                   CLE009
     C           FESTTL    IFEQ 01                                        CLE009
     C           FESTTL    OREQ 02                                        CLE009
     C           FESTTL    OREQ 07                                        CLE009
     C           FESTTL    OREQ 08                                        CLE009
     C           FESTTL    OREQ 12                                        CLE009
      *                                                                   CLE009
     C                     CALL 'AONOSTR0'                                CLE009
     C                     PARM *BLANKS   PRTCD                           CLE009
     C                     PARM '*KEY   ' POPTN                           CLE009
     C                     PARM           PCUSN   6                       CLE009
     C                     PARM FEFCCY    PCURR                           CLE009
     C**********           PARM           PACCD   4                                    CLE009 CGL029
     C                     PARM           PACCD  10                                           CGL029
     C                     PARM           PACSN   2                       CLE009
     C                     PARM FEOURS    PNONB   2                       CLE009
     C                     PARM           PBRCA   3                       CLE009
     C                     PARM           PCSSN  10                       CLE009
     C                     PARM           PPNOI   1                       CLE009
     C           SDNOST    PARM SDNOST    DSFDY                           CLE009
      *                                                                   CLE009
     C           PRTCD     IFNE *BLANKS                                   CLE009
     C           *LOCK     IN   LDA                                       CLE009
     C                     MOVEL'AONOSTR0'DBFILE           ************   CLE009
     C                     Z-ADD29        DBASE            * DBERR 29 *   CLE009
     C                     MOVELFEFCCY    DBKEY            ************   CLE009
     C                     OUT  LDA                                       CLE009
     C                     EXSR *PSSR                                     CLE009
      *                                                                   CLE009
     C                     ELSE                                           CLE009
      *                                                                   CLE009
      ** Set Location to Nostro Location                                  CLE009
      *                                                                   CLE009
     C                     MOVE A7NOSN    ZLOC                            CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C                     ELSE                                           CLE009
      *                                                                   CLE009
     C                     MOVE *BLANKS   ZLOC                            CLE009
      *                                                                   CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
      ** Calculate number of working days forward                         CLE009
      *                                                                   CLE009
     C                     EXSR ZFWDT                                     CLE009
      *                                                                   CLE009
      ** Set Telex Notice date to day number                              CLE009
      *                                                                   CLE009
     C                     Z-ADDZDYNBR    TXDT    50                      CLE009
      *                                                                   CLE009
      ** If next payment date is before or equals to Telex Notice date    CLE009
      ** and Next Payment Telex indicator is set to 'Y'                   CLE009
      *                                                                   CLE009
     C           FENPYD    IFLE TXDT                                      CLE009
     C           FEPPTI    ANDEQ'Y'                                       CLE009
      *                                                                   CLE009
      ** Update Fees file                                                 CLE009
      *                                                                   CLE009
     C                     MOVE 'A'       FESWRI                          CLE009
     C                     UPDATLEFEEDF                                   CLE009
     C                     ELSE                                           CLE009
      *                                                                   CLE009
      ** If end date is before or equals to Telex Notice date and End     CLE009
      ** Telex indicator is set to 'Y'                                    CLE009
      *                                                                   CLE009
     C           FEPEND    IFLE TXDT                                      CLE009
     C           FEEPTI    ANDEQ'Y'                                       CLE009
      *                                                                   CLE009
      ** Update Fees file                                                 CLE009
      *                                                                   CLE009
     C                     MOVE 'A'       FESWRI                          CLE009
     C                     UPDATLEFEEDF                                   CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C           WKFEE     READELEFEEDL1                 41               CLE009
     C                     ENDDO                                          CLE009
     C                     ENDIF                                          CLE009
     C                     ENDIF                                          CLE009
      *                                                                   CLE009
     C                     MOVE *BLANKS   WKSWRI                          CLE009
      *                                                                   CLE009
     C                     ENDSR                                          CLE009
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRVDET  -  Validate Detail Screen Input                      *
      *                                                               *
      *  Called by: SRDETL, SRBTCH, SRPLAY                            *
      *  Calls    :                                                   *
      *  AOCURRR0   Access program for Currency Details               *
      *  SREROR  -  Error Processing                                  *
      *  ZALIGN  -  Val/right aligned numberic fields                 *
      *  ZCHKH   -  Check if holiday for ccy/locn                     *
      *  ZDATE1  -  Validate & cvt date to a day nbr                  *
      *  ZEDIT   -  Ins dec point & 0s for numeric                    *
      *                                                               *
      *****************************************************************
      *
     C           SRVDET    BEGSR
      *
      ** Get Facility Details
      *
     C                     MOVE F2CNUM    WKCNUM
     C                     MOVE F2FACT    WKFACT
     C                     MOVE F2FCNO    WKFCNO
     C                     MOVEL'A'       WKRCTP
     C           FCTYKY    CHAINFCLTY                80
      *
     C                     MOVEL'B'       WKRCTP
     C           FCTYKY    CHAINFCLTY                80
      *
     C                     MOVELRVCR      WVRVCR  1
      *
      ** Validate Currency
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY    'POPTN   7
     C           F2ACCY    PARM F2ACCY    PK101   3
     C********** SDCURR    PARM SDCURR    DSFDY                                               CSD006
     C           SDCURR    PARM SDCURR    DSSDY                                               CSD006
      *
     C           PRTCD     IFNE *BLANK
     C                     MOVE *ON       *IN21
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0018' ZAMSID
     C                     EXSR SREROR
      *
     C           PRTCD     IFEQ '*NOSEL  '
     C                     MOVE *BLANKS   F2ACCY
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           F2ACCY    IFNE FCCY
     C           *IN21     ANDEQ*OFF
     C                     MOVE *ON       *IN21
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0201' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
      ** Validate Amount
      *
     C           F1FATP    IFEQ 'FI'
     C           F1FATP    OREQ 'FD'
      *
     C           F2AAMT    IFEQ *BLANKS
     C                     MOVE *ON       *IN22
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0202' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           F1FATP    IFEQ 'RC'
      *
     C           F2AAMT    IFNE *BLANKS
     C                     MOVE *ON       *IN22
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0203' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           *IN22     IFEQ *OFF
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELF2AAMT    ZFIELD
     C                     Z-ADDA6NBDP    ZADEC
     C           15        SUB  ZADEC     ZADIG
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    WAAMT
     C           *IN43     IFEQ *ON                                                           165649
     C           F2SIGN    IFNE ' '                                                           165649
     C           F2SIGN    ANDNE'-'                                                           165649
     C           F2SIGN    ANDNE'+'                                                           165649
     C                     MOVE *ON       *IN44                                               165649
     C                     MOVEL'E'       WMSEV                                               165649
     C                     MOVEL'LEF0326' ZAMSID                                              165649
     C                     EXSR SREROR                                                        165649
     C                     ELSE                                                               165649
     C                     MOVE F2SIGN    WSIGN                                               165649
     C                     END                                                                165649
     C                     END                                                                165649
      *
     C           *IN99     IFEQ *ON
     C                     MOVE *ON       *IN22
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0204' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C           F1FATP    IFEQ 'FI'
     C           F1FATP    OREQ 'FD'
      *
     C           *IN22     IFEQ *OFF
     C           WAAMT     ANDEQ*ZERO
     C                     MOVE *ON       *IN22
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0202' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** for Facility Decrease
      *
     C           *IN22     IFEQ *OFF
     C           F1FATP    ANDEQ'FD'
     C           PIGASS    ANDNE'A'                                       157217
      *
     C                     Z-ADD*ZERO     WAVAIL 130
     C                     Z-ADD1         WI      20
      *
     C           WI        DOWLE10
     C           FAMT      SUB  @OAM,WI   WAVAIL
      *
     C           @RUN,WI   IFGE WVLDT
     C                     LEAVE
     C                     ENDIF
      *
     C                     ADD  1         WI
     C                     ENDDO
      *
     C           WVLDT     IFLT @RUN,1
     C           FAMT      SUB  @OAM,1    WAVAIL
     C                     ENDIF
      *
     C                     MOVE F2CNUM    WKCNUM
     C                     MOVE F2FACT    WKFACT
     C                     MOVE F2FCNO    WKFCNO
     C                     Z-ADD*ZERO     WKVLDT
     C                     Z-ADD*ZERO     WKSQNO
     C           FCAMKY    SETLLLEFCAML6
     C                     MOVE F2SQNO    WKSQNO
     C***********          READ LEFCAML6            N    80               135744
     C           FCAMK3    READELEFCAML6            N    80               135744
     C                     Z-ADD*ZERO     WFAMD  130                                          191838
      *
     C           *IN80     DOWEQ*OFF
     C***********CNUM      ANDEQWKCNUM                                    135744
     C***********FACT      ANDEQWKFACT                                    135744
     C***********FCNO      ANDEQWKFCNO                                    135744
     C           VLDT      ANDLEWVLDT
      *
     C           VLDT      IFEQ WVLDT                                                         191838
      *                                                                                       191838
     C           FATP      IFEQ 'FI'                                                          191838
     C                     SUB  AAMT      WFAMD                                               191838
     C                     ENDIF                                                              191838
      *                                                                                       191838
     C           FATP      IFEQ 'FD'                                                          191838
     C           SQNO      ANDNEWKSQNO                                                        191838
     C                     ADD  AAMT      WFAMD                                               191838
     C                     ENDIF                                                              191838
      *                                                                                       191838
     C                     ENDIF                                                              191838
      *                                                                                       191838
     C           FATP      IFEQ 'FI'
     C                     ADD  AAMT      WAVAIL
     C                     ENDIF
      *
     C           FATP      IFEQ 'FD'
      *
     C           VLDT      IFNE WVLDT
     C           SQNO      ORNE WKSQNO
     C                     SUB  AAMT      WAVAIL
     C                     ENDIF
      *
     C                     ENDIF
      *
     C***********          READ LEFCAML6            N    80               135744
     C           FCAMK3    READELEFCAML6            N    80               135744
     C                     ENDDO
      *                                                                                       191838
     C                     ADD  WAAMT     WFAMD                                               191838
      *
     C           WAAMT     IFGT FAMT                                                          191838
     C           WFAMD     ORGT FAMT                                                          191838
     C                     MOVE *ON       *IN22                                               191838
     C                     MOVEL'E'       WMSEV                                               191838
     C                     MOVEL'LEF0324' ZAMSID                                              191838
     C                     EXSR SREROR                                                        191838
     C                     ELSE                                                               191838
     C           *IN22     IFEQ *OFF                                                          191838
     C           WPAMT     ANDNEWAAMT                                                         191838
     C           WAAMT     IFGT WAVAIL
     C                     MOVE *ON       *IN22
     C**********           MOVEL'E'       WMSEV                                               191838
     C**********           MOVEL'LEF0205' ZAMSID                                              191838
     C                     MOVEL'W'       WMSEV                                               191838
     C                     MOVEL'LEF0325' ZAMSID                                              191838
     C                     EXSR SREROR
     C                     ENDIF
      *                                                                                       191838
     C                     ENDIF                                                              191838
      *
     C                     ENDIF                                                              191838
     C                     Z-ADDWAAMT     WPAMT  130                                          191838
      *                                                                                       191838
     C                     ENDIF
      *
      ** Default Amendment Amount to Available Amount at expiry
      *
     C           F1FATP    IFEQ 'FR'
     C           F2AAMT    ANDEQ*BLANKS
     C**********           ADD  1         WERR                            186075
     C********** FAMT      SUB  TOTD      WAAMT                           186075
     C                     MOVE *BLANKS   ZFIELD
      *
     C********** WAAMT     IFLE *ZERO                                     186075
     C**********           MOVE *ON       *IN22                           186075
     C**********           MOVEL'W'       WMSEV                           186075
     C**********           MOVEL'LEF0235' ZAMSID                          186075
     C**********           EXSR SREROR                                    186075
     C                     Z-ADDFAMT      WAAMT
     C**********           ENDIF                                          186075
      *                                                                                       195955
      ** If the facility amount from file is negative, get its absolute                       195955
      ** value so that it will be properly displayed on the screen.                           195955
      ** Appropriate value must also be assigned to F2SIGN field.                             195955
      *                                                                                       195955
     C           WAAMT     IFLT 0                                                             195955
     C                     Z-SUBWAAMT     WAAMT                                               195955
     C                     MOVE '-'       F2SIGN                                              195955
     C                     ELSE                                                               195955
     C                     MOVE '+'       F2SIGN                                              195955
     C                     ENDIF                                                              195955
      *
     C                     MOVE WAAMT     ZFIELD
     C                     Z-ADDA6NBDP    ZADEC
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    F2AAMT
      *
     C                     ENDIF
      *
      ** Revolving Credit
      *
     C           F1FATP    IFEQ 'RC'
      *                                                                   CLE014
     C                     SELEC                                          CLE014
      *                                                                   CLE014
     C           CLE014    WHEQ 'Y'                                       CLE014
     C           TRCA      ANDEQ'CA'                                      CLE014
     C           F2RVCR    IFNE 'Y'                                       CLE014
     C           F2RVCR    ANDNE'N'                                       CLE014
     C           F2RVCR    ANDNE'T'                                       CLE014
     C                     MOVE *ON       *IN23                           CLE014
     C                     MOVEL'E'       WMSEV                           CLE014
     C                     MOVEL'LEF0351' ZAMSID                          CLE014
     C                     EXSR SREROR                                    CLE014
     C                     ENDIF                                          CLE014
      *                                                                   CLE014
     C                     OTHER                                          CLE014
      *
     C           F2RVCR    IFNE 'Y'
     C           F2RVCR    ANDNE'N'
     C                     MOVE *ON       *IN23
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0206' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
     C                     ENDSL                                          CLE014
      *
     C           *IN23     IFEQ *OFF
     C           F2RVCR    ANDNEWPRVCR
      *
     C           F2RVCR    IFEQ WVRVCR
     C                     MOVE *ON       *IN23
     C                     MOVEL'W'       WMSEV
     C                     MOVEL'LEF0236' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     MOVE F2RVCR    WPRVCR  1
      *
     C                     ENDIF
      *
     C                     ELSE
      *
     C           F2RVCR    IFNE *BLANKS
     C                     MOVE *ON       *IN23
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0207' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** New Expiry date
      *
     C           F1FATP    IFEQ 'FR'
      *
     C           F2NDEX    IFEQ *BLANKS
     C                     MOVE *ON       *IN24
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0208' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C           *IN24     IFEQ *OFF
      *
     C                     Z-ADD0         NUM7
     C                     MOVE *BLANKS   ALF6
     C                     MOVELF2NDEX    NUM7
     C                     MOVELNUM7      ALF6
      *
     C           F2NDEX    IFNE ALF6
     C                     MOVE *ON       *IN24
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0210' ZAMSID
     C                     EXSR SREROR
     C                     ELSE
     C                     MOVE *BLANKS   ZDATE
     C                     MOVE F2NDEX    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    WNDEX
     C           *IN99     IFEQ '1'
     C                     MOVE *ON       *IN24
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0210' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           *IN24     IFEQ *OFF
      *
     C***********WNDEX     IFLT DTAP                                      153357
     C           RECI      IFEQ 'E'                                       153357
     C           WNDEX     ANDLTDTEX                                      153357
     C           RECI      OREQ 'C'                                       153357
     C           WNDEX     ANDLTCDTE                                      153357
     C                     MOVE *ON       *IN24
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0211' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           *IN24     IFEQ *OFF
     C           F2NDEX    ANDNEWPNDEX
      *
     C                     Z-ADDWNDEX     ZDAYNO
     C                     MOVELF2ACCY    ZCCY
     C                     EXSR ZCHKH
      *
     C           ZIND      IFEQ 'H'
     C                     MOVE *ON       *IN24
     C                     MOVEL'W'       WMSEV
     C                     MOVEL'LEF0212' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     MOVELF2NDEX    WPNDEX  6
      *
     C                     ENDIF
      *
     C                     ELSE
      *
     C           F2NDEX    IFNE *BLANKS
     C                     MOVE *ON       *IN24
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0209' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C           *IN24     IFEQ *OFF
     C                     Z-ADD*ZERO     WNDEX
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRVUSR  -  Validate User/Action/Branch                       *
      *                                                               *
      *  Called by: SRSVAL, SRVISC                                    *
      *  Calls    :                                                   *
      *  SREROR  -  Error Processing                                  *
      *  ZVACTBU -  Val Act Code for User/Pgm/Brch                    *
      *  ZVACTU  -  Validate Act Code for User/Pgm                    *
      *  ZVOBU   -  Validate Originating Branch/User                  *
      *                                                               *
      *****************************************************************
      *
     C           SRVUSR    BEGSR
      *
     C                     MOVEL'N'       WVERR   1
     C**********           Z-ADD1         ZMGRP   40                                          CSF002
     C**********           MOVEL'FCAM'    ZICDE   4                                           CSF002
     C                     MOVEL'00000'   UNQCDE 10                                           CSF002
     C                     MOVE '02000'   UNQCDE                                              CSF002
      *
      ** If Multibranching ind. is 'Y', validate action code for
      ** User/Program/Branch
      *
     C           MBIN      IFEQ 'Y'
      *
     C           PMODE     IFNE 'B'
     C                     CALL 'ZVACTBU'
     C                     PARM           WVACTI  1
     C                     PARM           WVBRCA  3
     C                     PARM *ZERO     @ERR
     C                     ELSE
     C                     CALL 'ZBACTBU'
     C                     PARM           WVACTI  1
     C                     PARM           WVBRCA  3
     C                     PARM           USER
     C**********           PARM           ZMGRP                                               CSF002
     C**********           PARM           ZICDE                                               CSF002
     C                     PARM           UNQCDE                                              CSF002
     C                     PARM *ZERO     @ERR
     C                     ENDIF
      *
     C           @ERR      IFEQ 1
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'Y'       WVERR
     C                     MOVEL'LEF0010' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C           @ERR      IFEQ 2
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'Y'       WVERR
     C                     MOVEL'LEF0011' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
      ** Else, validate action code for User/Program
      *
     C                     ELSE
      *
     C           PMODE     IFNE 'B'
     C                     CALL 'ZVACTU'
     C                     PARM           WVACTI
     C                     PARM *ZERO     @ERR
     C                     ELSE
     C                     CALL 'ZBACTU'
     C                     PARM           WVACTI
     C                     PARM           USER
     C**********           PARM           ZMGRP                                               CSF002
     C**********           PARM           ZICDE                                               CSF002
     C                     PARM           UNQCDE                                              CSF002
     C                     PARM *ZERO     @ERR
     C                     ENDIF
      *
     C           @ERR      IFEQ 1
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'Y'       WVERR
     C                     MOVEL'LEF0009' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     ENDIF
      *
      * If Originating Branches User Validation is required               168022
     C           BKOBUV    IFEQ 'Y'                                       168022
      ** Validate Originating Branch/User
      *
     C           PMODE     IFNE 'B'
     C                     CALL 'ZVOBU'
     C                     PARM           WVORBR  3
     C                     PARM *ZERO     @ERR
     C                     ELSE
     C                     CALL 'ZBOBU'
     C                     PARM           WVORBR  3
     C                     PARM           USER
     C                     PARM *ZERO     @ERR
     C                     ENDIF
      *
     C           @ERR      IFEQ 1
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'Y'       WVERR
     C                     MOVEL'LEF0012' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C           @ERR      IFEQ 2
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'Y'       WVERR
     C                     MOVEL'LEF0013' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
     C                     ENDIF                                          168022
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRVISC  -  Validate Initial Screen Input                     *
      *                                                               *
      *  Called by: SRISCR, SRBTCH, SRPLAY, SRISSF                    *
      *  Calls    :                                                   *
      *  AOCUSTR0   Access program for Customer Details               *
      *  SREROR  -  Error Processing                                  *
      *  SRVUSR  -  Validate User/Action/Branch                       *
      *  ZALIGN  -  Val/right aligned numberic fields                 *
      *  ZCHKH   -  Check if holiday for ccy/locn                     *
      *  ZDATE1  -  Validate & cvt date to a day nbr                  *
      *                                                               *
      *****************************************************************
      *
     C           SRVISC    BEGSR
      *
      ** Validate Action code
      *
     C           F1ACTI    IFNE 'E'
     C           F1ACTI    ANDNE'A'
     C           F1ACTI    ANDNE'X'
     C           F1ACTI    ANDNE'D'
     C           F1ACTI    ANDNE'I'
     C                     MOVE *ON       *IN11
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0001' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C           *IN11     IFEQ *OFF
      *
     C           F1ACTI    IFEQ 'X'
      *
     C           *IN31     IFEQ *OFF
     C           CLE002    OREQ 'N'
     C                     MOVE *ON       *IN11
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0213' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Validate Customer Number
      *
     C           F1CUST    IFEQ *BLANKS
     C                     MOVE *ON       *IN12
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0214' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C           *IN12     IFEQ *OFF
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY    'POPTN   7
     C                     PARM F1CUST    PKEY1  10
     C                     PARM           PKYST   7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           PRTCD     IFNE *BLANK
     C                     MOVE *ON       *IN12
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0214' ZAMSID
     C                     EXSR SREROR
      *
     C           PRTCD     IFEQ '*NOSEL  '
     C                     MOVE *BLANKS   F1CUST
     C                     ENDIF
      *
     C                     ELSE
     C                     MOVE *BLANKS   F1CUST
     C                     MOVE BBCUST    F1CUST
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Validate Facility Type and Facility Number
      *
     C           F1FACT    IFEQ *BLANKS
     C           F1FCNO    OREQ *BLANKS
     C                     MOVE *ON       *IN13
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0215' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C           *IN13     IFEQ *OFF
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELF1FACT    ZFIELD
     C                     Z-ADD*ZERO     ZADEC
     C           15        SUB  ZADEC     ZADIG
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ *ON
     C                     MOVE *ON       *IN13
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0004' ZAMSID
     C                     EXSR SREROR
     C                     ELSE
     C                     MOVE ZFIELD    F1FACT
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           *IN13     IFEQ *OFF
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELF1FCNO    ZFIELD
     C                     Z-ADD*ZERO     ZADEC
     C           15        SUB  ZADEC     ZADIG
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ *ON
     C                     MOVE *ON       *IN13
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0005' ZAMSID
     C                     EXSR SREROR
     C                     ELSE
     C                     MOVE ZFIELD    F1FCNO
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Validate Facility Reference
      *
     C           *IN12     IFEQ *OFF
     C           *IN13     ANDEQ*OFF
     C                     MOVE F1CUST    WKCNUM
     C                     MOVE F1FACT    WKFACT
     C                     MOVE F1FCNO    WKFCNO
     C                     MOVEL'A'       WKRCTP
     C           FCTYKY    CHAINFCLTY                80
      *
     C           *IN80     IFEQ *OFF
     C                     MOVELORBR      WVORBR
     C                     MOVELBRCA      WVBRCA
     C                     ELSE
     C                     MOVEA'11'      *IN,12
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0006' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     ENDIF
     C/COPY WNCPYSRC,LE2150C012
      *
      ** Validate Amendment type
      *
     C           F1ACTI    IFEQ 'I'
      *
     C           F1FATP    IFNE 'FI'
     C           F1FATP    ANDNE'FD'
     C           F1FATP    ANDNE'FR'
     C           F1FATP    ANDNE'RC'
     C                     MOVE *ON       *IN14
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0216' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C           *IN14     IFEQ *OFF
      *
     C           RECI      IFEQ 'D'
      *
     C           F1FATP    IFEQ 'FR'
     C                     MOVE *ON       *IN14
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0218' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C***********          ELSE                                           159692
     C                     ENDIF                                          159692
      *                                                                   159692
     C           RECI      IFNE 'D'                                       159692
     C           RECI      ANDNE'E'                                       159692
      *
     C           F1FATP    IFEQ 'FR'
     C           RECI      ANDEQ'R'
     C                     MOVE *ON       *IN14
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0279' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C           F1FATP    IFNE 'FR'
      *                                                                   149746
     C                     MOVE F1CUST    WKCNUM                          149746
     C                     MOVELF1FACT    WKFACT                          149746
     C                     MOVELF1FCNO    WKFCNO                          149746
     C                     MOVE F1VLDT    ZDATE                           149746
     C                     EXSR ZDATE1                                    149746
     C                     Z-ADDZDAYNO    WVLDT                           149746
     C           FCAMK3    SETLLLEFCAML3                                  149746
     C           FCAMK3    READELEFCAML3                 80               149746
      *                                                                   149746
     C                     MOVE ' '       WFCR                            149746
     C           *IN80     DOWEQ'0'                                       149746
     C           VLDT      ANDLEWVLDT                                     149746
     C           FATP      IFEQ 'FR'                                      149746
     C                     MOVE 'Y'       WFCR    1                       149746
     C                     LEAVE                                          149746
     C                     ELSE                                           149746
     C***********FCAMK4    READELEFCAML3                 80         149746159692
     C           FCAMK3    READELEFCAML3                 80               159692
     C                     ENDIF                                          149746
     C                     ENDDO                                          149746
     C           WFCR      IFEQ ' '                                       149746
     C           *IN80     OREQ '1'                                       149746
     C                     MOVE *ON       *IN14
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0219' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF                                          149746
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           *IN14     IFEQ *OFF
     C           F1FATP    ANDEQ'FR'
     C                     MOVE F1CUST    WKCNUM
     C                     MOVE F1FACT    WKFACT
     C                     MOVE F1FCNO    WKFCNO
     C                     Z-ADD*ZERO     WKVLDT
     C                     Z-ADD*ZERO     WKSQNO
     C           FCAMKY    SETLLLEFCAML6
     C***********          READ LEFCAML6            N    80               135744
     C           FCAMK3    READELEFCAML6            N    80               135744
      *
     C           *IN80     DOWEQ*OFF
     C***********CNUM      ANDEQWKCNUM                                    135744
     C***********FACT      ANDEQWKFACT                                    135744
     C***********FCNO      ANDEQWKFCNO                                    135744
      *
     C           FATP      IFEQ 'FR'
     C                     MOVE *ON       *IN14
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0234' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C***********          READ LEFCAML6            N    80               135744
     C           FCAMK3    READELEFCAML6            N    80               135744
     C                     ENDDO
      *
     C                     ENDIF
      *
     C                     ELSE
      *
     C           F1FATP    IFNE *BLANKS
     C                     MOVE *ON       *IN14
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0217' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Validate Value Date
      *
     C           F1VLDT    IFEQ *BLANKS
     C           F1FATP    IFEQ 'FR'                                      154797
     C                     Z-ADDBJRDNB    ZDAYNO                          154797
     C                     EXSR ZDATE2                                    154797
     C                     MOVE ZDATE     F1VLDT                          154797
     C                     ELSE                                           154797
     C                     MOVE *ON       *IN15
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0220' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF                                          154797
     C                     ENDIF
      *
     C           *IN15     IFEQ *OFF
      *
     C                     Z-ADD0         NUM7    70
     C                     MOVE *BLANKS   ALF6    6
     C                     MOVELF1VLDT    NUM7
     C                     MOVELNUM7      ALF6
      *
     C           F1VLDT    IFNE ALF6
     C                     MOVE *ON       *IN15
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0220' ZAMSID
     C                     EXSR SREROR
     C                     ELSE
     C                     MOVE *BLANKS   ZDATE
     C                     MOVE F1VLDT    ZDATE
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    WVLDT
     C           *IN99     IFEQ '1'
     C                     MOVE *ON       *IN15
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0220' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           *IN15     IFEQ *OFF
      *
     C           WVLDT     IFLT DTAP
     C                     MOVE *ON       *IN15
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0221' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           *IN15     IFEQ *OFF
      *
     C           WVLDT     IFGT DTEX
     C           F1FATP    ANDNE'FR'
      *                                                                   149746
     C           WFCR      IFEQ ' '                                       149746
     C           F1ACTI    ANDEQ'I'                                       149746
     C           WFCR      OREQ 'Y'                                       149746
     C           WVLDT     ANDGTNDEX                                      149746
     C                     MOVE *ON       *IN15
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0222' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF                                          149746
     C                     ENDIF
     C           WVLDT     IFNE DTEX
     C           F1FATP    ANDEQ'FR'
     C           RECI      ANDEQ'E'                                       127737
     C                     MOVE *ON       *IN15
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0278' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
     C***********WVLDT     IFNE CDTE                               127737 154797
      *                                                                   154797
      ** If facility is closed, check whether closed before or after      154797
      ** expiry and condition value date validation on the result.        154797
      *                                                                   154797
     C           DTEX      IFGT BJRDNB                                    154797
      *                                                                   154797
     C           WVLDT     IFNE BJRDNB                                    154797
     C           F1FATP    ANDEQ'FR'                                      127737
     C           RECI      ANDEQ'C'                                       127737
     C                     MOVE *ON       *IN15                           127737
     C                     MOVEL'E'       WMSEV                           127737
     C                     MOVEL'LEF0292' ZAMSID                          127737
     C                     EXSR SREROR                                    127737
     C                     ENDIF                                          127737
      *                                                                   154797
     C                     ELSE                                           154797
      *                                                                   154797
     C           WVLDT     IFNE DTEX                                      154797
     C           F1FATP    ANDEQ'FR'                                      154797
     C           RECI      ANDEQ'C'                                       154797
     C                     MOVE *ON       *IN15                           154797
     C                     MOVEL'E'       WMSEV                           154797
     C                     MOVEL'LEF0292' ZAMSID                          154797
     C                     EXSR SREROR                                    154797
     C                     ENDIF                                          154797
      *                                                                   154797
     C                     ENDIF                                          154797
      *
     C                     ENDIF
      *
     C           F1ACTI    IFEQ 'I'
      *
     C           *IN15     IFEQ *OFF
     C           WPVLDT    ANDNEF1VLDT
      *
     C                     Z-ADDWVLDT     ZDAYNO
     C                     MOVELFCCY      ZCCY
     C                     EXSR ZCHKH
      *
     C           ZIND      IFEQ 'H'
     C                     MOVE *ON       *IN15
     C                     MOVEL'W'       WMSEV
     C                     MOVEL'LEF0223' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     MOVELF1VLDT    WPVLDT  6
      *
     C                     ENDIF
      *
      ** Validate Sequence No.
      *
     C           F1ACTI    IFEQ 'I'
      *
     C           F1SQNO    IFNE *BLANKS
     C                     MOVE *ON       *IN16
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0224' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     ELSE
      *
     C           F1SQNO    IFEQ *BLANKS
     C                     MOVE *ON       *IN16
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0225' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C           *IN16     IFEQ *OFF
      *
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVELF1SQNO    ZFIELD
     C                     Z-ADD*ZERO     ZADEC
     C           15        SUB  ZADEC     ZADIG
     C                     EXSR ZALIGN
      *
     C           *IN99     IFEQ *ON
     C                     MOVE *ON       *IN16
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0225' ZAMSID
     C                     EXSR SREROR
     C                     ELSE
     C                     MOVE ZFIELD    F1SQNO
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Access Facility Amendment record for action not Insert
      ** and no other errors
      *
     C           F1ACTI    IFNE 'I'
     C           *IN11     ANDEQ*OFF
     C           *IN12     ANDEQ*OFF
     C           *IN13     ANDEQ*OFF
     C           *IN14     ANDEQ*OFF
     C           *IN15     ANDEQ*OFF
     C           *IN16     ANDEQ*OFF
      *
     C                     MOVE F1CUST    WKCNUM
     C                     MOVE F1FACT    WKFACT
     C                     MOVE F1FCNO    WKFCNO
     C                     Z-ADDWVLDT     WKVLDT
     C                     MOVE F1SQNO    WKSQNO
      *
     C           FCAMKY    CHAINLEFCAML3             80
      *
     C           *IN80     IFEQ *ON
     C                     MOVEA'11111'   *IN,12
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0226' ZAMSID
     C                     EXSR SREROR
     C                     ELSE
      *
     C                     MOVELFATP      F1FATP
     C                     MOVELACCY      WACCY
     C                     MOVELRVCR      WRVCR
     C                     Z-ADDAAMT      WAAMT
     C                     Z-ADDNDEX      WNDEX
      *
     C           F1ACTI    IFEQ 'A'
     C           RECI      ANDNE'D'
     C                     MOVEA'111111'  *IN,11
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0227' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C           F1ACTI    IFEQ 'X'
      *
     C           RECI      IFNE 'D'
     C                     MOVEA'111111'  *IN,11
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0227' ZAMSID
     C                     EXSR SREROR
     C                     ELSE
      *
     C           ASTS      IFNE 'C'
     C           ASTS      ANDNE'R'
     C                     MOVEA'111111'  *IN,11
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0228' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     ENDIF
     C/COPY WNCPYSRC,LE2150C004
      *
     C           USER      IFEQ AUSR
     C           USER      OREQ IUSR
     C                     MOVEA'111111'  *IN,11
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0231' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
     C/COPY WNCPYSRC,LE2150C005
      *
     C                     ENDIF
      *
     C           F1ACTI    IFEQ 'D'
     C           RECI      ANDNE'D'
     C                     MOVEA'111111'  *IN,11
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0229' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** Execute User authority check for user, action, branch
      *
     C           PMODE     IFNE 'P'
      *
     C           *IN11     IFEQ *OFF
     C           *IN12     ANDEQ*OFF
     C           *IN13     ANDEQ*OFF
     C           *IN14     ANDEQ*OFF
     C           *IN15     ANDEQ*OFF
     C           *IN16     ANDEQ*OFF
     C                     MOVELF1ACTI    WVACTI
     C                     EXSR SRVUSR
      *
     C           WVERR     IFEQ 'Y'
     C                     MOVEA'111111'  *IN,11
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRVRVW  -  Validate Review From                              *
      *                                                               *
      *  Called by: SRISCR, SRSELE                                    *
      *  Calls    :                                                   *
      *  SREROR  -  Error Processing                                  *
      *  ZALIGN  -  Val/right aligned numberic fields                 *
      *                                                               *
      *****************************************************************
      *
     C           SRVRVW    BEGSR
      *
     C                     MOVEA'000'     *IN,17
     C                     MOVEA'000'     *IN,27
      *
      ** Validate Review from Customer
      *
     C           C0CNUM    IFNE *BLANKS
      *
     C                     MOVE '0'       WWRK6   7
     C                     MOVELC0CNUM    WWRK6
     C**********           TESTN          WWRK6      40                                       CSD027
     C********** *IN40     IFEQ *OFF                                                          CSD027
     C**********           MOVE *ON       *IN17                                               CSD027
     C**********           MOVE *ON       *IN27                                               CSD027
     C**********           MOVEL'E'       WMSEV                                               CSD027
     C**********           MOVEL'LEF0214' ZAMSID                                              CSD027
     C**********           EXSR SREROR                                                        CSD027
     C**********           ENDIF                                                              CSD027
      *
     C                     ENDIF
      *
     C           *IN27     IFEQ *OFF
     C                     MOVE C0CNUM    F1CNUM
     C                     ENDIF
      *
      ** Validate Review Facility Type/Number
      *
     C           C0FACT    IFNE *BLANKS
      *
     C                     MOVE '0'       WWRK3   4
     C                     MOVELC0FACT    WWRK3
     C                     TESTN          WWRK3      40
     C           *IN40     IFEQ *OFF
     C                     MOVE *ON       *IN18
     C                     MOVE *ON       *IN28
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0004' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           *IN28     IFEQ *OFF
     C                     MOVE C0FACT    F1FAC2
     C                     ENDIF
      *
     C           C0FCNO    IFNE *BLANKS
      *
     C                     MOVE '0'       WWRK2   3
     C                     MOVELC0FCNO    WWRK2
     C                     TESTN          WWRK2      40
     C           *IN40     IFEQ *OFF
     C                     MOVE *ON       *IN18
     C                     MOVE *ON       *IN28
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0005' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C           *IN28     IFEQ *OFF
     C                     MOVE C0FCNO    F1FCN2
     C                     ENDIF
      *
      ** Validate Req. Authorisation
      *
     C           C0FAAM    IFEQ *BLANKS
     C                     MOVEL'N'       C0FAAM
     C                     ENDIF
      *
     C           C0FAAM    IFNE 'Y'
     C           C0FAAM    ANDNE'N'
     C                     MOVE *ON       *IN19
     C                     MOVE *ON       *IN29
     C                     MOVEL'E'       WMSEV
     C                     MOVEL'LEF0014' ZAMSID
     C                     EXSR SREROR
     C                     ENDIF
      *
     C           *IN29     IFEQ *OFF
     C                     MOVELC0FAAM    F1FAAM
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRSQNO  -  Get Sequence No.                                  *
      *                                                               *
      *  Called by: SRUPDT                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SRSQNO    BEGSR
      *
     C                     MOVE F2CNUM    WKCNUM
     C                     MOVE F2FACT    WKFACT
     C                     MOVE F2FCNO    WKFCNO
     C                     MOVE WVLDT     WKVLDT
     C                     Z-ADD*ZERO     WKSQNO
     C                     Z-ADD1         WSQNO
      *
     C********** FCAMKY    SETLLLEFCAML3                                  190017
     C***********          READ LEFCAML3                 80               135744
     C********** FCAMK4    READELEFCAML3                 80        135744 190017
     C           FCAMKY    SETLLLEFCAMSQ                                  190017
     C           FCAMK4    READELEFCAMSQ                 80               190017
      *
     C           *IN80     DOWEQ*OFF
     C***********CNUM      ANDEQWKCNUM                                    135744
     C***********FACT      ANDEQWKFACT                                    135744
     C***********FCNO      ANDEQWKFCNO                                    135744
     C***********VLDT      ANDEQWKVLDT                                    135744
     C********** SQNO      ANDEQWSQNO                                     190017
     C           SQNOQ     ANDEQWSQNO                                     190017
     C                     ADD  1         WSQNO
     C***********          READ LEFCAML3                 80               135744
     C********** FCAMK4    READELEFCAML3                 80        135744 190017
     C           FCAMK4    READELEFCAMSQ                 80               190017
     C                     ENDDO
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRWIND  -  Window Processing                                 *
      *                                                               *
      *  Called by: SRDETL                                            *
      *  Calls    :                                                   *
      *  WN0010  -  Window manager                                    *
      *  ZASNMS  -  Sends messages to the program's message queue     *
      *                                                               *
      *****************************************************************
      *
     C           SRWIND    BEGSR
      *
     C/COPY WNCPYSRC,LE2150MOV1
      *
     C                     MOVELF2ACTI    UACTCD
     C           F2ACTI    IFEQ 'X'
     C                     MOVE 'E'       UACTCD
     C                     ENDIF
      *
     C                     CALL 'WN0010'
     C                     PARM SPGM      UPGM   10        Program
     C                     PARM *BLANK    UFKEY   2        Function Key
     C                     PARM           UACTCD  1        Action
     C                     PARM           DATA             Trans Data
     C                     PARM *BLANK    W0RTN   7        Return Code
     C                     PARM           SPARE 256        Spare Field
      *
     C                     SELEC
     C           W0RTN     WHEQ 'Y2U9999'
     C                     MOVE *ON       *INKC
      *
     C           W0RTN     WHEQ 'USR0790'
     C                     MOVE *ON       *INKL
      *
     C           W0RTN     WHNE *BLANKS
     C                     MOVELW0RTN     ZAMSID
     C                     MOVEL'WNMSGF  'ZAMSGF
     C                     MOVEL'LERMSGF 'ZAMSGF
     C                     EXSR ZASNMS
     C                     ENDSL
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * SREROR  -  Error processing                                   *
      *                                                               *
      * Called by: SRLOAD, SRDETL, SRSVAL, SRVDET, SRVUSR, SRVISC,    *
      *            SRVRVW                                             *
      * Calls    :                                                    *
      *  SRCLSE  -  Closedown                                         *
      *  Y2RVMGC -  Retrieve a message                                *
      *  ZASNMS  -  Sends messages to the program's message queue     *
      *                                                               *
      *****************************************************************
      *
     C           SREROR    BEGSR
      *
     C                     MOVEL'LERMSGF 'ZAMSGF
     C                     ADD  1         WERR    20
      *
     C           ZAPGMQ    IFEQ *BLANK
     C                     MOVELSPGM      ZAPGMQ
     C                     END
      *
      ** On Interactive/Record Mode, send message to program msgq
      *
     C           PMODE     IFEQ 'I'
     C           PMODE     OREQ 'R'
     C                     EXSR ZASNMS
     C                     ELSE
      *
      ** For Batch and Play mode, return message
      *
     C                     CALL 'Y2RVMGC'
     C                     PARM           PRTCD
     C           WMSID     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C           WMSTX     PARM           ZAMSTX132        Messge text
      *
     C***********          ROLBK                                          129467
      *
      ** Play Mode
      *
     C           PMODE     IFEQ 'P'
      *
      ** Update message severity, id, text.  No other fields should
      ** be updated in Test-harness-file
      *
     C                     MOVELWMSEV     SFMSEV
     C                     MOVELZAMSID    SFMSID
     C                     MOVELZAMSTX    SFMSTX
     C                     UPDATLE2150D0
      *
     C           WMSEV     IFEQ 'E'
     C                     DUMP
     C                     CLOSELE2150PD
     C                     ENDIF
      *
     C                     ELSE
      *
      ** Batch Mode
      *
     C                     MOVELWMSEV     POMSEV
     C                     MOVELWMSID     POMSID
     C                     MOVELWMSTX     POMSTX
      *
     C                     ENDIF
      *
      ** If not a warning error, stop validation and return
      *
     C           WMSEV     IFEQ 'E'
     C                     DUMP
     C                     EXSR SRCLSE
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * SRTEXT  -  Set up text from message file                      *
      * Called by: All subroutines setting of texts                   *
      * Calls    : None                                               *
      *****************************************************************
      *
     C           SRTEXT    BEGSR
      *
     C                     CALL 'Y2RVMGC'
     C                     PARM *BLANKS   PRTCD
     C                     PARM           ZAMSID           Message Id.
     C                     PARM           ZAMSGF           Message file.
     C                     PARM           ZAMSDA           Message data.
     C                     PARM           ZAMSTX           Messge text
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  ZASNMS  -  Sends messages to the program's message queue     *
      *                                                               *
      *  Called by: SRWIND, SREROR                                    *
      *  Calls    :                                                   *
      *  Y2SNMGC -  Send a message                                    *
      *                                                               *
      *****************************************************************
      *
     C           ZASNMS    BEGSR
      *
     C                     CALL 'Y2SNMGC'
     C                     PARM           ZAPGMQ 10        Program queue
     C                     PARM           ZAPGRL  5        Rel queue
     C                     PARM           ZAMSID  7        Message Id.
     C                     PARM           ZAMSGF 10        Message file.
     C                     PARM           ZAMSDA132        Message data.
     C                     PARM           ZAMSTP  7        Message type.
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRCLRS  -  Clears the subfile message queue                  *
      *                                                               *
      *  Called by: SRCLRS, SRSELE, SRDETL, SRISSF                    *
      *  Calls    :                                                   *
      *  Y2CLMSC -  Clear specified program message queue             *
      *                                                               *
      *****************************************************************
      *
     C           SRCLRS    BEGSR
      *
     C                     CALL 'Y2CLMSC'
     C                     PARM SPGM      ZAPGM  10
     C                     PARM '*SAME'   ZAPGRL  5
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  GLZADD  -  Add an amount to the total                        *
      *                                                               *
      *  Called by: SRUPDT, GLZSUB                                    *
      *  Calls    :                                                   *
      *  GLZSUM  -  Carry out the additon for subroutine              *
      *                                                               *
      *****************************************************************
      *
     C           GLZADD    BEGSR                                       ***
      *
     C           ZZAMT     IFNE 0
      *
      ** Split ZZAMT into integer and decimal fields
      *
     C                     Z-ADDZZAMT     ZZAMTI 150       Integer
     C                     MOVE ZZAMT     ZZAMTD  30       Decimal
      *
      ** Both ZZAMTI and ZZAMTD contain a 'sign' zone now
      *
     C                     EXSR GLZSUM
     C                     ENDIF
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  GLZSUB  -  Subroutine to subtract an amount to the total     *
      *                                                               *
      *  Called by: SRUPDT                                            *
      *  Calls    :                                                   *
      *  GLZADD  -  Add an amount to the total                        *
      *                                                               *
      *****************************************************************
      *
     C           GLZSUB    BEGSR
      *
      ** Processing - Just reverse the 'sign' and add
      *
     C                     Z-SUBZZAMT     ZZAMT  153       Reverse 'sign'
     C                     EXSR GLZADD                       and 'add'
     C                     Z-SUBZZAMT     ZZAMT            Restore 'sign'
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  GLZSUM  -  Carry out the additon for subroutine              *
      *                                                               *
      *  Called by: GLZADD                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           GLZSUM    BEGSR
      *
     C                     Z-ADDZZTOTI    ZZTOTI 150       DEFINE ZZTOTI
     C                     Z-ADDZZTOTD    ZZTOTD  30       DEFINE ZZTOTD
     C                     SETOF                     919293
     C                     SETOF                     949599
      *
      ** Determine sign of ZZAMTI & D,  92 if neg
      *
     C           ZZAMTI    COMP 0                      9293
     C   93      ZZAMTD    COMP 0                      9293
     C   93                GOTO ZZSEND                     ZERO BYPASS
      *
      ** Determine sign of ZZTOTI & D, 91 if neg
      *
     C           ZZTOTI    COMP 0                      9193
     C   93      ZZTOTD    COMP 0                      9193
      *
      ** If ZZTOTAL is zero, return ZZAMOUNT
      *
     C   93                Z-ADDZZAMTI    ZZTOTI
     C   93                Z-ADDZZAMTD    ZZTOTD
     C   93                GOTO ZZSEND                     ZERO BYPASS
      *
      ** If signs differ, bypass overflow checks
      *
     C   91N92
     CORN91 92             GOTO ZZOFPS
      *
     C           ZZAMTD    ADD  ZZTOTD    ZZWK2   40
     C           ZZWK2     COMP 999                  93    '93' = CARRY
     C  N93      ZZWK2     COMP -999                   93    INTO INTEGER.
      *
     C   93N92   ZZAMTI    ADD  1         ZZWK3  150       ADD 'CARRY' +VE
     C      93 92ZZAMTI    SUB  1         ZZWK3            SUB 'CARRY' -VE
     C   93      ZZTOTI    ADD  ZZWK3     ZZWK3
     C  N93      ZZTOTI    ADD  ZZAMTI    ZZWK3
      *
      ** If the modulus of ZZWK3 is lt mod. ZZTOTI then O/F has occured
      *
     C  N92      ZZWK3     COMP ZZTOTI                 99  -92 MEANS NOS.
     C   92      ZZWK3     COMP ZZTOTI               99     NEGATIVE
     C  N99                Z-ADDZZWK2     ZZTOTD
     C  N99                Z-ADDZZWK3     ZZTOTI
      *
      ** If O/F zeroise ZZAMT, Ind '99' set and ZZTOT fields left intact
      *
     C   99                Z-ADD0         ZZAMT  153
     C                     GOTO ZZSEND
      *
      ** The 'signs' are different
      *
     C           ZZOFPS    TAG                             ***  ZZOFPS ***
      *
      * If ZZAMT was negative, make it pos. to como with ZZTOT
      *
     C   92                Z-SUBZZAMTI    ZZAMTI 150
     C   92                Z-SUBZZAMTD    ZZAMTD  30
      *
      ** If ZZTOT was negative, make it pos. to comp with ZZAMT.
      *
     C   91                Z-SUBZZTOTI    ZZTOTI
     C   91                Z-SUBZZTOTD    ZZTOTD
      *
      ** Both ZZAMT and ZZTOT are now positive
      *
     C           ZZTOTI    COMP ZZAMTI               93  95
     C   95      ZZTOTD    COMP ZZAMTD               93  95
      *
      ** If ZZTOT eq. ZZAMT return ZERO.
      *
     C   95                Z-ADD0         ZZTOTI
     C   95                Z-ADD0         ZZTOTD
     C   95                GOTO ZZSEND
      *
      ** If ZZTOT gt. ZZAMT.
      *
     C   93      ZZAMTD    COMP ZZTOTD               94
     C   93 94   ZZTOTI    SUB  1         ZZTOTI
     C   93 94   ZZTOTD    ADD  1000      ZZWK2
     C   93      ZZTOTI    SUB  ZZAMTI    ZZTOTI
     C   93 94   ZZWK2     SUB  ZZAMTD    ZZTOTD
     C   93N94   ZZTOTD    SUB  ZZAMTD    ZZTOTD
      *
      ** If ZZAMT gt. ZZTOT.
      *
     C  N93      ZZTOTD    COMP ZZAMTD               94
     C  N93 94   ZZAMTI    SUB  1         ZZWK3  150
     C  N93 94   ZZAMTD    ADD  1000      ZZWK2
     C  N93 94   ZZWK3     SUB  ZZTOTI    ZZTOTI
     C  N93N94   ZZAMTI    SUB  ZZTOTI    ZZTOTI
     C  N93 94   ZZWK2     SUB  ZZTOTD    ZZTOTD
     C  N93N94   ZZAMTD    SUB  ZZTOTD    ZZTOTD
      *
      ** Reverse sign of ZZTOT if larger I/P fields were negative
      *
     C                     SETOF                     94
     C   93 91
     CORN93 92             SETON                     94
     C   94                Z-SUBZZTOTI    ZZTOTI
     C   94                Z-SUBZZTOTD    ZZTOTD
      *
      ** Restore sign of ZZAMTI & ZZAMTD if it was reversed
      *
     C   92                Z-SUBZZAMTI    ZZAMTI
     C   92                Z-SUBZZAMTD    ZZAMTD
     C           ZZSEND    TAG
      *
      **   If ZZTOTD is zero, and ZZTOTI is neg. set up ZZNEGD.
      *
     C                     SETOF                     96
     C           ZZTOTD    COMP 0                        91
     C   91      ZZTOTI    COMP 0                      96
     C   96                MOVE '.000-'   ZZNEGD  5
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR   -  Error handling subroutine                         *
      *                                                               *
      *            Called automatically if a program error occurs,    *
      *            or directly by the program code using EXSR.        *
      *            This subroutine DUMPs the program just once.       *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                                       **
      *
     C           PMODE     IFEQ 'I'
     C           PMODE     OREQ 'R'
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     ENDIF
     C                     CALL 'DBERRCTL'
      *
     C                     ELSE
      *
     C                     ROLBK
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
     C/COPY ZSRSRC,ZDATE1Z2
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZALIGNZ2
     C/COPY ZSRSRC,ZEDITZ2
     C/COPY ZSRSRC,ZACCH
     C/COPY ZSRSRC,ZCHKH
     C/COPY ZSRSRC,ZFWDT                                                  CLE009
      /EJECT
** STAMP - Dummy timestamp                                                                    CPK015
0001-01-01-00.00.00.000000                                                                    CPK015
** CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
