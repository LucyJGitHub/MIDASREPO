     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Syndicated loans exception report')           *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE3120 - Syndicated Loans Exception Report                   *
      *                                                               *
      *  Function:  This program will report all automatically        *
      *  (I/C and   generated participations sold which have          *
      *    COB)     subsequently been amended manually and for which  *
      *             further amendments to loan details cannot be      *
      *             applied automatically.                            *
      *                                                               *
      *  Called By: LEC3120 - Syndicated Loans Exception Report - Ctl *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CPK028             Date 12Jun07               *
      *                 246192A            Date 19Apr07               *
      *                 246192             Date 29Jan07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006             Date 15May01               *
      *                 CLE011             Date 07Nov00               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CRT004             Date 04May00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 176443             Date 18Apr00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 122461             Date 11Feb99               *
      *                 121783             Date 20AUG97               *
      *                 CLE004 *Create     Date 02Dec96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference                                *
      *  CPK028 - Customer Number fields should be alpha.             *
      *  246192A - Check the loan details from FCLTYFM to verify      *
      *           if loan is a syndicated one. (additional validation)*
      *  246192 - Select all syndicated loans that are unlinked from  *
      *           a syndicate.                                        *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD006 - Use long DS with SDCURRPD.                          *
      *  CLE011 - Rate Fixing Days for Lending Rollover Processing    *
      *           Recompile                                           *
      *  CRT004 - Cashier Midas TCP/IP interface.                     *
      *             Recompiled over SDBRCHPD Changes                  *
      *  176443 - Avoid MCH3601 to be sent.                           *
      *           Pass correct parameter to AOCUSTR0.                 *
      *  122461 - Report should include records with 'manual/system   *
      *           generated' set to M, and 'linked to syndicate' set  *
      *           to Y.                                               *
      *  121783 - Call new access object AOBRCHR1 instead of AOBRCHR0 *
      *           (uses the long data structure DSSDY)                *
      *  CLE004 - Customer Lending Enhancements - Syndication         *
      *                                                               *
      *****************************************************************
     FCLOANLB IF  E           K        DISK         KINFSR *PSSR
     FFCLTY   IF  E           K        DISK         KINFSR *PSSR                             246192A
     F            FCLTYHHF                          KIGNORE                                  246192A
     F            FCLTYFMF                          KRENAMEFCLTYFM                           246192A
     F            FCLTYZZF                          KIGNORE                                  246192A
     F            FCLTYFNF                          KIGNORE                                  246192A
     FLE3120AUO   E                    PRINTER                        UC
     F                                              KINFDS SPOOLU
     FLE3120P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   37  - Multibranching ON                                     *
      *                                                               *
      *                                                               *
      *   89  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRINIT - Program Initialisation                              *
      *  SRPROC - Detail Processing                                   *
      *  SRBRCH - Process Change of Branch.                           *
      *  SRLOAN - Process Report Lines                                *
      *                                                               *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *  SRAUDT - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMYY/MMDDYY)  *
      *  ZFRPED - Edit an Amount                                      *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZFRPEDZ1
      /SPACE 3
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      ***  File Information Data Structure for LE3120P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ***  File Information Data Structure for LE3120AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      ***  Dummy Data Structures used by Access Programs.
      *
     ISDBANK    E DSSDBANKPD
      ***  External data structures for Bank Details
      *
     ISDBRCH    E DSSDBRCHPD
      ***  External data structures for Branch Details
      *
     ISDCUST    E DSSDCUSTPD
     I              QQDFAC                          QQDFA1                                    CGL029
      ***  External data structures for Customer Details
      *
     ISDCURR    E DSSDCURRPD
      ***  External data structures for Currency Details
      *
     IDSFDY     E DSDSFDY
      ** Short data structure for access objects
      *
     IDSSDY     E DSDSSDY                                                 121783
      ** Long data structure for access objects                           121783
      *                                                                   121783
     I/COPY ZSRSRC,ZFRPEDZ2
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR SRINIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR SRPROC
      *
      ***  Output Audit Report and End Program.
      *
     C                     EXSR SRAUDT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT - Program Initialisation                              *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  AOBANKR0 Access Program for Bank Details                     *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR                           *** SRINIT ***
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PENTY   3
      *
      ***  Initialise LDA field.
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'LE3120'  DBPGM
     C                     OUT  LDA
      *
      ***  Call Access Program for Bank Details - Title, Run Date and
      ***  Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Handle Database Error.
      *
     C           PRTCD     IFNE *BLANKS
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     Z-ADD001       DBASE            * DB ERROR 01 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF
      *
      ***  Check System Date Format, DDMMYY (*IN98 off)
      ***                            MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      **   Access RUNDAT for multibranching indicator.
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
     C           AGMBIN    IFEQ 'Y'
     C                     SETON                     37
     C                     END
      *
      ***  Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRPROC
      *****************************************************************
      *                                                               *
      *  SRPROC - Detail Processing                                   *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  SRBRCH - Process Change of Branch.                           *
      *  SRLOAN - Process Report Lines                                *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *****************************************************************
      *
     C           SRPROC    BEGSR                           *** SRPROC ***
      *
      ** Check if only a single branch to be process
      *
     C           PENTY     IFNE 'ALL'
     C           PENTY     ANDNE'   '
     C           PENTY     SETLLCLOANLB
     C                     ENDIF
      *
      ***  Read first record from File.
      *
     C                     READ CLOANLB                  89
      *
      ***  Do While not End of File.
      *
     C           *IN89     DOWEQ*OFF
     C           PENTY     ANDNE'ALL'
     C           PENTY     ANDNE'   '
     C           PENTY     ANDEQBRCA
     C           *IN89     OREQ *OFF
     C           PENTY     ANDEQ'ALL'
     C           *IN89     OREQ *OFF
     C           PENTY     ANDEQ'   '
      *
      ***  Do Branch processing.
      *
     C                     EXSR SRBRCH
      *
      ***  Do While same Branch and not End of File.
      *
     C           A8BRCD    DOWEQBRCA
     C           *IN89     ANDEQ*OFF
      *
      * Locate the Customer                                                                  246192A
      *                                                                                      246192A
     C           KFCLTY    KLIST                                                             246192A
     C**********           KFLD           KCNUM   60                                  246192A CPK028
     C                     KFLD           KCNUM   6                                           CPK028
     C                     KFLD           KFACT   30                                         246192A
     C                     KFLD           KFCNO   20                                         246192A
      *                                                                                      246192A
     C**********           Z-ADDFCUS      KCNUM                                       246192A CPK028
     C                     MOVE FCUS      KCNUM                                               CPK028
     C                     Z-ADDFTYP      KFACT                                              246192A
     C                     Z-ADDFSEQ      KFCNO                                              246192A
      *                                                                                      246192A
     C           KFCLTY    CHAINFCLTYFM              88                                      246192A
     C           *IN88     IFEQ '0'                                                          246192A
     C           SYIN      ANDEQ'Y'                                                          246192A
      *                                                                                      246192A
     C***********LNKS      IFEQ 'N'                        Lnk to syndicte122461
     C***********MNSG      ANDEQ'S'                        Mnl/Sys Gen    122461
     C***********PCRF      ANDNE*BLANKS                    PC Ref         122461
     C********** LNKS      IFEQ 'Y'                                                    122461 246192
     C********** MNSG      ANDEQ'M'                                                    122461 246192
     C           LNKS      IFEQ 'N'                                                           246192
     C           LPFI      ANDNE*BLANKS                                                       246192
     C********** LNKS      OREQ 'N'                                                   246192 246192A
     C           LPFI      ANDNE'A'                                                           246192
      *
      ***  Count records read.
      *
     C                     ADD  1         RCOUNT
      *
      ***  Process Report Lines.
      *
     C                     EXSR SRLOAN
      *
     C                     ENDIF
      *
     C                     ENDIF                                                             246192A
      ***  Read next record.
      *
     C                     READ CLOANLB                  89
      *
      ***  End of Do While same Branch and not End of File.
      *
     C                     ENDDO
      *
      ***  Write final records and Totals to Report.
      *
     C                     EXSR WP1END
      *
      ***  Close P1 Printer File.
      *
     C                     CLOSELE3120P1
      *
      ***  End of Do While not End of File.
      *
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRBRCH
      *****************************************************************
      *                                                               *
      *  SRBRCH - Process Change of Branch.                           *
      *                                                               *
      *  Called By: SRPROC                                            *
      *  Calls:                                                       *
      *  RCFP1  - RCF Processing for P1 Report                        *
      ***AOBRCHR0*Access*Program*for*Branch*Details********************   121783
      *  AOBRCHR1 Access Program for Branch Details                   *   121783
      *****************************************************************
      *
     C           SRBRCH    BEGSR                           *** SRBRCH ***
      *
     C                     OPEN LE3120P1
      *
      ***  RCF Processing for Printer File.
      *
     C                     EXSR RCFP1
      *
      ***  Call Access Program for Branch Details.
      *
     C***********          CALL 'AOBRCHR0'                                121783
     C                     CALL 'AOBRCHR1'                                121783
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM BRCA      PBRCD   3
     C           SDBRCH    PARM SDBRCH    DSSDY                           121783
     C***********SDBRCH    PARM SDBRCH    DSFDY                           121783
      *
      ***  Handle Database Error.
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBRCHPD'DBFILE           ***************
     C                     MOVELBRCA      DBKEY            * DB ERROR 02 *
     C                     Z-ADD002       DBASE            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Write out the record.
      *
     C                     WRITEHEADP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRLOAN
      *****************************************************************
      *                                                               *
      *  SRLOAN - Process Report Lines                                *
      *                                                               *
      *  Called By: SRPROC                                            *
      *  Calls:                                                       *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  AOCUSTR0 Access Program for Customer Details                 *
      *                                                               *
      *****************************************************************
      *
     C           SRLOAN    BEGSR                           *** SRLOAN ***
      *
     C                     MOVELCNUM      PKEY1  10
      *
      ***  Call Access Program for Customer Details.
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM           PKEY1
     C                     PARM           PKYST   7
     C***********SDCUST    PARM SDCUST    DSFDY                           176443
     C           SDCUST    PARM SDCUST    DSSDY                           176443
      *
      ***  Handle Database Error.
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDCUSTPD'DBFILE           ***************
     C                     MOVELCNUM      DBKEY            * DB ERROR 03 *
     C                     Z-ADD003       DBASE            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Write out the record.
      *
     C                     EXSR WP1REC
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1REC
      *****************************************************************
      *                                                               *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *                                                               *
      *  Called By: SRLOAN                                            *
      *  Calls:                                                       *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMYY/MMDDYY)  *
      *  ZFRPED - Edit an Amount                                      *
      *  AOCURRR0 Access Program for Currency Details                 *
      *                                                               *
      *****************************************************************
      *
     C           WP1REC    BEGSR                           *** WP1REC ***
      *
      *----------------------------------------------------------------
      *
      *    Perform any formatting before printing via ZDATE2, ZSEDIT
      *
      *----------------------------------------------------------------
      *
      ***  Format Start Date to DDMMMYY.
      *
     C                     Z-ADDVDAT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    RVDAT
      *
      ***  Format Maturity Date to DDMMMYY.
      *
     C                     Z-ADDMDAT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    RMDAT
      *
      ***  Call Access Program for Currency Details.
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM CCY       PK101   3
     C********** SDCURR    PARM SDCURR    DSFDY                                 CSD006
     C           SDCURR    PARM SDCURR    DSSDY                                 CSD006
      *
      ***  Handle Database Error.
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDCURRPD'DBFILE           ***************
     C                     MOVELCCY       DBKEY            * DB ERROR 04 *
     C                     Z-ADD004       DBASE            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ***  Format Principal Amount.
      *
     C                     MOVEL*BLANKS   RCPAM
     C                     Z-ADDCPAM      ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT25    RCPAM
      *
      ***  Move File Fields to Report Fields.
      *
     C**********           Z-ADDCNUM      RCNUM                                               CSD027
     C                     MOVE CNUM      RCNUM                                               CSD027
     C                     MOVELBBCRNM    RBCRNM
     C**********           Z-ADDLNRF      RLNRF                                               CLE148
     C                     MOVELLNRF      RLNRF                                               CLE148
     C                     MOVELLTYP      RLTYP
     C                     MOVELSUTP      RSUTP
     C                     MOVELCLAS      RCLAS                                               CLE042
     C                     MOVELCCY       RCCY
     C                     Z-ADDINTR      RINTR
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Detail Record.
      *
     C                     WRITEDETAIL
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *  Called By: SRPROC                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           WP1END    BEGSR                           *** WP1END ***
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD3         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Total Format.
      *
     C                     WRITETRAILP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRAUDT
      *****************************************************************
      *                                                               *
      *  SRAUDT - Produce Audit Report and End Program                *
      *                                                               *
      *  Called By: Main Processing, SRPROC, *PSSR                    *
      *  Calls:                                                       *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *****************************************************************
      *
     C           SRAUDT    BEGSR                           *** SRAUDT ***
      *
     C                     OPEN LE3120AU
      *
      ***  RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ***  Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ELSE
      *
      ***  Else, Output Number of Records Processed.
      *
     C                     WRITECONTROL
      *
     C                     ENDIF
     C                     ENDIF
      *
     C                     CLOSELE3120AU
      *
      ***  End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR SRAUDT
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/ZDATE2
     C/COPY ZSRSRC,ZDATE2Z2
      /TITLE SR/ZFRPED
     C/COPY ZSRSRC,ZFRPEDZ3
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
