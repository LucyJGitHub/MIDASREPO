     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Manual Facility Utilisation Audit Report')
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LE3160 - Manual Facility Utilisation Audit Report            *
      *                                                               *
      *  Function:  This program provides a full list on-request      *
      *  (I/C and   (on I/C) of all manual facility utilisations      *
      *   COB)      entered in the system and automatically (on COB)  *
      *             print all utilisations that are inserted,         *
      *             amended or deleted today.                         *
      *                                                               *
      *  Called By: LEC3161 - Manual Facility Util. (Mandatory)       *
      *             LEC3160 - Manual Facility Util. (On-request)      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. AR674226           Date 04Dec19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 198714             Date 13Feb02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CLE009             Date 11May99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 147935             Date 03Dec98               *
      *                 CLE005 *Create     Date 22May97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR674226 - Manual transaction reached 999. Increase the size *
      *             of utilisation sequence no. and transaction       *
      *             sequence. Recompile. (Child: AR674227)            *
      *           - Applied for MD054782.                             *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  198714 - Printer overflow problem encountered when there is  *
      *           more than 15 Facility Utilizations to be reported.  *
      *  CLE009 - LE I/C Swift Message Generation for Fees (Recompile)*
      *  147935 - Addition of insert, authorise and amend user        *
      *           profiles to lending reports                         *
      *  CLE005 - Customer Lending Enhancements - Others (T3)         *
      *                                                               *
      *****************************************************************
     FLEMNFUL0IF  E           K        DISK
     FLE3160AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
     FLE3160P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *  10 - Last record to be printed on the page.                  *                       198714
      *  11 - Indicator for printing the Sub-Heading.                 *                       198714
      *   37  - Multibranching ON                                     *
      *   40  - Deleted utilisation                                   *
      *   89  - End of File                                           *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  INIT   - Program Initialisation                              *
      *  PROCES - Detail Processing                                   *
      *  REPORT - Process Report Lines                                *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  WP1END - Write End of Report                                 *
      *  INRFLD - Reinitilise report fields                           *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMYY/MMDDYY)  *
      *  ZFRPED - Format an Amount                                    *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZFRPEDZ1
     E/COPY ZSRSRC,ZEDITZ1
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      ** File Information Data Structure for LE3160P1.
      *
     I                                        9   9 OPEN1
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
     ISPOOLU      DS
      ** File Information Data Structure for LE3160AU.
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     ISDBANK    E DSSDBANKPD
      ***  External data structures for Bank Details
      *
     ISDCURR    E DSSDCURRPD
      ** Currency Details
      *
     ISDBRCH    E DSSDBRCHPD
      ** Branch Details
      *
     IDSFDY     E DSDSFDY
      ** Short data structure for access objects
      *
     IDSSDY     E DSDSSDY
      ** Long data structure for access objects
      *
     I/COPY ZSRSRC,ZFRPEDZ2
     C/TITLE Main Processing
      *
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ** Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ** Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      ** Output Audit Report and End Program.
      *
     C                     EXSR AUDIT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  AOBANKR0 Access Program                                      *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR                           ***  INIT  ***
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQN   5
     C                     PARM           PRENT   3
     C                     PARM           PPHAS   1
      *
      ** Access RUNDAT for multibranching indicator.
      *
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
     C           AGMBIN    IFEQ 'Y'
     C                     SETON                     37
     C                     END
      *
      ** Initialise LDA field.
      *
     C           *LOCK     IN   LDA
     C                     MOVEL'LE3160'  DBPGM
     C                     OUT  LDA
      *
      ** Call Access Program for Bank Details - Title, Run Date and
      ** Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     Z-ADD001       DBASE            * DB ERROR 01 *
     C                     OUT  LDA                        ***************
     C                     EXSR *PSSR
     C                     ENDIF
     C                     MOVELBJRDNB    WRUND   50
      *
      ** Check System Date Format, DDMMYY (*IN98 off)
      **                           MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     SETON                     98
     C                     ENDIF
      *
      ** RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ** Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
     C                     MOVE '0'       *IN11                                               198714
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  REPORT - Process Report Lines                                *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR                           *** PROCES ***
      *
      ** Read first record from File.
      *
     C           *LOVAL    SETLLLEMNFUL0
     C                     READ LEMNFUL0                 89
      *
      ** Process Report lines while not EOF.
      *
     C           *IN89     DOWEQ*OFF
      *
     C           PPHAS     IFEQ 'I'
     C           PRENT     ANDNE'ALL'
     C           PRENT     ANDEQBRCA
     C           PPHAS     OREQ 'I'
     C           PRENT     ANDEQ'ALL'
     C           PPHAS     OREQ 'I'
     C           PRENT     ANDEQ*BLANKS
     C           PPHAS     OREQ 'C'
     C           TLCD      ANDEQWRUND
     C                     ADD  1         RCOUNT
     C                     EXSR REPORT
     C                     ENDIF
      *
      ** Read next record.
      *
     C                     READ LEMNFUL0                 89
      *
     C           PPHAS     IFEQ 'I'
     C           BRCA      ANDNEWPBRC
     C           PRENT     ANDNE'ALL'
     C           PRENT     ANDNE*BLANKS
     C           RCOUNT    ANDGE1
     C                     MOVE *ON       *IN89
     C                     ENDIF
      *
     C                     ENDDO
      *
      ** Write Totals to Report.
      *
     C           OPEN1     IFEQ '1'
     C                     EXSR WP1END
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/REPORT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  WP1END - Write End of Report                                 *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  AOBRCHR0 Access Program                                      *
      *  AOCURRR0 Access Program                                      *
      *                                                               *
      *****************************************************************
      *
     C           REPORT    BEGSR                           *** REPORT ***
      *
      ** Open and register to RCF.
      *
      ** (1) Not multi-branching.
      *
     C           AGMBIN    IFEQ 'N'
     C           OPEN1     ANDNE'1'
     C                     OPEN LE3160P1
     C                     EXSR RCFP1
     C                     ENDIF
      *
      ** (2) Multi-branching.
      *
     C           AGMBIN    IFEQ 'Y'
     C           BRCA      ANDNEWPBRC
     C                     MOVE BRCA      WPBRC   3
      *
     C           OPEN1     IFEQ '1'
     C                     EXSR WP1END
     C                     CLOSELE3160P1
     C                     ENDIF
      *
     C                     OPEN LE3160P1
     C                     EXSR RCFP1
      *
      ** Get the branch description.
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   WRTCD
     C                     PARM '*KEY   ' WOPTN
     C           RBRCA     PARM BRCA      WBRCA   3
     C           SDBRCH    PARM SDBRCH    DSSDY
      *
      ** Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBRCHPD'DBFILE           ***************
     C                     Z-ADD002       DBASE            * DB ERROR 02 *
     C                     MOVELBRCA      DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ** Get the utilisation currency's number of decimal places.
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*BLANK  'WRTCD
     C                     PARM '*KEY    'WOPTN
     C                     PARM UCCY      WCURR   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
      ** Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCURRPD'DBFILE           ***************
     C                     Z-ADD003       DBASE            * DB ERROR 03 *
     C                     MOVELUCCY      DBKEY            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Write out the record.
      *
     C                     EXSR WP1REC
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1REC
      *****************************************************************
      *                                                               *
      *  WP1REC - Subroutine to Write a Detail record to the Report.  *
      *                                                               *
      *  Called By: REPORT                                            *
      *  Calls:                                                       *
      *  INRFLD - Reinitilise report fields                           *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMYY/MMDDYY)  *
      *  ZFRPED - Format an Amount                                    *
      *                                                               *
      *****************************************************************
      *
     C           WP1REC    BEGSR                           *** WP1REC ***
      *
      ** Reinitialise Report fields.
      *
     C                     EXSR INRFLD
      *
     C                     MOVE CNUM      RCNUM
     C                     MOVE FACT      RFACT
     C                     MOVE FCNO      RFCNO
     C                     MOVE UCCY      RUCCY
     C                     MOVELUNAR      RUNAR
     C                     MOVELIUSR      RIUSR                           147935
     C                     MOVELAUSR      RAUSR                           147935
     C                     MOVELXUSR      RXUSR                           147935
      *
     C           RECI      IFEQ 'R'
     C                     MOVE *ON       *IN40
     C                     ELSE
     C                     MOVE *OFF      *IN40
     C                     ENDIF
      *
      ** Perform formatting for amount, rate and date fields.
      *
      ** (1) Amount
      *
     C                     MOVE *BLANKS   ZFLD15
     C                     Z-ADDUAMT      ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT1     RUAMT
      *
      ** (2) Exchange rate
      *
     C           UXRT      IFEQ 0
     C                     MOVE *BLANKS   RUXRT
     C                     ELSE
     C                     Z-ADD7         ZADEC
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE UXRT      ZFIELD
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    RUXRT
     C                     ENDIF
      *
      ** (3) Start date
      *
     C                     Z-ADDSTDT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RSTDT
      *
      ** (4) Expiry date
      *
     C           EXDT      IFEQ 0
     C                     MOVE *BLANKS   REXDT
     C                     ELSE
     C                     MOVE EXDT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    REXDT
     C                     ENDIF
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
      *
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C           *IN10     OREQ '1'                                                           198714
     C                     WRITEHEADP1
     C                     ENDIF
     C                     MOVE '0'       *IN10                                               198714
     C                     Z-ADD3         RQDLN1                                              198714
     C           OFLLN1    SUB  PRTLN1    DIFLN1                                              198714
     C           DIFLN1    IFLE RQDLN1                                                        198714
     C                     MOVE '1'       *IN10                                               198714
     C                     ENDIF                                                              198714
      *
      ** Write out Detail Record.
      *
     C                     WRITEDETAIL
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/INRFLD
      *****************************************************************
      *                                                               *
      *  INRFLD - Subroutine to reinitialise report fields to blanks. *
      *                                                               *
      *  Called By: WP1REC                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           INRFLD    BEGSR                           *** INRFLD ***
      *
     C                     MOVE *BLANKS   RCNUM
     C                     MOVE *BLANKS   RFACT
     C                     MOVE *BLANKS   RFCNO
     C                     MOVE *BLANKS   RUCCY
     C                     MOVE *BLANKS   RUNAR
     C                     MOVE *BLANKS   RUAMT
     C                     MOVE *BLANKS   RUXRT
     C                     MOVE *BLANKS   RSTDT
     C                     MOVE *BLANKS   REXDT
     C                     MOVE *BLANKS   RIUSR                           147935
     C                     MOVE *BLANKS   RAUSR                           147935
     C                     MOVE *BLANKS   RXUSR                           147935
      *
     C                     ENDSR
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: PROCES, REPORT                                    *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           WP1END    BEGSR                           *** WP1END ***
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     MOVE '1'       *IN11                                               198714
     C                     WRITEHEADP1
     C                     MOVE '0'       *IN10                                               198714
     C                     MOVE '0'       *IN11                                               198714
     C                     ENDIF
      *
      ** Write out Total Format.
      *
     C                     WRITETRAILP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, *PSSR                            *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ** Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
     C                     WRITECONTROL
      *
      ** If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ** Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ENDIF
     C                     ENDIF
      *
      ** End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ** Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C           AGMBIN    IFEQ 'Y'
     C                     MOVE BRCA      SENTY
     C                     ELSE
     C                     MOVE *BLANKS   SENTY
     C                     ENDIF
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQN     SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ** Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQN     SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/ZDATE2
     C/COPY ZSRSRC,ZDATE2Z2
      /TITLE SR/ZFRPED
     C/COPY ZSRSRC,ZFRPEDZ3
      /TITLE SR/ZEDIT
     C/COPY ZSRSRC,ZEDITZ2
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
