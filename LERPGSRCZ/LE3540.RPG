     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Generate selected facilities')
      *****************************************************************
      *
      *  Midas   -  Lending Module
      *
      *  LE3540  -  LE Generate Selected Facilities
      *
      *  Function:  This program outputs details of all the facilities
      *             to be converted for a specific selection criterion.
      *
      *  (c) Finastra International Limited 2001                      *
      *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 190627             Date 3MAy01                *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU021  *CREATE    Date 30Apr98               *
      *
      *-------------------------------------------------------------------
      *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  190627 - Recompiled due to fields added to FCLTY1 by 189544. *
      *  CEU021 - EMU LE Facility Currency Conversion
      *
      *****************************************************************
      *  Indicator    Description
      *  ¯¯¯¯¯¯¯¯¯    ¯¯¯¯¯¯¯¯¯¯¯
      *  01 - 19      Input Control.
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)
      *   20          Record Not Found
      *   21          File Error Occurred
      *   22          Start/End Of File Reached
      *   25          Lookup/Scan.
      *  30 - 39      Program Work Indicators
      *  40 - 49      Output Control.
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)
      *  90 - 99      Error Control.
      *
      *-------------------------------------------------------------------
      *  Routine      Description
      *  ¯¯¯¯¯¯¯      ¯¯¯¯¯¯¯¯¯¯¯
      *  PFCLT        Process Facilities Selected for Insertion
      *  XSFCUP       Set Fields for Output to LEFCUPPD
      *  XWFCUP       Write LEFCUPPD Record
      *  *INZSR       Initial Routine
      *  *PSSR        Program Error Routine
      *  @DEFN        Definition Routine (Not Called)
      *
      *-------------------------------------------------------------------
      *  File         Description
      *  ¯¯¯¯         ¯¯¯¯¯¯¯¯¯¯¯
      *  FCLTY1       Midas LE Facilities.
      *  LEFCONL0     Midas LE Fac. Currency Conv by Rererence.
      *  LEFCUPL1     Midas LE Fac. Selected for Conv by Ref/Fac.
      *  LEFCUPPD     Midas LE Fac. Selected for Conversion.
      *
      *===================================================================
     F/EJECT
      *===================================================================
     FFCLTY1  IP  E           K        DISK         KINFSR *PSSR
      *
     FLEFCONL0IF  E           K        DISK         KINFSR *PSSR
     FLEFCUPL1IF  E           K        DISK         KINFSR *PSSR
     FLEFCUPPDO   E                    DISK         KINFSR *PSSR
     F            LEFCUPD0                          KRENAMELEFCUPD1
      *===================================================================
     E/EJECT
      *===================================================================
     E                    CPY@    1   1 80               Object Copyright
      *===================================================================
     I/EJECT
      *===================================================================
     IPSDS       SDS
     I                                        1  10 D@PGMN
      * Program Name
      *-------------------------------------------------------------------
     ILDA       E DSLDA                         256
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *        Defines:                     142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *-------------------------------------------------------------------
     ISDGELR    E DSSDGELRPD
      * General Ledger Details
      *-------------------------------------------------------------------
     IDSFDY     E DSDSFDY
      * First DS for Access Programs, short data structure.
      *-------------------------------------------------------------------
     IDSSDY     E DSDSSDY
      * Second DS for Access Programs, long data structure.
      *-------------------------------------------------------------------
     I              'Duplicate'           C         N@DUPL
      *===================================================================
      /EJECT
      *===================================================================
      * Main Processing Control.
      *
      *-------------------------------------------------------------------
      * Process Selected Facilities.
      *
     C                     EXSR PFCLT                      Proc Record
      *===================================================================
      /EJECT
     C           PFCLT     BEGSR
      *===================================================================
      * PFCLT: Process Facilities Selected for Insertion.
      *
      *-------------------------------------------------------------------
      * Process Facilities Selected for Insertion.
      *
     C                     EXSR XSFCUP                     Set Data Items
      *
     C                     EXSR XWFCUP                     Wrt Record
      *===================================================================
     C                     ENDSR
      /EJECT
     C           XSFCUP    BEGSR
      *===================================================================
      * XSFCUP: Set Fields For Output To LEFCUPPD.
      *
      *-------------------------------------------------------------------
      * Set Data Items.
      *
     C                     MOVE P@REFR    FSREFR           Set Reference
     C**********           Z-ADDCNUM      FSCNUM           Set Customer                       CSD027
     C                     MOVE CNUM      FSCNUM           Set Customer                       CSD027
     C                     Z-ADDFACT      FSFACT           Set Facility
     C                     Z-ADDFCNO      FSFCNO           Set Sequence
     C                     MOVE BRCA      FSBRCA           Set Branch
     C                     MOVE FCCY      FSOCCY           Set Org Ccy
     C                     Z-ADDFAMT      FSOAMT           Set Org Amt
     C                     MOVE FCGEAK    FSGEAK           Set Gen a/c
     C                     Z-ADDFCGADT    FSGADT           Set Day No
     C                     MOVEL'OK'      FSSTAT    P      Set Status
     C                     MOVE *BLANKS   FSADVP           Clr Adv Prt
      *-------------------------------------------------------------------
      * Check If Facility Exists/Duplicate.
      *
     C           FCCY      IFEQ BKEURO                     If Exists
     C                     MOVEL'Exists'  FSSTAT    P       Set Status
      *
     C                     ELSE                            Else
     C**********           Z-ADDCNUM      KCNUM             Set Customer                      CSD027
     C                     MOVE CNUM      KCNUM             Set Customer                      CSD027
     C                     Z-ADDFACT      KFACT             Set Facility
     C                     Z-ADDFCNO      KFCNO             Set Sequence
     C           KFCUP     SETLLLEFCUPL1               2122 Postn File
      *
     C           *IN21     IFEQ *ON                         If File Err
     C           *LOCK     IN   LDA                          Rtv LDA
     C                     MOVEL'LEFCUPL1'DBFILE    P        Set File Nam
     C                     MOVELFSCNUM    DBKEY     P        Set Key
     C                     MOVELD@PGMN    DBPGM     P        Set Pgm
     C                     Z-ADD030       DBASE              Set Number
     C                     OUT  LDA                          Wrt Record
     C                     EXSR *PSSR                        Proc Error
     C                     ENDIF                            End If
      *
     C           *IN22     IFEQ *ON                         If Rec Found
     C                     MOVELN@DUPL    FSSTAT    P        Set Status
     C                     ENDIF                            End If
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
      /EJECT
     C           XWFCUP    BEGSR
      *===================================================================
      * XWFCUP: Write LEFCUPPD Record.
      *
      *-------------------------------------------------------------------
      * Write LEFCUPPD Record.
      *
     C                     WRITELEFCUPD1               21  Wrt Record
      *
     C           *IN21     IFEQ *ON                        If File Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVEL'LEFCUPPD'DBFILE    P       Set File Name
     C                     MOVELFSREFR    DBKEY     P       Set Key
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     Z-ADD010       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
      /EJECT
     C           *INZSR    BEGSR
      *===================================================================
      * *INZSR: Initialization Routine.
      *
      *-------------------------------------------------------------------
      * Clear & Populate Local dataarea with program name.
      *
     C           *LOCK     IN   LDA                        Rtv LDA
     C                     CLEARLDA                        Clr LDA
     C                     MOVELD@PGMN    DBPGM     P      Set Pgm Name
     C                     OUT  LDA                        Wrt Record
      *-------------------------------------------------------------------
      * Initialise Parameters, Work Fields, Etc.
      *
     C                     MOVEACPY@      CPY2@@ 80        Set Copyright
      *-------------------------------------------------------------------
      * Retrieve Fields Required From Facility Currency Conversion.
      *
     C                     MOVE P@REFR    KREFR            Set Ref Key
     C           KFCON     CHAINLEFCONL0             2021  Rtv Record
      *
     C           *IN21     IFEQ *ON                        If File Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVEL'LEFCONL0'DBFILE    P       Set File N m
     C                     MOVELP@REFR    DBKEY     P       Set Key
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     Z-ADD010       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * Retrieve Euro Currency.
      *
     C**********           CALL 'AOGELRR0'             2121Rtv GL ICD Dtl                     CGL029
     C                     CALL 'AOGELRR1'             2121                                   CGL029
     C                     PARM *BLANKS   P@RTCD  7        -O:Return Code
     C                     PARM '*FIRST ' P@OPTN  7        -I:Option
     C********** SDGELR    PARM *BLANKS   DSFDY            -O:Format                          CGL029
     C           SDGELR    PARM *BLANKS   DSSDY                                               CGL029
      *
     C           *IN21     IFEQ *ON                        If Call Error
     C           P@RTCD    ORNE *BLANKS                    Or Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'SDGELRPD'DBFILE    P       Set File
     C                     MOVE P@OPTN    DBKEY     P       Set Key
     C                     Z-ADD20        DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           *PSSR     BEGSR
      *===================================================================
      * *PSSR: Error Handling.
      *
      *-------------------------------------------------------------------
      * Check To See That *PSSR Has Not Been Called Yet.
      *
     C           WKRUN     IFEQ *BLANK                     If Not Exec
     C                     MOVE 'Y'       WKRUN   1         Set Off Ind
     C                     DUMP                             Dmp Pgm
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * If *PSSR Already Run, Then Just End The Program Here.
      *
     C                     MOVE *ON       *INU7            Set U7 Ind
     C                     MOVE *ON       *INU8            Set U8 Ind
     C                     MOVE *ON       *INLR            Set EndPgm Ind
     C                     RETRN                           Exit Program
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           @DEFN     BEGSR
      *===================================================================
      * @DEFN: Definition Routine (Not Called).
      *
      *-------------------------------------------------------------------
      * Dataareas...
      *
     C           *NAMVAR   DEFN           LDA              Dcl LDA
      *-------------------------------------------------------------------
      * Data Item Definitions...
      *
     C           *LIKE     DEFN BRCA      KBRCA            Dfn Branch
     C           *LIKE     DEFN CNUM      KCNUM            Dfn Customer
     C           *LIKE     DEFN FACT      KFACT            Dfn Facility
     C           *LIKE     DEFN FCNO      KFCNO            Dfn Sequence
     C           *LIKE     DEFN FCREFR    KREFR            Dfn Reference
      *-------------------------------------------------------------------
      * Lists...
      *
     C           *ENTRY    PLIST                           Prg: Entry
     C                     PARM           P@REFR 10        -Reference
      *
     C           KFCON     KLIST                           Key: LEFCONL0
     C                     KFLD           KREFR            -Reference
      *
     C           KFCUP     KLIST                           Key: LEFCUPL0
     C                     KFLD           KCNUM            -Customer No
     C                     KFLD           KFACT            -Fac type
     C                     KFLD           KFCNO            -Fac No
      *===================================================================
     C                     ENDSR
      /EJECT
** CPY@: Object Copyright
(c) Finastra International Limited 2001
