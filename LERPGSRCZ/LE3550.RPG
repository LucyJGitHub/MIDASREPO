     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Delete Selected Facilities')                  *
      *****************************************************************
      *
      *  Midas   -  Lending Module
      *
      *  LE3550  -  LE Delete Selected Facilities
      *
      *  Function:  This program deletes all the facilities selected
      *             for conversion for a specific selection criterea.
      *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *
      *  Last Amend No. CLE134             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU021  *CREATE    Date 28Apr98               *
      *
      *-------------------------------------------------------------------
      *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CEU021 - EMU LE Facility Currency Conversion
      *
      *****************************************************************
      *  Indicator    Description
      *  ¯¯¯¯¯¯¯¯¯    ¯¯¯¯¯¯¯¯¯¯¯
      *  01 - 19      Input Control.
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)
      *   20          Record Not Found
      *   21          File Error Occurred
      *   22          Start/End Of File Reached
      *   25          Lookup/Scan.
      *  30 - 39      Program Work Indicators
      *  40 - 49      Output Control.
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)
      *  90 - 99      Error Control.
      *
      *-------------------------------------------------------------------
      *  Routine      Description
      *  ¯¯¯¯¯¯¯      ¯¯¯¯¯¯¯¯¯¯¯
      *  PFCUP        Process Facilities selected for deletion
      *  XRFCUP       Read Selected Facilities File
      *  XUFCUP       Update Selected Facilities File
      *  XDFCUP       Delete Selected Facilities Record
      *  *INZSR       Initial Routine
      *  *PSSR        Program Error Routine
      *  @DEFN        Definition Routine (Not Called)
      *
      *-------------------------------------------------------------------
      *  File         Description
      *  ¯¯¯¯         ¯¯¯¯¯¯¯¯¯¯¯
      *  LEFCUPL0     Midas LE Fac. Selected for Conv by Facility.
      *  LEFCUPL1     Midas LE Fac. Selected for Conv by Ref/Fac.
      *
      *===================================================================
     F/EJECT
      *===================================================================
     FLEFCUPL0UP  E           K        DISK         KINFSR *PSSR
      *
     FLEFCUPL1UF  E           K        DISK         KINFSR *PSSR
     F            LEFCUPD0                          KRENAMELEFCUPD1
      *===================================================================
     E/EJECT
      *===================================================================
     E                    CPY@    1   1 80               Object Copyright
      *===================================================================
     I/EJECT
      *===================================================================
     IPSDS       SDS
     I                                        1  10 D@PGMN
      * Program Name
      *-------------------------------------------------------------------
     ILDA       E DSLDA                         256
      * Local data area for database error details
      * *LOCK IN LDA immediately before and OUT LDA immediately
      * after each database error handling.
      *
      *                                     134 141 DBFILE
      *        Defines:                     142 170 DBKEY
      *                                     171 180 DBPGM
      *                                     181 1830DBASE
      *-------------------------------------------------------------------
     I              'Duplicate'           C         N@DUPL
      *===================================================================
     C/EJECT
      *===================================================================
      * Main Processing Control.
      *
      *-------------------------------------------------------------------
      * Process Selected Facilities.
      *
     C                     EXSR PFCUP                      Proc Record
      *===================================================================
      /EJECT
     C           PFCUP     BEGSR
      *===================================================================
      * PFCUP: Process Facilities selected for deletion.
      *
      *-------------------------------------------------------------------
      * Process Facilities.
      *
     C                     EXSR XDFCUP                     Dlt Facility
      *
     C**********           Z-ADDFSCNUM    KCNUM            Set Customer                       CSD027
     C                     MOVE FSCNUM    KCNUM            Set Customer                       CSD027
     C                     Z-ADDFSFACT    KFACT            Set Facility
     C                     Z-ADDFSFCNO    KFCNO            Set Sequence
      *
     C                     EXSR XRFCUP                     Rtv Facility
      *
     C           *IN20     IFEQ *OFF                       If Rec found
     C           FSSTAT    ANDEQN@DUPL                     And Duplicate
     C                     MOVEL'OK'      FSSTAT    P       Set status
     C                     EXSR XUFCUP                      Upd Record
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
      /EJECT
     C           XRFCUP    BEGSR
      *===================================================================
      * XRFCUP: Retrieve Selected Facilities File.
      *
      *-------------------------------------------------------------------
      * Retrieve Record.
      *
     C           KLEFCU    SETLLLEFCUPL1                   Postn File
     C           KLEFCU    READELEFCUPL1               2120Rtv Record
      *
     C           *IN21     IFEQ *ON                        If File Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVEL'LEFCUPL1'DBFILE    P       Set File Name
     C                     MOVELFSCNUM    DBKEY     P       Set Key
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     Z-ADD020       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
      /EJECT
     C           XUFCUP    BEGSR
      *===================================================================
      * XUFCUP: Update Selected Facilities File.
      *
      *-------------------------------------------------------------------
      * Update Facility Record.
      *
     C                     UPDATLEFCUPD1               21  Upd Record
      *
     C           *IN21     IFEQ *ON                        If File Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVEL'LEFCUPL1'DBFILE    P       Set File Name
     C                     MOVELFSREFR    DBKEY     P       Set Key
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     Z-ADD030       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
      /EJECT
     C           XDFCUP    BEGSR
      *===================================================================
      * XDFCUP: Delete Facilities selected for deletion.
      *
      *-------------------------------------------------------------------
      * Delete Record.
      *
     C                     DELETLEFCUPL0               21  Dlt Record
      *
     C           *IN21     IFEQ *ON                        If File Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVEL'LEFCUPL0'DBFILE    P       Set File Name
     C                     MOVELFSREFR    DBKEY     P       Set Key
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     Z-ADD010       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
      /EJECT
     C           *INZSR    BEGSR
      *===================================================================
      * *INZSR: Initialization Routine.
      *
      *-------------------------------------------------------------------
      * Clear & Populate Local dataarea with program name.
      *
     C           *LOCK     IN   LDA                        Rtv LDA
     C                     CLEARLDA                        Clr LDA
     C                     MOVELD@PGMN    DBPGM     P      Set Pgm Name
     C                     OUT  LDA                        Wrt Record
      *-------------------------------------------------------------------
      * Initialise Parameters, Work Fields, Etc.
      *
     C                     MOVEACPY@      CPY@@  80        Set Copyright
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           *PSSR     BEGSR
      *===================================================================
      * *PSSR: Error Handling.
      *
      *-------------------------------------------------------------------
      * Check To See That *PSSR Has Not Been Called Yet.
      *
     C           WKRUN     IFEQ *BLANK                     If Not Exec
     C                     MOVE 'Y'       WKRUN   1         Set Off Ind
     C                     DUMP                             Dmp Pgm
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * If *PSSR Already Run, Then Just End The Program Here.
      *
     C                     MOVE *ON       *INU7            Set U7 Ind
     C                     MOVE *ON       *INU8            Set U8 Ind
     C                     MOVE *ON       *INLR            Set EndPgm Ind
     C                     RETRN                           Exit Program
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           @DEFN     BEGSR
      *===================================================================
      * @DEFN: Definition Routine (Not Called).
      *
      *-------------------------------------------------------------------
      * Dataareas...
      *
     C           *NAMVAR   DEFN           LDA              Dcl LDA
      *-------------------------------------------------------------------
      * Data Item Definitions...
      *
     C           *LIKE     DEFN FSCNUM    KCNUM            Dfn Customer
     C           *LIKE     DEFN FSFACT    KFACT            Dfn Facility
     C           *LIKE     DEFN FSFCNO    KFCNO            Dfn Sequence
      *-------------------------------------------------------------------
      * Key Lists...
      *
     C           KLEFCU    KLIST                           Key: LERFCUPL0
     C                     KFLD           KCNUM            -Customer No
     C                     KFLD           KFACT            -Fac type
     C                     KFLD           KFCNO            -Fac No
      *===================================================================
     C                     ENDSR
      /EJECT
** CPY@: Object Copyright
(c) Misys International Banking Systems Ltd. 2001
