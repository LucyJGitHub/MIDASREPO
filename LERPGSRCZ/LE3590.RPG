     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Facility currency conv. - risk parts')        *
      *****************************************************************
      *                                                               *
      *  Midas   -  Lending Module                                    *
      *                                                               *
      *  LE3590  -  Facility Currency Conversion - Risk Participants  *
      *                                                               *
      *  Function:  This Close of Business program will update        *
      *             selected facilities. It will convert the share    *
      *             amount from the original facility currency to     *
      *             the Euro equivalent.                              *
      *             All changes are detailed on an audit report.      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE134             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CLE106             Date 22Mar05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP070             Date 08Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006             Date 15May01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU021  *CREATE    Date 17Apr98               *
      *                                                               *
      *----------------------------------------------------------------
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CLE106 - Syndication Manager                                 *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP070 - Conversion of LE inputs into modular structure to   *
      *           use as APIs. Recompile over changed LEPARTPD        *
      *  CSD006 - Use long DS with SDCURRPD.                          *
      *  CEU021 - EMU LE Facility Currency Conversion                 *
      *                                                               *
      *****************************************************************
      *  Indicator    Description                                     *
      *  ¯¯¯¯¯¯¯¯¯    ¯¯¯¯¯¯¯¯¯¯¯                                     *
      *  01 - 19      Input Control.                                  *
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)      *
      *   20          Record Not Found                                *
      *   21          File Error Occurred                             *
      *   22          Start/End Of File Reached                       *
      *   25          Lookup/Scan.                                    *
      *  30 - 39      Program Work Indicators                         *
      *  40 - 49      Output Control.                                 *
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)    *
      *  90 - 99      Error Control.                                  *
      *                                                               *
      *----------------------------------------------------------------
      *  Routine      Description                                     *
      *  ¯¯¯¯¯¯¯      ¯¯¯¯¯¯¯¯¯¯¯                                     *
      *  PIFCUP       Read Facilities Selected for Conversion         *
      *  PTTLR        Last Record Total Time                          *
      ***XUFCLT*******Update*Risk*Participants*File********************                       CLE106
      *  XUFCLT       Update Assignment File                          *                       CLE106
      *  XCAM01       Convert Amounts To Euro                         *
      *  XSP1D1       Set Details Record For Report                   *
      *  XWP1W1       Write Details Report Record                     *
      *  *INZSR       Initial Routine                                 *
      *  *PSSR        Program Error Routine                           *
      *  @DEFN        Definition Routine (Not Called)                 *
      *  ZDATE2       Convert a Day Number to Date Formats            *                       CLE106
      *                                                               *
      *----------------------------------------------------------------
      *  File         Description                                     *
      *  ¯¯¯¯         ¯¯¯¯¯¯¯¯¯¯¯                                     *
      *  LEFCUPL2     Midas LE Fac. Selected for Cy Conv. Branch/Fclty*
      ***LEPARTL3*****Midas*LE*Risk*Participants.**********************                       CLE106
      *  LEPARTL3     Midas LE Assignments                            *                       CLE106
      ***LE3590P1*****Midas*LE*Facility*Cy*Conv.*Risk*Participants.****                       CLE106
      *  LE3590P1     Midas LE Facility Cy Conv. Assignments Report   *                       CLE106
      *                                                               *
      *================================================================
     F/EJECT
      *================================================================
     FLEFCUPL2IP  E           K        DISK         KINFSR *PSSR
      *
     FLEPARTL3UF  E           K        DISK         KINFSR *PSSR
      *
     FLE3590P1O   E             81     PRINTER      KINFSR *PSSR
     F                                              KINFDS SPOOL1
      *===================================================================
     E/EJECT
      *===================================================================
     E                    CPY@    1   1 80               Object Copyright
      /COPY ZSRSRC,ZFRPEDZ1
      /COPY ZSRSRC,ZDATE2Z1                                                                   CLE106
      *===================================================================
     I/EJECT
      *===================================================================
     IPSDS       SDS
     I                                        1  10 D@PGMN
      * Program Name
      *-------------------------------------------------------------------
     ILDA       E DSLDA                         256
      * Dataarea: Local Data Area.
      *-------------------------------------------------------------------
     IDSFDY     E DSDSFDY
      *First DS for Access Programs, short data structure.
      *-------------------------------------------------------------------
     IDSSDY     E DSDSSDY
      *Second DS for Access Programs, long data structure.
      *-------------------------------------------------------------------
     IDSBANK    E DSSDBANKPD
      * Definition: Bank Details Accessed Via Access Program.
      *-------------------------------------------------------------------
     IDSGELR    E DSSDGELRPD
      * Definition: General Ledger Details Accessed Via Access Program.
      *-------------------------------------------------------------------
     IDSCURR    E DSSDCURRPD
      * Definition: Currencies Accessed Via Access Program.
      *-------------------------------------------------------------------
     IDSKITE      DS                             90
      * Datastructure: Key - LEPARTL3
     I                                        1   3 KBRCH
     I**********                              4   90KCNUM                                     CSD027
     I                                        4   9 KCNUM                                     CSD027
     I                                       10  120KFTYP
     I                                       13  140KFSEQ
      *-------------------------------------------------------------------
     ISPOOL1      DS
      * File information data structure.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     IRUNDT     E DSRUNDAT                                                                    CLE106
     I              AGDFF                           DATF                                      CLE106
      *-------------------------------------------------------------------
      /COPY ZSRSRC,ZFRPEDZ2
      *===================================================================
     C/EJECT
      *===================================================================
      * Main Processing Control.
      *
      ***  Check System Date Format; DDMMYY OR MMDDYY.                                        CLE106
     C           DATF      IFEQ 'M'                                                           CLE106
     C                     MOVE '1'       *IN98                                               CLE106
     C                     END                                                                CLE106
      *-------------------------------------------------------------------
      * Process Selected Facilities.
      *
     C                     EXSR PIFCUP                     Proc Record
      *
     CLR                   EXSR PTTLR                      Proc Last Rec
      *===================================================================
     C/EJECT
     C           PIFCUP    BEGSR
      *===================================================================
      * PIFCUP: Process Facilities selected for conversion.
      *
      *-------------------------------------------------------------------
      * Set up key for LEPARTL3
      *
     C                     MOVE FSBRCA    KBRCH            Set Branch No
     C                     MOVE FSCNUM    KCNUM            Set Cust No
     C                     MOVE FSFACT    KFTYP            Set Fac Type
     C                     MOVE FSFCNO    KFSEQ            Set Fac Seq No
      *-------------------------------------------------------------------
      * Position File
      *
     C           KLEPA     SETLLLEPARTL3                   Postn File
     C           KLEPA     READELEPARTL3               2122Rtv Record
      *
     C           *IN21     IFEQ *ON                        If Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'LEPARTL3'DBFILE    P       Set File
     C                     MOVELDSKITE    DBKEY     P       Set Key
     C                     Z-ADD010       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      **Set*Risk*Participant*Share*Amounts.****************************.******************    CLE106
      * Set Assignment Share Amounts.                                                         CLE106
      *
     C           *IN22     DOWEQ*OFF                       DoWhile Rec
      *
     C                     Z-ADDSHAM      WKSHAM 150        Set Prt AmtofAction
     C                     Z-ADDSHAM      WKAMT             Set ParmAmtofAction
     C                     EXSR XCAM01                      Cvt Amount
     C                     Z-ADDWKAMT     SHAM              Set Amount of Action
      *
     C                     MOVE CCY       WKFCCY  3         Set Ccy
     C                     MOVE BKEURO    CCY               Set Rcd Ccy
      *
     C                     EXSR XULEPA                      Upd Record
      *
     C                     EXSR XSP1D1                      Set Prt Record
      *
     C           KLEPA     READELEPARTL3               2122 Rtv Record
      *
     C           *IN21     IFEQ *ON                         If Error
     C           *LOCK     IN   LDA                          Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P        Set Pgm
     C                     MOVEL'LEPARTL3'DBFILE    P        Set File
     C                     MOVELDSKITE    DBKEY     P        Set Key
     C                     Z-ADD020       DBASE              Set Ref Point
     C                     OUT  LDA                          Wrt Record
     C                     EXSR *PSSR                        Proc Error
     C                     ENDIF                            End If
      *
     C                     ENDDO                           End Do
      *
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           PTTLR     BEGSR
      *===================================================================
      * PTTLR : Last Record Total Time.
      *
      *-------------------------------------------------------------------
      * Write End of Report.
      *
     C           *IN81     IFEQ *ON                        If Overflow
     C                     MOVE *OFF      *IN81             Reset Overflow
     C                     WRITEP1H010                 21   Wrt Hdr
      *
     C           *IN21     IFEQ *ON                         If File Error
     C           *LOCK     IN   LDA                          Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P        Set Pgm
     C                     MOVEL'LE3590P1'DBFILE    P        Set File
     C                     MOVEL'P1H010'  DBKEY     P        Set Key
     C                     Z-ADD030       DBASE              Set Ref Point
     C                     OUT  LDA                          Wrt Record
     C                     EXSR *PSSR                        Proc Error
     C                     ENDIF                            End If
     C                     ENDIF                           End If
      *
     C           WKDET     IFEQ 'Y'                        If Details Rptd
     C                     WRITEP1EOR                       Wrt End Of Rpt
     C                     ELSE                            Else
     C                     WRITEP1NDR                       Wrt No DetRptd
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           XCAM01    BEGSR
      *===================================================================
      * XCAM01: Convert Amounts Into Euro Equivalent.
      *
      *-------------------------------------------------------------------
      * Use program EU0200 to convert amounts into Euro.
      *
     C                     Z-ADDWKAMT     P@FRAM 183       Set Parm Amt
      *
     C                     CALL 'EU0200'               2121Cvt Amount
     C                     PARM *BLANKS   P@RTCD  7        -O:Return code
     C                     PARM           P@FRAM           -I:Fr Amount
     C                     PARM FSOCCY    P@FRCY  3        -I:Fr Ccy
     C                     PARM BKEURO    P@TOCY  3        -O:To Ccy
     C                     PARM           P@OUTA 150       -O:Outright Amt
     C                     PARM           P@INTA 183       -O:Interim Amt
     C                     PARM           P@EXRT 159       -O:Exchange Rate
     C                     PARM           P@MDIN  1        -O:Mult/Div
      *
     C           P@RTCD    IFNE *BLANKS                    If Rtn Error
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'EU0200'  DBFILE    P       Set File
     C                     MOVEL'*FIRST'  DBKEY     P       Set Key
     C                     Z-ADD040       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *
     C                     Z-ADDP@OUTA    WKAMT  150       Set Rtn Amount
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           XULEPA    BEGSR
      *===================================================================
      **XULEPA:*Update*Midas*LE*Risk*Participants*File.********************                   CLE106
      * XULEPA: Update Midas LE Assignments File.                                             CLE106
      *
      *-------------------------------------------------------------------
      **Update*Risk*Participants*File.*************************************                   CLE106
      * Update Assignments File.                                                              CLE106
      *
     C                     UPDATLEPARTD0               21  Upd Record
      *
     C           *IN21     IFEQ *ON                        If Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'LEPARD0' DBFILE    P       Set File
     C                     MOVELDSKITE    DBKEY     P       Set Key
     C                     Z-ADD050       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           XSP1D1    BEGSR
      *===================================================================
      * XSP1D1 Set Printer(P1) Details.
      *
      *-------------------------------------------------------------------
      * Set Facilities Data.
      *
     C                     MOVE FSCNUM    P1CNUM           Set Customer
     C                     MOVE FSFACT    WKFACT  3        Set Fac type
     C                     MOVE WKFACT    P1FACT           Set Facility
     C                     MOVE FSFCNO    P1FCNO           Set Sequence
      *-------------------------------------------------------------------                    CLE106
      * Set Assignment Data.                                                                  CLE106
      *                                                                                       CLE106
     C                     MOVE JDTE      P1DATE           Assigned Date                      CLE106
     C           JDTE      COMP 0                    51    Assignment Date                    CLE106
     C   51                Z-ADDJDTE      ZDAYNO                                              CLE106
     C   51                EXSR ZDATE2                                                        CLE106
     C   51                MOVE ZADATE    P1DATE  7                                           CLE106
     C*                                                                                       CLE106
     C                     MOVE SEQN      P1SEQN           Sequence No.                       CLE106
      *-------------------------------------------------------------------
      * Retrieve Currencies.
      *
     C                     CALL 'AOCURRR0'             2121Rtv Currencies
     C                     PARM *BLANKS   P@RTCD  7        -O:Return code
     C                     PARM '*KEY   ' P@OPTN  7        -I:Option
     C                     PARM WKFCCY    P@CYCD  3        -I:Currency
     C********** DSCURR    PARM *BLANKS   DSFDY            -O:Format            CSD006
     C           DSCURR    PARM *BLANKS   DSSDY            -O:Format            CSD006
      *
     C           *IN21     IFEQ *ON                        If Call Error
     C           P@RTCD    OREQ '*ERROR*'                  Or Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'AOCURRR0'DBFILE    P       Set File
     C                     MOVEL'*FIRST'  DBKEY     P       Set Key
     C                     Z-ADD060       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *
     C                     Z-ADDA6NBDP    WKODP   10       Set Orig DecPl
      *-------------------------------------------------------------------
      * Retrieve Euro Currency.
      *
     C                     CALL 'AOCURRR0'             2121Access Currencies
     C                     PARM *BLANKS   P@RTCD  7        -O:Return code
     C                     PARM '*KEY   ' P@OPTN  7        -I:Option
     C                     PARM BKEURO    P@CYCD  3        -I:Currency
     C********** DSCURR    PARM *BLANKS   DSFDY            -O:Format            CSD006
     C           DSCURR    PARM *BLANKS   DSSDY            -O:Format            CSD006
      *
     C           *IN21     IFEQ *ON                        If Call Error
     C           P@RTCD    OREQ '*ERROR*'                  Or Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'AOCURRR0'DBFILE    P       Set File
     C                     MOVEL'*FIRST'  DBKEY     P       Set Key
     C                     Z-ADD070       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *
     C                     Z-ADDA6NBDP    WKCDP   10       Set Conv DecPl
      *
     C                     Z-ADDWKSHAM    ZFLD15           Set Amount
     C                     Z-ADDWKODP     ZDECS            Set Dec Pos
     C                     MOVE *BLANKS   ZECODE           Clr Edt Cde
     C                     EXSR ZFRPED                     Edit Amount
     C                     MOVE ZOUT25    P1OVAL           Set OrigVal
      *
     C                     Z-ADDSHAM      ZFLD15           Set Amount
     C                     Z-ADDWKCDP     ZDECS            Set Dec Pos
     C                     MOVE *BLANKS   ZECODE           Clr Edt Cde
     C                     EXSR ZFRPED                     Edit Amount
     C                     MOVE ZOUT25    P1CVAL           Set ConvVal
      *
     C                     EXSR XWP1W1                     Prt Detail
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           XWP1W1    BEGSR
      *===================================================================
      * XWP1W1: Write Printer(P1) Details.
      *
      *-------------------------------------------------------------------
      * Write Printer(P1) Detail Line.
      *
     C           *IN81     IFEQ *ON                        If Overflow
     C                     MOVE *OFF      *IN81             Reset Overflow
     C                     WRITEP1H010                 21   Wrt Hdr
      *
     C           *IN21     IFEQ *ON                         If File Error
     C           *LOCK     IN   LDA                          Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P        Set Pgm
     C                     MOVEL'LE3590P1'DBFILE    P        Set File
     C                     MOVEL'P1H010'  DBKEY     P        Set Key
     C                     Z-ADD080       DBASE              Set Ref Point
     C                     OUT  LDA                          Wrt Record
     C                     EXSR *PSSR                        Proc Error
     C                     ENDIF                            End If
     C                     ENDIF                           End If
      *
     C                     WRITEP1D010                 21  Wrt Details
      *
     C           *IN21     IFEQ *ON                        If File Error
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'LE3590P1'DBFILE    P       Set File
     C                     MOVEL'P1D010'  DBKEY     P       Set Key
     C                     Z-ADD090       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *
     C                     MOVE 'Y'       WKDET            Set Details Flg
      *===================================================================
     C                     ENDSR
     C/EJECT
      /COPY ZSRSRC,ZFRPEDZ3
     C/EJECT
     C           *INZSR    BEGSR
      *===================================================================
      * *INZSR: Initialization Routine.
      *
      *-------------------------------------------------------------------
      * Initialise Parameters, Work Fields, Etc.
      *
     C                     MOVEACPY@      MKI@@  80        Set Copyright
     C                     MOVE 'N'       WKDET   1        Set Details Flg
     C                     MOVE *ON       *IN81            Set Prt Hd Ind
      *-------------------------------------------------------------------
      * Retrieve Bank Details.
      *
     C                     CALL 'AOBANKR0'             2121Access Bank
     C                     PARM *BLANKS   P@RTCD  7        -O:Return Code
     C                     PARM '*KEY   ' P@OPTN  7        -I:Option
     C           DSBANK    PARM *BLANKS   DSFDY            -O:Format
      *
     C           *IN21     IFEQ *ON                        If Call Error
     C           P@RTCD    OREQ '*ERROR*'                  Or Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'AOBANKR0'DBFILE    P       Set File
     C                     MOVEL'*FIRST'  DBKEY     P       Set Key
     C                     Z-ADD100       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * Retrieve General Ledger Details (suspense account code).
      *
     C**********           CALL 'AOGELRR0'             2121Rtv GL ICD Dtl                     CGL029
     C                     CALL 'AOGELRR1'             2121                                   CGL029
     C                     PARM *BLANKS   P@RTCD  7        -O:Return Code
     C                     PARM '*KEY   ' P@OPTN  7        -I:Option
     C********** DSGELR    PARM *BLANK    DSFDY            -O:Format                          CGL029
     C           DSGELR    PARM *BLANK    DSSDY                                               CGL029
      *
     C           *IN21     IFEQ *ON                        If Call Error
     C           P@RTCD    ORNE *BLANKS                    Or Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'SDGELRPD'DBFILE    P       Set File
     C                     MOVE P@OPTN    DBKEY     P       Set Key
     C                     Z-ADD110       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * Set Spool File Number And CALL ZSFILE.
      *
     C                     Z-ADDSFNUM1    SPLNO            Set Spool No
      *
     C                     CALL 'ZSFILE'               2121Call RCF
     C                     PARM           P@RSEQ           -O:Rpt Seq
     C                     PARM *BLANKS   RENT    3        -I:Entity
     C                     PARM           SFILE1           -I:Spool Name
     C                     PARM           SPLNO   60       -I:Spool Number
     C                     PARM *BLANKS   ZERR    1        -O:Return Code
      *
     C           ZERR      IFEQ 'Y'                        If Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'ZSFILE  'DBFILE    P       Set File
     C                     MOVELSFILE1    DBKEY     P       Set Key
     C                     Z-ADD120       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           *PSSR     BEGSR
      *===================================================================
      * *PSSR: Error Handling.
      *
      *-------------------------------------------------------------------
      * Check To See That *PSSR Has Not Been Called Yet.
      *
     C           WKRUN     IFEQ *BLANK                     If Not Exec
     C                     MOVE 'Y'       WKRUN   1         Set Off Ind
     C                     DUMP                             Dmp Pgm
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * If *PSSR Already Run, Then Just End The Program Here.
      *
     C                     MOVE *ON       *INU7            Set U7 Ind
     C                     MOVE *ON       *INU8            Set U8 Ind
     C                     MOVE *ON       *INLR            Set EndPgm Ind
     C                     RETRN                           Exit Program
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           @DEFN     BEGSR
      *===================================================================
      * @DEFN: Definition Routine (Not Called).
      *
      *-------------------------------------------------------------------
      * Dataareas...
      *
     C           *NAMVAR   DEFN           LDA              Dcl LDA
      *-------------------------------------------------------------------
      * Lists...
      *
     C           KLEPA     KLIST                           Key: FCLTY
     C                     KFLD           KBRCH            -Branch No
     C                     KFLD           KCNUM            -Customer No
     C                     KFLD           KFTYP            -Fac type
     C                     KFLD           KFSEQ            -Fac seq
      *
     C           *ENTRY    PLIST                           Prg: Entry
     C                     PARM           P@RSEQ  5        -Report Seq
      *===================================================================
     C                     ENDSR
     C/EJECT
     C/COPY ZSRSRC,ZDATE2Z4                                                                   CLE106
** CPY@: Object Copyright
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN                                          CLE106
0366073110961461                                                                              CLE106
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR                                            CLE106
000031059090120151181212243273304334365                                                       CLE106
**   ZMNM - MONTHS SHORT NAMES                                                                CLE106
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC                                                          CLE106
