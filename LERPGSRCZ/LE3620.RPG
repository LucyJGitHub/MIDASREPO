     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Facility Currency Conv. - Manual Util.')
      *****************************************************************
      *
      *  Midas   -  Lending Module
      *
      *  LE3620  -  Facility Currency Conversion - Manual Utilisation
      *
      *  Function:  This Close of Business program will update
      *             selected facilities. It will update the exchange
      *             rate and the exchange indicator. It is called
      *             by CL program LEC3570.
      *             All changes are detailed on an audit report.
      *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD055657           Date 14May20               *
      *  Prev Amend No. AR674226           Date 04Dec19               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 199698             Date 28Dec01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CLE009             Date 11May99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 144410             Date 07Oct98               *
      *                 CEU021  *CREATE    Date 21Apr98               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD055657 - Manual transaction from FFC TI Interface went to  *
      *             repair queue because the facility exchange rate is*
      *             longer than the defined facility exchange rate in *
      *             Midas. Increase the field definition of the Midas *
      *             facility exchange rate. (Recompile)               *
      *  AR674226 - Manual transaction reached 999. Increase the size *
      *             of utilisation sequence no. and transaction       *
      *             sequence. Recompile. (Child: AR674227)            *
      *           - Applied for MD054782.                             *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  199698 - Amended description of program in header box. Put a *
      *           call to LE3620 in CL program LEC3570. If UCCY isn't *
      *           EUR or an 'In' ccy, calculate exchange rate between *
      *           UCCY and new FCCY, using existing value of UXRT.    *
      *  CLE009 - LE I/C Swift Message Generation for Fees (Recompile)*
      *  144410 - Wrong condition when getting the exchange rate for  *
      *           utilisation currency                                *
      *  CEU021 - EMU LE Facility Currency Conversion
      *
      *****************************************************************
      *  Indicator    Description
      *  ¯¯¯¯¯¯¯¯¯    ¯¯¯¯¯¯¯¯¯¯¯
      *  01 - 19      Input Control.
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)
      *   20          Record Not Found
      *   21          File Error Occurred
      *   22          Start/End Of File Reached
      *   25          Lookup/Scan.
      *  30 - 39      Program Work Indicators
      *  40 - 49      Output Control.
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)
      *  90 - 99      Error Control.
      *
      *-------------------------------------------------------------------
      *  Routine      Description
      *  ¯¯¯¯¯¯¯      ¯¯¯¯¯¯¯¯¯¯¯
      *  PIFCUP       Read Facilities Selected for Conversion
      *  PTTLR        Last Record Total Time
      *  XULEMN       Update Utilisation Details
      *  XCAM01       Convert Amounts To Euro
      *  XSP1D1       Set Details Record For Report
      *  XWP1W1       Write Details Report Record
      *  *INZSR       Initial Routine
      *  *PSSR        Program Error Routine
      *  @DEFN        Definition Routine (Not Called)
      *
      *-------------------------------------------------------------------
      *  File         Description
      *  ¯¯¯¯         ¯¯¯¯¯¯¯¯¯¯¯
      *  LEFCUPL2     Midas LE Fac. Selected for Cy Conv. Branch/Facility
      *  LEMNFUL0     Midas LE Utilisation Details
      *  LE3620P1     Midas LE Facility Cy Conv. Exchange Rates.
      *
      *===================================================================
     F/EJECT
      *===================================================================
     FLEFCUPL2IP  E           K        DISK         KINFSR *PSSR
      *
     FLEMNFUL0UF  E           K        DISK         KINFSR *PSSR
      *
     FLE3620P1O   E             81     PRINTER      KINFSR *PSSR
     F                                              KINFDS SPOOL1
      *===================================================================
     E/EJECT
      *===================================================================
     E                    CPY@    1   1 80               Object Copyright
      /COPY ZSRSRC,ZSEFMT1
      *===================================================================
     I/EJECT
      *===================================================================
     IPSDS       SDS
     I                                        1  10 D@PGMN
      * Program Name
      *-------------------------------------------------------------------
     ILDA       E DSLDA                         256
      * Dataarea: Local Data Area.
      *-------------------------------------------------------------------
     IDSFDY     E DSDSFDY
      *First DS for Access Programs, short data structure.
      *-------------------------------------------------------------------
     IDSSDY     E DSDSSDY
      *Second DS for Access Programs, long data structure.
      *-------------------------------------------------------------------
     IDSBANK    E DSSDBANKPD
      * Definition: Bank Details Accessed Via Access Program.
      *-------------------------------------------------------------------
     IDSGELR    E DSSDGELRPD
      * Definition: General Ledger Details Accessed Via Access Program.
      *-------------------------------------------------------------------
     IDSCURR    E DSSDCURRPD
      * Definition: Currencies Accessed Via Access Program.
      *-------------------------------------------------------------------
     IDSKITE      DS                             90
      * Datastructure: Key - LEMNFUL0
     I                                        1   3 KBRCH
     I**********                              4   90KCNUM                                     CSD027
     I                                        4   9 KCNUM                                     CSD027
     I                                       10  120KFTYP
     I                                       13  140KFSEQ
      *-------------------------------------------------------------------
     ISPOOL1      DS
      * File information data structure.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
      *-------------------------------------------------------------------
      /COPY ZSRSRC,ZSEFMT2
      *===================================================================
     C/EJECT
      *===================================================================
      * Main Processing Control.
      *
      *-------------------------------------------------------------------
      * Process Selected Facilities.
      *
     C                     EXSR PIFCUP                     Proc Record
      *
     CLR                   EXSR PTTLR                      Proc Last Rec
      *===================================================================
     C/EJECT
     C           PIFCUP    BEGSR
      *===================================================================
      * PIFCUP: Process Facilities selected for conversion.
      *
      *-------------------------------------------------------------------
      * Set up key for LEMNFUL0
      *
     C                     MOVE FSBRCA    KBRCH            Set Branch
     C                     MOVE FSCNUM    KCNUM            Set Cust No
     C                     MOVE FSFACT    KFTYP            Set Fac typ e
     C                     MOVE FSFCNO    KFSEQ            Set Fac Seq
      *-------------------------------------------------------------------
      * Position File.
      *
     C           KLEMN     SETLLLEMNFUL0                   Postn File
     C           KLEMN     READELEMNFUL0               2122Rtv Record
      *
     C           *IN21     IFEQ *ON                        If Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'LEMNFUL0'DBFILE    P       Set File
     C                     MOVELDSKITE    DBKEY     P       Set Key
     C                     Z-ADD010       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * Set Exchange Rates.                                            .
      *
     C           *IN22     DOWEQ*OFF                       DoWhile Rec
      *
     C           RECI      IFEQ 'D'                         If Live
     C                     Z-ADDUXRT      WKUXRT 138         Set Orig Ex
     C                     MOVE UXID      WKUXID  1          Set Orig Ind
     C                     MOVE UCCY      WKCCY   3          Set Ccy
     C                     EXSR XRCY20                       Obtain CcyDet
     C                     MOVE A6INCY    WKINCY  1          Set IN Ccy
     C***********WKINCY    IFEQ 'Y'                          If IN Ccy    144410
      * If utilisation currency is already 'EUR', use rate of 1.0000 D:                       199698
     C           UCCY      IFEQ BKEURO                        If Euro Ccy
     C                     Z-ADD1         UXRT                 Set Rate
     C                     Z-ADD1         WKCUXR 138           Set Rate
     C***********          MOVE 'M'       UXID                 Set Ex Ind 144410
     C                     MOVE 'D'       UXID                 Set Ex Ind 144410
     C                     ELSE                               Else
      * If utilisation currency is an 'In' ccy, use fixed EMU rate:                           199698
     C           WKINCY    IFEQ 'Y'                          If IN Ccy    144410
     C                     Z-ADD0         WKAMT                Reset Amt
     C                     EXSR XCAM01                         Cvt Amt
     C                     Z-ADDP@EXRT    UXRT                 Set Rate
     C                     Z-ADDP@EXRT    WKCUXR               Set Rate
     C                     MOVE P@MDIN    UXID                 Set Ex Ind
     C***********          ENDIF                              End If      144410
     C                     ELSE                              Else
      * If utilisation ccy is not 'In', calculate new rate using                              199698
      * existing UXRT rate between MFU ccy & original facility ccy.                           199698
     C                     Z-ADDUXRT      ZRATE1 138          Set Rate                        199698
     C                     MOVE UXID      ZMDI1   1           Set MD Ind                      199698
     C                     MOVE FSOCCY    WKCCY               Set Ccy                         199698
     C                     EXSR XRCY20                        Obtain Ccy                      199698
     C                     Z-ADDA6EUER    ZRATE2 138          Set Rate                        199698
     C           A6EUMD    IFEQ 'M'                                                           199698
     C                     MOVE 'D'       ZMDI2   1                                           199698
     C                     ELSE                                                               199698
     C                     MOVE 'M'       ZMDI2                                               199698
     C                     ENDIF                                                              199698
     C**********           Z-ADDA6SPRT    ZRATE1 138          Set Rate                        199698
     C**********           MOVE A6MDIN    ZMDI1   1           Set MD Ind                      199698
     C**********           MOVE BKEURO    WKCCY               Set Ccy                         199698
     C**********           EXSR XRCY20                        Obtain Ccy                      199698
     C**********           Z-ADDA6SPRT    ZRATE2 138          Set Rate                        199698
     C**********           MOVE A6MDIN    ZMDI2   1           Set MD Ind                      199698
      * ZXRATE will produce cross rate between UCCY and EUR, using                            199698
      * the UXRT rate (UCCY->FSOCCY) and the fixed FSOCCY->EUR rate.                          199698
     C                     Z-ADD0         ZRATEX 138          Reset Rate
     C                     MOVE *BLANK    ZMDIX   1           Clr MD Ind
     C                     EXSR ZXRATE                        Obtain Rate
     C                     Z-ADDZRATEX    UXRT                Set RateOfEx
     C                     Z-ADDZRATEX    WKCUXR 138          Set RateOfEx
     C                     MOVE ZMDIX     UXID                Set Ex Ind
     C                     ENDIF                             End If       144410
     C                     ENDIF                             End If
      *
     C                     EXSR XULEMN                       Upd Record
      *
     C                     EXSR XSP1D1                       Set Prt Rcd
      *
     C                     ENDIF                            End If
      *
     C           KLEMN     READELEMNFUL0               2122 Rtv Record
      *
     C           *IN21     IFEQ *ON                         If Error
     C           *LOCK     IN   LDA                          Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P        Set Pgm
     C                     MOVEL'LEMNFUL0'DBFILE    P        Set File
     C                     MOVELDSKITE    DBKEY     P        Set Key
     C                     Z-ADD020       DBASE              Set Ref Point
     C                     OUT  LDA                          Wrt Record
     C                     EXSR *PSSR                        Proc Error
     C                     ENDIF                            End If
      *
     C                     ENDDO                           End Do
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           PTTLR     BEGSR
      *===================================================================
      * PTTLR : Last Record Total Time.
      *
      *-------------------------------------------------------------------
      * Write End of Report.
      *
     C           *IN81     IFEQ *ON                        If Overflow
     C                     MOVE *OFF      *IN81             Reset Overflow
     C                     WRITEP1H010                 21   Wrt Hdr
      *
     C           *IN21     IFEQ *ON                         If File Error
     C           *LOCK     IN   LDA                          Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P        Set Pgm
     C                     MOVEL'LE3620P1'DBFILE    P        Set File
     C                     MOVEL'P1H010'  DBKEY     P        Set Key
     C                     Z-ADD030       DBASE              Set Ref Point
     C                     OUT  LDA                          Wrt Record
     C                     EXSR *PSSR                        Proc Error
     C                     ENDIF                            End If
     C                     ENDIF                           End If
      *
     C           WKDET     IFEQ 'Y'                        If Details Rptd
     C                     WRITEP1EOR                       Wrt End Of Rpt
     C                     ELSE                            Else
     C                     WRITEP1NDR                       Wrt No DetRptd
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           XCAM01    BEGSR
      *===================================================================
      * XCAM01: Convert Amounts Into Euro Equivalent.
      *
      *-------------------------------------------------------------------
      * Use program EU0200 to convert amounts into Euro.
      *
     C                     Z-ADDWKAMT     P@FRAM 183       Set Parm Amt
      *
     C                     CALL 'EU0200'               2121Cvt Amount
     C                     PARM *BLANKS   P@RTCD  7        -O:Return code
     C                     PARM           P@FRAM           -I:Fr Amount
     C                     PARM UCCY      P@FRCY  3        -I:Fr Ccy
     C                     PARM BKEURO    P@TOCY  3        -O:To Ccy
     C                     PARM           P@OUTA 150       -O:Outright Amt
     C                     PARM           P@INTA 183       -O:Interim Amt
     C                     PARM           P@EXRT 159       -O:Exchange Rate
     C                     PARM           P@MDIN  1        -O:Mult/Div
      *
     C           P@RTCD    IFNE *BLANKS                    If Rtn Error
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'EU0200'  DBFILE    P       Set File
     C                     MOVEL'*FIRST'  DBKEY     P       Set Key
     C                     Z-ADD040       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *
     C                     Z-ADDP@OUTA    WKAMT  150       Set Rtn Amount
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           XRCY20    BEGSR
      *===================================================================
      * XRCY20: Retrieve Currency Details.
      *
      *-------------------------------------------------------------------
      *
     C                     CALL 'AOCURRR0'             2121Rtv Currencies
     C                     PARM *BLANKS   P@RTCD  7        -O:Return code
     C                     PARM '*KEY   ' P@OPTN  7        -I:Option
     C                     PARM WKCCY     P@CYCD  3        -I:Currency
     C           DSCURR    PARM *BLANKS   DSSDY            -O:Format
      *
     C           *IN21     IFEQ *ON                        If Call Error
     C           P@RTCD    OREQ '*ERROR*'                  Or Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'AOCURRR0'DBFILE    P       Set File
     C                     MOVEL'*KEY  '  DBKEY     P       Set Key
     C                     Z-ADD050       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
     C/EJECT
      /COPY ZSRSRC,ZXRATEZ1
     C/EJECT
     C           XULEMN    BEGSR
      *===================================================================
      * XULEMN: Update Midas LE Utilisation Details file.
      *
      *-------------------------------------------------------------------
      * Update Live Utilisation Details File.
      *
     C                     UPDATLEMNFUPF               21  Upd Record
      *
     C           *IN21     IFEQ *ON                        If Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'LEMNFUPF'DBFILE    P       Set File
     C                     MOVELDSKITE    DBKEY     P       Set Key
     C                     Z-ADD060       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           XSP1D1    BEGSR
      *===================================================================
      * XSP1D1 Set Printer(P1) Details.
      *
      *-------------------------------------------------------------------
      * Set Facilities Data.
      *
     C                     MOVE FSCNUM    P1CNUM           Set Customer
     C                     MOVE FSFACT    P1FACT           Set Fac Type
     C                     MOVE FSFCNO    P1FCNO           Set Fac Seq
     C                     MOVE SQNO      P1SEQ            Set Sequence
      *
     C                     MOVE WKUXRT    ZRT13            Set Rate
     C                     MOVE *BLANKS   ZPBAS            Clr Per Base
     C                     MOVE *BLANKS   ZRT13A           Clr Out Fmt
     C                     EXSR ZSEFMT                     Edit Rate
     C                     MOVE ZRT13A    P1OEXR           Set OrigVal
      *
     C                     MOVE WKUXID    P1OEXI           Set Orig Ind
      *
     C                     MOVE WKCUXR    ZRT13            Set Rate
     C                     MOVE *BLANKS   ZPBAS            Clr Per Base
     C                     MOVE *BLANKS   ZRT13A           Clr Out Fmt
     C                     EXSR ZSEFMT                     Edit Rate
     C                     MOVE ZRT13A    P1CEXR           Set ConvVal
      *
     C                     MOVE UXID      P1CEXI           Set Conv Ind
      *
     C                     EXSR XWP1W1                     Prt Detail
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           XWP1W1    BEGSR
      *===================================================================
      * XWP1W1: Write Printer(P1) Details.
      *
      *-------------------------------------------------------------------
      * Write Printer(P1) Detail Line.
      *
     C           *IN81     IFEQ *ON                        If Overflow
     C                     MOVE *OFF      *IN81             Reset Overflow
     C                     WRITEP1H010                 21   Wrt Hdr
      *
     C           *IN21     IFEQ *ON                         If File Error
     C           *LOCK     IN   LDA                          Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P        Set Pgm
     C                     MOVEL'LE3620P1'DBFILE    P        Set File
     C                     MOVEL'P1H010'  DBKEY     P        Set Key
     C                     Z-ADD070       DBASE              Set Ref Point
     C                     OUT  LDA                          Wrt Record
     C                     EXSR *PSSR                        Proc Error
     C                     ENDIF                            End If
     C                     ENDIF                           End If
      *
     C                     WRITEP1D010                 21  Wrt Details
      *
     C           *IN21     IFEQ *ON                        If File Error
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'LE3620P1'DBFILE    P       Set File
     C                     MOVEL'P1D010'  DBKEY     P       Set Key
     C                     Z-ADD080       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *
     C                     MOVE 'Y'       WKDET            Set Details Flg
      *===================================================================
     C                     ENDSR
     C/EJECT
      /COPY ZSRSRC,ZSEFMT3
     C/EJECT
     C           *INZSR    BEGSR
      *===================================================================
      * *INZSR: Initialization Routine.
      *
      *-------------------------------------------------------------------
      * Initialise Parameters, Work Fields, Etc.
      *
     C                     MOVEACPY@      MKI@@  80        Set Copyright
     C                     MOVE 'N'       WKDET   1        Set Details Flg
     C                     MOVE *ON       *IN81            Set Prt Hd Ind
      *-------------------------------------------------------------------
      * Retrieve Bank Details.
      *
     C                     CALL 'AOBANKR0'             2121Access Bank
     C                     PARM *BLANKS   P@RTCD  7        -O:Return Code
     C                     PARM '*KEY   ' P@OPTN  7        -I:Option
     C           DSBANK    PARM *BLANKS   DSFDY            -O:Format
      *
     C           *IN21     IFEQ *ON                        If Call Error
     C           P@RTCD    OREQ '*ERROR*'                  Or Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'AOBANKR0'DBFILE    P       Set File
     C                     MOVELP@OPTN    DBKEY     P       Set Key
     C                     Z-ADD090       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * Retrieve General Ledger Details (suspense account code).
      *
     C**********           CALL 'AOGELRR0'             2121Rtv GL ICD Dtl                     CGL029
     C                     CALL 'AOGELRR1'             2121                                   CGL029
     C                     PARM *BLANKS   P@RTCD  7        -O:Return Code
     C                     PARM '*KEY   ' P@OPTN  7        -I:Option
     C********** DSGELR    PARM *BLANK    DSFDY            -O:Format                          CGL029
     C           DSGELR    PARM *BLANK    DSSDY                                               CGL029
      *
     C           *IN21     IFEQ *ON                        If Call Error
     C           P@RTCD    ORNE *BLANKS                    Or Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'SDGELRPD'DBFILE    P       Set File
     C                     MOVE P@OPTN    DBKEY     P       Set Key
     C                     Z-ADD100       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * Set Spool File Number And CALL ZSFILE.
      *
     C                     Z-ADDSFNUM1    SPLNO            Set Spool No
      *
     C                     CALL 'ZSFILE'               2121Call RCF
     C                     PARM           P@RSEQ           -O:Rpt Seq
     C                     PARM *BLANKS   RENT    3        -I:Entity
     C                     PARM           SFILE1           -I:Spool Name
     C                     PARM           SPLNO   60       -I:Spool Number
     C                     PARM *BLANKS   ZERR    1        -O:Return Code
      *
     C           ZERR      IFEQ 'Y'                        If Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'ZSFILE  'DBFILE    P       Set File
     C                     MOVELSFILE1    DBKEY     P       Set Key
     C                     Z-ADD110       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           *PSSR     BEGSR
      *===================================================================
      * *PSSR: Error Handling.
      *
      *-------------------------------------------------------------------
      * Check To See That *PSSR Has Not Been Called Yet.
      *
     C           WKRUN     IFEQ *BLANK                     If Not Exec
     C                     MOVE 'Y'       WKRUN   1         Set Off Ind
     C                     DUMP                             Dmp Pgm
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * If *PSSR Already Run, Then Just End The Program Here.
      *
     C                     MOVE *ON       *INU7            Set U7 Ind
     C                     MOVE *ON       *INU8            Set U8 Ind
     C                     MOVE *ON       *INLR            Set EndPgm Ind
     C                     RETRN                           Exit Program
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           @DEFN     BEGSR
      *===================================================================
      * @DEFN: Definition Routine (Not Called).
      *
      *-------------------------------------------------------------------
      * Dataareas...
      *
     C           *NAMVAR   DEFN           LDA              Dcl LDA
      *-------------------------------------------------------------------
      * Lists...
      *
     C           KLEMN     KLIST                           Key: LEMNFUL0
     C                     KFLD           KBRCH            -Branch
     C                     KFLD           KCNUM            -Customer No
     C                     KFLD           KFTYP            -Fac type
     C                     KFLD           KFSEQ            -Fac seq
      *
     C           *ENTRY    PLIST                           Prg: Entry
     C                     PARM           P@RSEQ  5        -Report Seq
      *===================================================================
     C                     ENDSR
     C/EJECT
** CPY@: Object Copyright
(c) Finastra International Limited 2001
(C) FINASTRA INTERNATIONAL LIMITEd 2001