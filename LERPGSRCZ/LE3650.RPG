     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Facility currency conv facility fees')        *
      *****************************************************************
      *
      *  Midas   -  Lending Module
      *
      *  LE3650  -  Facility Currency Conversion - Facility Fees
      *
      *  Function:  This Close of Business program will update
      *             selected facilities. It will update the fee
      *             exchange rate, the fee exchange indicator
      *             and the multiply/divide indicator.
      *             All changes are detailed on an audit report.
      *
      *  (c) Finastra International Limited 2001                      *
      *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      *                 CLE139             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 204452             Date 02May02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 199694             Date 28Dec01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CLE009             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 144196             Date 05Oct98               *
      *                 143512             Date 29Sep98               *
      *                 CEU021  *CREATE    Date 21Apr98               *
      *                                                               *
      *
      *-------------------------------------------------------------------
      *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference                                *
      *  CLE139 - Lending Fee Capitalisation (Recompile)              *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  204452 - Recompiled due to changed PF/LEFEED.                *
      *  199694 - When converting from EUR to an 'In' ccy, EU0200 out-*
      *           puts the wrong multiply/divide indicator P@MDIN, so *
      *           exchange rate is wrong on 'In' ccy fees. Swap P@MDIN*
      *           If fee currency is not 'In', include existing FEERAT*
      *           in the calculation - not just current spot rates. If*
      *           the fee does not already have a fixed rate, do not  *
      *           output a new rate, but do list them on the report.  *
      *  CLE009 - LE I/C Swift Message Generation for Fees            *
      *           Recompiled due to changes in LEFEED                 *
      *  144196 - Add Fee Sequence and Fee Ccy to Printer Report      *
      *  143512 - Fees accrued to date is wrong because of wrong      *
      *           fclty currency exchange rate.                       *
      *  CEU021 - EMU LE Facility Currency Conversion
      *
      *****************************************************************
      *  Indicator    Description
      *  ¯¯¯¯¯¯¯¯¯    ¯¯¯¯¯¯¯¯¯¯¯
      *  01 - 19      Input Control.
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)
      *   20          Record Not Found
      *   21          File Error Occurred
      *   22          Start/End Of File Reached
      *   25          Lookup/Scan.
      *  30 - 39      Program Work Indicators
      *  40 - 49      Output Control.
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)
      *  90 - 99      Error Control.
      *
      *-------------------------------------------------------------------
      *  Routine      Description
      *  ¯¯¯¯¯¯¯      ¯¯¯¯¯¯¯¯¯¯¯
      *  PIFCUP       Read Facilities Selected for Conversion
      *  PTTLR        Last Record Total Time
      *  XULEFE       Update Fees Master Details
      *  XCAM01       Convert Amounts To Euro
      *  XSP1D1       Set Details Record For Report
      *  XWP1W1       Write Details Report Record
      *  *INZSR       Initial Routine
      *  *PSSR        Program Error Routine
      *  @DEFN        Definition Routine (Not Called)
      *
      *-------------------------------------------------------------------
      *  File         Description
      *  ¯¯¯¯         ¯¯¯¯¯¯¯¯¯¯¯
      *  LEFCUPL2     Midas LE Fac. Selected for Cy Conv. Branch/Facility
      *  LEFEED       Midas LE Fees Master Details
      *  LE3650P1     Midas LE Facility Cy Conv. Facility Fees.
      *
      *===================================================================
     F/EJECT
      *===================================================================
     FLEFCUPL2IP  E           K        DISK         KINFSR *PSSR
      *
     FLEFEED  UF  E           K        DISK         KINFSR *PSSR
      *
     FLE3650P1O   E             81     PRINTER      KINFSR *PSSR
     F                                              KINFDS SPOOL1
      *===================================================================
     E/EJECT
      *===================================================================
     E                    CPY@    1   1 80               Object Copyright
      /COPY ZSRSRC,ZSEFMT1
      *===================================================================
     I/EJECT
      *===================================================================
     IPSDS       SDS
     I                                        1  10 D@PGMN
      * Program Name
      *-------------------------------------------------------------------
     ILDA       E DSLDA                         256
      * Dataarea: Local Data Area.
      *-------------------------------------------------------------------
     IDSFDY     E DSDSFDY
      *First DS for Access Programs, short data structure.
      *-------------------------------------------------------------------
     IDSSDY     E DSDSSDY
      *Second DS for Access Programs, long data structure.
      *-------------------------------------------------------------------
     IDSBANK    E DSSDBANKPD
      * Definition: Bank Details Accessed Via Access Program.
      *-------------------------------------------------------------------
     IDSGELR    E DSSDGELRPD
      * Definition: General Ledger Details Accessed Via Access Program.
      *-------------------------------------------------------------------
     IDSCURR    E DSSDCURRPD
      * Definition: Currencies Accessed Via Access Program.
      *-------------------------------------------------------------------
     IDSKITE      DS                             90
      * Datastructure: Key - LEFEED
     I                                        1   3 KBRCA
     I**********                              4   90KCNUM                                     CSD027
     I                                        4   9 KCNUM                                     CSD027
     I                                       10  140KFACT
     I**********                             15  200KLOAN                                     CLE148
     I                                       15  20 KLOAN                                     CLE148
      *-------------------------------------------------------------------
     I            DS
      * Datastructure: Concatenate key fields.
     I                                        1   50DSFACT
     I                                        1   30FSFACT
     I                                        4   50FSFCNO
      *-------------------------------------------------------------------
     ISPOOL1      DS
      * File information data structure.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
      *-------------------------------------------------------------------
      /COPY ZSRSRC,ZSEFMT2
      *===================================================================
     C/EJECT
      *===================================================================
      * Main Processing Control.
      *
      *-------------------------------------------------------------------
      * Process Selected Facilities.
      *
     C                     EXSR PIFCUP                     Proc Record
      *
     CLR                   EXSR PTTLR                      Proc Last Rec
      *===================================================================
     C/EJECT
     C           PIFCUP    BEGSR
      *===================================================================
      * PIFCUP: Process Facilities selected for conversion.
      *
      *-------------------------------------------------------------------
      * Set up key for LEFEED
      *
     C                     MOVE FSBRCA    KBRCA            Set Branch
     C                     MOVE FSCNUM    KCNUM            Set Cust No
     C                     MOVE DSFACT    KFACT            Set Facilitye
     C**********           Z-ADD0         KLOAN            Reset Loan No                      CLE148
     C                     MOVEL*BLANKS   KLOAN                                               CLE148
      *-------------------------------------------------------------------
      * Position File.
      *
     C           KLEFE     SETLLLEFEED                     Postn File
     C           KLEFE     READELEFEED                 2122Rtv Record
      *
     C           *IN21     IFEQ *ON                        If Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'LEFEED'  DBFILE    P       Set File
     C                     MOVELDSKITE    DBKEY     P       Set Key
     C                     Z-ADD010       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * Set Fee Exchange Rates.                                        .
      *
     C           *IN22     DOWEQ*OFF                       DoWhile Rec
      *
     C           FERECI    IFEQ 'D'                         If Live
     C                     Z-ADDFEERAT    WKERAT 138         Set Orig Ex
     C                     MOVE FEMDIN    WKEIND  1          Set Orig Ind
      *                                                                                       199694
      * Don't convert rate if fee does not already have one:                                  199694
     C           FEERAT    IFNE 0                                                             199694
     C                     MOVE FEFCCY    WKCCY   3          Set Ccy
     C                     EXSR XRCY20                       Obtain CcyDet
     C                     MOVE A6INCY    WKINCY  1          Set IN Ccy
     C***********WKINCY    IFEQ 'Y'                          If IN Ccy    144196
     C           FEFCCY    IFEQ BKEURO                        If Euro Ccy
     C                     Z-ADD1         FEERAT               Set Rate
     C***********          MOVE 'M'       FEMDIN               Set Ex Ind 144196
     C                     MOVE 'D'       FEMDIN               Set Ex Ind 144196
     C                     ELSE                               Else
     C           WKINCY    IFEQ 'Y'                          If IN Ccy    144196
     C                     Z-ADD0         WKAMT                Reset Amt
     C                     EXSR XCAM01                         Cvt Amt
     C                     Z-ADDP@EXRT    FEERAT               Set Rate
      * When converting from EUR to 'In' currency, EU0200 does not                            199694
      * reverse multiply/divide indicator so need to swap it now                              199694
     C           P@MDIN    IFEQ 'M'                                                           199694
     C                     MOVE 'D'       WMDIN   1                                           199694
     C                     ELSE                                                               199694
     C                     MOVE 'M'       WMDIN                                               199694
     C                     ENDIF                                                              199694
      *                                                                                       199694
     C                     MOVE WMDIN     FEMDIN               Set Ex Ind                     199694
     C**********           MOVE P@MDIN    FEMDIN               Set Ex Ind                     199694
     C***********          ENDIF                              End If      144196
     C                     ELSE                              Else
     C**********           Z-ADDA6SPRT    ZRATE1 138          Set Rate                        199694
     C**********           MOVE A6MDIN    ZMDI1   1           Set MD Ind                      199694
     C**********           MOVE BKEURO    WKCCY               Set Ccy                         199694
     C**********           EXSR XRCY20                        Obtain Ccy                      199694
     C**********           Z-ADDA6SPRT    ZRATE2 138          Set Rate                        199694
     C**********           MOVE A6MDIN    ZMDI2   1           Set MD Ind                      199694
     C**********           Z-ADD0         ZRATEX 138          Reset Rate                      199694
     C**********           MOVE *BLANK    ZMDIX   1           Clr MD Ind                      199694
     C**********           EXSR ZXRATE                        Obtain Rate                     199694
      *  Else, fee currency is not an 'In' currency, so calculate                             199694
      *  new rate using old value of FEERAT:                                                  199694
     C                     MOVE FSOCCY    WKCCY               Old Fac Ccy                     199694
     C                     EXSR XRCY20                        Obtain Ccy                      199694
     C                     Z-ADDA6EUER    ZRATE1 138          Set Rate                        199694
     C           A6EUMD    IFEQ 'D'                                                           199694
     C                     MOVE 'M'       ZMDI1   1                                           199694
     C                     ELSE                                                               199694
     C                     MOVE 'D'       ZMDI1                                               199694
     C                     ENDIF                                                              199694
     C                     Z-ADDFEERAT    ZRATE2 138          Set Rate                        199694
      *                                                                                       199694
     C           FEMDIN    IFEQ 'M'                                                           199694
     C                     MOVE 'D'       ZMDI2   1                                           199694
     C                     ELSE                                                               199694
     C                     MOVE 'M'       ZMDI2                                               199694
     C                     ENDIF                                                              199694
      *                                                                                       199694
     C                     Z-ADD0         ZRATEX 138          Reset Rate                      199694
     C                     MOVE *BLANK    ZMDIX   1           Clr MD Ind                      199694
     C                     EXSR ZXRATE                        Obtain Rate                     199694
     C                     Z-ADDZRATEX    FEERAT              Set RateOfEx
     C                     MOVE ZMDIX     FEMDIN              Set Ex Ind
     C                     ENDIF                             End If
     C                     ENDIF                             End If       144196
      *
     C                     EXSR XULEFE                       Upd Record
      *    End of "If fee has non-zero FEERAT".                                               199694
     C                     ENDIF                                                              199694
      *
     C                     EXSR XSP1D1                       Set Prt Rcd
      *
     C                     ENDIF                            End If
      *
     C           KLEFE     READELEFEED                 2122 Rtv Record
      *
     C           *IN21     IFEQ *ON                         If Error
     C           *LOCK     IN   LDA                          Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P        Set Pgm
     C                     MOVEL'LEFEED'  DBFILE    P        Set File
     C                     MOVELDSKITE    DBKEY     P        Set Key
     C                     Z-ADD020       DBASE              Set Ref Point
     C                     OUT  LDA                          Wrt Record
     C                     EXSR *PSSR                        Proc Error
     C                     ENDIF                            End If
      *
     C                     ENDDO                           End Do
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           PTTLR     BEGSR
      *===================================================================
      * PTTLR : Last Record Total Time.
      *
      *-------------------------------------------------------------------
      * Write End of Report.
      *
     C           *IN81     IFEQ *ON                        If Overflow
     C                     MOVE *OFF      *IN81             Reset Overflow
     C                     WRITEP1H010                 21   Wrt Hdr
      *
     C           *IN21     IFEQ *ON                         If File Error
     C           *LOCK     IN   LDA                          Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P        Set Pgm
     C                     MOVEL'LE3650P1'DBFILE    P        Set File
     C                     MOVEL'P1H010'  DBKEY     P        Set Key
     C                     Z-ADD030       DBASE              Set Ref Point
     C                     OUT  LDA                          Wrt Record
     C                     EXSR *PSSR                        Proc Error
     C                     ENDIF                            End If
     C                     ENDIF                           End If
      *
     C           WKDET     IFEQ 'Y'                        If Details Rptd
     C                     WRITEP1EOR                       Wrt End Of Rpt
     C                     ELSE                            Else
     C                     WRITEP1NDR                       Wrt No DetRptd
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           XCAM01    BEGSR
      *===================================================================
      * XCAM01: Convert Amounts Into Euro Equivalent.
      *
      *-------------------------------------------------------------------
      * Use program EU0200 to convert amounts into Euro.
      *
     C                     Z-ADDWKAMT     P@FRAM 183       Set Parm Amt
      *
     C                     CALL 'EU0200'               2121Cvt Amount
     C                     PARM *BLANKS   P@RTCD  7        -O:Return code
     C                     PARM           P@FRAM           -I:Fr Amount
     C***********          PARM FEFCCY    P@FRCY  3        -I:Fr Ccy      143512
     C***********          PARM BKEURO    P@TOCY  3        -O:To Ccy      143512
     C                     PARM BKEURO    P@FRCY  3        -I:Fr Ccy      143512
     C                     PARM FEFCCY    P@TOCY  3        -O:To Ccy      143512
     C                     PARM           P@OUTA 150       -O:Outright Amt
     C                     PARM           P@INTA 183       -O:Interim Amt
     C                     PARM           P@EXRT 159       -O:Exchange Rate
     C                     PARM           P@MDIN  1        -O:Mult/Div
      *
     C           P@RTCD    IFNE *BLANKS                    If Rtn Error
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'EU0200'  DBFILE    P       Set File
     C                     MOVEL'*FIRST'  DBKEY     P       Set Key
     C                     Z-ADD040       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *
     C                     Z-ADDP@OUTA    WKAMT  150       Set Rtn Amount
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           XRCY20    BEGSR
      *===================================================================
      * XRCY20: Retrieve Currency Details.
      *
      *-------------------------------------------------------------------
      *
     C                     CALL 'AOCURRR0'             2121Rtv Currencies
     C                     PARM *BLANKS   P@RTCD  7        -O:Return code
     C                     PARM '*KEY   ' P@OPTN  7        -I:Option
     C                     PARM WKCCY     P@CYCD  3        -I:Currency
     C           DSCURR    PARM *BLANKS   DSSDY            -O:Format
      *
     C           *IN21     IFEQ *ON                        If Call Error
     C           P@RTCD    OREQ '*ERROR*'                  Or Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'AOCURRR0'DBFILE    P       Set File
     C                     MOVEL'*KEY  '  DBKEY     P       Set Key
     C                     Z-ADD050       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
     C/EJECT
      /COPY ZSRSRC,ZXRATEZ1
     C/EJECT
     C           XULEFE    BEGSR
      *===================================================================
      * XULEFE: Update Midas LE Facility Fees File.
      *
      *-------------------------------------------------------------------
      * Update Live Utilisation Details File.
      *
     C                     UPDATLEFEEDF                21  Upd Record
      *
     C           *IN21     IFEQ *ON                        If Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'LEFEEDF' DBFILE    P       Set File
     C                     MOVELDSKITE    DBKEY     P       Set Key
     C                     Z-ADD060       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           XSP1D1    BEGSR
      *===================================================================
      * XSP1D1 Set Printer(P1) Details.
      *
      *-------------------------------------------------------------------
      * Set Facilities Data.
      *
     C                     MOVE FSCNUM    P1CNUM           Set Customer
     C                     MOVE FSFACT    P1FACT           Set Fac Type
     C                     MOVE FSFCNO    P1FCNO           Set Fac Seq
      *
     C                     MOVE FEFSEQ    P1FSEQ           Set Fee Seq    144196
     C                     MOVE FEFCCY    P1FCCY           Set Fee Ccy    144196
      *                                                                   144196
     C                     MOVE WKERAT    ZRT13            Set Rate
     C                     MOVE *BLANKS   ZPBAS            Clr Per Base
     C                     MOVE *BLANKS   ZRT13A           Clr Out Fmt
     C                     EXSR ZSEFMT                     Edit Rate
     C                     MOVE ZRT13A    P1OEXR           Set OrigVal
      *
     C                     MOVE WKEIND    P1OEXI           Set Orig Ind
      *
     C                     MOVE FEERAT    ZRT13            Set Rate
     C                     MOVE *BLANKS   ZPBAS            Clr Per Base
     C                     MOVE *BLANKS   ZRT13A           Clr Out Fmt
     C                     EXSR ZSEFMT                     Edit Rate
     C                     MOVE ZRT13A    P1CEXR           Set ConvVal
      *
     C                     MOVE FEMDIN    P1CEXI           Set Conv Ind
      *
     C                     EXSR XWP1W1                     Prt Detail
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           XWP1W1    BEGSR
      *===================================================================
      * XWP1W1: Write Printer(P1) Details.
      *
      *-------------------------------------------------------------------
      * Write Printer(P1) Detail Line.
      *
     C           *IN81     IFEQ *ON                        If Overflow
     C                     MOVE *OFF      *IN81             Reset Overflow
     C                     WRITEP1H010                 21   Wrt Hdr
      *
     C           *IN21     IFEQ *ON                         If File Error
     C           *LOCK     IN   LDA                          Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P        Set Pgm
     C                     MOVEL'LE3650P1'DBFILE    P        Set File
     C                     MOVEL'P1H010'  DBKEY     P        Set Key
     C                     Z-ADD070       DBASE              Set Ref Point
     C                     OUT  LDA                          Wrt Record
     C                     EXSR *PSSR                        Proc Error
     C                     ENDIF                            End If
     C                     ENDIF                           End If
      *
     C                     WRITEP1D010                 21  Wrt Details
      *
     C           *IN21     IFEQ *ON                        If File Error
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'LE3650P1'DBFILE    P       Set File
     C                     MOVEL'P1D010'  DBKEY     P       Set Key
     C                     Z-ADD080       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *
     C                     MOVE 'Y'       WKDET            Set Details Flg
      *===================================================================
     C                     ENDSR
     C/EJECT
      /COPY ZSRSRC,ZSEFMT3
     C/EJECT
     C           *INZSR    BEGSR
      *===================================================================
      * *INZSR: Initialization Routine.
      *
      *-------------------------------------------------------------------
      * Initialise Parameters, Work Fields, Etc.
      *
     C                     MOVEACPY@      MKI@@  80        Set Copyright
     C                     MOVE 'N'       WKDET   1        Set Details Flg
     C                     MOVE *ON       *IN81            Set Prt Hd Ind
      *-------------------------------------------------------------------
      * Retrieve Bank Details.
      *
     C                     CALL 'AOBANKR0'             2121Access Bank
     C                     PARM *BLANKS   P@RTCD  7        -O:Return Code
     C                     PARM '*KEY   ' P@OPTN  7        -I:Option
     C           DSBANK    PARM *BLANKS   DSFDY            -O:Format
      *
     C           *IN21     IFEQ *ON                        If Call Error
     C           P@RTCD    OREQ '*ERROR*'                  Or Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'AOBANKR0'DBFILE    P       Set File
     C                     MOVELP@OPTN    DBKEY     P       Set Key
     C                     Z-ADD090       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * Retrieve General Ledger Details (suspense account code).
      *
     C**********           CALL 'AOGELRR0'             2121Rtv GL ICD Dtl                     CGL029
     C                     CALL 'AOGELRR1'             2121                                   CGL029
     C                     PARM *BLANKS   P@RTCD  7        -O:Return Code
     C                     PARM '*KEY   ' P@OPTN  7        -I:Option
     C********** DSGELR    PARM *BLANK    DSFDY            -O:Format                          CGL029
     C           DSGELR    PARM *BLANK    DSSDY                                               CGL029
      *
     C           *IN21     IFEQ *ON                        If Call Error
     C           P@RTCD    ORNE *BLANKS                    Or Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'SDGELRPD'DBFILE    P       Set File
     C                     MOVE P@OPTN    DBKEY     P       Set Key
     C                     Z-ADD100       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * Set Spool File Number And CALL ZSFILE.
      *
     C                     Z-ADDSFNUM1    SPLNO            Set Spool No
      *
     C                     CALL 'ZSFILE'                   Call RCF
     C                     PARM           P@RSEQ           -O:Rpt Seq
     C                     PARM *BLANKS   RENT    3        -I:Entity
     C                     PARM           SFILE1           -I:Spool Name
     C                     PARM           SPLNO   60       -I:Spool Number
     C                     PARM *BLANKS   ZERR    1        -O:Return Code
      *
     C           ZERR      IFEQ 'Y'                        If Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'ZSFILE  'DBFILE    P       Set File
     C                     MOVELSFILE1    DBKEY     P       Set Key
     C                     Z-ADD110       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           *PSSR     BEGSR
      *===================================================================
      * *PSSR: Error Handling.
      *
      *-------------------------------------------------------------------
      * Check To See That *PSSR Has Not Been Called Yet.
      *
     C           WKRUN     IFEQ *BLANK                     If Not Exec
     C                     MOVE 'Y'       WKRUN   1         Set Off Ind
     C                     DUMP                             Dmp Pgm
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * If *PSSR Already Run, Then Just End The Program Here.
      *
     C                     MOVE *ON       *INU7            Set U7 Ind
     C                     MOVE *ON       *INU8            Set U8 Ind
     C                     MOVE *ON       *INLR            Set EndPgm Ind
     C                     RETRN                           Exit Program
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           @DEFN     BEGSR
      *===================================================================
      * @DEFN: Definition Routine (Not Called).
      *
      *-------------------------------------------------------------------
      * Dataareas...
      *
     C           *NAMVAR   DEFN           LDA              Dcl LDA
      *-------------------------------------------------------------------
      * Lists...
      *
     C           KLEFE     KLIST                           Key: LEFEED
     C                     KFLD           KBRCA            -Branch
     C                     KFLD           KCNUM            -Customer No
     C                     KFLD           KFACT            -Facility
     C                     KFLD           KLOAN            -Loan No
      *
     C           *ENTRY    PLIST                           Prg: Entry
     C                     PARM           P@RSEQ  5        -Report Seq
      *===================================================================
     C                     ENDSR
     C/EJECT
** CPY@: Object Copyright
(c) Finastra International Limited 2001
