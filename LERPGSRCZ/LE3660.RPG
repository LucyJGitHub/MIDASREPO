     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Facility currency conv. - history header')    *
      *****************************************************************
      *
      *  Midas   -  Lending Module
      *
      *  LE3660  -  Facility Currency Conversion - History Header
      *
      *  Function:  This Close of Business program will update
      *             facility history headers for the selected
      *             facilities. It will convert the exchange rate to
      *             the cross rate between the Facility Currency and
      *             Euro as well as setting the multiply/divide
      *             indicator between the Facility Currency and Euro.
      *             Facility amounts (Original Facility Amount, Last
      *             Facility Amount and Last Total Drawn) are also
      *             converted to the Euro equivalent. All changes are
      *             detailed on an audit report.
      *
      *  (c) Finastra International Limited 2001                      *
      *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE134             Date 01Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CLE023             Date 15Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006             Date 15May01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 150150             Date 08Jan99               *
      *                 144197             Date 07Oct98               *
      *                 CEU021  *CREATE    Date 01Apr98
      *
      *-------------------------------------------------------------------
      *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE023 - Lending Facility History Improvements. (Recompiled) *
      *  CSD006 - Use long DS with SDCURRPD.
      *  150150 - This fix is to remove 144197 from this program
      *           effectively restoring it to a previous version.
      *           This is because the original problem has been
      *           solved by 148813 in LE3600.
      *           The code has been physically backed out rather than
      *           commented out to make the resulting code more
      *           readable. This has only been allowed because this
      *           fix has not yet been released in an Interim Release.
      *  144197 - Amounts in FACHISA/H are incorrectly converted      *
      *  CEU021 - EMU LE Facility Currency Conversion
      *
      *****************************************************************
      *  Indicator    Description
      *  ¯¯¯¯¯¯¯¯¯    ¯¯¯¯¯¯¯¯¯¯¯
      *  01 - 19      Input Control.
      *  20 - 29      I/O Conditioning (ie. CHAIN, LOKUP etc...)
      *   20          Record Not Found
      *   21          File Error Occurred
      *   22          Start/End Of File Reached
      *   25          Lookup/Scan.
      *  30 - 39      Program Work Indicators
      *  40 - 49      Output Control.
      *  50 - 89      Output Conditioning (ie. Cursor Positioning)
      *  90 - 99      Error Control.
      *
      *-------------------------------------------------------------------
      *  Routine      Description
      *  ¯¯¯¯¯¯¯      ¯¯¯¯¯¯¯¯¯¯¯
      *  PIFCUP       Read Facilities Selected for Conversion
      *  PTTLR        Last Record Total Time
      *  XUFACH       Update History Header
      *  XCAM01       Convert Amounts To Euro
      *  XSP1D1       Set Details Record For Report
      *  XWP1W1       Write Details Report Record
      *  *INZSR       Initial Routine
      *  *PSSR        Program Error Routine
      *  @DEFN        Definition Routine (Not Called)
      *
      *-------------------------------------------------------------------
      *  File         Description
      *  ¯¯¯¯         ¯¯¯¯¯¯¯¯¯¯¯
      *  LEFCUPL2     Midas LE Fac. Selected for Cy Conv. Branch/Fac.
      *  FACHISHL     Midas LE Fees Facility History Header
      *  LE3660P1     Midas LE Facility Cy Conv. History Header Report
      *
      *===================================================================
     F/EJECT
      *===================================================================
     FLEFCUPL2IP  E           K        DISK         KINFSR *PSSR
      *
     FFACHISHLUF  E           K        DISK         KINFSR *PSSR
      *
     FLE3660P1O   E             81     PRINTER      KINFSR *PSSR
     F                                              KINFDS SPOOL1
      *===================================================================
     E/EJECT
      *===================================================================
     E                    CPY@    1   1 80               Object Copyright
      /COPY ZSRSRC,ZFRPEDZ1
      *===================================================================
     I/EJECT
      *===================================================================
     IPSDS       SDS
     I                                        1  10 D@PGMN
      * Program Name
      *-------------------------------------------------------------------
     ILDA       E DSLDA                         256
      * Dataarea: Local Data Area.
      *-------------------------------------------------------------------
     IDSFDY     E DSDSFDY
      *First DS for Access Programs, short data structure.
      *-------------------------------------------------------------------
     IDSSDY     E DSDSSDY
      *Second DS for Access Programs, long data structure.
      *-------------------------------------------------------------------
     IDSBANK    E DSSDBANKPD
      * Definition: Bank Details Accessed Via Access Program.
      *-------------------------------------------------------------------
     IDSGELR    E DSSDGELRPD
      * Definition: General Ledger Details Accessed Via Access Program.
      *-------------------------------------------------------------------
     IDSFACT    E DSSDFACTPD
      * Definition: Facility Types Accessed Via Access Program.
      *-------------------------------------------------------------------
     IDSCURR    E DSSDCURRPD
      * Definition: Currencies Accessed Via Access Program.
      *-------------------------------------------------------------------
     IDSKITE      DS                             90
      * Datastructure: Key - FACHISHL.
     I                                        1   3 KBRCH
     I**********                              4   90KCNUM                                     CSD027
     I                                        4   9 KCNUM                                     CSD027
     I                                       10  120KFTYP
     I                                       13  140KFSEQ
      *-------------------------------------------------------------------
     IDSCON       DS
      * Datastructure: FACHISHL Constants.
     I                                        1   1 DSMCCY
     I                                        2   4 DSFCCY
      *-------------------------------------------------------------------
     ISPOOL1      DS
      * File information data structure.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
      *-------------------------------------------------------------------
      /COPY ZSRSRC,ZFRPEDZ2
      *===================================================================
     C/EJECT
      *===================================================================
      * Main Processing Control.
      *
      *-------------------------------------------------------------------
      * Process Selected Facilities.
      *
     C                     EXSR PIFCUP                     Proc Record
      *
     CLR                   EXSR PTTLR                      Proc Last Rec
      *===================================================================
     C/EJECT
     C           PIFCUP    BEGSR
      *===================================================================
      * PIFCUP: Process Facilities selected for conversion.
      *
      *-------------------------------------------------------------------
      * Set up key for FACHISHL.
      *
     C                     MOVE FSBRCA    KBRCH            Set Brch Code
     C                     MOVE FSCNUM    KCNUM            Set Cust No
     C                     MOVE FSFACT    KFTYP            Set Fac type
     C                     MOVE FSFCNO    KFSEQ            Set Fac Seq
      *-------------------------------------------------------------------
      * Position File.
      *
     C           KFACHI    SETLLFACHISHL                   Postn File
     C           KFACHI    READEFACHISHL               2122Rtv Record
      *
     C           *IN21     IFEQ *ON                        If Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm Nm
     C                     MOVEL'FACHISHL'DBFILE    P       Set File Nm
     C                     MOVELDSKITE    DBKEY     P       Set Key
     C                     Z-ADD010       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * Set Midas LE Fees Facility History Header Record.
      *
     C           *IN22     DOWEQ*OFF                       DoWhile Rec
     C                     Z-ADDFHOFAM    WKOFAM 150        Set Prt Orig Am
      *
     C                     Z-ADDFHOFAM    WKAMT             Set ParmOrig Am
     C                     EXSR XCAM01                      Cvt Amount
     C                     Z-ADDWKAMT     FHOFAM            Set Orig Amt
      *
     C                     Z-ADDFHLFAM    WKLFAM 150        Set Prt Last Amt
      *
     C                     Z-ADDFHLFAM    WKAMT             Set Parm Last Amt
     C                     EXSR XCAM01                      Cvt Amount
     C                     Z-ADDWKAMT     FHLFAM            Set Last Fac Amt
      *
     C                     Z-ADDFHLDAM    WKLDAM 150        Set Prt Dr Amt
      *
     C                     Z-ADDFHLDAM    WKAMT             Set Parm Dr Amt
     C                     EXSR XCAM01                      Cvt Amount
     C                     Z-ADDWKAMT     FHLDAM            Set Drawn Amt
      *
     C                     MOVE FHFCCY    WKFCCY  3         Set Fac Ccy
     C                     MOVE FHMCCY    WKMCCY  1         Set Multi Ccy
     C                     MOVE DSFCCY    FHFCCY            Set MultiCcy
     C                     MOVE DSMCCY    FHMCCY            Set Euro Ccy
      *
     C                     EXSR XUFACH                      Wrt Record
      *
     C                     EXSR XSP1D1                      Set Prt Record
      *
     C                     EXSR XWP1W1                      Wrt Prt Record
      *
     C           KFACHI    READEFACHISHL               2122 Rtv Record
      *
     C           *IN21     IFEQ *ON                         If Error
     C           *LOCK     IN   LDA                          Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P        Set Pgm
     C                     MOVEL'FACHISHL'DBFILE    P        Set File
     C                     MOVELDSKITE    DBKEY     P        Set Key
     C                     Z-ADD020       DBASE              Set Ref Point
     C                     OUT  LDA                          Wrt Record
     C                     EXSR *PSSR                        Proc Error
     C                     ENDIF                            End If
     C                     ENDDO                           End DoWhile
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           PTTLR     BEGSR
      *===================================================================
      * PTTLR : Last Record Total Time.
      *
      *-------------------------------------------------------------------
      * Write End of Report.
      *
     C           *IN81     IFEQ *ON                        If Overflow
     C                     MOVE *OFF      *IN81             Reset Overflow
     C                     WRITEP1H010                 21   Wrt Hdr
      *
     C           *IN21     IFEQ *ON                         If File Error
     C           *LOCK     IN   LDA                          Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P        Set Pgm
     C                     MOVEL'LE3660P1'DBFILE    P        Set File
     C                     MOVEL'P1H010'  DBKEY     P        Set Key
     C                     Z-ADD030       DBASE              Set Ref Point
     C                     OUT  LDA                          Wrt Record
     C                     EXSR *PSSR                        Proc Error
     C                     ENDIF                            End If
     C                     ENDIF                           End If
      *
     C           WKDET     IFEQ 'Y'                        If Details Rptd
     C                     WRITEP1EOR                       Wrt End Of Rpt
     C                     ELSE                            Else
     C                     WRITEP1NDR                       Wrt No DetRptd
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           XCAM01    BEGSR
      *===================================================================
      * XCAM01: Convert Amounts Into Euro Equivalent.
      *
      *-------------------------------------------------------------------
      * Use program EU0200 to convert amounts into Euro.
      *
     C                     Z-ADDWKAMT     P@FRAM 183       Set Parm Amt
      *
     C                     CALL 'EU0200'                   Cvt Amount
     C                     PARM *BLANKS   P@RTCD  7        -O:Return code
     C                     PARM           P@FRAM           -I:Fr Amount
     C                     PARM FSOCCY    P@FRCY  3        -I:Fr Ccy
     C                     PARM BKEURO    P@TOCY  3        -O:To Ccy
     C                     PARM           P@OUTA 150       -O:Outright Amt
     C                     PARM           P@INTA 183       -O:Interim Amt
     C                     PARM           P@EXRT 159       -O:Exchange Rate
     C                     PARM           P@MDIN  1        -O:Mult/Div
      *
     C           P@RTCD    IFNE *BLANKS                    If Rtn Error
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'EU0200'  DBFILE    P       Set File
     C                     MOVEL'*FIRST'  DBKEY     P       Set Key
     C                     Z-ADD040       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *
     C                     Z-ADDP@OUTA    WKAMT  150       Set Rtn Amount
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           XUFACH    BEGSR
      *===================================================================
      * XUFACH: Update Midas LE Fees Facility History Header Record.
      *
      *-------------------------------------------------------------------
      * Update Facility History Header.
      *
     C                     UPDATFACHISHF               21  Upd Record
      *
     C           *IN21     IFEQ *ON                        If Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'FACHISHF'DBFILE    P       Set File
     C                     MOVELDSKITE    DBKEY     P       Set Key
     C                     Z-ADD050       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           XSP1D1    BEGSR
      *===================================================================
      * XSP1D1 Set Printer(P1) Details.
      *
      *-------------------------------------------------------------------
      * Set Facilities Data.
      *
     C                     MOVE FSCNUM    P1CNUM           Set Customer
     C                     MOVE FSFACT    WKFACT  3        Set Fac type
     C                     MOVE WKFACT    P1FACT           Set Facility
     C                     MOVE FSFCNO    P1FCNO           Set Sequence
      *-------------------------------------------------------------------
      * Retrieve Facility Types.
      *
     C                     CALL 'AOFACTR0'             2121Rtv Fac types
     C                     PARM *BLANKS   P@RTCD  7        -O:Return code
     C                     PARM '*KEY   ' P@OPTN  7        -I:Option
     C                     PARM WKFACT    P@FCTY  3        -I:Fac type
     C           DSFACT    PARM *BLANKS   DSFDY            -O:Format
      *
     C           *IN21     IFEQ *ON                        If Call Error
     C           P@RTCD    OREQ '*ERROR*'                  Or Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'AOFACTR0'DBFILE    P       Set File
     C                     MOVEL'*FIRST'  DBKEY     P       Set Key
     C                     Z-ADD060       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *
     C                     MOVE AMFCNM    P1CNAM           Set Name
      *-------------------------------------------------------------------
      * Retrieve Currencies.
      *
     C                     CALL 'AOCURRR0'             2121Rtv Currencies
     C                     PARM *BLANKS   P@RTCD  7        -O:Return code
     C                     PARM '*KEY   ' P@OPTN  7        -I:Option
     C                     PARM WKFCCY    P@CYCD  3        -I:Currency
     C********** DSCURR    PARM *BLANKS   DSFDY            -O:Format            CSD006
     C           DSCURR    PARM *BLANKS   DSSDY            -O:Format            CSD006
      *
     C           *IN21     IFEQ *ON                        If Call Error
     C           P@RTCD    OREQ '*ERROR*'                  Or Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'AOCURRR0'DBFILE    P       Set File
     C                     MOVEL'*FIRST'  DBKEY     P       Set Key
     C                     Z-ADD070       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * Set Original Amounts.
      *
     C                     MOVE WKFCCY    P1OCCY           Set Orig Ccy
     C                     MOVE A6CYNM    P1OCYN           Set Orig Name
      *
     C                     MOVE WKMCCY    P1OCI            Set Prt Multi Ccy
      *
     C           WKMCCY    IFEQ 'Y'                        If Multi Ccy
     C                     MOVEL'YES'     P1OCIN    P       Set Multi Ccy Narr
     C                     ELSE                            Else
     C                     MOVEL'NO'      P1OCIN    P       Set Multi Ccy Narr
     C                     ENDIF                           End If
      *
     C                     Z-ADDWKOFAM    ZFLD15           Set Amount
     C                     Z-ADDA6NBDP    ZDECS            Set Dec Pos
     C                     MOVE *BLANKS   ZECODE           Clr Edit Cde
     C                     EXSR ZFRPED                     Edit Amount
     C                     MOVE ZOUT25    P1OOAM           Set Prt Orig Amt
      *
     C                     Z-ADDWKLFAM    ZFLD15           Set Amount
     C                     Z-ADDA6NBDP    ZDECS            Set Dec Pos
     C                     MOVE *BLANKS   ZECODE           Clr Edit Cde
     C                     EXSR ZFRPED                     Edit Amount
     C                     MOVE ZOUT25    P1OLAM           Set Prt LastAmt
      *
     C                     Z-ADDWKLDAM    ZFLD15           Set Amount
     C                     Z-ADDA6NBDP    ZDECS            Set Dec Pos
     C                     MOVE *BLANKS   ZECODE           Clr Edit cde
     C                     EXSR ZFRPED                     Edit Amount
     C                     MOVE ZOUT25    P1ODAM           Set Prt Drawn Amt
      *-------------------------------------------------------------------
      * Set Converted Amounts.
      *
     C                     CALL 'AOCURRR0'             2121Access Currencies
     C                     PARM *BLANKS   P@RTCD  7        -O:Return code
     C                     PARM '*KEY   ' P@OPTN  7        -I:Option
     C                     PARM DSFCCY    P@CYCD  3        -I:Currency
     C********** DSCURR    PARM *BLANKS   DSFDY            -O:Format            CSD006
     C           DSCURR    PARM *BLANKS   DSSDY            -O:Format            CSD006
      *
     C           *IN21     IFEQ *ON                        If Call Error
     C           P@RTCD    OREQ '*ERROR*'                  Or Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'AOCURRR0'DBFILE    P       Set File
     C                     MOVEL'*FIRST'  DBKEY     P       Set Key
     C                     Z-ADD080       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *
     C                     MOVE DSFCCY    P1CCCY           Set Orig Ccy
     C                     MOVE A6CYNM    P1CCYN           Set Orig Name
      *
     C                     MOVE DSMCCY    P1CCI            Set Prt Multi Ccy
      *
     C           DSMCCY    IFEQ 'Y'                        If Multi Ccy
     C                     MOVEL'YES'     P1CCIN    P       Set Multi Ccy Narr
     C                     ELSE                            Else
     C                     MOVEL'NO'      P1CCIN    P       Set Multi Ccy Narr
     C                     ENDIF                           End If
      *
     C                     Z-ADDFHOFAM    ZFLD15           Set Amount
     C                     Z-ADDA6NBDP    ZDECS            Set Dec Pos
     C                     MOVE *BLANKS   ZECODE           Clr Edit Cde
     C                     EXSR ZFRPED                     Edit Amount
     C                     MOVE ZOUT25    P1COAM           Set Prt Orig Amt
      *
     C                     Z-ADDFHLFAM    ZFLD15           Set Amount
     C                     Z-ADDA6NBDP    ZDECS            Set Dec Pos
     C                     MOVE *BLANKS   ZECODE           Clr Edit Cde
     C                     EXSR ZFRPED                     Edit Amount
     C                     MOVE ZOUT25    P1CLAM           Set Prt LastAmt
      *
     C                     Z-ADDFHLDAM    ZFLD15           Set Amount
     C                     Z-ADDA6NBDP    ZDECS            Set Dec Pos
     C                     MOVE *BLANKS   ZECODE           Clr Edit Cde
     C                     EXSR ZFRPED                     Edit Amount
     C                     MOVE ZOUT25    P1CDAM           Set Prt Drawn Amt
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           XWP1W1    BEGSR
      *===================================================================
      * XWP1W1: Write Printer(P1) Details.
      *
      *-------------------------------------------------------------------
      * Write Printer(P1) Detail Line.
      *
     C           *IN81     IFEQ *ON                        If Overflow
     C                     MOVE *OFF      *IN81             Reset Overflow
     C                     WRITEP1H010                 21   Wrt Hdr
      *
     C           *IN21     IFEQ *ON                         If File Error
     C           *LOCK     IN   LDA                          Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P        Set Pgm
     C                     MOVEL'LE3660P1'DBFILE    P        Set File
     C                     MOVEL'P1H010'  DBKEY     P        Set Key
     C                     Z-ADD090       DBASE              Set Ref Point
     C                     OUT  LDA                          Wrt Record
     C                     EXSR *PSSR                        Proc Error
     C                     ENDIF                            End If
     C                     ENDIF                           End If
      *
     C                     WRITEP1D010                 21  Wrt Details
      *
     C           *IN21     IFEQ *ON                        If File Error
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'LE3660P1'DBFILE    P       Set File
     C                     MOVEL'P1D010'  DBKEY     P       Set Key
     C                     Z-ADD100       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *
     C                     MOVE 'Y'       WKDET            Set Details Flg
      *===================================================================
     C                     ENDSR
     C/EJECT
      /COPY ZSRSRC,ZFRPEDZ3
     C/EJECT
     C           *INZSR    BEGSR
      *===================================================================
      * *INZSR: Initialization Routine.
      *
      *-------------------------------------------------------------------
      * Initialise Parameters, Work Fields, Etc.
      *
     C                     MOVEACPY@      MKI@@  80        Set Copyright
     C                     MOVE 'N'       WKDET   1        Set Details Flg
     C                     MOVE *ON       *IN81            Set Prt Hd Ind
      *-------------------------------------------------------------------
      * Retrieve Bank Details.
      *
     C                     CALL 'AOBANKR0'             2121Access Bank
     C                     PARM *BLANKS   P@RTCD  7        -O:Return Code
     C                     PARM '*KEY   ' P@OPTN  7        -I:Option
     C           DSBANK    PARM *BLANKS   DSFDY            -O:Format
      *
     C           *IN21     IFEQ *ON                        If Call Error
     C           P@RTCD    OREQ '*ERROR*'                  Or Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'AOBANKR0'DBFILE    P       Set File
     C                     MOVEL'*FIRST'  DBKEY     P       Set Key
     C                     Z-ADD110       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * Retrieve General Ledger Details (suspense account code).
      *
     C**********           CALL 'AOGELRR0'             2121Rtv GL ICD Dtl                     CGL029
     C                     CALL 'AOGELRR1'             2121                                   CGL029
     C                     PARM *BLANKS   P@RTCD  7        -O:Return Code
     C                     PARM '*FIRST ' P@OPTN  7        -I:Option
     C********** DSGELR    PARM *BLANK    DSFDY            -O:Format                          CGL029
     C           DSGELR    PARM *BLANK    DSSDY                                               CGL029
      *
     C           *IN21     IFEQ *ON                        If Call Error
     C           P@RTCD    ORNE *BLANKS                    Or Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'SDGELRPD'DBFILE    P       Set File
     C                     MOVE P@OPTN    DBKEY     P       Set Key
     C                     Z-ADD120       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * Set Up Currency Data.
      *
     C                     MOVE 'Y'       DSMCCY           Set MultiCcy
     C                     MOVE BKEURO    DSFCCY           Set Euro Ccy
      *-------------------------------------------------------------------
      * Set Spool File Number And CALL ZSFILE.
      *
     C                     Z-ADDSFNUM1    SPLNO            Set Spool No
      *
     C                     CALL 'ZSFILE'               2121Call RCF
     C                     PARM           P@RSEQ           -O:Rpt Seq
     C                     PARM *BLANKS   RENT    3        -I:Entity
     C                     PARM           SFILE1           -I:Spool Name
     C                     PARM           SPLNO   60       -I:Spool Number
     C                     PARM *BLANKS   ZERR    1        -O:Return Code
      *
     C           ZERR      IFEQ 'Y'                        If Rtn Error
     C           *LOCK     IN   LDA                         Rtv LDA
     C                     MOVELD@PGMN    DBPGM     P       Set Pgm
     C                     MOVEL'ZSFILE  'DBFILE    P       Set File
     C                     MOVELSFILE1    DBKEY     P       Set Key
     C                     Z-ADD130       DBASE             Set Ref Point
     C                     OUT  LDA                         Wrt Record
     C                     EXSR *PSSR                       Proc Error
     C                     ENDIF                           End If
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           *PSSR     BEGSR
      *===================================================================
      * *PSSR: Error Handling.
      *
      *-------------------------------------------------------------------
      * Check To See That *PSSR Has Not Been Called Yet.
      *
     C           WKRUN     IFEQ *BLANK                     If Not Exec
     C                     MOVE 'Y'       WKRUN   1         Set Off Ind
     C                     DUMP                             Dmp Pgm
     C                     ENDIF                           End If
      *-------------------------------------------------------------------
      * If *PSSR Already Run, Then Just End The Program Here.
      *
     C                     MOVE *ON       *INU7            Set U7 Ind
     C                     MOVE *ON       *INU8            Set U8 Ind
     C                     MOVE *ON       *INLR            Set EndPgm Ind
     C                     RETRN                           Exit Program
      *===================================================================
     C                     ENDSR
     C/EJECT
     C           @DEFN     BEGSR
      *===================================================================
      * @DEFN: Definition Routine (Not Called).
      *
      *-------------------------------------------------------------------
      * Dataareas...
      *
     C           *NAMVAR   DEFN           LDA              Dcl LDA
      *-------------------------------------------------------------------
      * Lists...
      *
     C           KFACHI    KLIST                           Key: FACHISHL
     C                     KFLD           KBRCH            -Branch Code
     C                     KFLD           KCNUM            -Customer No
     C                     KFLD           KFTYP            -Fac type
     C                     KFLD           KFSEQ            -Fac seq
      *
     C           *ENTRY    PLIST                           Prg: Entry
     C                     PARM           P@RSEQ  5        -Report Seq
      *===================================================================
     C                     ENDSR
     C/EJECT
** CPY@: Object Copyright
(c) Finastra International Limited 2001
