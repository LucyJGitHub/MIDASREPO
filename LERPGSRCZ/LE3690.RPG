     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Settlement Currency Exception Report')        *
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  LE3690 - Midas LE Settlement Currency Exception Report       *
      *                                                               *
      *  Function:  This program will report all facilities whose     *
      *  transaction currency differs from any account currency.      *
      *  Because facilities converted from 'IN' to Euros will still   *
      *  have account details denominated in 'IN'.                    *
      *  (phase(s) I/C & Cob)                                         *
      *                                                               *
      *  Called By: LEC3690                                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CCB020             Date 06Aug12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CLE106             Date 27Apr05               *
      *                 CGL029             Date 01Sep03               *
      *                 CLE031             Date 07Feb03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU021  *CREATE    Date 24Jul98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CCB020 - COB Restructure - Secondary COB Infrastructure      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CLE106 - Syndication Manager                                 *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CLE031 - Lending Enhancements                                *
      *  CEU021 - EMU LE Facility Currency Conversion
      *                                                               *
      *****************************************************************
     FFCLTY   IF  E           K        DISK         KINFSR *PSSR
      *
     FACCNTL1 IF  E           K        DISK         KINFSR *PSSR
      *
     FACCNT   IF  E           K        DISK         KINFSR *PSSR
     F            ACCNTABF                          KRENAMEACCNTX
      *
     FLE3690AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
     FLE3690P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *                                                               *
      *   89  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  INIT   - Program Initialisation                              *
      *  PROCES - Detail Processing                                   *
      *  REPORT - Process Report Lines                                *
      *                                                               *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     IDBERR     E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Installation Control Details
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      ***  File Information Data Structure for LE3690P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ***  File Information Data Structure for LE3690AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
      ***  Handle Database Error.
     I                                        1  12 KEYLIS
     I**********                              1   60KCUST                                     CSD027
     I                                        1   6 KCUST                                     CSD027
     I                                        7   90KFACT
     I                                       10  110KFCNO
     I                                       12  12 KRECT
      ***  Dummy Data Structures used by Access Programs.
      *
     ISDBANK    E DSSDBANKPD
      ***  External data structures for Bank Details
     ISDNOST    E DSSDNOSTPD
      ***  External data structures for Nostro Details
     ISCSARD    E DSSCSARDPD                                                                  CLE031
      **  External data structure for Switchable Features                                     CLE031
      *                                                                                       CLE031
      *-------------------------------------------------------------------
     IDSFDY     E DSDSFDY
      *First DS for Access Programs, short data structure.
      *-------------------------------------------------------------------
     IDSSDY     E DSDSSDY
      *Second DS for Access Programs, long data structure.
      *-------------------------------------------------------------------
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      ***  Output Audit Report and End Program.
      *
     C                     EXSR AUDIT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  INIT   - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  RCFP1  - RCF Processing for Report                           *
      *  AOBANKR0 Access Program                                      *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR                           ***  INIT  ***
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C                     PARM           RSEQ    5
     C                     PARM           RLEV    1
     C                     PARM           RENT    3
     C**********           PARM           ICT     1                                           CCB020
      *
      ***  Initialise LDA field.
      *
     C                     MOVEL'LE3690'  DBPGM
      *
      ***  Call Access Program for Bank Details - Title, Run Date and
      ***  Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C                     MOVE 'SDBANKPD'DBFILE           ***************
     C                     Z-ADD001       DBASE            * DB ERROR 01 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF
      *                                                                                       CLE031
      ** Check whether SAR CLE031 is installed or not.                                        CLE031
      *                                                                                       CLE031
     C                     CALL 'AOSARDR0'                                                    CLE031
     C                     PARM *BLANK    @RTCD   7                                           CLE031
     C                     PARM '*VERIFY' @OPTN   7                                           CLE031
     C                     PARM 'CLE031'  WSARD   6                                           CLE031
     C           SCSARD    PARM SCSARD    DSFDY                                               CLE031
      *                                                                                       CLE031
     C           @RTCD     IFEQ *BLANK                                                        CLE031
     C                     MOVE 'Y'       CLE031  1                                           CLE031
     C                     ELSE                                                               CLE031
     C                     MOVE 'N'       CLE031                                              CLE031
     C           @RTCD     IFNE '*NRF   '                                                     CLE031
     C           *LOCK     IN   LDA                                                           CLE031
     C                     MOVEL'LE3690  'DBPGM                                               CLE031
     C                     MOVEL'SCSARDPD'DBFILE                                              CLE031
     C                     Z-ADD004       DBASE                                               CLE031
     C                     MOVEL'CLE031'  DBKEY                                               CLE031
     C                     OUT  LDA                                                           CLE031
     C                     EXSR *PSSR                                                         CLE031
     C                     ENDIF                                                              CLE031
      *                                                                                       CLE031
     C                     ENDIF                                                              CLE031
      *
      ***  RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ***  RCF Processing for Printer file.
      *
     C                     OPEN LE3690P1
     C                     EXSR RCFP1
      *
      ***  Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
     C                     Z-ADD0         OUTPUT  50
      *
      ***  Set up key list to chain to FCLTY
      *
     C           FACILI    KLIST
     C                     KFLD           KCUST            -Customer No
     C                     KFLD           KFACT            -Facility Type
     C                     KFLD           KFCNO            -Facility No
     C                     KFLD           KRECT            -Record Type
      *
      ***  Set up key list to chain to ACCNT
      *
     C           ACCKEY    KLIST
     C                     KFLD           KCNUM            -Customer No
     C                     KFLD           KCCY             -Currency
      *
     C           *LIKE     DEFN CNUM      CNUM2
     C           *LIKE     DEFN CNUM      KCNUM
     C           *LIKE     DEFN FACT      FACT2
     C           *LIKE     DEFN FCNO      FCNO2
     C           *LIKE     DEFN FCCY      FCCY2
     C           *LIKE     DEFN FCCY      KCCY
     C           *LIKE     DEFN ACSQ      ACSQ2
     C           *LIKE     DEFN PMFT      PMFT2
     C           *LIKE     DEFN PMFN      PMFN2
     C           *LIKE     DEFN PMFC      PMFC2
      *                                                                                       CCB020
      ** Check if the system is in COB Phase                                                  CCB020
      *                                                                                       CCB020
     C                     MOVEL'CBC00100'@RTPRM  9                                           CCB020
     C                     MOVE '1'       @RTPRM                                              CCB020
     C                     CALL @RTPRM                                                        CCB020
     C                     PARM *BLANKS   COBST   4                                           CCB020
      *                                                                                       CCB020
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  AUDIT  - Produce Audit Report and End Program                *
      *  REPORT - Process Report Lines                                *
      *  WP1END - Write End of Report                                 *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR                           *** PROCES ***
      *
      ***  Read first record from File.
      *
     C                     READ FCLTYFMF                 89
     C**********           Z-ADDCNUM      CNUM2                                               CSD027
     C                     MOVE CNUM      CNUM2                                               CSD027
     C                     Z-ADDFACT      FACT2
     C                     Z-ADDFCNO      FCNO2
     C                     MOVELFCCY      FCCY2
     C                     MOVELPMFT      PMFT2
     C                     MOVELPMFN      PMFN2
     C                     MOVELPMFC      PMFC2
      *
      ***  If End of Records, (*IN89), Perform: Audit Reporting (No
      ***  Records).
      *
     C           *IN89     IFEQ *ON
     C                     EXSR AUDIT
     C                     ELSE
     C                     WRITEHEADP1
     C                     ENDIF
     C                     Z-ADD0         RCOUNT  50
      *
      ***  Do Until End of File.
      *
     C           *IN89     DOWEQ*OFF
      *
      ***  Display fields on report, if they are live records.
      *
     C           RECI      IFEQ 'D'
     C                     MOVELFACT2     P1FACT
     C                     MOVELFCNO2     P1FCNO
     C                     MOVELCNUM2     P1CNUM
     C                     MOVELFCCY2     P1TRCY
     C                     MOVELPMFT2     P1PFTP
     C                     MOVELPMFN2     P1PFNO
     C                     MOVELPMFC2     P1PCNO
      *
      ***  Count records read.
      *
     C                     ADD  1         RCOUNT
      *
      ***  Process Report Lines.
      *
     C                     EXSR REPORT
     C                     ENDIF
      *
      ***  Read next record.
      *
     C                     READ FCLTYFMF                 89
     C**********           Z-ADDCNUM      CNUM2                                               CSD027
     C                     MOVE CNUM      CNUM2                                               CSD027
     C                     Z-ADDFACT      FACT2
     C                     Z-ADDFCNO      FCNO2
     C                     MOVELFCCY      FCCY2
     C                     MOVELPMFT      PMFT2
     C                     MOVELPMFN      PMFN2
     C                     MOVELPMFC      PMFC2
      *
      ***  End of Do Until End of Valid Records.
     C                     ENDDO
      *
      *
      ***  Write final records and Totals to Report.
      *
     C                     EXSR REPORT
     C                     EXSR WP1END
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/REPORT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *        None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           REPORT    BEGSR                           *** REPORT ***
      *
      ***  Transaction Currency.
      *
     C           CLE031    IFEQ 'N'                                       CLE031
     C           SCCY      IFEQ *BLANKS
     C                     MOVE FCCY2     P1TRCY
     C                     ELSE
     C                     MOVE SCCY      P1TRCY
     C                     ENDIF
      *                                                                   CLE031
     C                     ELSE                                           CLE031
      *                                                                   CLE031
     C                     MOVE FCCY2     P1TRCY                          CLE031
      *                                                                   CLE031
     C           PRTR      IFEQ 'P'                                       CLE031
     C           PRTR      OREQ 'S'                                                           CLE106
     C           PRTR      OREQ 'R'                                                           CLE106
     C           PSCY      IFNE *BLANKS                                   CLE031
     C                     MOVE PSCY      P1TRCY                          CLE031
     C                     ENDIF                                          CLE031
     C                     ELSE                                           CLE031
     C           SCCY      IFNE *BLANKS                                   CLE031
     C                     MOVE SCCY      P1TRCY                          CLE031
     C                     ENDIF                                          CLE031
     C                     ENDIF                                          CLE031
      *                                                                   CLE031
     C                     ENDIF                                          CLE031
      *                                                                   CLE031
      *
      ***  Write records to printer file. Either Account or Retail
      ***  or Nostro.
      *
      *
      ***  If Receipt Settlement method.
      *
     C           CLE031    IFEQ 'Y'                                       CLE031
     C                     MOVE SCCY      WKCCY   3                       CLE031
     C                     ELSE                                           CLE031
     C                     MOVE FCCY2     WKCCY                           CLE031
     C                     ENDIF                                          CLE031
      *                                                                   CLE031
     C                     SELEC
     C           RSTM      WHEQ 14
     C                     MOVELRONS      ACCOUN 100
     C                     EXSR SRRETL
     C           RSTM      WHEQ 1
     C           RSTM      OREQ 2
     C           RSTM      OREQ 7
     C           RSTM      OREQ 8
     C           RSTM      OREQ 12
     C                     MOVELRONS      NOSTRO  2 P
     C                     EXSR SRNOST
     C           RSTM      WHEQ 5
     C           RSTM      OREQ 4
     C                     MOVELRONS      P1ACNO    P
     C                     MOVELRONS      KCNUM     P
     C                     EXSR SRACCT
     C                     ENDSL
      *
      ***  If Pay Settlement method.
      *
     C           CLE031    IFEQ 'Y'                                       CLE031
     C                     MOVE PSCY      WKCCY                           CLE031
     C                     ENDIF                                          CLE031
      *                                                                   CLE031
     C                     SELEC
     C           PSTM      WHEQ 14
     C                     MOVELPONS      ACCOUN
     C                     EXSR SRRETL
     C           PSTM      WHEQ 1
     C           PSTM      OREQ 2
     C           PSTM      OREQ 7
     C           PSTM      OREQ 8
     C           PSTM      OREQ 12
     C                     MOVELPONS      NOSTRO    P
     C                     EXSR SRNOST
     C           PSTM      WHEQ 5
     C           PSTM      OREQ 4
     C                     MOVELPONS      KCNUM     P
     C                     MOVELPONS      P1ACNO    P
     C                     EXSR SRACCT
     C                     ENDSL
      *
      ***  If Settlement type.
      *
     C           CLE031    IFEQ 'Y'                                       CLE031
     C                     MOVE FCCY2     WKCCY                           CLE031
     C                     ENDIF                                          CLE031
      *                                                                   CLE031
     C                     MOVELCNUM2     KCUST
     C                     MOVELFACT2     KFACT
     C                     MOVELFCNO2     KFCNO
     C                     MOVEL'B'       KRECT
     C           FACILI    CHAINFCLTYFNF             89
      *
      ***  Handle Database Error.
     C           *IN89     IFEQ *ON
     C                     MOVEL'FCLTY   'DBFILE
     C                     MOVELKEYLIS    DBKEY            ***************
     C                     Z-ADD002       DBASE            * DB ERROR 02 *
     C                     EXSR *PSSR                      ***************
     C                     ENDIF
      *
      ***  If format FCLTYFNF then place stars in prime fields.
      *
     C                     MOVE '***'     P1PFTP
     C                     MOVE '**'      P1PFNO
     C                     MOVE '******'  P1PCNO
     C                     SELEC
     C           SETT      WHEQ 14
     C                     MOVELOBAC      ACCOUN    P
     C                     EXSR SRRETL
     C           SETT      WHEQ 1
     C           SETT      OREQ 2
     C           SETT      OREQ 7
     C           SETT      OREQ 8
     C           SETT      OREQ 12
     C                     MOVELOBAC      NOSTRO    P
     C                     EXSR SRNOST
     C           SETT      WHEQ 5
     C           SETT      OREQ 4
     C                     MOVELOBAC      KCNUM     P
     C                     MOVELOBAC      P1ACNO    P
     C                     EXSR SRACCT
     C                     ENDSL
     C                     MOVE *BLANKS   P1PFTP
     C                     MOVE *BLANKS   P1PFNO
     C                     MOVE *BLANKS   P1PCNO
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRRETL
      *****************************************************************
      *                                                               *
      *  SRRETL - Process Retail Accounts                             *
      *                                                               *
      *  Called By: REPORT                                            *
      *  Calls:                                                       *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *                                                               *
      *****************************************************************
      *
     C           SRRETL    BEGSR                           *** RETAIL ***
     C           ACCOUN    CHAINACCNTL1              89
      *
      ***  Check whether account exists and output details if it does
      ***  not or account Ccy is not equal to Facility Ccy.
     C           *IN89     IFEQ *ON
     C                     MOVE '***'     P1ACCY
     C                     MOVE ACCOUN    P1RTAC
     C                     MOVE *BLANKS   P1ACNO
     C                     MOVE *BLANKS   P1NNUM
     C                     EXSR WP1REC
     C                     ELSE
     C           CCY       IFNE FCCY2
     C                     MOVE CCY       P1ACCY
     C                     MOVE ACNO      P1RTAC
     C                     MOVE *BLANKS   P1ACNO
     C                     MOVE *BLANKS   P1NNUM
     C                     EXSR WP1REC
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRNOST
      *****************************************************************
      *                                                               *
      *  SRNOST - Process Nostro Accounts                             *
      *                                                               *
      *  Called By: REPORT                                            *
      *  Calls:                                                       *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *                                                               *
      *****************************************************************
      *
     C           SRNOST    BEGSR                           *** NOSTRO ***
      *
      *** Check whether Nostro exists and output details if it does not
      *
     C                     CALL 'AONOSTR0'
     C                     PARM '*MSG   ' WRTCD
     C                     PARM '*KEY   ' WOPTN
     C                     PARM           P@CUST  6        I:Customer No  S01230
     C**********           PARM FCCY2     P@CYCD  3 I:Currency     S01230 CLE031
     C                     PARM WKCCY     P@CYCD  3        I:Currency     CLE031
     C**********           PARM *BLANKS   P@ACCD  4        I:A/c Code                  S01230 CGL029
     C                     PARM *BLANKS   P@ACCD 10                                           CGL029
     C                     PARM *BLANKS   P@ACSN  2        I:A/c Seq      S01230
     C                     PARM NOSTRO    P@NONB  2        I:Nostro No    S01230
     C                     PARM *BLANKS   P@BRCD  3        I:Branch Code  S01230
     C                     PARM *BLANKS   P@CSSN 10        I:Cust ShortN  S01230
     C                     PARM *BLANKS   P@PNOI  1        I:Prime Nost InS01230
     C           SDNOST    PARM SDNOST    DSSDY
      *
      ***  Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           WRTCD     ANDNE'*NRF    '
     C                     MOVE 'SDNOSTPD'DBFILE
     C**********           MOVE FCCY2     DBKEY                           CLE031
     C                     MOVE WKCCY     DBKEY                           CLE031
     C                     MOVELNOSTRO    DBKEY            ***************
     C                     Z-ADD003       DBASE            * DB ERROR 03 *
     C                     EXSR *PSSR                      ***************
     C                     ELSE
     C           WRTCD     IFEQ '*NRF    '
     C                     MOVE '***'     P1ACCY
     C                     MOVE NOSTRO    P1NNUM
     C                     MOVE *BLANKS   P1ACNO
     C                     MOVE *BLANKS   P1RTAC
     C                     EXSR WP1REC
     C                     ENDIF
     C                     ENDIF
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRACCT
      *****************************************************************
      *                                                               *
      *  SRACCT - Process Account number                              *
      *                                                               *
      *  Called By: REPORT                                            *
      *  Calls:                                                       *
      *  WP1REC - Format and Write a Detail Record to the Report      *
      *                                                               *
      *****************************************************************
      *
     C           SRACCT    BEGSR                           *** ACCOUNT***
     C*********            MOVELFCCY2     KCCY             -Currency      CLE031
     C                     MOVELWKCCY     KCCY             -Currency      CLE031
     C           ACCKEY    CHAINACCNTX               89
      *
      ***Check whether account exists and output details if it does not
     C           *IN89     IFEQ *ON
     C                     MOVE '***'     P1ACCY
     C                     MOVE *BLANKS   P1NNUM
     C                     MOVE *BLANKS   P1RTAC
     C                     EXSR WP1REC
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /TITLE SR/WP1REC
      *****************************************************************
      *                                                               *
      *  WP1REC - Subroutine to Write a Detail record to the Report.  *
      *                                                               *
      *  Called By: SRRETL SRNOST SRACCT                              *
      *  Calls:                                                       *
      *                                                               *
      *****************************************************************
      *
     C           WP1REC    BEGSR                           *** WP1REC ***
      *
      *----------------------------------------------------------------
      *
      *    Perform any formatting before printing via ZDATE2, ZSEDIT
      *
      *----------------------------------------------------------------
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     ADD  1         OUTPUT
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Detail Record.
      *
     C                     WRITEDETAIL
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/WP1END
      *****************************************************************
      *                                                               *
      *  WP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           WP1END    BEGSR                           *** WP1END ***
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Total Format.
      *
     C                     WRITETRAILP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/AUDIT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, PROCES, *PSSR                    *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
      *
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ***  Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
     C                     WRITECONTROL
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C           OUTPUT    IFEQ 0
     C                     WRITEHEADP1
     C                     WRITENODTL1
     C                     WRITETRAILP1
     C                     WRITENODTLS
     C                     ELSE
     C           OUTPUT    IFGT 0
     C********** ICT       ANDNE'A'                                                           CCB020
     C           COBST     ANDEQ'*YES'                                                        CCB020
     C                     SETON                     U7U8
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ***  End Program and Return.
      *
     C                     CLOSELE3690P1
     C                     CLOSELE3690AU
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C           *NAMVAR   DEFN LDA       LDA   256
     C           *LOCK     IN   LDA
     C                     MOVELDBERR     LDA
     C                     OUT  LDA
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFP1
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM RSEQ      SEQ     5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM *BLANK    ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/RCFAU
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM RSEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2001
