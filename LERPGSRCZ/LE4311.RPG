     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Cash Flow Using All-in-Rate Report')          *
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  LE4311 - Cash Flow Using All-in-Rate Report                  *
      *                                                               *
      *  Function:  This program will report details from the         *
      *  (COB)      cash flow file LECFLAPD.                          *
      *                                                               *
      *  NOTE: Check LE4111 for changes that may apply to it if there *
      *        are amendments done to this program.                   *
      *                                                               *
      *  Called By: LEC4311 - Cash Flow Using All-In-Rate Report      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 230112             Date 14Oct04               *
      *                 226871             Date 06May04               *
      *                 CGL029             Date 01Sep03               *
      *                 CAS005  *CREATE    Date 16Dec02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *  230112 - No interest rate reported on LE4111P1 and LE4311P1  *
      *           (Repayment of interest).                            *
      *  226871 - Fees should be incorporated in the computation      *
      *           of NPV.                                             *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *                                                               *
      *****************************************************************
     FLECFLAL1IF  E           K        DISK
      ** Cash flows by brch/ccy/book/loan type/loan no.
      *
     FLE4311AUO   E                    PRINTER      KINFDS SPOOLU
      ** Cash Flow Using All-in-Rate Report - Audit
      *
     FLE4311P1O   E                    PRINTER      KINFDS SPOOL1     UC
      ** Cash Flow Using All-in-Rate Report
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *  80 - End of File                                             *
      *  98 - System Date Format                                      *
      *                                                               *
      *  LR - Last record indicator (program termination)             *
      *  U7 and U8 - DB error processing indicator                    *
      *                                                               *
      *****************************************************************
      /SPACE
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E  I N D E X                               *
      *                                                               *
      *  SRINIT - Initialisation Processing                           *
      *  SRPRNT - Print transaction details                           *
      *  SRBLNK - Blank out detail fields                             *
      *  SRLOAD - Load details line                                   *
      *  SRRCFP - Subroutine to register the P1 Printer File via RCF  *
      *  SRRCFA - Subroutine to register the AU Printer File via RCF  *
      *  SRTERM - Termination process                                 *
      *  SRAUDT - Audit report processing                             *
      *                                                               *
      *  *PSSR  - DB Error Processing                                 *
      *                                                               *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZFRPEDZ1
      /EJECT
      *
     I/COPY ZSRSRC,ZFRPEDZ2
      *
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     ISPOOL1      DS
      ** File Information Data Structure for LE4311P1.
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ** File Information Data Structure for LE4311AU.
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     ILVELDS      DS                              6
     I                                        1   1 WL6
     I                                        2   2 WL5
     I                                        3   3 WL4
     I                                        4   4 WL3
     I                                        5   5 WL2
     I                                        6   6 WL1
      *
     ISDBANK    E DSSDBANKPD
      ** External data structures for Bank Details
      *
     ISDCURR    E DSSDCURRPD
      ** External data structures for Currency Details
      *
     ISDBRCH    E DSSDBRCHPD
      ** External data structures for branch details
      *
     IDSSDY     E DSDSSDY
      ** Long data structure for access objects
      *
     IDSFDY     E DSDSFDY
      ** Dummy Data Structures used by Access Programs.
      *
     ISDGELR    E DSSDGELRPD
      ** General Ledger Details accessed via access program
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ** Perform Initialisation.
      *
     C                     EXSR SRINIT
      *
      ** Perform Detail Processing.
      *
     C           *LOVAL    SETLLLECFLAL1
     C                     READ LECFLAL1                 80
      *
      ** No details to report
      *
     C           *IN80     IFEQ *ON
     C                     EXSR SRAUDT
     C                     EXSR SRTERM
     C                     ENDIF
      *
      ** Print transaction details
      *
     C           *IN80     DOWEQ*OFF
     C           LAFTYP    IFNE 'SFAN'                                                        226871
     C           LAFTYP    ANDNE'EFAN'                                                        226871
     C           LAFTYP    ANDNE'SFBN'                                                        226871
     C                     EXSR SRPRNT
     C                     ENDIF                                                              226871
     C                     READ LECFLAL1                 80
     C                     ENDDO
      *
      ** Write 'End of Report for Branch'
      *
     C           PRTLN1    IFGT 56
     C                     WRITELE4311D1
     C                     ENDIF
      *
     C                     WRITELE4311D4
      *
      ** Terminate Program
      *
     C                     EXSR SRTERM
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:                                                       *
      *  AOBANKR0 - Access Program for Bank Details                   *
      *  SRRCFA   - Subroutine to register the AU Printer File via RCF*
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PBRAN   3
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Define break fields
      *
     C           *LIKE     DEFN LABRCA    WBRCA
     C           *LIKE     DEFN LACYCD    WCYCD
     C           *LIKE     DEFN LABKCD    WBKCD
     C           *LIKE     DEFN LALTYP    WLTYP
     C           *LIKE     DEFN LALNST    WLNST
     C           *LIKE     DEFN LALNCL    WLNCL                                               CLE042
     C           *LIKE     DEFN LALNRF    WLNRF
      *
      ** Initialise LDA field.
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANK    DBFILE
     C                     MOVE *BLANK    DBKEY
     C                     Z-ADD*ZERO     DBASE
     C                     MOVEL'LE4311'  DBPGM
     C                     OUT  LDA
      *
      ** Call Access Program for Bank Details - Title, Run Date and
      ** Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Handle Database Error.
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBANKPD'DBFILE
     C                     MOVELPOPTN     DBKEY
     C                     Z-ADD1         DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Check System Date Format, DDMMYY (*IN98 off)
      **                           MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ** Call access program for General Ledger details.
      *
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*DBERR ' PRTCD
     C                     PARM '*FIRST ' POPTN
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *
      ** Database Error
      *
     C           PRTCD     IFNE *BLANK
     C                     MOVEL'SDGELRPD'DBFILE
     C                     Z-ADD2         DBASE
     C                     MOVELPOPTN     DBKEY
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Set up Event Control Date.  It is the lesser of either the
      ** (Next Working Day - 1) or the Accrual Profit Date
      *
     C           BJDNWD    SUB  1         EVCD    50
      *
     C           BKAPDT    IFLT EVCD
     C                     Z-ADDBKAPDT    EVCD
     C                     ENDIF
      *
      ** Convert the event control date to DDMMMYY
      *
     C                     Z-ADDEVCD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    PEVCD
      *
      ** RCF Processing for Audit File
      *
     C                     EXSR SRRCFA
      *
     C                     Z-ADD*ZERO     WCOUNT  30
     C                     MOVE *BLANK    LVELDS
     C                     MOVE *BLANK    WBRCA
     C                     MOVE *BLANK    WCYCD
     C                     MOVE *BLANK    WBKCD
     C                     MOVE *BLANK    WLTYP
     C                     MOVE *BLANK    WLNST
     C                     MOVE *BLANK    WLNCL                                               CLE042
     C**********           Z-ADD*ZERO     WLNRF                                               CLE148
     C                     MOVEL*BLANKS   WLNRF                                               CLE148
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRPRNT - Print transaction details                           *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:                                                       *
      *  SRRCFP   - Subroutine to register the P1 Printer File via RCF*
      *  AOBRCHR0 - Access Object for Branch Details                  *
      *  AOCURRR0 - Access Object for Currency Details                *
      *  SRBLNK   - Blank out detail fields                           *
      *  SRLOAD   - Load details line                                 *
      *                                                               *
      *****************************************************************
      *
     C           SRPRNT    BEGSR
      *
      ** Determine which field has changed upon read to LECFLAL1
      *
     C                     MOVE *ALL'N'   LVELDS
      *
     C                     SELEC
      *
     C           LABRCA    WHNE WBRCA
     C                     MOVEL'YYYYYY'  LVELDS
     C           LACYCD    WHNE WCYCD
     C                     MOVEL'NYYYYY'  LVELDS
     C           LABKCD    WHNE WBKCD
     C                     MOVEL'NNYYYY'  LVELDS
     C           LALTYP    WHNE WLTYP
     C                     MOVEL'NNNYYY'  LVELDS
     C           LALNST    WHNE WLNST
     C           LALNCL    ORNE WLNCL                                                         CLE042
     C                     MOVEL'NNNNYY'  LVELDS
     C           LALNRF    WHNE WLNRF
     C                     MOVEL'NNNNNY'  LVELDS
      *
     C                     ENDSL
      *
      ** Change in branch code
      *
     C           WL6       IFEQ 'Y'
      *
      ** Write 'End of Report for Branch'
      *
     C           WBRCA     IFNE *BLANK
      *
     C           PRTLN1    IFGT 56
     C                     WRITELE4311D1
     C                     ENDIF
      *
     C                     WRITELE4311D4
     C                     ENDIF
      *
      ** Ensure report spool file recorded by RCF
      ** transfer binary data into numeric field
      *
     C                     EXSR SRRCFP
      *
      ** Initialise page number field
      *
     C                     Z-ADD*ZERO     PAGE
      *
      ** Use access program to read branch codes file
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM LABRCA    PBRCH   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBRCHPD'DBFILE
     C                     MOVELPBRCH     DBKEY
     C                     Z-ADD3         DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVELLABRCA    PBRCA
     C                     MOVELA8BRNM    PBRNM
     C                     ENDIF
     C                     ENDIF
      *
      ** Change in currency code
      *
     C           WL5       IFEQ 'Y'
      *
      ** Use access program to read currency codes file
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM LACYCD    PCURR   3
     C           SDCURR    PARM SDCURR    DSFDY
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCURRPD'DBFILE
     C                     MOVELPCURR     DBKEY
     C                     Z-ADD4         DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVELLACYCD    PCYCD
     C                     MOVELA6CYNM    PCYNM
     C                     WRITELE4311D1
     C                     ENDIF
     C                     ENDIF
      *
      ** At change of loan number, skip line
      *
     C           WL1       IFEQ 'Y'
      *
     C           PRTLN1    IFGT 58
     C                     WRITELE4311D1
     C                     ENDIF
      *
     C                     WRITELE4311D3
     C                     ENDIF
      *
      ** Load details onto detail line
      *
     C                     EXSR SRBLNK
     C           LACAMT    IFNE 0
     C                     EXSR SRLOAD
      *
     C           PRTLN1    IFGT 58
     C                     WRITELE4311D1
     C                     ENDIF
      *
     C                     WRITELE4311D2
     C                     ENDIF
      *
      ** Save break fields
      *
     C                     MOVE LABRCA    WBRCA
     C                     MOVE LACYCD    WCYCD
     C                     MOVE LABKCD    WBKCD
     C                     MOVE LALTYP    WLTYP
     C                     MOVE LALNST    WLNST
     C                     MOVE LALNCL    WLNCL                                               CLE042
     C**********           Z-ADDLALNRF    WLNRF                                               CLE148
     C                     MOVELLALNRF    WLNRF                                               CLE148
      *
      ** Increment record counter
      *
     C           LACAMT    IFNE 0
     C                     ADD  1         WCOUNT
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRBLNK - Blank out detail fields                             *
      *                                                               *
      *  Called By: SRPRNT subroutine                                 *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRBLNK    BEGSR
      *
     C                     MOVE *BLANKS   PBKCD
     C                     MOVE *BLANKS   PLTYP
     C                     MOVE *BLANKS   PLNST
     C                     MOVE *BLANKS   PLNCL                                               CLE042
     C                     MOVE *BLANKS   PLNRF
     C                     MOVE *BLANKS   PADLN
     C                     MOVE *BLANKS   PCNUM
     C                     MOVE *BLANKS   PDATE
     C                     MOVE *BLANKS   PNDYP
     C                     MOVE *ZEROS    PICBS
     C                     MOVE *BLANKS   PFTYP
     C                     MOVE *BLANKS   PDATE
     C                     MOVE *BLANKS   PYLDC
     C                     MOVE *BLANKS   PCAMT
     C                     MOVE *BLANKS   PNPVA
     C                     MOVE *BLANKS   PIRAT
     C                     MOVE *BLANKS   PDSCF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRLOAD - Load details line                                   *
      *                                                               *
      *  Called By: SRPRNT subroutine                                 *
      *                                                               *
      *  Calls:                                                       *
      *  ZFRPED   - Edit numeric field                                *
      *                                                               *
      *****************************************************************
      *
     C           SRLOAD    BEGSR
      *
     C                     MOVE LABKCD    PBKCD
     C                     MOVE LALTYP    PLTYP
     C                     MOVE LALNST    PLNST
     C                     MOVE LALNCL    PLNCL                                               CLE042
     C                     MOVE LALNRF    PLNRF
     C********** LAADLN    IFNE *ZERO                                                         CLE148
     C           LAADLN    IFNE *BLANKS                                                       CLE148
     C                     MOVE LAADLN    PADLN
     C                     ENDIF
     C                     MOVE LACNUM    PCNUM
     C                     MOVE LADATE    PDATE
     C                     Z-ADDLAICBS    PICBS
     C                     MOVE LAFTYP    PFTYP
     C                     MOVE LADATE    PDATE
     C                     MOVE LAYLDC    PYLDC
      *
      ** Edit the 'Number of Days in Interest Period'
      *
     C                     Z-ADDLANDYP    ZFLD15
     C                     Z-ADD*ZERO     ZDECS
     C                     MOVE 'C'       ZECODE
     C                     EXSR ZFRPED
     C                     MOVELZOUT22    WTMP20 20
     C                     MOVE WTMP20    PNDYP
      *
      ** Edit the Cash flow amount according to the number of
      ** decimal places of the Currency, if currency is not blanks.
      *
     C           LACYCD    IFNE *BLANKS
     C           LAIOIN    IFEQ 'O'
     C                     Z-SUBLACAMT    LACAMT
     C                     ENDIF
     C                     Z-ADDLACAMT    ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZFRPED
     C                     MOVELZOUT22    PCAMT
     C                     ENDIF
      *
      ** Edit the NPV amount according to the number of
      ** decimal places of the Currency, if currency is not blanks.
      *
     C           LACYCD    IFNE *BLANKS
     C           LAIOIN    IFEQ 'O'
     C                     Z-SUBLANPVA    LANPVA
     C                     ENDIF
     C                     Z-ADDLANPVA    ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZFRPED
     C                     MOVELZOUT22    PNPVA
     C                     ENDIF
      *
      ** Edit the Interest rate (print interest rate only for interest amount events)
      *
     C           PFTYP     IFEQ 'INTP'
     C           PFTYP     OREQ 'SINT'
     C           PFTYP     OREQ 'MINT'
     C           PFTYP     OREQ 'SMIN'
     C           PFTYP     OREQ 'SRPI'                                                        230112
     C           PFTYP     OREQ 'SSRI'                                                        230112
     C           LAIRAT    MULT 10000000  ZFLD15
     C                     Z-ADD7         ZDECS
     C                     MOVE 'C'       ZECODE
     C                     EXSR ZFRPED
     C                     MOVE ZOUT1     PIRAT
     C                     ENDIF
      *
      ** Edit the Discount factor
      *
     C           LADSCF    MULT 1000000000ZFLD15
     C                     Z-ADD9         ZDECS
     C                     MOVE 'C'       ZECODE
     C                     EXSR ZFRPED
     C                     MOVE ZOUT1     PDSCF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRRCFP - Subroutine to register the P1 Printer File via RCF  *
      *                                                               *
      *  Called By: SRPRNT subroutine                                 *
      *                                                               *
      *  Calls:                                                       *
      *  ZSFILE   - Setup spool file                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRRCFP    BEGSR
      *
      ** Ensure Report Spool File recorded by RCF.
      *
     C                     CLOSELE4311P1
     C                     OPEN LE4311P1
     C                     Z-ADDSFNUM1    PZSNUM  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ    5
     C                     PARM           PSENTY  3
     C                     PARM           SFILE1
     C                     PARM           PZSNUM
     C                     PARM *BLANK    PZSERR  1
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           PZSERR    IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      *  SRRCFA - Subroutine to register the AU Printer File via RCF  *
      *                                                               *
      *  Called By: SRPRNT subroutine                                 *
      *                                                               *
      *  Calls:                                                       *
      *  ZSFILE   - Setup spool file                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRRCFA    BEGSR
      *
      ** Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    PZSNMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ
     C                     PARM *BLANKS   PSENTY
     C                     PARM           SFILEU
     C                     PARM           PZSNMU
     C                     PARM *BLANK    PZSERR
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           PZSERR    IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRTERM - Termination process                                 *
      *                                                               *
      *  Called By: Main processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRTERM    BEGSR
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRAUDT - Audit report processing                             *
      *                                                               *
      *  Called By: Main and *PSSR subroutine                         *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRAUDT    BEGSR
      *
      ** Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
      *
      ** If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ** Or, if no records read, Output "No Details" message.
      *
     C           WCOUNT    IFEQ 0
     C                     WRITENODET
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *  Called By: SRINIT and SRPRNT subroutine                      *
      *                                                               *
      *  Calls:                                                       *
      *  SRAUDT - Audit report processing                             *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
      ** Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
      *
     C                     EXSR SRAUDT
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZFRPEDZ3
      *****************************************************************
** CPY@
(c) Misys International Banking Systems Ltd. 2002
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
