     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Accruals Using Net-Treasury-Rate Report')     *
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  LE5210 - Accruals Using Net-Treasury-Rate Report             *
      *                                                               *
      *  NOTE: Check LE1120 for changes that may apply to it if there *
      *        are amendments done to this program.                   *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      *                 CER059             Date 19Jul10               *
      *                 CER047             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 BUG3799            Date 19Apr05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 CAS005  *CREATE    Date 16Dec02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference                                *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER047 - German Features LF037-00 Reporting §24c             *
      *           (Recompile)                                         *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *  BUG3799 - Tries to execute SRAUDT twice if no details on     *
      *            SLACN                                              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *                                                               *
      *****************************************************************
     FSLACN   IF  E           K        DISK
     F            LEACXZZF                          KIGNORE
      ** Accrual Extract Using Net-Treasury-Rate
      *
     FLEACNZZ IF  E                    DISK
      *
     FLE5210AUO   E                    PRINTER      KINFDS SPOOLU
      ** Accrual Extract Using Net-Treasury-Rate - Audit
      *
     FLE5210P1O   E             87     PRINTER      KINFDS SPOOL1
      ** Accrual Extract Using Net-Treasury-Rate - Report
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *  86 - Out of Balance Indicator                                *
      *  87 - Overflow Indicator                                      *
      *  89 - READ Indicator                                          *
      *  98 - System Date Format                                      *
      *                                                               *
      *  LR - Last record indicator (program termination)             *
      *  U7 and U8 - DB error processing indicator                    *
      *                                                               *
      *****************************************************************
      /SPACE
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E  I N D E X                               *
      *                                                               *
      *  SRAUDT - Subroutine to Output Audit report and End Program   *
      *  SRBRCA - Process Change in Branch                            *
      *  SRCCY  - Process Change in Currency                          *
      *  SRCHK  - Subroutine to Check if Totals are to be printed     *
      *  SRENDR - End of Branch report                                *
      *  SRINIT - Initialisation Processing                           *
      *  SRLNRF - Process Change in Loan Reference                    *
      *  SRLTST - Process Change in Loan Type/Sub-type                *
      *  SRMAIN - Subroutine to do the Detail Processing              *
      *  SRPREC - Subroutine to Write a Detail record to the Report   *
      *  SRREPT - Process Report Lines                                *
      *  SRRCFA - Subroutine to register the AU Printer File via RCF  *
      *  SRRCFP - Subroutine to register the P1 Printer File via RCF  *
      *  SRTOTL - Accumulate the totals                               *
      *                                                               *
      *  *PSSR  - DB Error Processing                                 *
      *                                                               *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZFRPEDZ1
      /EJECT
      *
     I/COPY ZSRSRC,ZFRPEDZ2
      *
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IPSDS       SDS
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     IRUNDT       DS
      ** Data Area Rundat
      *
     I                                        1   7 RUNA
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
      *
     ISPOOL1      DS
      ** File Information Data Structure for LE5210P1
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ** File Information Data Structure for LE5210AU
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     ISDBANK    E DSSDBANKPD
      ** External data structures for Bank Details
      *
     ISDCURR    E DSSDCURRPD
      ** External data structures for Currency Details
      *
     ISDBRCH    E DSSDBRCHPD
      ** External data structures for branch details
      *
     ISDCUST    E DSSDCUSTPD
     I              QQDFAC                          DFACQQ                                    CGL029
      ** External data structure for Customer details
      *
     ISDLOAN    E DSSDLOANPD
      ** External data structure for Loan Type/Subtype Details
      *
     IDSSDY     E DSDSSDY
      ** Long data structure for access objects
      *
     IDSFDY     E DSDSFDY
      ** Dummy Data Structures used by Access Programs
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ** Perform Initialisation
      *
     C                     EXSR SRINIT
      *
      ** Perform Detail Processing
      *
     C                     EXSR SRMAIN
      *
      ** Output Audit Report and End Program
      *
     C                     EXSR SRAUDT
      *
     C                     MOVE *ON       *INLR
      *
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT - Subroutine to do Program Initialisation             *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Receive Parameter List
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
      *
      ** Initialise LDA field
      *
     C                     MOVEL'LE5210'  DBPGM
      *
     C           DATF      IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ** Call Access Program for Bank Details - Title, Run Date and
      ** Run Day Number
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Handle Database Error
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBANKPD'DBFILE
     C                     Z-ADD1         DBASE
     C                     MOVELPOPTN     DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Access RUNDAT for multibranching indicator
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** RCF Processing for Printer File
      *
     C                     EXSR SRRCFP
      *
      ** RCF Processing for Audit File
      *
     C                     EXSR SRRCFA
      *
      ** Report Work fields
      *
     C                     Z-ADD0         WLNPR  130
     C                     Z-ADD0         WLNIAD 130
     C                     Z-ADD0         WLNITA 130
     C                     Z-ADD0         WLTPR  130
     C                     Z-ADD0         WLTIAD 130
     C                     Z-ADD0         WLTITA 130
     C                     Z-ADD0         WCYPR  130
     C                     Z-ADD0         WCYIAD 130
     C                     Z-ADD0         WCYITA 130
     C                     Z-ADD2         COUNT
      *
     C                     READ LEACXHHF                 89
      *
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE
     C                     MOVEL'H'       DBKEY
     C                     MOVEL'LEACNHH' DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     Z-ADDTDAT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RDATE
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRMAIN - Subroutine to do the Detail Processing              *
      *                                                               *
      *****************************************************************
      *
     C           SRMAIN    BEGSR
      *
      ** Read first record from File
      *
     C                     READ SLACN                    89
      *
      ** If End of Records, (*IN89), Perform: Audit Reporting (No
      ** Records)
      *
     C           *IN89     IFEQ *ON
      *                                                                                      BUG3799
      ** Do not perform subroutine SRAUDT here, because it will be called anyway.            BUG3799
      ** It can't be called twice                                                            BUG3799
      *                                                                                      BUG3799
     C**********           EXSR SRAUDT                                                       BUG3799
     C                     ELSE
     C                     EXSR SRBRCA
     C                     ENDIF
      *
      ** Do Until End of File
      *
     C           *IN89     DOWEQ*OFF
      *
      ** Count records read
      *
     C                     ADD  1         COUNT   50
      *
      ** Process Report Lines
      *
     C                     EXSR SRREPT
      *
      ** Accumulate totals
      *
     C                     EXSR SRTOTL
      *
      ** Save key fields to work fields
      *
     C                     MOVE BRCA      WKBRCA  3
     C                     MOVE CCY       WKCCY   3
     C                     MOVE LTYP      WKLTYP  2
     C                     MOVE SUTP      WKSUTP  2
     C                     MOVE CLAS      WKCLAS  4                                           CLE042
     C**********           Z-ADDCNUM      WKCNUM  60                                          CSD027
     C                     MOVE CNUM      WKCNUM  6                                           CSD027
     C**********           Z-ADDLNRF      WKLNRF  60                                          CLE148
     C**********           Z-ADDPSLN      WKPSLN  60                                          CLE148
     C                     MOVELLNRF      WKLNRF  6                                           CLE148
     C                     MOVELPSLN      WKPSLN  6                                           CLE148
      *
      ** Read next record
      *
     C                     READ SLACN                    89
      *
      ** Check if totals are to be printed
      *
     C                     EXSR SRCHK
      *
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRTOTL - Accumulate the totals                               *
      *                                                               *
      *****************************************************************
      *
     C           SRTOTL    BEGSR
      *
     C********** PSLN      IFEQ *ZEROS                                                        CLE148
     C           PSLN      IFEQ *BLANKS                                                       CLE148
     C                     Z-ADDCPAM      WLNPR
     C                     Z-ADDAMAD      WLNIAD
     C                     Z-ADDAMTA      WLNITA
     C                     ELSE
     C           WLNPR     ADD  CPAM      WLNPR
     C           WLNIAD    ADD  AMAD      WLNIAD
     C           WLNITA    ADD  AMTA      WLNITA
     C                     ENDIF
      *
     C           WLTPR     ADD  CPAM      WLTPR
     C           WLTIAD    ADD  AMAD      WLTIAD
     C           WLTITA    ADD  AMTA      WLTITA
      *
     C           WCYPR     ADD  CPAM      WCYPR
     C           WCYIAD    ADD  AMAD      WCYIAD
     C           WCYITA    ADD  AMTA      WCYITA
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRREPT - Process Report Lines                                *
      *                                                               *
      *****************************************************************
      *
     C           SRREPT    BEGSR
      *
      ** Check if Loan Type/Subtype Narrative has to be print out
      *
     C           WKLTYP    IFNE LTYP
     C           WKSUTP    ORNE SUTP
     C           WKCLAS    ORNE CLAS                                                          CLE042
     C                     CALL 'AOLOANR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM LTYP      PLTYP   2
     C                     PARM SUTP      PSUTP   2
     C                     PARM CLAS      PCLAS   4                                           CLE042
     C           SDLOAN    PARM SDLOAN    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C*********************MOVELLTYP      WKKEY   4                                           CLE042
     C*********************MOVE SUTP      WKKEY                                               CLE042
     C                     MOVELLTYP      WKKEY  10                                           CLE042
     C                     CAT  SUTP:1    WKKEY                                               CLE042
     C                     CAT  CLAS:1    WKKEY                                               CLE042
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE
     C                     MOVELWKKEY     DBKEY
     C                     MOVEL'SDLOANPD'DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVE AYLNTD    RNARR
      *
     C           *IN87     IFEQ *ON
     C                     MOVE *OFF      *IN87
     C                     WRITEHEADP1
     C                     ENDIF
      *
     C                     WRITEHEADP2
     C                     ENDIF
      *
     C           PRCNUM    IFNE CNUM
     C                     MOVE CNUM      RCNUM
     C**********           Z-ADDCNUM      PRCNUM  60                                          CSD027
     C                     MOVE CNUM      PRCNUM  6                                           CSD027
      *
      ** Customer Name and Town
      *
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM RCNUM     PCUST  10
     C                     PARM *BLANKS   PKYSTS  7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD4         DBASE
     C                     MOVELRCNUM     DBKEY
     C                     MOVEL'SDCUSTPD'DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVE BBCRNM    RCNAME
     C                     MOVE BBCRTN    RTOWN
     C                     ELSE
     C                     MOVE *BLANKS   RCNUM
     C                     MOVE *BLANKS   RCNAME
     C                     MOVE *BLANKS   RTOWN
     C                     ENDIF
      *
     C                     MOVE LNRF      RLNRF
      *
      ** Participations Sold
      *
     C********** PSLN      IFNE *ZEROS                                                        CLE148
     C           PSLN      IFNE *BLANKS                                                       CLE148
     C                     MOVE *BLANKS   RCNUM
     C                     MOVE *BLANKS   RTOWN
     C                     MOVE *BLANKS   RCNAME
     C                     MOVEL'PART.'   WKPSLD 10
     C                     MOVE 'SOLD'    WKPSLD
     C                     MOVE WKPSLD    RCNAME
     C                     MOVE PSLN      RLNRF
      *
     C                     Z-SUBCPAM      CPAM
     C                     Z-SUBAMAD      AMAD
     C                     Z-SUBAMTA      AMTA
     C                     ENDIF
      *
      ** Format Date
      *
     C           ACFR      IFNE *ZEROS
     C                     Z-ADDACFR      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RACFR
     C                     ENDIF
      *
     C           ACTD      IFNE *ZEROS
     C                     Z-ADDACTD      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RACTD
     C                     ELSE
     C                     MOVE 'CALL   ' RACTD
     C                     ENDIF
      *
      ** Interest Rate
      *
     C           INTR      MULT 10000000  ZFLD15
     C                     Z-ADD7         ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    RINTR
      *
      ** Access the currency details
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM CCY       PCCY    3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD5         DBASE
     C                     MOVELCCY       DBKEY
     C                     MOVEL'SDCURRPD'DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVE CCY       RCCY
      *
     C                     Z-ADDCPAM      ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    RCPAM
      *
     C                     Z-ADDAMAD      ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    RAMAD
      *
     C                     Z-ADDAMTA      ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    RAMTA
      *
      ** Write out the record
      *
     C                     EXSR SRPREC
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRPREC - Subroutine to Write a Detail record to the Report   *
      *                                                               *
      *****************************************************************
      *
     C           SRPREC    BEGSR
      *
     C           *IN87     IFEQ *ON
     C                     WRITEHEADP1
     C                     MOVE *OFF      *IN87
     C                     ENDIF
      *
      ** Write out Detail Record
      *
     C                     WRITEDETAIL1
     C           RTOWN     IFNE *BLANKS
     C                     WRITEDETAIL2
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRCHK  - Subroutine to Check if Totals are to be printed     *
      *                                                               *
      *****************************************************************
      *
     C           SRCHK     BEGSR
      *
      ** Change of Branch
      *
     C           WKBRCA    IFNE BRCA
     C           *IN89     OREQ *ON
     C                     EXSR SRLTST
     C                     EXSR SRCCY
     C                     EXSR SRENDR
     C           *IN89     IFEQ *OFF
     C                     EXSR SRBRCA
     C                     ENDIF
     C                     GOTO ECHK
     C                     ENDIF
      *
      ** Change in Loan reference for Parts sold loans
      *
     C           WKLNRF    IFNE LNRF
     C********** WKPSLN    ANDNE*ZEROS                                                        CLE148
     C           WKPSLN    ANDNE*BLANKS                                                       CLE148
     C                     EXSR SRLNRF
     C                     ENDIF
      *
      ** Change in Loan type/Subtype
      *
     C           WKLTYP    IFNE LTYP
     C           WKCCY     ANDEQCCY
     C           WKSUTP    ORNE SUTP
     C           WKCCY     ANDEQCCY
     C           WKCLAS    ORNE CLAS                                                          CLE042
     C           WKCCY     ANDEQCCY                                                           CLE042
     C                     EXSR SRLTST
     C                     ENDIF
      *
      ** Change in currency
      *
     C           WKCCY     IFNE CCY
     C                     EXSR SRLTST
     C                     EXSR SRCCY
     C                     ENDIF
      *
     C           ECHK      ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRLTST - Process Change in Loan Type/Sub-type                *
      *                                                               *
      *****************************************************************
      *
     C           SRLTST    BEGSR
      *
     C                     Z-ADDWLTPR     ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    RLTPR
      *
     C                     Z-ADDWLTIAD    ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    RLTIAD
      *
     C                     Z-ADDWLTITA    ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    RLTITA
      *
     C           *IN87     IFEQ *ON
     C                     MOVE *OFF      *IN87
     C                     WRITEHEADP1
     C                     ENDIF
      *
     C                     WRITETOTAL2
     C                     MOVE *BLANKS   PRCNUM
      *
      ** Initialize total fields for Loan Type/Sub-type
      *
     C                     Z-ADD0         WLTPR
     C                     Z-ADD0         WLTIAD
     C                     Z-ADD0         WLTITA
     C                     Z-ADD0         WLNPR
     C                     Z-ADD0         WLNIAD
     C                     Z-ADD0         WLNITA
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRCCY  - Process Change in Currency                          *
      *                                                               *
      *****************************************************************
      *
     C           SRCCY     BEGSR
      *
     C                     MOVE WKCCY     RCCYT
      *
     C                     Z-ADDWCYPR     ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    RCYPR
      *
     C                     Z-ADDWCYIAD    ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    RCYIAD
      *
     C                     Z-ADDWCYITA    ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    RCYITA
      *
     C           *IN87     IFEQ *ON
     C                     MOVE *OFF      *IN87
     C                     WRITEHEADP1
     C                     ENDIF
      *
     C                     WRITETOTAL3
     C                     MOVE *ON       *IN87
      *
      ** Initialize total fields for Currency change
      *
     C                     Z-ADD0         WCYPR
     C                     Z-ADD0         WCYIAD
     C                     Z-ADD0         WCYITA
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRENDR - End of Branch report                                *
      *                                                               *
      *****************************************************************
      *
     C           SRENDR    BEGSR
      *
     C                     MOVE WKBRCA    RBRCA
     C                     WRITETRAILP
      *
      ** Initialize total fields
      *
     C                     Z-ADD0         WCYPR
     C                     Z-ADD0         WCYIAD
     C                     Z-ADD0         WCYITA
     C                     Z-ADD0         WLTPR
     C                     Z-ADD0         WLTIAD
     C                     Z-ADD0         WLTITA
     C                     Z-ADD0         WLNPR
     C                     Z-ADD0         WLNIAD
     C                     Z-ADD0         WLNITA
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRLNRF - Process Change in Loan Reference                    *
      *                                                               *
      *****************************************************************
      *
     C           SRLNRF    BEGSR
      *
     C                     MOVE WKLNRF    RPSLN
      *
     C                     Z-ADDWLNPR     ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    RLNPR
      *
     C                     Z-ADDWLNIAD    ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    RLNIAD
      *
     C                     Z-ADDWLNITA    ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    RLNITA
      *
     C           *IN87     IFEQ *ON
     C                     MOVE *OFF      *IN87
     C                     WRITEHEADP1
     C                     ENDIF
      *
     C                     WRITETOTAL1
      *
      ** Initialize total fields
      *
     C                     Z-ADD0         WLNPR
     C                     Z-ADD0         WLNIAD
     C                     Z-ADD0         WLNITA
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRBRCA - Process Change in Branch                            *
      *                                                               *
      *****************************************************************
      *
     C           SRBRCA    BEGSR
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM BRCA      PBRCA   3
     C           SDBRCH    PARM SDBRCH    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD6         DBASE
     C                     MOVELBRCA      DBKEY
     C                     MOVEL'SDBRCHPD'DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     WRITEHEADP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRAUDT - Subroutine to Output Audit report and End Program   *
      *                                                               *
      *****************************************************************
      *
     C           SRAUDT    BEGSR
      *
     C                     READ LEACXZZF                 89
     C           *IN89     IFEQ *ON
     C           *LOCK     IN   LDA
     C                     Z-ADD7         DBASE
     C                     MOVEL'Z'       DBKEY
     C                     MOVEL'LEACNZZ' DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     MOVE COUNT     RCOUNT
     C                     MOVE NORE      RNORE
      *
     C           NORE      IFNE COUNT
     C                     MOVE *ON       *IN86
     C                     ENDIF
      *
      ** Output Report Header and File Controls
      *
     C                     WRITEHEADAU
      *
     C           COUNT     IFEQ 0
     C                     WRITENODTLS
     C                     ELSE
     C                     WRITECONTROL
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
      ** Check to see that *PSSR has not already been called
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     WRITEHEADAU
     C                     WRITEDBERROR
      *
      ** If *PSSR already run, then just end the program here
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRRCFP
      *****************************************************************
      *                                                               *
      *  SRRCFP - Subroutine to register the P1 Printer File via RCF  *
      *                                                               *
      *****************************************************************
      *
     C           SRRCFP    BEGSR
      *
      ** Ensure Report Spool File recorded by RCF
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRRCFA
      *****************************************************************
      *                                                               *
      *  SRRCFA - Subroutine to register the AU Printer File via RCF  *
      *                                                               *
      *****************************************************************
      *
     C           SRRCFA    BEGSR
      *
      ** Ensure Audit Spool File recorded by RCF
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZFRPEDZ3
      *****************************************************************
** CPY@
(c) Finastra International Limited 2002
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
