     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Extract Manual Changes - Takeon')                *
      *****************************************************************
      *                                                                *
      *  Midas - Customer Lending Module                              **
      *                                                                *
      *   LER030B - EXTRACT MANUAL CHANGES TO FAC AMT - TAKE ON VERSION*
      *                                                                *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. AR674226           Date 04Dec19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE148             Date 23Jul12               *
      *                 AR787620           Date 10Jun11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 225591             Date 08Jul04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4.01.03 ----------------------------------------*
      *                 210813             Date 24Oct02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CLE023             Date 19Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CLE005             Date 22May97               *
      *                 E29404             Date 04Apr92               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR674226 - Manual transaction reached 999. Increase the size *
      *             of utilisation sequence no. and transaction       *
      *             sequence. Recompile. (Child: AR674227)            *
      *           - Applied for MD054782.                             *
      *  AR674226 - Manual transaction reached 999. Increase the size *
      *             of utilisation sequence no. and transaction       *
      *             sequence. Recompile. (Child: AR674227)            *
      *           - Applied for MD054782.                             *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  AR787620 - Hooks Incorporation                               *
      *             WNCPYSRC,LEH00302                                 *
      *             WNCPYSRC,LEH00303                                 *
      *             WNCPYSRC,LEH00304                                 *
      *             WNCPYSRC,LEH00305                                 *
      *             WNCPYSRC,LEH00306                                 *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  225591 - Facility history changes for repayments due to      *
      *           rollover (recompile)                                *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  210813 - Populate HATSEQ in HISACT (added to file by 200289).*
      *  CLE023 - Lending Facility History Improvements. (Recompiled) *
      *  CLE005 - Customer Lending Enhancements - Others (T3)          *
      *   E29404 - INCLUDE EXPIRED FACILITIES.                         *
      *                                                                *
      ******************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*****************************************************************
     FFCLTYFM IP  E                    DISK
      * Facility Record A
      *
     FFCLTY   IF  E           K        DISK
     F            FCLTYHHF                          KIGNORE
     F            FCLTYZZF                          KIGNORE
     F            FCLTYFMF                          KIGNORE
      *
     FSDBANKPDIF  E                    DISK
      * Bank Details
      *
     FFACHISH UF  E           K        DISK                      A
      * Facility History Header
      *
     FHISACT  O   E                    DISK
      * History Actions
      *
     FFACACT  O   E                    DISK
      * Facilities Actioned
     F/COPY WNCPYSRC,LEH00302
      *
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F* 01    ON = NOT first cycle                                    *
     F* 10    Principal Increase                                      *
     F* 11    Principal Decrease                                      *
     F* 20    Data change                                             *
     F* 90    CHAIN fail on FCLTYFN                                   *
     F* 99    CHAIN fail on FACHISH, or No SDBANKPD record found      *
     F* U7    Database error                                          *
     F* U8    Database error                                          *
     F*                                                               *
     F*****************************************************************
     E                    CPY@    1   1 80               Copyright Statement
      *
      /EJECT
     E/COPY WNCPYSRC,LEH00303
     ILDA       E DSLDA
      *
     ISCSARD    E DSSCSARDPD                                              CLE005
      ** Switchable Features File                                         CLE005
      *                                                                   CLE005
     IDSFDY     E DSDSFDY                                                 CLE005
      *                                                                   CLE005
      *
      *****************************************************************
      *
     C                     MOVEACPY@      BIS@   80
      *
      ** Define LDA
      *
     C           *NAMVAR   DEFN           LDA
      *
     C           KEYB      KLIST
     C                     KFLD           FHCNUM
     C                     KFLD           FHFTYP
     C                     KFLD           FHFSEQ
     C                     KFLD           @RTB
     C                     MOVE 'B'       @RTB    1
     C           KEY001    KLIST                           Key to
     C                     KFLD           CNUM             History file
     C                     KFLD           FACT
     C                     KFLD           FCNO
      *
     C  N01                EXSR B0000                      initialisation
      *
     C***        RECI      IFNE 'E'                                       E29404
     C***        RECI      CABNE'D'       AEXIT            Expired/Canc   E29404
     C***                  END                                            E29404
      *
     C           RECI      IFEQ 'E'                                       E29404
     C           RECI      OREQ 'D'                                       E29404
     C           CLE005    OREQ 'Y'                                       CLE005
     C           RECI      ANDEQ'C'                                       CLE005
      *
     C           KEY001    CHAINFACHISH              99
      *
     C           *IN99     IFEQ '0'
      *
     C           FAMT      IFNE FHLFAM
     C           RVCR      ORNE FHRCIN
      *                                                                   CLE005
      ** If SAR CLE005 is Present, include checking of FCTI               CLE005
      *                                                                   CLE005
     C           CLE005    OREQ 'Y'                                       CLE005
     C           FCTI      ANDNEFHFTIN                                    CLE005
      *                                                                   CLE005
     C           CLE005    OREQ 'Y'                                       CLE005
     C           FHRECI    ANDEQ'D'                                       CLE005
     C           FHRECI    ANDNERECI                                      CLE005
      *                                                                   CLE005
     C                     Z-ADD*ZERO     HASEQ                           CLE005
     C                     Z-ADD0         HATSEQ                                              210813
      *                                                                   CLE005
     C                     MOVE CNUM      HACNUM
     C           FACT      MULT 100       HAFCTY
     C                     ADD  FCNO      HAFCTY
      *
     C           DTAP      IFGT BJRDNB
     C                     MOVE DTAP      HADATE
     C                     ELSE
     C                     MOVE BJRDNB    HADATE
     C                     END
      *
     C           FAMT      SUB  FHLFAM    HAAAMT     1011  Determine -
     C                     MOVE 'RC'      HAACTN           Rev credit
     C   10                MOVE 'FI'      HAACTN           or Increase
     C   11                MOVE 'FD'      HAACTN           or Decrease
     C   11                MULT -1        HAAAMT
     C                     WRITEHISACTF                    History file
      *                                                                   CLE005
      ** If SAR CLE005 is Present and change in FCTI                      CLE005
      *                                                                   CLE005
     C           CLE005    IFEQ 'Y'                                       CLE005
      *                                                                   CLE005
     C           FHFTIN    IFNE FCTI                                      CLE005
     C                     MOVEL'TC'      HAACTN                          CLE005
     C                     Z-ADD*ZERO     HAAAMT                          CLE005
     C                     Z-ADD-1        HASEQ                           CLE005
     C                     Z-ADD-1        HATSEQ                                              210813
     C                     WRITEHISACTF                                   CLE005
     C                     ENDIF                                          CLE005
      *                                                                   CLE005
     C           FHRECI    IFEQ 'D'                                       CLE005
     C           RECI      ANDEQ'E'                                       CLE005
     C                     MOVEL'FE'      HAACTN                          CLE005
     C                     Z-ADD*ZERO     HAAAMT                          CLE005
     C                     WRITEHISACTF                                   CLE005
     C                     ENDIF                                          CLE005
      *                                                                   CLE005
     C           FHRECI    IFEQ 'D'                                       CLE005
     C           RECI      ANDEQ'C'                                       CLE005
     C                     MOVEL'FC'      HAACTN                          CLE005
     C                     Z-ADD*ZERO     HAAAMT                          CLE005
     C                     WRITEHISACTF                                   CLE005
     C                     ENDIF                                          CLE005
      *                                                                   CLE005
     C                     ENDIF                                          CLE005
      *                                                                   CLE005
     C                     MOVE HACNUM    FCCNUM
     C                     MOVE HAFCTY    FCFCTY
     C                     MOVE HADATE    FCDATE
     C                     WRITEFACACTF                    Action  file
     C                     END
      *
     C                     SETOF                     20    Data change
      *
     C           FHBRCH    IFNE BRCA
     C                     MOVE BRCA      FHBRCH
     C                     SETON                     20
     C                     END
      *
     C           FHOLPC    IFNE OVPC                       Overline
     C                     Z-ADDOVPC      FHOLPC     202020 percentage
     C                     END                              change
      *
     C           FHEXDT    IFNE DTEX                       Expiry date
     C                     MOVE DTEX      FHEXDT           change
     C                     SETON                     20
     C                     END
      *
     C           FHDAPP    IFNE DTAP                       APPROV date
     C                     MOVE DTAP      FHDAPP           change
     C                     SETON                     20
     C                     END
      *
     C           FHRCIN    IFNE RVCR
     C                     MOVE RVCR      FHRCIN
     C                     SETON                     20
     C                     END
      *
     C           FHMCCY    IFNE MCCY
     C                     MOVE MCCY      FHMCCY
     C                     SETON                     20
     C                     END
      *
     C           FHMCUS    IFNE MCSI                       Multi
     C                     MOVE MCSI      FHMCUS           customer
     C                     SETON                     20    change
     C                     END
      *
     C           FHCTYR    IFNE CRSK
     C                     MOVE CRSK      FHCTYR
     C                     SETON                     20
     C                     END
      *
     C           FHNOMR    IFNE NOMR
     C                     Z-ADDNOMR      FHNOMR     202020
     C                     END
     C           FHACOF    IFNE ACOF
     C                     MOVE ACOF      FHACOF
     C                     SETON                     20
     C                     END
      *                                                                   CLE005
      ** If SAR CLE005 is Present and change in FCTI                      CLE005
      *                                                                   CLE005
     C           CLE005    IFEQ 'Y'                                       CLE005
     C           FHFTIN    ANDNEFCTI                                      CLE005
     C                     MOVELFHFTIN    FHPFTI                          CLE005
     C                     MOVELFCTI      FHFTIN                          CLE005
     C                     MOVE *ON       *IN20                           CLE005
     C                     ENDIF                                          CLE005
      *
     C/COPY WNCPYSRC,LEH00304
     C   20                UPDATFACHISHF
     C                     ELSE                            Create
     C                     MOVE RECI      FHRECI           History
     C                     MOVE CNUM      FHCNUM           Header
     C                     MOVE FACT      FHFTYP
     C                     MOVE FCNO      FHFSEQ
     C                     MOVE BRCA      FHBRCH
     C                     MOVE FCTI      FHFTIN
     C                     MOVE FCCY      FHFCCY
     C                     MOVE FAMT      FHOFAM
     C                     MOVE OVPC      FHOLPC
     C                     MOVE DTAP      FHDAPP
     C                     MOVE DTEX      FHEXDT
     C                     MOVE RVCR      FHRCIN
     C                     MOVE MCCY      FHMCCY
     C                     MOVE MCSI      FHMCUS
     C                     MOVE CRSK      FHCTYR
     C                     MOVE NOMR      FHNOMR
     C                     MOVE ACOF      FHACOF
     C                     Z-ADDFAMT      FHLFAM
     C           KEYB      CHAINFCLTYFNF             90
     C                     Z-ADDCAMD      FHLDAM
      *                                                                   CLE005
      ** If SAR CLE005 is Present, initialise FHPFTI                      CLE005
      *                                                                   CLE005
     C           CLE005    IFEQ 'Y'                                       CLE005
     C                     MOVE *BLANKS   FHPFTI                          CLE005
     C                     ENDIF                                          CLE005
      *                                                                   CLE005
     C/COPY WNCPYSRC,LEH00305
     C                     WRITEFACHISHF                   History file
     C                     MOVE CNUM      FCCNUM
     C           FACT      MULT 100       FCFCTY
     C                     ADD  FCNO      FCFCTY
     C                     MOVE DTAP      FCDATE
     C                     WRITEFACACTF                    Action file
     C                     END
      *
     C***********AEXIT     TAG                                            E29404
     C                     END                                            E29404
      *                                                                   E29404
      *
     C/SPACE 3
     C********************************************************************
     C*
     C*  B0000 --- INITIALIZATION SUBROUTINE
     C*
     C*  CALLED FROM: MAIN PROCESSING
     C*
     C*  CALLS: *PSSR (IF DATABASE ERROR ON SDBANKPD)
     C*
     C********************************************************************
      *
     C           B0000     BEGSR
      *
     C                     SETON                     01
     C           1         SETLLSDBANKPD
     C                     READ SDBANKPD                 99
      *
     C           *IN99     IFEQ '1'
     C           *LOCK     IN   LDA                        ***************
     C                     MOVE '001'     DBASE            * DBERROR 001 *
     C                     MOVEL'BANK'    DBKEY            ***************
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVEL'LER030B' DBPGM
     C                     OUT  LDA                                       CLE005
     C                     EXSR *PSSR
     C                     END
      *                                                                   CLE005
      ** Check if CLE005 is switched *ON.                                 CLE005
      *                                                                   CLE005
     C                     MOVE 'N'       CLE005  1                       CLE005
     C                     CALL 'AOSARDR0'                                CLE005
     C                     PARM *BLANKS   @RTCD   7                       CLE005
     C                     PARM '*VERIFY' @OPTN   7                       CLE005
     C                     PARM 'CLE005'  @SARD   6                       CLE005
     C           SCSARD    PARM SCSARD    DSFDY                           CLE005
      *                                                                   CLE005
     C           @RTCD     IFEQ *BLANKS                                   CLE005
     C                     MOVE 'Y'       CLE005                          CLE005
     C                     END                                            CLE005
      *                                                                   CLE005
     C           @RTCD     IFNE *BLANKS                                   CLE005
     C           @RTCD     ANDNE'*NRF   '                                 CLE005
      *                                                                   CLE005
      ** Call to program ended in error                                   CLE005
      *                                                                   CLE005
     C           *LOCK     IN   LDA                                       CLE005
     C                     MOVEL'CLE005  'DBKEY            ************   CLE005
     C                     MOVE 'SCSARDPD'DBFILE           * DBERR 02 *   CLE005
     C                     Z-ADD002       DBASE            ************   CLE005
     C                     MOVEL'LER030B' DBPGM                           CLE005
     C                     OUT  LDA                                       CLE005
     C                     EXSR *PSSR                                     CLE005
     C                     ENDIF                                          CLE005
      *
     C/COPY WNCPYSRC,LEH00306
     C                     ENDSR
     C*
     C********************************************************************
     C/SPACE 3
     C********************************************************************
     C*
     C*  *PSSR --- SUBROUTINE TO HANDLE ABNORMAL ERROR CONDITIONS
     C*
     C*  CALLED FROM: AFTER ABNORMAL OPERATION OCCURS
     C*
     C*  CALLS: NOTHING
     C*
     C********************************************************************
     C*
     C           *PSSR     BEGSR                           ** *PSSR **
     C*
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
     C*
     C                     SETON                     U7U8LR
     C                     RETRN
     C*
     C                     ENDSR                           ** *PSSR **
     C*
     C********************************************************************
** CPY@
(c) Finastra International Limited 2001
