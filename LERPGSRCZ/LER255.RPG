     H        1   Y
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE/GL Profitability Extract Update Trailers')
      *****************************************************************
      *                                                               *
      *  Midas - Lending Module                                       *
      *                                                               *
      *  LER255 - RE/GL Profitability Extract Update Trailers         *
      *                                                               *
      *  Function - This program will update the Lending Retail and   *
      *             General Ledger Trailer with the number of records *
      *             on the corresponding physical files.              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CCB003  *Create    Date 28Oct96               *
      *  Prev Amend No. XXXXXX             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CCB003 - COB Performance Enhancements - Task Split           *
      *           Update trailers LEPEGLZ/LEPEREZ                     *
      *                                                               *
      *****************************************************************
     FLEPEGLZ UF  E                    DISK         KINFSR *PSSR
      * LE GL Extract Trailer
      *
     FLEPEREZ UF  E                    DISK         KINFSR *PSSR
      * LE RE Extract Trailer
      *
      *****************************************************************
      *                                                               *
      *  Function of indicators                                       *
      *  ----------------------                                       *
      *                                                               *
      *     79 - Error while reading trailers                         *
      *     80 - READ indicator for LEPEGLZ                           *
      *     81 - READ indicator for LEPEREZ                           *
      *                                                               *
      *  U7+U8 - Database error occurs                                *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      *****************************************************************
      /SPACE 3
      *****************************************************************
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
      ** Program Status Data Structure
     IPSDS       SDS
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      * MAIN PROCESSING                                               *
      *****************************************************************
     C           *ENTRY    PLIST
     C                     PARM           NGLREC 100
     C                     PARM           NREREC 100
      *
      ** Initial processing
      *
     C                     EXSR INIT
      *
      ** Main processing
      *
     C                     EXSR MAIN
      *
      ** End processing
      *
     C                     EXSR END
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      * MAIN - Update trailer records with parameters received        *
      *                                                               *
      * Called by: - MAIN PROCESSING                                  *
      *****************************************************************
     C           MAIN      BEGSR
      *
      ** Access and update record
      *
     C                     READ LEPEGLZ                7980
      *
      ** If no record on file then database error
     C           *IN79     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     MOVEL'LEPEGLZ' DBFILE
     C                     MOVEL'*NRF'    DBKEY
     C                     Z-ADD1         DBASE
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     Z-ADDNGLREC    GZNREC
     C                     UPDATLEPEGLZF
      *
      ** Access and update record
      *
     C                     READ LEPEREZ                7981
      *
      ** If no record on file then database error
     C           *IN79     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     SETON                       U7U8
     C                     MOVEL'LEPEREZ' DBFILE
     C                     MOVEL'*NRF'    DBKEY
     C                     Z-ADD2         DBASE
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     Z-ADDNREREC    RZNREC
     C                     UPDATLEPEREZF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * END - Exit program                                            *
      *                                                               *
      * Called by: MAIN PROCESSING                                    *
      *****************************************************************
     C           END       BEGSR
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * INIT - Initial processing                                     *
      *                                                               *
      * Called by: MAIN PROCESSING                                    *
      *****************************************************************
     C           INIT      BEGSR
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      ** Define LDA
     C           *NAMVAR   DEFN           LDA
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      * Called by: (**calling routines**)                             *
      * Calls: None                                                   *
      *****************************************************************
     C           *PSSR     BEGSR
      *
     C                     MOVEL'LER255'  DBPGM
     C                     OUT  LDA
      *
     C                     DUMP
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
** CPY@ - Object copyright
(c) Misys International Banking Systems Ltd. 2001
