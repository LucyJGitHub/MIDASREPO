     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Margin Accrual,Profby Extr,A/CKeyGen')           *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Customer Lending Module                          *
     F*                                                               *
     F*  LER310 - Margin Accruals, Profitabilty Extract & Account Key *
     F*           Generation.                                         *
     F*  Function: To list all the account keys generated by the      *
     F*   (COB)    process that accrues margin and updates the        *
     F*            profitability files with the margin amounts        *
     F*                                                               *
     F*  Called By: CLP/LERC31                                        *
     F*                                                               *
     F*  Calls: RPG/LERDATEF - To convert a date to a day no. for     *
     F*                        first day of month                     *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD051991           Date 22Mar19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 MD047993           Date 23Sep17               *
      *                 MD035545           Date 11Sep15               *
      *                 MD024222           Date 05May14               *
      *                 MD022067           Date 27Aug13               *
      *                 AR996895           Date 25Nov12               *
      *                 AR1056323          Date 14Nov12               *
      *                 AR567790           Date 01Aug12               *
      *                 CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      *                 AR821740           Date 29Aug11               *
      *                 263666             Date 01Feb10               *
      *                 CER059             Date 19Jul10               *
      *                 CER016A            Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CSC042             Date 16Feb09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 246141             Date 26Mar07               *
      *                 164847             Date 30Nov06               *
      *                 BUG12990           Date 05Jan07               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 242286             Date 25Sep06               *
      *                 208013             Date 06Jun06               *
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CLE106             Date 21Feb05               *
      *                 CGL029             Date 01Sep03               *
      *                 CGL020             Date 12Dec02               *
      *                 CLE028             Date 27Jun02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CLE011             Date 07Nov00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 149562             Date 24Feb99               *
      *                 CLE005             Date 22May97               *
     F*                 CAC001             Date 13Aug96               *
     F*                 103173             Date 22May96               *
     F*                 103175             DATE 22MAY96               *
     F*                 103177             DATE 22MAY96               *
     F*                 103178             DATE 22MAY96               *
     F*                 097375             DATE 12FEB96               *
     F*                 074894             DATE 01SEP94               *
     F*                 057399             DATE 10MAR94               *
     F*                 060109             DATE 23FEB94               *
     F*                 E49419             DATE 13JAN93               *
     F*                 E30465             DATE 13APR92               *
     F*                 E32395             DATE 13APR92               *
     F*                                                               *
     F*---------------------------------------------------------------*
      *                                                               *
      *  MD051991 - LERSTS' MIDMON not updated after run              *
      *  MD046248 - Finastra Rebranding                               *
      *  MD047993 - Modified MD035545. Removed dependency on Accrue   *
      *             on Last Day indicator.                            *
      *  MD035545 - LE: Interest Calculation in Leap year. Consider   *
      *             Accrue on Last Day Indicator for method 6.        *      
      *  MD024222 - Changed variable RECI to LNRECI on the condition  *
      *             for tagging a delete on LEPEMGD. LNRECI is the    *
      *             renamed RECI for CLOANCL. (Child: AR961727)       *
      *  MD022067 - LERC21 interfere with LERC30A                     *
      *  AR996895 - Incorrect acrrual interest calculation            *
      *             (Child: AR996897) (Recompile)                     *
      *  AR1056323 - Revert back changes of CLE134 for LKEY1DP and    *
      *              LKEYFED (Recompile)                              *
      *  AR567790 - XM account key was generated. Ignore XI and XP    *
      *             account keys. (Child: AR567791)                   *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  AR821740 - COB - No stamp tax account keys generated         *
      *             (Recompile)                                       *
      *  263666 - Recompiled due to changes in ZINTDYZ2               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER016A - German Interest Calculation: Upgarde of FGE059     *
      *            to Midas Plus (Recompile)                          *
      *  CSC042 - COB Performance Fixes                               *
      *           Cut down the number of AOCURRR* accesses. RZB Sofia *
      *           accesses AOCURRR* more than 189K times              *
      *  246141 - Recompiled due to changes in ZINTDYZ2               *
      *  164847 - Recompile over changes in /COPY ZINTDYZ2.           *
      *  BUG12990 - Recompiled due to changes in ZINTDYZ2/ZINTDYZ2LE  *
      *  242286 - Recompile over changes in /COPY ZINTDYZ2.           *
      *  208013 - Amendment to Margin rate is allowed, this causes    *
      *           MDIATD not to be correctly synchronised with the    *
      *           loan's outstanding interest not posted.  To handle  *
      *           the proportioning of margin to be posted correctly  *
      *           when margin rate is increased from zero, LEPEMGD    *
      *           should always be updated with the accrual amount    *
      *           regardless of the margin rate and MDIATD should be  *
      *           initilised based on values in CLOANCL.              *
      *         - Do not produce margin account keys for zero margin. *
      *           This causes creation of LEPEMGD record that will    *
      *           cause error if margin rate is added.                *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *  CLE106 - Syndication Manager (Recompile)                     *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL020 - Compliance Watch - Additional A/C Postings          *
      *           Information                                         *
      *  CLE028 - Flat Rate Personal Loans (Rule of 78s)              *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *  CLE011 - Rate Fixing Days for Lending Rollover Processing    *
      *           Recompile                                           *
      *  149562 - When Loan Subtype is changed, the LEPEMGD record is *
      *           deleted, thus the margin figures in LER480P1 are    *
      *           blank.  For reversal account keys, check first      *
      *           if the loan is still in CLOAN, before setting       *
      *           REV to 'Y'. Applied fix 140733.                     *
     F*  CLE005 - Customer Lending Enhancements - Others (T3)         *
     F*  CAC001 - Profit Centre Accounting Development:               *
     F*           Do not process new COF account keys.                *
     F*  103173 - Margin accrual is incorrect for discounted loans    *
     F*           with rollover or matured early.                     *
     F*  103175 - Margin accrual is not generated for loans entered   *
     F*           and matured on the same day.                        *
     F*  103177 - Incorrect rate used in calculating the margin.      *
     F*           Applied fix 089914.                                 *
     F*  103178 - Recompiled program due to changes in LKEYMG.        *
     F*           Include the event date as key field so that a/c     *
     F*           keys will be processed according to the sequence    *
     F*           they were generated.                                *
     F*  097375 - Margin amounts calculated incorrectly because the   *
     F*           work field have been set up with no decimal places. *
     F*           Work field have now been changed to:                *
     F*                    WRK1    15,0 ==> 25,9                      *
     F*  074894 - Divide by zero error due to zero interest rate.     *
     F*           Loans with zero interest rate but with margin rate  *
     F*           ('dummy loans') should not generate margin account  *
     F*           keys - fix is to move zero to field MKEAMT.         *
     F*  057399 - Report LER310P1 shows incorrect rundate.            *
     F*           (The date is not wrong at all, but it takes into    *
     F*           consideration EOM, ACCRUAL, & NON-WORKING DAYS,     *
     F*           the actual processing date is not always shown.)    *
     F*           To fix, amend prtfs LER310AU and LER310P1 to show   *
     F*           both dates, and recompile RPG pgm LER310.           *
     F*  060109 - Split reporting of margin a/c keys generated to     *
     F*           branches.                                           *
     F*  E49419 - Adjustments should update month-to-date with        *
     F*           adjustment amount, and MDADJA with the negative     *
     F*           of the adjustment amount for monthly accruals.      *
     F*           Also, corrected hashing. (81 not set off after      *
     F*           negativity check).                                  *
     F*           Also, MDADJA not initialised for new LEPEMGD records*
     F*           Also, LASTM updated here, but LER316 needs its old  *
     F*           value, so only update it here if daily accruals.    *
     F*  E30465 - WRONG ACCOUNT KEYS GENERATED (POS 7 : RECOURSE      *
     F*           IND. = 'R' OR ' ' AND NOT 'Y' OR 'N').              *
     F*  E32395 - DOESN'T PROCESS "GUARANTEE ISSUED" COMMISSION       *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*****************************************************************
     F*
     FLKEYMG  IP  E           K        DISK
     F*           AIADJAQF                          KRENAMEMTRANDF
     F            LKEY1DPF                          KRENAMELKEYF
     FLEPEMGD UF  E           K        DISK                      A
     FLEPEMGZ UF  E                    DISK
     FSDBRCHL0IF  E           K        DISK                               060109
     FCLOAN   IF  E           K        DISK
     F            CLOANHHF                          KIGNORE
     F            CLOANCKF                          KIGNORE
     F            CLOANZ1F                          KIGNORE
     FMKEYHH  O   E                    DISK
     FMKEYDP  O   E                    DISK
     FMKEYZZ  O   E                    DISK
     FLER310P1O   E             80     PRINTER      KINFDS SPOOL      UC
     FLER310AUO   E                    PRINTER      KINFDS SPOOLU
     F/COPY WNCPYSRC,LER310F001
     F/EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   L1  - Level Break on LKEYMG file on Customer Number and     *
     F*         Loan Reference                                        *
     F*                                                               *
     F*   01  - MTRANDF format of LKEYMG file                         *
     F*   02  - LKEYF format of LKEYMG file                           *
     F*   10  - Payment Key                                           *
     F*   20  - First Cycle indicator                                 *
     F*   25  - Chain on LEPEMGD indicator                            *
     F*   30  - Zero decimal places                                   *
     F*   31  - One decimal place                                     *
     F*   32  - Two decimal places                                    *
     F*   33  - Three decimal places                                  *
     F*   40  - Sort Sequence is 3                                    *
     F*   41  - Sort Sequence is 4                                    *
     F*   70  - Multibranch                                           *   060109
     F*   71  - Nofind ind. for CHAIN to SDBRCHL0                     *   060109
     F*   80  - Overflow for -P1                                      *
     F*   81  - Work Indicator                                        *
     F*   89  - Work Indicator                                        *
     F*                                                               *
     F* 90-99 - Used by Standard Subroutines                          *
     F*                                                               *
     F* U7-U8 - Data Base Error                                       *
     F*    U8 - File Controls out of Balance                          *
     F*                                                               *
     F*****************************************************************
     F/SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  L1SR   - To process end of control group                     *
     F*  LRSR   - Total time last record processing                   *
     F*  MOVKEY - To save last Int.Payment Key                        *
     F*  MKYWRT - To write Margin A/C Keys                            *
     F*  HASHSR - To add Event Amount to hash total for trailer       *
     F*  ADJKEY - To set up new A/C Key for manual adjustments to     *
     F*           Accrued Margin                                      *
     F*  MGNEXT - To set up Margin extract record                     *
     F*  ZDATE2 - To Convert a Day Number to Date Formats             *
     F*  INIT   - To perform first cycle calculations                 *
     F*  AUDIT  - To put out audit report and record it on RCF        *
     F*  DBERR  - To perform database error processing                *
     F*  *PSSR  - To Handle Abnormal Error Conditions                 *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
     E                    PWR     1   4  4 1
     E                    AGG        12 15 0
     E                    CURR      999  3               Currency Code                        CSC042
     E                    ##CD      999  1 0             Decimal Places                       CSC042
     E                    ##CS      999 13 8             Spot Rate                            CSC042
     E                    ##CM      999  1               Mult/Div Ind                         CSC042
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZDATE9Z1                                                                   208013
     I/EJECT
     I*
     I* RENAME FIELDS/DEFINE CONTROL GROUP
     I*
     IMTRANDF     01
     I                                              CNUM  L1
     I                                              LNRF  L1
     ILKEYF       02
     I                                              CNUM  L1
     I              LNNO                            LNRF  L1
     ICLOANCLF
     I              RECI                            LNRECI
     I              MDAT                            LNMDAT
     I              ORED                            LNORED
     I              SLID                            LNSLID
     I              CPAM                            LNCPAM
     I              INTR                            LNINTR                103177
     I*
     I            DS
     I* DATASTRUCTURE FOR A/C KEY
     I*
     I****************************************1  10 AKEY                                      CLE042
     I                                        1  14 AKEY                                      CLE042
     I                                        1   2 AK1
     I                                        3   3 ETYPE
     I                                        4   7 AK2
     I                                        6   6 AK6                   CAC001
     I                                        8   8 AK3
     I                                        9   9 WTYPE
     I                                       10  10 ATYPE
     I                                       11  14 AK11                                      CLE042
     I*
     I            DS
     I* SPLIT RUN DATE INTO MONTH
     I*
     I                                        1   7 RUNDAX
     I                                        3   5 MMM
     I*
     I            DS
     I***  For Run Date checks
     I*
     I                                        1   7 ZADATE
     I                                        1   2 DACC
     I                                        3   5 MACC
     I*
     I            DS
     I* DIVIDE UP UK START OF MONTH DATE
     I*
     I                                        1   6 SMDT
     I                                        1   2 SMDD
     I                                        3   40SM
     I                                        5   60SY
     I*
     I            DS
     I* DIVIDE UP US START OF MONTH DATE
     I*
     I                                        1   6 SMDTUS
     I                                        1   20SMUS
     I                                        3   4 SMDDUS
     I*
     I            DS
     I* AGGREGATE BALANCES DATA STRUCTURE
     I*
     I                                    P   1  960AGG
     I                                    P   1   80MDJANA
     I                                    P   9  160MDFEBA
     I                                    P  17  240MDMARA
     I                                    P  25  320MDAPRA
     I                                    P  33  400MDMAYA
     I                                    P  41  480MDJUNA
     I                                    P  49  560MDJULA
     I                                    P  57  640MDAUGA
     I                                    P  65  720MDSEPA
     I                                    P  73  800MDOCTA
     I                                    P  81  880MDNOVA
     I                                    P  89  960MDDECA
     I*
     ISPOOL       DS
     I**   FILE INFORMATION DATA STRUCTURE - P1
     I*
     I                                       83  92 SFILE
     I                                    B 123 1240SFNUM
     I*
     ISPOOLU      DS
     I**   FILE INFORMATION DATA STRUCTURE - AU
     I*
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
     I*
     IPSDS       SDS
     I** PROGRAM STATUS DATA STRUCTURE - REQUIRED FOR DUMP
     I                                      244 253 JOB
     I                                      254 263 USER
     I*
     ILDA       E DSLDA
     I* DATABASE ERROR DS
     I*                                                                     0078
     ILERSTS    E DSLERSTS                                                  0078
     I** Fees/Profitability enhancement status data area                    0078
     I              FACHISD                         WHISST                  0078
     I              LERC2030                        LERC20                  0078
     I              LERC135                         LERC13                  0078
     I              LERC315                         LERC35                  0078
     I              LERC425                         LERC42                  0078
     I              EOYFLAG                         EOYFLG                  0078
     I*
     IRUNDAT    E DSRUNDAT
     I* RUNDAT DATA AREA
     I                                       13  13 MBIN                  060109
     I*
     ISDBANK    E DSSDBANKPD
     I**  EXTERNAL DS FOR BANK DETAILS
     I                                       43  43 TSTB
     I                                       45  47 MRUN
     I*
     ISDGELR    E DSSDGELRPD
      **  EXTERNAL DS FOR GENERAL LEDGER DETAILS
     I*
     ISDCURR    E DSSDCURRPD
     I**  EXTERNAL DS FOR CURRENCY DETAILS
     I*
     ISDMMOD    E DSSDMMODPD                                              CAC001
     I**  EXTERNAL DS FOR MODULE DETAILS                                  CAC001
     I*                                                                   CAC001
     IDSFDY     E DSDSFDY
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
     I*
     IDSSDY     E DSDSSDY
     I* SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE
     I*
     ISCSARD    E DSSCSARDPD                                              CLE005
      ** SAR File Details Accessed via Access Program AOSARDR0            CLE005
      *                                                                   CLE005
     I              'LECalcBasis6LeapYear'C         WPARML                                  MD035545
     I/COPY ZSRSRC,ZINTDYZ1                                                                   208013
     I/COPY ZSRSRC,ZDATE9Z2                                                                   208013
     I/COPY ZSRSRC,ZDAT10Z1                                                                   208013
     C/EJECT
     C           CSC042    IFNE 'Y'                                                           CSC042
     C                     MOVEL'Y'       CSC042  1                                           CSC042
     C                     Z-ADD0         K       40                                          CSC042
     C                     END                                                                CSC042
     C*
     C* CHANGE OF CUST &/OR LOAN
     C*
     C           *INL1     IFEQ '1'
     C*
     C* PERFORM INITIAL PROCESSING FOR 1ST.TIME THROUGH ONLY
     C*
     C           *IN20     CASEQ'0'       INIT
     C                     END
     C*
     C* CHAIN TO MARGIN EXTRACT FILE FOR LATER UPDATE.
     C*
     C           KEYMGN    CHAINLEPEMGD              25
     C*
     C* IF CHAIN FAIL, CLEAR MARGIN BUFFER FOR FIELDS USED IN
     C* DETAIL PROCESSING.
     C*
     C           *IN25     IFEQ '1'
     C                     Z-ADD0         MDMATD
     C                     Z-ADD0         MDPSTD
     C                     Z-ADD0         MDIATD
     C                     Z-ADD0         MDADJA                          E49419
     C                     END
     C*
     C* RESET 'NEW' FLAG.
     C*
     C                     MOVE ' '       NEW     1
     C*
     C* ACCESS LOAN RECORD FOR NEW CONTROL GROUP.
     C*
     C           LNRF      CHAINCLOAN                89
     C           *IN89     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVELLNRF      DBKEY            ************
     C                     Z-ADD3         DBASE            * DBERR 3
     C                     MOVEL'CLOANCL' DBFILE           ************
     C                     MOVEL'LER310'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     EXSR DBERR
     C                     ELSE                                                               208013
      *                                                                                       208013
      ** Recalculate MDIATD as part of initialisation                                         208013
      *                                                                                       208013
     C           ORED      IFLE BJRDNB                                                        208013
     C           VDAT      ANDLEBJRDNB                                                        208013
     C           *IN25     ANDEQ'1'                                                           208013
     C                     EXSR SRCALC                                                        208013
     C                     ENDIF                                                              208013
     C                     END                             ***E001
     C*
     C* IF A CALL LOAN (MATURITY DATE ZERO), MOVE DATE OF NEXT
     C* WORKING DAY IN TO MATURITY DATE FIELD FOR USE IN PGM.
     C*
     C           LNMDAT    IFEQ 0
     C                     Z-ADDBJDNWD    LNMDAT
     C                     END
     C*
     C* IF NEW LOAN, EOM RUN, INPUT TODAY & MATURING THIS RUN
     C* DO NOT PROCESS AS A LOAN EXTRACT RECORD WILL NOT BE
     C* WRITTEN FOR THIS LOAN BY LER220. MARGIN ACCOUNT KEYS
     C* WILL STILL BE WRITTEN.
     C* NOTE: EOM WILL ONLY EQUAL 'Y' DURING AN END OF MONTH
     C*       FOLLOWED BY A WORKING DAY, OR DURING THE SECOND (ANWD1)
     C*       RUN OF AN END OF MONTH FOLLOWED BY A NON-WORKING
     C*       DAY. IN THE LATTER CASE, MATURED LOANS ARE KEPT ON
     C*       CLOAN DURING THE FIRST (NORMAL) RUN.
     C*
     C           *IN25     IFEQ '1'
     C           EOM       ANDEQ'Y'
     C           LNORED    ANDEQRUNDAY
     C           LNMDAT    ANDLECODT
     C                     MOVE 'Y'       NEW
     C                     END
     C*
     C**IGNORE*LOAN*IF*PROCESSING*TYPE*IS*70.***************              E32395
     C***********
     C***********PTYP      IFNE 70                                        E32395
     C*
     C* GET LATEST MARGIN RATE.
     C*
     C                     Z-ADDMARG      MDMGNR
     C*
     C* GET CURRENCY DEC.PLACES FOR EDITING ON REPORT
     C*
     C                     MOVELCCY       CURKEY
     C                     MOVELCCY       ##KC                                                CSC042
     C                     EXSR ##CHKC                                                        CSC042
     C           ##FIC     IFEQ 'Y'                                                           CSC042
      * Found in the array so use the details                                                 CSC042
     C                     MOVELCURR,Q    A6CYCD                                              CSC042
     C                     Z-ADD##CD,Q    A6NBDP                                              CSC042
     C                     Z-ADD##CS,Q    A6SPRT                                              CSC042
     C                     MOVEL##CM,Q    A6MDIN                                              CSC042
     C                     ELSE                                                               CSC042
      * Not in the array so access DB                                                         CSC042
     C                     CALL 'AOCURRR0'
     C                     PARM '*BLANKS' @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM           CURKEY  3
     C           SDCURR    PARM SDCURR    DSSDY
     C           @RTCD     IFNE *BLANKS
     C                     SETON                     89
     C           *LOCK     IN   LDA
     C                     MOVELCCY       DBKEY            ************
     C                     Z-ADD4         DBASE            * DBERR 4
     C                     MOVEL'SDCURRPD'DBFILE           ************
     C                     MOVEL'LER310'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     EXSR DBERR
     C                     ELSE                                                               CSC042
      * Found in the DB so add to the array if there is space                                 CSC042
     C           K         IFLT 999                                                           CSC042
     C                     ADD  1         K                                                   CSC042
     C                     MOVELA6CYCD    CURR,K                                              CSC042
     C                     Z-ADDA6NBDP    ##CD,K                                              CSC042
     C                     Z-ADDA6SPRT    ##CS,K                                              CSC042
     C                     MOVELA6MDIN    ##CM,K                                              CSC042
     C                     END                                                                CSC042
     C                     END
     C                     END                                                                CSC042
     C*
     C                     Z-ADD0         TACC   150
     C                     Z-ADD0         TPST   150
     C                     Z-ADD0         INTACC 150
     C                     Z-ADD0         INTPAY 150
     C                     Z-ADD0         WACC   150
     C                     Z-ADD0         INWACC 150
     C                     MOVE ' '       PAY     1
     C                     MOVE ' '       REV     1
     C                     MOVE ' '       MGN     1
     C*
     C*********************END                                            E32395
     C*
     C                     END
     C*
     C***DETAIL*PROCESSING:*IGNORE*RECORDS*IF*PROC.TYPE*70.*              E32395
     C*
     C***********PTYP      IFNE 70                                        E32395
     C*
     C*  MANUAL ADJUSTMENT RECORD:
     C*     ADD/SUBTRACT MANUAL ADJUSTMENT IN TO CUMULATIVE
     C*     ACCRUED MARGIN, SET UP NEW ADJUSTMENT A/C KEY & OUTPUT.
     C*
     C           *IN01     IFEQ '1'
     C*
     C***  Blank out value of AKEY so can use ETYPE to identify           E49419
     C***  adjustments (ETYPE=' ') from account keys (ETYPE not ' ').     E49419
     C*                                                                   E49419
     C                     MOVE *BLANKS   AKEY                            E49419
     C*                                                                   E49419
     C           RECI      IFEQ 'J'
     C           LNRECI    ANDNE'R'
     C*
     C                     EXSR ADJKEY
     C**********           ADD  EAMT      TACC                                                208013
      *                                                                                       208013
      ** Add adjustment directly to MDMATD                                                    208013
      *                                                                                       208013
     C                     ADD  EAMT      MDMATD                                              208013
     C                     EXSR MKYWRT
     C*
     C                     END
     C*
     C                     END
     C*
     C*  INTEREST A/C KEY:
     C*     ONLY PROCESS IF AMOUNT TYPE = I INTEREST
     C*                              OR = C INTEREST CAPITALIZED
     C*                              OR = D DISCOUNT
     C*                              OR = P PREMIUM
     C*
     C           *IN02     IFEQ '1'
     C                     CLEARWWTYPE                                                      AR567790
     C           CLE134    IFEQ 'Y'                                                         AR567790
     C                     MOVELWTYPE     WWTYPE  2                                         AR567790
     C                     MOVE ATYPE     WWTYPE                                            AR567790
     C                     ENDIF                                                            AR567790
      *                                                                   CAC001
      **  If Analytical Accounting is installed, do not process new       CAC001
      **  Cost of Funds account keys                                      CAC001
      *                                                                   CAC001
     C           BGN0ST    IFNE 'Y'                                       CAC001
     C           BGN0ST    OREQ 'Y'                                       CAC001
     C           AK6       ANDNE'T'                                       CAC001
     C           AK6       ANDNE'F'                                       CAC001
     C*
     C           ATYPE     IFEQ 'I'
     C           WWTYPE    ANDNE'XI'                                                        AR567790
     C           ATYPE     OREQ 'C'
     C           ATYPE     OREQ 'D'
     C           ATYPE     OREQ 'P'
     C           WTYPE     ANDNE'U'                                                           CAS001
     C           WWTYPE    ANDNE'XP'                                                        AR567790
     C           ATYPE     OREQ 'N'                                       103173
     C           ATYPE     OREQ 'E'                                       103173
     C*
     C*     NOT A REVERSAL A/C KEY
     C*
     C           REVI      IFEQ 0
     C*
     C*     IF ACCRUAL KEY CALCULATE MARGIN AMOUNT & WRITE KEY
     C*     USING SAME DETAILS AS READ FROM INTEREST KEY.
     C*
     C           ETYPE     IFEQ 'R'
     C           WTYPE     ANDEQ'W'
     C           ETYPE     OREQ 'A'
     C           ETYPE     OREQ 'V'
     C           ETYPE     OREQ 'R'                                       103173
     C           ATYPE     ANDEQ'N'                                       103173
     C           ETYPE     OREQ 'R'                                       103173
     C           ATYPE     ANDEQ'E'                                       103173
     C*
     C*  Calculate margin amount.                                         074894
     C*  Loans with zero interest rate but with margin rate ('dummy       074894
     C*  loans') should not generate margin account keys                  074894
      * Work field changed to accomodate decimal places                   097375
     C***********EAMT      MULT MARG      WRK1   150                      097375
     C           EAMT      MULT MARG      WRK1   259                      097375
     C           INTR      IFEQ 0                                         074894
     C                     Z-ADD0         MKEAMT 130H                     074894
     C                     ELSE                                           074894
     C           WRK1      DIV  INTR      MKEAMT 130H
     C                     END                                            074894
     C/COPY WNCPYSRC,LER310C001
     C*
     C* ADD AMOUNT TO ACCRUALS IF ACC.KEY & LIVE RECORD.
     C*
     C           ETYPE     IFEQ 'A'
     C           RECI      ANDEQ'D'
      *                                                                                       208013
      ** Regardless of margin amount computed, set on update of file                          208013
      ** if interest accrual amount is not zero and record existed                            208013
      ** in LEPEMGD.                                                                          208013
      *                                                                                       208013
     C           EAMT      IFNE *ZERO                                                         208013
     C           *IN25     ANDEQ'0'                                                           208013
     C                     MOVE 'Y'       MGN                                                 208013
     C                     ENDIF                                                              208013
     C*
     C* IF DISCOUNT ACCRUAL KEY FOR INTEREST ROUNDING ADJUSTMENT
     C* AT MATURITY, SET MARGIN KEY AMOUNT TO REMAINING ACCRUAL
     C* AMOUNT TO CLEAR ACCOUNTS.
     C*
     C           ATYPE     IFEQ 'D'
     C           LNMDAT    ANDLECODT
     C           LNMDAT    ANDNERLDT                                      103173
     C           LNMDAT    ANDGTORMD                                      103173
     C           ATYPE     OREQ 'P'
     C           WTYPE     ANDNE'U'                                                           CAS001
     C           LNMDAT    ANDLECODT
     C           LNMDAT    ANDNERLDT                                      103173
     C           LNMDAT    ANDGTORMD                                      103173
     C*                                                                   103175
     C** For loan maturing on this run and there is no record in          103175
     C** LEPEMGD, post the amount of value date a/c key.                  103175
     C*                                                                   103175
     C           *IN25     IFEQ '1'                                       103175
     C           LNORED    ANDEQBJRDNB                                    103175
     C                     Z-ADDTPST      TACC                            103175
     C                     Z-ADDTPST      MKEAMT                          103175
     C                     ADD  EAMT      INTACC                          103175
     C                     ELSE                                           103175
     C                     Z-SUBMDMATD    TACC
     C                     Z-SUBMDMATD    MKEAMT
     C                     ADD  EAMT      INTACC
     C                     END                                            103175
     C                     ELSE
     C                     ADD  MKEAMT    TACC
     C                     ADD  EAMT      INTACC
     C                     END
     C*
     C                     END
     C*
     C* ADD AMOUNT TO POSTINGS IF VALUE DATE KEY FOR DISCOUNT.
     C** Also add amount to posting if rollover key for discount.         103173
     C*
     C           ETYPE     IFEQ 'V'
     C           ETYPE     OREQ 'R'                                       103173
     C           ATYPE     ANDEQ'N'                                       103173
     C                     ADD  MKEAMT    TPST
     C                     SUB  EAMT      INTACC
     C                     END
     C*                                                                   103173
     C** For rebate account keys, make sure the margin of the discounted  103173
     C** loan is adjusted.                                                103173
     C*                                                                   103173
     C           ETYPE     IFEQ 'R'                                       103173
     C           ATYPE     ANDEQ'E'                                       103173
     C                     Z-ADDMDMATD    MKEAMT                          103173
     C                     ADD  TACC      MKEAMT                          103173
     C                     ADD  MKEAMT    TPST                            103173
     C                     ADD  EAMT      INTACC                          103173
     C                     END                                            103173
     C*
     C* ADD AMOUNT TO WRITE OFF IF WRITE-OFF KEY.
     C*
     C           ETYPE     IFEQ 'R'
     C           WTYPE     ANDEQ'W'
     C                     ADD  MKEAMT    WACC
     C                     ADD  EAMT      INWACC
     C                     END
     C*
     C* WRITE MARGIN A/C KEY.
     C*
     C           MKEAMT    IFNE 0
     C                     Z-ADDMKEAMT    EAMT
     C                     EXSR MKYWRT
     C                     END
     C*
     C                     END
     C*
     C*     IF PAYMENT KEY ADD EVENT AMOUNT READ TO TOTAL INT.POSTED
     C*     AND STORE A/C KEY DETAILS FOR OUTPUT LATER.
     C*
     C           ETYPE     IFEQ 'R'
     C           WTYPE     ANDNE'W'
     C           RECI      ANDEQ'D'
     C           PTYP      ANDNE63                                        103173
     C           PTYP      ANDNE65                                        103173
     C           PTYP      ANDNE67                                        103173
     C*
     C           PTYP      IFNE 80                                                            CLE028
     C           PTYP      OREQ 80                                                            CLE028
     C           ADIF      ANDNE'Y'                                                           CLE028
     C           CLE028    ANDEQ'Y'                                                           CLE028
     C                     ADD  EAMT      INTPAY
     C                     MOVE 'Y'       PAY
     C                     MOVE '1'       *IN10
     C                     EXSR MOVKEY
     C                     ENDIF                                                              CLE028
     C*
     C                     END
     C*
     C*     REVERSAL A/C KEY.
     C*
     C                     ELSE
     C*
     C           LNRF      CHAINCLOAN                89                   149562
     C           *IN89     IFEQ '1'                                       149562
     C           *IN89     OREQ '0'                                       149562
     C********** RECI      ANDEQ'R'                                                  149562 MD024222
     C           LNRECI    ANDEQ'R'                                                         MD024222
     C                     MOVE 'Y'       REV
     C                     ELSE                                           149562
     C                     MOVE SUTP      MDSUTP                          149562
     C                     MOVE CLAS      MDCLAS                                              CLE042
     C                     ENDIF                                          149562
     C*
     C*     ACCRUAL KEY - WRITE CORRESPONDING MARGIN KEY WITH
     C*                   MARGIN ACCRUED TO DATE.
     C*     IF A DISCOUNTED LOAN, ACCRUED AMOUNT TO BE REVERSED
     C*     IS ACCRUED PLUS POSTED.
     C*
     C           ETYPE     IFEQ 'A'
     C*
     C           ATYPE     IFEQ 'D'
     C           ATYPE     OREQ 'P'
     C           WTYPE     ANDNE'U'                                                           CAS001
     C           MDMATD    ADD  MDPSTD    EAMT
     C                     ELSE
     C                     Z-ADDMDMATD    EAMT
     C                     END
     C*
     C                     EXSR MKYWRT
     C*
     C                     END
     C*
     C*     PAYMENT KEY OR VALUE DATE KEY FOR DISCOUNTED LOAN:
     C*           - WRITE CORRESPONDING MARGIN KEY WITH
     C*             MARGIN POSTED TO DATE.
     C*
     C           ETYPE     IFEQ 'R'
     C           ETYPE     OREQ 'V'
     C*
     C                     Z-ADDMDPSTD    EAMT
     C                     EXSR MKYWRT
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     ENDIF                                          CAC001
      *                                                                   CAC001
     C                     END
     C*
     C*********************END                                            E32395
     C/COPY WNCPYSRC,LER310C002
     C*
     C* END OF CONTROL GROUP PROCESSING
     C*
     CL1                   EXSR L1SR
     C*
     CLR                   EXSR LRSR
     C*
      /EJECT                                                                                  CSC042
     C********************************************************************                    CSC042
     C*                                                                                       CSC042
     C*  ##CHKC  - Check the currency details                                                 CSC042
     C*                                                                                       CSC042
     C********************************************************************                    CSC042
     C           ##CHKC    BEGSR                                                              CSC042
      *                                                                                       CSC042
     C           *LIKE     DEFN BJCYCD    ##KC                                                CSC042
     C           *LIKE     DEFN Q         ##QSV                                               CSC042
      *                                                                                       CSC042
      * Set off the currency found indicator                                                  CSC042
     C                     MOVEL*BLANK    ##FIC   1                                           CSC042
      *                                                                                       CSC042
      * Save the state of indicator 87 so it can be reset at the end                          CSC042
      * of the SR as it may be used elsewhere in the code                                     CSC042
     C           *IN87     IFEQ *ON                                                           CSC042
     C                     MOVEL'Y'       #IN87   1                                           CSC042
     C                     ELSE                                                               CSC042
     C                     MOVEL'N'       #IN87                                               CSC042
     C                     END                                                                CSC042
      *                                                                                       CSC042
     C                     SETOF                         87                                   CSC042
      *                                                                                       CSC042
      * Restore Q from ##QSV in case Q is used elsewhere in the code                          CSC042
     C                     Z-ADD##QSV     Q       40                                          CSC042
      *                                                                                       CSC042
      * Are we looking for the same details as the last time this                             CSC042
      * subroutine was called i.e. is Q already set correctly?                                CSC042
      * If so then use the data rather than do another LOKUP                                  CSC042
      * First make sure that we don't reference the 0th array element                         CSC042
     C           Q         IFEQ 0                                                             CSC042
     C                     Z-ADD1         Q                                                   CSC042
     C                     END                                                                CSC042
      * Now see if this search is same as last one performed                                  CSC042
      * If we have a match then seton 87 so the code after this                               CSC042
      * subroutine knows it can use Q directly                                                CSC042
     C           ##KC      IFEQ CURR,Q                                                        CSC042
     C                     SETON                         87                                   CSC042
     C                     ELSE                                                               CSC042
      * Otherwise hunt in the array and seton 87 if found                                     CSC042
     C                     Z-ADD1         Q                                                   CSC042
     C           ##KC      LOKUPCURR,Q                   87                                   CSC042
     C                     END                                                                CSC042
      *                                                                                       CSC042
      * Save Q for next time                                                                  CSC042
     C           *IN87     IFEQ *ON                                                           CSC042
     C                     Z-ADDQ         ##QSV                                               CSC042
      * Set the indicator to show the data was found                                          CSC042
     C                     MOVEL'Y'       ##FIC                                               CSC042
     C                     END                                                                CSC042
      *                                                                                       CSC042
      * Reset the state of indicator 87.                                                      CSC042
     C           #IN87     IFEQ 'Y'                                                           CSC042
     C                     MOVEL*ON       *IN87                                               CSC042
     C                     ELSE                                                               CSC042
     C                     MOVEL*OFF      *IN87                                               CSC042
     C                     END                                                                CSC042
      *                                                                                       CSC042
     C                     ENDSR                                                              CSC042
      *                                                                                       CSC042
     C********************************************************************                    CSC042
     C/EJECT
     C*****************************************************************
     C*
     C***  L1SR SUBROUTINE TO PROCESS END OF CONTROL GROUP
     C*
     C***  CALLED FROM: MAIN PROCESSING (TOTAL TIME)
     C*
     C***  CALLS: MGNEXT,MOVKEY,MKYWRT
     C*
     C********************************************************************
     C*
     C           L1SR      BEGSR
     C*
     C**ONLY*PROCESS*IF*LOAN*HAD*PTYP*NOT*EQUAL*70.********               E32395
     C*
     C***********PTYP      IFNE 70
     C*
     C* PROCESSING FOR NON-REVERSALS
     C*
     C           REV       IFNE 'Y'
     C*
     C*     PREVIOUS LOAN WAS LIVE & HAD INTEREST PAYMENT A/C KEYS.
     C*     CALCULATE EVENT AMOUNT FOR MARGIN A/C KEY, RETRIEVE
     C*     LAST PAYMENT A/C KEY DETAILS & WRITE MARGIN ACCOUNT
     C*     KEY FOR MARGIN POSTING AMOUNT.
     C*     IF MATURING THIS RUN THEN CALCULATE POSTING AMOUNT AS
     C*     MARGIN ACCRUED SINCE LAST RUN PLUS ACCRUED AMOUNT THIS
     C*     RUN. OTHERWISE, CALCULATE MARGIN AS A PROPORTION OF
     C*     INTEREST PAYMENT, I.E.:
     C*
     C*           INTEREST PAYMENT
     C*           ~~~~~~~~~~~~~~~~   X  MARGIN ACCRUED
     C*           INTEREST ACCRUED
     C*
     C* N.B.IF LOAN NOT ON SYSTEM NOW, MATURING TODAY & NO ACCRUAL
     C*     KEY READ, HENCE MDIATD & INTACC BOTH ZERO GIVING WRK2
     C*     = ZERO, DO NOT PROCESS. (HAPPENS FOR SET UP ONLY).
     C*
     C           PAY       IFEQ 'Y'
      *                                                                                       208013
      ** Set on update of LEPEMGD to compensate negative INTPAY                               208013
      ** This will enable update of MDIATD on negative INTPAY                                 208013
      ** For any other adjustments regarding MDIATD, manual update                            208013
      ** may be required or run the utility UTLE0130                                          208013
      *                                                                                       208013
     C                     MOVE 'Y'       MGN                                                 208013
     C*
     C           LNMDAT    IFLE CODT
     C           INTPAY    ANDGT0                                                             208013
     C*
     C           MDMATD    ADD  TACC      TPST
     C           TPST      IFNE 0
     C                     MOVE '0'       *IN10
     C                     EXSR MOVKEY
     C                     Z-ADDTPST      EAMT
     C                     EXSR MKYWRT
     C                     END
     C*
     C                     ELSE
      *                                                                                       208013
      ** Calculation should not include recent accruals.  This will                           208013
      ** only be true if margin rate is unamendable.                                          208013
     C*
     C********** TACC      ADD  MDMATD    TPST                                                208013
     C********** INTACC    ADD  MDIATD    WRK2   150                                          208013
     C                     Z-ADDMDIATD    WRK2   150                                          208013
     C                     Z-ADDMDMATD    TPST                                                208013
     C           WRK2      IFNE 0
      *                                                                                       208013
      ** Do not post negative Margin from negative manual repayments                          208013
      *                                                                                       208013
     C           INTPAY    ANDGT0                                                             208013
     C           INTPAY    DIV  WRK2      WRK3   154H
     C                     MULT WRK3      TPST
     C                     MOVE '0'       *IN10
     C                     EXSR MOVKEY
     C                     Z-ADDTPST      EAMT
     C                     EXSR MKYWRT
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C* UPDATE/WRITE MARGIN EXTRACT RECORD WITH CHANGED DATA IF AT
     C* LEAST 1 A/C KEY WRITTEN, ELSE UPDATE TO RELEASE RECORD IF
     C* FOUND & NO KEYS WRITTEN. DO NOT WRITE NEW EXTRACT RECORD IF
     C* 'NEW' FLAG = 'Y' (SEE L1 DETAIL PROCESSING).
     C*
     C           NEW       IFEQ 'Y'
     C                     MOVE ' '       MGN
     C                     END
     C*
     C           MGN       CASEQ'Y'       MGNEXT
     C                     END
     C           *IN25     IFEQ '0'
     C           *IN20     IFEQ '1'
     C                     UPDATLEPEMGDF
     C                     END
     C                     ELSE
     C           MGN       IFEQ 'Y'
     C                     WRITELEPEMGDF
     C                     ADD  1         NADD    50
     C                     END
     C                     END
     C*
     C                     END
     C*
     C***  PREVIOUS LOAN WAS REVERSED - DELETE MARGIN EXTRACT RECORD.
     C*
     C           REV       IFEQ 'Y'
     C*
     C           *IN25     IFEQ '0'
     C                     DELETLEPEMGDF
     C                     ADD  1         NDEL    50
     C                     END
     C*
     C                     END
     C*
     C*********************END                                            E32395
     C*
     C***  Zero margin adjustment storage field                           E49419
     C*                                                                   E49419
     C                     Z-ADD0         MADJ   130                      E49419
     C*                                                                   E49419
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************************
     C*
     C***  LRSR SUBROUTINE FOR TOTAL TIME LASR RECORD PROCESSING
     C*
     C***  CALLED FROM: MAIN PROCESSING (TOTAL TIME)
     C*
     C***  CALLS: INIT
     C*
     C********************************************************************
     C*
     C           LRSR      BEGSR
     C*
     C***  ALL ADJUSTMENTS & A/C KEYS READ: UPDATE MARGIN TRAILER
     C*
     C                     READ LEPEMGZ                  89
     C           *IN89     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVEL'*READ'   DBKEY            ************
     C                     Z-ADD5         DBASE            * DBERR 5
     C                     MOVEL'LEPEMGZ' DBFILE           ************
     C                     MOVEL'LER310'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     EXSR DBERR
     C                     END
     C*
     C                     Z-ADDMZNREC    NPREV   50
     C                     ADD  NADD      MZNREC
     C                     SUB  NDEL      MZNREC
     C                     UPDATLEPEMGZF
     C*
     C*** IF EMPTY LKEYMG FILE READ, FORCE INIT ROUTINE TO GET HEADINGS
     C*
     C           *IN20     CASEQ'0'       INIT
     C                     END
     C*
     C***  WRITE CONTROL TOTALS TO REPORT.
     C*
     C           NORE      IFNE 0
     C                     WRITELER310T1
     C                     WRITELER310T2
     C                     END
     C*
     C***  Ensure report spool file recorded by RCF
     C*
     C                     EXSR AUDIT
     C                     WRITELER310T3
     C*
     C***  WRITE MARGIN A/C KEYS TRAILER
     C*
     C                     ADD  2         NORE
     C                     MOVE 'T'       RECI
     C                     WRITELKEY1ZZF
     C*                                                                     0078
     C** Output the control date to the dataarea LERSTS for use next        0078
     C** run                                                                0078
     C***  But only if the system is on daily accruals. LER316 (which     E49419
     C***  only runs in monthly systems) needs the old value, so don't    E49419
     C***  update it until the end of LER316.                             E49419
     C*                                                                     0078
     C           BKAPFQ    IFEQ 'D'                                       E49419
     C           *LOCK     IN   LERSTS                                                      MD022067
      *                                                                                     MD051991
      ** Save the last run date for the margin adjusment in LER315                          MD051991
      *                                                                                     MD051991
     C                     Z-ADDLASTMN    MIDMON  50                                        MD051991
     C                     Z-ADDCODT      LASTMN  50                        0078
     C                     END                                            E49419
     C*                                                                   E49419
     C                     OUT  LERSTS                                      0078
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************************
     C*
     C***  MOVKEY SUBROUTINE TO SAVE LAST INT.PAYMENT KEY
     C*
     C***  CALLED FROM: MAIN PROCESSING,L1SR
     C*
     C***  CALLS: NOTHING
     C*
     C********************************************************************
     C*
     C           MOVKEY    BEGSR
     C*
     C* MOVE FIELDS TO SAVE FIELDS IF JUST READ
     C*
     C           *IN10     IFEQ '1'
     C*
     C                     MOVE RECI      SRECI
     C                     MOVE AKEY      SAKEY
     C                     MOVE BRCA      SBRCA
     C                     Z-ADDACSQ      SACSQ
     C                     Z-ADDEDAT      SEDAT
     C                     MOVE ECCY      SECCY
     C                     Z-ADDSETP      SSETP
     C                     MOVE OSAC      SOSAC
     C                     Z-ADDACSQ      SACSQ
     C                     Z-ADDREVI      SREVI
     C                     Z-ADDOTHA      SOTHA
     C                     MOVE OTHC      SOTHC
     C                     Z-ADDEXRT      SEXRT
     C                     Z-ADDSTDT      SSTDT
     C                     Z-ADDSLID      SSLID
     C                     Z-ADDMDAT      SMDAT
     C                     Z-ADDBRTT      SBRTT
     C                     Z-ADDBRTE      SBRTE
     C                     Z-ADDRTSP      SRTSP
     C                     Z-ADDINTR      SINTR
     C                     Z-ADDSSEQ      SSSEQ
     C                     Z-ADDCPAM      SCPAM
     C                     Z-ADDPACD      SPACD
     C                     Z-ADDICBS      SICBS
     C                     MOVE ACOF      SACOF
     C                     MOVE FACO      SFACO
     C                     Z-ADDPELAP     SPELAP
     C                     Z-ADDORED      SORED
      *                                                                                       CGL020
      ** AML Fields                                                                           CGL020
      *                                                                                       CGL020
     C           CGL020    IFEQ 'Y'                                                           CGL020
      *                                                                                       CGL020
     C                     MOVE F1RACT    WRACT                                               CGL020
     C                     MOVE F1RCTY    WRCTY                                               CGL020
     C                     MOVE F1RBNK    WRBNK                                               CGL020
     C                     MOVE F1RCDE    WRCDE                                               CGL020
     C                     MOVE F1PACT    WPACT                                               CGL020
     C                     MOVE F1PCTY    WPCTY                                               CGL020
     C                     MOVE F1PBNK    WPBNK                                               CGL020
     C                     MOVE F1PCDE    WPCDE                                               CGL020
     C                     MOVE F1DACT    WDACT                                               CGL020
     C                     MOVE F1DCTY    WDCTY                                               CGL020
     C                     MOVE F1DBNK    WDBNK                                               CGL020
     C                     Z-ADDF1SDAT    WSDAT                                               CGL020
      *                                                                                       CGL020
     C                     ENDIF                                                              CGL020
     C*
     C* SET UP FIELDS FOR OUTPUT
     C*
     C                     ELSE
     C*
     C                     MOVE SRECI     RECI
     C                     MOVE SAKEY     AKEY
     C                     MOVE SBRCA     BRCA
     C                     Z-ADDSACSQ     ACSQ
     C                     Z-ADDSEDAT     EDAT
     C                     MOVE SECCY     ECCY
     C                     Z-ADDSSETP     SETP
     C                     MOVE SOSAC     OSAC
     C                     Z-ADDSACSQ     ACSQ
     C                     Z-ADDSREVI     REVI
     C                     Z-ADDSOTHA     OTHA
     C                     MOVE SOTHC     OTHC
     C                     Z-ADDSEXRT     EXRT
     C                     Z-ADDSSTDT     STDT
     C                     Z-ADDSSLID     SLID
     C                     Z-ADDSMDAT     MDAT
     C                     Z-ADDSBRTT     BRTT
     C                     Z-ADDSBRTE     BRTE
     C                     Z-ADDSRTSP     RTSP
     C                     Z-ADDSINTR     INTR
     C                     Z-ADDSSSEQ     SSEQ
     C                     Z-ADDSCPAM     CPAM
     C                     Z-ADDSPACD     PACD
     C                     Z-ADDSICBS     ICBS
     C                     MOVE SACOF     ACOF
     C                     MOVE SFACO     FACO
     C                     Z-ADDSPELAP    PELAP
     C                     Z-ADDSORED     ORED
      *                                                                                       CGL020
      ** AML Fields                                                                           CGL020
      *                                                                                       CGL020
     C           CGL020    IFEQ 'Y'                                                           CGL020
      *                                                                                       CGL020
     C                     MOVE WRACT     F1RACT                                              CGL020
     C                     MOVE WRCTY     F1RCTY                                              CGL020
     C                     MOVE WRBNK     F1RBNK                                              CGL020
     C                     MOVE WRCDE     F1RCDE                                              CGL020
     C                     MOVE WPACT     F1PACT                                              CGL020
     C                     MOVE WPCTY     F1PCTY                                              CGL020
     C                     MOVE WPBNK     F1PBNK                                              CGL020
     C                     MOVE WPCDE     F1PCDE                                              CGL020
     C                     MOVE WDACT     F1DACT                                              CGL020
     C                     MOVE WDCTY     F1DCTY                                              CGL020
     C                     MOVE WDBNK     F1DBNK                                              CGL020
     C                     Z-ADDWSDAT     F1SDAT                                              CGL020
      *                                                                                       CGL020
     C                     ENDIF                                                              CGL020
     C*
     C                     END
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************************
     C*
     C***  MKYWRT SUBROUTINE TO WRITE MARGIN A/C KEYS.
     C*
     C***  CALLED FROM: MAIN PROCESSING,L1SR
     C*
     C***  CALLS: HASHSR
     C*
     C********************************************************************
     C*
     C           MKYWRT    BEGSR
      *                                                                                       208013
      ** Only write margin account keys if margin rate is non zero.                           208013
      *                                                                                       208013
     C           MARG      IFNE 0                                                             208013
     C*
     C* SET UP FIELDS FOR OUTPUT & SET FLAG FOR AT LEAST 1 RECORD
     C* WRITTEN FOR THIS LOAN.
     C*
     C*********************MOVE*'M'*******AKEY                                                CLE042
     C                     MOVE 'M'       ATYPE                                               CLE042
     C                     MOVE LNRF      LNNO
     C                     Z-ADDMARG      INTR
     C                     WRITELKEY1DPF
     C                     ADD  1         NORE
     C                     MOVE 'Y'       MGN
     C*
     C* ADD EVENT AMOUNT TO HASH TOTAL FOR TRAILER
     C*
     C           EAMT      DIV  1000      GPAMT
     C                     EXSR HASHSR
     C*
     C* SET UP AMOUNT FIELD FOR REPORT FOR NO. OF DEC.PLACES.
     C* & SET INDICATOR FOR NARRATIVE FIELD.
     C*
     C                     MOVEA'0000'    *IN,30
     C           A6NBDP    ADD  30        DP      20
     C                     MOVE '1'       *IN,DP
     C   30                MOVE EAMT      EAMT0  130
     C   31                MOVE EAMT      EAMT1  131
     C   32                MOVE EAMT      EAMT2  132
     C   33                MOVE EAMT      EAMT3  133
     C                     MOVEA'00'      *IN,40
     C           RECI      IFEQ 'G'
     C           SSEQ      IFEQ 3
     C                     MOVE '1'       *IN40
     C                     END
     C           SSEQ      IFEQ 4
     C                     MOVE '1'       *IN41
     C                     END
     C                     END
     C*
     C* WRITE HEADINGS IF O/FLOW OR 1ST.DETAIL WRITTEN.
     C* WRITE DETAIL LINE TO REPORT
     C*
     C           *IN80     IFEQ '1'
     C           NORE      OREQ 1
     C           MBIN      OREQ 'Y'                                       060109
     C           SAVBR     ANDNEBRCA                                      060109
     C*
     C*** IF 1ST DETAIL, ENSURE RCF USED
     C*
     C           NORE      IFEQ 1
     C           MBIN      OREQ 'Y'                                       060109
     C           SAVBR     ANDNEBRCA                                      060109
     C*                                                                   060109
     C           MBIN      IFEQ 'Y'                                       060109
     C*                                                                   060109
     C** Output no. of records for each branch and the narrative          060109
     C** 'END OF BRANCH REPORT'                                           060109
     C*                                                                   060109
     C           SAVBR     IFNE '   '                                     060109
     C                     WRITELER310T1                                  060109
     C                     WRITELER310T2                                  060109
     C                     END                                            060109
     C*                                                                   060109
     C** Save branch for comparison (detect branch change)                060109
     C*                                                                   060109
     C                     MOVE BRCA      SAVBR                           060109
     C*                                                                   060109
     C** Search branch code in branch file                                060109
     C*                                                                   060109
     C           BRCA      CHAIN@A8REB1              71                   060109
     C*                                                                   060109
     C** If chain fails - DB error                                        060109
     C*                                                                   060109
     C           *IN71     IFEQ '1'                                       060109
     C           *LOCK     IN   LDA                                       060109
     C                     MOVELBRCA      DBKEY            ************   060109
     C                     Z-ADD6         DBASE            * DBERR 6      060109
     C                     MOVEL'SDBRCHL0'DBFILE           ************   060109
     C                     MOVEL'LER310'  DBPGM                           060109
     C                     OUT  LDA                                       060109
     C                     EXSR *PSSR                                     060109
     C                     EXSR DBERR                                     060109
     C                     END                                            060109
     C*                                                                   060109
     C                     Z-ADD0         SNORE                           060109
     C*                                                                   060109
     C                     END                                            060109
     C*                                                                   060109
     C                     CLOSELER310P1
     C                     OPEN LER310P1
     C                     Z-ADD0         PAGE1
     C*
     C***  Ensure report spool file recorded by RCF
     C*
     C                     Z-ADDSFNUM     ZSNUM
     C*
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ     5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILE
     C                     PARM           ZSNUM   60
     C                     PARM           ZSERR   1
      *
      ** If error during ZSFILE processing, return to calling program
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
      *
     C                     END
      *
     C                     WRITELER310H1
     C                     WRITELER310H2
     C                     MOVE '0'       *IN80
     C                     END
     C                     WRITELER310D1
     C*                                                                   060109
     C           *IN70     IFEQ '1'                                       060109
     C                     ADD  1         SNORE                           060109
     C                     END                                            060109
     C                     ELSE                                                               208013
      *                                                                                       208013
      ** Update LEPEMGD if the loan is found in the said file                                 208013
      *                                                                                       208013
     C           *IN25     IFEQ '0'                                                           208013
     C                     MOVE 'Y'       MGN                                                 208013
     C                     ENDIF                                                              208013
     C                     ENDIF                                                              208013
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************************
     C*
     C***  HASHSR SUBROUTINE TO ADD EVENT AMOUNT TO HASH TOTAL FOR
     C***  TRAILER
     C*
     C***  CALLED FROM: MKYWRT
     C*
     C***  CALLS: NOTHING
     C*
     C********************************************************************
     C*
     C           HASHSR    BEGSR
     C*
     C**   SET UP FIELDS TO BE HASHED
     C*
     C                     Z-ADDGPAMT     GPAMT  153   81
     C   81                Z-SUBGPAMT     GPAMT
     C                     Z-ADDGPAMT     WKAMTI 150
     C                     MOVE GPAMT     WKAMTD  30
     C*
     C**  ADD DECIMAL PART AND CHECK FOR CARRY
     C*
     C                     ADD  WKAMTD    VLRL
     C*
     C                     MOVE '0'       *IN81                           E49419
     C           VLRL      IFLT WKAMTD
     C                     SETON                     81
     C                     END
     C*
     C   81                ADD  1         WKAMTI
     C                     SETOF                     81
     C*
     C**  ADD INTEGER PART
     C*
     C                     ADD  WKAMTI    VLRF
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************************
     C*
     C***  ADJKEY SUBROUTINE TO SET UP NEW A/C KEY FOR MANUAL
     C***  ADJUSTMENTS TO ACCRUED MARGIN.
     C*
     C***  CALLED FROM: MAIN PROCESSING
     C*
     C***  CALLS: NOTHING
     C*
     C********************************************************************
     C*
     C           ADJKEY    BEGSR
     C*
     C                     MOVE 'D'       RECI
     C                     MOVE *BLANKS   AKEY
     C                     MOVE LTYP      AK1
     C                     MOVE 'A'       ETYPE
     C                     MOVELSUTP      AK2
     C                     MOVELCLAS      AK11                                                CLE042
     C*********************MOVE RCSI      AK2                             E30465
     C*                                                                   E30465
     C* POS. 7 RECOURSE IND. (R = WITH RECOURSE)                          E30465
     C*                                                                   E30465
     C           RCSI      IFEQ 'R'                                       E30465
     C           RCSI      OREQ 'Y'                                       E30465
     C                     MOVE 'R'       AK2                             E30465
     C                     ELSE                                           E30465
     C                     MOVE ' '       AK2                             E30465
     C                     END                                            E30465
     C*                                                                   E30465
     C                     Z-ADDLASQ      ACSQ
     C                     Z-ADDRUNDAY    EDAT
     C           SIGN      IFEQ '+'
     C                     Z-ADDMAAM      EAMT
     C                     ELSE
     C                     Z-SUBMAAM      EAMT
     C                     END
     C*                                                                   E49419
     C***  Keep track of margin adjustments so can distinguish from       E49419
     C***  from total margin figure                                       E49419
     C*                                                                   E49419
     C                     ADD  EAMT      MADJ                            E49419
     C*                                                                   E49419
     C                     MOVE CCY       ECCY
     C                     Z-ADD0         SETP
     C                     MOVE *BLANKS   OSAC
     C                     Z-ADD0         REVI
     C                     Z-ADD0         OTHA
     C                     MOVE *BLANKS   OTHC
     C                     Z-ADDA6SPRT    EXRT
     C                     Z-ADDVDAT      STDT
     C                     Z-ADDLNMDAT    MDAT
     C                     Z-ADD3         SSEQ
     C                     Z-ADDLNCPAM    CPAM
     C                     Z-ADDIACD      PACD
     C                     MOVE *BLANKS   FACO
     C                     Z-ADDBKAPDT    PELAP
     C                     Z-ADDLNORED    ORED
      *                                                                                       CGL020
      ** AML Fields                                                                           CGL020
      *                                                                                       CGL020
     C           CGL020    IFEQ 'Y'                                                           CGL020
      *                                                                                       CGL020
     C                     MOVE *BLANKS   F1RACT                                              CGL020
     C                     MOVE *BLANKS   F1RCTY                                              CGL020
     C                     MOVE *BLANKS   F1RBNK                                              CGL020
     C                     MOVE *BLANKS   F1RCDE                                              CGL020
     C                     MOVE *BLANKS   F1PACT                                              CGL020
     C                     MOVE *BLANKS   F1PCTY                                              CGL020
     C                     MOVE *BLANKS   F1PBNK                                              CGL020
     C                     MOVE *BLANKS   F1PCDE                                              CGL020
     C                     MOVE *BLANKS   F1DACT                                              CGL020
     C                     MOVE *BLANKS   F1DCTY                                              CGL020
     C                     MOVE *BLANKS   F1DBNK                                              CGL020
     C                     Z-ADD0         F1SDAT                                              CGL020
      *                                                                                       CGL020
     C                     ENDIF                                                              CGL020
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************************
     C*
     C***  MGNEXT SUBROUTINE TO SET UP MARGIN EXTRACT RECORD.
     C*
     C***  CALLED FROM: L1SR
     C*
     C***  CALLS: NOTHING
     C*
     C********************************************************************
     C*
     C           MGNEXT    BEGSR
     C*
     C* IF NEW RECORD SET UP FIELDS
     C*
     C           *IN25     IFEQ '1'
     C                     MOVE CNUM      MDCNUM
     C                     MOVE LNRF      MDLNRF
     C                     MOVE LTYP      MDLTYP
     C                     MOVE SUTP      MDSUTP
     C                     MOVE CLAS      MDCLAS                                              CLE042
     C                     Z-ADD0         AGG
     C                     END
     C*
     C*  UPDATE AMOUNT FIELDS
     C*
     C                     Z-ADDMARG      MDMGNR
     C                     ADD  TACC      MDMATD
     C                     SUB  WACC      MDMATD
     C                     ADD  INTACC    MDIATD
     C                     SUB  INWACC    MDIATD
     C           TPST      IFNE 0
     C                     SELEC                                                              208013
     C           TPST      WHGT 0                                                             208013
     C           MDMATD    ANDGE0                                                             208013
     C           MDMATD    ANDLTTPST                                                          208013
     C                     Z-ADD0         MDMATD                                              208013
     C           TPST      WHLT 0                                                             208013
     C           MDMATD    ANDLE0                                                             208013
      *                                                                                       208013
      ** Get Absolute value                                                                   208013
      *                                                                                       208013
     C           *LIKE     DEFN MDMATD    WMDMAT                                              208013
     C           *LIKE     DEFN TPST      WTPST                                               208013
     C                     Z-SUBMDMATD    WMDMAT                                              208013
     C                     Z-SUBTPST      WTPST                                               208013
     C           WMDMAT    IFLT WTPST                                                         208013
     C                     Z-ADD0         MDMATD                                              208013
     C                     ENDIF                                                              208013
     C                     OTHER                                                              208013
     C                     SUB  TPST      MDMATD
     C                     ENDSL                                                              208013
     C                     SELEC                                                              208013
     C           INTPAY    WHGT 0                                                             208013
     C           MDIATD    ANDGE0                                                             208013
     C           MDIATD    ANDLTINTPAY                                                        208013
     C                     Z-ADD0         MDIATD                                              208013
     C           INTPAY    WHLT 0                                                             208013
     C           MDIATD    ANDLE0                                                             208013
      *                                                                                       208013
      ** Get Absolute value                                                                   208013
      *                                                                                       208013
     C           *LIKE     DEFN MDIATD    WMDIAT                                              208013
     C           *LIKE     DEFN INTPAY    WINTPA                                              208013
     C                     Z-SUBMDIATD    WMDIAT                                              208013
     C                     Z-SUBINTPAY    WINTPA                                              208013
     C           WMDIAT    IFLT WINTPA                                                        208013
     C                     Z-ADD0         MDIATD                                              208013
     C                     ENDIF                                                              208013
     C                     OTHER                                                              208013
     C                     SUB  INTPAY    MDIATD
     C                     ENDSL                                                              208013
     C                     ADD  TPST      MDPSTD
     C                     END
     C*
     C* CONVERT ACCRUAL AMOUNT TO BASE CURRENCY USING SPOT RATE
     C*
     C                     SUB  WACC      TACC
     C           TACC      IFNE 0
     C*
     C* MULTIPLY BY FACTOR OF 10 TO KEEP 2 DECIMAL PLACES FOR CURRENCY.
     C*
     C           A6NBDP    ADD  1         CP      10
     C                     MULT PWR,CP    TACC
     C*
     C           A6MDIN    IFEQ 'M'
     C                     MULT A6SPRT    TACC
     C                     ELSE
     C                     DIV  A6SPRT    TACC      H
     C                     END
     C*
     C                     END
     C*
     C**  IF PARTICIPATIONS SOLD THEN REVERSE AMOUNT
     C*
     C           PTYP      IFEQ 66
     C           PTYP      OREQ 67
      *                                                                   CLE005
      ** If CLE005 is installed, include additional processing types      CLE005
      ** 69 and 72.                                                       CLE005
      *                                                                   CLE005
     C           PTYP      OREQ 69                                        CLE005
     C           CLE005    ANDEQ'Y'                                       CLE005
     C           PTYP      OREQ 72                                        CLE005
     C           CLE005    ANDEQ'Y'                                       CLE005
     C                     Z-SUBTACC      TACC
     C                     END
     C*
     C* ADD AGGREGATE IN TO APPROPRIATE MONTH.
     C*
     C                     ADD  TACC      AGG,MN
     C*                                                                   E49419
     C***  Zero MDADJA on accrual days                                    E49419
     C*                                                                   E49419
     C           ETYPE     IFEQ 'A'                                       E49419
     C           ETYPE     OREQ 'R'                                       E49419
     C           PTYP      ANDNE63                                        103173
     C           PTYP      ANDNE65                                        103173
     C           PTYP      ANDNE67                                        103173
     C*                                                                   E49419
     C***********          Z-ADD0         MDADJA                          E49419
     C*                                                                   E49419
     C* Let's have another go at setting up the margin adjustment field   E49419
     C* correctly, hopefully taking into account the purpose the margin   E49419
     C* adjustment is supposed to serve!!                                 E49419
     C*                                                                   E49419
     C***********          SUB  TACC      MDADJA                          E49419
      *                                                                                       CLE028
     C           PTYP      IFNE 80                                                            CLE028
     C           PTYP      OREQ 80                                                            CLE028
     C           ADIF      ANDNE'Y'                                                           CLE028
     C           CLE028    ANDEQ'Y'                                                           CLE028
     C                     Z-ADD0         MDADJA                          E49419
     C*                                                                   E49419
     C                     ELSE                                           E49419
     C*
     C                     SUB  MADJ      MDADJA                          E49419
     C*                                                                   E49419
     C                     END                                            E49419
     C                     ENDIF                                                              CLE028
     C*                                                                   E49419
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C/COPY ZSRSRC,ZDATE2Z2
      *
     C/EJECT
     C*****************************************************************
     C*
     C***  INIT SUBROUTINE TO PERFORM FIRST CYCLE CALCS.
     C*
     C***  CALLED FROM: MAIN PROCESSING,LRSR
     C*
     C***  CALLS: ZDATE2,*PSSR,DBERR
     C*
     C********************************************************************
     C*
     C           INIT      BEGSR
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C           *NAMVAR   DEFN           LDA
     C                     IN   LDA
     C*                                                                     0078
     C           *NAMVAR   DEFN           LERSTS                            0078
     C********** *LOCK     IN   LERSTS                                                 0078 MD022067
     C                     IN   LERSTS                                                      MD022067
     C                     Z-ADDLASTMN    LASTM   50                        0078
      ****************************************************************                      MD051991
      ***Save*the*last*run*date*for*the*margin*adjusment*in*LER315****                      MD051991
      ****************************************************************                      MD051991
     C**********           Z-ADDLASTMN    MIDMON  50                                        MD051991
     C*                                                                   E49419
     C***  Zero margin adjustment storage field                           E49419
     C*                                                                   E49419
     C                     Z-ADD0         MADJ                            E49419
     C*
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C*
     C                     MOVE '1'       *IN20
     C                     MOVEL'LER310'  DBPGM
     C*                                                                   060109
     C** Set on indicator and initialize dummy field for multibranch      060109
     C*                                                                   060109
     C           MBIN      IFEQ 'Y'                                       060109
     C                     MOVE '1'       *IN70                           060109
     C                     MOVE '   '     SAVBR                           060109
     C                     END                                            060109
     C*
     C*     ACCESS BANK DETAILS FOR TITLE
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C           @RTCD     IFNE *BLANK
     C                     SETON                     89
     C           *LOCK     IN   LDA
     C                     MOVEL@OPTN     DBKEY            ***************
     C                     Z-ADD1         DBASE            * DBERROR 1   *
     C                     MOVEL'SDBANKPD'DBFILE           ***************
     C                     MOVEL'LER310'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     EXSR DBERR
     C                     ELSE
     C*
     C***  CHECK SYSTEM DATE FORMAT, DDMMYY OR MMDDYY.
     C*
     C           AGDFF     IFEQ 'M'
     C                     SETON                     98     ON=MMDDYY
     C                     END
     C*
     C                     END
      *                                                                   CAC001
      **  Access Module Details                                           CAC001
      *                                                                   CAC001
     C                     CALL 'AOMMODR0'                                CAC001
     C                     PARM '*MSG   ' @RTCD                           CAC001
     C                     PARM '*FIRST ' @OPTN                           CAC001
     C           SDMMOD    PARM SDMMOD    DSFDY                           CAC001
     C           @RTCD     IFNE *BLANKS                                   CAC001
     C                     SETON                     89                   CAC001
     C                     SETON                     U7U8LR               CAC001
     C           *LOCK     IN   LDA                                       CAC001
     C                     MOVEL@OPTN     DBKEY            ************   CAC001
     C                     Z-ADD7         DBASE            * DBERR 07 *   CAC001
     C                     MOVEL'SDMMODPD'DBFILE           ************   CAC001
     C                     MOVEL'LER310'  DBPGM                           CAC001
     C                     OUT  LDA                                       CAC001
     C                     EXSR *PSSR                                     CAC001
     C                     EXSR DBERR                                     CAC001
     C                     ENDIF                                          CAC001
     C*
     C*     ACCESS GENERAL LEDGER DETAILS FOR ACCRUALS/PROFIT DATE.
     C*
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*MSG   ' @RTCD
     C                     PARM '*FIRST ' @OPTN
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
     C           @RTCD     IFNE *BLANKS
     C                     SETON                     89
     C                     SETON                     U7U8LR
     C           *LOCK     IN   LDA
     C                     MOVEL@OPTN     DBKEY            ***************
     C                     Z-ADD2         DBASE            * DBERR 2     *
     C                     MOVEL'SDGELRPD'DBFILE           ***************
     C                     MOVEL'LER310'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     EXSR DBERR
     C                     END
      *                                                                                     AR567790
      ** Check if CLE134 is ON                                                              AR567790
      *                                                                                     AR567790
     C                     CALL 'AOSARDR0'                                                  AR567790
     C                     PARM *BLANKS   @RTCD                                             AR567790
     C                     PARM '*KEY   ' @OPTN                                             AR567790
     C                     PARM 'CLE134 ' @SARD   6                                         AR567790
     C           SCSARD    PARM SCSARD    DSFDY                                             AR567790
     C           @RTCD     IFEQ *BLANK                                                      AR567790
     C                     MOVE 'Y'       CLE134  1                                         AR567790
     C                     ELSE                                                             AR567790
     C                     MOVE 'N'       CLE134                                            AR567790
     C                     ENDIF                                                            AR567790
      *                                                                   CLE005
      ** Check if SAR CLE005 is switched *ON.                             CLE005
      *                                                                   CLE005
     C                     CALL 'AOSARDR0'                                CLE005
     C                     PARM *BLANKS   @RTCD                           CLE005
     C                     PARM '*KEY   ' @OPTN                           CLE005
     C                     PARM 'CLE005 ' @SARD   6                       CLE005
     C           SCSARD    PARM SCSARD    DSFDY                           CLE005
     C           @RTCD     IFEQ *BLANK                                    CLE005
     C                     MOVE 'Y'       CLE005  1                       CLE005
     C                     ELSE                                           CLE005
     C                     MOVE 'N'       CLE005                          CLE005
     C           @RTCD     IFNE '*NRF    '                                CLE005
     C                     SETON                     89                   CLE005
     C                     SETON                     U7U8LR               CLE005
     C           *LOCK     IN   LDA                                       CLE005
     C                     MOVEL'SCSARDPD'DBFILE           ***************CLE005
     C                     Z-ADD008       DBASE            * DB ERROR 08 *CLE005
     C                     MOVEL'CLE005'  DBKEY            ***************CLE005
     C                     MOVEL'LER310'  DBPGM                           CLE005
     C                     OUT  LDA                                       CLE005
     C                     EXSR *PSSR                                     CLE005
     C                     EXSR DBERR                                     CLE005
     C                     ENDIF                                          CLE005
     C                     ENDIF                                          CLE005
      *                                                                   CLE005
      ** Check if Flat Rate Personal Loans (Rule of 78s) is installed                         CLE028
      *                                                                                       CLE028
     C                     CALL 'AOSARDR0'                                                    CLE028
     C                     PARM *BLANKS   PRTCD   7                                           CLE028
     C                     PARM '*VERIFY' POPTN   7                                           CLE028
     C                     PARM 'CLE028'  PSARD   6                                           CLE028
     C           SCSARD    PARM SCSARD    DSFDY                                               CLE028
      *                                                                                       CLE028
     C           PRTCD     IFNE *BLANKS                                                       CLE028
     C           PRTCD     ANDNE'*NRF   '                                                     CLE028
     C                     MOVE *ON       *INU7                                               CLE028
     C                     MOVE *ON       *INU8                                               CLE028
     C           *LOCK     IN   LDA                                                           CLE028
     C                     MOVEL'SCSARDPD'DBFILE                                              CLE028
     C                     Z-ADD13        DBASE                                               CLE028
     C                     MOVELPSARD     DBKEY                                               CLE028
     C                     MOVEL'LER310'  DBPGM                                               CLE028
     C                     OUT  LDA                                                           CLE028
     C                     EXSR *PSSR                                                         CLE028
     C                     EXSR DBERR                                                         CLE028
     C                     ENDIF                                                              CLE028
      *                                                                                       CLE028
     C           PRTCD     IFEQ *BLANKS                                                       CLE028
     C                     MOVE 'Y'       CLE028  1                                           CLE028
     C                     ELSE                                                               CLE028
     C                     MOVE 'N'       CLE028                                              CLE028
     C                     ENDIF                                                              CLE028
     C*                                                                                       CLE028
     C                     MOVE 'N'       CGL020  1                                           CGL020
     C                     CALL 'AOSARDR0'                                                    CGL020
     C                     PARM '       ' PRTCD   7                                           CGL020
     C                     PARM '*VERIFY' POPTN   7                                           CGL020
     C                     PARM 'CGL020'  PSARD   6                                           CGL020
     C           SCSARD    PARM SCSARD    DSFDY                                               CGL020
      *                                                                                       CGL020
     C           PRTCD     IFNE *BLANK                                                        CGL020
     C           PRTCD     ANDNE'*NRF   '                                                     CGL020
     C                     MOVEL'SCSARDPD'DBFILE                                              CGL020
     C                     Z-ADD30        DBASE                                               CGL020
     C                     MOVEL@SARD     DBKEY                                               CGL020
     C                     EXSR DBERR                                                         CGL020
     C                     ENDIF                                                              CGL020
      *                                                                                       CGL020
     C           PRTCD     IFEQ *BLANK                                                        CGL020
     C                     MOVE 'Y'       CGL020                                              CGL020
     C                     ENDIF                                                              CGL020
      *                                                                                     MD035545
      ** Retrieve system value setting.                                                     MD035545
      *                                                                                     MD035545
     C                     MOVELWPARML    P@OP01                                            MD035545
     C                     CALL 'AOSVALR0'                                                  MD035545
     C                     PARM *BLANKS   @RTCD                                             MD035545
     C                     PARM           P@OP01 20                                         MD035545
     C                     PARM           P@VL01200                                         MD035545
     C                     PARM           P@OP02 20                                         MD035545
     C                     PARM           P@VL02200                                         MD035545
     C                     PARM           P@OP03 20                                         MD035545
     C                     PARM           P@VL03200                                         MD035545
     C                     PARM           P@OP04 20                                         MD035545
     C                     PARM           P@VL04200                                         MD035545
     C                     PARM           P@OP05 20                                         MD035545
     C                     PARM           P@VL05200                                         MD035545
     C                     PARM           P@OP06 20                                         MD035545
     C                     PARM           P@VL06200                                         MD035545
     C                     PARM           P@OP07 20                                         MD035545
     C                     PARM           P@VL07200                                         MD035545
     C                     PARM           P@OP08 20                                         MD035545
     C                     PARM           P@VL08200                                         MD035545
     C                     PARM           P@OP09 20                                         MD035545
     C                     PARM           P@VL09200                                         MD035545
     C                     PARM           P@OP10 20                                         MD035545
     C                     PARM           P@VL10200                                         MD035545
      *                                                                                     MD035545
      ** If system value is not found then default value to 'N'.                            MD035545
      *                                                                                     MD035545
     C           @RTCD     IFNE '       '                                                   MD035545
     C                     MOVE 'N'       WCALC6                                            MD035545
     C                     ELSE                                                             MD035545
     C                     MOVELP@VL01    WCALC6                                            MD035545
     C********** WCALC6    IFEQ 'Y'                                                MD035545 MD047993
     C********** BKALDI    ANDEQ'Y'                                                MD035545 MD047993
     C**********           MOVE 'Y'       WCALC6                                   MD035545 MD047993
     C**********           ELSE                                                    MD035545 MD047993
     C**********           MOVE 'N'       WCALC6                                   MD035545 MD047993
     C**********           ENDIF                                                   MD035545 MD047993
     C                     ENDIF                                                            MD035545
     C/COPY WNCPYSRC,LEH01283
      *                                                                                       CGL020
     C                     MOVE *BLANKS   F1FFLG                                              CGL020
     C                     Z-ADD0         F1FVAL                                              CGL020
     C                     Z-ADD0         F1FPCT                                              CGL020
     C*
     C* CALCULATE RUN DATE IN CASE SECOND RUN OF EOM. IN THIS CASE
     C* RUN DATE MUST BE RESET TO THE FIRST OF THE NEW MONTH.
     C*
     C***  THIS APPEARS TO BE ALL THE OLD ROUTINE RUNCHK USED TO DO
     C***  FOR THIS PROGRAM, SO I'VE REPLACED IT WITH THE FOLLOWING
     C***  30-ODD LINES
     C*
     C** First calculate the date of next working day minus one             0078
     C*                                                                     0078
     C           BJDNWD    SUB  1         DNWD1   50                        0078
     C*                                                                     0078
     C** The use of accrual profit day is now conditioned on                0078
     C** (i) accrual frequency being daily or (ii) accrual frequency        0078
     C** monthly AND BKAPDT falling between run date date of next           0078
     C** working day minus 1                                                0078
     C*                                                                     0078
     C* (i)                                                                 0078
     C           BKAPFQ    IFEQ 'D'                                         0078
     C* (ii)                                                                0078
     C           BKAPFQ    OREQ 'M'                                         0078
     C           BKAPDT    ANDGTBJRDNB                                      0078
     C***********BKAPDT    ANDLTDNWD1                                   00780111
     C           BKAPDT    ANDLEDNWD1                                       0111
     C*                                                                     0078
     C** Or if the accrual day has been missed                              0078
     C*                                                                     0078
     C           LASTM     ORLT BKAPDT                                      0078
     C           BKAPDT    ANDLTBJRDNB                                      0078
     C                     Z-ADDBKAPDT    ZDAYNO
     C                     ELSE                                             0078
     C*                                                                     0078
     C** If this an accrue non-working day run but not for an end           0078
     C** of month then the first run should be for today                    0078
     C** and the second run will deal with the non-working days             0078
     C*                                                                     0078
     C           LASTM     IFLT BJRDNB                                      0078
     C           BJRDNB    ANDLTDNWD1                                       0078
     C                     Z-ADDBJRDNB    ZDAYNO                            0078
     C                     ELSE                                             0078
     C                     Z-ADDDNWD1     ZDAYNO                            0078
     C                     END                                              0078
     C                     END                                              0078
     C*                                                                     0078
     C** Either way put the correct day number into runday for later        0078
     C*                                                                     0078
     C                     Z-ADDZDAYNO    RUNDAY  50                        0078
     C*                                                                     0078
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RUNDAX
     C*                                                                     0078
     C** If the first position of the alpha date is blank make it           0078
     C** zero for the later date comparisons                                0078
     C*                                                                     0078
     C                     MOVELRUNDAX    TEST    1                         0078
     C           TEST      IFEQ ' '                                         0078
     C                     MOVEL'0'       RUNDAX                            0078
     C                     END                                              0078
     C**********           Z-ADDBKAPDT    RUNDAY  50                        0078
     C*
     C***TEST*WHETHER*THE*FIRST*POSITION*OF*RUNA*IS*BLANK*AND*IF*SO****     0078
     C***REPLACE*WITH*A*ZERO*******************************************     0078
     C**********                                                            0078
     C********** TSTB      IFEQ ' '                                         0078
     C**********           MOVEL'0'       RUNDAX                            0078
     C**********           END                                              0078
     C*
     C*  DETERMINE TYPE OF RUN
     C*  IF MONTH OF NEXT WORKING DAY NE MONTH OF RUN DATE, THEN
     C*  MUST BE EOM
     C*  THEN IF NEXT WORKING DAY NE 1ST, ITS EOM FOLLOWED BY A NWD
     C*  RETURN PARM WITH FOLLOWING VALUES: 1 = NORMAL EOD
     C*                                     2 = EOM FOLLOWED BY WD
     C*                                     3 = EOM FOLLOWED BY NWD
     C*
     C* CALC. DDMMYY VERSION OF DATE OF NEXT WORKING DAY
     C*
     C                     Z-ADDBJDNWD    ZDAYNO
     C                     EXSR ZDATE2
     C*
     C           MACC      IFNE MRUN
     C           DACC      IFEQ '01'
     C                     Z-ADD2         ACC     10
     C                     ELSE
     C                     Z-ADD3         ACC
     C                     END
     C                     ELSE
     C                     Z-ADD1         ACC
     C                     END
     C*
     C***  END OF RUNCHK REPLACEMENT CODING
     C*
     C* IF RUN IS 2, OR 3 AND RUN DATE WAS CHANGED, THEN SET EOM = 'Y'.
     C* I.E. THIS IS EOM FOLLOWED BY WORKING DAY OR 2ND.RUN (ANWD1)
     C*      OF EOM FOLLOWED BY NON-WORKING DAY.
     C*
     C                     MOVE ' '       EOM     1
     C           ACC       IFEQ 2
     C           ACC       OREQ 3
     C           RUNDAY    ANDNEAGRDNB
     C                     MOVE 'Y'       EOM
     C                     END
     C**
     C** CALCULATE START OF YEAR DATE AND START OF MONTH
     C**
     C           BJPEYD    ADD  1         SYDY    50
     C                     Z-ADDSYDY      ZDAYNO  50
     C*
     C                     EXSR ZDATE2
     C*
     C                     MOVELZADATE    SYDYD   2
     C**
     C** CONVERT DATE OF NEXT WORKING DAY TO DATE FORMAT AND USE THE
     C** START OF YEAR DAY FIELD TO SET UP THE NEXT START OF MONTH
     C** DATE
     C**
     C                     Z-ADDBJDNWD    ZDAYNO
     C                     EXSR ZDATE2
     C*
     C**  IF THE FORMAT FOR THE DATE IS UK (DDMMYY) THEN ENSURE THE
     C**  CORRECT DATA STRUCTURE IS USED TO UPDATE THE DD PORTION OF
     C**  START OF MONTH DATE
     C*
     C                     MOVE ZDATE     SMDT    6
     C*
     C           AGDFF     IFEQ 'D'
     C                     MOVELSYDYD     SMDT
     C*
     C                     ELSE
     C*
     C**  OTHERWISE USE THE U.S. FORMAT (MMDDYY) DATA STRUCTURE TO
     C**  UPDATE THE DD PORTION OF START OF MONTH DATE
     C*
     C                     MOVE ZDATE     SMDTUS  6
     C                     MOVE SYDYD     SMDDUS
     C                     MOVE SMDDUS    SMDD
     C                     MOVE SMUS      SM
     C                     END
     C*
     C** DIVIDE ZDATE (CONVERTED DATE OF NEXT WORKING DAY) BY 10000
     C** TO GET THE DAY NUMBER (2 DIGITS) AND MOVE THE START OF YEAR
     C** DAY INTO A TWO DIGIT WORK FIELD FOR LATER COMPARISON
     C*
     C           ZDATE     DIV  10000     DNWDDD  20
     C                     MOVELSYDYD     WKDDSY  20
     C*
     C** IF THE DAY NUMBER OF THE DATE OF NEXT WORKING
     C** DAY IS LESS THAN THE START OF MONTH DAY SUBTRACT
     C** ONE FROM THE MONTH NUMBER OF THE DATE OF NEXT WORKING DAY
     C** THIS ENSURES THAT THE CONVERSION AND CHECKING OF THE START
     C** OF MONTH DATE IS PERFORMED ON THE CURRENT MONTH & NOT THE NEXT
     C*
     C           WKDDSY    IFGT DNWDDD
     C                     SUB  1         SM
     C           SM        IFEQ 0
     C                     Z-ADD12        SM
     C                     SUB  1         SY
     C                     END
     C                     END
     C**
     C                     MOVE SMDT      ZDATE
     C**
     C** CONVERT START OF NEXT MONTH DATE TO DAY NUMBER FORMAT
     C**
     C                     CALL 'LERDATEF'
     C                     PARM           ZDATE
     C                     PARM           ZDAYNO
     C                     PARM           GRTCD   1
     C                     Z-ADDZDAYNO    SMDY    50
     C**
     C***CHECK*WHETHER*THE*START*OF*THE*NEW*MONTH*FALLS*AFTER*AGRDNB***     0078
     C***AND*ON*OR*BEFORE*THE*DATE*OF*NEXT*WORKING*DAY*AND*IF*SO*******     0078
     C***USE*THE*LAST*DAY*OF*THE*MONTH*AS*THE*ACCRUAL/PROFIT*DAY*******     0078
     C**
     C**********                                                            0078
     C********** SMDY      IFGT AGRDNB                                      0078
     C********** SMDY      ANDLEBJDNWD                                      0078
     C********** BKAPDT    ANDEQAGRDNB                                      0078
     C********** SMDY      SUB  1         CODT                              0078
     C**********                                                            0078
     C********** SM        SUB  1         MN      20                        0078
     C**********                                                            0078
     C**********           ELSE                                             0078
     C**********                                                            0078
     C**********           ADD  SM        MN                                0078
     C*                                                                     0078
     C***  Set up appropriate month to put amounts under                    0078
     C*                                                                     0078
     C                     Z-ADD1         MN      20                        0078
     C           MMM       LOKUPZMNM,MN                  50                 0078
     C*                                                                     0078
     C***  If day of RUNDAX is before the day of the Start of Year          0078
     C***  then amount should be put in previous month                      0078
     C*                                                                     0078
     C                     MOVELRUNDAX    DAYDAX  2                         0078
     C           DAYDAX    IFLT SYDYD                                       0078
     C                     SUB  1         MN                                0078
     C           MN        IFEQ 0                                           0078
     C                     Z-ADD12        MN                                0078
     C                     END                                              0078
     C                     END                                              0078
     C*
     C* CALC. EARLIER OF ACCRUALS/PROFIT DATE & (NEXT WORKING DAY - 1)
     C*
     C           BJDNWD    SUB  1         CODT    50
     C*                                                                     0078
     C** Use the new date field on LERSTS to check for setting the          0078
     C** control date correctly. If LASTM is greater than or equal to       0078
     C** RUND then DNWD-1 is the correct value because this is the          0078
     C** second run of the day, otherwise if this is the first run          0078
     C*                                                                     0078
     C           LASTM     IFLT BJRDNB                                      0078
     C*                                                                     0078
     C** and the accrual profit date is between today and DNWD-1            0078
     C*                                                                     0078
     C***********BKAPDT    IFLT CODT                                        0111
     C           BKAPDT    IFLE CODT                                        0111
     C           BJRDNB    ANDLTBKAPDT
     C*                                                                     0078
     C***  or the accrual profit date is before run date and after last     0078
     C***  run of facility update (ie - you missed the accrual profit       0078
     C***  day somehow)                                                     0078
     C*                                                                     0078
     C           BKAPDT    ORLT BJRDNB                                      0078
     C           BKAPDT    ANDGTLASTM                                       0078
     C*                                                                     0078
     C** the first run should include days up to accrual day                0078
     C*                                                                     0078
     C                     Z-ADDBKAPDT    CODT
     C                     ELSE                                             0078
     C*                                                                     0078
     C** if neither of the above cases apply, this is a normal run          0078
     C** or the first run when there is no EOM & CODT=RUND                  0078
     C*                                                                     0078
     C                     Z-ADDBJRDNB    CODT                              0078
     C*                                                                     0078
     C                     END
     C                     END
     C*
     C**********                                                            0078
     C***CHECK*WHETHER*THE*MONTH*IS*NOW*ZERO*AND*ADJUST****************     0078
     C**********                                                            0078
     C********** MN        IFEQ 0                                           0078
     C**********           MOVE '12'      MN                                0078
     C**********           END                                              0078
     C*
     C* WRITE MARGIN A/C KEYS HEADER
     C* SAVE REC ID READ IN
     C*
     C                     MOVE RECI      SAVE    1
     C                     MOVE 'H'       RECI
     C                     WRITELKEY1HHF
     C                     MOVE SAVE      RECI
     C*
     C* DEFINE KEY LIST FOR LEPEMGD
     C*
     C           KEYMGN    KLIST
     C                     KFLD           BRCA
     C                     KFLD           CNUM
     C                     KFLD           LNRF
     C*
     C* DEFINE FIELDS FOR SAVE OF INTEREST PAYMENT A/C KEY.
     C*
     C           *LIKE     DEFN RECI      SRECI
     C           *LIKE     DEFN AKEY      SAKEY
     C           *LIKE     DEFN BRCA      SBRCA
     C           *LIKE     DEFN ACSQ      SACSQ
     C           *LIKE     DEFN EDAT      SEDAT
     C           *LIKE     DEFN ECCY      SECCY
     C           *LIKE     DEFN SETP      SSETP
     C           *LIKE     DEFN OSAC      SOSAC
     C           *LIKE     DEFN REVI      SREVI
     C           *LIKE     DEFN OTHA      SOTHA
     C           *LIKE     DEFN OTHC      SOTHC
     C           *LIKE     DEFN EXRT      SEXRT
     C           *LIKE     DEFN STDT      SSTDT
     C           *LIKE     DEFN SLID      SSLID
     C           *LIKE     DEFN MDAT      SMDAT
     C           *LIKE     DEFN BRTT      SBRTT
     C           *LIKE     DEFN BRTE      SBRTE
     C           *LIKE     DEFN RTSP      SRTSP
     C           *LIKE     DEFN INTR      SINTR
     C           *LIKE     DEFN SSEQ      SSSEQ
     C           *LIKE     DEFN CPAM      SCPAM
     C           *LIKE     DEFN PACD      SPACD
     C           *LIKE     DEFN ICBS      SICBS
     C           *LIKE     DEFN ACOF      SACOF
     C           *LIKE     DEFN FACO      SFACO
     C           *LIKE     DEFN PELAP     SPELAP
     C           *LIKE     DEFN ORED      SORED
      *                                                                                       CGL020
      ** AML Feilds                                                                           CGL020
      *                                                                                       CGL020
     C           CGL020    IFEQ 'Y'                                                           CGL020
      *                                                                                       CGL020
     C           *LIKE     DEFN F1RACT    WRACT                                               CGL020
     C           *LIKE     DEFN F1RCTY    WRCTY                                               CGL020
     C           *LIKE     DEFN F1RBNK    WRBNK                                               CGL020
     C           *LIKE     DEFN F1RCDE    WRCDE                                               CGL020
     C           *LIKE     DEFN F1PACT    WPACT                                               CGL020
     C           *LIKE     DEFN F1PCTY    WPCTY                                               CGL020
     C           *LIKE     DEFN F1PBNK    WPBNK                                               CGL020
     C           *LIKE     DEFN F1PCDE    WPCDE                                               CGL020
     C           *LIKE     DEFN F1DACT    WDACT                                               CGL020
     C           *LIKE     DEFN F1DCTY    WDCTY                                               CGL020
     C           *LIKE     DEFN F1DBNK    WDBNK                                               CGL020
     C           *LIKE     DEFN F1SDAT    WSDAT                                               CGL020
      *                                                                                       CGL020
     C                     ENDIF                                                              CGL020
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************************
     C*
     C***  AUDIT SUBROUTINE TO PRINT AUDIT REPORT
     C*
     C***  CALLED FROM:
     C*
     C***  CALLS: NOTHING
     C*
     C*****************************************************************
     C*
     C           AUDIT     BEGSR
     C*
     C***  Ensure report spool file recorded by RCF
     C*
     C                     Z-ADD0         PAGE
     C                     Z-ADDSFNUMU    ZSNUMU
     C*
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ     5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU  60
     C                     PARM           ZSERR   1
      *
      ** If error during ZSFILE processing, return to calling program
     C           ZSERR     IFEQ 'Y'
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
     C*
     C                     WRITELER310A1
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************************
     C*
     C***  DBERR SUBROUTINE TO PERFORM DATABASE ERROR PROCESSING
     C*
     C***  CALLED FROM: MAIN PROCESSING,INIT
     C*
     C***  CALLS: NOTHING
     C*
     C********************************************************************
     C*
     C           DBERR     BEGSR
     C*
     C                     EXSR AUDIT
     C                     WRITEDBFMT
     C*
     C*** IF -P1 OPEN, WRITE DATABASE ERROR MESSAGE TO IT
     C*
     C           NORE      IFNE 0
     C                     WRITEDBFMTP
     C                     END
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************************
     C*
     C***  *PSSR  --- SUBROUTINE TO HANDLE ABNORMAL ERROR CONDITIONS
     C*
     C***  CALLED FROM: AFTER ABNORNAL OPERATION OCCURS
     C*
     C***  CALLS: NOTHING
     C*
     C********************************************************************
     C*
     C           *PSSR     BEGSR                           ** *PSSR **
     C*
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
     C*
     C                     ENDSR                           ** *PSSR **
     C********************************************************************
     C/EJECT                                                                                  208013
      *****************************************************************                       208013
      *                                                               *                       208013
      *  SRCALC - Subroutine to calculate the correct MDMATD & MDIATD *                       208013
      *                                                               *                       208013
      *  Called from:  Main processing                                *                       208013
      *                                                               *                       208013
      *  Calls: None                                                  *                       208013
      *                                                               *                       208013
      *****************************************************************                       208013
      *                                                                                       208013
     C           SRCALC    BEGSR                                                              208013
     C                     Z-ADDIACD      ZIBEG                                               208013
     C                     TESTB'3'       LONI           55                                   208013
     C           *IN55     CABEQ'1'       STOPAC                                              208013
      *                                                                                       208013
      ** If loan had gone Past due.                                                           208013
      *                                                                                       208013
     C                     MOVE *BLANK    FIN     1                                           208013
     C           MDAT      IFLT IACD                                                          208013
     C           MDAT      ANDNE*ZEROS                                                        208013
     C           AIAP      ADD  AIMN      INOUT                                               208013
     C           PDIN      ADD  INOUT     INOUT                                               208013
     C                     MOVE 'Y'       FIN     1                                           208013
     C                     ENDIF                                                              208013
     C           FIN       CABEQ'Y'       SUBTRT                                              208013
     C           CPAM      CABLE0         INTTAG                                              208013
      *                                                                                       208013
     C                     Z-ADD0         ZINTR                                               208013
     C           MDAT      IFGT BJRDNB                                                        208013
     C           MDAT      OREQ 0                                                             208013
     C                     Z-ADDBJRDNB    ZIEND                                               208013
     C                     ELSE                                                               208013
     C                     Z-ADDMDAT      ZIEND                                               208013
     C                     ENDIF                                                              208013
      *                                                                                       208013
     C           ADDI      IFEQ 'B'                                                           208013
     C           ZIBEG     ANDEQVDAT                                                          208013
     C                     ADD  1         ZIEND                                               208013
     C                     ENDIF                                                              208013
      *                                                                                       208013
     C                     MOVE ICBS      ZICALC                                              208013
     C                     Z-ADDCPAM      ZIAMT                                               208013
     C                     Z-ADDINTR      ZIRATE                                              208013
     C           ZIEND     IFGT ZIBEG                                                         208013
     C/COPY WNCPYSRC,LEH01284
     C                     EXSR GLINTC                                                        208013
     C/COPY WNCPYSRC,LEH01386
     C           RDFC      IFEQ 'Y'                                                           208013
     C                     Z-ADDZINTR     RDWORK 130                                          208013
     C                     Z-ADDRDWORK    ZINTR                                               208013
     C                     ENDIF                                                              208013
     C                     ELSE                                                               208013
     C                     Z-ADD0         ZINTR                                               208013
     C                     ENDIF                                                              208013
     C           INTTAG    TAG                             ** INTTAG TAG *                    208013
     C           ZINTR     ADD  AITC      INOUT  130H                                         208013
     C           INOUT     ADD  AIAP      INOUT                                               208013
     C           INOUT     ADD  AIMN      INOUT                                               208013
     C                     GOTO SUBTRT                                                        208013
     C           STOPAC    TAG                             ** STOPAC TAG *                    208013
     C           AITC      ADD  AIAP      INOUT                                               208013
     C           INOUT     ADD  AIMN      INOUT                                               208013
     C           SUBTRT    TAG                             ** SUBTRT TAG *                    208013
     C           INOUT     SUB  IWOD      INOUT                                               208013
     C           INOUT     SUB  IPRD      INOUT                                               208013
     C           INOUT     SUB  ICTD      INOUT                                               208013
      *                                                                                       208013
     C           INOUT     CABEQMDIATD    CALCEN                                              208013
     C                     Z-ADDINOUT     MDIATD                                              208013
     C           CALCEN    ENDSR                                                              208013
     C********************************************************************                    208013
     C/COPY ZSRSRC,GLINTCZ1                                                                   208013
     C/COPY ZSRSRC,ZINTDYZ2                                                                   208013
     C/COPY ZSRSRC,ZDATE9Z3                                                                   208013
     C/COPY ZSRSRC,ZDAT10Z2                                                                   208013
     C/EJECT
** CPY@   **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
**   PWR  - POWER ARRAY FOR CONVERSION OF AGGREGATE AMOUNTS
1000
0100
0010
0001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
** @YD  USED BY SR. ZDATE9 - YEARS, IN DAYS, CUMULATIVE IN 4 YEAR SPAN                        208013
00366007310109601461
** @MD  USED BY SR. ZDATE9 - MONTHS IN DAYS CUMULATIVE THROUGH YEAR                           208013
00000000310005900090001200015100181002120024300273003040033400365
