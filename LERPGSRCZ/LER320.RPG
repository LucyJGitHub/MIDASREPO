     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Adjustments to accrued margin input')         *
      *****************************************************************
      *                                                               *
      *  Midas - Customer Lending Module                              *
      *                                                               *
      *  LER320 - ACCRUED MARGIN ADJUSTMENTS                          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE141             Date 08Feb16               *
      *                 CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      *                 CER059             Date 19Jul10               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 BUG6770            Date 03May05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 CAS006             Date 21Jan03               *
      *                 CAS005             Date 16Dec02               *
      *                 CLE028             Date 27Jun02               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 207629             Date 12Aug02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CLE011             Date 07Nov00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01408             Date 08Sep96               *
      *                 MOF60              Date 14Aug91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE141 - Currency and Location Business Day Convention       *
      *           (Recompile)                                         *
      *  CLE134 - Past Due Call Loan Processing                       *
      *  CLE148 - Alpha Loan Reference                                *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus                                          *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type                              *
      *  BUG6770 - Webfacing error - Adjustment to Accrued Margin     *
      *            (Recompile)                                        *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CAS005 - Enahancements to CAS001, CAS002 and CAS004          *
      *           (Recompile)                                         *
      *  CLE028 - Flat Rate Personal Loans (Rule of 78s).             *
      *  207629 - Check System Value LoanCustOriginPurch to decide if *
      *           original borrower should be input as the customer of*
      *           a Part Purchased, instead of 'Purchased From' num.  *
      *  CLE011 - Rate Fixing Days for Lending Rollover Processing    *
      *           Recompile                                           *
      *   S01408 - Addition of core hook LER230CCP7                   *
      *          - Addition of core hook LER230CCP6                   *
      *          - Addition of core hook LER230CCP5                   *
      *          - Addition of core hook LER230CCP4                   *
      *          - Addition of core hook LER230CCP3                   *
      *          - Addition of core hook LER230CCP2                   *
      *          - Addition of core hook LER230CCP1                   *
      *          - Addition of core hook LER230IIP1                   *
      *          - Addition of core hook LER230EEP1                   *
      *          - Addition of core hook LER230FFP2                   *
      *          - Addition of core hook LER230FFP1                   *
      *   MOF60  - CUSTOMER PROFITABILITY & FEES PROCESSING           *
      *                                                               *
      *****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      *                                                                   S01408
     F/COPY WNCPYSRC,LER320FFP1                                           S01408
      *                                                                   S01408
     FLER320FMCF  F     206            WORKSTN      KPASS  *NOIND
      *                                                                   S01408
     F/COPY WNCPYSRC,LER320FFP2                                           S01408
      *                                                                   S01408
     F*CLOAN***IF**F     436  7AI     2 DISK                              CLE011
     F*CLOAN***IF**F    1310  7AI     2 DISK                                           CLE011 CLE028
     F***CLOAN** IF  F    1375  7AI     2 DISK                                         CLE028 CGL029
     F*CLOAN***IF**F****1461  7AI     2 DISK                                           CGL029 CLE042
     F***CLOAN** IF  F    1773  7AI     2 DISK                                         CLE042 CER020
     F***CLOAN   IF  F    1782  7AI     2 DISK                                         CER020 CLE134
     FCLOAN   IF  F    1874  7AI     2 DISK                                                   CLE134
     F                                              KCOMIT
     FMTRAN   UF  E           K        DISK                      A
     F                                              KCOMIT
     F* FUNCTION OF INDICATORS
     F* 01      WORKSTATION - INSERT SCREEN
     F* 02                  - DUMMY SCREEN
     F* 03      USED TO CONDITION FIRST TIME PASS THROUGH               *
     F*         RPG CALCULATIONS CYCLE                          *
     F* 04      LOANS FILE - DETAIL REC-A TYPE 'D'
     F* 05      SET ON IF ERROR DURING READ FROM THE WORKSTATION       /
     F* 06                       - CATCHALL
     F* 07            MTRAN FILE - HEADER
     F* 08                       - DETAIL
     F* 09                       - CATCHALL
     F* 20      LOAN NO. BLANK
     F* 21      CUSTOMER NO/SHORTNAME BLANK
     F* 22      ADJUSTMENT AMT. BLANK
     F* 23      SIGN BLANK
     F* 24      CURRENCY BLANK
     F* 30      ERROR CODES SET UP
     F* 32      NON-DISPLAY ERROR CODES
     F* 33      NON-DISPLAY ERROR MESSAGES
     F* 34      DETAIL REC. UPDATED BY ANOTHER WORKSTATION
     F* 38      PROCESSING TYPE = 63,65,67 OR LOAN VAL/MAT DATE I      NV.
     F* 40      OUTPUT BLANK INSERT SCREEN
     F* 41      BLINK LOAN NO.
     F* 44      UNSUCCESSFUL CHAIN TO LOANS FILE
     F* 45      MTRAN FILE CHAIN FAIL
     F* 49      UNSUCCESSFUL CHAIN TO MTRAN FILE
     F* 51      BLINK CUSTOMER
     F* 54      INVALID CUSTOMER NUMBER                                NO.
     F* 60      OUTPUT TO SCREEN
     F* 61      BLINK ADJUSTMENT AMT.
     F* 63      BLINK SIGN
     F* 64      SIGN IS 'PLUS'
     F* 65      SIGN IS 'MINUS'
     F* 66      ERROR CONDITION ON SCREEN INPUT
     F* 67      BLINK CURRENCY
     F* 68      INVALID CURRENCY
     F* 69      REC. UPDATED BY ANOTHER WORKSTATION
     F* 77      OUTPUT TO MTRAN - HEADER
     F* 78                      - DETAIL
     F* 79      OUTPUT TO LOANS FILE - DETAIL RECORD & LOG AFTER
     F* 80      RELEASE LOANS FILE
     F* 82      MTRAN FILE NEARLY FULL
     F* U7
     F* U8
     F/EJECT
     E                    EMES    1   7 70               ERROR MESSAGES
     E                    ERCD       19  4               ERROR CODES
     E                    ZA1        16  1               ZALIGN SR
     E                    CPY@    1   1 80
     E                    ZA2        16  1               ZALIGN SR
     E/SPACE 3
      *                                                                   S01408
     E/COPY WNCPYSRC,LER320EEP1                                           S01408
      *                                                                   S01408
     I*
     ILER320FMNS  01   1 CS   2 CI
     I                                        1   2 SID
     I                                        3   8 INLOAN          20
     I                                        9  18 INCUSN          21
     I                                       19  32 INADJ           22
     I                                       33  33 INSIGN          23
     I                                       34  36 INCCY           24
     I                                       19  36 SCFLD
     I                                        1  36 LOTSCN
     I        NS  02
     I*
     I**  LOANS FILE - DETAIL - REC-A TYPE 'D'
     ICLOAN   NS  04   1 CD   8 CA
     I                                        1   1 RECI
     I**********                              2   70LNRF                                      CLE148
     I                                        2   7 LNRF                                      CLE148
     I                                        8   8 RCDT
     I                                        9   90MRIN
     I                                       10  110PTYP
     I**********                             12  170CNUM                                      CSD027
     I                                       12  17 CNUM                                      CSD027
     I                                       18  20 BRCA
     I                                       21  22 LTYP
     I                                       23  24 SUTP
     I                                    P  25  270DDAT
     I                                    P  28  300VDAT
     I                                    P  31  330MDAT
     I                                       34  350LASQ
     I**********                             36  410FCUS                                      CSD027
     I                                       36  41 FCUS                                      CSD027
     I                                       42  440FTYP
     I                                       45  460FSEQ
     I                                       47  49 CCY
     I                                    P  50  560CPAM
     I                                       57  57 INTC
     I                                    P  58  630INTR
     I**********                             64  650BRTT                                      CSD103
     I                                       64  65 BRTT                                      CSD103
     I                                    P  66  710BRTE
     I                                    P  72  770RTSP
     I                                    P  78  815MARG
     I                                       82  82 SPIN
     I                                       83  83 CHIN
     I                                    P  84  897WTRT
     I                                       90  90 WTIN
     I                                       91  910REPT
     I                                    P  92  980RAMT
     I                                    P  99 1010NRPD
     I                                      102 102 RFRQ
     I                                      103 1040RDYN
     I                                      105 107 FCCY
     I                                      108 1100LIND
     I                                      111 112 CRSK
     I                                      113 114 ACOF
     I                                      115 115 NACD
     I                                      116 116 RELI
     I                                      117 117 AUTO
     I                                    P 118 1240PSAM
     I                                    P 125 1318BCXR
     I                                    P 132 1388FCXR
     I                                      139 1400SDST
     I**********                            141 152 OSDA                                      CGL029
     I                                      141 152 OSDAQQ                                    CGL029
     I                                      153 162 TSTN
     I                                      163 1640MDST
     I**********                            165 176 OMDA                                      CGL029
     I                                      165 176 OMDAQQ                                    CGL029
     I                                      177 186 TMAN
     I                                      187 196 FACO
     I                                    P 197 1990RLDT
     I                                    P 200 2020NROD
     I                                      203 203 RLFQ
     I                                      204 2050RLDY
     I**********                            207 2080NBRA                                      CSD103
     I                                      207 208 NBRA                                      CSD103
     I                                    P 209 2147NRTS
     I                                      215 215 NSIN
     I                                      216 2160NCAC
     I                                    P 217 2230AWTP
     I                                    P 224 2300WTRD
     I                                    P 231 2370WTWD
     I                                    P 238 2440IWOD
     I                                    P 245 2510PWOD
     I                                      252 2520BMDI
     I                                      253 2530FMDI
     I                                    P 254 2600PDIN
     I                                      271 273 ORBR
     I                                      287 287 BTIN
     I                                      288 2890BLDY
     I**********                            290 2950OLNO                                      CSD027
     I                                      290 295 OLNO                                      CSD027
     I                                      296 296 RCSI
     I                                      297 2970ICBS
     I                                      298 298 IPFR
     I                                    P 299 3050TOTI
     I                                    P 306 3080NIDT
     I                                    P 309 3110IBDT
     I                                    P 312 3140SLID
     I                                    P 315 3210AIPD
     I                                    P 322 3240DLRC
     I                                    P 325 3270IACD
     I                                    P 328 3340AITC
     I                                    P 335 3410TFEP
     I                                    P 342 3480AIAP
     I                                    P 349 3550IPRD
     I                                    P 356 3620IPRM
     I                                    P 363 3690ICTD
     I                                    P 370 3760AIMN
     I                                    P 377 3790RBDT
     I                                    P 380 3810NOPS
     I                                      382 382 NCHI
     I                                      384 3850IFDY
     I                                      386 386 LONI
     I                                      387 387 CNFI
     I                                      388 388 ACEI
     I                                      389 389 TLXI
     I                                      390 390 CBLI
     I                                    P 391 3930ORED
     I                                    P 394 3960LCD
     I                                      397 397 CHTP
     I                                    P 398 4000TNLU
     I                                      401 403 NCUR
     I                                    P 404 4100NANT
     I                                    P 411 4178NFER
     I                                    P 418 4240NRAY
     I                                    P 425 4310FCRA
     I                                    P 432 4340LCRD
     I                                      435 436 BOKC
     I**************************************437 454 OSDA                               CGL029 CLE042
     I**************************************455 472 OMDA                               CGL029 CLE042
     I                                     15611578 OSDA                                      CLE042
     I                                     15791596 OMDA                                      CLE042
     I                                     17801782 LOIC                                      CER020
     I** RE-DEFINE LAST CHANGE DETAILS AND LOT FIELDS FOR O/P
     I                                      394 400 LSTC
     I                                        1 200 LOTLN1
     I                                      201 436 LOTLN2
     I** CATCHALL
     I        NS  06
     I*
     I*
     I*    SINGLE ITEM TRANSACTIONS FILE - HEADER RECORD.
     I*
     IMTRANZF     07
     I*
     I*    SINGLE ITEM TRANSACTIONS FILE - DETAIL RECORD.
     I*    MANUAL ADJUSTMENT TO ACCRUED MARGIN - LOANS.
     I*
     IMTRANDF     08
     I*
     I* LOCAL DATA AREA
     ILDA        UDS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     IPSDS       SDS
     I                                      244 246 WSID
     I                                      245 246 WSID2
     I                                      254 263 USRID
     I*
     I**  DATA STRUCTURE  TO UPDATE LAST TRANSACTION NUMBER DTAARA
     IDNATN       DS                              5
     I                                        1   50FNATN
     I**
     I**   DATA STRUCTURE USED IN COMMITMENT
     ICMTTXT      DS
     I                                        1   50NATN
     I                                        6   7 MDID
     I                                        8  15 PGMN
     I                                       16  18 WSIDX
     I                                       19  240MTIME
     I                                       25  25 ACTCD
     I                                       26  35 USRIDX
     I                                       51  86 LOTSCN
     I*
     IRUNDAT      DS
      **  DATA AREA RUNDAT
     I                                        1   7 RUNA
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
     I*
     ISDCURR    E DSSDCURRPD
      **  EXTERNAL DS FOR CURRENCY DETAILS
     I              A6NBDP                          CDP
      *
     ISDCUST    E DSSDCUSTPD
      **  EXTERNAL DS FOR CUSTOMER DETAILS
      *
     ISDGELR    E DSSDGELRPD
      **  EXTERNAL DS FOR GENERAL LEDGER DETAILS
     I*
     ISCSARD    E DSSCSARDPD                                                                  CLE028
      ** External DS for SAR details                                                          CLE028
      *                                                                                       CLE028
     IDSFDY     E DSDSFDY
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
     I*
     IDSSDY     E DSDSSDY
     I* SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE
     I*
      ** Datastructure to hold input parameter for AOSVALR0.                                  207629
     I            DS                                                                          207629
     I                                        1  20 WPARM                                     207629
     I                                        1   5 WPARM1                                    207629
     I                                        6  10 WPARM2                                    207629
     I                                       11  15 WPARM3                                    207629
     I                                       16  20 WPARM4                                    207629
      *                                                                   S01408
     I/COPY WNCPYSRC,LER320IIP1                                           S01408
      *                                                                   S01408
      *****************************************************************
     C/TITLE MAIN PROCESSING
      *****************************************************************
      *
     C                     MOVEACPY@      BIS@   80
      *
      **  READ WORKSTATION FILE / HELP FACILITY
     C           *IN03     IFEQ '1'
     C           *IN05     DOUEQ'0'
     C                     READ LER320FM                 05
     C                     END
     C  N02                GOTO MAINPR
     C                     END
     C  N03                SETON                     0302
     C                     SETOF                     05
      *
      ** First cycle processing
     C                     SETON                     60
     C                     MOVEL'LER320  'DBPGM
      ** Define MTRAN
     C           KMTRAN    KLIST
     C                     KFLD           RRNT             Rel Rec No.
      *
     C           *LIKE     DEFN RRNT      #RRNT
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
      **  Check system date format, DDMMYY OR MMDDYY.
     C           DATF      COMP 'M'                      98MMDDYY, 98 ON
      *
      **  Access general ledger details
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *                                                                                       CLE028
      ** Check if Flat Rate Personal Loans (Rule of 78s) is installed                         CLE028
      *                                                                                       CLE028
     C                     CALL 'AOSARDR0'                                                    CLE028
     C                     PARM *BLANKS   PRTCD   7                                           CLE028
     C                     PARM '*VERIFY' POPTN   7                                           CLE028
     C                     PARM 'CLE028'  PSARD   6                                           CLE028
     C           SCSARD    PARM SCSARD    DSFDY                                               CLE028
      *                                                                                       CLE028
     C           PRTCD     IFNE *BLANKS                                                       CLE028
     C           PRTCD     ANDNE'*NRF   '                                                     CLE028
     C                     MOVE *ON       *INU7                                               CLE028
     C                     MOVE *ON       *INU8                                               CLE028
     C                     MOVEL'SCSARDPD'DBFILE                                              CLE028
     C                     MOVEL'005'     DBASE                                               CLE028
     C                     MOVELPSARD     DBKEY                                               CLE028
     C                     GOTO ENDPR                                                         CLE028
     C                     ENDIF                                                              CLE028
      *                                                                                       CLE028
     C           PRTCD     IFEQ *BLANKS                                                       CLE028
     C                     MOVE 'Y'       CLE028  1                                           CLE028
     C                     ELSE                                                               CLE028
     C                     MOVE 'N'       CLE028                                              CLE028
     C                     ENDIF                                                              CLE028
      *                                                                                       207629
      ** Check System Value: Use Original Borrower or Purchased From?                         207629
      ** Set up AOSVALR0 parameter with GISVAL "LoanCustOriginPurch"                          207629
     C                     MOVE *BLANKS   WPARM                                               207629
     C                     MOVEL'LoanC'   WPARM1                                              207629
     C                     MOVEL'ustOr'   WPARM2                                              207629
     C                     MOVEL'iginP'   WPARM3                                              207629
     C                     MOVEL'urch'    WPARM4                                              207629
     C                     MOVELWPARM     SVALK1                                              207629
      * Call Access object:                                                                   207629
     C                     CALL 'AOSVALR0'                                                    207629
     C                     PARM           RTNCDE  7                                           207629
     C                     PARM           SVALK1 20                                           207629
     C                     PARM           SVAL1 200                                           207629
     C                     PARM           SVALK2 20                                           207629
     C                     PARM           SVAL2 200                                           207629
     C                     PARM           SVALK3 20                                           207629
     C                     PARM           SVAL3 200                                           207629
     C                     PARM           SVALK4 20                                           207629
     C                     PARM           SVAL4 200                                           207629
     C                     PARM           SVALK5 20                                           207629
     C                     PARM           SVAL5 200                                           207629
     C                     PARM           SVALK6 20                                           207629
     C                     PARM           SVAL6 200                                           207629
     C                     PARM           SVALK7 20                                           207629
     C                     PARM           SVAL7 200                                           207629
     C                     PARM           SVALK8 20                                           207629
     C                     PARM           SVAL8 200                                           207629
     C                     PARM           SVALK9 20                                           207629
     C                     PARM           SVAL9 200                                           207629
     C                     PARM           SVALK0 20                                           207629
     C                     PARM           SVAL10200                                           207629
      * If the system value is not found then issue a database error                          207629
     C           RTNCDE    IFNE '        '                                                    207629
     C                     SETON                     U7U8                                     207629
     C                     MOVEL'SDSVALPD'DBFILE           ************                       207629
     C                     Z-ADD05        DBASE            * DBERR 05 *                       207629
     C                     MOVEL'SVALK1'  DBKEY            ************                       207629
     C                     GOTO ENDPR                                                         207629
     C                     ELSE                                                               207629
      * Isolate the first character of SVAL1 which = 'P' or 'O'.                              207629
     C                     MOVELSVAL1     WRK01   1        1 character fld                    207629
     C           WRK01     IFEQ 'O'                                                           207629
     C                     MOVE 'O'       PPCUST  1                                           207629
     C                     ELSE                                                               207629
     C                     MOVE 'P'       PPCUST                                              207629
     C                     ENDIF                                                              207629
     C                     ENDIF                                                              207629
      *
      ** Define fields
     C                     MOVE 'SI'      SID
     C                     MOVE ' '       BLK150150
     C                     MOVE ' '       BLK16  16
     C                     MOVE ' '       BLK76  76
     C                     MOVE ' '       BLK34  34
     C                     Z-ADD0         ZERO05  50
      *                                                                   S01408
     C/COPY WNCPYSRC,LER320CCP1                                           S01408
      *                                                                   S01408
     C           MAINPR    TAG
      *
     C                     EXSR SETOF
     C                     EXSR TIME
      *                                                                   S01408
     C/COPY WNCPYSRC,LER320CCP2                                           S01408
      *                                                                   S01408
     C   KL
     COR 02                SETON                     40
     C   KC
     COR KL
     COR 02                GOTO ENDPR
     C                     EXSR SRINS
     C                     EXSR TIME
      *
     C           ENDPR     TAG
      *
     C   U7
     COR KC                SETON                     LR
      ** If any error codes are set up, move into array
     C           S         COMP 0                    30
     C   30                MOVEAERCD,1    ERCDS
     C           ERRMSG    COMP BLK76                    33
     C  N30                SETON                     32
      *
      **  Execute error routine, if database error exists
     CLR         *INU7     IFEQ '1'
     CLR         *INU8     ANDEQ'1'
     CLR                   EXSR *PSSR
     CLR                   END
      ********************************************************************
     C/TITLE SR/SETOF
      ********************************************************************
     C*
     C* SETOF - SUBROUTINE TO RESET SCREEN INDICATORS AND ERROR ARRAY
     C*         INDEX
     C*
     C* CALLED FROM: MAIN PROCESSING
     C*
     C* CALLS: NOTHING
     C*
     C********************************************************************
      *
     C           SETOF     BEGSR
     C                     SETOF                     33
     C                     SETOF                     326944
     C                     SETOF                     415161
     C                     SETOF                     636740
     C                     SETOF                     546865
     C                     MOVE '    '    ERCD
     C                     MOVE BLK76     ERCDS  76
     C                     MOVE BLK76     ERRMSG 76
     C                     Z-ADD0         S       20
     C                     ENDSR
      ********************************************************************
     C/TITLE SR/SRINS
      ********************************************************************
      *
      * SRINS - SUBROUTINE TO PROCESS INSERT SCREEN
      *
      * CALLED FROM: MAIN PROCESSING
      *
      * CALLS: ZALIGN, UDLOG,
      *
      ********************************************************************
      *
     C           SRINS     BEGSR
      *
      ** Validate loan no & check that loan is on file
     C   20                SETON                     44
     C   20                GOTO LNERR
     C                     MOVELINLOAN    LNKEY   7
     C                     MOVE 'A'       LNKEY
     C           LNKEY     CHAINCLOAN                44
      *
      **  Release loans record
     C  N44                SETON                     80
     C  N44                SETOF                     7778
     C   06                SETON                     44
     C           LNERR     TAG                             ** LNERR TAG **
     C   44      1         ADD  S         S          41
     C   44                MOVE ' 025'    ERCD,S
     C   44                GOTO ENDINS
      *                                                                   S01408
     C/COPY WNCPYSRC,LER320CCP3                                           S01408
      *                                                                   S01408
      *
      ** Amends not allowed to discounted loans/participations
      ** and Flat rate personal loans, if CLE028 is installed                                 CLE028
      *                                                                                       CLE028
     C           PTYP      COMP 63                       38
     C     N38   PTYP      COMP 65                       38
     C     N38   PTYP      COMP 67                       38
     C           *IN38     IFEQ *OFF                                                          CLE028
     C           PTYP      ANDEQ80                                                            CLE028
     C           CLE028    ANDEQ'Y'                                                           CLE028
     C                     MOVE *ON       *IN38                                               CLE028
     C                     ENDIF                                                              CLE028
     C      38   1         ADD  S         S          41
     C      38             MOVE ' 179'    ERCD,S
     C      38             GOTO ENDINS
      *
      ** Check value date and maturity date of loan against run date
     C           RUND      COMP VDAT                   3838
     C   38                GOTO DATERR
     C           MDAT      COMP 0                    38
     C   38      RUND      COMP MDAT                 38
     C           DATERR    TAG                             ** DATERR TAG *
     C   38      1         ADD  S         S          41
     C   38                MOVE ' 106'    ERCD,S
     C   38                GOTO ENDINS
      ** Save last update details
     C                     MOVE LSTC      LSTCS   7
      *
      **  If multibranching = Yes, check authority of booking branch/
      **  action code, else check authority on action code.
     C           MBIN      IFEQ 'Y'
     C                     CALL 'ZVACTBU'
     C                     PARM 'I'       @ZACTN  1
     C                     PARM BRCA      @ZBR    3
     C                     PARM           @ERR    10
      *
     C                     ELSE
      *
     C                     CALL 'ZVACTU'
     C                     PARM 'I'       @ZACTN  1
     C                     PARM           @ERR    10
      *
     C                     END
      *
     C           @ERR      IFNE 0
     C                     ADD  1         S          41
      *
     C           MBIN      IFEQ 'Y'
     C           @ERR      IFEQ 1
     C                     MOVE '303'     ERCD,S
     C                     MOVE EMES,3    ERRMSG
     C                     ELSE
     C                     MOVE '304'     ERCD,S
     C                     MOVE EMES,4    ERRMSG
     C                     END
     C                     ELSE
      *
     C                     MOVE '302'     ERCD,S
     C                     MOVE EMES,5    ERRMSG
     C                     END
     C                     END
      *
      **  Check authority on originating branch, when originating
      **  branch validation is yes.
     C           MBIN      IFEQ 'Y'
     C           BKOBRU    ANDEQ'Y'
     C           BKOBUV    ANDEQ'Y'
      *
     C                     CALL 'ZVOBU'
     C                     PARM ORBR      @BRCX   3
     C                     PARM           @ERR    10
      *
     C           @ERR      IFNE 0
     C                     ADD  1         S          41
     C           @ERR      IFEQ 1
     C                     MOVE '305'     ERCD,S
     C                     MOVE EMES,6    ERRMSG
     C                     ELSE
     C                     MOVE '306'     ERCD,S
     C                     MOVE EMES,7    ERRMSG
     C                     END
     C                     END
      *
     C                     END
      *                                                                   S01408
     C/COPY WNCPYSRC,LER320CCP4                                           S01408
      *                                                                   S01408
      *
      **  Check customer entered is equal to a loan customer
     C                     MOVELINCUSN    KEYC   10
     C                     CALL 'AOCUSTR0'
     C                     PARM '*BLANKS' @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM           KEYC   10
     C                     PARM *BLANKS   KEYSTS  7
     C           SDCUST    PARM SDCUST    DSSDY
     C           @RTCD     COMP *BLANKS              1717
     C  N17                MOVEL*BLANKS   INCUSN
     C  N17                MOVELBBCUST    INCUSN
     C   17      1         ADD  S         S          51
     C      17             MOVE ' 035'    ERCD,S
     C      17             GOTO ENDINS
     C**********           MOVE BBCUST    CNUMS   60                                          CSD027
     C                     MOVE BBCUST    CNUMS   6                                           CSD027
      *                                                                                       207629
      ** If the loan is a Participation Purchased, & System Value                             207629
      ** LoanCustOriginPurch = 'O', use Original Borrower:                                    207629
     C           PTYP      IFEQ 64                                                            207629
     C           PTYP      OREQ 65                                                            207629
     C           PTYP      OREQ 68                                                            207629
     C           PTYP      OREQ 71                                                            207629
     C           PPCUST    IFEQ 'O'                                                           207629
     C           CNUMS     COMP OLNO                 5454                                     207629
     C                     ELSE                                                               207629
     C           CNUMS     COMP CNUM                 5454                                     207629
     C                     ENDIF                                                              207629
     C                     ELSE                                                               207629
      * Else, not a Participation Purchased.                                                  207629
     C           CNUMS     COMP CNUM                 5454
     C                     ENDIF                                                              207629
     C   54      1         ADD  S         S          51
     C   54                MOVE ' 104'    ERCD,S
     C   54                GOTO ENDINS
      *
      ** Validate adjustment - obtain correct no. of decimal places
     C   22                GOTO ADJERR
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM CCY       K101    3                    A
     C           SDCURR    PARM SDCURR    DSSDY                        B
     C           @RTCD     IFNE *BLANKS
     C                     SETON                     U7U8
     C                     MOVE '001'     DBASE            * DB ERROR 001*
     C                     MOVE CCY       DBKEY
     C                     MOVE 'SDCURRPD'DBFILE
     C                     GOTO ENDINS
     C                     END
     C                     MOVE BLK16     ZFIELD
     C                     MOVE INADJ     ZFIELD
     C                     Z-ADDCDP       ZADEC
     C           13        SUB  CDP       ZADIG
     C                     EXSR ZALIGN
     C  N99                MOVE ZFIELD    OTADJ  130
     C  N99                GOTO SGNCHK
     C           ADJERR    TAG                             ** ADJERR TAG *
     C   22
     COR 99      1         ADD  S         S          61
     C   22
     COR 99                MOVE ' 180'    ERCD,S
      *
      ** Validate sign
     C           SGNCHK    TAG
     C           INSIGN    COMP '+'                      64
     C  N64      INSIGN    COMP '-'                      65
     C  N64N65
     COR 23                SETON                     63
     C   63      1         ADD  S         S
     C   63                MOVE ' 182'    ERCD,S
      *
      ** Validate currency
     C   24                SETON                     68
     C           *IN68     IFNE '1'
     C                     CALL 'AOCURRR0'
     C                     PARM           @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM INCCY     CKEY    3
     C           SDCURR    PARM SDCURR    DSSDY
     C           @RTCD     IFNE *BLANKS
     C                     SETON                     68
     C                     ELSE
     C                     MOVELA6CYCD    INCCY
     C                     END
     C                     END
      *
     C  N68      INCCY     COMP CCY                  6868
     C   68      1         ADD  S         S          67
     C   68                MOVE ' 189'    ERCD,S
      *
     C           ENDINS    TAG                             ** ENDINS TAG *
      *
      ** End of validation - if no errors, log & update
     C           S         COMP 0                    66
     C   66
     COR U7 U8             GOTO ENDALL
      *
     C                     EXSR UDLOG
      *
      ** If successful exit, return fresh insert screen
     C  N69                SETON                     40
     C   69                SETON                     40
     C           ENDALL    ENDSR
      *
     C********************************************************************
     C/TITLE SR/UPDATE
      ********************************************************************
      *
      * UPDATE - SUBROUTINE TO UPDATE MTRAN FILE
      *
      * CALLED FROM: UDLOG
      *
      * CALLS: GLZADD
      *
      ********************************************************************
      *
     C           UPDATE    BEGSR
      ** MTRAN header update
     C                     Z-ADD1         #RRNT
     C           #RRNT     CHAINMTRAN                49
     C  N07                SETON                     U7U8
     C  N07                MOVE '002'     DBASE
     C  N07                MOVEL'00001   'DBKEY
     C  N07                MOVE 'MTRAN'   DBFILE
     C  N07                GOTO ENDUP
     C           NAVT      ADD  1         NAVT2   50
      *
      ** Move adjustment amount into ZZAMT for hash totalling
     C   64      NOIL      ADD  1         NOIL2   50
     C   65      NODL      ADD  1         NODL2   50
     C                     MOVE OTADJ     ZZAMT
      *
     C   64                Z-ADDHLIW      ZZTOTI
     C   64                Z-ADDHLID      ZZTOTD
     C   65                Z-ADDHLDW      ZZTOTI
     C   65                Z-ADDHLDD      ZZTOTD
     C                     EXSR GLZADD
     C   64                Z-ADDZZTOTI    HLIW2  150
     C   64                Z-ADDZZTOTD    HLID2   30
     C   65                Z-ADDZZTOTI    HLDW2  150
     C   65                Z-ADDZZTOTD    HLDD2   30
      *
     C           ENDUP     ENDSR
      *
      ********************************************************************
     C/TITLE SR/TIME
      ********************************************************************
      *
      * TIME - SUBROUTINE TO GET CORRECT TIME
      *
      * CALLED FROM: MAIN PROCESSING, UDLOG
      *
      * CALLS: NOTHING
      *
     C********************************************************************
      *
      ** SUBROUTINE TO GET CORRECT TIME
      ** CALLED FROM M.P.,UDLOG
     C           TIME      BEGSR
     C                     TIME           MTIME   60       ** TIME **
     C                     ENDSR
      *
      ********************************************************************
     C/TITLE SR/UDLOG
     C********************************************************************
      *
      * UDLOG - SUBROUTINE TO PERFORM UPDATING
      *
      * CALLED FROM: SRINS, UDLOG
      *
      * CALLS: LOGCHN,
      *
      ********************************************************************
      *
     C           UDLOG     BEGSR
      *
      ** If a database or system error is found indicators are set
      ** to reflect the error condition, release files, release
      ** screen and end program.
      *                                                                   S01408
     C/COPY WNCPYSRC,LER320CCP5                                           S01408
      *                                                                   S01408
      *
      **  Access LOANS record to be updated
     C           LNKEY     CHAINCLOAN                44
     C   44                SETON                     U7U8
     C   44                MOVE '003'     DBASE
     C   44                MOVELLNKEY     DBKEY
     C   44                MOVE 'CLOAN'   DBFILE
     C   44                GOTO ENDD
      ** Check if record updated by anotherS
     C   04      LSTC      COMP LSTCS                    34
     C   04N34
     COR 06                SETON                     69
     C   69                MOVELEMES,1    ERRMSG
     C   69                SETON                     80
     C   69                SETOF                     7778
     C   69                GOTO ENDD
     C*
     C**  Obtain last transactin number and set up standard output info
     C                     EXSR ZTNLU1
     C                     MOVE 'LE'      MDID    2
     C                     MOVEL'LER320'  PGMN    8
     C                     MOVE WSID      WSIDX
     C                     MOVE 'A'       ACTCD
     C                     MOVE USRID     USRIDX
      *
      ** Update MTRAN detail, lock header by read & dummy update
     C                     Z-ADD1         #RRNT
     C           #RRNT     CHAINMTRAN                45
     C  N07                SETON                     U7U8
     C  N07                MOVEL'00001   'DBKEY
     C  N07                MOVEL'MTRAN'   DBFILE
     C  N07                MOVE '004'     DBASE
     C  N07                GOTO ENDD
      *
      ** Attempt to retrieve MTRAND Record, if found, set pgm error
     C           NAVT      CHAINMTRAN                45
     C  N45                SETON                     U7U8
     C  N45                MOVELNAVT      DBKEY
     C  N45                MOVEL'MTRAN'   DBFILE
     C  N45                MOVE '006'     DBASE
     C  N45                GOTO ENDD
     C                     MOVE 'J'       RECI
     C                     MOVE INLOAN    LNRF
     C                     MOVE INCCY     CCY
     C                     MOVE OTADJ     MAAM
     C                     MOVE INSIGN    SIGN
     C                     MOVE FTYP      FACT
     C                     MOVE FSEQ      FCNO
     C                     MOVE NAVT      RRNT
     C                     MOVE NATN      TNLU
     C                     EXSR TIME
     C                     MOVE MTIME     UDTM
     C                     SETON                     78
     C                     SETOF                     7778
     C                     WRITEMTRANDF
      *                                                                   S01408
     C/COPY WNCPYSRC,LER320CCP7                                           S01408
      *                                                                   S01408
      *
      ** Update MTRAN header record
     C                     EXSR UPDATE
     C                     MOVE NAVT2     NAVT
     C   64                MOVE NOIL2     NOIL
     C   64                MOVE HLIW2     HLIW
     C   64                MOVE HLID2     HLID
     C   65                MOVE NODL2     NODL
     C   65                MOVE HLDW2     HLDW
     C   65                MOVE HLDD2     HLDD
     C                     MOVE NATN      TNLU
     C                     SETON                     77
     C                     SETOF                     7778
     C                     UPDATMTRANZF
      *
      ** Get log file record to log update.
     C   64      AIMN      ADD  OTADJ     AIANA  130       ** LOANS FILE *
     C   65      AIMN      SUB  OTADJ     AIANA  130       ** UPDATE **
      *                                                                   S01408
     C/COPY WNCPYSRC,LER320CCP6                                           S01408
      *                                                                   S01408
      *
      **  End commitment cycle
     C           CMTTXT    COMIT
      *
     C           ENDD      ENDSR
      ********************************************************************
     C/TITLE SR/ZALIGN
      ********************************************************************
      *
      * ZALIGN - SUBROUTINE TO VALIDATE AND RIGHT-ALIGN NUMBERIC
      *          FIELDS
      *
      * CALLED FROM:
      *
      * CALLS:
      *
      ********************************************************************
      *
      **  ZALIGN SR. TO VALIDATE AND RIGHT-ALIGN NUMERIC FIELDS.
      *
     C           ZALIGN    BEGSR                           *** ZALIGN ***
      *
     C                     SETOF                     929399
      *
      **   SAVE INPUT FIELD IN ARRAY, ZA1.
     C                     MOVEAZFIELD    ZA1
      *
      **   CALCULATION TO DEFINE NUMBER STRUCTURE CONTROL FIELDS.
     C                     Z-ADDZADIG     ZADIG   20
     C                     Z-ADDZADEC     ZADEC   10
      *
      **   CALCULATIONS TO DEFINE/CLEAR FIELDS.
     C                     MOVE ' '       ZA2
     C                     MOVEAZA2       ZFIELD 16
     C                     Z-ADD0         ZX      20
     C                     Z-ADD0         ZY      20
      *
      **   ENSURE REQUIRED STRUCTURE OF FIELD IS VALID.
     C           ZADIG     ADD  ZADEC     ZZ      20
     C           ZZ        COMP 15                   99
     C   99                GOTO ZAEND
      *
      **   LOOP TO FIND DECIMAL POINT, BLANKS AND CHARACTERS.
     C           ZATAG1    TAG                             ** ZATAG1 TAG *
     C           ZX        ADD  1         ZX
      *
      **   CHECK FOR DECIMAL POINT. ERROR IF SECOND DECIMAL POINT.
     C           ZA1,ZX    COMP '.'                      90
     C   90 93             SETON                     99
     C   99                GOTO ZAEND
      *
      **   CHECK FOR 'M' CONSTANT. ERROR IF NOT LAST CHARACTER ENTERED
     C           ZA1,ZX    COMP 'M'                      96
     C   96                DO
     C           ZX        COMP 15                   99  99
     C   99                GOTO ZAEND
     C           ZX        ADD  1         ZX
     C           ZA1,ZX    COMP ' '                  9999
     C   99                GOTO ZAEND
     C           ZX        SUB  1         ZX
     C           ZX        COMP 9                    99
     C   99                GOTO ZAEND
     C                     END
      *
      **   CHECK FOR 'T' CONSTANT. ERROR IF NOT LAST CHARACTER ENTERED
     C           ZA1,ZX    COMP 'T'                      97
     C   97                DO
     C           ZX        COMP 15                   99  99
     C   99                GOTO ZAEND
     C           ZX        ADD  1         ZX
     C           ZA1,ZX    COMP ' '                  9999
     C   99                GOTO ZAEND
     C           ZX        SUB  1         ZX
     C           ZX        COMP 12                   99
     C   99                GOTO ZAEND
     C                     END
      *
      **   CLEAR ALPHAMERIC CONSTANT FROM ZA1
     C   96
     COR 97                MOVE ' '       ZA1,ZX
      *
      **   CHECK FOR BLANKS. BYPASS FOR FIRST BLANK AFTER A DIGIT.
     C           ZA1,ZX    COMP ' '                      91
     C   91 92             GOTO ZATAG2
      *
      **   CHECK FOR NUMERIC, IF NOT '.' OR ' '.
     C  N90N91   ZA1,ZX    COMP '0'                    99
     C  N90N91N99ZA1,ZX    COMP '9'                  99
     C   99                GOTO ZAEND                      NOT NUMERIC
      *
      **   STORE DIGITS IN ARRAY AND HOW MANY.
      **   ZY, TOTAL OF DIGITS IN THE INPUT FIELD.
      **   ZZ, TOTAL OF DIGITS TO THE LEFT OF DECIMAL POINT.
     C  N90N91   ZY        ADD  1         ZY         92
     C  N90N91             MOVE ZA1,ZX    ZA2,ZY
     C   90                Z-ADDZY        ZZ         93  93
      **
     C           ZX        COMP 16                     94  CHECK IF ALL
     C   94                GOTO ZATAG1
      *
     C           ZATAG2    TAG                             ** ZATAG2 TAG *
      *
      **   IF 'M' SPECIFIED, MULTIPLY ZA1 BY 1 MILLION
     C   96      ZZ        ADD  6         ZZ
      *
      **   IF 'T' SPECIFIED, MULTIPLY ZA1 BY 1 THOUSAND
     C   97      ZZ        ADD  3         ZZ
      *
      **   FILL IN ZEROS IN ANY BLANKS LEFT OF DECIMAL POINT
     C   96 93
     COR 97 93   ZZ        DOWGTZY
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C                     END
      *
      **   IF CONSTANT SPECIFIED WITH NO DECIMAL POINT ZEROISE BLANKS
     C   96N93             DO   6
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C                     END
      *
     C   97N93             DO   3
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C                     END
      *
      **   IF NO DECIMAL POINT FOUND, SET TOTAL FIELD ZZ.
     C  N93                Z-ADDZY        ZZ
      *
      **   CHECK FOR EMBEDDED BLANKS.
     C   91 92             MOVEAZA1,ZX    ZFIELD
     C   91 92   ZFIELD    COMP ' '                  9999
     C   99                GOTO ZAEND                      EMBEDDED BLANK
      *
      **   ENSURE THAT NUMBER OF DIGITS ENTERED EITHER SIDE
      **   OF DECIMAL POINT ARE NOT MORE THAN ALLOWED.
     C           ZZ        COMP ZADIG                99
     C           ZY        SUB  ZZ        ZX
     C  N99      ZADEC     SUB  ZX        ZX           9995
     C   99                GOTO ZAEND
     C   95                GOTO ZATAG4                     NO TRAILING
      *                                                    BLANKS.
     C**   FILL THE TRAILING BLANKS WITH ZEROS.
     C           ZY        ADD  ZX        ZZ
     C           ZATAG3    TAG                             ** ZATAG3 TAG *
     C           ZY        ADD  1         ZY
     C                     MOVE '0'       ZA2,ZY
     C           ZY        COMP ZZ                     94
     C   94                GOTO ZATAG3
      *
      **   RIGHT-ALIGN THE VALUE BY MOVING BACK TO ARRAY, ZA1.
     C           ZATAG4    TAG                             ** ZATAG4 TAG *
     C                     MOVE '0'       ZA1
     C                     Z-ADD16        ZX
     C           ZATAG5    TAG                             ** ZATAG5 TAG *
     C           ZY        COMP 0                        94
     C   94                GOTO ZATAG6
     C                     MOVE ZA2,ZY    ZA1,ZX
     C           ZY        SUB  1         ZY
     C           ZX        SUB  1         ZX
     C                     GOTO ZATAG5
     C           ZATAG6    TAG                             ** ZATAG6 TAG *
      *
      **   MOVE VALIDATED AND RIGHT-ALIGNED NUMBER BACK INTO ZFIELD.
     C                     MOVEAZA1       ZFIELD
      *
     C                     SETOF                     9697
      *
     C           ZAEND     ENDSR                           * ZAEND ENDSR *
      *
     C********************************************************************
     C/TITLE SR/GLZADD
     C********************************************************************
     C*
     C* GLZADD - SUBROUTINE TO ADD AN AMOUNT (ZZAMT) TO THE TOTAL
     C*          (ZZTOTI, ZZTOTD, IND '99' IS SET BY AN OVERFLOW ERROR)
     C*
     C* CALLED FROM:
     C*
     C* CALLS:
     C*
     C********************************************************************
     C*
     CSR         GLZADD    BEGSR                           *** GLZADD ****
     C*
     CSR                   Z-ADDZZAMT     ZZAMT  153     91-DEFINE ZZAMT
     CSR 91                GOTO ZZAEND                     AMT = 0.BYPASS
     C*
     C* SPLIT ZZAMT INTO INTEGER AND DECIMAL FIELDS
     CSR                   Z-ADDZZAMT     ZZAMTI 150       INTEGER FIELD
     CSR                   MOVE ZZAMT     ZZAMTD  30       DECIMAL FIELD
     C* BOTH ZZAMTI AND ZZAMTD CONTAIN A 'SIGN' ZONE NOW
     C*
     CSR                   EXSR GLZSUM
     CSR         ZZAEND    ENDSR                           ** ZZAEND TAG *
     C*
     C********************************************************************
     C/TITLE SR/GLZSUM
     C********************************************************************
     C*
     C* GLZSUM - SUBROUTINE TO CARRY OUT THE ADDITION FOR SUBROUTINE
     C*          GLZADD, GLZSUB, GLZCMP.
     C*          PARAMETERS PASSED -
     C*                     I/P ZZAMTI ZZAMTD
     C*                     O/P ZZTOTI ZZTOTD ZZNEGD IND 96 IND 99.
     C*
     C* CALLED FROM:
     C*
     C* CALLS:
     C*
     C********************************************************************
     C*
     CSR         GLZSUM    BEGSR                           *** GLZSUM ****
     C*
     CSR                   Z-ADDZZTOTI    ZZTOTI 150       DEFINE ZZTOTI
     CSR                   Z-ADDZZTOTD    ZZTOTD  30       DEFINE ZZTOTD
     CSR                   SETOF                     919293
     CSR                   SETOF                     949599
     C*
     C*    DETERMINE SIGN OF ZZAMTI & D,   92 IF NEG
     CSR         ZZAMTI    COMP 0                      9293
     CSR 93      ZZAMTD    COMP 0                      9293
     CSR 93                GOTO ZZSEND                     ZERO BYPASS
     C*
     C*    DETERMINE SIGN OF ZZTOTI & D, 91 IF NEG.
     CSR         ZZTOTI    COMP 0                      9193
     CSR 93      ZZTOTD    COMP 0                      9193
     C*
     C*    IF ZZTOTAL IS ZERO, RETURN ZZAMOUNT.
     CSR 93                Z-ADDZZAMTI    ZZTOTI
     CSR 93                Z-ADDZZAMTD    ZZTOTD
     CSR 93                GOTO ZZSEND                     ZERO BYPASS
     C*    IF SIGNS DIFFER BYPASS OVERFLOW CHECKS.
     CSR 91N92
     CORN91 92             GOTO ZZOFPS
     C*
     CSR         ZZAMTD    ADD  ZZTOTD    ZZWK2   40
     CSR         ZZWK2     COMP 999                  93    '93' = CARRY
     CSRN93      ZZWK2     COMP -999                   93    INTO INTEGER.
     C*
     CSR 93N92   ZZAMTI    ADD  1         ZZWK3  150       ADD 'CARRY' +VE
     CSR    93 92ZZAMTI    SUB  1         ZZWK3            SUB 'CARRY' -VE
     CSR 93      ZZTOTI    ADD  ZZWK3     ZZWK3
     CSRN93      ZZTOTI    ADD  ZZAMTI    ZZWK3
     C*
     C* IF THE MODULUS OF ZZWK3 IS LT MOD. ZZTOTI THEN O/F HAS OCCURED
     CSRN92      ZZWK3     COMP ZZTOTI                 99  -92 MEANS NOS.
     CSR 92      ZZWK3     COMP ZZTOTI               99     NEGATIVE
     CSRN99                Z-ADDZZWK2     ZZTOTD
     CSRN99                Z-ADDZZWK3     ZZTOTI
     C*
     C* IF O/F ZEROIZE ZZAMT , IND '99' SET AND ZZTOT FIELDS LEFT INTACT.
     CSR 99                Z-ADD0         ZZAMT  153
     CSR                   GOTO ZZSEND
     C*
     C* THE 'SIGNS' ARE DIFFERENT
     CSR         ZZOFPS    TAG                             ** ZZOFPS TAG**
     C* IF ZZAMT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZTOT
     C*
     CSR 92                Z-SUBZZAMTI    ZZAMTI 150
     CSR 92                Z-SUBZZAMTD    ZZAMTD  30
     C*
     C* IF ZZTOT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZAMT.
     C*
     CSR 91                Z-SUBZZTOTI    ZZTOTI
     CSR 91                Z-SUBZZTOTD    ZZTOTD
     C* BOTH ZZAMT AND ZZTOT ARE NOW POSITIVE.
     C*
     CSR         ZZTOTI    COMP ZZAMTI               93  95
     CSR 95      ZZTOTD    COMP ZZAMTD               93  95
     C*
     C* IF ZZTOT EQ. ZZAMT RETURN ZERO.
     CSR 95                Z-ADD0         ZZTOTI
     CSR 95                Z-ADD0         ZZTOTD
     CSR 95                GOTO ZZSEND
     C*
     C* IF ZZTOT GT. ZZAMT.
     CSR 93      ZZAMTD    COMP ZZTOTD               94
     CSR 93 94   ZZTOTI    SUB  1         ZZTOTI
     CSR 93 94   ZZTOTD    ADD  1000      ZZWK2
     CSR 93      ZZTOTI    SUB  ZZAMTI    ZZTOTI
     CSR 93 94   ZZWK2     SUB  ZZAMTD    ZZTOTD
     CSR 93N94   ZZTOTD    SUB  ZZAMTD    ZZTOTD
     C*
     C*
     C* IF ZZAMT GT. ZZTOT.
     CSRN93      ZZTOTD    COMP ZZAMTD               94
     CSRN93 94   ZZAMTI    SUB  1         ZZWK3  150
     CSRN93 94   ZZAMTD    ADD  1000      ZZWK2
     CSRN93 94   ZZWK3     SUB  ZZTOTI    ZZTOTI
     CSRN93N94   ZZAMTI    SUB  ZZTOTI    ZZTOTI
     CSRN93 94   ZZWK2     SUB  ZZTOTD    ZZTOTD
     CSRN93N94   ZZAMTD    SUB  ZZTOTD    ZZTOTD
     C*
     C* REVERSE SIGN OF ZZTOT IF LARGER I/P FIELDS WERE NEGATIVE
     CSR                   SETOF                     94
     CSR 93 91
     CORN93 92             SETON                     94
     CSR 94                Z-SUBZZTOTI    ZZTOTI
     CSR 94                Z-SUBZZTOTD    ZZTOTD
     C**
     C**   RESTORE SIGN OF ZZAMTI & ZZAMTD IF IT WAS REVERSED.
     CSR 92                Z-SUBZZAMTI    ZZAMTI
     CSR 92                Z-SUBZZAMTD    ZZAMTD
     CSR         ZZSEND    TAG                             ** ZZSEND TAG *
     C**
     C**   IF ZZTOTD IS ZERO, AND ZZTOTI IS NEG. SET UP ZZNEGD.
     CSR                   SETOF                     96
     CSR         ZZTOTD    COMP 0                        91
     CSR 91      ZZTOTI    COMP 0                      96
     CSR 96                MOVE '.000-'   ZZNEGD  5
     CSR                   ENDSR                             ** ENDSR **
     C********************************************************************
     C/TITLE SR/ZTNLU1
     C*****************************************************************
     C*
     C* ZTNLU1 - SUBROUTINE TO LOCK THE TRANSACTION NUMBER DATA AREA
     C*          AND THEN UPDATE AND RELEASE THE DATA AREA.
     C*
     C* CALLED FROM: UDLOG
     C*
     C* CALLS: NOTHING
     C*
     C********************************************************************
     C*
     C           ZTNLU1    BEGSR
     C           *NAMVAR   DEFN MNATN     DNATN
     C           *LOCK     IN   DNATN
     C                     MOVE FNATN     NATN    50
     C                     MOVE FNATN     ZZWK05  50
     C                     ADD  1         ZZWK05
     C                     MOVE ZZWK05    FNATN
     C                     OUT  DNATN
     C                     ENDSR
     C********************************************************************
     C/TITLE SR/*PSSR
     C********************************************************************
     C*
     C* *PSSR  --- SUBROUTINE TO HANDLE ABNORMAL ERROR CONDITIONS
     C*
     C* CALLED FROM: AFTER ABNORNAL OPERATION OCCURS
     C*
     C* CALLS: NOTHING
     C*
     C********************************************************************
     C*
     C           *PSSR     BEGSR                           ** *PSSR **
     C*
     C           WRUN      IFEQ *BLANKS
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     CALL 'DBERRCTL'
     C                     ELSE
     C                     RETRN
     C                     END
     C*
     C                     ENDSR                           ** *PSSR **
     C********************************************************************
     C/COPY WNCPYSRC,LER320C001
     C/EJECT
     OLER320FMD        60NKCNU7
     O                                   K8 'LER320F1'
     O/COPY WNCPYSRC,LER320O001
     O                         *IN41      1
     O                         *IN51      2
     O                         *IN61      3
     O                         *IN63      4
     O                         *IN67      5
     O                         *IN32      6
     O                         *IN33      7
     O                         SID        9
     O                         MTIME     17 '  .  .  '
     O                         WSID      20
     O                         INLOAN    26
     O                         INCUSN    36
     O                         SCFLD     54
     O                       40BLK34     54
     O                       69INLOAN    26
     O                         ERCDS    130
     O                         ERRMSG   206
     O/COPY WNCPYSRC,LER320O002
**   EMES - ERROR MESSAGES
THIS RECORD HAS BEEN UPDATED BY ANOTHER WORK STATION
WARNING - MTRAN FILE NEARLY FULL
USER IS NOT AUTH. TO ANY ACTION CODES FOR THIS MENU ITEM AND BRANCH
USER IS NOT AUTH. TO THE ACTION CODE FOR THIS MENU ITEM AND BRANCH
USER IS NOT AUTHORISED TO THE ACTION CODE FOR THIS MENU ITEM
USER IS NOT AUTHORISED TO ANY ORIGINATING BRANCHES
USER IS NOT AUTHORISED TO THE ORIGINATING BRANCH
** CPY@   **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
     X/COPY WNCPYSRC,LER320X001
