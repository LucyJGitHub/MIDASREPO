     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Update fees file account officers')           *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Customer Lending Module
     F*                                                               *
     F*  LER426 - Update of fees file account officers                *
     F*                                                               *
     F*  Function: Updates A/C officer field of fees file to that of  *
     F*   (COB)    the transaction (facility or loan) on which the    *
     F*            fee is taken.                                      *
     F*                                                               *
     F*  Called By: CLP/LERC426                                       *
     F*                                                               *
     F*  Calls: None                                                  *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      *                 CLE139             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CAS009             Date 16May05               *
      *                 CAS006             Date 21Jan03               *
      *                 CAS005             Date 16Dec02               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 204452             Date 02May02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CLE011             Date 07Nov00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CLE009             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 141453             Date 17Feb99               *
     F*                 127966             Date 13Oct97               *
     F*                 100515             DATE 22MAY96               *
     F*                 Xnnnnn             DATE ddmmmyy               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference                                *
      *  CLE139 - Lending Fee Capitalisation (Recompile)              *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CAS009 - EIR/AC Accounting (Recompile)                       *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CAS005 - Enahancements to CAS001, CAS002 and CAS004          *
      *           (Recompile)                                         *
      *  204452 - Recompiled due to changed PF/LEFEED.                *
      *  CLE011 - Rate Fixing Days for Lending Rollover Processing    *
      *           Recompile                                           *
      *  CLE009 - LE I/C Swift Message Generation for Fees            *
      *           Recompiled due to changes in LEFEED                 *
      *  141453 - Recompiled due to changes in PF/LEFEED              *
     F*  127966 - Profit Centre Accounting Development:               *
     F*           Recompiled due to changes in LEFEEDL1               *
     F*  100515 - Recompile program due to changes in LEFEED.         *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     FLEFEEDL1UF  E           K        DISK         KINFSR *PSSR
     FCLOAN   IF  E           K        DISK
     F            CLOANHHF                          KIGNORE
     F            CLOANCKF                          KIGNORE
     F            CLOANZ1F                          KIGNORE
     FFCLTY   IF  E           K        DISK
     F            FCLTYHHF                          KIGNORE
     F            FCLTYFNF                          KIGNORE
     F            FCLTYZZF                          KIGNORE
     F/EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*    01  - Read of LF/LEFEEDL1                                  *
     F*    10  - Chain to LF/CLOAN                                    *
     F*    20  - Chain to LF/FCLTY                                    *
     F*                                                               *
     F*  90-99 - Used by Standard Subroutines                         *
     F*                                                               *
     F*  U7+U8 - Database Error                                       *
     F*                                                               *
     F*****************************************************************
     F/SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  INIT   - Program initialisation.                             *
     F*  PROCES - Detail processing.                                  *
     F*  *PSSR  - Program exception processing.                       *
     F*  TERM   - Program termination.                                *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E                    CPY@    1   1 80               COPYRIGHT
     E/SPACE 3
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID.
      *
     I                                      244 253 JOB
     I                                      244 246 WSID
      *
     I            DS
      *
      ** Split facilty type and sequence from LF/LEFEEDL1 field
      *
     I                                        1   50FEFACL
     I                                        1   30FTYP
     I                                        4   50FSEQ
      *
     ILDA       E DSLDA
      *
      ** Local Data Area.
      *
     ISDACOF    E DSSDACOFPD
      *
      ** Account Officer Details.
      *
     IDSFDY     E DSDSFDY
      *
      ** Short data structure for access programs.
      *
     C/EJECT
     C/TITLE Main Processing
      *
      ** Perform Initialisation.
      *
     C                     EXSR INIT
      *
      ** Perform Detail Processing.
      *
     C                     EXSR PROCES
      *
      ** Program Termination.
      *
     C                     EXSR TERM
      *
      *****************************************************************
      /TITLE SR/INIT
      *****************************************************************
      *                                                               *
      *  INIT   - Program initialisation subroutine.                  *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:     Nothing                                           *
      *                                                               *
      *****************************************************************
      *
     C           INIT      BEGSR
      *
      ** Set up copyright statement.
      *
     C                     MOVEACPY@      BIS@   80
      *
      ** Key list for LF/CLOAN.
      *
     C           LKEY      KLIST
     C                     KFLD           FELOAN
     C                     KFLD           RCDT    1
      *
      ** Key list for LF/FCLTY.
      *
     C           FKEY      KLIST
     C                     KFLD           FECNUM
     C                     KFLD           FTYP
     C                     KFLD           FSEQ
     C                     KFLD           RCDT
      *
      ** Initialise control fields (customer number, facility and
      ** loan references of last record read), loan and facility
      ** record IDs.
      *
     C**********           Z-ADD*ZEROS    ZCNUM   60                                          CSD027
     C                     MOVE *BLANKS   ZCNUM   6                                           CSD027
     C                     Z-ADD*ZEROS    ZFACL   50
     C**********           Z-ADD*ZEROS    ZLOAN   60                                          CLE148
     C                     MOVEL*BLANKS   ZLOAN   6                                           CLE148
     C                     MOVE 'A'       RCDT
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/PROCES
      *****************************************************************
      *                                                               *
      *  PROCES - Detail processing subroutine.                       *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls:     Nothing                                           *
      *                                                               *
      *****************************************************************
      *
     C           PROCES    BEGSR
      *
      ** Read LF/LEFEEDL1 until EOF.
      *
     C                     READ LEFEEDL1                 01
      *
     C           *IN01     DOWEQ'0'
      *
      ** Process loan fee if facility details are zero ...
      *
     C           FEFACL    IFEQ *ZEROS
      *
      ** Access LF/CLOAN for loan account officer on change of
      ** fee loan reference.
      *
     C           FELOAN    IFNE ZLOAN
      *
     C**********           Z-ADDFELOAN    ZLOAN                                               CLE148
     C                     MOVELFELOAN    ZLOAN                                               CLE148
     C           LKEY      CHAINCLOAN                10
      *
      ** If loan is not found on LF/CLOAN it will have been dropped
      ** after maturity, fee account officer will remain the same.
      *
     C           *IN10     IFEQ '0'
     C                     MOVE ACOF      UPACOF  2
     C                     ELSE
     C                     MOVE FEACOF    UPACOF
     C                     END
      *
     C                     END
      *
      ** Update PF/LEFEED.
      *
     C                     MOVE UPACOF    FEACOF
     C                     EXCPTUPDFEE
      *
      ** ... else process facility fee.
      *
     C                     ELSE
      *
      ** Access LF/FCLTY for facility account officer on change of
      ** fee facility reference.
      *
     C           FECNUM    IFNE ZCNUM
     C           FEFACL    ORNE ZFACL
      *
     C**********           Z-ADDFECNUM    ZCNUM                                               CSD027
     C                     MOVE FECNUM    ZCNUM                                               CSD027
     C                     Z-ADDFEFACL    ZFACL
     C           FKEY      CHAINFCLTY                20
      *
      ** If facility is not found on LF/FCLTY it will have been
      ** dropped after maturity, fee account officer will remain
      ** the same.
      *
     C           *IN20     IFEQ '0'
     C                     MOVE ACOF      UPACOF
     C                     ELSE
     C                     MOVE FEACOF    UPACOF
     C                     END
      *
     C                     END
      *
      ** Update PF/LEFEED.
      *
     C                     MOVE UPACOF    FEACOF
     C                     EXCPTUPDFEE
     C                     END
      *
      ** Read LF/LEFEEDL1 until EOF.
      *
     C                     READ LEFEEDL1                 01
      *
     C                     END
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception processing.                        *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *  Called By: VARIOUS                                           *
      *                                                               *
      *  Calls:     AUDIT                                             *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
      ** Ensure dump is executed only once.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     SETON                     U7U8
     C                     DUMP
     C                     EXSR TERM
     C                     END
      *
      ** Exit program.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/TERM
      *****************************************************************
      *                                                               *
      *  TERM   - Program termination subroutine.                     *
      *                                                               *
      *  Called By: Main Processing, *PSSR                            *
      *                                                               *
      *  Calls:     Nothing                                           *
      *                                                               *
      *****************************************************************
      *
     C           TERM      BEGSR
      *
      ***  End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *
     C********************************************************************
     C/EJECT
      *
      ** Update of PF/LEFEED.
      *
     OLEFEEDF E                UPDFEE
     O                         FEACOF
      *
**  CPY@
(c) Finastra International Limited 2001
