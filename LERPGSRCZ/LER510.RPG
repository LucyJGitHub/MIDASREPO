     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Facility EOY processing')                     *
     F*****************************************************************
     F*                                                               *
     F*  Midas Customer Lending Module
     F*                                                               *
     F*  LER510 - Facilities - End of Year                            *
     F*                                                               *
     F*  Function:  This program initialises the monthly aggregate    *
     F*  (COB)      balances fields with zeros for Live, Matured      *
     F*             and Archived Facility records and outputs these   *
     F*             values to NPEFCD or PEFC123.                      *
     F*                                                               *
     F*  Called By: LERC510 - End of Year processing - profitability  *
     F*                       extract.                                *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 204452             Date 02May02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CLE009             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 141453             Date 17Feb99               *
     F*                 110779             DATE 21JAN97               *
     F*                 100515             DATE 22MAY96               *
     F*                 069242             DATE 13APR94               *
     F*                 053574             DATE 13DEC93               *
     F*                 MOF60              DATE 16MAR92               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  204452 - Recompiled due to changed PF/LEFEED.                *
      *  CLE009 - LE I/C Swift Message Generation for Fees            *
      *           Recompiled due to changes in LEFEED                 *
      *  141453 - Recompiled due to changes in PF/LEFEED              *
     F*  110779 - Remove redundant end of century calculation, which  *
     F*           will not work after the year 2000 anyway.           *
     F*  100515 - Recompile program due to changes in LEFEED.         *
     F*  069242 - Lotus 123 files are now redundant                   *
     F*  053574 - Recompiled due to changes in LEPEFCZ/PF.            *
     F*  MOF60  - CUSTOMER PROFITABILITY & FEES PROCESSING            *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*****************************************************************
      *
     FLEPEFCD IF  E           K        DISK
      * Profitability Extract Facilities - Detail
      *
     FLEPEFCZ IF  E           K        DISK
      * Profitability Extract Facilities - Audit
      *
     FLEFEED  IF  E           K        DISK
      * Lending Fee Details
      *
     FSDACOFL0IF  E           K        DISK
      *  Account Officer File
      *
     FNPEFCD  O   E                    DISK
     F            LEPEFCDF                          KRENAMENPEFCDF
      * New Profitability Extract - Facilities
      *
     F***PEFC123 O   E                    DISK                            069242
      **Archived*Profitability*Extract*-*Facilities********************   069242
      *
     FNPEFCZ  O   E                    DISK
      * New Profitability Extract Facilities - Audit
      *
     FLER510AUO   E                    PRINTER      KINFDS SPOOL
      * Control Report
      *
      /EJECT
     F*******************************************************************
     F*                                                                 *
     F*  F U N C T I O N   O F   I N D I C A T O R S                    *
     F*                                                                 *
     F*   01  - First cycle indicator                                   *
     F*                                                                 *
     F*   10  - Account Officer present on 'AO' array                   *
     F*                                                                 *
     F*   20  - Record found on LEFEED.                                 *
     F*   21  - End of File LEFEED.                                     *
     F*                                                                 *
     F*   50  - File Controls out of balance.                           *
     F*                                                                 *
     F*   51  - Account Officer array index *EQ '1'                     *
     F*                                                                 *
     F*   53  - End of File SDACOFL0.                                   *
     F*   54  - End of File SDACOFL0.                                   *
     F*                                                                 *
     F*   98  - Date Format indicator.                                  *
     F*                                                                 *
     F*   U7  - Database error                                          *
     F*   U8  - Database error                                          *
     F*                                                                 *
     F*******************************************************************
      /SPACE 3
     F*******************************************************************
     F*                                                                 *
     F*  S U B R O U T I N E   I N D E X                                *
     F*                                                                 *
     F*  B0000  - Initialization subroutine                             *
     F*  C0000  - Unmatured Facility processing                         *
     F*  D0000  - Matured Facility processing                           *
     F*  E0000  - Archived Facility processing                          *
     F*  F0000  - End of Program control                                *
     F*  Z1000  - Obtain department                                     *
     F*  DBERR  - Database error output subroutine                      *
     F*  *PSSR  - Standard error processing subroutine                  *
     F*                                                                 *
     F*******************************************************************
      /EJECT
     E                    AO         50  2               Account Officers
     E                    DC         50  3               Department
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZDATE2Z1
      *
      /EJECT
      *
     I            DS
      *  FACILITY CODE
     I                                        1   50GFACLY
     I                                        1   30FDFTYP
     I                                        4   50FDFSEQ
      *
     ISPOOL       DS
      ** File information data structure
     I                                       83  92 SFILE
     I                                    B 123 1240SFNUM
     I                                    B 367 3680LINE
      *
     IPSDS       SDS
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
     I                                      244 253 JOB
     I                                      254 263 USER
      *
     ILERSTS    E DSLERSTS
     I              FACHISD                         HISDAY
     I              LERC2030                        RENAM1
     I              LERC135                         RENAM2
     I              LERC315                         RENAM3
     I              LERC425                         RENAM4
     I              EOYFLAG                         EOYFLG
      *
     ILDA       E DSLDA
      * DATABASE ERROR DS
      *
     IRUNDAT    E DSRUNDAT
      ** Standard data area layout for system flags & run date.
      *
     ISDGELR    E DSSDGELRPD
      ** GENERAL LEDGER DETAILS
      *
     ISDACOF    E DSSDACOFPD
      ** ACCOUNT OFFICER DETAILS ACCESSED VIA ACCESS PROGRAM
      *
     ISDBANK    E DSSDBANKPD
      ** Bank details
      *
     ISDCUST    E DSSDCUSTPD
      ** CUSTOMER DETAILS
      *
     IDSFDY     E DSDSFDY
      ** First DS for access programs, short data structure
      *
     IDSSDY     E DSDSSDY
      * SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE
      *
     C/EJECT
      *
     C           KEY002    KLIST
     C                     KFLD           BRCA
     C                     KFLD           FDFCUS
     C                     KFLD           GFACLY
      *
     C                     EXSR B0000
      *
     C                     READ LEPEFCD                  02
      *
     C           *IN02     DOWEQ'0'
      *
     C                     ADD  1         GFCREC  50
     C**********           EXSR E0000                                     069242
     C           FDECFG    CASEQ'D'       C0000
     C           FDECFG    CASEQ'E'       C0000
     C           FDECFG    CASEQ'C'       D0000
     C                     END
     C                     READ LEPEFCD                  02
      *
     C                     END
      *
     C                     EXSR F0000
      *
     C                     SETON                     LR
      *
     C                     RETRN
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * B0000  - Initialisation Subroutine                            *
      *                                                               *
      * Called by: Main Processing                                    *
      *                                                               *
      * Calls: *PSSR                                                  *
      *                                                               *
      *****************************************************************
      *
     C           B0000     BEGSR
      *
     C                     SETOF                     02
      *
      ** Set up copyright statement
     C                     MOVEACPY@      BIS@   80
      *
      ** Define LDA
     C           *NAMVAR   DEFN           LDA
      *
      ** Define & read external data areas used in program.
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C*                                                                    0112
     C**   CHECK SYSTEM DATE FORMAT, DDMMYY OR MMDDYY.                     0112
     C           AGDFF     IFEQ 'M'                                        0112
     C                     SETON                         98MMDDYY, 98 ON   0112
     C                     END                                             0112
     C*                                                                    0112
      *
     C           *NAMVAR   DEFN           LERSTS
     C                     IN   LERSTS
      *
      ** Access bank details
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *                                                    **************
     C           PRTCD     IFNE *BLANKS                    * DBERROR 01 *
     C           *LOCK     IN   LDA                        **************
     C                     Z-ADD1         DBASE
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVE ' LER510 'DBPGM
     C                     MOVEL'BANK    'DBKEY
     C                     OUT  LDA
     C                     EXSR DBERR
     C                     EXSR *PSSR
     C                     END
      *
      ** Read General Ledger Details for Accrual Profit Date
      ** Access General Ledger details
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*FIRST ' POPTN
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA                        ***************
     C                     MOVEL'SDGELRPD'DBFILE           * DBERROR 02  *
     C                     MOVEL' LER510 'DBPGM            ***************
     C                     Z-ADD2         DBASE
     C                     OUT  LDA
     C                     EXSR DBERR
     C                     EXSR *PSSR
     C                     END
      *
     C                     MOVE BJMRDT    YY      20
      *
      ** ADD THE END OF YEAR DAYNO FROM PF/SDBANK TO THE INPUT FIELD
     C** ZDAYNO(MIDAS DAY NUMBER , AND CALL THE PROGRAM TO
     C** CONVERT THIS INTO A DDMMYY/MMDDYY  FORMAT WHICH IS RETURNED TO
     C** THIS PROGRAM AS ZDATE (DAY/MONTH YEAR).
     C*
     C**  IF THIS IS A FORCED END OF YEAR RUN THEN USE ACCRUAL PROFIT
     C**  DATE AS (FORCED) END OF YEAR DATE.
     C**  THIS IS BECAUSE SD0640 PUTS THE END OF MONTH DATE INTO THE
     C**  ACCRUAL PROFIT DATE IF THE END OF MONTH IS ON OR BEFORE NEXT
     C**  WORKING DAY, WHICH IT WILL BE BECAUSE THE FORCED END OF YEAR
     C**  MUST COINCIDE WITH AN END OF MONTH.
     C*
     C           EOYFLG    IFNE 'Y'
     C                     Z-ADDBJEYD     GEOYDY  50
     C                     ELSE
     C                     Z-ADDBKAPDT    GEOYDY  50
     C                     END
     C                     Z-ADDBJEYD     ZDAYNO  50
     C                     EXSR ZDATE2
     C*********************Z-ADDYY        PFCCYY                          069242
     C*
     C** THIS DATE IS THEN IN THE CORRECT FORMAT TO MOVE 99
     C** TO THE YEAR(YY) VALUE OF ZDATE AND PRODUCE THE END OF
     C** CENTURY DATE. THEN THIS DDMMYY FORMAT IS CHANGED BACK INTO
     C** A MIDAS RUNDAY NUMBER.
     C*********************                                                 0112
     C*****CHECK*SYSTEM*DATE FORMAT, DDMMYY OR MMDDYY.                      0112
     C***********AGDFF*****IFEQ 'M'                                         0112
     C*********************SETON                         98MMDDYY, 98 ON    0112
     C*********************END                                              0112
     C*
     C*********************MOVE '99'      ZDAT2A  2                       110779
     C*********************MOVE ZDAT2A    ZDATE   60                      110779
     C*********************EXSR ZDATE1                                    110779
     C************IN99*****IFEQ '1'                                       110779
     C************LOCK*****IN   LDA                                       110779
     C*********************Z-ADD3         DBASE            ************** 110779
     C*********************MOVEL'ZDATE1'  DBKEY            * DBERROR 03 * 110779
     C*********************MOVEL'LER510'  DBPGM            ************** 110779
     C*********************OUT  LDA                                       110779
     C*********************EXSR DBERR                                     110779
     C*********************EXSR *PSSR                                     110779
     C*********************END                                            110779
      *                                                                   110779
      ****ALL*THAT*REMAINS*IS*TO*CHECK*TO*SEE*IF*THE*RUNDATE***********   069242
      ****IS*GREATER*THAN*THE*END*OF*CENTURY*DATE*AND*IF*IT*IS*********   069242
      ****THEN*ADD*100*TO*THE*ORIGINAL*FIELDS...***********************   069242
     C***********BJRDNB    IFGT ZDAYNO                                    069242
     C***********          ADD  100       PFCCYY                          069242
     C***********          END                                            069242
     C*
     C****SIMILARLY*THE*START*OF*YEAR*DAY*NUMBER*(END*OF*YEAR*DAY-*****   069242
      *                                                                   069242
     C**  THE START OF YEAR DAY NUMBER (END OF YEAR DAY-                  069242
     C**  NUMBER + 1) SHOULD BE CONVERTED TO A DATE (DDMMYY) FORMAT
     C**  AND STORED TO USE IN UPDATES AT A LATER STAGE
     C*
     C**  IF THIS IS A FORCED END OF YEAR RUN THEN USE ACCRUAL PROFIT
     C**  DATE AS (FORCED) END OF YEAR DATE.
     C**  THIS IS BECAUSE SD0640 PUTS THE END OF MONTH DATE INTO THE
     C**  ACCRUAL PROFIT DATE IF THE END OF MONTH IS ON OR BEFORE NEXT
     C**  WORKING DAY, WHICH IT WILL BE BECAUSE THE FORCED END OF YEAR
     C**  MUST COINCIDE WITH AN END OF MONTH.
     C*
     C                     Z-ADD0         MIDNUM  50
     C           EOYFLG    IFNE 'Y'
     C           BJEYD     ADD  1         MIDNUM
     C                     ELSE
     C           BKAPDT    ADD  1         MIDNUM
     C                     END
     C                     Z-ADDMIDNUM    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZDATE     DSTORE  20
      *
     C                     Z-ADD50        X       20
     C                     READ SDACOFL0                 53
      *
     C           *IN53     DOWEQ'0'
     C                     MOVE AZACOC    AO,X
     C                     MOVE AZDPCD    DC,X
     C                     SUB  1         X              51
     C                     READ SDACOFL0                 53
     C  N51                END
      *
     C           X         IFLT 50
     C                     ADD  1         X
     C                     END
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * C0000  - Unmatured Records Subroutine                         *
      *                                                               *
      * Called by: Main Processing                                    *
      *                                                               *
      * Calls: *PSSR                                                  *
      *                                                               *
      *****************************************************************
      *
     C           C0000     BEGSR
     C                     Z-ADD0         FDJANA
     C                     Z-ADD0         FDFEBA
     C                     Z-ADD0         FDMARA
     C                     Z-ADD0         FDAPRA
     C                     Z-ADD0         FDMAYA
     C                     Z-ADD0         FDJUNA
     C                     Z-ADD0         FDJULA
     C                     Z-ADD0         FDAUGA
     C                     Z-ADD0         FDSEPA
     C                     Z-ADD0         FDOCTA
     C                     Z-ADD0         FDNOVA
     C                     Z-ADD0         FDDECA
     C*
     C** IF NOT EXPIRED PRIOR TO THE END OF THE LAST YEAR, RESET THE
     C** FACILITY START DATE TO THE FIRST OF JAN FOR THE NEW YEAR.
     C*
     C** FCLTY END DAY NO LONGER COMPARED WITH END OF YEAR DATE BECAUSE
     C** THE CL ALREADY PERFORMS SUFFICIENT CHECKING AND IN SOME INSTANCES
     C** THE ORIGINAL CONDITION CAUSES PROBLEMS WITH THE NEW START DATE
     C*
     C*
     C           GEOYDY    ADD  1         FDFSDY
     C*
     C**  ADD THE FACILITY START DAY NUMBER TO THE WORKFIELD
     C**  AND CONVERT THIS USING ZDATE2
     C*
     C                     Z-ADDFDFSDY    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     FDFSDT
     C*
     C                     WRITENPEFCDF
     C                     ADD  1         NFCREC
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * D0000  - Matured records subroutine                           *
      *                                                               *
      * Called by: Main Processing                                    *
      *                                                               *
      * Calls: *PSSR                                                  *
      *                                                               *
      *****************************************************************
      *
     C           D0000     BEGSR
      *
     C           KEY002    SETLLLEFEED                   20
     C           *IN20     IFEQ '1'
      *
     C           KEY002    READELEFEED                   21
     C           FERECI    IFEQ 'D'
     C                     SETON                     21
     C                     END
      *
     C           *IN21     DOWEQ'0'
     C           KEY002    READELEFEED                   21
     C           FERECI    IFEQ 'D'
     C                     SETON                     21
     C                     END
     C                     END
      *
     C                     Z-ADD0         FDJANA
     C                     Z-ADD0         FDFEBA
     C                     Z-ADD0         FDMARA
     C                     Z-ADD0         FDAPRA
     C                     Z-ADD0         FDMAYA
     C                     Z-ADD0         FDJUNA
     C                     Z-ADD0         FDJULA
     C                     Z-ADD0         FDAUGA
     C                     Z-ADD0         FDSEPA
     C                     Z-ADD0         FDOCTA
     C                     Z-ADD0         FDNOVA
     C                     Z-ADD0         FDDECA
     C*
     C** INITIALISE START DATE WITH START OF YEAR DAY OF MONTH RATHER
     C** THAN ONE
     C                     Z-ADDDSTORE    FDSTDY
     C                     Z-ADDDSTORE    FDENDY
     C*
     C           GEOYDY    ADD  1         FDFSDY
     C           GEOYDY    ADD  1         FDENDT
     C           GEOYDY    ADD  1         FDFEDY
     C*
     C**  ADD THE FACILITY START DAY NUMBER TO THE WORKFIELD
     C**  AND CONVERT THIS USING ZDATE2
     C*
     C                     Z-ADDFDFSDY    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     FDFSDT
     C                     MOVE ZDATE     FDFEDT
     C*
     C                     WRITENPEFCDF
     C                     ADD  1         NFCREC
      *
     C                     END
      *
     C                     ENDSR
      /EJECT
      *****************************************************************   069242
      *****************************************************************   069242
      **E0000**-*Archived*records*subroutine***************************   069242
      *****************************************************************   069242
      **Called*by:*Main*Processing*************************************   069242
      *****************************************************************   069242
      **Calls:**PSSR***************************************************   069242
      *****************************************************************   069242
      *****************************************************************   069242
      *
     C***********E0000     BEGSR                                          069242
     C***********          MOVE FDFCUS    PFCCUS                          069242
     C***********          MOVE FDFTYP    PFCTYP                          069242
     C***********          MOVE FDFSEQ    PFCSEQ                          069242
     C***********          MOVE FDFCUS    BBCUST                          069242
     C***********          EXSR Z1000                                     069242
     C***********          MOVE AO,Y      PFCCOF                          069242
     C***********          MOVE DC,Y      PFCEPT                          069242
     C***********          MOVE FDFSDT    PFCSDT                          069242
     C***********          MOVE FDFEDT    PFCEDT                          069242
     C***********          MOVE FDDADT    PFCADT                          069242
     C***********          Z-ADDFDJANA    PFCANA                          069242
     C***********          Z-ADDFDFEBA    PFCEBA                          069242
     C***********          Z-ADDFDMARA    PFCARA                          069242
     C***********          Z-ADDFDAPRA    PFCPRA                          069242
     C***********          Z-ADDFDMAYA    PFCAYA                          069242
     C***********          Z-ADDFDJUNA    PFCUNA                          069242
     C***********          Z-ADDFDJULA    PFCULA                          069242
     C***********          Z-ADDFDAUGA    PFCUGA                          069242
     C***********          Z-ADDFDSEPA    PFCEPA                          069242
     C***********          Z-ADDFDOCTA    PFCCTA                          069242
     C***********          Z-ADDFDNOVA    PFCOVA                          069242
     C***********          Z-ADDFDDECA    PFCECA                          069242
      *
     C***********          WRITEPEFC123F                                  069242
     C***********          ADD  1         CADREC  50                      069242
     C***********EEXIT     ENDSR                                          069242
      /EJECT
      *****************************************************************
      *                                                               *
      * F0000  - End of program control                               *
      *                                                               *
      * Called by: Main Processing                                    *
      *                                                               *
      * Calls: *PSSR                                                  *
      *                                                               *
      *****************************************************************
     C*
     C           F0000     BEGSR
     C                     READ LEPEFCZ                  54
     C           GFCREC    IFNE FZNREC
     C                     SETON                         50
     C                     END
      *
     C           *INU7     IFEQ '0'
     C           *INU8     ANDEQ'0'
     C                     WRITELER510P
     C                     WRITELER510D
     C                     WRITELER510T
     C                     END
      *
     C                     MOVEA*IN,50    U7      1
     C           U7        IFNE '0'
     C                     SETON                         U7
     C                     END
      *
     C           *INU7     IFEQ '0'
     C                     WRITENPEFCZF
     C                     END
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * Z1000  - Obtain Department                                    *
      *                                                               *
      * Called by: E1000                                              *
      *                                                               *
      * Calls: *PSSR                                                  *
      *                                                               *
      *****************************************************************
      *
     C           Z1000     BEGSR
      *
     C           BBCUST    IFNE GCUS
      *
     C                     MOVE BBCUST    GCUS    6
     C                     CALL 'AOCUSTR0'
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY    'POPTN
     C                     PARM GCUS      KEYC   10
     C                     PARM           KEYSTS  7
     C           SDCUST    PARM SDCUST    DSSDY
     C*
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDCUSTPD'DBFILE           **************
     C                     MOVELGCUS      DBKEY            * DBERROR 04 *
     C                     MOVEL' LER510 'DBPGM            ***************
     C                     Z-ADD4         DBASE
     C                     OUT  LDA
     C                     EXSR DBERR
     C                     EXSR *PSSR
     C                     END
      *
     C                     Z-ADDX         Y       20
     C           BBACOC    LOKUPAO,Y                     10
      *
     C           *IN10     IFNE '1'
     C                     Z-ADD50        Y
      *
     C                     CALL 'AOACOFR0'
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM BBACOC    ACOKEY  2
     C           SDACOF    PARM SDACOF    DSFDY
      *
     C                     MOVE BBACOC    AO,50
      *
     C           PRTCD     IFEQ *BLANKS
     C                     MOVE AZDPCD    DC,50
     C                     ELSE
     C                     MOVE '***'     DC,50
     C                     END
     C                     END
     C                     END
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * DBERR  - Obtain Department                                    *
      *                                                               *
      * Called by: B0000, Z1000                                       *
      *                                                               *
      * Calls: *PSSR                                                  *
      *                                                               *
      *****************************************************************
      *
     C           DBERR     BEGSR
      *
     C                     WRITELER510P
     C                     WRITEDBFMT
     C                     WRITELER510T
     C                     ENDSR
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*    * P S S R -  T O   H A N D L E   A B N O R M A L I T I E S *
     C*    ~~~~~~~~~    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ *
     C*    CALLED FROM     :   ALL ABNORMAL CONDITIONS / ERRORS       *
     C*    CALLS           :   AUDIT                                  *
     C*                                                               *
     C*    THIS PROGRAM HANDLES ABNORMAL CONDITIONS SUCH AS READ FAIL *
     C*    CHAIN FAIL ETC, AND PRODUCES A FORMATTED RPG DUMP          *
     C*                                                               *
     C*****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     SETON                     U7U8LR
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     RETRN
      *
     C           PSSR      ENDSR
      *
     C********************************************************************
     C/EJECT
     C/TITLE SR/ZDATE1
     C/COPY ZSRSRC,ZDATE1Z2
     C/TITLE SR/ZDATE2
     C/COPY ZSRSRC,ZDATE2Z2
     C********************************************************************
** CPY@   **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
