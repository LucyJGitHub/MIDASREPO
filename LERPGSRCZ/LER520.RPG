     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas LE Fees and margin EOY processing')              *
     F*****************************************************************
     F*                                                               *
     F*  Midas Customer Lending Module
     F*                                                               *
     F*  LER520 - Loan/Margin - End of Year                           *
     F*                                                               *
     F*  Function:  This program initialises the monthly aggregate    *
     F*             balances fields with zeros for Live, Matured      *
     F*             and Archived Loan records and outputs these       *
     F*             values to NPELND, NPEMGD or PELN123.              *
     F*                                                               *
     F*  Called By: LERC520 - End of Year processing - profitability  *
     F*                       extract for Loan Margins.               *
     F*                                                               *
     F*  (COB)
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      *  Last Amend No. MD024222           Date 05May14               *
      *  Prev Amend No. CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      *                 CAS006             Date 21Jan03               *
      *                 CAS005             Date 16Dec02               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 204452             Date 02May02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CLE011             Date 07Nov00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CLE009             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 141453             Date 17Feb99               *
     F*                 125441             DATE 22MAY98               *
     F*                 110779             DATE 21JAN97               *
     F*                 100515             DATE 22MAY96               *
     F*                 069242             DATE 13APR94               *
     F*                 MOF60              DATE 16MAR92               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD024222 - To prevent FOOB in LERC520, write record of a     *
      *             matured loan to PF/NPEMGD with same validation    *
      *             when writing in PF/NPELND in SR/D0000.            *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CAS005 - Enahancements to CAS001, CAS002 and CAS004          *
      *           (Recompile)                                         *
      *  204452 - Recompiled due to changed PF/LEFEED.                *
      *  CLE011 - Rate Fixing Days for Lending Rollover Processing    *
      *           Recompile                                           *
      *  CLE009 - LE I/C Swift Message Generation for Fees            *
      *           Recompiled due to changes in LEFEED                 *
      *  141453 - Recompiled due to changes in PF/LEFEED              *
     F*  125441 - Database error in LER520, audit control out of      *
     F*           balance. Applied fix 112686 - introduce a new       *
     F*           indicator for LOKUP of account officer to avoid     *
     F*           confusion with missing record in LEPEMGD.           *
     F*  110779 - Remove redundant end of century calculation, which  *
     F*           will not work after the year 2000 anyway.           *
     F*  100515 - Recompile program due to changes in LEFEED.         *
     F*  069242 - Lotus 123 files are now redundant                   *
     F*  MOF60  - CUSTOMER PROFITABILITY & FEES PROCESSING            *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*****************************************************************
      *
     FLEPELND IF  E           K        DISK
      * Profitability Extract Loans - Detail
      *
     FLEPEMGD IF  E           K        DISK
      * Profitability Extract Margin - Detail
      *
     FCLOAN   IF  E           K        DISK
      * Loan Master File Detail Records
     F            CLOANHHF                          KIGNORE
     F            CLOANCKF                          KIGNORE
     F            CLOANZ1F                          KIGNORE
      *
     FLEPELNZ IF  E           K        DISK
      * Proftability Extract Loans - Audit
      *
     FLEPEMGZ IF  E                    DISK
      * Profitability Extract Margin - Audit
      *
     FLEFEED  IF  E           K        DISK
      * Lending Fee Details
      *
     FSDACOFL0IF  E           K        DISK
      *  Account Officer File
      *
     FNPELND  O   E                    DISK
      * New Profitability Extract - Loans
     F            LEPELNDF                          KRENAMENPELNDF
      *
     FNPEMGD  O   E                    DISK
      * New Profitability Extract - Margin
     F            LEPEMGDF                          KRENAMENPEMGDF
      *
     F**PELN123 O   E                    DISK                             069242
      **Archived*Profitability*Extract*-*Loans*************************   069242
      *
     FNPELNZ  O   E                    DISK
      * New Profitability Extract Loans - Audit
      *
     FNPEMGZ  O   E                    DISK
      * New Profitability Extract Margin - Audit
      *
     FLER520AUO   E                    PRINTER      KINFDS SPOOL
      * Control Report
      *
      /EJECT
     F*******************************************************************
     F*                                                                 *
     F*  F U N C T I O N   O F   I N D I C A T O R S                    *
     F*                                                                 *
     F*   02  - End of file LEPELND.                                    *
     F*   10  - CHAIN fail on LEPEMGD file.                             *
     F*   11  - CHAIN fail on CLOAN file.                               *
     F*   12  - Account officer code found in AO table.                 * 125441
     F*   20  - Record found on LEFEED equalling KEY002                 *
     F*   21  - End of File LEFEED.                                     *
     F*                                                                 *
     F*   50  - File controls out of balance LEPEMGZ.                   *
     F*   51  - File controls out of balance LEPELMZ.                   *
     F*                                                                 *
     F*   88  - Array index of Account Officer array equals '1'         *
     F*                                                                 *
     F*   89  - End of file SDACOFL0, LEPELNZ, LEPEMGZ.                 *
     F*                                                                 *
     F*   U7  - Database error                                          *
     F*   U8  - Database error                                          *
     F*                                                                 *
     F*******************************************************************
      /SPACE 3
     F*******************************************************************
     F*                                                                 *
     F*  S U B R O U T I N E   I N D E X                                *
     F*                                                                 *
     F*  B0000  - Initialization subroutine                             *
     F*  C0000  - Unmatured Facility processing                         *
     F*  D0000  - Matured Facility processing                           *
     F*  E0000  - Archived Facility processing                          *
     F*  F0000  - End of Program control                                *
     F*  Z1000  - Obtain department                                     *
     F*  DBERR  - Database error output subroutine                      *
     F*  *PSSR  - Standard error processing subroutine                  *
     F*                                                                 *
     F*******************************************************************
      /EJECT
     E                    AO         50  2               Account Officers
     E                    DC         50  3               Department
     E                    CPY@    1   1 80
     E*
     E**  CPY@ Array holds copyright statement for object
     E/COPY ZSRSRC,ZDATE2Z1
      *
      /EJECT
      *
     IPSDS       SDS
      ** Data Area giving Installation Control Details
      *
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
     I                                      244 253 JOB
     I                                      254 263 USER
      *
     ISPOOL       DS
      ** File information data structure
     I                                       83  92 SFILE
     I                                    B 123 1240SFNUM
     I                                    B 367 3680LINE
      *
     ILERSTS    E DSLERSTS
     I              FACHISD                         HISDAY
     I              LERC2030                        RENAM1
     I              LERC135                         RENAM2
     I              LERC315                         RENAM3
     I              LERC425                         RENAM4
     I              EOYFLAG                         EOYFLG
      *
     IRUNDAT    E DSRUNDAT
      ** Standard data area layout for system flags & run date.
      *
     ILDA       E DSLDA
      * DATABASE ERROR DS
      *
     ISDGELR    E DSSDGELRPD
      ** GENERAL LEDGER DETAILS
      *
     ISDBANK    E DSSDBANKPD
      ** Bank details
      *
     ISDACOF    E DSSDACOFPD
      ** ACCOUNT OFFICER DETAILS
      *
     ISDCUST    E DSSDCUSTPD
      ** CUSTOMER DETAILS
      *
     IDSFDY     E DSDSFDY
      ** First DS for access programs, short data structure
      *
     IDSSDY     E DSDSSDY
      * SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE
      *
     C/EJECT
      *
     C           KEY001    KLIST
     C                     KFLD           BRCA
     C                     KFLD           LDCNUM
     C                     KFLD           LDLNRF
      *
     C           KEY002    KLIST
     C                     KFLD           BRCA
     C                     KFLD           LDCNUM
     C                     KFLD           GDUM5   50
     C                     KFLD           LDLNRF
      *
     C                     EXSR B0000
      *
     C                     READ LEPELND                  02
      *
     C           *IN02     DOWEQ'0'
      *
     C                     ADD  1         GLNREC  50
     C           KEY001    CHAINLEPEMGD              10
      *
     C           *IN10     IFEQ '0'
     C                     ADD  1         GMGREC  50
     C                     END
      *
     C           LDLNRF    CHAINCLOAN                11
     C***********          EXSR E0000                                     069242
     C           *IN11     CASEQ'1'       D0000
     C           MDAT      CASEQ0         C0000
     C           MDAT      CASGTGEOYDY    C0000
     C                     CAS            D0000
     C                     END
      *
     C                     READ LEPELND                  02
     C                     END
      *
     C                     EXSR F0000
      *
     C                     SETON                     LR
      *
     C                     RETRN
      *
      /EJECT
      *****************************************************************
      *                                                               *
      * B0000  - Initialisation Subroutine                            *
      *                                                               *
      * Called by: Main Processing                                    *
      *                                                               *
      * Calls: *PSSR                                                  *
      *                                                               *
      *****************************************************************
      *
     C           B0000     BEGSR
      *
      ** Set up copyright statement
     C                     MOVEACPY@      BIS@   80
      *
      ** Define LDA
     C           *NAMVAR   DEFN           LDA
      *
      ** Define & read external data areas used in program.
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C*                                                                    0112
     C**   CHECK SYSTEM DATE FORMAT, DDMMYY OR MMDDYY.                     0112
     C           AGDFF     IFEQ 'M'                                        0112
     C                     SETON                         98MMDDYY, 98 ON   0112
     C                     END                                             0112
     C*                                                                    0112
      *
     C           *NAMVAR   DEFN           LERSTS
     C                     IN   LERSTS
      *
     C                     SETOF                     02
      *
      ** Access bank details
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *                                                    **************
     C           PRTCD     IFNE *BLANKS                    * DBERROR 01 *
     C           *LOCK     IN   LDA                        **************
     C                     Z-ADD1         DBASE
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVEL'LER520'  DBPGM
     C                     MOVEL'BANK    'DBKEY
     C                     OUT  LDA
     C                     EXSR DBERR
     C                     EXSR *PSSR
     C                     END
      *
      ** Access General Ledger Details for Accrual Profit Date
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*FIRST ' POPTN
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *
     C           PRTCD     IFNE *BLANK
     C           *LOCK     IN   LDA                        ***************
     C                     MOVEL'SDGELRPD'DBFILE           * DBERROR 02  *
     C                     MOVEL'LER520'  DBPGM            ***************
     C                     Z-ADD2         DBASE
     C                     OUT  LDA
     C                     EXSR DBERR
     C                     EXSR *PSSR
     C                     END
      *
      **  IF THIS IS A FORCED END OF YEAR RUN THEN USE ACCRUAL PROFIT
      **  DATE AS (FORCED) END OF YEAR DATE.
      **  THIS IS BECAUSE SD0640 PUTS THE END OF MONTH DATE INTO THE
      **  ACCRUAL PROFIT DATE IF THE END OF MONTH IS ON OR BEFORE NEXT
      **  WORKING DAY, WHICH IT WILL BE BECAUSE THE FORCED END OF YEAR
      **  MUST COINCIDE WITH AN END OF MONTH.
      *
     C           EOYFLG    IFNE 'Y'
     C                     MOVE BJEYD     GEOYDY  50
     C                     ELSE
     C                     MOVE BKAPDT    GEOYDY  50
     C                     END
     C                     MOVE BJMRDT    YY      20
     C**
     C** CONVERT END OF YEAR DAY (BJEYD) PLUS ONE (IE START OF YEAR
     C** DATE) TO DATE FORMAT FOR INITIALISATION OF MATURED LOAN
     C** RECORDS
     C**
     C**  IF THIS IS A FORCED END OF YEAR RUN THEN USE ACCRUAL PROFIT
     C**  DATE AS (FORCED) END OF YEAR DATE.
     C**  THIS IS BECAUSE SD0640 PUTS THE END OF MONTH DATE INTO THE
     C**  ACCRUAL PROFIT DATE IF THE END OF MONTH IS ON OR BEFORE NEXT
     C**  WORKING DAY, WHICH IT WILL BE BECAUSE THE FORCED END OF YEAR
     C**  MUST COINCIDE WITH AN END OF MONTH.
     C*
     C           EOYFLG    IFNE 'Y'
     C                     Z-ADDBJEYD     ZDAYNO
     C                     ELSE
     C                     Z-ADDBKAPDT    ZDAYNO
     C                     END
     C                     ADD  1         ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    DSTORE  20
     C*
     C** CONVERT END OF YEAR DAY BJEYD TO END OF CENTURY DATE BY
     C** CONVERTING FIRST TO END OF YEAR AND THEN MOVING 99 INTO
     C** THE YEAR FIELD
     C*********************                                                 0112
     C*****CHECK*SYSTEM*DATE FORMAT, DDMMYY OR MMDDYY.                      0112
     C***********AGDFF*****IFEQ 'M'                                         0112
     C*********************SETON                         98MMDDYY, 98 ON    0112
     C*********************END                                              0112
     C*
     C                     Z-ADDBJEYD     ZDAYNO
     C                     EXSR ZDATE2
     C*********************MOVE '99'      ZDATE                           110779
     C*********************EXSR ZDATE1                                    110779
     C*********************                                               110779
     C************IN99*****IFEQ '1'                                       110779
     C************LOCK*****IN   LDA                                       110779
     C*********************Z-ADD5         DBASE            ************** 110779
     C*********************MOVEL'ZDATE1'  DBFILE           * DBERROR 05 * 110779
     C*********************MOVELZDAYNO    DBKEY            ************** 110779
     C*********************MOVEL'LER520  'DBPGM                           110779
     C*********************OUT  LDA                                       110779
     C*********************END                                            110779
     C*
     C***********          Z-ADDYY        PLNCYY                          069242
      *
     C***********BJRDNB    IFGT ZDAYNO                                    069242
     C***********          ADD  100       PLNCYY                          069242
     C***********          END                                            069242
      *
     C                     Z-ADD50        X       20
      *
     C                     READ SDACOFL0                 89
      *
     C           *IN89     DOWEQ'0'
     C                     MOVE AZACOC    AO,X
     C                     MOVE AZDPCD    DC,X
     C                     SUB  1         X              88
     C                     READ SDACOFL0                 89
     C  N88                END
      *
     C           X         IFLT 50
     C                     ADD  1         X
     C                     END
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * C0000  - Unmatured Records Subroutine                         *
      *                                                               *
      * Called by: Main Processing                                    *
      *                                                               *
      * Calls: *PSSR                                                  *
      *                                                               *
      *****************************************************************
      *
     C           C0000     BEGSR
     C                     Z-ADD0         LDJANA
     C                     Z-ADD0         LDFEBA
     C                     Z-ADD0         LDMARA
     C                     Z-ADD0         LDAPRA
     C                     Z-ADD0         LDMAYA
     C                     Z-ADD0         LDJUNA
     C                     Z-ADD0         LDJULA
     C                     Z-ADD0         LDAUGA
     C                     Z-ADD0         LDSEPA
     C                     Z-ADD0         LDOCTA
     C                     Z-ADD0         LDNOVA
     C                     Z-ADD0         LDDECA
     C*
     C** IF NOT MATURED PRIOR TO THE END OF THE LAST YEAR, RESET THE
     C** LOAN START DATE TO THE FIRST OF JAN FOR THE NEW YEAR.
     C*
     C** THE END OF YEAR DATE SHOULD NOT NECESSARILY BE
     C** 31ST DECEMBER THERFORE PROCESSING HAS BEEN AMENDED ACCORDINGLY
     C**
     C** LOAN END DAY NO LONGER COMPARED WITH END OF YEAR DATE BECAUSE
     C** THE CL ALREADY PERFORMS SUFFICIENT CHECKING AND IN SOME INSTANCES
     C** THE ORIGINAL CONDITION CAUSES PROBLEMS WITH THE NEW START DATE
     C**
      *
     C           GEOYDY    ADD  1         LDLSDY
     C*
     C**  ADD THE LOAN START DAY NUMBER TO THE WORKFIELD
     C**  AND CONVERT THIS
     C*
     C                     Z-ADDLDLSDY    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     LDLSDT
     C*
     C                     WRITENPELNDF
     C                     ADD  1         NLNREC
     C           *IN10     IFEQ '0'
     C                     Z-ADD0         MDJANA
     C                     Z-ADD0         MDFEBA
     C                     Z-ADD0         MDMARA
     C                     Z-ADD0         MDAPRA
     C                     Z-ADD0         MDMAYA
     C                     Z-ADD0         MDJUNA
     C                     Z-ADD0         MDJULA
     C                     Z-ADD0         MDAUGA
     C                     Z-ADD0         MDSEPA
     C                     Z-ADD0         MDOCTA
     C                     Z-ADD0         MDNOVA
     C                     Z-ADD0         MDDECA
     C                     WRITENPEMGDF
     C                     ADD  1         NMGREC
     C                     END
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * D0000  - Matured records subroutine                           *
      *                                                               *
      * Called by: Main Processing                                    *
      *                                                               *
      * Calls: *PSSR                                                  *
      *                                                               *
      *****************************************************************
      *
     C           D0000     BEGSR
      *
     C           KEY002    SETLLLEFEED                   20
     C           *IN20     IFEQ '1'
      *
     C           KEY002    READELEFEED                   21
     C           FERECI    IFEQ 'D'
     C                     SETON                     21
     C                     END
      *
     C           *IN21     DOWEQ'0'
     C           KEY002    READELEFEED                   21
     C           FERECI    IFEQ 'D'
     C                     SETON                     21
     C                     END
     C                     END
      *
     C                     Z-ADD0         LDJANA
     C                     Z-ADD0         LDFEBA
     C                     Z-ADD0         LDMARA
     C                     Z-ADD0         LDAPRA
     C                     Z-ADD0         LDMAYA
     C                     Z-ADD0         LDJUNA
     C                     Z-ADD0         LDJULA
     C                     Z-ADD0         LDAUGA
     C                     Z-ADD0         LDSEPA
     C                     Z-ADD0         LDOCTA
     C                     Z-ADD0         LDNOVA
     C                     Z-ADD0         LDDECA
     C*
     C** INITIALISE START DATE WITH START OF YEAR DAY OF MONTH RATHER
     C** THAN ONE
     C                     Z-ADDDSTORE    LDSTDY
     C                     Z-ADDDSTORE    LDENDY
      *
     C**
     C** THE END OF YEAR DATE SHOULD NOT NECESSARILY BE
     C** 31ST DECEMBER THERFORE PROCESSING HAS BEEN AMENDED ACCORDINGLY
     C           GEOYDY    ADD  1         LDENDT
     C           GEOYDY    ADD  1         LDLSDY
     C           GEOYDY    ADD  1         LDLEDY
     C*
     C**  ADD THE LOAN START DAY NUMBER TO THE WORKFIELD
     C**  AND CONVERT THIS
     C                     Z-ADDLDLSDY    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     LDLSDT
     C                     MOVE ZDATE     LDLEDT
     C*
     C                     WRITENPELNDF
     C                     ADD  1         NLNREC
      *                                                                                     MD024222
      ** Also write record in NPEMGD                                                        MD024222
      *                                                                                     MD024222
     C           *IN10     IFEQ '0'                                                         MD024222
     C                     Z-ADD0         MDJANA                                            MD024222
     C                     Z-ADD0         MDFEBA                                            MD024222
     C                     Z-ADD0         MDMARA                                            MD024222
     C                     Z-ADD0         MDAPRA                                            MD024222
     C                     Z-ADD0         MDMAYA                                            MD024222
     C                     Z-ADD0         MDJUNA                                            MD024222
     C                     Z-ADD0         MDJULA                                            MD024222
     C                     Z-ADD0         MDAUGA                                            MD024222
     C                     Z-ADD0         MDSEPA                                            MD024222
     C                     Z-ADD0         MDOCTA                                            MD024222
     C                     Z-ADD0         MDNOVA                                            MD024222
     C                     Z-ADD0         MDDECA                                            MD024222
      *                                                                                     MD024222
     C                     WRITENPEMGDF                                                     MD024222
     C                     ADD  1         NMGREC                                            MD024222
     C                     END                                                              MD024222
     C*
     C                     END
     C*
     C                     ENDSR
      /EJECT
      *****************************************************************   069242
      *****************************************************************   069242
      **E0000**-*Archived*records*subroutine***************************   069242
      *****************************************************************   069242
      **Called*by:*Main*Processing*************************************   069242
      *****************************************************************   069242
      **Calls:**PSSR***************************************************   069242
      *****************************************************************   069242
      *****************************************************************   069242
      *
     C***********E0000     BEGSR                                          069242
     C***********          MOVE LDCNUM    PLNNUM                          069242
     C***********          MOVE LDFCUS    PLNCUS                          069242
     C***********          MOVE LDFTYP    PLNFTP                          069242
     C***********          MOVE LDSUTP    PLNSTP                          069242
     C***********          MOVE LDGPID    PLNPID                          069242
     C***********          MOVE LDGPNA    PLNPNA                          069242
     C***********          MOVE LDCNUM    BBCUST                          069242
     C***********          EXSR Z1000                                     069242
     C***********          MOVE AO,Y      PLNCOF                          069242
     C***********          MOVE DC,Y      PLNEPT                          069242
     C***********          MOVE LDMPFG    PLNPFG                          069242
     C***********          MOVE LDLEDT    PLNEDT                          069242
     C***********          MOVE LDVADT    PLNADT                          069242
     C***********          Z-ADDLDJANA    PLNANA                          069242
     C***********          Z-ADDLDFEBA    PLNEBA                          069242
     C***********          Z-ADDLDMARA    PLNARA                          069242
     C***********          Z-ADDLDAPRA    PLNPRA                          069242
     C***********          Z-ADDLDMAYA    PLNAYA                          069242
     C***********          Z-ADDLDJUNA    PLNUNA                          069242
     C***********          Z-ADDLDJULA    PLNULA                          069242
     C***********          Z-ADDLDAUGA    PLNUGA                          069242
     C***********          Z-ADDLDSEPA    PLNEPA                          069242
     C***********          Z-ADDLDOCTA    PLNCTA                          069242
     C***********          Z-ADDLDNOVA    PLNOVA                          069242
     C***********          Z-ADDLDDECA    PLNECA                          069242
     C*
     C****ADDITION*OF*UPDATE*TO*SEVERAL*ARCHIVE*FIELDS*NOT*************   069242
     C****ALREADY*INCLUDED*(FOUND*TO*BE*BLANK*AFTER*RUN)***************   069242
     C***********          Z-ADDLDFSEQ    PLNSEQ                          069242
     C***********          Z-ADDLDLNRF    PLNNRF                          069242
     C***********          MOVE LDLTYP    PLNLTP                          069242
      *
     C************IN10     IFEQ '0'                                       069242
     C***********          Z-ADDMDJANA    PLNANM                          069242
     C***********          Z-ADDMDFEBA    PLNEBM                          069242
     C***********          Z-ADDMDMARA    PLNARM                          069242
     C***********          Z-ADDMDAPRA    PLNPRM                          069242
     C***********          Z-ADDMDMAYA    PLNAYM                          069242
     C***********          Z-ADDMDJUNA    PLNUNM                          069242
     C***********          Z-ADDMDJULA    PLNULM                          069242
     C***********          Z-ADDMDAUGA    PLNUGM                          069242
     C***********          Z-ADDMDSEPA    PLNEPM                          069242
     C***********          Z-ADDMDOCTA    PLNCTM                          069242
     C***********          Z-ADDMDNOVA    PLNOVM                          069242
     C***********          Z-ADDMDDECA    PLNECM                          069242
     C***********          Z-ADDMDMGNR    PLNGNR                          069242
     C***********          ELSE                                           069242
     C***********          Z-ADD0         PLNANM                          069242
     C***********          Z-ADD0         PLNEBM                          069242
     C***********          Z-ADD0         PLNARM                          069242
     C***********          Z-ADD0         PLNPRM                          069242
     C***********          Z-ADD0         PLNAYM                          069242
     C***********          Z-ADD0         PLNUNM                          069242
     C***********          Z-ADD0         PLNULM                          069242
     C***********          Z-ADD0         PLNUGM                          069242
     C***********          Z-ADD0         PLNEPM                          069242
     C***********          Z-ADD0         PLNCTM                          069242
     C***********          Z-ADD0         PLNOVM                          069242
     C***********          Z-ADD0         PLNECM                          069242
     C***********          Z-ADD0         PLNGNR                          069242
     C***********          END                                            069242
      *
     C*********************WRITEPELN123F                                  069242
     C***********          ADD  1         CADREC  50                      069242
     C***********          ENDSR                                          069242
      /EJECT
      *****************************************************************
      *                                                               *
      * F0000  - End of program control                               *
      *                                                               *
      * Called by: Main Processing                                    *
      *                                                               *
      * Calls: *PSSR                                                  *
      *                                                               *
      *****************************************************************
      *
     C           F0000     BEGSR
     C                     READ LEPELNZ                  89
     C                     READ LEPEMGZ                  89
     C*
     C           GLNREC    IFNE LZNREC
     C                     SETON                     50
     C                     END
     C*
     C           GMGREC    IFNE MZNREC
     C                     SETON                     51
     C                     END
     C*
     C                     WRITELER520P
     C                     WRITELER520D
     C                     WRITELER520T
     C*
     C           *IN50     IFNE '0'
     C           *IN51     ORNE '0'
     C                     SETON                     U7
     C                     END
      *
     C           *INU7     IFEQ '0'
     C                     WRITENPELNZF
     C                     WRITENPEMGZF
     C                     END
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * Z1000  - Obtain Department                                    *
      *                                                               *
      * Called by: E0000                                              *
      *                                                               *
      * Calls: *PSSR                                                  *
      *                                                               *
      *****************************************************************
      *
     C           Z1000     BEGSR
      *
     C           BBCUST    IFNE GCUS
      *
     C                     MOVELBBCUST    GCUS    6
     C                     CALL 'AOCUSTR0'
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY    'POPTN
     C                     PARM GCUS      KEYC   10
     C                     PARM           KEYSTS  7
     C           SDCUST    PARM SDCUST    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE            **************
     C                     MOVELGCUS      DBKEY            * DBERROR 03 *
     C                     MOVEL'LER520'  DBPGM            **************
     C                     MOVEL'SDCUSTPD'DBFILE
     C                     OUT  LDA
     C                     EXSR DBERR
     C                     EXSR *PSSR
     C                     END
      *
     C                     Z-ADDX         Y       20
     C***********BBACOC    LOKUPAO,Y                     10               125441
     C           BBACOC    LOKUPAO,Y                     12               125441
      *
     C************IN10     IFNE '1'                                       125441
     C           *IN12     IFNE '1'                                       125441
     C                     Z-ADD50        Y
      *
     C                     CALL 'AOACOFR0'
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM BBACOC    ACOKEY  2
     C           SDACOF    PARM SDACOF    DSFDY
      *
     C                     MOVE BBACOC    AO,50
     C           PRTCD     IFEQ *BLANKS
     C                     MOVE AZDPCD    DC,50
     C                     ELSE
     C                     MOVE '***'     DC,50
     C                     END
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * DBERR  - Writes database error format on Audit Report         *
      *                                                               *
      * Called by: B0000, Z1000                                       *
      *                                                               *
      * Calls: Nothing                                                *
      *                                                               *
      *****************************************************************
      *
     C           DBERR     BEGSR
      *
     C                     WRITELER520P
     C                     WRITEDBFMT
     C                     WRITELER520T
     C                     ENDSR
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*    * P S S R -  T O   H A N D L E   A B N O R M A L I T I E S *
     C*    ~~~~~~~~~    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ *
     C*    CALLED FROM     :   ALL ABNORMAL CONDITIONS / ERRORS       *
     C*    CALLS           :   AUDIT                                  *
     C*                                                               *
     C*    THIS PROGRAM HANDLES ABNORMAL CONDITIONS SUCH AS READ FAIL *
     C*    CHAIN FAIL ETC, AND PRODUCES A FORMATTED RPG DUMP          *
     C*                                                               *
     C*****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     SETON                     U7U8LR
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     END
      *
     C                     RETRN
      *
     C           PSSR      ENDSR
      *
     C********************************************************************
     C/EJECT
     C/TITLE SR/ZDATE1
     C/COPY ZSRSRC,ZDATE1Z2
     C/TITLE SR/ZDATE2
     C/COPY ZSRSRC,ZDATE2Z2
     C********************************************************************
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
