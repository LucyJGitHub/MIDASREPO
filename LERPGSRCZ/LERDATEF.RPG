     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Convrt date to dayno 1ST DAY OF MTH')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Customer Lending Module
     F*                                                               *
     F*  LERDATEF - Validate and convert a date to a day no. for first*
     F*             day of month                                      *
     F*                                                               *
     F*  Function: Routine to validate and convert a date to a day no.*
     F*   (COB)    for first day of month.                            *
     F*                                                               *
     F*            N.B. Processing is the same as for                 *
     F*            ZDATE2 but the following invalid dates             *
     F*            will be converted to valid first days in           *
     F*            month as shown:                                    *
     F*                                                               *
     F*            Feb 29th (non-leap year) changed to Mar 1st        *
     F*            Feb 30th,31st            changed to Mar 1st        *
     F*            Apr 31st                 changed to May 1st        *
     F*            Jun 31st                 changed to Jul 1st        *
     F*            Sep 31st                 changed to Oct 1st        *
     F*            Nov 31st                 changed to Dec 1st        *
     F*                                                               *
     F*  Parameters: GDATE  - (I) 6 digit alpha field containing      *
     F*                       the date to be validated and            *
     F*                       converted.                              *
     F*              GDAYNO - (O) 5 digit numeric field holding       *
     F*                       the day number of the converted         *
     F*                       field. If an invalid date was           *
     F*                       passed, this field will contain         *
     F*                       zeros.                                  *
     F*              GRTCD  - (O) The value of *IN99. 0 = valid       *
     F*                       numeric input passed via parameter      *
     F*                       GDATE, 1 = invalid.                     *
     F*                                                               *
     F*  Called By: Lending Module programs                           *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  LAST AMEND NO. 158199             DATE 08Jun99               *
     F*  PREV AMEND NO. XXXXXX             DATE DDMMMYY               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
     F*  158199 - Year2000 compliancy. Check for invalid date because *
     F*           of Y2K and correct it.                              *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*****************************************************************
     F*
     F/EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   99  - Error Indicator                                       *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     E                    CPY@    1   1 80
     E                    GYDY    4   4  4 0
     E                    GMDY   13  13  3 0
     E/SPACE 3
     I*
     IPSDS       SDS
     I** PROGRAM STATUS DATA STRUCTURE - REQUIRED FOR DUMP
     I                                      244 253 JOB
     I                                      254 263 USER
     I*
     C/EJECT
     C*******************************************************************
     C*
     C***  Receive input parameters.
     C*
     C           *ENTRY    PLIST
     C                     PARM           GDATE
     C                     PARM           GDAYNO
     C                     PARM           GRTCD   1
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C***  N.B. THE YEAR 2000, BEING DIVISIBLE BY 400, IS A LEAP YEAR.
     C*
     C***  CLEAR DAY NUMBER. RESET ERROR INDICATOR
     C*
     C                     Z-ADD0         GDAYNO  50
     C                     SETOF                     99
     C*
     C***  CALCULATION TO DEFINE INPUT DATE FIELD.
     C*    AND COPE WITH YEAR 2000                                        158199
     C*
     C                     MOVE GDATE     WORKY   20                      158199
     C           WORKY     IFLT 0                                         158199
     C                     MOVE 99        GDATE                           158199
     C                     END                                            158199
     C                     Z-ADDGDATE     GDATE   60
     C*
     C***  GET INDIVIDUAL DAY, MONTH AND YEAR FIELDS.
     C*
     C                     MOVE GDATE     GYEAR   20
     C                     MOVELGDATE     GDAY    20
     C                     MOVE GDATE     GWRK4   40
     C                     MOVELGWRK4     GMTH    20
     C*
     C***  ENSURE MONTH IS VALID. BYPASS IF ERROR
     C*
     C           GMTH      IFLE 0
     C           GMTH      ORGT 12
     C                     SETON                     99
     C                     GOTO GDEND1
     C                     END
     C*
     C***  ENSURE DAY OF MONTH IS VALID (FROM 1-31). BYPASS IF ERROR.
     C*
     C***  N.B. 30TH FEB, 31ST JUNE ETC. WILL BE CONVERTED TO VALID FIRST
     C***  DAY OF MONTH, I.E. 1ST OF NEXT MONTH (1ST MARCH, 1ST JULY ETC.)
     C*
     C           GDAY      IFLE 0
     C           GDAY      ORGT 31
     C                     SETON                     99
     C                     GOTO GDEND1
     C                     END
     C*
     C***  CHECK FOR 30 DAY MONTHS. IF DAY IS 31ST THEN CHANGE DATE TO
     C***  1ST DAY OF NEXT MONTH FOR VALID START OF MONTH DATE.
     C*
     C           GMTH      IFEQ 4
     C           GMTH      OREQ 6
     C           GMTH      OREQ 9
     C           GMTH      OREQ 11
     C*
     C           GDAY      IFGT 30
     C                     Z-ADD1         GDAY
     C                     ADD  1         GMTH
     C                     END
     C*
     C                     END
     C*
     C***  CHECK FOR LEAP YEAR: REMAINDER WILL BE ZERO IF IT IS A LEAP YR
     C*
     C           GYEAR     ADD  28        GYEAR
     C           GYEAR     DIV  4         GLYR    20
     C                     MVR            GLY     10
     C*
     C***  IF FEBUARY FURTHER VALIDATE DAY.
     C*
     C           GMTH      IFEQ 2
     C*
     C***  IF GREATER THAN 28 AND NOT A LEAP YEAR CHANGE DAY TO
     C***  1ST MARCH FOR VALID START OF MONTH DATE.
     C*
     C           GLY       IFGT 0
     C           GDAY      ANDGT28
     C                     Z-ADD1         GDAY
     C                     ADD  1         GMTH
     C                     END
     C*
     C***  IF GREATER THAN 29 AND A LEAP YEAR CHANGE DAY TO
     C***  1ST MARCH FOR VALID START OF MONTH DATE.
     C*
     C           GLY       IFEQ 0
     C           GDAY      ANDGT29
     C                     Z-ADD1         GDAY
     C                     ADD  1         GMTH
     C                     END
     C*
     C                     END
     C*
     C***  DETERMINE NUMBER OF DAYS FROM 31/12/1971.
     C*
     C***  MULTIPLY NO. OF FOUR-YEAR SPANS, BY NO. OF DAYS IN SPAN
     C*
     C           GLYR      MULT 1461      GDAYNO
     C*
     C***  IF NOT A LEAP YEAR, ADD APPROPRIATE NO. OF DAYS FOR EXTRA
     C***  YEARS USING REMAINDER FIELD (IE. 1, 2 OR 3 X 356)
     C*
     C           GLY       IFGT 0
     C           GDAYNO    ADD  GYDY,GLY  GDAYNO
     C                     END
     C*
     C***  IF IT IS A LEAP YEAR, AND LATER THAN FEBRUARY, ADD ONE FOR
     C***  29TH OF FEB
     C*
     C           GLY       IFEQ 0
     C           GMTH      ANDGT2
     C           GDAYNO    ADD  1         GDAYNO
     C                     END
     C*
     C***  ADD APPROPRIATE NUMBER OF DAYS FOR THE MONTH NUMBER
     C*
     C           GDAYNO    ADD  GMDY,GMTH GDAYNO
     C*
     C***  ADD NUMBER OF DAYS IN MONTH
     C*
     C           GDAYNO    ADD  GDAY      GDAYNO
     C*
     C           GDEND1    TAG                             * GDEND1 TAG *
     C*
     C                     MOVE *IN99     GRTCD
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C*
     C********************************************************************
     C/EJECT
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
**   GYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   GMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
