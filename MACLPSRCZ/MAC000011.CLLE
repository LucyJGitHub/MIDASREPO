/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas Dump spool items to M/Fiche Bureau')            */
/*********************************************************************/
/*                                                                   */
/*       Midas - Microfiche Module                                   */
/*                                                                   */
/*       MAC000011 - Copy/Duplicate Spool files to different         */
/*                   bureau file as per outq name. Create the        */
/*                   file if not found.                              */
/*                                                                   */
/*       (c) Finastra International Limited 2002                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CFC002  *CREATE    Date 29Apr02              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CFC002 - Multiple Output Queues In Std Output Instructions  */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/* THIS PROGRAM COPYS MICROFICHE OUTQ SPOOL FILE ITEMS INTO THE      */
/* MICROFICHE BUREAU FILE; IF FILE IS NOT THERE, IT WILL BE CREATED. */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(100)
             DCL        VAR(&MSGF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MFLAG) TYPE(*CHAR) LEN(1)
             DCL        VAR(&OUTQ0) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTQ1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTQ2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTQ3) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTQ4) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTQ5) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTQ6) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTQ7) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTQ8) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTQ9) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QUEUE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COUNT) TYPE(*DEC) LEN(2 0) VALUE(0)
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(7) +
                          VALUE('xxDMLIB')
             DCL        VAR(&SDSTAT) TYPE(*CHAR) LEN(256)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2002')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/** Retrieve Midas system ID */
 
             RTVDTAARA  DTAARA(SDSTAT) RTNVAR(&SDSTAT)
             CHGVAR     VAR(%SST(&DMLIB 1 2)) VALUE(%SST(&SDSTAT 6 2))
 
             CHGJOB     SWS(XXXXXX00)
 
/**  Check to see if duplication function is turned ON/OFF */
 
             CALL       PGM(MA000011) PARM(&MFLAG &OUTQ0 &OUTQ1 &OUTQ2 +
                          &OUTQ3 &OUTQ4 &OUTQ5 &OUTQ6 &OUTQ7 &OUTQ8 +
                          &OUTQ9)
 
             IF         COND(&MFLAG *EQ 'N') THEN(GOTO CMDLBL(END))
 
             OVRPRTF    FILE(QPRTSPLQ) OUTQ(AU##MFQ) HOLD(*YES)
 NEXTQ:
/** If count is greater than 10, end program */
             IF         COND(&COUNT *GE 10) THEN(GOTO CMDLBL(END))
             CHGVAR     VAR(&COUNT) VALUE(&COUNT + 1)
 
/** FIRST OUT Q */
             IF         COND(&COUNT *EQ 1) THEN(DO)
             CHGVAR     VAR(&QUEUE) VALUE(&OUTQ0)
             GOTO       CHECK
             ENDDO
/** 2ND   OUT Q */
             IF         COND(&COUNT *EQ 2) THEN(DO)
               CHGVAR     VAR(&QUEUE) VALUE(&OUTQ1)
               GOTO       CHECK
             ENDDO
/** THIRD OUT Q */
             IF         COND(&COUNT *EQ 3) THEN(DO)
               CHGVAR     VAR(&QUEUE) VALUE(&OUTQ2)
               GOTO       CHECK
             ENDDO
/** FOURTH OUT Q */
             IF         COND(&COUNT *EQ 4) THEN(DO)
               CHGVAR     VAR(&QUEUE) VALUE(&OUTQ3)
               GOTO       CHECK
             ENDDO
/** FIFTH  OUT Q */
             IF         COND(&COUNT *EQ 5) THEN(DO)
               CHGVAR     VAR(&QUEUE) VALUE(&OUTQ4)
               GOTO       CHECK
             ENDDO
/** 6TH OUT Q */
             IF         COND(&COUNT *EQ 6) THEN(DO)
               CHGVAR     VAR(&QUEUE) VALUE(&OUTQ5)
               GOTO       CHECK
             ENDDO
/** 7TH OUT Q */
             IF         COND(&COUNT *EQ 7) THEN(DO)
               CHGVAR     VAR(&QUEUE) VALUE(&OUTQ6)
               GOTO       CHECK
             ENDDO
/** 8TH OUT Q */
             IF         COND(&COUNT *EQ 8) THEN(DO)
               CHGVAR     VAR(&QUEUE) VALUE(&OUTQ7)
               GOTO       CHECK
             ENDDO
/** 9TH OUT Q */
             IF         COND(&COUNT *EQ 9) THEN(DO)
               CHGVAR     VAR(&QUEUE) VALUE(&OUTQ8)
               GOTO       CHECK
             ENDDO
/** 10TH  OUT Q */
             IF         COND(&COUNT *EQ 10) THEN(DO)
               CHGVAR     VAR(&QUEUE) VALUE(&OUTQ9)
               GOTO       CHECK
             ENDDO
 
CHECK:
/** If blank for outq name, skip processing  */
 
             IF         COND(&QUEUE *EQ ' ') THEN(GOTO CMDLBL(END))
 
/**  Check if microfiche bureau file (same name as outq) exist */
/**  If not, create a duplicate copy of the file as per MAFICDPD */
 
             CHKOBJ     OBJ(&DMLIB/&QUEUE) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(DO)
                CPYF    FROMFILE(MAFICDPD) TOFILE(&DMLIB/&QUEUE) +
                        TOMBR(&QUEUE) MBROPT(*ADD) CRTFILE(*YES)
             ENDDO
 
/**  Clear OUTQ details work file that holds spool file details */
 
             WRKOUTQ    OUTQ(&QUEUE) OUTPUT(*PRINT)
             CPYSPLF    FILE(QPRTSPLQ) TOFILE(MAFWRKPD) SPLNBR(*LAST)
 
/** Receive next record into program to copy spool file */
 
             CALL       PGM(MAC000012) PARM(&QUEUE &DMLIB)
             GOTO NEXTQ
 
ABNOR:
             RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGID(&MSGID) +
                          MSGF(&MSGF) MSGFLIB(&MSGFLIB)
             SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                          MSGDTA(&MSG) TOMSGQ(MOPERQ) MSGTYPE(*ESCAPE)
             SNDPGMMSG  MSG('MAC000011 Copying of microfiche output +
                          queues spool details to microfiche data +
                          file ended abnormally') TOMSGQ(MOPERQ)
 
/** Finally, clear OUTQ that this program works on */
/** Also, delete override of file */
 
 END:        CLROUTQ    OUTQ(AU##MFQ)
             MONMSG     MSGID(CPF3357 CPF3417)
             DLTOVR     FILE(*ALL)
             ENDPGM
