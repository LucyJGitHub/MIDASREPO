/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas Dump spool items to M/Fiche Bureau')            */
/*********************************************************************/
/*                                                                   */
/*       Midas - Microfiche Module                                   */
/*                                                                   */
/*       MAC000012 - Copy/Duplicate Spool files to Microfiche        */
/*                   bureau file Run.                                */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2002           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. CFC002  *CREATE    Date 29Apr02              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CFC002 - Multiple Output Queues In Std Output Instructions  */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/* THIS PROGRAM COPYS MICROFICHE OUTQ SPOOL FILE ITEMS INTO THE      */
/* MICROFICHE BUREAU FILE; CALLED BY MAC000011                       */
/*                                                                   */
/*********************************************************************/
 
             PGM        PARM(&QUEUE &DMLIB)
 
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(100)
             DCL        VAR(&MSGF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGFLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&FILNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SPLNBR) TYPE(*DEC) LEN(4 0)
             DCL        VAR(&JOBNBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QUEUE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CLRFLG) TYPE(*CHAR) LEN(1)
 
             DCLF       FILE(MAFWRKPD) RCDFMT(MAFWRKD0)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2002')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(XXXXXX00)
 
/** Receive next record into program to copy spool file */
 
             CHGVAR     VAR(&CLRFLG) VALUE(' ')
NEXTREC:
             RCVF       DEV(*FILE) RCDFMT(MAFWRKD0)
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(END))
 
/**  Manipulate spool files details; ignore parts of details not needed. */
/** Header page line 1 */
             IF         COND(%SST(&OUTD 119 4) *EQ 'Page') THEN(GOTO +
                          CMDLBL(NEXTREC))
 
/** Header page line 2 */
             IF         COND(%SST(&OUTD 109 4) *EQ 'Time') THEN(GOTO +
                          CMDLBL(NEXTREC))
 
/** Number of pages is 0 */
             IF         COND(%SST(&OUTD 45 2) *EQ ' 0') THEN(GOTO +
                          CMDLBL(NEXTREC))
 
 
/** No spool files */
             IF         COND(%SST(&OUTD 7 17) *EQ 'No spooled +
                          output') THEN(GOTO CMDLBL(NEXTREC))
 
/** End of OUTQ spool items; therefore get next OUTQ details  */
             IF         COND(%SST(&OUTD 10 9) *EQ '* * * * *') +
                          THEN(GOTO CMDLBL(NEXTREC))
 
/**   IF proper record found, copy spool file details */
 
             CHGVAR     VAR(&FILNAM) VALUE(%sst(&OUTD 2 10))
             CHGVAR     VAR(&USER) VALUE(%sst(&OUTD 13 10))
             CHGVAR     VAR(&JOBNAM) VALUE(%sst(&OUTD 82 10))
             CHGVAR     VAR(&JOBNBR) VALUE(%sst(&OUTD 93 6))
             CHGVAR     VAR(&SPLNBR) VALUE(%sst(&OUTD 73 4))
 
             CPYSPLF    FILE(&FILNAM) TOFILE(&DMLIB/&QUEUE) +
                          JOB(&JOBNBR/&USER/&JOBNAM) +
                          SPLNBR(&SPLNBR) MBROPT(*ADD) CTLCHAR(*FCFC)
 
             MONMSG     MSGID(CPF2207 CPF3309 CPF3342 CPF3429 CPF3483 +
                          CPF3493 CPF9837 CPF3207 CPF3330 CPF3343 +
                          CPF3430 CPF3486 CPF3499 CPF9845 CPF3303 +
                          CPF3340 CPF3344 CPF3482 CPF3492 CPF9812 +
                          CPF9846)                          EXEC(DO)
                RCVMSG     MSGTYPE(*EXCP) MSGDTA(&MSG) MSGID(&MSGID) +
                           MSGF(&MSGF) MSGFLIB(&MSGFLIB)
                SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                           MSGDTA(&MSG) TOMSGQ(MOPERQ) MSGTYPE(*ESCAPE)
                CHGVAR     VAR(&CLRFLG) VALUE('N')
             ENDDO
             GOTO       CMDLBL(NEXTREC)
 
ABNOR:
             SNDPGMMSG  MSG('Copy/Duplicate Spool files to +
                          Microfiche Bureau ended abnormally.') +
                          TOMSGQ(MOPERQ)
 
/** Only clear if no problem encountered */
 
 END:        IF         COND(&CLRFLG *EQ ' ') THEN(DO)
                CLROUTQ    OUTQ(&QUEUE)
                MONMSG     MSGID(CPF3357 CPF3417)
             ENDDO
             ENDPGM
