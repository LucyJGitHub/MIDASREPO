      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MA Select Archive Identifier Select rcd')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Microfiche/Optical Disk Archiving Module
     A*                                                               *
     A*  MA0320S# - SELECT ARCHIVE IDENTIFIER SELECT SCREEN           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 028255             Date 26Aug93               *
      *                 S01199             Date 07Nov90               *
     A*                 LN0661             DATE 22AUG90               *   LN0661
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  028255 - Change LVLCHK type in creation parameters to (*NO)  *
     A*  S01199 - CURSOR SENSITIVE HELP PROCESSING                    *   LN0661
     A*  LN0661 - STANDARDISE SCREEN APPEARANCE                       *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
/****A*:*LVLCHK(*YES)****AUT(*USE)**********************************:**   028255
     A*****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A          R #SFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(36 'Invalid #1SEL     ')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1CHTP         1   H      TEXT('Type of Last Change')
     A            #1SEL          1   B  7  2
     A                                      VALUES('1')
     A                                      CHECK(AB)
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A            #1ARID        10      7  5TEXT('Archive ID')
     A                                      DSPATR(HI)
     A            #1MEDM         1      7 19TEXT('Medium')
     A                                      DSPATR(HI)
     A            #1ADSC        30      7 25TEXT('Description')
     A                                      DSPATR(HI)
     A            #1DEPT         3      7 57TEXT('Department')
     A                                      DSPATR(HI)
     A            #1AOTQ        10      7 64TEXT('Output Queue')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A N82                                  ROLLUP(27 'Next page.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2ARID')
     A                                      SETOF(32 'Invalid: #2MEDM')
     A                                      SETOF(33 'Invalid: #2ADSC')
     A                                      SETOF(34 'Invalid: #2DEPT')
     A                                      SETOF(35 'Invalid: #2AOTQ')
     A*.........................................................................
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A*                                                    /*S01199*/
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A*                                                                 /LN0661/
     A************                          DSPATR(HI)
     A*                                                                 /LN0661/
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'MA0320S1'
     A*                                                                 /LN0661/
     A************                          DSPATR(HI)
     A*                                                                 /LN0661/
     A            ##RUNA         7      1 72TEXT('midas rundate')
     A N25                                  OVRDTA
     A*                                                                 /LN0661/
     A************                          DSPATR(HI)
     A*                                                                 /LN0661/
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A*                                                                 /LN0661/
     A************                          DSPATR(HI)
     A************                      2 35'Select Archive Identifiers'
     A                                  2 27'Select Archive Identifiers'
     A*                                                                 /LN0661/
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A*                                                                 /LN0661/
     A************                          DSPATR(HI)
     A*                                                                 /LN0661/
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2CHTP         1   H      TEXT('Type of Last Change')
     A*                                                                 /LN0661/
     A************                      3  5'Archive'
     A                                  4  5'Archive ID'
     A************                      3 17'Medium'
     A                                  4 17'Medium'
     A************                      3 25'Description'
     A                                  4 25'Description'
     A************                      3 57'Dept.'
     A                                  4 57'Dept.'
     A************                      3 64'Output Queue'
     A                                  4 64'Output Queue'
     A************                      4  5'ID'
     A*                                                                 /LN0661/
     A            #2ARID        10   B  5  5TEXT('Archive ID')
     A*                                                                 /LN0661/
     A************                          CHECK(FE)
     A*                                                                 /LN0661/
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2MEDM         1   B  5 19TEXT('Medium')
     A*                                                                 /LN0661/
     A**Condition:*Valid*Codes*******
     A************                          VALUES('M' +
     A************                                 'O' '?')
     A************                          CHECK(AB)
     A*                                                                 /LN0661/
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2ADSC        30   B  5 25TEXT('Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2DEPT         3   B  5 57TEXT('Department')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2AOTQ        10   B  5 64TEXT('Output Queue')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys.')
     A                                      OVERLAY PUTOVR
     A*                                                                 /LN0661/
     A************##AENA        78     21  2TEXT('Command Text 1 - ACT')
     A            ##AENA        78     23  2TEXT('Command Text 1')
     A*                                                                 /LN0661/
     A N25                                  OVRDTA
     A*                                                                 /LN0661/
     A************                          DSPATR(HI)
     A************##AFNA        78     22  2TEXT('Command Text 2 - ACT')
     A*N25********                          OVRDTA
     A************                          DSPATR(HI)
     A*                                                                 /LN0661/
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A*                                                                 /LN0661/
     A************                          SFL SFLMSGRCD(23)
     A                                      SFL SFLMSGRCD(24)
     A*                                                                 /LN0661/
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A*                                                                 /LN0661/
     A************                          SFLPAG(02) SFLSIZ(06)
     A                                      SFLPAG(01) SFLSIZ(03)
     A*                                                                 /LN0661/
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
