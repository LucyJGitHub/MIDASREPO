      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MA Microfiche control data maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Microfiche Module                                    *
      *                                                               *
      *  DSPF/MA0340M# - Microfiche Control Data Maintenance          *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CFC002  *CREATE    Date 29Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFC002 - Multiple Output Queues In Std Output Instructions   *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03(03 'Exit')
      *
     A          R MA0340X0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A                                      PUTOVR
      *
     A            SUSER         10A  O  1  2TEXT('User ID')
     A                                  1 13'MA0340M'
     A            SWSID         10A  O  2  2TEXT('Workstation ID')
     A                                  1 25'MICROFICHE CONTROL DATA'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SADATE         7A  O  1 72
     A                                  2 71TIME
     A                                      EDTWRD('0 :  :  ')
     A                                  5  7'Action'
     A            SAACTN         1A  B  5 26
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A                                      OVRATR
     A                                  5 31'(I,A,E)'
     A                                 23  2'F3=Exit'
     A          R MA0340X2
     A                                      TEXT('CLEAR DISPLAY')
     A                                  9  7' '
     A          R MA0340X1
     A                                      CA12(12 'Previous')
     A                                      OVERLAY
     A                                      PUTOVR
     A            SUSER         10A  O  1  2TEXT('User ID')
     A                                  1 13'MA0340M'
     A            SWSID         10A  O  2  2TEXT('Workstation ID')
     A                                  1 25'MICROFICHE CONTROL DATA'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SAFUNC         7A  O  1 60OVRDTA
     A            SADATE         7A  O  1 72
     A                                  2 71TIME
     A                                      EDTWRD('0 :  :  ')
     A  71        CUR            1   O  1  2DSPATR(PC)
     A                                  5  7'Action'
     A            SAACTN         1A  O  5 26OVRDTA
     A                                  5 31'(I,A,E)'
     A                                  9  7'Microfiche Output Queues'
     A                                 21  7'Microfiche On/Off'
     A            SAMFOF         1A  B 21 34
     A                                      DSPATR(MDT)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 21 36'(Y/N)'
     A            OUTQ01        10A  B  9 34
     A  41
     AO 50                                  DSPATR(PC)
     A N71
     AON50                                  DSPATR(UL)
     A  50                                  DSPATR(RI)
     A  71                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      OVRATR
     A            OUTQ02        10A  B 10 34OVRDTA
     A  51                                  DSPATR(PC RI)
     A N71
     AON51                                  DSPATR(UL)
     A  71                                  DSPATR(PR)
     A                                      OVRATR
     A            OUTQ03        10A  B 11 34OVRDTA
     A  52                                  DSPATR(PC RI)
     A N71
     AON52                                  DSPATR(UL)
     A  71                                  DSPATR(PR)
     A            OUTQ04        10A  B 12 34OVRDTA
     A  53                                  DSPATR(PC RI)
     A N71
     AON53                                  DSPATR(UL)
     A  71                                  DSPATR(PR)
     A            OUTQ05        10A  B 13 34OVRDTA
     A  54                                  DSPATR(PC RI)
     A N71
     AON54                                  DSPATR(UL)
     A  71                                  DSPATR(PR)
     A            OUTQ06        10A  B 14 34OVRDTA
     A  55                                  DSPATR(PC RI)
     A N71
     AON55                                  DSPATR(UL)
     A  71                                  DSPATR(PR)
     A            OUTQ07        10A  B 15 34OVRDTA
     A  56                                  DSPATR(PC RI)
     A N71
     AON56                                  DSPATR(UL)
     A  71                                  DSPATR(PR)
     A            OUTQ08        10A  B 16 34OVRDTA
     A  57                                  DSPATR(PC RI)
     A N71
     AON57                                  DSPATR(UL)
     A  71                                  DSPATR(PR)
     A            OUTQ09        10A  B 17 34OVRDTA
     A  58                                  DSPATR(PC RI)
     A N71
     AON58                                  DSPATR(UL)
     A  71                                  DSPATR(PR)
     A            OUTQ10        10A  B 18 34OVRDTA
     A  59                                  DSPATR(PC RI)
     A N71
     AON59                                  DSPATR(UL)
     A  71                                  DSPATR(PR)
     A                                 23  2'F3=Exit     F12=Previous'
     A          R MA0340S0
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     A          R MA0340C0                  SFLCTL(MA0340S0)
     A                                      SFLSIZ(6)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A  10                                  SFLEND
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      PUTOVR
     A                                      BLINK
     A            PGMQ                      SFLPGMQ
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Previous'
