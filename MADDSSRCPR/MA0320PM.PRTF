      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Report Archive ID's Print file')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Microfiche/Optical Disk Archiving Module
     A*                                                               *
     A*  MA0320PM - REPORT ARCHIVE ID'S                               *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. LN1086             Date 20Dec90               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  LN1086 - PRINT 'NO DETAILS' IF NULL PROCESSING               *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R $PAGHDR                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'REFERENCE'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##TITL        53        47TEXT('User Report Title')
     A                                   110'DATE'
     A            ##RUNA         7       115TEXT('midas rundate')
     A                                   123'PAGE'
     A                                   128PAGNBR
     A                                      EDTCDE(3)
     A                                   110'TIME'
     A                                      SPACEB(1)
     A                                   115TIME
     A                                      EDTWRD('  :  :  ')
     A            ##E8TX        53        47TEXT('Audit/Report Title')
     A                                      SPACEB(2)
     A            ##BJNA        53        47TEXT('Report Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R $COLHDG                   TEXT('Detail line headings.')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                     2'Archive'
     A                                    16'Description'
     A                                    50'Medium'
     A                                    60'Department'
     A                                    74'Output Queue'
     A                                   109' Last'
     A                                   118'Change'
     A                                     2'ID'
     A                                      SPACEB(1)
     A                                   109'Change'
     A                                   118' Type'
      *=========================================================================
     A          R $DTLRCD                   TEXT('Detail line.             ')
     A                                      SPACEB(1)
      *.........................................................................
     A            $RARID        10         2TEXT('Archive ID')
     A            $RADSC        30        16TEXT('Description')
     A            $RMEDM         1        53TEXT('Medium')
     A            $RDEPT         3        63TEXT('Department')
     A            $RAOTQ        10        75TEXT('Output Queue')
     A            $RA0NA         7       109TEXT('Alpha Change Date')
     A            $RANST         6       118TEXT('Alpha Of Last Change')
      * 'Hidden' internal version of Last Change Date
     A  99N99     $RLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     $RCHTP         1         1SPACEB(1)
      *=========================================================================
     A          R $FINTTL                   TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A N79                                58'Number of Records'
     A N79        $TAPNB         5  0     76TEXT('No of Calculated Recs')
     A                                      EDTCDE(3)
     A N78                                56'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(1)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     $TCON          1         1SPACEB(1)
      *=========================================================================
     A          R $ENDRPT                   TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A****                                            LN1086
     A N78                                56'*** NO DETAILS TO REPORT ***'
     A                                      SPACEA(2)
     A****                                            LN1086
     A                                    59'*** END OF REPORT ***'
      *=========================================================================
