      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Report Collation Report Print file')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Microfiche/Optical Disk Archiving Module
     A*                                                               *
     A*  MA0330PM - REPORT COLLATION REPORT                           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. LN1174             Date 26Feb91               *
      *                 LN1086             Date 20Dec90               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  LN1174 - REPLACE PRINT 'SUFFIX' WITH 'FILE NAME'             *
     A*  LN1086 - PRINT 'NO DETAILS' IF NULL PROCESSING               *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
      *=========================================================================
     A          R $PAGHDR                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     2'REFERENCE'
     A            ##PGM         10        12TEXT('*PROGRAM')
     A            ##TITL        53        47TEXT('User Report Title')
     A                                   110'DATE'
     A            ##RUNA         7       115TEXT('midas rundate')
     A                                   123'PAGE'
     A                                   128PAGNBR
     A                                      EDTCDE(3)
     A                                   110'TIME'
     A                                      SPACEB(1)
     A                                   115TIME
     A                                      EDTWRD('  :  :  ')
     A            ##E8TX        53        47TEXT('Audit/Report Title')
     A                                      SPACEB(2)
     A            ##BJNA        53        47TEXT('Report Underline')
     A                                      SPACEB(1)
      *=========================================================================
     A          R $1KEYHDR                  TEXT('Report name              ')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     2'Report name'
     A            $1RNAM        10        14TEXT('Report name')
      *=========================================================================
     A          R $2KEYHDR                  TEXT('Print File Name          ')
     A                                      SPACEB(2)
      *.........................................................................
      *                                                                 /LN1174/
     A************                        10'Print Suffix'
     A************$2RSFX        10        23TEXT('Print File Name')
     A                                    10'Print File Name'
     A            $2RSFX        10        26TEXT('Print File Name')
      *                                                                 /LN1174/
      * 'Hidden' internal version of Report name
     A  99N99     $2RNAM        10         1SPACEB(1)
      *=========================================================================
     A          R $COLHDG                   TEXT('Detail line headings.')
     A                                      SPACEB(2)
     A                                      SPACEA(1)
      *.........................................................................
     A                                    16' Collation'
     A                                    30'    Collation'
     A                                    83' Collation Frequency'
     A                                   118' Last'
     A                                   127'Change'
     A                                    16'Report Name'
     A                                      SPACEB(1)
     A                                    30'Report Description'
     A                                   118'Change'
     A                                   127' Type'
      *=========================================================================
     A          R $DTLRCD                   TEXT('Detail line.             ')
     A                                      SPACEB(1)
      *.........................................................................
     A            $RCORP        10        16TEXT('Collation Report Name')
     A            $RCRDS        50        30TEXT('Collation Report Desc')
     A            $RBBNA        30        83TEXT('Frequency condition')
     A            $RA0NA         7       118TEXT('Alpha Change Date')
     A            $RANST         6       127TEXT('Alpha Of Last Change')
      * 'Hidden' internal version of Report name
     A  99N99     $RRNAM        10         1SPACEB(1)
      * 'Hidden' internal version of Print File Name
     A  99N99     $RRSFX        10         1SPACEB(1)
      * 'Hidden' internal version of Collation Freqency
     A  99N99     $RCLFR         1         1SPACEB(1)
      * 'Hidden' internal version of Last Change Date
     A  99N99     $RLCD          5  0      1SPACEB(1)
      * 'Hidden' internal version of Type of Last Change
     A  99N99     $RCHTP         1         1SPACEB(1)
      *=========================================================================
     A          R $3KEYTTL                  TEXT('Print File Name          ')
      *.........................................................................
      * 'Hidden' internal version of Report name
     A  99N99     $3RNAM        10         1SPACEB(1)
      * 'Hidden' internal version of Print File Name
     A  99N99     $3RSFX        10         1SPACEB(1)
      *=========================================================================
     A          R $4KEYTTL                  TEXT('Report name              ')
      *.........................................................................
      * 'Hidden' internal version of Report name
     A  99N99     $4RNAM        10         1SPACEB(1)
      *=========================================================================
     A          R $FINTTL                   TEXT('Final totals             ')
     A                                      SPACEB(2)
      *.........................................................................
     A N79                                56'No of Calculated Recs'
     A N79        $TAPNB         5  0     78TEXT('No of Calculated Recs')
     A                                      EDTCDE(3)
     A N78                                56'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(2)
      * 'Hidden' internal version of *CON (Screen constant)
     A  99N99     $TCON          1         1SPACEB(1)
      *=========================================================================
     A          R $ENDRPT                   TEXT('End of report            ')
     A                                      SPACEB(2)
      *.........................................................................
     A****                                            LN1086
     A N78                                56'*** NO DETAILS TO REPORT ***'
     A                                      SPACEA(2)
     A****                                            LN1086
     A                                    59'*** END OF REPORT ***'
      *=========================================================================
