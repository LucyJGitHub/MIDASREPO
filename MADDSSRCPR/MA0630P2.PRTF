      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Archiving Exception Report')
      *****************************************************************
      *                                                               *
      *  Midas - Microfiche/Optical Disk Archiving Module             *
      *                                                               *
      *  MA0630P2 - ARCHIVING EXCEPTION REPORT                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. LN1174             Date 26Feb91               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  LN1174 - REPLACE PRINT 'SUFFIX' WITH 'FILE NAME'             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R HEADP2
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                      SPACEA(2)
     A                                     7'REFERENCE MA0630'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(3)
      *
     A                                    45'YESTERDAY''S ARCHIVING EXCEPTION'
     A                                   + 0' REPORT'
     A                                      SPACEA(2)
     A                                     5'The following reports were no long'
     A                                   + 0'er on the output queue when Archiv'
     A                                   + 0'ing took place and have therefore '
     A                                   + 0'not been archived'
     A                                      SPACEA(2)
      *
     A
     A                                     8'REPORT NAME'
     A*                                                                 /LN1174/
     A************                        30'PRINT SUFFIX'
     A                                    30'PRINT FILE NAME'
     A*                                                                 /LN1174/
     A                                    52'DESCRIPTION'
     A                                   112'ARCHIVE ID'
     A                                      SPACEA(2)
      *
     A          R DETP2                     SPACEA(1)
     A            EXRNAM        10        10
     A            EXRSFX        10        31
     A            EXPFTX        50        52
     A            EXARID        10       112
      *
     A          R NORP2                     SPACEB(3)
     A                                    55'No details to report'
      *
     A          R ENDP2                     SPACEB(3)
     A                                    55'End of report'
