     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MA Collation control')
/*OVRF*: OVRDBF ODBUILD  MAOPTDPD                                   : *   S01382
      *****************************************************************
      *                                                               *
      *  Midas - Microfiche/Optical Disk Archiving Module             *
      *                                                               *
      *  MA0620 - COLLATION CONTROL (ARCHIVE COLLATION REPORTS)       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG5869            Date 21Mar05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01382             Date 05Aug92               *
      *                 E29281             Date 23Oct91               *
      *                     S00000   DATE - ddmmmyy                   *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG5869 - For archiving from outq, assign entity details as  *
      *            held by RCF; for archiving on request, report name *
      *            should include suffix.                             *
      *     S01382 - OPTICAL DISK DEVELOPMENT                         *  *
      *     E29281 - OUTPUT '1*$*$' TO THE OPTICAL DISK HEADER RECORD *
      *                                                               *
      *****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
      *****************************************************************
      *
      * Read all records on the Collation Work file (MACWRKL1)
      *  and archive all records with the Collation Frequency
      *  equal to one of the frequencies on the EDDFLG data
      *  area (= FRQ array).
      *
      * Delete all archived collation records selected for
      *  archiving.
      *
      *****************************************************************
      *
      * FUNCTION OF INDICATORS
      * ========================
      *
      *      U7,U8 Database Error
      *   01       Collation Header Record
      *   02       Collation Detail Record
      *   10       Collation Frequencies due for processing today
      *   20       Write Bureau Header Record
      *   50       EOF on MACWKYL1 file
      *   51       End of records on MACWKYL1 for this frequency
      *   55       No record on MAARCHL1 file
      *   56       No record on FCRSFXL1, or MARCOLL1 file
      *   59       Database, File access error
      *
      *****************************************************************
      /EJECT
     FMACWRKL1UF  E           K        DISK
      *
     FFCRSFXL1IF  E           K        DISK
     FMARCOLL1IF  E           K        DISK
     FMAARCHL1IF  E           K        DISK
      *
     FMAFICDPDO   E                    DISK
     F***********MAOPTDPDO   E                    DISK                    S01382
     FODBUILD IF  E                    DISK                      A    UC  S01382
     F            MAOPTDD0                          KRENAMEODBUILD0       S01382
     E                    PCMD    1   2 74                                S01382
     E                    FRQ         7  1
      * Collation Frequencies - Today's selections
     E                    @CPY    1   1 80
      * Copyright statement
     IMACHDRD0    01
     IMACDTLD0    02
     IODBUILD0                                                            S01382
     I              MACCHR                          ODCCHR                S01382
     I              ODRDTL                          OPTD                  S01382
     IFICH      E DSMAFICHPD
     IFICD      E DSMAFICDPD                                              S01382
      /EJECT
      *FICH      E DSMAFICHPD
      ** Microfiche Header Record layout
      *
     I***********OPTH      E DSMAOPTHPD                                   S01382
      ***Optical*Disk*Header*Record*layout                                S01382
      *
     I***********OPTD      E DSMAOPTDPD                                   S01382
     IDSOPTD    E DSODBUILD                                               S01382
     I              MACCHR                          ODCCHR                S01382
     I              ODRDTL                          OPTD                  S01382
      ** Optical Disk Detail Record layout
      *
     IEDDFLG      DS                              7
      * Collation Frequencies data area
      *
     I                                        1   7 FRQ
      *
     IRUNDAT      DS                             13
      ** MIDAS Run Date data area
      *
     I                                        1   7 @@DATA
     I                                    P   8  100@@DATN
      *
     ILDA        UDS                            256
      ** Standard MIDAS Data Area fields
      *
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
     I            DS
      ** Data structuse for DB Error KEY File output
      *
      ** MACWRKL0/L1 key
     I                                        1  21 CWRK
      ** MARCOLL1 key
     I                                       12  32 RCOL
      *
     I                                        1  10 CLARID
     I                                       11  11 CLCFRQ
      *
     I                                       12  21 CLRNAM
     I                                       22  31 CLRSFX
      *
     I                                       32  32 ##CFRQ
      *                                                                   S01382
     IIHEAD       DS                                                      S01382
      ** Internal header record data structure                            S01382
      *                                                                   S01382
     I                                        1 198 HEADER                S01382
     I                                        1   4 OUTPUT                S01382
     I                                        5  14 REPORT                S01382
     I                                       25  34 JOBNAM                S01382
     I                                       54  63 PRTFNM                S01382
      *                                                                   S01382
     I            DS                                                      S01382
      * Data structure for copy file                                      S01382
     I                                        1 148 PCMD                  S01382
     I                                       47  56 @TOMBR                S01382
      *
     C                     MOVEA@CPY      @BIS   80
      /EJECT
      *
      * Collation Work file
      *
     C           CWRKK     KLIST
     C                     KFLD           CLARID
     C                     KFLD           CLCFRQ
     C                     KFLD           CLRNAM
     C                     KFLD           CLRSFX
      *
      * Collation Work file (short key)
      *
     C           CWRKSK    KLIST
     C                     KFLD           CLARID
     C                     KFLD           CLCFRQ
      *
      * Reports for collation & SUFFIXES
      *
     C           RCOLK     KLIST
     C                     KFLD           CLRNAM
     C                     KFLD           CLRSFX
      *
      ** Define MIDAS data areas, LDA, EDDFLG and RUNDAT
      *
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN           EDDFLG
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   EDDFLG
     C                     IN   RUNDAT
      *******
      * Define/Initialize key & work Fields
      *
     C                     MOVE *BLANKS   LDA
     C***********          MOVE 'MA0620'  DBPGM                           S01382
     C                     MOVEL'MA0620'  DBPGM                           S01382
     C                     MOVE ' '       RECOUT  1                       S01382
      *                                                                   S01382
      * Open PF/ODBUILD under user control                                S01382
     C                     OPEN ODBUILD                                   S01382
      *                                                                   S01382
      * Dummy chain to PF/ODBUILD to allow input specs to be coded for    S01382
      * renaming of field on file                                         S01382
      *                                                                   S01382
     C           1         CHAINODBUILD0             60                   S01382
      *
     C                     MOVE *BLANKS   CLARID
     C                     MOVE *BLANKS   CLCFRQ
     C                     MOVE *BLANKS   ##CFRQ
     C                     MOVE *BLANKS   CLRSFX
      *
     C           *LIKE     DEFN CLARID    ##ARID
     C           *LIKE     DEFN CLRSFX    ##RSFX
     C                     MOVE *BLANKS   ##ARID
     C                     MOVE *BLANKS   ##RSFX
      *
      * Setup Bureau Header output fields
      *
     C                     MOVE *ALL'\'   FICH
     C                     MOVE '*HDR*'   MARTP5
     C                     MOVE @@DATA    MAADAT
     C                     Z-ADD@@DATN    MAMDAT
      *
     C***********          MOVE *ALL'\'   OPTH                            S01382
     C***********          MOVE '*HDR*'   M1RTP5                          E29281
     C***********          MOVE '1*$*$'   M1RTP5                    E29281S01382
     C***********          MOVE @@DATA    M1ADAT                          S01382
     C***********          Z-ADD@@DATN    M1MDAT                          S01382
      *
      **********************************************************
      /EJECT
      **********************************************************
      *                                                        *
      *   M A I N L I N E    P R O C E S S I N G               *
      *                                                        *
      **********************************************************
      *
      *******
      * Read all Collation Work file records to E.O.F
      *
     C           *IN50     DOUEQ'1'
      *
      * Set file pointer to next Archive-ID/Frequency to be archived
      *
     C           CWRKSK    SETGTMACWRKL1
      *
      * Read the Collation Work record
      *
     C                     MOVEA'00'      *IN,01
     C                     READ MACWRKL1               5950
      * File error ??
     C           *IN59     IFEQ '1'
     C           *LOCK     IN   LDA                                       S01382
     C                     MOVE 'MACWRKL1'DBFILE
     C                     MOVELCWRK      DBKEY
     C                     Z-ADD1         DBASE             *************
     C                     OUT  LDA                         ************* S01382
     C                     SETON                     U7U8LR * DB err 01 *
     C                     RETRN                            *************
     C                     END
      *                                                                   S01382
      * On change of Archive ID or at end of file                         S01382
     C           CLARID    IFNE ##ARID                                    S01382
     C           ##ARID    ANDNE*BLANKS                                   S01382
     C           *IN50     OREQ '1'                                       S01382
      *                                                                   S01382
      * If archiving to Optical Disk                                      S01382
     C           EAMEDM    IFEQ 'O'                                       S01382
     C           EAMEDM    OREQ 'B'                                       S01382
      *                                                                   S01382
      * If records have been output to PF/ODBUILD                         S01382
     C           RECOUT    IFEQ 'Y'                                       S01382
      *                                                                   S01382
      * Close and open PF/ODBUILD to ensure that all records are          S01382
      * written to PF/ODBUILD before the copy to PF/MAOPTDPD              S01382
     C                     CLOSEODBUILD                                   S01382
     C                     OPEN ODBUILD                                   S01382
      *                                                                   S01382
      * Copy records from PF/ODBUILD to the member for the Archive ID     S01382
      * just processed, in PF/MAOPTDPD                                    S01382
     C                     MOVE *BLANKS   QCMD   74                       S01382
     C                     MOVEL##ARID    @TOMBR                          S01382
     C                     MOVEAPCMD,1    QCMD                            S01382
      *                                                                   S01382
     C                     CALL 'QCMDEXC'                                 S01382
     C                     PARM           QCMD                            S01382
     C                     PARM 74        QLNGTH 155                      S01382
      *                                                                   S01382
      * Close PF/ODBUILD to free member ready for the CLRPFM              S01382
     C                     CLOSEODBUILD                                   S01382
      *                                                                   S01382
      * Clear PF/ODBUILD                                                  S01382
     C                     MOVE *BLANKS   QCMD                            S01382
     C                     MOVEAPCMD,2    QCMD                            S01382
      *                                                                   S01382
     C                     CALL 'QCMDEXC'                                 S01382
     C                     PARM           QCMD                            S01382
     C                     PARM 74        QLNGTH 155                      S01382
      *                                                                   S01382
      * Reopen PF/ODBUILD                                                 S01382
     C                     OPEN ODBUILD                                   S01382
     C                     MOVE ' '       RECOUT                          S01382
     C                     END                                            S01382
     C                     END                                            S01382
     C                     END                                            S01382
      *
      * Check for E.O.F
      *
     C           *IN50     IFEQ '0'
      *
      * At change of Archive ID, access Archiving information
      *
     C           CLARID    IFNE ##ARID
     C                     MOVE CLARID    ##ARID
     C                     MOVE '1'       *IN20
      *
     C           CLARID    CHAINMAARCHL1             5559
      * DB error ??
     C           *IN55     IFEQ '1'
     C           *IN59     OREQ '1'
     C           *LOCK     IN   LDA                                       S01382
     C                     MOVE 'MAARCHL1'DBFILE
     C                     MOVELCLARID    DBKEY
     C                     Z-ADD2         DBASE             *************
     C                     OUT  LDA                         ************* S01382
     C                     SETON                     U7U8LR * DB err 02 *
     C                     RETRN                            *************
     C                     END
     C                     END
      *
      * Check, if Frequency read is to be processed today
      *
     C           CLCFRQ    IFNE ##CFRQ
     C                     MOVE CLCFRQ    ##CFRQ
     C                     MOVE '1'       *IN20
      *
     C                     Z-ADD1         I       10
     C           CLCFRQ    LOKUPFRQ,I                    10
     C                     END
      *
      * Archive All reports for this frequency
      *
     C           *IN10     IFEQ '1'
     C                     EXSR SARCH
     C                     ELSE
     C           CWRKSK    SETGTMACWRKL1
     C                     END
      *
      * end IF (E.O.F)
     C                     END
      *
      * end DO (E.O.F)
     C                     END
      **********************************************************
      *                                                                   S01382
      * Close PF/ODBUILD at end of program                                S01382
     C                     CLOSEODBUILD                                   S01382
      *
      * End Of Pgm
     C                     MOVE '1'       *INLR
      *
      ***********************************************************
      /EJECT
      ***********************************************************
      *
      * SARCH -  Read Collation Reports for the frequency
      *=========
      *      called by :  Mainline processing
      *
      *      calls     :  SDET - write Bureau Header/Details for
      *                          the Freq/Report
      *                   SDLT - Delete all Collation Work File Records for
      *                          the Archive ID/Frequency/Report processed.
      ***********************************************************
      *
     C           SARCH     BEGSR
      *
     C                     MOVE '0'       *IN51
      *
      * Read (MACWRKL1) work records untill ALL records for
      *  the frequency are read.
      *
      *         =====================
     C           *IN51     DOUEQ'1'
      *         =====================
      *
      * Access SUFFIXES & 'Reports For Collation' File (MARCOLL1)
      *  for each Report read
      *
     C           CLRSFX    IFNE ##RSFX
     C                     MOVE CLRSFX    ##RSFX
     C                     MOVE '1'       *IN20
      *
     C           RCOLK     CHAINFCRSFXL1             5659
      * DB error ??
     C           *IN56     IFEQ '1'
     C           *IN59     OREQ '1'
     C           *LOCK     IN   LDA                                       S01382
     C                     MOVE 'FCRSFXL1'DBFILE
     C                     MOVELRCOL      DBKEY
     C                     Z-ADD3         DBASE             *************
     C                     OUT  LDA                         ************* S01382
     C                     SETON                     U7U8LR * DB err 03 *
     C                     RETRN                            *************
     C                     END
      *
     C           RCOLK     CHAINMARCOLL1             5656
      *
     C           *IN56     IFEQ '0'
     C                     MOVELD4CRDS    DYPFTX
     C                     END
      *
     C                     END
      *
      ******
      * SDET  -  Write Bureau Header & Detail records for
      *           the Archive-ID/Frequency/Report using the
      *           details from the Collation Work File (MACWRKL1)
      *
     C                     EXSR SDET
      *
      ********
      *  Read next Collation Work File (MACWRKL1) record
      *  for the Archive-ID/Frequency
      *
     C                     MOVEA'00'      *IN,01
     C           CWRKSK    READEMACWRKL1               5951
      * File error ??
     C           *IN59     IFEQ '1'
     C           *LOCK     IN   LDA                                       S01382
     C                     MOVE 'MACWRKL1'DBFILE
     C                     MOVELCWRK      DBKEY
     C                     Z-ADD4         DBASE             *************
     C                     OUT  LDA                         ************* S01382
     C                     SETON                     U7U8LR * DB err 04 *
     C                     RETRN                            *************
     C                     END
      *
      * end DO (*in51)
      *         =====================
     C                     END
      *         =====================
      *
     C                     ENDSR
      *
      ***********************************************************
      /EJECT
      ***********************************************************
      *
      * SDET  -  Write Bureau Header & Detail records for
      *           the Archive ID., Frequency and Report.
      *
      *      called by :  SARCH
      *
      ***********************************************************
      *
     C           SDET      BEGSR
      *
      * Write a Bureau HEADER record
      *
     C           *IN20     IFEQ '1'
     C                     MOVE '0'       *IN20
      *
      * ( Microfiche   )
     C           EAMEDM    IFEQ 'M'
     C           EAMEDM    OREQ 'B'                                       S01382
     C                     MOVE CLARID    MAARID
     C                     MOVE CLRNAM    MARNAM
     C                     MOVE CLRSFX    MARSFX
      *
     C                     MOVE @@DATA    MAADAT
     C                     Z-ADD@@DATN    MAMDAT
     C                     MOVELEAADSC    MAADSC
     C                     MOVE EADEPT    MADEPT
     C                     MOVELDYPFTX    MAPFTX
      *
     C***********          MOVE *BLANKS   OPTD                            S01382
     C***********          MOVELFICH      OPTD                            S01382
     C                     MOVE *BLANKS   FICD                            S01382
     C                     MOVELFICH      FICD                            S01382
     C                     WRITEMAFICDD0
     C                     END                                            S01382
      *                                                                   S01382
      * ( Optical Disk )
     C***********          ELSE                                           S01382
      ***********                                                         S01382
     C***********          MOVE CLARID    M1ARID                          S01382
     C***********          MOVE CLRNAM    M1RNAM                          S01382
     C***********          MOVE CLRSFX    M1RSFX                          S01382
      ***********                                                         S01382
     C***********          MOVE CLFMTP    M1FMTP                          S01382
     C***********          MOVE CLPLEN    M1PLEN                          S01382
     C***********          MOVE CLLPI     M1LPI                           S01382
      ***********                                                         S01382
     C***********          MOVE @@DATA    M1ADAT                          S01382
     C***********          Z-ADD@@DATN    M1MDAT                          S01382
     C***********          MOVELEAADSC    M1ADSC                          S01382
     C***********          MOVE EADEPT    M1DEPT                          S01382
     C***********          MOVELDYPFTX    M1PFTX                          S01382
     C***********          MOVELOPTH      OPTD                            S01382
     C***********          WRITEMAOPTDD0                                  S01382
      *                                                                   S01382
     C           EAMEDM    IFEQ 'O'                                       S01382
     C           EAMEDM    OREQ 'B'                                       S01382
      *                                                                   S01382
      * Call MA0640 to create header record                               S01382
     C                     MOVE *BLANKS   IHEAD                           S01382
     C                     MOVE 'COLL'    OUTPUT                          S01382
     C**********           MOVE CLRNAM    REPORT                                      S01382 BUG5869
     C                     MOVE CLRSFX    REPORT                                             BUG5869
     C                     MOVE CLRSFX    PRTFNM                          S01382
     C           *IN56     IFEQ '0'                                       S01382
     C                     MOVE D4CORP    JOBNAM                          S01382
     C                     END                                            S01382
      *                                                                   S01382
     C                     OUT  LDA                                       S01382
     C                     CALL 'MA0640'                                  S01382
     C                     PARM           IHEAD                           S01382
     C                     PARM           @RUN    1                       S01382
     C           *LOCK     IN   LDA                                       S01382
      *                                                                   S01382
      * If error has occurred in called program, exit this program        S01382
     C           @RUN      IFNE ' '                                       S01382
     C                     DUMP                                           S01382
     C                     SETON                     U7U8LR               S01382
     C                     RETRN                                          S01382
     C                     END                                            S01382
      *                                                                   S01382
     C                     MOVE '*'       ODCCHR                          S01382
     C                     MOVE *BLANKS   OPTD                            S01382
     C                     MOVELIHEAD     OPTD                            S01382
     C                     WRITEODBUILD0                                  S01382
     C                     MOVE 'Y'       RECOUT                          S01382
     C                     END
      *
     C                     END
      *
      ******
      * Delete Processed Collation record
      *
     C           *IN01     IFEQ '1'
     C                     DELETMACHDRD0               59
      * File error ??
     C           *IN59     IFEQ '1'
     C           *LOCK     IN   LDA                                       S01382
     C                     MOVE 'MACHDRD0'DBFILE
     C                     MOVELCWRK      DBKEY
     C                     Z-ADD5         DBASE             *************
     C                     OUT  LDA                         ************* S01382
     C                     SETON                     U7U8LR * DB err 05 *
     C                     RETRN                            *************
     C                     END
      * (*in01 End)
     C                     END
      /EJECT
      *
     C           *IN02     IFEQ '1'
      *
      * Setup DETAIL output fields
      * Write Microfich/Optical-Disk Bureau DETAILS File records
      *
     C***********          MOVE CLCCHR    MACCHR                          S01382
     C***********          MOVE CLRDTL    MARDTL                          S01382
     C***********          MOVE *BLANKS   F00021                          S01382
      *
      * ( Microfiche   )
      *
     C           EAMEDM    IFEQ 'M'
     C           EAMEDM    OREQ 'B'                                       S01382
     C                     MOVE CLCCHR    MACCHR                          S01382
     C                     MOVE CLRDTL    MARDTL                          S01382
     C                     WRITEMAFICDD0
     C                     END                                            S01382
      * ( Optical Disk )
     C***********          ELSE                                           S01382
     C***********          WRITEMAOPTDD0                                  S01382
     C           EAMEDM    IFEQ 'O'                                       S01382
     C           EAMEDM    OREQ 'B'                                       S01382
     C                     MOVE *BLANKS   OPTD                            S01382
     C                     MOVE CLCCHR    ODCCHR                          S01382
     C                     MOVELCLRDTL    OPTD                            S01382
     C                     WRITEODBUILD0                                  S01382
     C                     MOVE 'Y'       RECOUT                          S01382
     C                     END
      *
      * Delete Processed Collation record
      *
     C                     DELETMACDTLD0               59
      * File error ??
     C           *IN59     IFEQ '1'
     C           *LOCK     IN   LDA                                       S01382
     C                     MOVE 'MACDTLD0'DBFILE
     C                     MOVELCWRK      DBKEY
     C                     Z-ADD6         DBASE             *************
     C                     OUT  LDA                         ************* S01382
     C                     SETON                     U7U8LR * DB err 06 *
     C                     RETRN                            *************
     C                     END
      *
      * (*in02 End)
     C                     END
      *
     C                     ENDSR
      ***********************************************************
      /EJECT
** PCMD                                                                   S01382
CPYF FROMFILE(ODBUILD) TOFILE(MAOPTDPD) TOMBR(          ) MBROPT(*ADD    )            S01382 BUG5869
CLRPFM FILE(ODBUILD)                                                      S01382
** @CPY   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
