      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MC Period Set Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas  Management Accounting Module
     A*                                                               *
     A*  MC0100DF - PERIOD SET & DETAILS MAINTENANCE                  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     LAST AMEND NO. XXXXXX            DATE  DDMMMYY            *
     A*     PREV AMEND NO. XXXXXX            DATE  DDMMMYY            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
     A*
     A                                      CA03
     A                                      CF09
     A                                      CF12
      *
      **  Screen Header 1
      *
     A          R MC0100F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'MC0100'
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A            SWSID          3A  O  2  2
     A                               O  2 30'PERIOD SETS +
     A                                       MAINTENANCE'
     A                                      DSPATR(HI)
     A                               O  2 71TIME
      *
      **  Screen Header 2
      *
     A          R MC0100F2
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'MC0100'
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A            SWSID          3A  O  2  2
     A                               O  2 30'PERIOD SETS +
     A                                       ENQUIRY'
     A                                      DSPATR(HI)
     A                               O  2 71TIME
      *
      **  Period Set Header & Positioner Line
      *
     A          R MC0100F3
     A                                      PUTOVR
     A                                      OVERLAY
     A                                  4  8'Set   Set Name'
     A                                  4 46'Periods   Last Defined'
     A                                  4 70'Referenced'
     A                                  5  8'Code'
     A                                  5 45'Per Annum    End Date'
     A                                  5 70'Elsewhere?'
     A            SPSTCB         1A  B  6  9
     A                                      DSPATR(HI)
     A            SPSNMB        30A  B  6 14
     A                                      DSPATR(HI)
     A            SPPANB         2N 0B  6 49
     A                                      DSPATR(HI)
     A            SLDEDB         7A  B  6 58
     A                                      DSPATR(HI)
     A            SREELB         1A  B  6 74
     A                                      DSPATR(HI)
      *
      **  Subfile Details format
      *
     A          R MC0100S1                  SFL
     A            SACTN     R        B  8 05REFFLD(CHTP)
     A                                      DSPATR(HI)
     A            SPSTC          1A  O  8  9
     A                                      DSPATR(HI)
     A            SPSNM         30A  O  8 14
     A                                      DSPATR(HI)
     A            SPPAN          2N 0O  8 49
     A                                      DSPATR(HI)
     A            SLDED          7A  O  8 58
     A                                      DSPATR(HI)
     A  13                                  DSPATR(RI)
     A            SREEL          1A  O  8 74
     A                                      DSPATR(HI)
      *
      **  Subfile Control
      *
     A          R MC0100C1                  SFLCTL(MC0100S1)
     A                                      SFLSIZ(0036)
     A                                      SFLPAG(0014)
     A  02                                  SFLDSP
     A  03                                  SFLDSPCTL
     A N03N02
     AO 05                                  SFLCLR
     A  04                                  SFLEND
     A  06                                  SFLMSG('No data to display')
     A
     A                                      CF05
     A                                      OVERLAY
     A                                      PUTOVR
     A
     A            MSGDSP        76A  O 23 01OVRDTA
      *
      **  Screen 2 Detail
      *
     A          R MC0100F4
     A  15                                  CF10
     A                                      PUTOVR
     A                                      BLINK
     A                                      OVERLAY
      *
      *
     A                                  5  2'Set Code.......................:'
     A            SPSTC          1A  B  5 35
     A                                      DSPATR(HI)
     A  10
     AO 11                                  DSPATR(PR)
     A N10N11N30                            DSPATR(UL)
     A  30                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                  7  2'Set Name.......................:'
     A            SPSNM         30A  B  7 35
     A                                      DSPATR(HI)
     A  10                                  DSPATR(PR)
     A N10                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8  2'Periods per Annum..............:'
     A            SPPAN          2N 0B  8 35
     A                                      DSPATR(HI)
     A  10
     AO 11                                  DSPATR(PR)
     A N10N11N31                            DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                  9  2'Periods per Term 1.............:'
     A            SPTM1          2N 0B  9 35
     A                                      DSPATR(HI)
     A  10
     AO 14                                  DSPATR(PR)
     A N10N34N14                            DSPATR(UL)
     A  34                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                 10  2'Periods per Term 2.............:'
     A            SPTM2          2N 0B 10 35
     A                                      DSPATR(HI)
     A  10
     AO 14                                  DSPATR(PR)
     A N10N35N14                            DSPATR(UL)
     A  35                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                 11  2'Frequency Code.................:'
     A            SPFCD          1A  B 11 35
     A                                      DSPATR(HI)
     A  10                                  DSPATR(PR)
     A N10N36                               DSPATR(UL)
     A  36                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                 12  2'Frequency Base Date............:'
     A            SPFBD          2N 0B 12 35
     A                                      DSPATR(HI)
     A  10                                  DSPATR(PR)
     A N10N38                               DSPATR(UL)
     A  38                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                 13  2'History Period (Years).........:'
     A            SHPYR          1N 0B 13 35
     A                                      DSPATR(HI)
     A  10                                  DSPATR(PR)
     A N10N39                               DSPATR(UL)
     A  39                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                 14  2'Start Date.....................:'
     A            SSPDD          7A  B 14 35
     A                                      DSPATR(HI)
     A  10
     AO 11                                  DSPATR(PR)
     A N10N11N32                            DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                 15  2'First Period Year..............:'
     A            SFPYR          4N 0B 15 35
     A                                      DSPATR(HI)
     A  10
     AO 11                                  DSPATR(PR)
     A N10N11N33                            DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A                                      CHECK(FE)
     A                                      OVRDTA
      *
     A            MSGDSP        76A  O 23 01OVRDTA
      *
      **  Clear screen
      *
     A          R CLSCREEN
     A                                  1  2' '
      *
      **  Message subfile
      *
     A          R MC0100S0                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      **  Message subfile control
      *
     A          R MC0100C0                  TEXT('MESSAGE SFL CTL')
     A                                      OVERLAY
     A                                      SFLCTL(MC0100S0)
     A                                      SFLDSP
     A                                      SFLINZ
        60
     AON60                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
                  SPGMQ                     SFLPGMQ
