      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MC Select Periodic Sets')
     A*****************************************************************
     A*                                                               *
     A*  Midas  Management Accounting Module
     A*                                                               *
     A*  MC0100S# - SELECT PERIOD SETS                                *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*      PREV AMEND NO. NNNNNN           DATE DDMMMYY             *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*      MD046248 - Finastra Rebranding                           *
     A*      XXXXXX - XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX              *
     A*                                                               *
     A*****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(34 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1PTM1         2  0H      TEXT('Periods per Term 1')
     A            #1PTM2         2  0H      TEXT('Periods per Term 2')
     A            #1PFCD         1   H      TEXT('Period Frequency Code')
     A            #1PFBD         2  0H      TEXT('Period Freq. Base Date')
     A            #1HPYR         1  0H      TEXT('History Period (Years)')
     A            #1FPYR         4  0H      TEXT('First Period Year')
     A            #1TYLC         1   H      TEXT('Type of last change')
     A            #1LCD          5  0H      TEXT('Last change date')
     A            #1SEL          1   B  9  2
     A                                      VALUES('1')
     A                                      CHECK(AB)
     A  34                                  DSPATR(RI PC) COLOR(WHT)
     A N34                                  DSPATR(UL HI) COLOR(GRN)
     A            #1PSTC         1      9 11TEXT('Period Set Code')
     A                                      DSPATR(HI) COLOR(WHT)
     A            #1PSNM        30      9 22TEXT('Period Set Name')
     A                                      DSPATR(HI) COLOR(WHT)
     A            #1PPAN         2  0   9 60TEXT('Periods per Annum')
     A                                      EDTCDE(3)
     A                                      DSPATR(HI) COLOR(WHT)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2PSTC')
     A                                      SETOF(32 'Invalid: #2PSNM')
     A                                      SETOF(33 'Invalid: #2PPAN')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'MC0100S1'
     A                                      COLOR(WHT)
     A            ##MRDT         7      1 72TEXT('Midas Rundate')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A                                  2 26'Select Periodic Processing Sets'
     A                                      DSPATR(HI) COLOR(WHT)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      COLOR(WHT)
     A            #2PTM1         2  0H      TEXT('Periods per Term 1')
     A            #2PTM2         2  0H      TEXT('Periods per Term 2')
     A            #2PFCD         1   H      TEXT('Period Frequency Code')
     A            #2PFBD         2  0H      TEXT('Period Freq. Base Date')
     A            #2HPYR         1  0H      TEXT('History Period (Years)')
     A            #2FPYR         4  0H      TEXT('First Period Year')
     A            #2TYLC         1   H      TEXT('Type of last change')
     A            #2LCD          5  0H      TEXT('Last change date')
     A                                  5  8'Periodic'
     A                                      COLOR(TRQ)
     A                                  5 22'Periodic Set Name'
     A                                      COLOR(TRQ)
     A                                  5 56'Periods'
     A                                      COLOR(TRQ)
     A                                  6  8'Set Code'
     A                                      COLOR(TRQ)
     A                                  6 56'per Annum'
     A                                      COLOR(TRQ)
     A            #2PSTC         1   B  7 11TEXT('Period Set Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC) COLOR(WHT)
     A N31                                  DSPATR(UL HI) COLOR(GRN)
     A N25                                  OVRATR
     A            #2PSNM        30   B  7 22TEXT('Period Set Name')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC) COLOR(WHT)
     A N32                                  DSPATR(UL HI) COLOR(GRN)
     A N25                                  OVRATR
     A            #2PPAN         2  0B  7 60TEXT('Periods per Annum')
     A                                      EDTCDE(4)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC) COLOR(WHT)
     A N33                                  DSPATR(UL HI) COLOR(GRN)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select request   F3=Exit'
     A                                      COLOR(WHT)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
