      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MC Period Details Maint. - insert')
     A********************************************************************
     A*                                                                  *
     A*  Midas  Management Accounting Module                  *
     A*                                                                  *
     A*     MC0115DF - Period details maintenance                        *
     A*                                                                  *
     A*  (c) Finastra International Limited 2001                         *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     PREV AMEND NO. XXXXXX            DATE  00XXX00               *
     A*                                      DATE                        *
     A*                                                                  *
     A*------------------------------------------------------------------*
     A*  MD046248 - Finastra Rebranding                                  *
     A*                                                                  *
     A*------------------------------------------------------------------*
     A*
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CF03
     A                                      CF12
     A                                      CF19
      *
      *  Format: headings display
     A          R HEADING
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            USER          10A  O  1  2
     A                                  1 13'MC0115'
     A            SMRDT     R        O  1 73REFFLD(MRDT)
     A            WSID          10A  O  2  2
     A                                  2 28'PERIOD DETAILS +
     A                                      MAINTENANCE'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *
      *  Format: Set details
     A          R SETDTL
     A                                      OVERLAY
     A                                  4 27'Set Code'
     A            SPSTC          1A  O  4 36DSPATR(HI)
     A                                  4 39'Period Year'
     A            SPDYR          4N 0O  4 51DSPATR(HI)
      *
      *  Format: subfile for period details (size = max. no. of
      *  periods per annum.)
     A          R SFLRCD                    SFL
     A            SPDNM         10A  B  9 11TEXT('Period Name')
     A                                      DSPATR(HI)
     A            SEOPDT         7A  B  9 26TEXT('End of Period Date')
     A                                      DSPATR(HI)
     A            SPDNY          2A  O  9 38TEXT('Period Number')
     A                                      DSPATR(HI)
     A            SSOT1          1A  O  9 48TEXT('Start of Term 1')
     A  83                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A            SSOT2          1A  O  9 59TEXT('Start of Term 2')
     A  84                                  DSPATR(ND)
     A                                      DSPATR(HI)
      *
      *  Format: subfile control.
     A          R SFLCTLR                   SFLCTL(SFLRCD)
     A                                      SFLSIZ(52)
     A                                      SFLPAG(13)
     A N81                                  SFLDSP
     A N81                                  SFLDSPCTL
     A  81                                  SFLCLR
     A N81                                  SFLEND
     A                                      OVERLAY
     A                                  6 11'Period Name'
     A                                  6 26'Period'
     A                                  6 36'Period'
     A                                  6 45'Start of'
     A  83                                  DSPATR(ND)
     A                                  6 56'Start of'
     A  84                                  DSPATR(ND)
     A                                  7 25'End Date'
     A                                  7 36'Number'
     A                                  7 46'Term 1?'
     A  83                                  DSPATR(ND)
     A                                  7 57'Term 2?'
     A  84                                  DSPATR(ND)
      *
      *  Format: Command keys details.
     A          R FOOTER                    TEXT('Command keys.')
     A                                      OVERLAY
     A                                 23  2'F3=Main Menu'
     A                                 23 16'F12=Period Set Screen'
     A                                 23 39'F19=Previous Year'
      *
      *  Format: Message Subfile
     A          R SFLRCDM                   TEXT('Message subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
     A          R SFLCTLM                   TEXT('Message subfile ctl')
     A                                      OVERLAY
     A                                      SFLCTL(SFLRCDM)
     A                                      SFLDSP
     A                                      SFLINZ
        60
     AON60                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
                  SPGMQ                     SFLPGMQ
