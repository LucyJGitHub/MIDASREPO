      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MC Closed Period Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Management Accounts Module                           *
      *                                                               *
      *  DSPF/MC0120DF - Closed Period Maintenance                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CMC001 *CREATE     Date 18MAR96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CMC001 - Management Accounts Enhancement for PCA.            *
      *                                                               *
      *****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CA03(03 'Exit')
     A                                      CA12(12 'Previous')
     A                                      INDARA
     A*
     A          R MC0120F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            SUSER         10A  O  1  2TEXT('User ID')
     A                                  1 13'MC0120'
     A            SMRDT     R           1 72REFFLD(MRDT)
     A            SWSID         10A  O  2  2TEXT('Workstation ID')
     A                                  2 71TIME
     A                                  2 27'CLOSED PERIOD MAINTENANCE'
     A                                      DSPATR(HI UL)
     A                                  4 20'Set'
     A                                  5 20'Code   Set Name'
     A            SPSTC          1A  O  7 21TEXT('Period Set Code')
     A                                      DSPATR(HI)
     A            SPSNM         30A  O  7 27TEXT('Period Name')
     A                                      DSPATR(HI)
     A*
     A          R MC0120S0                  TEXT('Subfile Format')
     A                                      SFL
     A*
     A            SSTAT          1A  B 12 13TEXT('Status')
     A                                      AUTO(RA)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A            SYEAR          4A  O 12 18TEXT('Period Year')
     A                                      DSPATR(HI)
     A            SPDNY          2A  O 12 25TEXT('Period Number')
     A                                      DSPATR(HI)
     A            SPDNM         10A  O 12 32TEXT('Period Name')
     A                                      DSPATR(HI)
     A            SEPDD          7A  O 12 46TEXT('Period End date')
     A                                      DSPATR(HI)
     A            SCLSD          7A  O 12 57TEXT('Closure Date')
     A                                      DSPATR(HI)
     A            SEPDDN         5S 0H      TEXT('Period End Date')
     A            SCLSDN         5S 0H      TEXT('Closure Date')
     A*
     A          R MC0120C0                  TEXT('Subfile Control Format')
     A                                      OVERLAY
     A                                      SFLCTL(MC0120S0)
     A  22                                  SFLDSP
     A  23                                  SFLDSPCTL
     A N22N23                               SFLCLR
     A  24
     AON24                                  SFLEND
     A                                      SFLSIZ(10)
     A                                      SFLPAG(9)
     A*
     A                                  9 21'Period'
     A                                  9 59'Date'
     A                                 10 11'Status Year Number'
     A                                 10 32'Period Name   End Date'
     A                                 10 58'Closed'
     A*
     A          R MC0120F1                  TEXT('Screen footer')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 24  2'F3=Main Menu'
     A                                 24 16'F12=Period Set Screen'
     A                                 24 40'C=Close  O=Open'
     A*
     A          R MC0120S1                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
     A*
     A*================================================================
     A*
     A          R MC0120C1                  TEXT('Program messages')
     A                                      SFLCTL(MC0120S1)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
