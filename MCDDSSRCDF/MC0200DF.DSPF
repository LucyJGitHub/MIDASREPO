      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MC Control Groups Maintenance-select')
     A*****************************************************************
     A*                                                               *
     A*  Midas  Management Accounting Module
     A*                                                               *
     A*  MC0200DF - Historic balance control groups maintenance       *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     PREV AMEND NO. XXXXXX            DATE  00XXX00            *
     A*                                      DATE                     *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
     A                                      CA03
     A                                      CA05
     A                                      CF09
      *
      *   Format: General headings display
     A          R HEADING                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            SUSER         10A  O  1  2TEXT('User ID')
     A                                  1 13'MC0200'
     A            SMRDT          7A  O  1 72TEXT('Run date')
     A            SWSID         10A  O  2  2
     A                               O  2 28'CONTROL GROUPS +
     A                                       MAINTENANCE'
     A                                      DSPATR(HI)
     A                               O  2 71TIME
      *
      * Format: subfile for control group summary
     A          R SFLRCD                    SFL
     A  82                                  SFLNXTCHG
     A            SACTN          1A  B  7  5TEXT('Selection field')
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI)
     A            SCGCD          2A  O  7  9TEXT('Control Group Code')
     A                                      DSPATR(HI)
     A            SCGDS         30A  O  7 15TEXT('Control Group Description')
     A                                      DSPATR(HI)
     A            SPSTC          1A  O  7 48TEXT('Period Set Code')
     A                                      DSPATR(HI)
     A            SPSTB          1A  O  7 56TEXT('Posting Basis')
     A                                      DSPATR(HI)
     A            SCCYB          3A  O  7 65TEXT('Currency of Balances')
     A                                      DSPATR(HI)
      *
      * Format: Subfile control
     A          R SFLCTLR                   SFLCTL(SFLRCD)
     A                                      SFLSIZ(16)
     A                                      SFLPAG(15)
     A  81                                  SFLCLR
     A  81                                  ERASE(SFLRCD)
     A N81                                  SFLDSPCTL
     A N81N06                               SFLDSP
     A N81N06                               SFLEND
     A                                      PUTOVR
     A                                      OVERLAY
     A            SPOSN          4  0H      TEXT('Subfile position')
     A                                      SFLRCDNBR(CURSOR)
     A                                  4  8'Group  Group'
     A                                  4 21'Description'
     A                                  4 47'Set   Posting'
     A                                  4 62'Currency of'
     A                                  5  8'Code'
     A                                  5 47'Code   Basis'
     A                                  5 63'Balances'
     A                                  6 28'* NO DETAILS TO DISPLAY *'
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N06                                  DSPATR(ND)
      *
      *  Format: Command keys details.
     A          R FOOTER                    TEXT('Command keys.')
     A                                      OVERLAY
     A                                 23  2'A=Amend'
     A                                 23 11'D=Delete'
     A                                 23 21'E=Enquire'
     A                                 23 32'F3=Main Menu'
     A                                 23 46'F5=Refresh'
     A                                 23 58'F9=Go to "Add" Mode'
      *
      *  Format: Message subfile
     A          R SFLRCDM                   TEXT('Message subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      *  Format: Message subfile control
     A          R SFLCTLM                   TEXT('Message subfile ctl')
     A                                      OVERLAY
     A                                      SFLCTL(SFLRCDM)
     A                                      SFLDSP
     A                                      SFLINZ
     A  81
     AON81                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A            SPGMQ                     SFLPGMQ
