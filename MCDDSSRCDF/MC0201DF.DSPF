      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MC Control Groups Maintenance-detail')
     A*****************************************************************
     A*                                                               *
     A*  Midas  Management Accounting Module
     A*                                                               *
     A*  MC0201DF - Historic balance control groups details           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     LAST AMEND NO. XXXXXX            DATE  00XXX00            *
     A*     PREV AMEND NO.                   DATE                     *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CHGINPDFT
      *
     A                                      CA03
     A  60                                  CF10
     A                                      CF12
      *
      *   Format: Detail screen
     A          R DETAIL                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
      *
      *
     A            SUSER         10A  O  1  2TEXT('User ID')
     A                                  1 13'MC0201'
     A            SMRDT          7A  O  1 72TEXT('Run date')
     A            SWSID         10A  O  2  2TEXT('Workstation ID')
     A N54
     AO 53 54                           2 28'CONTROL GROUPS +
     A                                       MAINTENANCE'
     A                                      DSPATR(HI)
     A  51                              2 55'- INSERT'
     A                                      DSPATR(HI)
     A  52                              2 55'- AMEND'
     A                                      DSPATR(HI)
     A  53                              2 55'- DELETE'
     A                                      DSPATR(HI)
     A  54N53                           2 30'CONTROL GROUPS +
     A                                      ENQUIRY'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                  5  2'Group Code......................:'
     A            SCGCD          2A  B  5 36TEXT('Group code')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N61                               DSPATR(HI)
     A  51N61                               DSPATR(UL)
     A     61                               DSPATR(RI PC)
     A                                  7  2'Group Description...............:'
     A            SCGDS         30A  B  7 36TEXT('Group description')
     A                                      CHECK(LC)
     A  54                                  DSPATR(PR)
     A N54                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8  2'Set Code........................:'
     A            SPSTC          1A  B  8 36TEXT('Set code')
     A N51                                  DSPATR(PR)
     A  62                                  DSPATR(RI PC)
     A  51N62                               DSPATR(UL)
     A N62                                  DSPATR(HI)
     A                                  9  2'Posting Basis...................:'
     A            SPSTB          1A  B  9 36TEXT('Posting basis')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N63                               DSPATR(HI)
     A  51N63                               DSPATR(UL)
     A  51 63                               DSPATR(RI PC)
     A                                 10  2'Number of Periods to +
     A                                       Retain.....:'
     A            SNPTR          3  0B 10 36TEXT('Number of periods')
     A                                      EDTCDE(4)
     A  54                                  DSPATR(PR)
     A  54
     AON54N64                               DSPATR(HI)
     A N54N64                               DSPATR(UL)
     A N54 64                               DSPATR(RI PC)
     A                                 10 50'Average DR/CR Balances.:'
     A            SADCB          1A  B 10 75TEXT('Average DR/CR balances')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N65                               DSPATR(HI)
     A  51N65                               DSPATR(UL)
     A  51 65                               DSPATR(RI PC)
     A                                 11  2'Currency of Balances............:'
     A            SCCYB          3A  B 11 36TEXT('Currency of balances')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N66                               DSPATR(HI)
     A  51N66                               DSPATR(UL)
     A  51 66                               DSPATR(RI PC)
     A                                 11 50'Update Audit...........:'
     A            SUPAU          1A  B 11 75TEXT('Update audit')
     A  54                                  DSPATR(PR)
     A  54
     AON54N67                               DSPATR(HI)
     A N54N67                               DSPATR(UL)
     A N54 67                               DSPATR(RI PC)
     A                                 12  2'Analysis by Account Codes.......:'
     A            SABAC          1A  B 12 36TEXT('Analysis by a/c codes')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N68                               DSPATR(HI)
     A  51N68                               DSPATR(UL)
     A  51 68                               DSPATR(RI PC)
     A                                 12 50'Average over A/C Open..:'
     A            SAOAO          1A  B 12 75TEXT('Average over a/c open')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N69                               DSPATR(HI)
     A  51N69                               DSPATR(UL)
     A  51 69                               DSPATR(RI PC)
     A                                 13 14'Currencies..........:'
     A            SABCY          1A  B 13 36TEXT('Analysis by currencies')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N70                               DSPATR(HI)
     A  51N70                               DSPATR(UL)
     A  51 70                               DSPATR(RI PC)
     A                                 13 42'Include Asset Accounts.........:'
     A            SIAAC          1A  B 13 75TEXT('Include asset a/c"s')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N71                               DSPATR(HI)
     A  51N71                               DSPATR(UL)
     A  51 71                               DSPATR(RI PC)
     A  40                             14 14'Branches............:'
     A  40        SABBR          1A  B 14 36TEXT('Analysis by branches')
     A N51 40                               DSPATR(PR)
     A N51 40
     AO 51N72 40                            DSPATR(HI)
     A  51N72 40                            DSPATR(UL)
     A  51 72 40                            DSPATR(RI PC)
     A                                 14 50'Liability Accounts.....:'
     A            SILAC          1A  B 14 75TEXT('Include liab. a/c"s')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N73                               DSPATR(HI)
     A  51N73                               DSPATR(UL)
     A  51 73                               DSPATR(RI PC)
     A                                 15 14'Customers...........:'
     A            SABCU          1A  B 15 36TEXT('Analysis by customers')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N74                               DSPATR(HI)
     A  51N74                               DSPATR(UL)
     A  51 74                               DSPATR(RI PC)
     A                                 15 50'Income Accounts........:'
     A            SIIAC          1A  B 15 75TEXT('Include inc. a/c"s')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N75                               DSPATR(HI)
     A  51N75                               DSPATR(UL)
     A  51 75                               DSPATR(RI PC)
     A                                 16 14'Account Sequences...:'
     A            SABAS          1A  B 16 36TEXT('Analysis by a/c seq.')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N76                               DSPATR(HI)
     A  51N76                               DSPATR(UL)
     A  51 76                               DSPATR(RI PC)
     A                                 16 50'Expense Accounts.......:'
     A            SIEAC          1A  B 16 75TEXT('Include exp. a/c"s')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N77                               DSPATR(HI)
     A  51N77                               DSPATR(UL)
     A  51 77                               DSPATR(RI PC)
     A  41                             17 14'Profit Centres......:'
     A  41        SABPC          1A  B 17 36TEXT('Analysis by profit ctr')
     A N51 41                               DSPATR(PR)
     A N51 41
     AO 51N78 41                            DSPATR(HI)
     A  51N78 41                            DSPATR(UL)
     A  51 78 41                            DSPATR(RI PC)
     A                                 17 50'Control Accounts.......:'
     A            SICAC          1A  B 17 75TEXT('Include control a/c"s')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N79                               DSPATR(HI)
     A  51N79                               DSPATR(UL)
     A  51 79                               DSPATR(RI PC)
     A                                 18 14'Books...............:'
     A            SABBK          1A  B 18 36TEXT('Analysis by book')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N80                               DSPATR(HI)
     A  51N80                               DSPATR(UL)
     A  51 80                               DSPATR(RI PC)
     A                                 18 50'Memorandum Accounts....:'
     A            SIMAC          1A  B 18 75TEXT('Include memo a/c"s')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N81                               DSPATR(HI)
     A  51N81                               DSPATR(UL)
     A  51 81                               DSPATR(RI PC)
     A                                 19 14'Transaction Types...:'
     A            SABTT          1A  B 19 36TEXT('Analysis by Tx types')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N82                               DSPATR(HI)
     A  51N82                               DSPATR(UL)
     A  51 82                               DSPATR(RI PC)
     A                                 19 50'Trading Accounts.......:'
     A            SITAC          1A  B 19 75TEXT('Include trading a/c"s')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N83                               DSPATR(HI)
     A  51N83                               DSPATR(UL)
     A  51 83                               DSPATR(RI PC)
     A                                 20 14'Trans''n Sub-types...:'
     A            SABTS          1A  B 20 36TEXT('Analysis by Tx subtypes')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N84                               DSPATR(HI)
     A  51N84                               DSPATR(UL)
     A  51 84                               DSPATR(RI PC)
     A                                 20 50'Contingent Accounts....:'
     A            SICTA          1A  B 20 75TEXT('Include conting. a/c"s')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N85                               DSPATR(HI)
     A  51N85                               DSPATR(UL)
     A  51 85                               DSPATR(RI PC)
     A                                 21 14'Associated Customers:'
     A            SASCU          1A  B 21 36TEXT('Analysis by assoc. custs')
     A N51                                  DSPATR(PR)
     A N51
     AO 51N86                               DSPATR(HI)
     A  51N86                               DSPATR(UL)
     A  51 86                               DSPATR(RI PC)
     A                                 23  2'F3=Main Menu'
     A  60                             23 16'F10=Delete'
     A                                 23 28'F12=Previous'
      *
      *  Format: Message subfile
     A          R SFLRCDM                   TEXT('Message subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      *  Format: Message subfile control
     A          R SFLCTLM                   TEXT('Message subfile ctl')
     A                                      OVERLAY
     A                                      SFLCTL(SFLRCDM)
     A                                      SFLDSP
     A                                      SFLINZ
     A  99
     AON99                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A            SPGMQ                     SFLPGMQ
