      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MC Management Accounts Report Prompt')
      ***************************************************************
      *
      *  Midas - Managment Accounts Module                            *
      *
      *  MC0214DF - Management Accounts Review Prompt Screen
      *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. nnnnnn             Date DDMMMYY               *
      *
      ***************************************************************
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *                                                               *
      *****************************************************************
      *
      *  F U N C T I O N   O F   I N D I C A T O R S
      *
      * 03   - F3 exit
      * 10   - F10 delete
      * 12   - F12 return to previous screen
      * 14   - Seton if system is multibranched
      * 15   - Seton if Profit Centres are used
      * 29   - Delete mode, enable F10 and prevent update
      * 30   - Enquiry mode, prevent update
      * 35   - Validation error on branch
      * 36   - Validation error on currency
      * 37   - Validation error on account code
      * 38   - Validation error on client
      * 39   - Validation error on profit centre
      * 44   - Validation error on group code
      * 45   - Validation error on period year
      * 46   - Validation error on period number
      * 47   - Validation error on book code
      * 48   - Validation error on associated customer number
      * 49   - Validation error on account sequence number
      * 60   - Message subfile sflend indicator (dummy - never set)
      *
      ***************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
     A                                      CA03(03 'Exit program')
     A                                      CA12(12 'Initial screen')
      *
      *
      ***************************************************************
      *
      **  Screen Format
      *
      ***************************************************************
      *
     A          R MC0214F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A  29                                  CA10(10 'Delete')
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'MC0214'
     A            AGMRDT    R        O  1 72REFFLD(RUNA)
     A            SWSID         10A  O  2  2
     A                               O  2 24'MANAGEMENT ACCOUNTS +
     A                                       REPORT PROMPT'
     A                                      DSPATR(HI)
     A            STIME          6  0O  2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  5 11'Group Code ...........:'
     A            SGRCD          2A  B  5 36TEXT('Group')
     A N29N30                               DSPATR(UL)
     A  29
     AO 30                                  DSPATR(PR)
     A     44                               DSPATR(RI PC)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A                                  6 11'Period Year ..........:'
     A            SPDYE          4A  B  6 36TEXT('Period year')
     A N29N30                               DSPATR(UL)
     A  29
     AO 30                                  DSPATR(PR)
     A     45                               DSPATR(RI PC)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A                                  7 11'Period Number ........:'
     A            SPDNY          2A  B  7 36TEXT('Period number')
     A N29N30                               DSPATR(UL)
     A  29
     AO 30                                  DSPATR(PR)
     A     46                               DSPATR(RI PC)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A  14                              9 11'Branch Code ..........:'
     A  14        SBRCD     R        B  9 36REFFLD(BRCDFM)
     A N29N30 14                            DSPATR(UL)
     A  29 14
     AO 30 14                               DSPATR(PR)
     A  14 35                               DSPATR(RI PC)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A                                 10 11'Customer Number ......:'
     A            SCUST          6A  B 10 36TEXT('Customer number')
     A N29N30                               DSPATR(UL)
     A  29
     AO 30                                  DSPATR(PR)
     A     38                               DSPATR(RI PC)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A                                 11 11'Currency Code ........:'
     A            SCYCD     R        B 11 36REFFLD(CYCD)
     A N29N30                               DSPATR(UL)
     A  29
     AO 30                                  DSPATR(PR)
     A     36                               DSPATR(RI PC)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A                                 12 11'Account Code .........:'
     A**Start of change CGL029
     A**********  SACCD          4A  B 12 36TEXT('Account code')                              CGL029
     A            SACCD         10A  B 12 36TEXT('Account code')
     A**End of change CGL029
     A N29N30                               DSPATR(UL)
     A  29
     AO 30                                  DSPATR(PR)
     A     37                               DSPATR(RI PC)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A                                 13 11'Account Sequence .....:'
     A            SACSN          2A  B 13 36TEXT('A/C sequence number')
     A N29N30                               DSPATR(UL)
     A  29
     AO 30                                  DSPATR(PR)
     A     49                               DSPATR(RI PC)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A  15                             14 11'Profit Centre Code ...:'
     A  15        SPCCD          4A  B 14 36TEXT('Profit centre')
     A N29N30 15                            DSPATR(UL)
     A  29 15
     AO 30 15                               DSPATR(PR)
     A  15 39                               DSPATR(RI PC)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A                                 15 11'Book Code ............:'
     A            SBKCD     R        B 15 36REFFLD(BKBO)
     A N29N30                               DSPATR(UL)
     A  29
     AO 30                                  DSPATR(PR)
     A     47                               DSPATR(RI PC)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A                                 16 11'Transaction Type .....:'
     A            SOTTP     R        B 16 36REFFLD(OTTP)
     A N29N30                               DSPATR(UL)
     A  29
     AO 30                                  DSPATR(PR)
     A     40                               DSPATR(RI PC)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A                                 17 11'Transaction Sub-type .:'
     A            STSTY     R        B 17 36REFFLD(TSTY)
     A N29N30                               DSPATR(UL)
     A  29
     AO 30                                  DSPATR(PR)
     A     41                               DSPATR(RI PC)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A                                 18 11'Associated Customer ..:'
     A            SASCU          6A  B 18 36TEXT('Associated customer number')
     A N29N30                               DSPATR(UL)
     A  29
     AO 30                                  DSPATR(PR)
     A     48                               DSPATR(RI PC)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A                                 23  2'F3=Main Menu'
     A                                 23 16'F10=Delete'
     A N29                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 28'F12=Previous Menu'
      *
      ***************************************************************
      *
      ** Message subfile
      *
      ***************************************************************
     A          R MC0214S1                  TEXT('Message subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SSFKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      ***************************************************************
      *
      **  Message subfile control
      *
      ***************************************************************
     A          R MC0214C1                  TEXT('Message subfile control')
     A                                      OVERLAY
     A                                      SFLCTL(MC0214S1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  60
     AON60                                  SFLEND
     A                                      SFLSIZ(3)
     A                                      SFLPAG(1)
     A            SPGMQ                     SFLPGMQ
