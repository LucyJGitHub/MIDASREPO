      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MC Account Balances Prompt')
     A*****************************************************************
     A*                                                               *
     A*  Midas Management Accounts
     A*                                                               *
     A*  MCC84DF  - Management Account Balances Prompt                *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. EML108             Date 13Jan20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 XXXXXX             DATE XXXXXXX               *
     A*                 S01416             DATE 17MAY93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  EML108 - Upgrade FMD003 TO FBM2.1SP21.                       *
      *           Use /copy EML108_001 & EML108_002                   *
      *  FMD003 - Management Accounts Automatic Balance Update        *
     A*  MD046248 - Finastra Rebranding                               *
     A*  S01416 - Account Balances Report                             *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CF03(01)
     A                                      CF12(02)
     A  03                                  CF10(04)
     A          R MCC84MC                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A            SUSER         10A  O  1  2TEXT('User ID')
     A                                  1 13'MCC84P'
     A                                  1 27'Management Account Balances'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SRUNA          7A  O  1 72
     A            SWSID         10A  O  2  2
     A                                  2 71TIME
     A*
     A                                  6  8'Control Group'
     A            GRPCD          2A  B  6 65
     A                                      DSPATR(UL)
     A  17                                  DSPATR(RI)
     A  17                                  DSPATR(PC)
     A*
     A                                  7  8'Relative Period Number'
     A            SRPDN          4A  B  7 65
     A                                      CHECK(RB)
     A                                      DSPATR(UL)
     A  18                                  DSPATR(RI)
     A  18                                  DSPATR(PC)
     A*
     A                                  8  8'Actual(A), Base(B), H.O.(H) or Loc-
     A                                      al(L) Currency'
     A            CCY            1A  B  8 65
     A                                      DSPATR(UL)
     A  10                                  DSPATR(RI)
     A  10                                  DSPATR(PC)
     A*
     A                                  9  8'BSPL Report Set (? Select), or ''C-
     A                                      '' for Chart of A/c''s  '
     A            RSNO           1A  B  9 65
     A                                      DSPATR(UL)
     A  11                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
     A*
     A                                 10  8'BSPL Level (1 - 9)'
     A            RSLV           1A  B 10 65
     A                                      DSPATR(UL)
     A  12                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A*
     A                                 11  8'Report BSPL / Account Codes (Y, N)'
     A            RPCD           1A  B 11 65
     A                                      DSPATR(UL)
     A  13                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A*
     A                                 12  8'First Term:  Period(P), Year-
     A                                      (Y), Term 1(1), Term 2(2)'
     A            TERM1          1A  B 12 65
     A                                      DSPATR(UL)
     A  19                                  DSPATR(RI)
     A  19                                  DSPATR(PC)
     A*
     A                                 13  8'Second Term: Period(P), Year-
     A                                      (Y), Term 1(1), Term 2(2)'
     A            TERM2          1A  B 13 65
     A                                      DSPATR(UL)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A*
     A                                 14  8'Type: Balance(B), Average(A), -
     A                                      Movements(M)'
     A            TYPE           1A  B 14 65
     A                                      DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A*
     A*
      */COPY WNCPYSRC,EML108_001                                                              EML108
     A                                 23  2'Press F3 To Exit, F12 for Previous-
     A                                       screen'
     A  10
     AO 11
     AO 12
     AO 13
     AO 14        MSG           77A  O 24  2DSPATR(RI)
     A  17
     AO 18
     AO 19
     AO 20
      */COPY WNCPYSRC,EML108_002                                                              EML108
     AO 21        MSG2          77A  O 24  2DSPATR(RI)
     A*
