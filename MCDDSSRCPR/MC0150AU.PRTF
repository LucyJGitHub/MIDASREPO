      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MC Period Set Details - Audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Management Accounting Module
     A*                                                               *
     A*  PRTF/MC0150AU - PERIOD SETS AUDIT REPORT                     *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     LAST AMEND. NO. XXXXXX        DATE 00XXX00                *
     A*     PREV AMEND. NO.               DATE                        *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      **  Page Headings
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     1'REFERENCE'
     A                                    11'MC0150AU'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   108'DATE'
     A            BJMRDT    R            113REFFLD(MRDT)
     A                                   123'PAGE'
     A            PAGEA          4  0    128EDTCDE(Z)
     A                                      SPACEA(2)
     A  40                                54'PERIOD SETS AUDIT REPORT'
     A                                      UNDERLINE SPACEA(2)
     A  41                                54'PERIOD SETS REPORT'
     A                                      UNDERLINE
     A  40                                60'FILE CONTROLS'
      *
      **  Audit Report Details
      *
     A          R AUDDET                    SKIPB(10)
     A                                    49'Number of Insertions Today:'
     A            NOINS          5  0     80EDTCDE(Z) SPACEA(2)
     A                                    49'Number of Amendments Today:'
     A            NOAMD          5  0     80EDTCDE(Z) SPACEA(2)
     A                                    49'Number of Deletions Today:'
     A            NODEL          5  0     80EDTCDE(Z) SPACEA(2)
     A                                    49'Number of Records on File:'
     A            NOREC          5  0     80EDTCDE(Z)
      *
      **  No Details line
      *
     A          R NODETS                    SKIPB(20)
     A                                    52'*** NO DETAILS TO REPORT ***'
      *
      **  Database Error Details
      *
     A          R DBERR                     SKIPB(20)
     A                                    54'Database Error in Program'
     A            DBPGM         10        80
     A                                    91'At'
     A            DBASE          3S 0     94SPACEA(1)
     A                                    54'In file '
     A            DBFILE         8        62
     A                                    73'With Key '
     A            DBKEY         29        82
      *
