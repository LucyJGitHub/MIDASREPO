      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MC Period Set Details')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Management Accounting Module
     A*                                                               *
     A*  PRTF/MC0150P1 - PERIOD SETS REPORT                           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. CMC001             DATE 18MAR96               *
     A*  PREV AMEND NO.                    DATE                       *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CMC001 - Management Accounts Enhancement for PCA:            *
     A*           Print the period's closure date if CMC001           *
     A*           feature is installed.                               *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      **  Page Headings
      *
     A          R HEAD1                     TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     1'REFERENCE'
     A                                    11'MC0150'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   108'DATE'
     A            BJMRDT    R            113REFFLD(MRDT)
     A                                   123'PAGE'
     A            PAGE1          4  0    128EDTCDE(Z)
     A                                      SPACEA(2)
     A N40                                54'PERIOD SETS REPORT - LIST'
     A                                      UNDERLINE
     A  40                                54'PERIOD SETS REPORT - AUDIT'
     A                                      UNDERLINE
      *
      **  Period Set Headings & Detail
      *
     A          R PERSET                    SPACEB(3)
     A                                     2'SET   SET NAME'
     A                                    41'PERIODS    PERIODS PER'
     A                                    65'FREQUENCY  FREQUENCY'
     A                                    87'HISTORY FIRST PERIOD'
     A                                   110'LAST CHANGE '
     A                                      SPACEA(1)
     A                                     2'CODE'
     A                                    40'PER ANNUM'
     A                                    51'TERM 1  /  2    CODE'
     A                                    76'BASE DATE  PERIOD'
     A                                    99'YEAR      DATE  /  TYPE'
     A                                      SPACEA(2)
     A            PSPSTC         1         3
     A            PSPSNM        30         8
     A            PSPPAN         2  0     43
     A            PSPTM1         2  0     55
     A            PSPTM2         2  0     61
     A            PSPFCD         1        69
     A            PSPFBD         2  0     79
     A            PSHPYR         1  0     90
     A            PSFPYR         4  0     99
     A            PSLCD          7       108
     A            PSTYLC         6       117
      *
      **  Period Detail Headings
      *
     A          R PERHDR                    SPACEB(3)
     A                                     6'PERIOD   PERIOD NO.'
     A                                    28'RELATIVE   START OF'
     A                                    51'START OF   START OF'
     A                                    75'START OF       END OF'
     A                                   101'PERIOD NAME'
      *                                              Begin /CMC001/
     A  55                               116'DATE'
     A                                   132' '
      *                                              End   /CMC001/
     A                                      SPACEA(1)
     A                                     7'YEAR     IN YEAR'
     A                                    27'PERIOD NO.  YEAR DATE'
     A                                    51'TERM 1     TERM 2'
     A                                    73'PERIOD DATE'
      *                                              Begin /CMC001/
     A***********                         87'PERIOD DATE' SPACEA(2)
     A                                    87'PERIOD DATE'
     A  55                               115'CLOSED'
     A                                   132' '
     A                                      SPACEA(2)
      *                                              End   /CMC001/
      *
      **  Period Details
      *
     A          R PERDET
     A  10                                  SPACEB(1)
     A                                      SPACEA(1)
     A  10        PDPRCY         4  0      7
     A            PDPDNY         2        18
     A            PDRPDN         3  0     30EDTCDE(L)
     A  10        PDSYDA         7        40
     A  11        PDST1A         7        51
     A  12        PDST2A         7        62
     A            PDSPDA         7        75
     A            PDEPDA         7        89
     A            PDPDNM        10       101
      *                                              Begin /CMC001/
     A  55        RCLSD          7A      114
      *                                              End   /CMC001/
      *
      **  End of Report
      *
     A          R ENDRPT                    SPACEB(2)
     A                                    56'*** END OF REPORT ***'
