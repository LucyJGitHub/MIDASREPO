      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MC Deleted Period Details')
     A*****************************************************************
      *
      *  Midas - Management Accounting Module                         *
      *
      *  MC0170P1 - Deleted Period Details Report
      *
      *  (c) Finastra International Limited 2001                      *
      *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  PREV AMEND nnnnnn                DATE  DDMMMYY
      *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
      ***************************************************************
      *  F U N C T I O N   O F   I N D I C A T O R S
      *
      * 43 - If setoff Term 1 is defined and will be printed
      * 44 - If setoff Term 2 is defined and will be printed
      * U6 - Program error
      * U7 - Database error
      * U8 - Database error
      *
      ***************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ***************************************************************
      *
      ** Headings for new page
      *
     A          R MC0170H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                  3  2'REFERENCE MC0170P1'
      *
     A            BJURPT    R           3 37REFFLD(TITL)
     A                                  3110'DATE'
     A            BJMRDT    R           3115REFFLD(RUNA)
     A                                  3123'PAGE'
     A            RPAGE          4  0   3128
     A                                      EDTCDE(Z)
     A                                  5 54'DELETED PERIOD DETAILS -
     A                                      REPORT'
      *
      ***************************************************************
      *
      ** Headings for new Period Set
      *
     A          R MC0170H2                  TEXT('New Period Set')
      *
     A                                     2'SET CODE:'
     A                                      SPACEB(3)
     A            STPSTC         1A       13
     A                                    17'SET NAME:'
     A            STPSNM        30A       28
     A                                    61'HISTORY RETENTION PERIOD:'
     A            STHPYR         1  0     88
     A                                    92'FIRST PERIOD YEAR:'
     A            STFPYR         4  0    112
      *
     A                                     6'PERIOD   PERIOD NO.   -
     A                                      RELATIVE    PERIOD NAME-
     A                                         START OF'
      *
     A                                      SPACEB(3)
     A N43                                66'START OF'
     A N44                                77'START OF'
     A                                    90'START OF       END OF'
      *
     A                                     7'YEAR     IN YEAR    -
     A                                      PERIOD NO.'
     A                                      SPACEB(1)
     A                                    54'YEAR DATE'
     A N43                                66'TERM 1'
     A N44                                77'TERM 2'
     A                                    88'PERIOD DATE   PERIOD DATE'
     A                                      SPACEA(1)
      ***************************************************************
      *
      ** Details
      *
     A          R MC0170D1                  TEXT('Details')
     A                                      SPACEB(1)
     A            PYEAR          4A        7TEXT('Period Year')
     A            DTPDNY         2A       18TEXT('Period Number')
     A            DTRPDN         3  0     30TEXT('Relative Period No.')
     A                                      EDTCDE(J)
     A            DTPDNM        10A       40TEXT('Period Name')
     A            RSYDD          7A       55TEXT('Start of Year')
     A N43        RSTT1          7A       66TEXT('Start of Term 1')
     A N44        RSTT2          7A       77TEXT('Start of Term 2')
     A            RSPRD          7A       90TEXT('Start of Period')
     A            REPRD          7A      104TEXT('End of Period')
      ***************************************************************
      *
      ** End of report trailer
      *
     A          R MC0170D2                  TEXT('Trailer')
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(3)
