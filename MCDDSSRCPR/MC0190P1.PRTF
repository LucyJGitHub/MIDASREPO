      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MC Generated period details report')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Management Accounting Module                *
     A*                                                                  *
     A*      PRTF/MC0190P1 - Generated period details report             *
     A*                                                                  *
     A*     (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*      PREV AMEND. NO. XXXXXX          DATE 00XXX00                *
     A*                                      DATE                        *
     A*                                                                  *
     A*------------------------------------------------------------------*
     A*                                                                  *
     A*  MD046248 - Finastra Rebranding                                  *
     A********************************************************************
     A                                      REF(MFRFL)
      *
      * 1st Format - Title, Set details and period details header
     A          R P11F                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(1)
     A                                     1'Reference MC0190P1'
     A            RURPT     R             40REFFLD(URPT)
     A                                   100'Date'
     A            RMRDT     R            105REFFLD(MRDT)
     A                                   113'Page'
     A            PAGE           4S 0    118EDTCDE(Z) SPACEA(2)
      *
      * Report title
     A                                    47'GENERATED PERIOD DETAILS REPORT'
     A                                      SPACEA(4)
      *
      * Set Details header
     A                                     2'SET   SET NAME'
     A                                    41'PERIODS    PERIODS PER  FREQUENCY'
     A                                    76'FREQUENCY  HISTORY FIRST PERIOD'
     A                                   110'LAST CHANGE' SPACEA(1)
     A                                     2'CODE'
     A                                    40'PER ANNUM  TERM 1  /  2    CODE'
     A                                    76'BASE DATE  PERIOD      YEAR'
     A                                   109'DATE  /  TYPE' SPACEA(2)
      *
      * Set Details
     A            RPSTC          1         3
     A            RPSNM         30         8
     A            RPPAN          2  0     43
     A            RPTM1          2  0     55
     A            RPTM2          2  0     61
     A            RPFCD          1        69
     A            RPFBD          2  0     79
     A            RHPYR          1  0     90
     A            RFPYR          4  0     99
     A            RLCD           7       108
     A            RLCHGT         6       117SPACEA(3)
      *
      * Period details header
     A                                     6'PERIOD     PERIOD NO.'
     A                                    32'RELATIVE     START OF'
                                          59'START OF     START OF'
     A                                    87'START OF        END OF'
     A                                       SPACEA(1)
     A                                     7'YEAR       IN YEAR'
     A                                    31'PERIOD NO.    YEAR DATE     TERM 1'
     A                                    72'TERM 2       PERIOD DATE'
     A                                   101'PERIOD DATE' SPACEA(1)
      *
      * 2nd Format - period details
     A          R P12F                      TEXT('Details')
     A  41                                  SPACEB(1)
     A                                      SPACEA(1)
     A  42        RPDYR          4         7
     A            RPDNY          2        20
     A            RRPDN          3S 0     34EDTCDE(L)
     A  42        RSOYPT         7        46
     A            RSOT1P         7        59
     A            RSOT2P         7        72
     A            RSOPDP         7        87
     A            REOPDP         7       103
      *
      * End of Report Format
     A          R P1EF                      TEXT('End of Report Format')
     A                                      SPACEB(2)
     A                                    56'*** END OF REPORT ***'
