      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MC Control Groups Report')
      *****************************************************************
      *
      *  Midas - Management Accounting Module                         *
      *
      *  MC0202P1 - Control Groups Report
      *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend 137276                DATE  30Apr99               *
      *  Prev Amend nnnnnn                DATE  DDMMMYY
      *
      *---------------------------------------------------------------*
      *                                                               *
      *  137276 - There is no END OF REPORT'.                         *
      *                                                               *
      ***************************************************************
      *  F U N C T I O N   O F   I N D I C A T O R S
      *
      * 25 - Audit/List to be printed on header.
      * 50 - Multibranching is switched on.
      * 51 - Profit Centres are being used.
      *
      ***************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ***************************************************************
      *
      ** Headings for new page
      *
     A          R MC0202H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                  3  2'REFERENCE MC0202P1'
      *
     A            BJURPT    R           3 40REFFLD(TITL)
     A                                  3110'DATE'
     A            BJMRDT    R           3115REFFLD(RUNA)
     A                                  3123'PAGE'
     A            PAGE1          4  0   3128
     A                                      EDTCDE(Z)
     A                                  5 53'CONTROL GROUPS REPORT '
     A                                      UNDERLINE
     A  25                              5 75'- AUDIT'
     A                                      UNDERLINE
     A N25                              5 75'- LIST'
     A                                      UNDERLINE
      ***************************************************************
      *
      ** Details for new each posting
      *
     A          R MC0202D1                  TEXT('Posting Details')
     A                                      SPACEB(3)
     A                                     2'GROUP CODE.'
     A            HCCGCD         2A       14TEXT('Control Group')
     A                                    20'GROUP DESCRIPTION.'
     A            HCCGDS        30A       39TEXT('Group Description')
     A*
     A                                     2'SET CODE...'
     A                                      SPACEB(1)
     A            HCPSTC         1A       14TEXT('Period Set Code')
     A                                    20'POSTING BASIS.....'
     A            HCPSTB         1A       39TEXT('Posting Basis')
     A                                    46'NUMBER OF PERIODS TO RETAIN'
     A            HCNPTR         3  0     74TEXT('Periods to Retain.')
     A                                    82'CURRENCY OF BALANCES.'
     A            HCCCYB         3A      104TEXT('Currency of Balances')
     A*
     A                                     2'AVERAGE OVER ACCOUNT OPEN..'
     A                                      SPACEB(1)
     A            HCAOAO         1A       30TEXT('Average Over Account Open')
     A                                    35'CURRENT PERIOD NUMBER.'
     A            HCCPNB         6  0     58TEXT('Current Period Number')
     A                                    69'UPDATE AUDIT..'
     A            HCUPAU         1A       84TEXT('Update Audit')
     A                                    90'AVERAGE DR/CR BALANCES.'
     A            HCADCB         1A      114TEXT('Average DR/CR Balances')
     A*
     A                                     2'ANALYSIS BY ACCOUNT +
     A                                      CODES..'
     A                                      SPACEB(1)
     A            HCABAC         1A       30TEXT('Anal. by Account Codes')
     A                                    35'CUSTOMERS...........'
     A            HCABCU         1A       56TEXT('Anal. by Customer')
     A                                    61'BOOKS.................'
     A            HCABBK         1A       84TEXT('Anal. by Book')
     A                                    89'ASSOCIATED CUSTOMERS...'
     A            HCASCU         1A      113TEXT('Anal. by Ass. Cust.')
     A*
     A                                    14'CURRENCIES.....'
     A                                      SPACEB(1)
     A            HCABCY         1A       30TEXT('Anal. by Currency')
     A                                    35'A/C SEQUENCE........'
     A            HCABAS         1A       56TEXT('Anal. by A/C Seq.')
     A                                    61'TRANSACTION TYPES.....'
     A            HCABTT         1A       84TEXT('Anal. by Tran. Type')
     A                                      SPACEA(1)
     A*
     A  50                                14'BRANCHES.......'
     A  50        HCABBR         1A       30TEXT('Anal. by Branch')
     A  51                                35'PROFIT CENTRES......'
     A  51        HCABPC         1A       56TEXT('Anal. by Prof. Cent.')
     A                                    61'TRANSACTION SUB-TYPES.'
     A            HCABTS         1A       84TEXT('Anal. by Sub-type')
     A*
     A                                     2'INCLUDE ASSET ACCOUNTS.....'
     A                                      SPACEB(1)
     A            HCIAAC         1A       30TEXT('Include Assets')
     A                                    35'EXPENSE ACCOUNTS....'
     A            HCIEAC         1A       56TEXT('Include Expense')
     A                                    61'TRADING ACCOUNTS......'
     A            HCITAC         1A       84TEXT('Include Trading')
     A*
     A                                    10'LIABILITY ACCOUNTS.'
     A                                      SPACEB(1)
     A            HCILAC         1A       30TEXT('Include Liabilities')
     A                                    35'CONTROL ACCOUNTS....'
     A            HCICAC         1A       56TEXT('Inclue Control')
     A                                    61'CONTINGENT ACCOUNTS...'
     A            HCICTA         1A       84TEXT('Include Contingents')
     A                                   100'LAST CHANGE TYPE.'
     A            RCYTLC         6A      118TEXT('Last Change Type')
     A*
     A                                    10'INCOME ACCOUNTS....'
     A                                      SPACEB(1)
     A            HCIIAC         1A       30TEXT('Include Income')
     A                                    35'MEMORANDUM ACCOUNTS.'
     A            HCIMAC         1A       56TEXT('Include Memoranda')
     A                                   112'DATE.'
     A            RHCLCD         7A      118TEXT('Date')
     A*                                                                         137276
     A          R MC0202E1                  TEXT('END OF REPORT')               137276
     A                                    43'** END OF REPORT **'               137276
