      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MC Management Accounts Review Audit')
      *****************************************************************
      *
      *  Midas - Management Accounting Module                         *
      *
      *  MC0215AU - Management Accounts Review
      *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. nnnnnn             Date DDMMMYY               *
      *
      ***************************************************************
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *                                                               *
      *****************************************************************
      *  F U N C T I O N   O F   I N D I C A T O R S
      *
      * 14 - System is multibranched
      *
      ***************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ***************************************************************
      *
      ** Headings
      *
     A          R MC0215A1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                  3  2'REFERENCE MC0215AU'
      *
     A            BJURPT    R           3 37REFFLD(TITL)
     A                                  3110'DATE'
     A            BJMRDT    R           3115REFFLD(RUNA)
     A                                  3123'PAGE 1'
     A                                  5 54'MANAGEMENT ACCOUNTS REPORT'
     A                                  7 10'GROUP'
     A            PGRCD          2      7 17
     A            HCCGDS        30      7 21
     A                                  7 56'FOR PERIOD'
     A            PPDYE          4      7 68
     A                                  7 73'-'
     A            PPDNY          2      7 75
      *
     A  14                             11  6'BRANCH'
     A                                 11 14'CURRENCY  ACCOUNT-
     A                                        CUSTOMER  SEQUENCE  PROFIT-
     A                                        BOOK'
     A                                 11 74'TRANSACTION'
     A                                 11 93'ASSOCIATED   PERIOD'
     A  14                             12  7'CODE'
     A**Start of change CGL029
     A**********                       12 16'CODE      CODE    NUMBER-                        CGL029
     A                                 12 16'CODE     CODE     NUMBER-
     A**End of change CGL029
     A                                          NUMBER   CENTRE  CODE'
     A                                 12 71'TYPE       SUB-TYPE'
     A                                 12 94'CUSTOMER   END DATE'
      *
     A  14        PBRCD          3A    14  7TEXT('Branch code')
     A            PCYCD          3A    14 16TEXT('Currency code')
     A**Start of change CGL029
     A**********  PACCD          4A    14 26TEXT('Account code')                              CGL029
     A            PACCD         10A    14 22TEXT('Account code')
     A**End of change CGL029
     A            PCUST          6A    14 34TEXT('Customer number')
     A            PACSN          2A    14 46TEXT('Account sequence')
     A            PPCCD          4A    14 54TEXT('Profit centre')
     A            PBKCD          2A    14 62TEXT('Book code')
     A            POTTP         10A    14 68TEXT('Transaction type')
     A            PTSTY         10A    14 81TEXT('Transaction subtype' )
     A            PASCU          6A    14 95TEXT('Associated customer')
      *
      ***************************************************************
      *
      ** No details to report format
      *
     A          R MC0215A2                  TEXT('No details format')
      *
     A                                    53'** NO DETAILS TO REPORT **'
     A                                      SPACEB(5)
      *
      ***************************************************************
      *
      ** Error format
      *
     A          R MC0215A3                  TEXT('Error format')
      *
     A                                    11'*** DATABASE IN ERROR ***'
     A                                      SPACEB(5)
      *
     A                                    49'File'
     A                                      SPACEB(1)
     A                                    60':-'
     A            DBFILE         8        64
      *
     A                                    49'Key'
     A                                      SPACEB(1)
     A                                    60':-'
     A            DBKEY         29        64
      *
     A                                    49'At point'
     A                                      SPACEB(1)
     A                                    60':-'
     A            DBASE          3S 0     64
