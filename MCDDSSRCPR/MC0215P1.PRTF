      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MC Management Accounts Report')
      **********************************************************
      *
      *  Midas - Management Accounting Module                         *
      *
      *  MC0215P1 - Management Accounts Report
      *
      *  (c) Finastra International Limited 2001                      *
      *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. nnnnnn             Date  DDMMMYY              *
      *
      ***************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *                                                               *
      *****************************************************************
      *
      *  C R E A T I O N     P A R A M E T E R S
      *
      *
      ***************************************************************
      *  F U N C T I O N   O F   I N D I C A T O R S
      *
      * 14 - System is multibranched
      * 15 - Profit Centres are used in system
      * 40 - DR/CR Average balances required
      * 43 - Term 1 not defined, do not display
      * 44 - Term 2 not defined, do not display
      * U6 - Program error
      * U7 - Database error
      * U8 - Database error
      *
      ***************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ***************************************************************
      *
      ** Headings
      *
     A          R MC0215H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                  3  2'REFERENCE MC0215P1'
      *
     A            BJURPT    R           3 37REFFLD(TITL)
     A                                  3110'DATE'
     A            BJMRDT    R           3115REFFLD(RUNA)
     A                                  3123'PAGE'
     A            RPAGE          4  0   3128
     A                                      EDTCDE(Z)
     A                                  5 54'MANAGEMENT ACCOUNTS REPORT'
     A                                  7 10'GROUP'
     A            PGRCD          2      7 17
     A            HCCGDS        30      7 21
     A                                  7 56'FOR PERIOD'
     A            PPDYE          4      7 68
     A                                  7 73'-'
     A            PPDNY          2      7 75
     A                                  9 32'PERIOD'
     A                                  9 57'YEAR TO DATE'
     A N43                              9 84'TERM 1 TO DATE'
     A N44                              9111'TERM 2 TO DATE'
     A  14                             11  6'BRANCH'
     A                                 11 14'CURRENCY  ACCOUNT-
     A                                        CUSTOMER  SEQUENCE'
     A  15                             11 53'PROFIT'
     A                                 11 61'BOOK'
     A                                 11 74'TRANSACTION'
     A                                 11 93'ASSOCIATED   PERIOD'
     A  14                             12  7'CODE'
     A**Start of change CGL029
     A**********                       12 16'CODE      CODE    NUMBER-                        CGL029
     A                                 12 16'CODE     CODE     NUMBER-
     A**End of change CGL029
     A                                          NUMBER'
     A  15                             12 53'CENTRE'
     A                                 12 61'CODE'
     A                                 12 71'TYPE       SUB-TYPE'
     A                                 12 94'CUSTOMER   END DATE'
      ***************************************************************
      *
      ** Details
      *
     A          R MC0215D1                  TEXT('Details')
     A                                      SPACEB(2)
     A  14        HBBRCD         3A        7TEXT('Branch code')
     A            HBCYCD         3A       16TEXT('Currency code')
     A**Start of change CGL029
     A**********  HBACCD         4A       26TEXT('Account code')                              CGL029
     A            HBACCD        10A       22TEXT('Account code')
     A**End of change CGL029
     A            HBCUST         6A       34TEXT('Customer number')
     A            HBACSN         2A       46TEXT('Account sequence')
     A  15        HBPCCD         4A       54TEXT('Profit centre')
     A            HBBKCD         2A       62TEXT('Book code')
     A            HBOTTP        10A       68TEXT('Transaction type')
     A            HBTSTY        10A       81TEXT('Transaction subtype' )
     A            HBASCU         6A       95TEXT('Associated customer')
     A            REPRD          7A      105TEXT('End period date')
      *
      ** line after next line
     A                                     5'OPENING BALANCE'
     A                                      SPACEB(2)
     A            RPROB         27A       21TEXT('Period Opening Balance')
      *
      ** Next line
     A                                     5'CLOSING BALANCE'
     A                                      SPACEB(1)
     A            RPRCB         27A       21TEXT('Period Opening Balance')
      *
      ** Next line
     A                                     1'NET       MOVEMENTS'
     A                                      SPACEB(1)
     A            RPRMD         27A       21TEXT('Period Movements to-date')
     A            RYRMD         27A       49TEXT('Yearly Movements to-date')
     A N43        RT1MD         27A       77TEXT('Term 1 Movements to-date')
     A N44        RT2MD         27A      105TEXT('Term 2 Movements to-date')
      *
      ** Next line
     A                                     5'AVERAGE BALANCE'
     A                                      SPACEB(1)
     A            RAPRD         27A       21TEXT('Avrge Balance Period to-date')
     A            RAYRD         27A       49TEXT('Average Balance Year to-date')
     A N43        RAT1D         27A       77TEXT('Avrage Balance Term1 to-date')
     A N44        RAT2D         27A      105TEXT('Avrage Balance Term2 to-date')
      *
      ** Next line
     A                                    10'START DATE'
     A                                      SPACEB(1)
     A            RSPRD          7A       31TEXT('Start of Period date')
     A            RDIPR          3A       39TEXT('Days in Period')
     A                                    43'DAYS'
     A            RSYDD          7A       59TEXT('Start of Year date')
     A            RDIYR          3A       67TEXT('Days in Year')
     A                                    71'DAYS'
     A N43        RSTT1          7A       87TEXT('Start of Term 1 date')
     A N43        RDIT1          3A       95TEXT('Days in Term 1')
     A N43                                99'DAYS'
     A N44        RSTT2          7A      115TEXT('Start of Term 2 date')
     A N44        RDIT2          3A      123TEXT('Days in Term 2')
     A N44                               127'DAYS'
      *
      ** Next line
     A     40                              1'DEBIT     MOVEMENTS'
     A     40                               SPACEB(1)
     A     40     RPDMT         27A       21TEXT('Period DR Movements')
     A     40     RYDMT         27A       49TEXT('Yearly DR Movements')
     A N43 40     RT1DM         27A       77TEXT('Term 1 DR Movements')
     A N44 40     RT2DM         27A      105TEXT('Term 2 DR Movements')
      *
      ** Next line
     A     40                              5'AVERAGE BALANCE'
     A     40                               SPACEB(1)
     A     40     RPRDA         27A       21TEXT('Period DR Average Balance')
     A     40     RYRDA         27A       49TEXT('Yearly DR Average Balance')
     A N43 40     RT1DA         27A       77TEXT('Term 1 DR Average Balance')
     A N44 40     RT2DA         27A      105TEXT('Term 2 DR Average Balance')
      *
      ** Next line
     A     40                              7'DAYS IN DEBIT'
     A     40                               SPACEB(1)
     A     40     RPDDD          3A       39TEXT('Period Days in DR')
     A     40                             43'DAYS'
     A     40     RYDDD          3A       67TEXT('Yearly Days in DR')
     A     40                             71'DAYS'
     A N43 40     RT1DD          3A       95TEXT('Term 1 Days in DR')
     A N43 40                             99'DAYS'
     A N44 40     RT2DD          3A      123TEXT('Term 2 Days in DR')
     A N44 40                            127'DAYS'
      *
      ** Next line
     A     40                              1'CREDIT    MOVEMENTS'
     A     40                               SPACEB(1)
     A     40     RPCMT         27A       21TEXT('Period CR Movements')
     A     40     RYCMT         27A       49TEXT('Yearly CR Movements')
     A N43 40     RT1CM         27A       77TEXT('Term 1 CR Movements')
     A N44 40     RT2CM         27A      105TEXT('Term 2 DR Movements')
      *
      ** Next line
     A     40                              5'AVERAGE BALANCE'
     A     40                               SPACEB(1)
     A     40     RPRCA         27A       21TEXT('Period CR Average Balance')
     A     40     RYRCA         27A       49TEXT('Yearly CR Average Balance')
     A N43 40     RT1CA         27A       77TEXT('Term 1 CR Average Balance')
     A N44 40     RT2CA         27A      105TEXT('Term 2 CR Average Balance')
      *
      ** Next line
     A     40                              6'DAYS IN CREDIT'
     A     40                               SPACEB(1)
     A     40     RPDDC          3A       39TEXT('Period Days in CR')
     A     40                             43'DAYS'
     A     40     RYDDC          3A       67TEXT('Yearly Days in CR')
     A     40                             71'DAYS'
     A N43 40     RT1DC          3A       95TEXT('Term 1 Days in CR')
     A N43 40                             99'DAYS'
     A N44 40     RT2DC          3A      123TEXT('Term 2 Days in CR')
     A N44 40                            127'DAYS'
      ***************************************************************
      *
      ** End of report trailer
      *
     A          R MC0215D2                  TEXT('Trailer')
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(3)
