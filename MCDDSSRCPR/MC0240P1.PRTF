      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MC Rework DR/CR Average Balances')
      ***************************************************************
      *
      *  Midas - Management Accounting Module                         *
      *
      *  MC0240P1 - Rework DR/CR Average Balances Report
      *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *
      ***************************************************************
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *                                                               *
      *****************************************************************
      *
      *  C R E A T I O N     P A R A M E T E R S
      *
      *
      ***************************************************************
      *
      *  F U N C T I O N   O F   I N D I C A T O R S
      *
      *      60 - Continuation from previous page.
      *      61 - Analysis by Account Code.
      *      62 - Analysis by Currency.
      *      63 - Analysis by Branch.
      *      64 - Analysis by Customer.
      *      65 - Analysis by Account Sequence.
      *      66 - Analysis by Profit Centre.
      *      67 - Analysis by Book Code.
      *      68 - Analysis by Transaction Type.
      *      69 - Analysis by Transaction Sub-type.
      *      70 - Analysis by Associated Customer.
      *      71 - T1 processing is not used.
      *      72 - T2 processing is not used.
      *      73 - Print B/F details.
      *      74 - Print Movement Adjustment details.
      *      75 - Print C/F details.
      *
      ***************************************************************
     A                                      INDARA
      *
      ** Page headings
     A          R MC0240H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                  3  2'REFERENCE MC0240P1'
     A            BJURPT        53A     3 37TEXT('Installation name')
     A                                  3110'DATE'
     A            BJMRDT         7A     3115TEXT('Date DDMMMYY')
     A                                  3123'PAGE'
     A                                  3128PAGNBR
     A                                      EDTCDE(3)
      *
     A                                  5 49'DR/CR AVERAGE BALANCES +
     A                                       REWORK AUDIT'
      *
     A                                  7  7'GROUP'
     A            HCCGCD         2A     7 13TEXT('Group code')
     A                                  7 16'-'
     A            HCCGDS        30A     7 18TEXT('Group description')
      *
     A                                  9  2'HISTORIC BALANCE:'
     A  60                              9 22'(Continued)'
      *
     A  63                             10  4'BRANCH'
     A  62                             10 11'CCY'
     A**Start of change CGL029
     A**61******                       10 16'A/C'                                             CGL029
     A**64******                       10 21'CUSTOMER'                                        CGL029
     A**65******                       10 30'A/C'                                             CGL029
     A**66******                       10 34'PRFT'                                            CGL029
     A**67******                       10 39'BOOK'                                            CGL029
     A**68******                       10 44'TRANS'                                           CGL029
     A**69******                       10 55'TRANS'                                           CGL029
     A**70******                       10 66'ASSOC.'                                          CGL029
     A**********                       10 75'DAYS IN'                                         CGL029
     A**********                       10 84'PERIOD'                                          CGL029
     A**********                       10 94'PERIOD'                                          CGL029
     A**********                       10102'LAST'                                            CGL029
     A**********                       10112'PERIOD'                                          CGL029
      **********                                                                              CGL029
     A**63******                       11  4'CODE'                                            CGL029
     A**62******                       11 11'CODE'                                            CGL029
     A**61******                       11 16'CODE'                                            CGL029
     A**64******                       11 22'NUMBER'                                          CGL029
     A**65******                       11 30'SEQ'                                             CGL029
     A**66******                       11 34'CNTR'                                            CGL029
     A**67******                       11 39'BOOK'                                            CGL029
     A**68******                       11 44'TYPE'                                            CGL029
     A**69******                       11 55'SUB-TYPE'                                        CGL029
     A**70******                       11 66'CUSTOMER'                                        CGL029
     A**********                       11 75'PERIOD'                                          CGL029
     A**********                       11 84'START'                                           CGL029
     A**********                       11 94'END'                                             CGL029
     A**********                       11102'MOVEMENT'                                        CGL029
     A**********                       11112'NUMBER'                                          CGL029
     A  61                             10 19'A/C'
     A  64                             10 27'CUSTOMER'
     A  65                             10 36'A/C'
     A  66                             10 40'PRFT'
     A  67                             10 45'BOOK'
     A  68                             10 50'TRANS'
     A  69                             10 61'TRANS'
     A  70                             10 72'ASSOC.'
     A                                 10 81'DAYS IN'
     A                                 10 90'PERIOD'
     A                                 10100'PERIOD'
     A                                 10108'LAST'
     A                                 10118'PERIOD'
      *
     A  63                             11  5'CODE'
     A  62                             11 11'CODE'
     A  61                             11 19'CODE'
     A  64                             11 28'NUMBER'
     A  65                             11 36'SEQ'
     A  66                             11 40'CNTR'
     A  67                             11 45'BOOK'
     A  68                             11 50'TYPE'
     A  69                             11 61'SUB-TYPE'
     A  70                             11 72'CUSTOMER'
     A                                 11 81'PERIOD'
     A                                 11 90'START'
     A                                 11100'END'
     A                                 11108'MOVEMENT'
     A                                 11118'NUMBER'
     A**End of change CGL029
      *
     A**Start of change CGL029
     A**63******  HBBRCD         3A    12  6TEXT('Branch code')                               CGL029
     A**6*******  HBCYCD         3A    12 12TEXT('Currency')                                  CGL029
     A**61******  HBACCD         4A    12 16TEXT('Account code')                              CGL029
     A**64******  HBCUST         6A    12 22TEXT('Customer number')                           CGL029
     A**65******  HBACSN         2A    12 30TEXT('Account sequence')                          CGL029
     A**66******  HBPCCD         4A    12 34TEXT('Profit centre')                             CGL029
     A**67******  HBBKCD         2A    12 40TEXT('Book code')                                 CGL029
     A**68******  HBOTTP        10A    12 44TEXT('Orig. transaction type')                    CGL029
     A**69******  HBTSTY        10A    12 55TEXT('Orig. trans. subtype')                      CGL029
     A**70******  HBACNB         6A    12 66TEXT('Associated customer')                       CGL029
     A**********  P1DPER         3  0  12 75TEXT('Days in period')                            CGL029
     A**********                            EDTCDE(J)                                         CGL029
     A**********  P1SPDD         7A    12 83TEXT('Period start date')                         CGL029
     A**********                       12 91'-'                                               CGL029
     A**********  P1EPDD         7A    12 93TEXT('Period end date')                           CGL029
     A**********  P1LMDT         7A    12101TEXT('Last movement date')                        CGL029
     A**********  P1PDCT         2A    12111TEXT('Period century, CC')                        CGL029
     A**********  P1PDYR         2A    12113TEXT('Period year, YY')                           CGL029
     A**********                       12115'-'                                               CGL029
     A**********  P1PDNY         2A    12116TEXT('Period number')                             CGL029
     A  63        HBBRCD         3A    12  5TEXT('Branch code')
     A  62        HBCYCD         3A    12 11TEXT('Currency')
     A  61        HBACCD        10A    12 16TEXT('Account code')
     A  64        HBCUST         6A    12 28TEXT('Customer number')
     A  65        HBACSN         2A    12 36TEXT('Account sequence')
     A  66        HBPCCD         4A    12 40TEXT('Profit centre')
     A  67        HBBKCD         2A    12 46TEXT('Book code')
     A  68        HBOTTP        10A    12 50TEXT('Orig. transaction type')
     A  69        HBTSTY        10A    12 61TEXT('Orig. trans. subtype')
     A  70        HBACNB         6A    12 72TEXT('Associated customer')
     A            P1DPER         3  0  12 81TEXT('Days in period')
     A                                      EDTCDE(J)
     A            P1SPDD         7A    12 89TEXT('Period start date')
     A                                 12 97'-'
     A            P1EPDD         7A    12 99TEXT('Period end date')
     A            P1LMDT         7A    12107TEXT('Last movement date')
     A            P1PDCT         2A    12117TEXT('Period century, CC')
     A            P1PDYR         2A    12119TEXT('Period year, YY')
     A                                 12121'-'
     A            P1PDNY         2A    12122TEXT('Period number')
     A**End of change CGL029
      *
     A                                 14 17'OPENING/'
     A                                 14 37'PERIOD MOVEMENTS/'
     A                                 14 57'YEAR-TO-DATE MOVEMENTS/'
     A  71                             14 81'TERM 1-TO-DATE MOVEMENTS/'
     A  72                             14107'TERM 2-TO-DATE MOVEMENTS/'
      *
     A                                 15 12'CLOSING BALANCE'
     A                                 15 37'WEIGHTED BALANCE'
     A                                 15 57'WEIGHTED BALANCE'
     A  71                             15 81'WEIGHTED BALANCE'
     A  72                             15107'WEIGHTED BALANCE'
      *
      * B/F, movement adjustments and C/F amounts.
     A          R MC0240D1                  TEXT('B/F, movement adjustment +
     A                                      + C/F amounts.')
      *
     A  73                                 2'BROUGHT FORWARD TOTALS'
     A                                      SPACEB(2)
     A  74                                 2'MOVEMENT ADJUSTMENTS'
     A                                      SPACEB(2)
     A  75                                 2'CARRIED FORWARD TOTALS:'
     A                                      SPACEB(2)
      *
     A     75     P1PDOB        26A        2TEXT('Opening balance')
     A                                      SPACEB(1)
     A     75     P1PMTD        26A       28TEXT('Period movements')
     A     75     P1YMTD        26A       54TEXT('YTD movements')
     A  71 75     P1T1MT        26A       80TEXT('T1-T-D movements')
     A  72 75     P1T2MT        26A      106TEXT('T2-T-D movements')
      *
     A     75     P1PDCB        26A        2TEXT('Closing balance')
     A                                      SPACEB(1)
     A     75     P1WPBL        26A       28TEXT('Pd. mov. weighted balance')
     A     75     P1WYBL        26A       54TEXT('YTD mov. weighted balance')
     A  71 75     P1WT1B        26A       80TEXT('T1-T-D mov. weighted balance')
     A  72 75     P1WT2B        26A      106TEXT('T2-T-D mov. weighted balance')
      *
     A                                     5'DEBIT MOVEMENTS'
     A                                      SPACEB(1)
     A            P1PDMT        26A       28TEXT('Debit-period movements')
     A            P1YDMT        26A       54TEXT('Debit-YTD movements')
     A  71        P1T1DM        26A       80TEXT('Debit- T1-T-D movements')
     A  72        P1T2DM        26A      106TEXT('Debit- T2-T-D movements')
      *
     A                                    11'WEIGHTED BALANCE'
     A                                      SPACEB(1)
     A            P1WPDB        26A       28TEXT('Debit-period weighted')
     A            P1WYDB        26A       54TEXT('Debit-YTD weighted')
     A  71        P1W1DB        26A       80TEXT('Debit- T1-T-D weighted')
     A  72        P1W2DB        26A      106TEXT('Debit- T2-T-D weighted')
      *
     A                                    11'DAYS IN DEBIT'
     A                                      SPACEB(1)
     A            P1PDDD         3  0     50TEXT('Debit-period days')
     A                                      EDTCDE(L)
     A            P1YDDD         3  0     76TEXT('Debit-YTD days')
     A                                      EDTCDE(L)
     A  71        P1T1DD         3  0    102TEXT('Debit- T1-T-D days')
     A                                      EDTCDE(L)
     A  72        P1T2DD         3  0    128TEXT('Debit- T2-T-D days')
     A                                      EDTCDE(L)
      *
     A                                     5'CREDIT MOVEMENTS'
     A                                      SPACEB(1)
     A            P1PCMT        26A       28TEXT('Credit-period movements')
     A            P1YCMT        26A       54TEXT('Credit-YTD movements')
     A  71        P1T1CM        26A       80TEXT('Credit- T1-T-D movements')
     A  72        P1T2CM        26A      106TEXT('Credit- T2-T-D movements')
      *
     A                                    11'WEIGHTED BALANCE'
     A                                      SPACEB(1)
     A            P1WPCB        26A       28TEXT('Credit-period weighted')
     A            P1WYCB        26A       54TEXT('Credit-YTD weighted')
     A  71        P1W1CB        26A       80TEXT('Credit- T1-T-D weighted')
     A  72        P1W2CB        26A      106TEXT('Credit- T2-T-D weighted')
      *
     A                                    11'DAYS IN CREDIT'
     A                                      SPACEB(1)
     A            P1PDDC         3  0     50TEXT('Credit-period days')
     A                                      EDTCDE(L)
     A            P1YDDC         3  0     76TEXT('Credit-YTD days')
     A                                      EDTCDE(L)
     A  71        P1T1DC         3  0    102TEXT('Credit- T1-T-D days')
     A                                      EDTCDE(L)
     A  72        P1T2DC         3  0    128TEXT('Credit- T2-T-D days')
     A                                      EDTCDE(L)
      *
      ** Movements header
     A          R MC0240H2                  TEXT('Movements header')
      *
     A                                     2'MOVEMENTS:'
     A                                      SPACEB(2)
      *
     A                                     5'MOVEMENT'
     A                                      SPACEB(1)
     A                                     7'DATE'
     A                                      SPACEB(1)
     A                                    14'TYPE'
     A                                    44'AMOUNT'
     A                                    59'AT RATE'
     A                                    69'= CONVERTED AMOUNT IN'
     A            CCCY           3A       91TEXT('Currency of balances')
     A                                    96'x DAYS'
     A                                   109'= WEIGHTED AMOUNT'
     A                                   128'VOID'
      *
      * Movement details
     A          R MC0240D2                  TEXT('Movement details')
      *
     A            P2EVDT         7A        6TEXT('Movement date')
     A                                      SPACEB(1)
     A            P2MTYP        14A       14TEXT('Movement type')
     A            P2PAMT        26A       28TEXT('Movement amount')
     A            P2RATE        14  8     55TEXT('Movement rate')
     A                                      EDTCDE(3)
     A            P2CVAM        26A       70TEXT('Movement converted amt')
     A            P2DAYS         3  0     97TEXT('Movement days')
     A                                      EDTCDE(3)
     A            P2CWAM        26A      102TEXT('Movement weighted amt')
     A            P2VOIN         1A      129TEXT('Movement void ind.')
      *
      * End of report trailer
     A          R MC0240T1                  TEXT('End of report')
     A                                      SPACEB(3)
     A                                    55'*** END OF REPORT ***'
