      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MC Deleted Management Accounts Rept.')
      ***************************************************************
      *
      *  Midas - Management Accounting Module                         *
      *
      *  MC0270P1 - Deleted Management Accounts Report
      *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. nnnnnn             Date ddmmmyy               *
      *
      ***************************************************************
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *                                                               *
      *****************************************************************
      *
      *  C R E A T I O N     P A R A M E T E R S
      *
      *
      ***************************************************************
      *  F U N C T I O N   O F   I N D I C A T O R S
      *
      * 14 - System is multibranched
      * 15 - Profit Centres are used in the system
      * 40 - Average Balances are maintained
      * U6 - Program error
      * U7 - Database error
      * U8 - Database error
      *
      ***************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ***************************************************************
      *
      ** Headings for new page
      *
     A          R MC0270H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                  3  2'REFERENCE MC0270P1'
      *
     A            BJURPT    R           3 37REFFLD(TITL)
     A                                  3110'DATE'
     A            BJMRDT    R           3115REFFLD(RUNA)
     A                                  3123'PAGE'
     A            RPAGE          4  0   3128
     A                                      EDTCDE(Z)
     A                                  5 54'DELETED MANAGEMENT -
     A                                      ACCOUNTS REPORT'
      *
      ***************************************************************
      *
      ** Headings for new Group Code
      *
     A          R MC0270H2                  TEXT('New Group headings')
     A                                    10'GROUP'
     A                                      SPACEB(3)
     A            HCCGCD         2A       17
     A            HCCGDS        30A       21
     A                                    56'EXPIRED PERIOD'
     A            PEXYR          4  0     73
     A                                    78'-'
     A            PEXPR          2  0     80
     A            RSPRD          7A       88
     A                                    97'TO'
     A            REPRD          7A      101
      *
     A  14                                 6'BRANCH'
     A  14                                  SPACEB(2)
     A                                    14'CURRENCY  ACCOUNT-
     A                                        CUSTOMER  SEQUENCE'
     A N14                                  SPACEB(2)
     A  15                                53'PROFIT'
     A                                    61'BOOK'
     A                                    74'TRANSACTION'
     A                                    93'ASSOCIATED      PERIOD'
      *
     A  14                                 7'CODE'
     A  14                                  SPACEB(1)
     A                                    16'CODE      CODE    NUMBER-
     A                                          NUMBER'
     A N14                                  SPACEB(1)
     A  15                                53'CENTRE'
     A                                    61'CODE'
     A                                    71'TYPE       SUB-TYPE'
     A                                    94'CUSTOMER    YEAR-NUMBER'
      *
      ***************************************************************
      *
      ** Details for new Management Account
      *
     A          R MC0270D1                  TEXT('Details')
      *
     A  14        HBBRCD         3A        7TEXT('Branch code')
     A  14                                  SPACEB(2)
     A            HBCYCD         3A       16TEXT('Currency code')
     A N14                                  SPACEB(2)
     A**Start of change CGL029
     A**********  HBACCD         4A       26TEXT('Account code')                              CGL029
     A            HBACCD        10A       22TEXT('Account code')
     A**End of change CGL029
     A            HBCUST         6A       34TEXT('Customer number')
     A            HBACSN         2A       46TEXT('Account sequence')
     A  15        HBPCCD         4A       54TEXT('Profit centre')
     A            HBBKCD         2A       62TEXT('Book code')
     A            HBOTTP        10A       68TEXT('Transaction type')
     A            HBTSTY        10A       81TEXT('Transaction subtype' )
     A            HBASCU         6A       95TEXT('Associated customer')
     A            PYEAR          4A      108TEXT('Period Year')
     A                                   112'-'
     A            HBPDNY         2A      113TEXT('Period Number')
      *
     A                                     7'DAYS IN PERIOD'
     A                                      SPACEB(2)
     A                                    34'MOVEMENTS'
     A                                    55'WEIGHTED BALANCE'
     A                                    86'OPENING BALANCE'
     A                                   114'CLOSING BALANCE'
      *
     A                                     2'NET'
     A                                      SPACEB(2)
     A            RDIPR          3A       12TEXT('Net Days in Period')
     A            RPMTD         27A       19TEXT('Net Period Movement')
     A            RWPBL         27A       47TEXT('Weighted Net Balance')
     A            RPROB         27A       77TEXT('Period Opening Balance')
     A            RPRCB         27A      105TEXT('Period Closing Balance')
      *
     A     40                              2'DEBIT'
     A                                      SPACEB(1)
     A     40     RPDDD          3A       12TEXT('Period Days in DR')
     A     40     RPDMT         27A       19TEXT('Period DR Movements')
     A     40     RWPDB         27A       47TEXT('Weighted DR Balance')
      *
     A     40                              2'CREDIT'
     A                                      SPACEB(1)
     A     40     RPDDC          3A       12TEXT('Period Days in CR')
     A     40     RPCMT         27A       19TEXT('Period CR Movements')
     A     40     RWPCB         27A       47TEXT('Weighted CR Balance')
      ***************************************************************
      *
      ** End of report trailer
      *
     A          R MC0270D2                  TEXT('Trailer')
     A                                    55'*** END OF REPORT ***'
     A                                      SPACEB(3)
