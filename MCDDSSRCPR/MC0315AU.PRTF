      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MC Balance Gen. Entries Audit Rpt')
      *****************************************************************
      *                                                               *
      *  Midas - Management Accounts Module                           *
      *                                                               *
      *  PRTF/MC0315AU - Generate Balancing Entries Audit Report      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CMC001 *CREATE     Date 18MAR96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      * CMC001 - Management Accounts Enhancement for PCA.             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE MC0315AU'
     A            BJURPT    R             39REFFLD(URPT)
     A                                   100'DATE'
     A            BJMRDT    R            105REFFLD(MRDT)
     A                                   122'PAGE'
     A                                   127PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    50'GENERATED BALANCING ENTRIES - A+
     A                                      UDIT REPORT'
     A                                      SPACEA(1)
     A                                    50'-------------------------------+
     A                                      -----------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
      *
     A                                    62'NUMBER'
     A                                    92'CALCULATED'
     A                                      SPACEA(1)
     A                                    85'DEBIT'
     A                                   105'CREDIT'
     A                                   121'DIFFERENCE'
     A                                      SPACEA(2)
      *
     A                                     6'MANAGEMENT A/C MOVEMENTS'
     A                                    27'- PF/'
     A                                      SPACEB(1)
     A                                    32'MCMOVEPD'
      *
     A                                    43'PROCESSED'
     A            P1RECS        10  0     58EDTCDE(3)
     A            P1DR          19  0     72EDTCDE(3)
     A            P1CR          19  0     92EDTCDE(3)
     A            P1BL          19  0    112EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                     6'PROFIT UNIT BALANCING ENTRIES'
     A                                    27'- PF/'
     A                                      SPACEB(1)
     A                                    32'MCPCBLPD'
      *
     A                                    43'OUTPUT'
     A                                      SPACEB(1)
     A            P2RECS        10  0     58EDTCDE(3)
     A            P2DR          19  0     72EDTCDE(3)
     A            P2CR          19  0     92EDTCDE(3)
     A            P2BL          19  0    112EDTCDE(3)
     A                                      SPACEA(2)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR                   TEXT('Error Format')
     A                                      SPACEB(7)
     A                                    40'*** Database in Error ***'
      *
     A                                      SPACEA(1)
     A                                    44'File'
     A                                    55':-'
     A            DBFILE         8A       59
      *
     A                                      SPACEA(1)
     A                                    44'Key'
     A                                    55':-'
     A            DBKEY         29        59
      *
     A                                      SPACEA(1)
     A                                    44'In Program'
     A                                    55':-'
     A            DBPGM         10A       59
      *
     A                                      SPACEA(1)
     A                                    44'At point'
     A                                    55':-'
     A            DBASE          3  0     59
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS                    TEXT('End of Report Format')
     A                                      SPACEB(5)
     A                                    40'*** No Details to Report ***'
      *
