      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MC Generated Entries  - Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Management Accounts Module                           *
      *                                                               *
      *  PRTF/MC0340AU - Generate Accounting Entries - Audit          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CMC001 *CREATE     Date 18MAR96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CMC001 -   Created for Management Accounts                   *
      *                                                               *
      *****************************************************************
     A                                       REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *
     A                                     6'REFERENCE MC0340AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'MANAGEMENT ACCOUNT GENERATED +
     A                                      ENTRIES - FILE CONTROLS'
     A                                      SPACEA(1)
     A                                    40'-----------------------------+
     A                                      -----------------------'
     A                                      SPACEA(3)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    60'CALCULATED'
     A                                      SPACEA(1)
     A                                    60'----------'
     A                                      SPACEA(3)
      *
     A                                    53'NUMBER'
     A                                    74'VALUE'
     A                                      SPACEA(3)
      *
     A                                     6'MANAGEMENT A/C GENERATED ENTRIES'
     A                                      SPACEA(1)
     A                                    27'- PF/MCMOVEPD   INPUT'
      *
     A            NUMREC         5  0     54EDTCDE(3)
     A            HASHI         15  0     61EDTCDE(Z)
     A            HASHD          3  0     76SPACEA(3)
      *
     A N30                                 6'GENERATED DAILY POSTINGS'
     A                                      SPACEA(1)
     A N30                                27'- PF/GEPCPD'
     A  30                                 6'GENERATED POSTINGS TO NEXT +
     A                                      WORKING DAY'
     A                                      SPACEA(1)
     A  30                                27'- PF/GEPCP2D'
     A                                    43'OUTPUT'
      *
     A            P1NUM          5  0     54EDTCDE(3)
     A            P1HRWN        15  0     61EDTCDE(Z)
     A            P1HRDC         3  0     76SPACEA(2)
      *
     A N30                                 6'GENERATED SPOT REVAL. POSTINGS'
     A                                      SPACEA(1)
     A N30                                27'- PF/GESRPD'
     A                                    43'OUTPUT'
      *
     A N30        P2NUM          5  0     54EDTCDE(3)
     A N30        P2HRWN        15  0     61EDTCDE(Z)
     A N30        P2HRDC         3  0     76SPACEA(2)
      *
     A N30                                 6'GENERATED F/CCY TFR POSTINGS'
     A                                      SPACEA(1)
     A N30                                27'- PF/GEXFPD'
     A N30                                43'OUTPUT'
      *
     A N30        P3NUM          5  0     54EDTCDE(3)
     A N30        P3HRWN        15  0     61EDTCDE(Z)
     A N30        P3HRDC         3  0     76SPACEA(2)
      *
     A N30                                 6'GENERATED TFR TO P&L POSTINGS'
     A                                      SPACEA(1)
     A N30                                27'- PF/GEXBPD'
     A N30                                43'OUTPUT'
      *
     A N30        P4NUM          5  0     54EDTCDE(3)
     A N30        P4HRWN        15  0     61EDTCDE(Z)
     A N30        P4HRDC         3  0     76SPACEA(2)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
