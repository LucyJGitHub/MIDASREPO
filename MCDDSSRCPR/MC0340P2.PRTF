      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MC Generated Entries for Spot Reval.')
      *****************************************************************
      *                                                               *
      *  Midas - Management Accounts Module                           *
      *                                                               *
      *  PRTF/MC0340P2 - Generate Account Entries for                 *
      *                  Spot Revaluation                             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CMC001 *CREATE     Date 18Mar96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CMC001 - Created for Management Accounts                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP2                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *
     A                                     6'REFERENCE MC0340P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'MANAGEMENT ACCOUNT GENERATED +
     A                                      ENTRIES - SPOT REVALUATIONS' +
     A                                      SPACEA(1)
     A                                    40'-----------------------------+
     A                                      ---------------------------'
     A                                      SPACEA(2)
      *
     A                                     4'CURRENCY :'
     A            CCY       R             15
     A                                    19'-'
     A            CYNM      R             21
      *
     A  70                                89'BRANCH -'
     A  70        A8BRCD    R             98REFFLD(BRCA)
     A  70        A8BRNM    R            102REFFLD(BRNM)
     A                                   132' '
     A                                      SPACEA(2)
      *
     A                                     1' '
     A                                     6'PRFT'
     A**Start of change CGL029
     A**********                          12'A/C'                                             CGL029
     A**********                          35'TRANS'                                           CGL029
     A**********                          46'TRANS'                                           CGL029
     A**********                          57' ASSOC. '                                        CGL029
     A                                    14'A/C'
     A                                    37'TRANS'
     A                                    48'TRANS'
     A                                    58' ASSOC. '
     A**End of change CGL029
     A                                    68'VALUE'
     A                                   132' '
     A                                      SPACEA(1)
      *
     A                                     6'CNTR'
     A**Start of change CGL029
     A**********                          12'CODE'                                            CGL029
     A**********                          18'CUSTOMER'                                        CGL029
     A**********                          29'BOOK'                                            CGL029
     A**********                          35'TYPE'                                            CGL029
     A**********                          45'SUB-TYPE'                                        CGL029
     A**********                          57'CUSTOMER'                                        CGL029
     A                                    14'CODE'
     A                                    22'CUSTOMER'
     A                                    32'BOOK'
     A                                    37'TYPE'
     A                                    47'SUB-TYPE'
     A                                    58'CUSTOMER'
     A**End of change CGL029
     A                                    68'DATE '
     A                                    80'NARRATIVE'
     A                                   120'AMOUNT'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAILP2
     A            PRFC      R              6
     A**Start of change CGL029
     A**********  ACOD      R             12                                                  CGL029
     A**********  CNUM      R             18                                                  CGL029
     A**********                          24'-'                                               CGL029
     A**********  ACSQ      R             25                                                  CGL029
     A**********  BOKC      R             30                                                  CGL029
     A**********  OTTP      R             35                                                  CGL029
     A**********  OTST      R             45                                                  CGL029
     A**********  ASOCA          6        58                                                  CGL029
     A            ACOD      R             11
      * Begin /CSD027/
     A**********  CNUM      R             22                                                  CSD027
     A            CNUM           6A       22TEXT('Customer number')
      * End /CSD027/
     A                                    28'-'
     A            ACSQ      R             29
     A            BOKC      R             33
     A            OTTP      R             37
     A            OTST      R             47
     A            ASOCA          6        59
     A**End of change CGL029
     A            ZADATE         7        67
     A            PNAR      R             76
     A            DAMT          21       106
     A  77                               127'CR'
     A  78                               129'REV'
     A                                   132' '
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAIL1P2                  SPACEB(2)
     A            PRFC      R              6
     A                                    12'**** PROFIT CENTRE DOES NOT +
     A                                      BALANCE FOR THIS DATE ****'
     A            ZADATE         7        67
     A                                    76'PROFIT CENTRE TOTAL'
     A            TAMT          21       106
     A  77                               127'CR'
     A                                   132' '
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAIL2P2
     A  70                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N70                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
