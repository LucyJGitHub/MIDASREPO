      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/**** *  TEXT('Midas MC F/M Accounts Rec'n Report - Detail')                                MD060753
/*EXI *  TEXT('Midas MC F/M Accounts Rec n Report - Detail')                                MD060753
      *****************************************************************
      *                                                               *
      *  Midas - Management Accounting Module                         *
      *                                                               *
      *  PRTF/MC0550P1 - Financial/Management Accounts Reconciliation *
      *                  Detail Report                                *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD060753           Date 10Nov22               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 124847 *C *CREATE  Date 05Sep97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD060753 - Error in source compilation                       *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  124847 - Financial/Management Accounts Reconciliation        *
      *           New printer file for MC0550.                        *
      *                                                               *
      *****************************************************************
     A*
     A                                      REF(MFRFL)
     A*
     A*--------------------------------*
     A**  Report Header Record Format  *
     A*--------------------------------*
     A*
     A          R MC0550D0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                      SKIPB(3)
     A                                     6'REFERENCE MC0550P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A*
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*
     A                                    51'FINANCIAL & MANAGEMENT +
     A                                      ACCOUNTS'
     A                                      SPACEA(1)
     A                                    51'-----------------------+
     A                                      --------'
     A                                      SPACEA(1)
     A                                    56'RECONCILIATION REPORT'
     A  10 11                            115'CONSOLIDATED'
     A                                   132' '
     A                                      SPACEA(1)
     A                                    56'---------------------'
     A                                      SPACEA(2)
     A*
     A*---------------*
     A**  Sub-Heading *
     A*---------------*
     A*
     A  10                                 6'BRANCH'
     A  10        RBRCA     R             15REFFLD(BRCA)
     A  10        RBRNM     R             19REFFLD(BRNM)
     A                                      SPACEA(1)
     A                                     6'CURRENCY'
     A            RCCY      R             15REFFLD(CCY)
     A                                      SPACEA(2)
     A*
     A*------------------------*
     A**  Detail Sub-Headings  *
     A*------------------------*
     A*
     A                                     1'CUSTOMER'
     A                                    11'A/C SEQ'
     A                                    24'ACCOUNT NAME'
     A                                    42'PROFIT CENTRE'
     A                                    66'BALANCE'
     A                                      SPACEA(1)
     A                                     1'--------'
     A                                    11'-------'
     A                                    20'--------------------'
     A                                    42'-------------'
     A                                    57'-------------------------'
     A                                      SPACEA(1)
     A*
     A*-------------------------*
     A**  Detail Record Format  *
     A*-------------------------*
     A*
     A          R MC0550D1
     A                                      SPACEB(1)
     A                                     1'A/C CODE'
     A**Start of change CGL029
     A**********  RACOD     R             10REFFLD(ACODA)                                     CGL029
     A**********  RACCN     R             15REFFLD(ACCN)                                      CGL029
     A            RACOD         10A       10
     A            RACCN     R             21REFFLD(ACCN)
     A**End of change CGL029
     A                                      SPACEA(1)
     A*
     A          R MC0550D2
     A*
     A            RCNUM     R              3REFFLD(CNUMA)
     A            RACSQ     R             13REFFLD(ACSQA)
     A            RANAM     R             20REFFLD(ANAM)
     A            RPRFC     R             46REFFLD(PRFC)
     A            RBALN         25A       57
     A                                      SPACEA(1)
     A*
     A*-----------------------------*
     A**  Database Error Reference  *
     A*-----------------------------*
     A*
     A          R MC0550D3                  SPACEB(3)
     A                                    32'*** REFERENCE MC0550'
     A                                    53'DATABASE ERROR ***'
     A*
     A*-------------------*
     A**  Report Trailer  *
     A*-------------------*
     A*
     A          R MC0550D4
     A  10N11                             46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(2)
     A N10
     AO 11                                51'***  END OF REPORT  ***'
     A                                      SPACEB(2)
