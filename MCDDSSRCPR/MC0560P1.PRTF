      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MC Funding GAP Accrual Report - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Management Accounting Module                         *
      *                                                               *
      *  PRTF/MC0560P1 - Funding GAP Accrual Report - Detail          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 158557 *CREATE     Date 28Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  158557 - PCA015, Provide a Funding Gap Accrual Report        *
      *           New printer file for MC0560.                        *
      *                                                               *
      *****************************************************************
     A*
     A                                      REF(MFRFL)
     A*
     A*--------------------------------*
     A**  Report Header Record Format  *
     A*--------------------------------*
     A*
     A          R MC0560D0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                      SKIPB(3)
     A                                     6'REFERENCE MC0560P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A*
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*
     A                                    54'FUNDING GAP ACCRUAL REPORT +
     A                                       - DETAIL'
     A                                      SPACEA(1)
     A                                    54'--------------------------'
     A                                      SPACEA(2)
     A*
     A*---------------*
     A**  Sub-Heading *
     A*---------------*
     A*
     A  10                                 6'BRANCH'
     A  10        RBRCA     R             15REFFLD(BRCA)
     A  10        RBRNM     R             19REFFLD(BRNM)
     A                                      SPACEA(2)
     A*
     A*------------------------*
     A**  Detail Sub-Headings  *
     A*------------------------*
     A*
     A                                    21'ACCOUNT'
     A                                    30'ACCOUNT'
     A                                    39'PROFIT'
     A                                    47'ACCRUAL'
     A                                    56'PERIOD'
     A                                      SPACEA(1)
     A*
     A                                     1'CUSTOMER'
     A                                    11'CURRENCY'
     A                                    22'CODE'
     A                                    32'SEQ'
     A                                    39'CENTRE'
     A                                    48'FROM'
     A                                    58'TO'
     A                                    72'GAP BALANCE'
     A                                    92'INTEREST RATE'
     A                                   112'ACCRUAL AMOUNT'
     A                                      SPACEA(1)
     A                                     1'--------'
     A                                    11'--------'
     A                                    21'-------'
     A                                    30'-------'
     A                                    39'------'
     A                                    47'-------'
     A                                    56'------'
     A                                    65'-------------------------'
     A                                    92'-------------'
     A                                   107'-------------------------'
     A                                      SPACEA(1)
     A*
     A*-------------------------*
     A**  Detail Record Format  *
     A*-------------------------*
     A*
     A          R MC0560D1
     A*
     A                                      SPACEB(1)
     A            RCUST     R              2REFFLD(CNUMA)
     A            RCURR     R             13REFFLD(CCY)
     A**Start of change CGL029
     A**********  RACOD     R             22REFFLD(ACODA)                                     CGL029
     A            RACOD         10A       19
     A**End of change CGL029
     A            RACSQ     R             32REFFLD(ACSQA)
     A            RPRFC     R             40REFFLD(PRFC)
     A            RFDAT          7A       47
     A            RTDAT          7A       56
     A            RBALN         24A       65
     A            BLSIGN         2A       88
     A            RRTSP         12A       91
     A            RACRL         24A      106
     A            ACSIGN         2A      129
     A                                      SPACEA(1)
     A*
     A*--------------------------*
     A**  Detail Summary Format  *
     A*--------------------------*
     A*
     A          R MC0560D2
     A*
     A                                   105'=========================='
     A                                      SPACEA(1)
     A            RTACR         25A      106
     A            SMSIGN         2A      129
     A                                      SPACEA(1)
     A*
     A*-----------------------------*
     A**  Database Error Reference  *
     A*-----------------------------*
     A*
     A          R MC0560D3                  SPACEB(3)
     A                                    32'*** REFERENCE MC0560'
     A                                    53'DATABASE ERROR ***'
     A*
     A*-------------------*
     A**  Report Trailer  *
     A*-------------------*
     A*
     A          R MC0560D4
     A  10                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(2)
     A N10                                51'***  END OF REPORT  ***'
     A                                      SPACEB(2)
