      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MC Management A/C Audit Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas   MANAGEMENT ACCOUNTS MODULE
     A*                                                               *
     A*  MC0750AU - MANAGEMENT ACCOUNTING REPORTING                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. XXXXXX             DATE XXXXXXX               *
     A*  NEW PROGRAM    S01416             DATE 17MAY93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  S01416 - Account Balances Report                             *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      ** Header Format
      *
     A          R MC0750AH                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'MC0750AU'
     A            BJURPT        53        40
     A                                   100'DATE'
     A            BJMRDT         7A      105
     A                                   120'PAGE'
     A                                   125PAGNBR
     A                                      EDTCDE(J) SPACEA(1)
      *
     A                                   100'TIME'
     A                                   105TIME EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A            G5RSNM        30A       48 SPACEA(1)
      *
     A            PRTDT         50A       50SPACEA(2)
      *
     A                                    16'REPORT OPTIONS' SPACEA(1)
     A                                    16'--------------' SPACEA(2)
     A            RPOP1         80A       16SPACEA(1)
      *
     A            RPOP2         80A       16SPACEA(1)
      ** Extra Report Options displayed for Management Accounts
      *
     A                                    16'Control Group selected is'
     A            PRGRP          2A       42
     A                                    45'-'
     A            PRCGDS        30A       47SPACEA(1)
     A                                    16'Relative Period Number is'
     A            PRRPNC         4A       42SPACEA(1)
     A                                    16'Term selected is'
     A            PRTRM         15A       33SPACEA(1)
     A                                    16'Type selected is'
     A            PRTYP          9A       33SPACEA(1)
     A                                    16'Analysis by'
     A            AULEH        102A       28SPACEA(1)
      *
      ** Detail Format
      *
     A          R MC0750AD                  TEXT('Detail Format')
     A                                      SPACEB(7)
     A                                    11'REPORT FILE MCBLRPPD'
     A                                      SPACEA(1)
     A                                    11'--------------------'
     A                                      SPACEA(5)
     A                                    11'RECORDS READ   '
     A            NRRD          15S 0     60EDTCDE(3)
     A                                      SPACEA(2)
     A                                    11'DEBIT COLUMN 1 TOTAL'
     A            HWNDR1        15  0     57EDTCDE(Z)
     A            HDCDR1         3  0     72
     A                                      SPACEA(2)
     A                                    11'CREDIT COLUMN 1 TOTAL'
     A            HWNCR1        15  0     57EDTCDE(Z)
     A            HDCCR1         3  0     72
     A                                      SPACEA(2)
     A                                    11'DEBIT COLUMN 2 TOTAL '
     A            HWNDR2        15  0     57EDTCDE(Z)
     A            HDCDR2         3  0     72
     A                                      SPACEA(2)
     A                                    11'CREDIT COLUMN 2 TOTAL'
     A            HWNCR2        15  0     57EDTCDE(Z)
     A            HDCCR2         3  0     72
     A                                      SPACEA(2)
      *
      ** Error Format
      *
     A          R MC0750AE                  TEXT('Error Format')
     A                                      SPACEB(7)
     A                                    40'*** Database in Error ***'
      *
     A                                      SPACEA(1)
     A                                    44'File'
     A                                    55':-'
     A            DBFILE         8        59
      *
     A                                      SPACEA(1)
     A                                    44'Key'
     A                                    55':-'
     A            DBKEY         29        59
      *
     A                                      SPACEA(1)
     A                                    44'In Program'
     A                                    55':-'
     A            DBPGM         10        59
      *
     A                                      SPACEA(1)
     A                                    44'At point'
     A                                    55':-'
     A            DBASE          3S 0     59
      *
      ** No Details to report
      *
     A          R MC0750AN                  TEXT('End of Report Format')
     A                                      SPACEB(5)
     A                                    40'*** No Details to Report ***'
      *
