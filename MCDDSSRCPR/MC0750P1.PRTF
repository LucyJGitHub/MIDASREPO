      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MC Management Accounting Reporting')
     A*****************************************************************
     A*                                                               *
     A*  Midas Management Accounts Module
     A*                                                               *
     A*  MC0750P1 - Management Accounting Balance Reporting           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND. NO. CSD005          DATE 24MAR99                    *
     A*  PREV AMEND NO. XXXXXX             DATE XXXXXXX               *
     A*  NEW PROGRAM    S01416             DATE 17MAY93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  CSD005 - Switchable feature- if account narrative feature       *
     A*           is active the current account description shall be     *
     A*           replaced with the account narrative.                   *
     A*  S01416 - Account Balances Report                             *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      **
      **   *************
      **   PAGE HEADINGS
      **   *************
      **
     A          R W01                       SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     1'REFERENCE MC0750P1'
     A            BJURPT        53A       39
     A                                   100'DATE'
     A            BJMRDT         7A      105
     A                                   122'PAGE'
     A                                   127PAGNBR
     A                                      EDTCDE(J) SPACEA(1)
      *
     A                                   100'TIME'
     A                                   105TIME EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    40'MANAGEMENT ACCOUNTS -'
     A            TRMTYP        25A       62SPACEA(1)
      *
     A            G5RSNM        30A       48SPACEA(1)
      *
     A            PRTDT         50A       51SPACEA(1)
      *
      *
      *
     A                                     1'CURRENCY - '
     A            RPCCY         32A       12
      *
     A  73                                93'BRANCH - '
     A  73        A8BRNM        30       102
      *
     A  74                                92'COMPANY - '
     A  74        APCMNM        30       102
      *
     A  75                                80'CONSOLIDATED'
      *
     A                                   132' ' SPACEA(1)
      *
     A                                    80'MANAGEMENT ACCOUNTS -'
     A            PRCGDS        30A      102SPACEA(1)
      *
     A**
      *
     A  44 54
     AO 44N43                             14'A/C'
     A  45                                54'PRFT'
     A  47                                63'TRANS'
     A  48                                70'TRANS'
     A  49N54                             78' ASSOC. '
     A                                   132' ' SPACEA(1)
      *
      *
     A  42                                 3'BRCH'
     A  44 54
     AO 44N43                             14'SEQ'
     A  49 54                             18'ASSOC.'
     A  49
     AO 43                                25'CUSTOMER'
     A  41                                50'CCY'
     A  45                                54'CNTR'
     A  46                                59'BK'
     A  47                                63'TYPE'
     A  48                                69'SUB-TYPE'
     A  43 54
     AO 49N54                             78'CUSTOMER'
     A N56        FMDT1         12A       94
     A N56 55     FMDT2         12A      118
     A                                   132' ' SPACEA(2)
      *
      *
     A          R W02                       SPACEA(1)
      *
     A**  Print extra blanks before a heading line
      *
     A  71N81                             86'---------------------'
     A  71N81 55
     AAN56                               110'---------------------'
     A N81                               132' ' SPACEA(1)
      *
      *************                                       Begin/CSD005/
      *
     A**61*****   NAM1          47         1
      *
     A**62*****   NAM2          47         3
      *
     A**63*****   NAM3          47         5
      *
     A**64*****   NAM4          47         7
      *
     A**65*****   NAM5          47         9
      *
     A**66*****   NAM6          47        11
      *
     A**67*****   NAM7          47        13
      *
     A**68*****   NAM8          47        15
      *
     A**69*****   NAM9          47        17
      *
     A  61        NAM1          72         1
      *
     A  62        NAM2          72         3
      *
     A  63        NAM3          72         5
      *
     A  64        NAM4          72         7
      *
     A  65        NAM5          72         9
      *
     A  66        NAM6          72        11
      *
     A  67        NAM7          72        13
      *
     A  68        NAM8          46        15
      *
     A  69        NAM9          42        17
      *
      *************                                         End/CSD005/
      *
     A  71        RAMT1         21        86
     A  71 55N56  RAMT2         21       110
     A  71 81                             86SPACEB(1) +
     A                                      '====================='
     A  71 81 55
     AAN56                               110'====================='
      *
     A*
      *
     A          R D01                       SPACEA(1)
     A*
     A* Branch
     A  42        XBRCA          3         4
     A*
     A* Customer number
     A  43N54
     AO 49 54     XBCUST         6         8
     A*
     A* A/C Seq
     A  44 43N54                          14 '-'
     A  44        XACSEQ         2        15
     A*
     A* Customer name
     A  43N54
     AO 49 54     XBCRNM        20        18
     A  43N54
     AO 49 54     XBCRTN        10        39
     A*
     A* Currency
     A  41        XBCURR         3        50
     A*
     A* Profit Centre
     A  45        XPROF          4        54
     A*
     A* Book
     A  46        XBOOK          2        59
     A*
     A* Trans Type
     A  47        XTYPE          5        63
     A*
     A* Trans Sub-Type
     A  48        XSUB          10        68
     A*
     A* Associated Customer
     A  43 54
     AO 49N54     XASSOC         6        79
     A*
     A* Detail value
     A            DAMT1         21        86
     A  55N56     DAMT2         21       110
      *
     A*/COPY WNCPYSRC,MC0750P101
      *
      ** End of Report Format
      *
     A          R W03                       TEXT('End of Report Format')
     A                                      SKIPB(58)
     A                                    56'*** End of'
     A  73                                67'Branch Report ***'
     A  74                                67'Company Report ***'
     A N73N74                             67'Report ***'
