     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MC Insert dummy relative period nos')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Management Accounting Module
     F*                                                               *
     F*  MC0118 - INSERT DUMMY RELATIVE PERIOD NUMBERS                *
     F*                                                               *
     F*  Function: This program will insert dummy relative period     *
     F*  I/C Int   numbers if the Set Code start date is in the       *
     F*            future.                                            *
     F*                                                               *
     F*  Called By: MC0115                                            *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  LAST AMEND NO. CMC001             DATE 18MAR96               *
     F*  PREV AMEND NO. XXXXXX             DATE   00XXX00             *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
     F*  CMC001 - Management Accounts Enhancement for PCA:            *
     F*           Recompile due to change of PF/GLPERDPD              *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*****************************************************************
     FGLPERDL0UF  E           K        DISK         KCOMIT
     F*****************************************************************
     F* F U N C T I O N  O F  I N D I C A T O R S                     *
     F*                                                               *
     F*  11 - Set lower limits on GLPERDL0 successful.                *
     F*  12 - End of GLPERDL0 reached.                                *
     F*                                                               *
     F* U7-U8 - Database Error.                                       *
     F*****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
      *  Copyright array
      /SPACE 3
     ILDA       E DSLDA
      *  Local Data Area
      *
     IPSDS       SDS
      * Program Status Data Structure
     I                                      244 253 JOB
     I                                      254 263 USER
      ********************************************************************
     C/EJECT
     C                     MOVEACPY@      BIS@   80
      *
     C                     MOVE 'Y'       RUN1    1
      *
      *  Parameter from MC0115 - Period Set Code
     C           *ENTRY    PLIST
     C                     PARM           SETCOD  1
      *
      * Initialise counter.
     C                     Z-ADD1         COUNT   30
      *
      * Set lower limits on GLPERDL0 to find first period for Set Code.
     C           SETCOD    SETLLGLPERDL0                 11
      *
     C           *IN11     IFEQ '1'
     C                     READEGLPERDL0                 12
      *
     C           SETCOD    DOWEQDTPSTC
     C           *IN12     ANDEQ'0'
      *
      * Update GLPERDL0 with dummy Relative Period Number
     C                     Z-ADDCOUNT     DTRPDN
     C                     EXCPTUPDAT
      *
     C                     ADD  1         COUNT
     C                     READ GLPERDL0                 12
     C                     END
      *
     C                     ELSE
      *
      * Database error handling.
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'MC0118'  DBPGM
     C                     MOVELSETCOD    DBKEY
     C                     MOVEL'GLPERDPD'DBFILE           ***************
     C                     Z-ADD001       DBASE            * DBERROR 001 *
     C                     OUT  LDA                        ***************
     C                     EXSR *PSSR
     C                     END
      *
     C                     SETON                     LR
     C/EJECT
      ********************************************************************
      *
      *  *PSSR Subroutine to handle program error
      *
      *    CALLED BY : Main Processing
      *
      *    CALLS     : PGM/DBERRCTL
      *
     C           *PSSR     BEGSR
      *
      * Check that this is the first time through the routine
      * to prevent recursive calling which would loop the progam.
     C           RUN1      IFEQ 'Y'
     C                     MOVE 'N'       RUN1
     C                     DUMP
     C                     END
      *
      * Call DBERRCTL to control the program termination
     C                     CALL 'DBERRCTL'
     C                     ENDSR
     O/EJECT
      * Update Relative Period Number field on GLPERDL0.
     O@BLREDR E                UPDAT
     O                         DTRPDN
      ********************************************************************
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
