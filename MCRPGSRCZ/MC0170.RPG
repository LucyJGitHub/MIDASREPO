     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MC Period File Reorganisation')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Management Accounting Module
     F*                                                               *
     F*  MC0170 - Period File Reorganisation                          *
     F*                                                               *
     F*  Function: This program calculates the earliest year to       *
     F*  (CoB)     retain details for each Period Set and resets      *
     F*            'First Period Year' to this value. Detail records  *
     F*            for the Period Set earlier than this date are      *
     F*            then deleted.                                      *
     F*            A P1 report of deleted records is produced if      *
     F*            required by icd. If report is required but no      *
     F*            records have been deleted an AU report only is     *
     F*            produced.                                          *
     F*            This program will be run monthly.                  *
     F*                                                               *
     F*  Called by: MCC0170 - Period File Reorganisation Control      *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  LAST AMEND NO. CMC001             DATE 18MAR96               *
     F*  PREV AMEND NO. XXXXXX             DATE NNAAANN               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
     F*  CMC001 - Management Accounts Enhancement for PCA:            *
     F*           Recompile due to change of PF/GLPERDPD              *
     F*                                                               *
     F*****************************************************************
      *
     FMC0170P1O   E                    PRINTER      KINFSR INFSR      UC
     F                                              KINFDS MCRPT
      ** P1 Report
     FMC0170AUO   E                    PRINTER      KINFSR INFSR      UC
     F                                              KINFDS MCAUD
      ** AU Report
     FGLPERDL0UF  E           K        DISK         KINFSR INFSR
      ** Period Details, used to delete records before history date
      ** Format name @BLREDR
     FGLPERDL4IF  E           K        DISK         KINFSR INFSR
      ** Period Details with Relative Period Number = 0
      ** Format name @PERDEG
     FGLPERSL0UF  E           K        DISK         KINFSR INFSR
      ** Period Sets
      ** Format name @BJREDS
      /EJECT
      *****************************************************************
      *
      *  F U N C T I O N   O F   I N D I C A T O R S
      *
      * 10 - EOF for GLPERSL0 used by main processing loop
      * 20 - Chain fail to Period Details file, GLPERDL4
      * 21 - EOF for GLPERDL0 used in sr/#B
      * 22 - Used for SETLL on Period Details file, GLPERDL0
      * 43 - If setoff Term 1 is defined and will be printed
      * 44 - If setoff Term 2 is defined and will be printed
      * 98 - Seton if date format is MMDDYY (rather than DDMMYY)
      * U1 - At least one record has been deleted
      * U6 - Program error
      * U7 - Database error
      * U8 - Database error
      *
      **************************************************************
      *
      *  S U B R O U T I N E   I N D E X
      *
      *  (Note that sr name indicates calling routine, eg sr/#BC is
      *   called from sr/#B, #Z routines are called from anywhere)
      *  (Note that sr's are ordered in the program to minimise
      *   paging by bunching sr as close to exsr line as possible)
      *
      * MAIN   - Main control routine
      * #B     - Process Period Details records for the Period Set
      * #BA    - Write to report
      * ZDATE2 - Convert Day Number to Date - Standard SR
      * #ZA    - Write audit trail and end of report line
      * #A     - Initial processing
      * *PSSR  - Program error subroutine
      * INFSR  - File error subroutine
      *
      **************************************************************
      *
      *  P R O G R A M   C A L L E D   I N D E X
      *
      * ZSFILE    - Log spool file to RCF
      * AOBANKR0  - Obtain bank details
      *
      **************************************************************
      /EJECT
      *
      ** CPYP  Copyright array
     E                    CPYP    1   1 80
      *
      ** Copy in date formatting arrays
      /COPY ZSRSRC,ZDATE2Z1
      **************************************************************
      /EJECT
     IDSFDY     E DSDSFDY
      **  First DS for access programs, Short Data Structure
      *
     ISDBANK    E DSSDBANKPD
      **  Bank details
      *
     IMCRPT       DS
      ** File information DS for MC0170P1 report
      *
     I                                       83  92 PFILE1
     I                                    B 123 1240PFNUM1
      *
     IMCAUD       DS
      ** File information DS for MC0170AU report
      *
     I                                       83  92 PFILEA
     I                                    B 123 1240PFNUMA
      *
     ILDA       E DSLDA
      ** Local data structure used for error handling
      *
      *
     IGLSTAT    E DSGLSTAT
      ** GLSTAT data structure, to obtain Print Option field
      *
     IPSDS       SDS
      **  Program Status DS
      *
     I                                      244 253 JOB
     I                                      244 253 SWSID
     I                                      254 263 SUSER
      *
      /EJECT
      **************************************************************
      * MAIN routine - Controlling routine
      *
      * NOTE: Generally fields beginning with 'P' are program defined.
      *       Generally fields beginning with 'R' are used by
      *       the printer file.
      *
      **************************************************************
      *
      ** Run initial processing
     C           PBIS      IFEQ *BLANKS                    B1
     C                     EXSR #A                         *1
     C                     END                             E1
     C           *INU7     CABEQ'1'       MAIN1            CAB GOTO
      *
      ** Process file lf/GLPERSL0, i.e. read all Period Sets
     C           *LOVAL    SETLL@BJREDS              10
      *
      ** Skip processing if no Period Sets defined
     C           *IN10     CABEQ'1'       MAIN1            CAB GOTO
      *
      ** Process all records on lf/GLPERSL0, i.e. all Period Sets
     C           *IN10     DOUEQ'1'                        B1
     C                     READ @BJREDS                  10*1
      *
      ** If Period Set record found, process details using sr/#B and
      ** update lf/GLPERSL0 for change to First Period Year
     C           *IN10     IFEQ '0'                        B*2
     C                     EXSR #B                         **2
     C           *INU7     CABEQ'1'       MAIN1            **2 CAB GOTO
     C                     EXCPTPERSL0                     **2
     C                     END                             E*2
      *
     C                     END                             E1
      *
     C           MAIN1     TAG                             TAG MAIN1
      *
      ** Write end of report and audit trail as required
     C           *INU7     IFEQ '0'                        B1
     C                     EXSR #ZA                        *1
     C                     END                             E1
      *
      ** Program termination
     C                     SETON                     LR
      *
      **************************************************************
      /EJECT
      **************************************************************
      *
      * SR/#B - Process Period Set just read
      *
      * Called from:      Main routine
      *
      **************************************************************
     C           #B        BEGSR
      *
      ** Must obtain earliest retained year using current year minus
      ** history retention period
      *
      ** Obtain Period Details record for current period and Period
      ** Set being processed. Read lf/GLPERDL4 which selects only
      ** records with Relative Period Number = 0 (i.e. current period)
     C           STPSTC    CHAIN@PERDEG              20
      *
      ** If record found, calculate Earliest Retained Year, PERRY,
      ** as current year minus historic retention period
     C           *IN20     IFEQ '0'                        B1
     C           DTPDCT    CAT  DTPDYR    PYEAR   4        *1
     C                     MOVE PYEAR     PYRNO   40       *1
     C           PYRNO     SUB  STHPYR    PERRY   40       *1
      *
      ** Reset First Period Year if before Earliest Retained Year
     C           STFPYR    IFLT PERRY                      B*2
     C                     Z-ADDPERRY     STFPYR           **2
     C                     END                             E*2
      *
      ** Else record not found for current period
     C                     ELSE                            X1
     C                     Z-ADD*ZERO     STFPYR           *1
     C                     END                             E1
      *
      *
      ** Now read all Period Details Period Set from lf/GLPERDL0
      ** for the Period Set being processed
     C           STPSTC    SETLL@BLREDR                  22
      *
      ** If no Period Details found then database error
     C           *IN22     IFEQ '0'                        B1
     C                     MOVEL'GLPERDL0'DBFILE           *1
     C                     Z-ADD002       DBASE            *1
     C                     MOVELSTPSTC    DBKEY            *1
     C                     MOVEL'MC0170'  DBPGM            *1
     C                     SETON                     U7U8LR*1
     C                     DUMP                            *1
     C                     EXSR #ZA                        *1
     C                     GOTO #B9                        *1 GOTO
     C                     END                             E1
      *
      ** Must delete all Period Details records for the Period Set
      ** being processed if they have Period century/year prior to
      ** the First Period Year
      *
      ** Do until end of file
     C           *IN21     DOUEQ'1'                        B1
      ** or a different Period Set
     C           DTPSTC    ORNE STPSTC                     B1
      *
      ** Read lf/GLPERDL0, Period Details file
     C                     READ @BLREDR                  21*1
      *
      ** Concatanate the Period Century/Year and put in numeric field
     C           DTPDCT    CAT  DTPDYR    PYEAR            *1
     C                     MOVE PYEAR     PYRNO            *1
      *
      ** Process records read
     C           *IN21     IFEQ '0'                        B*2
      *
      ** Process records to be deleted (i.e. prior to retention period)
     C           DTPSTC    IFEQ STPSTC                     B**3
     C           PYRNO     ANDLTSTFPYR                     B**3
      *
      ** If File Reorganisation Print Option (GLSTAT) is set then
      ** report all deleted Period Detail records
     C           FRPR      IFEQ 'Y'                        B***4
     C                     EXSR #BA                        ****4
     C           *INU7     CABEQ'1'       #B9              ****4 GOTO
     C                     END                             E***4
      *
     C                     DELET@BLREDR                    ***3
     C                     SETON                     U1    ***3
      *
      ** Else, if record is not to be deleted must release
      ** the record (it is read in for update), in this case there
      ** will be no records prior to the retention period
     C                     ELSE                            X**3
     C                     EXCPTPERDL0                     ***3
     C                     END                             E**3
     C                     END                             E*2
     C                     END                             E1
      *
     C           #B9       ENDSR
      **************************************************************
      /EJECT
      **************************************************************
      *
      * SR/#BA    Produce report listing deleted Period Details
      *
      * Called from:     #B
      *
      **************************************************************
     C           #BA       BEGSR
      *
      ** If P1 report is not open, open it and register with RCF
     C           PFILP1    IFEQ *BLANKS                    B1
     C                     MOVE 'OPN'     PFILP1  3        *1
     C                     OPEN MC0170P1                   *1
      *
      ** Output headings
     C                     Z-ADD1         RPAGE            *1
     C                     Z-ADD7         PLINES  20       *1
     C                     WRITEMC0170H1                   *1
      *
      ** Convert binary number to packed numeric and run RCF processing
     C                     Z-ADDPFNUM1    PSNUM   60       *1
     C                     CALL 'ZSFILE'                   *1
     C                     PARM           PSEQ    5        *1
     C                     PARM *BLANKS   PPARM   3        *1
     C                     PARM           PFILE1           *1
     C                     PARM           PSNUM            *1
     C                     PARM           PSERR   1        *1
      *
     C           PSERR     IFEQ 'Y'                        B*2
     C                     SETON                     U7U8LR**2
     C                     Z-ADD003       DBASE            **2
     C                     MOVELPFILE1    DBFILE           **2
     C                     MOVEL'See Dump'DBKEY            **2
     C                     MOVEL'ZSFILE ' DBPGM            **2
     C                     DUMP                            **2
     C                     EXSR #ZA                        **2
     C                     GOTO #BA9                       **2 GOTO
     C                     END                             E*2
     C                     END                             E1
      *
      ** Output headings if page is full
     C           PLINES    IFGE 58                         B1
     C           STPSTC    ANDEQPPSTC                      B1
     C           PLINES    ORGE 51                         B1
     C           STPSTC    ANDNEPPSTC                      B1
     C                     ADD  1         RPAGE            *1
     C                     Z-ADD7         PLINES           *1
     C                     WRITEMC0170H1                   *1
     C                     END                             E1
      *
      ** If the Period Set has changed must output the subheadings
      ** and increment line count
     C           STPSTC    IFNE PPSTC                      B1
     C                     WRITEMC0170H2                   *1
     C                     MOVE STPSTC    PPSTC            *1
     C           *LIKE     DEFN STPSTC    PPSTC            *1
     C                     ADD  6         PLINES           *1
     C                     END                             E1
      *
      ** Now set up the detail lines, the following fields do not
      ** need formatting here:
      ** Period Year (PYEAR)
      ** Period Number in year (DTPDNY)
      ** Relative Period Number (DTRPDN)
      ** Period Name (DTPDNM)
      *
      ** Start of Year Date
     C                     Z-ADDDTSOYD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RSYDD
      *
      ** Determine if Term 1 is defined, set output indicator
      ** Only print Term 1 details if *IN43 is off
     C           STPTM1    IFGT *ZERO                      B1
     C                     Z-ADDDTST1D    ZDAYNO           *1
     C                     EXSR ZDATE2                     *1
     C                     MOVE ZADATE    RSTT1            *1
     C                     SETOF                     43    *1
     C                     ELSE                            X1
     C                     SETON                     43    *1
     C                     END                             E1
      *
      ** Determine if Term 2 is defined, set output indicator
      ** Only print Term 2 details if *IN44 is off
     C           STPTM2    IFGT *ZERO                      B1
     C                     Z-ADDDTST2D    ZDAYNO           *1
     C                     EXSR ZDATE2                     *1
     C                     MOVE ZADATE    RSTT2            *1
     C                     SETOF                     44    *1
     C                     ELSE                            X1
     C                     SETON                     44    *1
     C                     END                             E1
      *
      ** Start of Period Date
     C                     Z-ADDDTSPDD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    RSPRD
      *
      ** End of Period Date
     C                     Z-ADDDTEPDD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    REPRD
      *
      ** Write a detail line, and increment line count
     C                     WRITEMC0170D1
     C                     ADD  1         PLINES
     C           #BA9      ENDSR
      **************************************************************
      /EJECT
      /COPY ZSRSRC,ZDATE2Z2
      /EJECT
      **************************************************************
      *
      * SR/#ZA- Audit trail production. An audit trail is produced
      *         if there are no records to delete and a report is
      *         required or a database or program error occurs.
      *         And write end of report line to P1 report.
      *
      * Called from:     MAIN routine
      *                  #A    Initial processing
      *                  INFSR File error processing
      *                  *PSSR File error processing
      *
      **************************************************************
     C           #ZA       BEGSR
      *
      ** Produce error report on audit trail if required
     C           *INU1     IFEQ '0'                        B1
     C           FRPR      ANDEQ'Y'                        B1
     C           *INU7     OREQ '1'                        B1
     C           *INU6     OREQ '1'                        B1
      *
     C                     OPEN MC0170AU                   *1
      *
      ** Convert binary number to packed numeric and run RCF processing
      ** to log audit trail to RCF
     C                     Z-ADDPFNUMA    PSNUM   60       *1
     C                     CALL 'ZSFILE'                   *1
     C                     PARM           PSEQ             *1
     C                     PARM *BLANKS   PPARM            *1
     C                     PARM           PFILEA           *1
     C                     PARM           PSNUM            *1
     C                     PARM           PSERR            *1
      *
     C           PSERR     IFEQ 'Y'                        B*2
     C                     Z-ADD004       DBASE            **2
     C                     MOVELPFILEA    DBFILE           **2
     C                     MOVEL'See Dump'DBKEY            **2
     C                     MOVEL'ZSFILE ' DBPGM            **2
     C                     SETON                     U7U8LR**2
     C                     DUMP                            **2
     C                     END                             E*2
      *
      ** Write headings
     C                     WRITEMC0170A1                   *1
      *
      ** Write error format
     C           *INU7     IFEQ '1'                        B*2
     C           *INU6     OREQ '1'                        B*2
     C                     WRITEMC0170A3                   **2
     C                     END                             E*2
      *
      ** Write no details format to AU report
     C           *INU1     IFEQ '0'                        B*2
     C           *INU7     ANDEQ'0'                        B*2
     C           *INU6     ANDEQ'0'                        B*2
     C                     WRITEMC0170A2                   **2
     C                     END                             E*2
      *
     C                     CLOSEMC0170AU                   *1
      *
     C                     END                             E1
      *
      *
      ** Write end of report line to the P1 report if required
     C           *INU1     IFEQ '1'                        B1
     C           FRPR      ANDEQ'Y'                        B1
     C           *INU7     ANDEQ'0'                        B1
     C           *INU6     ANDEQ'0'                        B1
      *
      ** If required start new page and write headings
     C           PLINES    IFGE 55                         B*2
     C                     ADD  1         RPAGE            **2
     C                     WRITEMC0170H1                   **2
     C                     END                             E*2
      *
     C                     WRITEMC0170D2                   *1
     C                     CLOSEMC0170P1                   *1
     C                     END                             E1
      *
     C           #ZA9      ENDSR
      **************************************************************
      /EJECT
      **************************************************************
      *
      * SR/#A - Initial processing
      *
      * Called from:     MAIN routine
      *
      **************************************************************
     C           #A        BEGSR
      *
      *
      ** Use copyright array to ensure copyright gets into object
     C                     MOVEACPYP      PBIS   80
      *
      ** Obtain bank details
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** DB error if not found
     C           PRTCD     IFNE *BLANK                     B1
     C                     MOVEL'SDBANKPD'DBFILE           *1
     C                     Z-ADD001       DBASE            *1
     C                     MOVEL'*FIRST  'DBKEY            *1
     C                     MOVEL'MC0170'  DBPGM            *1
     C                     SETON                     U7U8LR*1
     C                     DUMP                            *1
     C                     EXSR #ZA                        *1
     C                     GOTO #A9                        *1 GOTO
     C                     END                             E1
      *
      ** Setup the date format indicator used by sr/ZDATE2
     C           BJDFIN    IFEQ 'M'                        B1
     C                     SETON                     98    *1
     C                     END                             E1
      *
      ** Obtain File Reorg. Print Option field from dtaara/GLSTAT
     C           *NAMVAR   DEFN           GLSTAT
     C                     IN   GLSTAT
      *
     C           #A9       ENDSR
      **************************************************************
      /EJECT
      **************************************************************
      *
      * SR/*PSSR - Program error subroutine
      *
      * Called by:   Executes whenever program error occurs
      *
      ***************************************************************
     C           *PSSR     BEGSR
      *
      ** Set PERR1 to 'Y' to prevent looping if further errors
     C           PERR1     IFNE 'Y'                        B1
     C                     MOVE 'Y'       PERR1   1        *1
     C                     SETON                     U6    *1
      *                                                    *1
      ** Set up fields in LDA
     C                     MOVEL'*PSSR   'DBKEY            *1
     C                     Z-ADD991       DBASE            *1
     C                     MOVEL'MC0170'  DBPGM            *1
      *
      ** Dump the program
     C                     DUMP                            *1
      *
      ** Produce error report on audit trail
     C                     EXSR #ZA                        *1
     C                     END                             E1
      *
      ** Terminate the program
     C                     SETON                       LR
     C                     RETRN
      *
     C                     ENDSR
      **************************************************************
      /EJECT
      **************************************************************
      *
      * SR/INFSR - File error subroutine
      *
      * Called by:   Executes whenever file exception/error occurs
      *
      ***************************************************************
      *
     C           INFSR     BEGSR
      *
      ** Set PERR2 to 'Y' to prevent looping if further errors
     C           PERR2     IFNE 'Y'                        B1
     C                     MOVE 'Y'       PERR2   1        *1
     C                     SETON                     U7U8  *1
      *                                                    *1
      ** Set up fields in LDA
     C                     MOVEL'INFSR   'DBKEY            *1
     C                     Z-ADD992       DBASE            *1
     C                     MOVEL'MC0170'  DBPGM            *1
      *
      ** Dump the program
     C                     DUMP                            *1
      *
      ** Produce error report on audit trail
     C                     EXSR #ZA                        *1
     C                     END                             E1
      *
      ** Terminate the program
     C                     SETON                       LR
     C                     RETRN
      *
     C                     ENDSR
      **************************************************************
      /EJECT
      *
      ** Release record from lf/GLPERDL0 if no update required
     O@BLREDR E                PERDL0
      *
      ** Update record from lf/GLPERSL0 for First Period Year change
     O@BJREDS E                PERSL0
     O                         STFPYR
      *
      **************************************************************
      *
** CPYP         OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN used by sr/ZDATE2
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR used by sr/ZDATE2
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES used by sr/ZDATE2
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
