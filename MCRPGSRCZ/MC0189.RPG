     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MC Check sets for dummy rel perd nos')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Management Accounting Module
     F*                                                               *
     F*  MC0189 - CHECK FOR DUMMY RELATIVE PERIOD NOS. AND UPDATE     *
     F*           PERIOD DETAILS FILE IF NECESSARY                    *
     F*                                                               *
     F*  Function: This program will find any period sets without a   *
     F*  COB       current relative period number and check if the    *
     F*            first period has become current (or later).  If    *
     F*            this is the case the relative period numbers are   *
     F*            updated.                                           *
     F*                                                               *
     F*  Called By: MCC0190 - Update Relative Period Numbers Control. *
     F*                                                               *
     F*  Calls: AOBANKR0 - Read Access to Bank Details.               *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  LAST AMEND NO. CMC001             DATE 18MAR96               *
     F*  PREV AMEND NO. XXXXXX             DATE   00XXX00             *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
     F*  CMC001 - Management Accounts Enhancement for PCA:            *
     F*           Recompile due to change of PF/GLPERDPD              *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*****************************************************************
     FGLPERDL2IF  E           K        DISK
     FGLPERSL1IF  E           K        DISK
     FGLPERDL0UF  E           K        DISK
     FMC0189AUO   E                    PRINTER                        UC
      /EJECT
     F*****************************************************************
     F*  F U N C T I O N  O F  I N D I C A T O R S                    *
     F*                                                               *
     F*  11 - End of GLPERSL1 reached.                                *
     F*  12 - End of GLPERDL2 reached.                                *
     F*  13 - Set lower limits for GLPERDL0 successful.               *
     F*  14 - End of GLPERDL0 reached.                                *
     F*                                                               *
     F* U7-U8 - Database Error.                                       *
     F*****************************************************************
      /SPACE 3
     F*****************************************************************
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  *PSSR     - Error handling subroutine                        *
     F*****************************************************************
      /EJECT
      *
     E                    CPY@    1   1 80
      *  Copyright array
      /SPACE 3
     ILDA       E DSLDA
      *  Local Data Area
      *
     ISDBANK    E DSSDBANKPD
      *  DS for SDBANKPD
      *
     IDSFDY     E DSDSFDY
      * Short Data Structure for Access Objects
      *
     IPSDS       SDS
      * Program Status Data Structure
     I                                      244 253 WSID
     I                                      254 263 USER
     C/EJECT
      *
     C                     MOVEACPY@      BIS@   80
      *
     C                     MOVE 'Y'       RUN1    1
     C                     MOVE *BLANKS   SEQNO   5
     C                     MOVEL'MC0189'  DBPGM
      *
      * Call Access Program AOBANKR0 to retrieve bank details.
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG    'RTNCD   7
     C                     PARM '*FIRST  'OPTN    7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      * If error found execute error routine.
     C           RTNCD     IFNE *BLANK
     C                     MOVEL'SDBANKPD'ERRFIL  8         *************
     C                     Z-ADD001       ERRBAS  30        *DBERROR 001*
     C                     EXSR *PSSR                       *************
     C                     END
      *
      * Set up report fields.
     C                     MOVE BJMRDT    RMRDT
     C                     MOVE BJURPT    RURPT
      *
      * Set lower limits on GLPERSL1 to read first record.
     C           *LOVAL    SETLLGLPERSL1
     C                     READ GLPERSL1                 11
      *
      * Do while there are records in GLPERSL1.
     C           *IN11     DOWEQ'0'
      *
      * Set lower limits on GLPERDL2 to read first record.
     C           *LOVAL    SETLLGLPERDL2
     C                     READ GLPERDL2                 12
      *
      * Do while the Set Code from GLPERDL2 does not match the Set Code
      * from GLPERSL1 and there are records on GLPERDL2.
     C           STPSTC    DOWNEDTPSTC
     C           *IN12     ANDEQ'0'
      *
      * Continue reading GLPERDL2 until a match is found or the end of
      * file is reached.
     C                     READ GLPERDL2                 12
     C                     END
      *
      * If the end of file was reached without a match being found
      * this means that the Set Code does not have a current
      * Relative Period Number.
     C           *IN12     IFEQ '1'
      *
      * Read the first period detail record for the Set Code and
      * check if the first start date is less than or equal to the
      * rundate.
     C           STPSTC    SETLLGLPERDL0                 13
     C           *IN13     IFEQ '1'
     C                     READEGLPERDL0                 14
     C           DTSOYD    IFLE BJRDNB
      *
      * If the first period has now become current then update
      * the relative period number for each period for the Set Code.
     C           STPSTC    DOWEQDTPSTC
     C           *IN14     ANDEQ'0'
     C                     SUB  1         DTRPDN
     C                     EXCPTUPDAT
     C                     READ GLPERDL0                 14
     C                     END
      *
     C                     END
      *
      * If detail record not found for the Set Code then perform
      * error handling.
     C                     ELSE
     C                     MOVEL'GLPERDPD'ERRFIL
     C                     MOVELSTPSTC    ERRKEY  1         *************
     C                     Z-ADD002       ERRBAS            *DBERROR 002*
     C                     EXSR *PSSR                       *************
     C                     END
      *
     C                     END
      *
     C                     READ GLPERSL1                 11
     C                     END
      *
     C                     SETON                     LR
     C/EJECT
      ********************************************************************
      *
      *  *PSSR Subroutine to handle program error
      *
      *    CALLED BY : Main Processing
      *
     C           *PSSR     BEGSR
      *
      * Check that this is the first time through the routine
      * to prevent recursive calling which would loop the progam.
     C           RUN1      IFEQ 'Y'
     C                     MOVE 'N'       RUN1
      *
     C                     DUMP
      *
      * Database error handling.
     C           ERRFIL    IFNE *BLANKS
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVELERRKEY    DBKEY
     C                     MOVELERRFIL    DBFILE
     C                     Z-ADDERRBAS    DBASE
     C                     OUT  LDA
      *
      * Open audit file and perform ZSFILE processing.
     C                     OPEN MC0189AU
      *
      * Write audit report.
     C                     WRITEAU1F
     C                     END
      *
     C                     END
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDSR
      ********************************************************************
      /EJECT
      * Update Relative Period Number field on GLPERDL0.
     O@BLREDR E                UPDAT
     O                         DTRPDN
      ********************************************************************
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
