     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MC Select Groups for Update')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Management Accounting Module
     F*                                                               *
     F*  MC0260 - Select Groups for Update                            *
     F*                                                               *
     F*  Function: This program allows the user to select one or all  *
     F*  (I/C Int) Control Groups for which the Management Accounts   *
     F*            are to be brought up to date.                      *
     F*                                                               *
     F*  Called by: MCC0260 - Select Groups for Update Control        *
     F*                                                               *
     F*  Calls: AOHBCGR0 - Access group details                       *
     F*                                                               *
     F*  Parameters: 2A Group code                                    *
     F*              1A Return code                                   *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  LAST AMEND NO.                    DATE                       *
     F*                                                               *
     F*****************************************************************
      *
     FMC0260DFCF  E                    WORKSTN
      * Select Groups for Update screen
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *  03  -  F3 pressed                                            *
      *  06  -  F6 pressed                                            *
      *  10  -  Valid Group Code has been entered                     *
      *  20  -  Invalid Group Code has been entered                   *
      *  U7  -  Database error                                        *
      *  U8  -  Database error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  None                                                         *
      *                                                               *
      *****************************************************************
      /SPACE 3
     E                    CPY@    1   1 80
      /SPACE 3
     IDSHBCG    E DSGLHBCGPD
      * Data structures for access programs
     IRUNDAT    E DS
      * Run Date DS
     IDSFDY     E DSDSFDY
      * First DS for Access Programs, Short Data Structure
     ILDA       E DSLDA
      * Local data structure used for error handling
     IPSDS       SDS
      * Program Status DS
     I                                      244 253 SWSID
     I                                      254 263 SUSER
      /EJECT
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
      * Receive parameters
     C           *ENTRY    PLIST
     C                     PARM           XGRPC   2        Group code
     C                     PARM           XRETC   1        Return code
      *
     C                     MOVEACPY@      BIS@   80
      *
      * Define the error DS
     C           *NAMVAR   DEFN           LDA
      *
      * In the Dataarea RUNDAT
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
      * Move run date (alpha) to screen field for display
     C                     MOVELAGMRDT    SMRDT   7
      *
      * Write select screen
     C           *INKF     DOUEQ'1'                        --- B001
     C           *INKC     DOUEQ'1'                        --- B002
     C           *IN10     OREQ '1'
     C                     EXFMTSELECT
     C                     SETOF                     102021
      *
     C           *IN03     IFEQ '1'                        --- B003
     C                     MOVEL'Y'       XRETC
     C                     SETON                         LR           *
     C                     RETRN
     C                     END                             --- E003
      *
      * Check that the group code is valid (if not left blank)
      * Error if not found
     C           GRPCD     IFNE *BLANKS                    --- B003
     C                     CALL 'AOHBCGR0'
     C                     PARM *BLANKS   P@RTCD  7
     C                     PARM '*KEY   ' P@OPTN  7
     C                     PARM GRPCD     P@HBCG  2
     C           DSHBCG    PARM DSHBCG    DSFDY
      *
      * Set indicator to display error message on screen if no
      * match found
     C           P@RTCD    IFNE *BLANKS                    --- B004
     C                     SETON                     20
      *
      * Else if last change type is 'D' (deleted) display error
      * message on screen
     C                     ELSE                                   4
     C           HCTYLC    IFEQ 'D'                        --- B005
     C                     MOVE '1'       *IN21
      *
      * Else set on valid screen indicator
     C                     ELSE                                   5
     C                     SETON                     10
     C                     END                             --- E005
     C                     END                             --- E004
      *
      * Else set on valid screen indicator without validation
      * if blanks are entered
     C                     ELSE                                   3
     C                     SETON                     10
     C                     END                             --- E003
     C                     END                             --- E002
      *
     C           *INKF     IFEQ '1'                        --- B002
     C           *IN10     ANDEQ'1'
     C                     MOVELGRPCD     XGRPC
     C                     SETON                         LR
     C                     RETRN
     C                     END                             --- E002
     C                     END                             --- E001
      *
      *================================================================
      *  P R O G R A M     E N D                                      *
      *                                                               *
     C                     SETON                         LR           *
     C                     RETRN                                      *
      *                                                               *
      *****************************************************************
      /EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
