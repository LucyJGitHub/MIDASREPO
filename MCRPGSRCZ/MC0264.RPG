     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MC Update One Control Group')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Management Accounting Module
     F*                                                               *
     F*  MC0264 - Update One Control Group                            *
     F*                                                               *
     F*  Function: This program reads a record from the Control       *
     F*  (I/C Bat) Groups file and passes out the relevant            *
     F*            information for updating the Management Accounts   *
     F*            for the group.                                     *
     F*                                                               *
     F*  Called by: MCC0264 - Update One Control Group Control        *
     F*                                                               *
     F*  Calls: None                                                  *
     F*                                                               *
     F*  Parameters: 2A Group code                                    *
     F*              1A Period Set                                    *
     F*              1A Posting Basis                                 *
     F*              5N Start Date                                    *
     F*              8A Sections                                      *
     F*              10A Analysis                                     *
     F*              6N Start Time                                    *
     F*                                                               *
     F*  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  PREV AMEND NO.                    DATE                       *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
     F*  MD046248 - Finastra Rebranding                               *
     F*****************************************************************
      *
     FGLHBCGL1IF  E           K        DISK
      * Historic Balance Control Groups
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *  50  -  DB Error accessing LF/GLHBCGL1                        *
      *  U7  -  Database error                                        *
      *  U8  -  Database error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  U01  -  Update parameters from LF/GLHBCGL1                   *
      *  I01  -  Initialise parameters                                *
      *                                                               *
      *****************************************************************
      /SPACE 3
     E                    CPY@    1   1 80
      /SPACE 3
     ILDA       E DSLDA
      * Local data structure used for error handling
     I            DS
      * DS for 8 sections fields
     I                                        1   8 DSSECT
     I                                        1   1 HCIAAC
     I                                        2   2 HCILAC
     I                                        3   3 HCIIAC
     I                                        4   4 HCIEAC
     I                                        5   5 HCICAC
     I                                        6   6 HCIMAC
     I                                        7   7 HCITAC
     I                                        8   8 HCICTA
     I            DS
      * DS for 10 analysis fields
     I                                        1  10 DSANAL
     I                                        1   1 HCABAC
     I                                        2   2 HCABCY
     I                                        3   3 HCABBR
     I                                        4   4 HCABCU
     I                                        5   5 HCABAS
     I                                        6   6 HCABPC
     I                                        7   7 HCABBK
     I                                        8   8 HCABTT
     I                                        9   9 HCABTS
     I                                       10  10 HCASCU
      /EJECT
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
      * Receive parameters
     C           *ENTRY    PLIST
     C                     PARM           XGRPC   2        Group code
     C                     PARM           XPERS   1        Period set
     C                     PARM           XPOSB   1        Post. basis
     C                     PARM           XSTRD   5        Start date
     C                     PARM           XSECT   8        Sections
     C                     PARM           XANAL  10        Analysis
     C                     PARM           XSTRT   6        Start time
      *
      * Access Control Group record, DB Error if not found
     C           XGRPC     CHAINGLHBCGL1             50
     C           *IN50     IFEQ '1'                        --- B001
     C           *LOCK     IN   LDA
     C                     MOVEL'GLHBCGL1'DBFILE           ************
     C                     Z-ADD1         DBASE            * Error 01 *
     C                     MOVELXGRPC     DBKEY            ************
     C                     MOVEL'MC0264'  DBPGM
     C                     DUMP
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END                             --- E001
      *
      * Define the error DS
     C           *NAMVAR   DEFN           LDA
      *
      * Set up copyright
     C                     MOVEACPY@      BIS@   80
      *
      * Initialise parameter fields
     C                     EXSR I01
      *
      * Update parameters
     C                     EXSR U01
      *================================================================
      *  P R O G R A M     E N D                                      *
      *                                                               *
     C                     SETON                         LR           *
     C                     RETRN                                      *
      *                                                               *
      *================================================================
      /EJECT
      *****************************************************************
      *                                                               *
      *  U01 - SR to update parameters from LF/GLHBCGL1               *
      *                                                               *
      *****************************************************************
      *
     C           U01       BEGSR
     C                     MOVE HCPSTC    XPERS            Period set
     C                     MOVE HCPSTB    XPOSB            Post. basis
     C           HCLPDT    DIV  1000000   WSTRD  110
     C                     MOVE WSTRD     XSTRD            Start date
     C                     MOVE DSSECT    XSECT            Sections
     C                     MOVE DSANAL    XANAL            Analysis
     C                     MULT 1000000   WSTRD
     C           HCLPDT    SUB  WSTRD     WSTRD            Start time
     C                     MOVE WSTRD     XSTRT            Start time
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  I01 - Initialise parameter fields                            *
      *                                                               *
      *****************************************************************
      *
     C           I01       BEGSR
     C                     MOVE *BLANKS   XPERS            Period set
     C                     MOVE *BLANKS   XPOSB            Period basis
     C                     MOVE *BLANKS   XSECT            Sections
     C                     MOVE *BLANKS   XANAL            Analysis
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
**  CPY@
(c) Finastra International Limited 2001
