     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MC Transfer of Foreign Ccy In/Ex')
      *****************************************************************
      *                                                               *
      *  Midas - Management Accounting Module                         *
      *                                                               *
      *  MC0410 - Transfer of Foreign Ccy Income/Expense              *
      *                                                               *
      *  Function: This program transfers a Foreign Currency in an    *
      *    (COB)   Income/Expense account to its Base Currency        *
      *            Equivalent, via the Spot and Base Currency         *
      *            Equivalent accounts.                               *
      *                                                               *
      *  Called by: MC0320 - Value Date Adjustment Entry Generation   *
      *                                                               *
      *  Calls: AOTRADR0   - ICD Trading Data                         *
      *         AOACODR0   - Accounts Codes Details                   *
      *         AOCURRR0   - Currency Details                         *
      *         AOPRFCR0   - Profit Centre Details                    *
      *         AOPCACR0   - PCA ICD                                  *
      *         RPG/GL0765 - Convert F/Ccy to Base Ccy                *
      *         RPG/MC0400 - Spot Revaluation of Foreign Currency     *
      *         RPG/MC0420 - Transfer of Base Ccy IN/EX to P&L        *
      *         RPG/MC0430 - Update Gap Accruals History file         *
      *                                                               *
      *  Parameters: 7A Return code                                   *
      *              3A Base Currency                                 *
      *              6N Branch Internal Cust Number                   *
      *              1A Locked Record                                 *
      *            200A Format MCMOVEDPD                              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD060935           Date 25Jan24               *
      *  Prev Amend No. MD059841           Date 21Jun22               *
      *                 MD059936           Date 14May22               *
      *                 CGL057             Date 02Nov21               *
      *                 AR548538           Date 10Mar20               *
      *                 215711             Date 10Mar20               *
      *                 224411             Date 05Feb20               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG12471           Date 08Nov06               *
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD005             Date 02Mar99               *
      *                 160094             Date 30Apr99               *
      *                 CTI002             Date 23SEP98               *
      *                 CMC001 *CREATE     DATE 18MAR96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD060935 - Condition 224411 with a system value. If system   *
      *             value is 'N', no update on gap accrual.           *
      *             If system value is 'Y',execute SR/PRP05 to update *
      *             gap accrual history.                              *
      *  MD059841 - Postings over Primary Funding Gap account are     *
      *             consolidated.  Introduce a new system value       *
      *             ConPosFundGapAccnt.                               *
      *  MD059936 - Balance not transferred for the end range of IE   *
      *             defined on Mapping Table                          *
      *           - added SDINEXL2 for additional validation of       *
      *             i/e account code                                  *
      *  CGL057 - Posting of Profit and Loss to Multiple Retained     *
      *           Earnings Accounts.                                  *
      *  AR548538 - Wrong narrative for IN/EX postings. Need to       *
      *             include FROM ACCNT. Details  Apply fix 232846.    *
      *           - Applied for MD-55379.                             *
      *  215711 - The system accrued the B/CCY transferred from F/CCY *
      *           IN/EX. PRP05 should not be executed for this entry. *
      *         - Applied for MD-55379.
      *  224411 - Gap accrual detail was incorrectly updated for the  *
      *           batch item posted to an IN/EX account. Only Gap     *
      *           requirements should create the gap balance and      *
      *           accrue interest.                                    *
      *         - Apply for MD-54700.                                 *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG12471 - Validate profit center code only if they are      *
      *             non-blanks.                                       *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSD005 - Standing Data Account Narrative.  Add new field     *
      *           to SDACODPD.  Recompilation of programs.            *
      *  160094 - Ensure correct Profit Centre used                   *
      *  CTI002 - Recompiled : File SDACODPD changed                  *
      *  CMC001 - Management Accounts Enhancement for PCA             *
      *                                                               *
      *****************************************************************
      *
     FMCPCACPDO   E                    DISK         KINFSR *PSSR
      **  Profit Centre Accounting File
      *
     FMCDIRYL2IF  E           K        DISK         KINFSR *PSSR
      **  Diary Events File (B/Ccy Tfr's)
      *
     FSDINEXL1IF  E           K        DISK                                                   CGL057
      ** SD Income and Expense mapping file - Retrieval Index                                 CGL057
      *                                                                                       CGL057
     FSDSRVLL1IF  E           K        DISK                                                   CGL057
      ** SD Spot Revaluation mapping file   - Retrieval Index                                 CGL057
      *
     FSDINEXL2IF  E           K        DISK                                                 MD059936
     F            SDINEXD0                          KRENAME@INEXL2                          MD059936
      ** SD Income and Expense mapping file - Retrieval Index                               MD059936
      *                                                                                     MD059936
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *  01  -  First Cycle                                           *
      *  11  -  End of file on read of MCDIRYL2                       *
      *  55  -  SDINEXL1 End-of-file                                  *                       CGL057
      *  56  -  SDSRVLL1 Chain fail                                   *                       CGL057
      *  U7  -  Database error                                        *
      *  U8  -  Database error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  PRP02  -  Generate Entries to Balance the Profit Centres,    *
      *            When Profit Centre is not an FX Holder.            *
      *  PRP03  -  Revaluation of the Spot Trade Amount for each Spot *
      *            Trade A/c Posting.                                 *
      *  PRP04  -  Transfer Base Ccy to Retained P&L for each         *
      *            Transferred Income/Expense Amount, when a Base     *
      *            Ccy Transfer Calendar Event is processed.          *
      *  PRP05  -  Update Gap Accrual History for each Profit Centre  *
      *            Gap.                                               *
      *  PRW01  -  Write to Profit Centre Accounting file for each    *
      *            Transfer from the Foreign Ccy Income/Expense       *
      *            Account Posting Amount is not Zero.                *
      *  PRW02  -  Write to Profit Centre Accounting file for each    *
      *            Transfer to the Spot Trade Account, when Posting   *
      *            Amount is not Zero.                                *
      *  PRW03  -  Write to Profit Centre Accounting file for each    *
      *            Transfer to the Foreign Base Ccy Equivalent Account*
      *            when Posting Amount is not Zero.                   *
      *  PRW04  -  Write to Profit Centre Accounting file for each    *
      *            Transfer to Base Ccy Income/Expense Account, when  *
      *            Posting Amount is not Zero.                        *
      *  PRW05  -  Write to Profit Centre Accounting file for each    *
      *            Transfer to the Base Ccy Spot Trade Account, when  *
      *            Base Ccy Spot Trade A/c is maintained.             *
      *  PRW06  -  Write to Profit Centre Accounting file for each    *
      *            Transfer to the Base Ccy Equivalent Account, when  *
      *            Base Ccy Spot Trade A/c is Maintained.             *
      *  PRW07  -  Write to Profit Centre Accounting file for each    *
      *            Foreign CCy IN/EX Profit Centre Gap Account, when  *
      *            Profit Centre is not an FX Holder.                 *
      *  PRW08  -  Write to Profit Centre Accounting file for each    *
      *            Foreign Spot Trade Profit Centre Gap Account, when *
      *            Profit Centre is not an FX Holder.                 *
      *  PRW09  -  Write to Profit Centre Accounting file for each    *
      *            Base Ccy Equivalent Profit Centre Gap Account, when*
      *            Profit Centre is not an FX Holder.                 *
      *  PRW10  -  Write to Profit Centre Accounting file for each    *
      *            Base Ccy IN/EX Profit Centre Gap Account, when     *
      *            Profit Centre is not an FX Holder.                 *
      *  GETACD -  Get the alternate Spot Trade & Equiv accts.        *                       CGL057
      *  ANALYS -  Access PCA 'Management A/c Analysis fields' for    *                       CGL057
      *            the alternate Spot Trade & Equiv accts.            *                       CGL057
      *  DBERR  -  Error handling                                     *
      *  *PSSR  -  Subroutine to Handle Abnormal Error Conditions     *
      *                                                               *
      *****************************************************************
      /SPACE 3
     E                    CPY@    1   1 80               Copyright
      /SPACE 3
     E/COPY WNCPYSRC,MC0410E001
      *
     ILDA       E DSLDA
      **  Local data structure used for error handling
     I/COPY WNCPYSRC,MC0410I002
      *
     ISDTRAD    E DSSDTRADPD
      **  ICD Trading Data
      *
     ISDACOD    E DSSDACODPD
     I**  Account code details
     I              QQACCD                          QQACD1                                    CGL029
      *
     ISDCURR    E DSSDCURRPD
      **  Currency Details
      *
     ISDPRFC    E DSSDPRFCPD
      **  Profit Centre Details
      *
     ISDPCAC    E DSSDPCACPD
      **  PCA ICD
     I              QQFNAA                          FNAAQQ                                    CGL029
     I              QQFNIC                          FNICQQ                                    CGL029
      *
     I/COPY WNCPYSRC,MC0410I001
     IDSMOVE    E DSMCMOVEPD
      *                                                                                       CGL057
     ISCSARD    E DSSCSARDPD                                                                  CGL057
      * SAR File Details Accessed via Access Program AOSARDR0                                 CGL057
      *
     IDSFDY     E DSDSFDY
      **  First DS for access programs, Short Data Structure
      *
     IDSSDY     E DSDSSDY
      **  Second DS for access programs, Long Data Structure
      *
      **  Posting Narrative
      *
     I              'INCOME & EXPENSE '   C         NTRF
     I              'TFR FROM '           C         NTRF1                                   AR548538
      *                                                                                       CGL029
      **  Zeros Constant                                                                      CGL029
     I              '0000000000'          C         WZEROS                                    CGL029
     I              'ConPosFundGapAccnt'  C         PSVAL1                                  MD059841
      *                                                                                     MD060935
      ** System Value to determine when to execute SR/PRP05                                 MD060935
      *                                                                                     MD060935
     I              'UpdGapAccrual'       C         WSYSVL                                  MD060935
      /EJECT
      *================================================================
      *  P R O G R A M     S T A R T                                  *
      *================================================================
      *
      **  Receive parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           XRETC   7        Return code
     C                     PARM           XBCCY   3        Base currency
     C**********           PARM           XBCUS   60       Branch customer                    CSD027
     C                     PARM           XBCUS   6        Branch customer                    CSD027
     C                     PARM           XLCKR   1        Locked Record
     C           DSMOVE    PARM           DSFDY            MCMOVEPD fmt
      *                                                                                       CGL057
      **  Key List to Spot Revaluation Mapping file.                                          CGL057
      *                                                                                       CGL057
     C           KLIST2    KLIST                                                              CGL057
     C                     KFLD           XCCY                                                CGL057
     C                     KFLD           ACOD#R                                              CGL057
      *
      **  Save DSMOVE parameters
      *
     C                     MOVE BRCA      XBRCA   3        Branch
     C                     MOVE CCY       XCCY    3        Currency
     C**********           MOVE ACOD      XACOD   4        Account code                       CGL029
     C                     MOVE ACOD      XACOD  10                                           CGL029
     C**********           Z-ADDCNUM      XCNUM   60       Customer                           CSD027
     C                     MOVE CNUM      XCNUM   6        Customer                           CSD027
     C                     Z-ADDACSQ      XACSQ   20       A/c Sequence Nbr
     C                     MOVE PRFC      XPRFC   4        Profit Centre
     C                     MOVE BOKC      XBOKC   2        Book
     C                     MOVE OTTP      XOTTP  10        Deal Type
     C                     MOVE OTST      XOTST  10        Deal Sub-Type
     C**********           Z-ADDASOC      XASOC   60       Associated Cust.                   CSD027
     C                     MOVE ASOC      XASOC   6        Associated Cust.                   CSD027
     C                     Z-ADDEVDT      XEVDT   50       Event Date
     C                     Z-ADDPAMT      XPAMT  190       Posting Amount
     C                     Z-ADDVOIN      XVOIN   10       Void Indicator
     C                     MOVE GETP      XGETP   1        Generated Entry Type
     C                     MOVE ACSC      XACSC   2        Account Section
     C                     Z-ADDCPNB      XCPNB   60       Current Period
     C                     Z-ADDPDTM      XPDTM  110       Posting Date/Time
     C                     MOVE ATYP      XATYP   1        Account Type
     C                     MOVE PNAR      XPNAR  30        Posting Narrative
      *
      **  Set up copyright
      *
     C                     MOVEACPY@      CPY2@  80
      *
      **  Define the error DS
      *
     C           *NAMVAR   DEFN           LDA
     C                     MOVEL'MC0410'  WPGM   10
      *
      **  First Cycle
      *
     C           *IN01     IFEQ '0'
      *
      **  Get 'Spot Trade A/c code', error if not found
      *
     C                     CALL 'AOTRADR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDTRAD    PARM SDTRAD    DSFDY
     C           PRTCD     IFNE *BLANKS
     C                     MOVEL'SDTRADPD'WFILE   8        ************
     C                     Z-ADD1         WBASE   30       * DBERR 01 *
     C                     MOVEL'*FIRST'  WKEY   29        ************
     C                     EXSR DBERR
     C                     ENDIF
      *
      **  Get 'Primary Funding Gap A/c' and 'EOY P&L Profit Centre',
      **  Error if not found
      *
     C                     CALL 'AOPCACR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST ' POPTN   7
     C           SDPCAC    PARM SDPCAC    DSFDY
     C           PRTCD     IFNE *BLANKS
     C                     MOVEL'SDPCACPD'WFILE   8        ************
     C                     Z-ADD2         WBASE   30       * DBERR 02 *
     C                     MOVEL'*FIRST'  WKEY   29        ************
     C                     EXSR DBERR
     C                     ENDIF
      *
      **  Get 'Mgmt A/c' Analysis fields using 'Spot Trade A/c',
      **  error if not found
      *
     C                     CALL 'AOACODR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C**********           PARM BLSPTA    PACCD   4                                           CGL029
     C********** SDACOD    PARM SDACOD    DSFDY                                               CGL029
     C                     PARM BLSPTA    PACCD  10                                           CGL029
     C           SDACOD    PARM SDACOD    DSSDY                                               CGL029
     C           PRTCD     IFEQ '*NRF   '
     C                     MOVE 'N'       A5MABK
     C                     MOVE 'N'       A5MAPR
     C                     MOVE 'N'       A5MAAC
     C                     ELSE
     C           PRTCD     IFNE *BLANKS
     C                     MOVEL'SDACODPD'WFILE   8        ************
     C                     Z-ADD3         WBASE   30       * DBERR 03 *
     C                     MOVELBLSPTA    WKEY   29        ************
     C                     EXSR DBERR
     C                     ENDIF
     C                     ENDIF
      *
      **  Save 'Mgmt A/c  Analysis' fields
      *
     C                     MOVE A5MABK    SPACBB  1
     C                     MOVE A5MAPR    SPACBP  1
     C                     MOVE A5MAAC    SPACBC  1
      *
      **  Get 'Base Ccy Spot Trade Equiv. A/c Code' using Base ccy,
      **  error if not found
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM XBCCY     PCURR   3
     C           SDCURR    PARM SDCURR    DSSDY
     C           PRTCD     IFNE *BLANKS
     C                     MOVEL'SDCURRPD'WFILE   8        ************
     C                     Z-ADD4         WBASE   30       * DBERR 04 *
     C                     MOVELXBCCY     WKEY   29        ************
     C                     EXSR DBERR
     C                     ENDIF
     C**********           MOVE A6SPAE    BCEQIV  4                                           CGL029
     C                     MOVE A6SPAE    BCEQIV 10                                           CGL029
      *                                                                                       CGL057
      **  Check if feature CGL057 is present.                                                 CGL057
      *                                                                                       CGL057
     C                     MOVE 'N'       CGL057  1                                           CGL057
     C                     CALL 'AOSARDR0'                                                    CGL057
     C                     PARM '       ' @RTCD   7                                           CGL057
     C                     PARM '*VERIFY' @OPTN   7                                           CGL057
     C                     PARM 'CGL057'  @SARD   6                                           CGL057
     C           SCSARD    PARM SCSARD    DSFDY                                               CGL057
     C*                                                                                       CGL057
     C           @RTCD     IFEQ *BLANKS                                                       CGL057
     C                     MOVE 'Y'       CGL057                                              CGL057
     C                     ENDIF                                                              CGL057
      *                                                                                       CGL057
      ** Get sysval ConPosFundGapAccnt.                                                     MD059841
      *                                                                                     MD059841
     C                     CALL 'AOSVALR0'                                                  MD059841
     C                     PARM *BLANKS   RTNCDE  7                                         MD059841
     C                     PARM PSVAL1    SVALK1 20                                         MD059841
     C                     PARM *BLANKS   SVAL1 200                                         MD059841
     C                     PARM           SVALK2 20                                         MD059841
     C                     PARM *BLANKS   SVAL2 200                                         MD059841
     C                     PARM           SVALK3 20                                         MD059841
     C                     PARM *BLANKS   SVAL3 200                                         MD059841
     C                     PARM           SVALK4 20                                         MD059841
     C                     PARM *BLANKS   SVAL4 200                                         MD059841
     C                     PARM           SVALK5 20                                         MD059841
     C                     PARM *BLANKS   SVAL5 200                                         MD059841
     C                     PARM           SVALK6 20                                         MD059841
     C                     PARM *BLANKS   SVAL6 200                                         MD059841
     C                     PARM           SVALK7 20                                         MD059841
     C                     PARM *BLANKS   SVAL7 200                                         MD059841
     C                     PARM           SVALK8 20                                         MD059841
     C                     PARM *BLANKS   SVAL8 200                                         MD059841
     C                     PARM           SVALK9 20                                         MD059841
     C                     PARM *BLANKS   SVAL9 200                                         MD059841
     C                     PARM           SVALK0 20                                         MD059841
     C                     PARM *BLANKS   SVAL10200                                         MD059841
      *                                                                                     MD059841
     C           RTNCDE    IFNE *BLANKS                                                     MD059841
     C                     MOVEL'SDSVLCTD'WFILE                                             MD059841
     C                     Z-ADD9         WBASE                                             MD059841
     C                     MOVELPSVAL1    WKEY                                              MD059841
     C                     EXSR DBERR                                                       MD059841
     C                     ELSE                                                             MD059841
     C                     MOVELSVAL1     CONPFA  1                                         MD059841
     C                     ENDIF                                                            MD059841
      *                                                                                     MD059841
      **  Call AOSVALR0 to check 'UpdGapAccrual'.                                           MD060935
      *                                                                                     MD060935
     C                     CALL 'AOSVALR0'                                                  MD060935
     C                     PARM *BLANKS   PRTCD   7                                         MD060935
     C                     PARM WSYSVL    WSVLK1 20                                         MD060935
     C                     PARM           WSVL1 200                                         MD060935
     C                     PARM *BLANKS   WSVLK2 20                                         MD060935
     C                     PARM           WSVL2 200                                         MD060935
     C                     PARM *BLANKS   WSVLK3 20                                         MD060935
     C                     PARM           WSVL3 200                                         MD060935
     C                     PARM *BLANKS   WSVLK4 20                                         MD060935
     C                     PARM           WSVL4 200                                         MD060935
     C                     PARM *BLANKS   WSVLK5 20                                         MD060935
     C                     PARM           WSVL5 200                                         MD060935
     C                     PARM *BLANKS   WSVLK6 20                                         MD060935
     C                     PARM           WSVL6 200                                         MD060935
     C                     PARM *BLANKS   WSVLK7 20                                         MD060935
     C                     PARM           WSVL7 200                                         MD060935
     C                     PARM *BLANKS   WSVLK8 20                                         MD060935
     C                     PARM           WSVL8 200                                         MD060935
     C                     PARM *BLANKS   WSVLK9 20                                         MD060935
     C                     PARM           WSVL9 200                                         MD060935
     C                     PARM *BLANKS   WSVLK0 20                                         MD060935
     C                     PARM           WSVL0 200                                         MD060935
      *                                                                                     MD060935
     C           PRTCD     IFNE *BLANKS                                                     MD060935
     C           WSVL1     ANDEQ'*NRF   '                                                   MD060935
     C                     MOVEL'SDSVLJW0'WFILE   8        ************                     MD060935
     C                     Z-ADD10        WBASE   30       * DBERR 10 *                     MD060935
     C                     MOVELWSYSVL    WKEY   29        ************                     MD060935
     C                     EXSR DBERR                                                       MD060935
     C                     ELSE                                                             MD060935
     C                     MOVELWSVL1     UPDGAP  1                                         MD060935
     C                     ENDIF                                                            MD060935
     C/COPY WNCPYSRC,MC0410C001
      *
      **  End First Cycle
      *
     C                     SETON                     01
     C                     ENDIF
      *
      **  If 'Currency' is not equal to 'Previous Currency', get 'Base
      **  Ccy Equiv. A/c Code', error if not found
      *
     C           XCCY      IFNE SVCCY
      *
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM XCCY      PCURR   3
     C           SDCURR    PARM SDCURR    DSSDY
     C           PRTCD     IFNE *BLANKS
     C                     MOVEL'SDCURRPD'WFILE   8        ************
     C                     Z-ADD5         WBASE   30       * DBERR 05 *
     C                     MOVELXCCY      WKEY   29        ************
     C                     EXSR DBERR
     C                     ENDIF
      *
      **  Save Currency to Previous Currency
      *
     C                     MOVE XCCY      SVCCY   3
     C                     ENDIF
      *
      **  If 'Profit Centre' is not equal to 'Previous Profit Centre',
      **  get 'FX Position Profit Centre', error if not found
      *
     C           XPRFC     IFNE SVPRFC
      *
     C           XPRFC     IFNE *BLANKS                                                     BUG12471
     C                     CALL 'AOPRFCR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*KEY   ' POPTN   7
     C                     PARM XPRFC     PPRFC   4
     C           SDPRFC    PARM SDPRFC    DSFDY
     C           PRTCD     IFNE *BLANKS
     C                     MOVEL'SDPRFCPD'WFILE   8        ************
     C                     Z-ADD6         WBASE   30       * DBERR 06 *
     C                     MOVELXPRFC     WKEY   29        ************
     C                     EXSR DBERR
     C                     ENDIF
     C                     ENDIF                                                            BUG12471
      *
      **  Save Profit Centre to Previous Profit Centre
      *
     C                     MOVE XPRFC     SVPRFC  4
      *
      **  If FX Position Profit Centre is blank, set it to
      **  Profit Centre
      *
     C           DSFPPC    IFEQ *BLANK
     C                     MOVE XPRFC     DSFPPC
     C                     ENDIF
      *
     C                     ENDIF
      *
      **  Call 'GL0765' to convert the posting amount to base currency
      **  using the historic exchange rate as at the transfer date
      *
     C                     CALL 'GL0765'
     C                     PARM *BLANKS   GLRTCD  7        Return code
     C                     PARM XCCY      GLCCY   3        Currency
     C                     PARM XBCCY     GLBCCY  3        Base currency
     C                     PARM XEVDT     GLEVDT  50       Event date
     C                     PARM XPAMT     GLPAMT 190       Posting amount
      *
     C           GLRTCD    IFNE *BLANKS
     C                     MOVEL'GL0765'  WFILE            ************
     C                     Z-ADD7         WBASE            * DBERR 07 *
     C                     MOVELGLRTCD    WKEY             ************
     C                     EXSR DBERR
     C                     ENDIF
     C/COPY WNCPYSRC,MC0410C002
      *
      **  Get alternate Spot Trading & Spot Trade Equivalent a/c codes                        CGL057
      **  to post to.                                                                         CGL057
      *                                                                                       CGL057
     C                     EXSR GETACD                                                        CGL057
      *                                                                                       CGL057
      **  Write to the Profit Centre Accounting file, for the IN/EX
      **  Account.
      *
     C                     EXSR PRW01
      *
      **  Write to the Profit Centre Accounting file, for the Spot Trade
      **  Account.
      *
     C                     EXSR PRW02
      *
      **  If the GL0765 Base Currency Amount is not zero,
      *
     C           GLPAMT    IFNE 0
      *
      **  Revaluation of the Spot Trade Amount, to generate Spot Revaln
      **  P&L entries due to fluctuations in the exchange rate since the
      **  transfer date.
      *
     C                     EXSR PRP03
      *
      **  Write to the Profit Centre Accounting file, for the Base Ccy
      **  Spot Equivalent Account.
      *
     C                     EXSR PRW03
      *
      **  Write to the Profit Centre Accounting file, for the Base Ccy
      **  IN/EX Account.
      *
     C                     EXSR PRW04
      *
      **  If the Base Ccy Equiv A/c Code for the Base Ccy is not Blank,
      **  then 2 additional entries are required because a Spot Trade
      **  Account is being maintained in Base Currency along with its
      **  Equivalent Account.
      *
     C           BCEQIV    IFNE *BLANK
     C********** BCEQIV    ANDNE'0000'                                                        CGL029
     C           BCEQIV    ANDNEWZEROS                                                        CGL029
      *
      **  Write to the Profit Centre Accounting file, for the Base Ccy
      **  Spot Trade Account.
      *
     C                     EXSR PRW05
      *
      **  Write to the Profit Centre Accounting file, for the Base Ccy
      **  Equivalent Account.
      *
     C                     EXSR PRW06
     C                     ENDIF
      *
     C                     ENDIF
      *
      **  If 'Profit Centre' Not Equal 'FX Profit Centre', generate
      **  entries to balance the profit centres.
      *
     C           XPRFC     IFNE DSFPPC
     C                     EXSR PRP02
     C                     ENDIF
      *
      **  Transfer Base Ccy to Retained P&L, for the Base Currency IN/EX
      **  amount, if GL0765 Base Ccy Amount is not zero
      *
     C           GLPAMT    IFNE 0
     C                     EXSR PRP04
     C                     ENDIF
     C/COPY WNCPYSRC,MC0410C003
      *
      **  Not SETON LR to leave data in memory
      *
     C                     RETRN
      *================================================================
      *  P R O G R A M     E N D                                      *
      *================================================================
      /EJECT                                                                                  CGL057
      *****************************************************************                       CGL057
      *  GETACD -  Get alternate Spot Trade & Equiv accts to post.    *                       CGL057
      *                                                               *                       CGL057
      *  fields:   ACOD#T = alternate Spot Trading a/c code to post   *                       CGL057
      *                     to in SR/PRW02.                           *                       CGL057
      *            ACOD#E = alternate Spot Trade Equiv a/c to post    *                       CGL057
      *                     to in SR/PRW03.                           *                       CGL057
      *            ACOD#R = alternate Spot Reval a/c to pass to       *                       CGL057
      *                     MC0400 in SR/PRP03                        *                       CGL057
      *                                                               *                       CGL057
      *  Called By:  Main Processing                                  *                       CGL057
      *                                                               *                       CGL057
      *  Calls    :                                                   *                       CGL057
      *****************************************************************                       CGL057
     C           GETACD    BEGSR                                                              CGL057
      *                                                                                       CGL057
      **  If CGL057 is present:                                                               CGL057
      **  Read new Income/Expense mapping file and check whether the                          CGL057
      **  account code being processed has an alternate Spot Reval a/c                        CGL057
      **  code defined.                                                                       CGL057
      **  It will check for an exact match; then check within a range.                        CGL057
      *                                                                                       CGL057
      **  If an alternate Spot Revaluation a/c code is found, use                             CGL057
      **  this a/c code along with the foreign currency to chain to the                       CGL057
      **  Spot Reval mapping file to access the alternate Spot Trade                          CGL057
      **  Equivalent a/c code and the alternate Spot Trading a/c code.                        CGL057
      *                                                                                       CGL057
      **  If IN/EX account code (individual or range) does not                                CGL057
      **  exist on the IN/EX mapping file   /or/                                              CGL057
      **  If Spot Reval a/c not specified on IN/EX mapping file   /or/                        CGL057
      **  If Spot Reval a/c not found on Spot Reval mapping file  /or/                        CGL057
      **  If feature CGL057 is switched off,                                                  CGL057
      **  then default to existing Spot Equiv and Spot Trading accts on ICD.                  CGL057
      *                                                                                       CGL057
     C                     MOVE *BLANKS   ACOD#R 10        Alt Spot Reval                     CGL057
     C                     MOVE *BLANKS   ACOD#E 10        Alt Spot Equiv                     CGL057
     C                     MOVE *BLANKS   ACOD#T 10        Alt Spot Trade                     CGL057
      *                                                                                       CGL057
     C           CGL057    IFEQ 'Y'                                                           CGL057
     C                     MOVELXACOD     #WKACD 100                                          CGL057
     C           #WKACD    SETLLSDINEXL1                                                      CGL057
     C           #WKACD    READESDINEXL1                 55                                   CGL057
      *                                                                                       CGL057
      * If exact match found in mapping file, use the                                         CGL057
      * alternate Spot Reval a/c code.                                                        CGL057
      *                                                                                       CGL057
     C           *IN55     IFEQ '0'                                                           CGL057
     C                     MOVE MASRVL    ACOD#R                                              CGL057
     C                     ELSE                                                               CGL057
      *                                                                                       CGL057
      * If no exact match, check most suitable range for the                                  CGL057
      * alternate Spot Reval a/c code.                                                        CGL057
      *                                                                                       CGL057
     C           #WKACD    IFGT MAINC1                                                      MD059936
     C           #WKACD    ANDLEMAINC2                                                      MD059936
     C                     MOVE MASRVL    ACOD#R                                            MD059936
     C                     ELSE                                                             MD059936
     C           #WKACD    SETLLSDINEXL1                                                      CGL057
     C                     READPSDINEXL1                 55                                   CGL057
      *                                                                                       CGL057
     C           *IN55     IFEQ '0'                                                           CGL057
     C           #WKACD    ANDGTMAINC1                                                        CGL057
     C           #WKACD    ANDLEMAINC2                                                        CGL057
     C                     MOVE MASRVL    ACOD#R                                              CGL057
     C                     ELSE                                                             MD059936
      *                                                                                     MD059936
      ** If account code is with in the range of another range of account codes             MD059936
      ** alternate Spot Reval a/c code.                                                     MD059936
      *                                                                                     MD059936
     C           #WKACD    SETLLSDINEXL2                                                    MD059936
     C                     READ SDINEXL2                 55                                 MD059936
     C           *IN55     DOWEQ'0'                                                         MD059936
     C           #WKACD    IFGT MAINC1                                                      MD059936
     C           #WKACD    ANDLEMAINC2                                                      MD059936
     C                     MOVE MASRVL    ACOD#R                                            MD059936
     C                     SETON                     55                                     MD059936
     C                     ITER                                                             MD059936
     C                     ENDIF                                                            MD059936
     C                     READ SDINEXL2                 55                                 MD059936
     C                     ENDDO                                                            MD059936
     C                     ENDIF                                                            MD059936
     C                     ENDIF                                                              CGL057
     C                     ENDIF                                                              CGL057
      *                                                                                       CGL057
      **  Check against Spot Reval mapping file.                                              CGL057
      *                                                                                       CGL057
     C           ACOD#R    IFNE *BLANKS                                                       CGL057
     C           KLIST2    CHAINSDSRVLL1             56                                       CGL057
     C           *IN56     IFEQ *OFF                                                          CGL057
     C                     MOVE MBSPAE    ACOD#E           Alt Spot Equiv                     CGL057
     C                     MOVE MBSPTA    ACOD#T           Alt Spot Trade                     CGL057
     C                     ELSE                                                               CGL057
      *                                                                                       CGL057
      **  If the alternate Spot Reval a/c code was not specified on the                       CGL057
      **  Mapped Spot Reval account file (meaning that no alternate                           CGL057
      **  Spot Trade Equiv or Spot Trading Account has been defined),                         CGL057
      **  then do NOT use this alternate Spot Reval account.                                  CGL057
      *                                                                                       CGL057
     C                     MOVE *BLANKS   ACOD#R           Alt Spot Reval                     CGL057
     C                     ENDIF                                                              CGL057
     C                     ENDIF                                                              CGL057
      *                                                                                       CGL057
     C                     ENDIF                                                              CGL057
      *                                                                                       CGL057
     C                     ENDSR                                                              CGL057
      *****************************************************************                       CGL057
      /EJECT                                                                                  CGL057
      *****************************************************************                       CGL057
      *  ANALYS -  Access PCA 'Management A/c Analysis fields' for    *                       CGL057
      *            the alternate Spot Trade & Equiv accts.            *                       CGL057
      *                                                               *                       CGL057
      *  Called By:   PRW02  -  Write to PCA file for each transfer   *                       CGL057
      *                         to Spot Trade Acct.                   *                       CGL057
      *               PRW03  -  Write to PCA file for each transfer   *                       CGL057
      *                         to Foreign Base Ccy Equivalent Acct.  *                       CGL057
      *                                                               *                       CGL057
      *  Calls    :                                                   *                       CGL057
      *****************************************************************                       CGL057
      *                                                                                       CGL057
     C           ANALYS    BEGSR                                                              CGL057
      *                                                                                       CGL057
      **  Get 'Management A/c Analysis fields' using Spot Trade A/c                           CGL057
      *                                                                                       CGL057
     C                     CALL 'AOACODR0'                                                    CGL057
     C                     PARM *BLANKS   PRTCD                                               CGL057
     C                     PARM '*KEY   ' POPTN                                               CGL057
     C                     PARM A#ACOD    PACCD  10                                           CGL057
     C           SDACOD    PARM SDACOD    DSSDY                                               CGL057
      *                                                                                       CGL057
      **  If not found, set 'Management A/c Analysis fields' to 'N'                           CGL057
      *                                                                                       CGL057
     C           PRTCD     IFEQ '*NRF   '                                                     CGL057
     C                     MOVE 'N'       A5MABK                                              CGL057
     C                     MOVE 'N'       A5MAPR                                              CGL057
     C                     MOVE 'N'       A5MAAC                                              CGL057
     C                     ELSE                                                               CGL057
     C           PRTCD     IFNE *BLANKS                                                       CGL057
     C                     MOVEL'SDACODPD'WFILE   8        ************                       CGL057
     C                     Z-ADD8         WBASE   30       * DBERR 08 *                       CGL057
     C                     MOVELA#ACOD    WKEY   29        ************                       CGL057
     C                     EXSR DBERR                                                         CGL057
     C                     ENDIF                                                              CGL057
      *                                                                                       CGL057
      **  Blank Book if P&L A/c Analysis is not required                                      CGL057
      *                                                                                       CGL057
     C           A5MABK    IFNE 'Y'                                                           CGL057
     C                     MOVE *BLANK    BOKC                                                CGL057
     C                     ENDIF                                                              CGL057
      *                                                                                       CGL057
      **  Blank Product if P&L A/c Analysis is not required                                   CGL057
      *                                                                                       CGL057
     C           A5MAPR    IFNE 'Y'                                                           CGL057
     C                     MOVE *BLANKS   OTST                                                CGL057
     C                     MOVE *BLANKS   OTTP                                                CGL057
     C                     ENDIF                                                              CGL057
      *                                                                                       CGL057
      **  Blank Assoc. Cust if P&L A/c Analysis is not required                               CGL057
      *                                                                                       CGL057
     C           A5MAAC    IFNE 'Y'                                                           CGL057
     C                     MOVE *BLANKS   ASOC                                                CGL057
     C                     ENDIF                                                              CGL057
      *                                                                                       CGL057
     C                     ENDIF                                                              CGL057
      *                                                                                       CGL057
     C                     ENDSR                                                              CGL057
      *****************************************************************                       CGL057
      /EJECT
      *****************************************************************
      *                                                               *
      *  PRP02  -  Generate Entries to Balance the Profit Centres,    *
      *            When Profit Centre is not an FX Holder.            *
      *                                                               *
      *  Called By:  Main Processing                                  *
      *                                                               *
      *  Calls    :  PRP05 - Update Gap Accrual History               *
      *              PRW07 - Write to PCA file for each F/Ccy IN/EX   *
      *                      Profit Centre Gap Account                *
      *              PRW08 - Write to PCA file for each Foreign Spot  *
      *                      Trade Profit Centre Gap Account          *
      *              PRW09 - Write to PCA file for each B/Ccy Equiv.  *
      *                      Profit Centre Gap Account                *
      *              PRW10 - Write to PCA file for each B/Ccy IN/EX   *
      *                      Profit Centre Gap Account                *
      *                                                               *
      *****************************************************************
     C           PRP02     BEGSR
      *
      **  Write to the Profit Centre Accounting file, for the F/Ccy Gap
      **  Funding Account (Profit Centre).
      *
     C                     EXSR PRW07
      *
      **  If 'Locked Record' not equal to 'Y', update Gap Accrual
      **  History, for the F/Ccy Gap Funding Account (Profit Centre).
      *
     C           XLCKR     IFNE 'Y'
     C           XATYP     ANDEQ'F'                                                           224411
     C           UPDGAP    ANDEQ'N'                                                         MD060935
     C           XLCKR     ORNE 'Y'                                                         MD060935
     C           UPDGAP    ANDEQ'Y'                                                         MD060935
     C                     EXSR PRP05
     C                     ENDIF
      *
      **  Write to the Profit Centre Accounting file, for the F/Ccy Gap
      **  Funding Account (FX Profit Centre).
      *
     C                     EXSR PRW08
      *
      **  Update Gap Accrual History, for the F/Ccy Gap Funding Account
      **  (FX Profit Centre).
      *
     C           XATYP     IFEQ 'F'                                                           224411
     C           UPDGAP    ANDEQ'N'                                                         MD060935
     C           UPDGAP    OREQ 'Y'                                                         MD060935
     C                     EXSR PRP05
     C                     ENDIF                                                              224411
      *
      **  If the GL0765 Base Ccy Amount is not zero,
      *
     C           GLPAMT    IFNE 0
      *
      **  Write to the Profit Centre Accounting file, for the B/Ccy Gap
      **  Funding Account (FX Profit Centre).
      *
     C                     EXSR PRW09
      *
      **  Update Gap Accrual History, for the B/Ccy Gap Funding Account
      **  (FX Profit Centre).
      *
     C**********           EXSR PRP05                                                         224411
     C           UPDGAP    IFEQ 'Y'                                                         MD060935
     C                     EXSR PRP05                                                       MD060935
     C                     ENDIF                                                            MD060935
      *
      **  Write to the Profit Centre Accounting file, for the B/Ccy Gap
      **  Funding Account (Profit Centre).
      *
     C                     EXSR PRW10
      *
      ****Update*Gap*Accrual*History,*for*the*B/Ccy*Gap*Funding*Account                       215711
      ****(Profit*Centre).*********************************************                       215711
      *
     C*********            EXSR PRP05                                                         215711
     C           UPDGAP    IFEQ 'Y'                                                         MD060935
     C                     EXSR PRP05                                                       MD060935
     C                     ENDIF                                                            MD060935
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  PRP03  -  Revaluation of the Spot Trade Amount for each Spot *
      *            Trade A/c Posting.                                 *
      *                                                               *
      *  Called By:  Main Processing                                  *
      *                                                               *
      *  Calls    :  DBERR - Error Handling                           *
      *                                                               *
      *****************************************************************
     C           PRP03     BEGSR
      *
      **  Set up DSMOVE
      *
     C                     MOVE XBRCA     BRCA             Branch
     C                     MOVE XCCY      CCY              Currency
     C                     MOVE XACOD     ACOD             Account code
     C**********           Z-ADDXCNUM     CNUM             Customer                           CSD027
     C                     MOVE XCNUM     CNUM             Customer                           CSD027
     C                     Z-ADDXACSQ     ACSQ             A/c Sequence Nbr
     C*********************MOVE XPRFC     PRFC             Profit Centre  160094
     C                     MOVE DSFPPC    PRFC             Profit Centre  160094
     C                     MOVE XBOKC     BOKC             Book
     C                     MOVE XOTTP     OTTP             Deal Type
     C                     MOVE XOTST     OTST             Deal Sub-Type
     C**********           Z-ADDXASOC     ASOC             Associated Cust.                   CSD027
     C                     MOVE XASOC     ASOC             Associated Cust.                   CSD027
     C                     Z-ADDXEVDT     EVDT             Event Date
     C                     Z-ADDXPAMT     PAMT             Posting Amount
     C                     Z-ADDXVOIN     VOIN             Void Indicator
     C                     MOVE XGETP     GETP             Generated Entry Type
     C                     MOVE XACSC     ACSC             Account Section
     C                     Z-ADDXCPNB     CPNB             Current Period
     C                     Z-ADDXPDTM     PDTM             Posting Date/Time
     C                     MOVE XATYP     ATYP             Account Type
     C                     MOVE XPNAR     PNAR             Posting Narrative
      *
      * If feature CGL057 is on and the alternate Spot Reval a/c was not                      CGL057
      * blank on SDINEXPD, then pass the alternate Spot Reval a/c and                         CGL057
      * alternate Spot Equiv a/c to MC0400.                                                   CGL057
      *                                                                                       CGL057
     C                     MOVE *BLANKS   A#SPAE           Spot Reval A/c                     CGL057
     C                     MOVE *BLANKS   A#SRVL           Spot Reval A/c                     CGL057
     C           CGL057    IFEQ 'Y'                                                           CGL057
     C           ACOD#R    ANDNE*BLANKS                                                       CGL057
     C                     MOVE ACOD#E    A#SPAE 10        B/Ccy Equiv. A/c                   CGL057
     C                     MOVE ACOD#R    A#SRVL 10        Spot Reval A/c                     CGL057
     C                     ELSE                                                               CGL057
     C                     MOVE A6SPAE    A#SPAE           B/Ccy Equiv. A/c                   CGL057
     C                     ENDIF                                                              CGL057
      *                                                                                       CGL057
      **  Call MC0400 - Spot Revaluation of Foreign Currency
      *
     C                     CALL 'MC0400'
     C                     PARM *BLANKS   PRTCD   7        Return code
     C                     PARM XBCCY     PBCCY   3        Base currency
     C**********           PARM XBCUS     PBCUS   60       Branch customer                    CSD027
     C                     PARM XBCUS     PBCUS   6        Branch customer                    CSD027
     C                     PARM GLPAMT    PPAMT  190       GL0765 B/Ccy Amt
     C**********           PARM A6SPAE    PSPAE   4        B/Ccy Equiv. A/c                   CGL029
     C**********           PARM A6SPAE    PSPAE  10                                    CGL029 CGL057
     C                     PARM A#SPAE    PSPAE  10        B/Ccy Equiv. A/c                   CGL057
     C                     PARM A#SRVL    PSRVL  10        Spot Reval A/c                     CGL057
     C                     PARM DSMOVE    DSFDY            MCMOVEPD fmt
      *
     C           PRTCD     IFNE *BLANKS
     C                     IN   LDA
     C                     MOVELDBFILE    WFILE
     C                     MOVE DBASE     WBASE
     C                     MOVELDBKEY     WKEY
     C                     MOVE DBPGM     WPGM
     C                     EXSR DBERR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  PRP04  -  Transfer Base Ccy to Retained P&L for each         *
      *            Transferred Income/Expense Amount, when a Base     *
      *            Ccy Transfer Calendar Event is processed.          *
      *                                                               *
      *  Called By:  Main Processing                                  *
      *                                                               *
      *  Calls    :  DBERR - Error Handling                           *
      *                                                               *
      *****************************************************************
     C           PRP04     BEGSR
      *
      **  If 'Event date' is not equal to 'Previous Event Date'
      *
     C           XEVDT     IFNE XXEVDT
     C                     Z-ADDXEVDT     XXEVDT  50
      *
      **  Set lower limits on calendar event file, using the 'Event Date'
      **  and read the first base ccy transfer date
      *
     C           XEVDT     SETLLMCDIRYL2
     C                     READ MCDIRYL2                 11
     C                     ENDIF
      *
      **  If 'Base Ccy Transfer EQ 'Y' Event Record Found.
      *
     C           *IN11     IFEQ *OFF
     C           GIBTFR    ANDEQ'Y'
      *
      **  Call MC0420 - Transfer Base Ccy IN/EX to the Retained P&L Account
      *
      **  Set up DSMOVE
      *
     C                     MOVE XBRCA     BRCA             Branch
     C                     MOVE XBCCY     CCY              Base Currency
     C                     MOVE XACOD     ACOD             Account code
     C**********           Z-ADDXCNUM     CNUM             Customer                           CSD027
     C                     MOVE XCNUM     CNUM             Customer                           CSD027
     C/COPY WNCPYSRC,MC0410C005
     C                     Z-ADDXACSQ     ACSQ             A/c Sequence Nbr
     C                     MOVE XPRFC     PRFC             Profit Centre
     C                     MOVE XBOKC     BOKC             Book
     C                     MOVE XOTTP     OTTP             Deal Type
     C                     MOVE XOTST     OTST             Deal Sub-Type
     C**********           Z-ADDXASOC     ASOC             Associated Cust.                   CSD027
     C                     MOVE XASOC     ASOC             Associated Cust.                   CSD027
     C                     Z-ADDGIEVDT    EVDT             Base Ccy Transfer Dat
     C                     Z-ADDGLPAMT    PAMT             GL0765 Base Ccy Amt
     C                     Z-ADDXVOIN     VOIN             Void Indicator
     C                     MOVE XGETP     GETP             Generated Entry Type
     C                     MOVE XACSC     ACSC             Account Section
     C                     Z-ADDXCPNB     CPNB             Current Period
     C                     Z-ADDXPDTM     PDTM             Posting Date/Time
     C                     MOVE XATYP     ATYP             Account Type
     C                     MOVE XPNAR     PNAR             Posting Narrative
      *
     C                     CALL 'MC0420'
     C                     PARM *BLANKS   PRTCD   7        Return code
     C**********           PARM XBCUS     PBCUS   60       Branch customer                    CSD027
     C                     PARM XBCUS     PBCUS   6        Branch customer                    CSD027
     C                     PARM 'N'       PLCKR   1        Locked Record
     C                     PARM DSMOVE    DSFDY            MCMOVEPD fmt
      *
     C           PRTCD     IFNE *BLANKS
     C                     IN   LDA
     C                     MOVELDBFILE    WFILE
     C                     MOVE DBASE     WBASE
     C                     MOVELDBKEY     WKEY
     C                     MOVELDBPGM     WPGM
     C                     EXSR DBERR
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  PRP05  -  Update Gap Accrual History for each Profit Centre  *
      *            Gap.                                               *
      *                                                               *
      *  Called By:  PRP02 - Generate Entries to Balance Profit Cent. *
      *                                                               *
      *  Calls    :  DBERR - Error Handling                           *
      *                                                               *
      *****************************************************************
     C           PRP05     BEGSR
      *
      **  Call MC0430 - Update Gap Accruals History file
      *
     C                     CALL 'MC0430'
     C                     PARM *BLANKS   PRTCD   7        Return code
     C                     PARM DSMOVE    DSFDY            MCMOVEPD fmt
      *
     C           PRTCD     IFNE *BLANKS
     C                     IN   LDA
     C                     MOVELDBFILE    WFILE
     C                     MOVE DBASE     WBASE
     C                     MOVELDBKEY     WKEY
     C                     MOVELDBPGM     WPGM
     C                     EXSR DBERR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  PRW01  -  Write to Profit Centre Accounting file for each    *
      *            Transfer from the Foreign Ccy Income/Expense       *
      *            Account when Posting Amount is not Zero.           *
      *                                                               *
      *  Called By:  Main Processing                                  *
      *                                                               *
      *  Calls    :  None                                             *
      *                                                               *
      *****************************************************************
     C           PRW01     BEGSR
      *
     C                     MOVE XBRCA     BRCA             Branch
     C                     MOVE XCCY      CCY              Currency
     C                     MOVE XACOD     ACOD             Account code
     C**********           Z-ADDXCNUM     CNUM             Customer                           CSD027
     C                     MOVE XCNUM     CNUM             Customer                           CSD027
     C                     Z-ADDXACSQ     ACSQ             A/c Sequence Nbr
     C                     MOVE XPRFC     PRFC             Profit Centre
     C                     MOVE XBOKC     BOKC             Book
     C                     MOVE XOTTP     OTTP             Deal Type
     C                     MOVE XOTST     OTST             Deal Sub-Type
     C**********           Z-ADDXASOC     ASOC             Associated Cust.                   CSD027
     C                     MOVE XASOC     ASOC             Associated Cust.                   CSD027
     C                     Z-ADDXEVDT     EVDT             Event Date
     C           XPAMT     MULT -1        PAMT             Posting Amount
     C                     Z-ADDXVOIN     VOIN             Void Indicator
     C                     MOVE 'X'       GETP             Generated Entry Type
     C                     MOVE XACSC     ACSC             Account Section
     C                     Z-ADDXCPNB     CPNB             Current Period
     C                     Z-ADDXPDTM     PDTM             Posting Date/Time
     C                     MOVE XATYP     ATYP             Account Type
     C           XPNAR     IFEQ *BLANK
     C                     MOVE *BLANK    PNAR
     C           'TRANSFER'CAT  'F/C':1   PNAR
     C           PNAR      CAT  NTRF:1    PNAR             Posting Narrative
     C                     ELSE
     C                     MOVE XPNAR     PNAR
     C                     ENDIF
     C/COPY WNCPYSRC,MC0410C006
      *
     C                     WRITEMCPCACD0
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  PRW02  -  Write to Profit Centre Accounting file for each    *
      *            Transfer to the Spot Trade Account, when Posting   *
      *            Amount is not Zero.                                *
      *                                                               *
      *  Called By:  Main Processing                                  *
      *                                                               *
      *  Calls    :  ANALYS  Access PCA 'Management A/c Analysis      *                       CGL057
      *                      fields' for the alternate Spot Trade     *                       CGL057
      *                      and Spot Equivalent Accts.               *                       CGL057
      *                                                               *
      *****************************************************************
     C           PRW02     BEGSR
      *
      **  Set up DSMOVE
      *
      * If feature CGL057 is on and the alternate Spot Trading a/c was                        CGL057
      * obtained in SR/GETACD, then use this a/c code.                                        CGL057
      *                                                                                       CGL057
     C           CGL057    IFEQ 'Y'                                                           CGL057
     C           ACOD#T    ANDNE*BLANKS                                                       CGL057
     C                     MOVE ACOD#T    ACOD             Spot Trade A/c                     CGL057
     C                     ELSE                                                               CGL057
     C                     MOVE BLSPTA    ACOD             Spot Trade A/c
     C                     ENDIF                                                              CGL057
      *                                                                                       CGL057
     C**********           Z-ADDXBCUS     CNUM             Branch Intern. Cus                 CSD027
     C                     MOVE XBCUS     CNUM             Branch Intern. Cus                 CSD027
     C                     MOVE '01'      ACSQ             A/c Sequence Nbr
     C                     MOVE DSFPPC    PRFC             Profit Centre
     C                     Z-ADDXPAMT     PAMT             Posting Amount
     C                     MOVE *BLANK    ACSC             Spot Trade A/c Sect.
     C                     MOVE *BLANK    ATYP             Spot Trade A/c Type
      *
      ** If feature CGL057 is on, use the PCA Management a/c fields for                       CGL057
      ** the alternate Spot Trading Account Code.                                             CGL057
      *                                                                                       CGL057
     C           CGL057    IFEQ 'Y'                                                           CGL057
     C                     MOVE ACOD      A#ACOD 10        Spot Trade A/c                     CGL057
     C                     EXSR ANALYS                                                        CGL057
      *                                                                                       CGL057
     C                     ELSE                                                               CGL057
      *                                                                                       CGL057
      **  Blank Book if Spot Trade Analysis is not required
      *
     C           SPACBB    IFNE 'Y'
     C                     MOVE *BLANK    BOKC
     C                     ENDIF
      *
      **  Blank Product if Spot Trade Analysis is not required
      *
     C           SPACBP    IFNE 'Y'
     C                     MOVE *BLANKS   OTST
     C                     MOVE *BLANKS   OTTP
     C                     ENDIF
      *
      **  Blank Assoc. Cust if Spot Trade Analysis is not required
      *
     C           SPACBC    IFNE 'Y'
     C**********           Z-ADD*ZERO     ASOC                                                CSD027
     C                     MOVE *BLANKS   ASOC                                                CSD027
     C                     ENDIF
      *                                                                                       CGL057
     C                     ENDIF                                                              CGL057
      *
     C                     WRITEMCPCACD0
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  PRW03  -  Write to Profit Centre Accounting file for each    *
      *            Transfer to the Foreign Base Ccy Equivalent Account*
      *            when Posting Amount is not Zero.                   *
      *                                                               *
      *  Called By:  Main Processing                                  *
      *                                                               *
      *  Calls    :  ANALYS  Access PCA 'Management A/c Analysis      *                       CGL057
      *                      fields' for the alternate Spot Trade     *                       CGL057
      *                      and Spot Equivalent Accts.               *                       CGL057
      *                                                               *
      *****************************************************************
     C           PRW03     BEGSR
      *
      **  Set up DSMOVE
      *
     C**********           Z-ADDXBCUS     CNUM             Branch Intern. Cus                 CSD027
     C                     MOVE XBCUS     CNUM             Branch Intern. Cus                 CSD027
     C                     MOVE XBCCY     CCY              Base Currency
      *                                                                                       CGL057
      * If feature CGL057 is on and the alternate Spot Trade Equivalent a/c                   CGL057
      * was obtained in SR/GETACD, then use this a/c code.                                    CGL057
      *                                                                                       CGL057
     C           CGL057    IFEQ 'Y'                                                           CGL057
     C           ACOD#E    ANDNE*BLANKS                                                       CGL057
     C                     MOVE ACOD#E    ACOD             Base Ccy Eq A/c code               CGL057
     C                     ELSE                                                               CGL057
     C                     MOVE A6SPAE    ACOD             Base Ccy Eq A/c code
     C                     ENDIF                                                              CGL057
      *                                                                                       CGL057
     C                     MOVE '01'      ACSQ             A/c Sequence Nbr
     C                     MOVE DSFPPC    PRFC             Profit Centre
     C           GLPAMT    MULT -1        PAMT             Posting Amount
     C                     MOVE 'X'       GETP             Spot Trade A/c Sect.
     C                     MOVE *BLANK    ACSC             Spot Trade A/c Sect.
     C                     MOVE *BLANK    ATYP             Spot Trade A/c Type
     C           XPNAR     IFEQ *BLANK
     C                     MOVE *BLANK    PNAR
     C           'TRANSFER'CAT  'F/C':1   PNAR
     C           PNAR      CAT  NTRF:1    PNAR             Posting Narrative
     C                     ELSE
     C                     MOVE XPNAR     PNAR
     C                     ENDIF
      *
      ** If feature CGL057 is on, use the PCA Management a/c fields                           CGL057
      ** for alternate Spot Trading Account Code.                                             CGL057
      *                                                                                       CGL057
     C           CGL057    IFEQ 'Y'                                                           CGL057
     C                     MOVE ACOD      A#ACOD 10        Spot Trade A/c                     CGL057
     C                     EXSR ANALYS                                                        CGL057
      *                                                                                       CGL057
     C                     ELSE                                                               CGL057
      *                                                                                       CGL057
      **  Blank Book if Spot Trade Analysis is not required
      *
     C           SPACBB    IFNE 'Y'
     C                     MOVE *BLANK    BOKC
     C                     ENDIF
      *
      **  Blank Product if Spot Trade Analysis is not required
      *
     C           SPACBP    IFNE 'Y'
     C                     MOVE *BLANKS   OTST
     C                     MOVE *BLANKS   OTTP
     C                     ENDIF
      *
      **  Blank Assoc. Cust if Spot Trade Analysis is not required
      *
     C           SPACBC    IFNE 'Y'
     C**********           Z-ADD*ZERO     ASOC                                                CSD027
     C                     MOVE *BLANKS   ASOC                                                CSD027
     C                     ENDIF
      *                                                                                       CGL057
     C                     ENDIF                                                              CGL057
      *
     C                     WRITEMCPCACD0
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  PRW04  -  Write to Profit Centre Accounting file for each    *
      *            Transfer to Base Ccy Income/Expense Account, when  *
      *            Posting Amount is not Zero.                        *
      *                                                               *
      *  Called By:  Main Processing                                  *
      *                                                               *
      *  Calls    :  None                                             *
      *                                                               *
      *****************************************************************
     C           PRW04     BEGSR
      *
      **  Set up DSMOVE
      *
     C                     MOVE XBRCA     BRCA             Branch
     C                     MOVE XBCCY     CCY              Base Currency
     C                     MOVE XACOD     ACOD             Account code
     C**********           Z-ADDXCNUM     CNUM             Customer                           CSD027
     C                     MOVE XCNUM     CNUM             Customer                           CSD027
     C                     Z-ADDXACSQ     ACSQ             A/c Sequence Nbr
     C                     MOVE XPRFC     PRFC             Profit Centre
     C                     MOVE XBOKC     BOKC             Book
     C                     MOVE XOTTP     OTTP             Deal Type
     C                     MOVE XOTST     OTST             Deal Sub-Type
     C**********           Z-ADDXASOC     ASOC             Associated Cust.                   CSD027
     C                     MOVE XASOC     ASOC             Associated Cust.                   CSD027
     C                     Z-ADDXEVDT     EVDT             Transfer Date
     C                     Z-ADDGLPAMT    PAMT             GL0765 Base Ccy Amt
     C                     Z-ADDXVOIN     VOIN             Void Indicator
     C                     MOVE 'X'       GETP             Generated Entry Type
     C                     MOVE XACSC     ACSC             Account Section
     C                     Z-ADDXCPNB     CPNB             Current Period
     C                     Z-ADDXPDTM     PDTM             Posting Date/Time
     C                     MOVE XATYP     ATYP             Account Type
     C                     MOVE *BLANK    PNAR
      *                                                                                     MD059841
      ** If ConPosFundGapAccnt is Y, then consolidate postings.                             MD059841
      ** If N, then produce detail postings as per AR548538.                                MD059841
      *                                                                                     MD059841
     C           CONPFA    IFEQ 'Y'                                                         MD059841
     C           'TRANSFER'CAT  XCCY:1    PNAR                                              MD059841
     C           PNAR      CAT  NTRF:1    PNAR                                              MD059841
     C                     ELSE                                                             MD059841
     C                     MOVE *BLANK    WNAR1  21                                         AR548538
     C********** 'TRANSFER'CAT  XCCY:1    PNAR                                              AR548538
     C********** PNAR      CAT  NTRF:1    PNAR             Posting Narrative                AR548538
     C                     MOVELNTRF1     PNAR                                              AR548538
     C                     MOVELXCNUM     NARL    9                                         AR548538
     C                     MOVE XCCY      NARL                                              AR548538
     C                     MOVELXACOD     NARR   12                                         AR548538
     C                     MOVE XACSQ     NARR                                              AR548538
      *                                                                                     AR548538
     C                     MOVELNARL      WNAR1                                             AR548538
     C                     MOVE NARR      WNAR1                                             AR548538
      *                                                                                     AR548538
     C           PNAR      CAT  WNAR1:1   PNAR                                              AR548538
      *                                                                                     AR548538
     C                     ENDIF                                                            MD059841
     C           XPNAR     IFNE *BLANK
     C                     MOVE *BLANK    PNAR
     C           'GAP TFR' CAT  XCCY:1    PNAR
     C           PNAR      CAT  NTRF:1    PNAR             Posting Narrative
     C                     ENDIF
     C/COPY WNCPYSRC,MC0410C007
      *
     C                     WRITEMCPCACD0
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  PRW05  -  Write to Profit Centre Accounting file for each    *
      *            Transfer to the Base Ccy Spot Trade Account, when  *
      *            Base Ccy Spot Trade A/c is maintained.             *
      *                                                               *
      *  Called By:  Main Processing                                  *
      *                                                               *
      *  Calls    :  None                                             *
      *                                                               *
      *****************************************************************
     C           PRW05     BEGSR
      *
      **  Set up DSMOVE
      *
     C                     MOVE BLSPTA    ACOD             Spot Trade A/c
     C**********           Z-ADDXBCUS     CNUM             Branch Internal Cust.              CSD027
     C                     MOVE XBCUS     CNUM             Branch Internal Cust.              CSD027
     C                     MOVE '01'      ACSQ             A/c Sequence Nbr
     C                     MOVE DSFPPC    PRFC             FX Profit Centre
     C           GLPAMT    MULT -1        PAMT             GM0765 Base Ccy Amt
     C                     MOVE *BLANK    ACSC             Spot Trade A/c Sect.
     C                     MOVE *BLANK    ATYP             Spot Trade A/c Type
     C                     MOVE *BLANK    PNAR
      *                                                                                     MD059841
      ** If ConPosFundGapAccnt is Y, then consolidate postings.                             MD059841
      ** If N, then produce detail postings as per AR548538.                                MD059841
      *                                                                                     MD059841
     C           CONPFA    IFEQ 'Y'                                                         MD059841
     C           'TRANSFER'CAT  XCCY:1    PNAR                                              MD059841
     C           PNAR      CAT  NTRF:1    PNAR                                              MD059841
     C                     ELSE                                                             MD059841
     C                     MOVE *BLANK    WNAR1  21                                         AR548538
     C********** 'TRANSFER'CAT  XCCY:1    PNAR                                              AR548538
     C********** PNAR      CAT  NTRF:1    PNAR             Posting Narrative                AR548538
     C                     MOVELNTRF1     PNAR                                              AR548538
     C                     MOVELXCNUM     NARL    9                                         AR548538
     C                     MOVE XCCY      NARL                                              AR548538
     C                     MOVELXACOD     NARR   12                                         AR548538
     C                     MOVE XACSQ     NARR                                              AR548538
      *                                                                                     AR548538
     C                     MOVELNARL      WNAR1                                             AR548538
     C                     MOVE NARR      WNAR1                                             AR548538
      *                                                                                     AR548538
     C           PNAR      CAT  WNAR1:1   PNAR                                              AR548538
      *                                                                                     AR548538
     C                     ENDIF                                                            MD059841
     C           XPNAR     IFNE *BLANK
     C                     MOVE *BLANK    PNAR
     C           'GAP TFR' CAT  XCCY:1    PNAR
     C           PNAR      CAT  NTRF:1    PNAR             Posting Narrative
     C                     ENDIF
      *
      **  Blank Book if Spot Trade Analysis is not required
      *
     C           SPACBB    IFNE 'Y'
     C                     MOVE *BLANK    BOKC
     C                     ENDIF
      *
      **  Blank Product if Spot Trade Analysis is not required
      *
     C           SPACBP    IFNE 'Y'
     C                     MOVE *BLANKS   OTST
     C                     MOVE *BLANKS   OTTP
     C                     ENDIF
      *
      **  Blank Assoc. Cust if Spot Trade Analysis is not required
      *
     C           SPACBC    IFNE 'Y'
     C**********           Z-ADD*ZERO     ASOC                                                CSD027
     C                     MOVE *BLANKS   ASOC                                                CSD027
     C                     ENDIF
      *
     C                     WRITEMCPCACD0
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  PRW06  -  Write to Profit Centre Accounting file for each    *
      *            Transfer to the Base Ccy Equivalent Account, when  *
      *            Base Ccy Spot Trade A/c is Maintained.             *
      *                                                               *
      *  Called By:  Main Processing                                  *
      *                                                               *
      *  Calls    :  None                                             *
      *                                                               *
      *****************************************************************
     C           PRW06     BEGSR
      *
      **  Set up DSMOVE
      *
     C                     MOVE BCEQIV    ACOD             Base Ccy Equiv. A/c
     C                     Z-ADDGLPAMT    PAMT             GL0765 BASE Ccy Amt
      *
     C                     WRITEMCPCACD0
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  PRW07  -  Write to Profit Centre Accounting file for each    *
      *            Foreign Ccy IN/EX Profit Centre Gap Account, when  *
      *            Profit Centre is not an FX Holder.                 *
      *                                                               *
      *  Called By:  PRP02 - Generate Entries to Balance Profit Cent. *
      *                                                               *
      *  Calls    :  None                                             *
      *                                                               *
      *****************************************************************
     C           PRW07     BEGSR
      *
      **  Set up DSMOVE
      *
     C                     MOVE XBRCA     BRCA             Branch
     C                     MOVE XCCY      CCY              Currency
     C                     MOVE FTPFGA    ACOD             Prime Gap A/c
     C**********           Z-ADDXBCUS     CNUM             Branch Internal Cust.              CSD027
     C                     MOVE XBCUS     CNUM             Branch Internal Cust.              CSD027
     C                     MOVE '01'      ACSQ             A/c Sequence Nbr
     C                     MOVE XPRFC     PRFC             Profit Centre
     C                     MOVE *BLANKS   BOKC             Book
     C                     MOVE *BLANKS   OTTP             Deal Type
     C                     MOVE *BLANKS   OTST             Deal Sub-Type
     C**********           Z-ADD*ZEROS    ASOC             Associated Cust.                   CSD027
     C                     MOVE *BLANKS   ASOC             Associated Cust.                   CSD027
     C                     Z-ADDXEVDT     EVDT             Event Date
     C                     Z-ADDXPAMT     PAMT             Posting Amount
     C                     Z-ADDXVOIN     VOIN             Void Indicator
     C                     MOVE 'X'       GETP             Generated Entry Type
     C                     MOVE *BLANK    ACSC             Prime Gap A/c Sect.
     C                     Z-ADDXCPNB     CPNB             Current Period
     C                     Z-ADDXPDTM     PDTM             Posting Date/Time
     C                     MOVE *BLANK    ATYP             Account Type
     C                     MOVE *BLANK    PNAR
      *                                                                                     MD059841
      ** If ConPosFundGapAccnt is Y, then consolidate postings.                             MD059841
      ** If N, then produce detail postings as per AR548538.                                MD059841
      *                                                                                     MD059841
     C           CONPFA    IFEQ 'Y'                                                         MD059841
     C           'TRANSFER'CAT  XCCY:1    PNAR                                              MD059841
     C           PNAR      CAT  NTRF:1    PNAR                                              MD059841
     C                     ELSE                                                             MD059841
     C                     MOVE *BLANK    WNAR1  21                                         AR548538
     C********** 'TRANSFER'CAT  XCCY:1    PNAR                                              AR548538
     C********** PNAR      CAT  NTRF:1    PNAR             Posting Narrative                AR548538
     C                     MOVELNTRF1     PNAR                                              AR548538
     C                     MOVELXCNUM     NARL    9                                         AR548538
     C                     MOVE XCCY      NARL                                              AR548538
     C                     MOVELXACOD     NARR   12                                         AR548538
     C                     MOVE XACSQ     NARR                                              AR548538
      *                                                                                     AR548538
     C                     MOVELNARL      WNAR1                                             AR548538
     C                     MOVE NARR      WNAR1                                             AR548538
      *                                                                                     AR548538
     C           PNAR      CAT  WNAR1:1   PNAR                                              AR548538
      *                                                                                     AR548538
     C                     ENDIF                                                            MD059841
     C           XPNAR     IFNE *BLANK
     C                     MOVE *BLANK    PNAR
     C           'GAP TFR' CAT  XCCY:1    PNAR
     C           PNAR      CAT  NTRF:1    PNAR             Posting Narrative
     C                     ENDIF
      *
     C                     WRITEMCPCACD0
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  PRW08  -  Write to Profit Centre Accounting file for each    *
      *            Foreign Spot Trade Profit Centre Gap Account, when *
      *            Profit Centre is not an FX Holder.                 *
      *                                                               *
      *  Called By:  PRP02 - Generate Entries to Balance Profit Cent. *
      *                                                               *
      *  Calls    :  None                                             *
      *                                                               *
      *****************************************************************
     C           PRW08     BEGSR
      *
      **  Set up DSMOVE
      *
     C                     MOVE DSFPPC    PRFC             FX Profit Centre
     C           XPAMT     MULT -1        PAMT             Posting Amount
      *
     C                     WRITEMCPCACD0
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  PRW09  -  Write to Profit Centre Accounting file for each    *
      *            Base Ccy Equivalent Profit Centre Gap Account, when*
      *            Profit Centre is not an FX Holder.                 *
      *                                                               *
      *  Called By:  PRP02 - Generate Entries to Balance Profit Cent. *
      *                                                               *
      *  Calls    :  None                                             *
      *                                                               *
      *****************************************************************
     C           PRW09     BEGSR
      *
      **  Set up DSMOVE
      *
     C                     MOVE XBCCY     CCY              Base Currency
     C                     MOVE DSFPPC    PRFC             FX Profit Centre
     C                     Z-ADDGLPAMT    PAMT             GL0765 Base Ccy Amt
      *
     C                     WRITEMCPCACD0
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  PRW10  -  Write to Profit Centre Accounting file for each    *
      *            Base Ccy IN/EX Profit Centre Gap Account, when     *
      *            Profit Centre is not an FX Holder.                 *
      *                                                               *
      *  Called By:  PRP02 - Generate Entries to Balance Profit Cent. *
      *                                                               *
      *  Calls    :  None                                             *
      *                                                               *
      *****************************************************************
     C           PRW10     BEGSR
      *
      **  Set up DSMOVE
      *
     C                     MOVE XPRFC     PRFC             Profit Centre
     C           GLPAMT    MULT -1        PAMT             GL0765 Base Ccy Amt
      *
     C                     WRITEMCPCACD0
      *
     C                     ENDSR
      *****************************************************************
     C/COPY WNCPYSRC,MC0410C004
      /EJECT
      *****************************************************************
      *                                                               *
      *  DBERR   -  Error handling                                    *
      *                                                               *
      *  Called By:  Main Processing                                  *
      *              PRP03 - Revaluation of Spot Trade Amount for     *
      *                      each Spot Trade Account posting          *
      *              PRP04 - Transfer B/Ccy to Retained P&L           *
      *              PRP05 - Update Gap Accrual History               *
      *                                                               *
      *  Calls    :  None                                             *
      *                                                               *
      *****************************************************************
     C           DBERR     BEGSR
      *
     C           *LOCK     IN   LDA
     C                     MOVELWFILE     DBFILE
     C                     MOVE WBASE     DBASE
     C                     MOVELWKEY      DBKEY
     C                     MOVELWPGM      DBPGM
     C                     OUT  LDA
     C                     MOVEL'*FCTFR'  XRETC
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
     C/TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR - Subroutine to handle abnormal error conditions       *
      *                                                               *
      *  Called by:  Abnormal Termination Processing                  *
      *                                                               *
      *  Calls    :  None                                             *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     MOVEL'*FCTFR'  XRETC
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR                           ** *PSSR **
      *
      *****************************************************************
      /SPACE 3
**  CPY@
(c) Finastra International Limited 2001
