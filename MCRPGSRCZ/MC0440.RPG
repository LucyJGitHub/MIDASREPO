     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MC Reset Rework Date for Rate Changes')
      *****************************************************************
      *                                                               *
      *  Midas - Management Accounts Module                           *
      *                                                               *
      *  MC0440 - Reset Rework Date for Rate Changes                  *
      *                                                               *
      *  Function: This program sets the rework date for the recal-   *
      *    (COB)   culation of Funding gap accruals due to a back-    *
      *            valued Base Rate change.  If the rework date is    *
      *            already before the base rate change date, no       *
      *            change is made.                                    *
      *                                                               *
      *  Called by: SD0600 - Base Rates Table Update                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  LAST AMEND NO. CMC001 *CREATE     DATE 18MAR96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CMC001 - Management Accounts Enhancement for PCA             *
      *                                                               *
      *****************************************************************
      *
     FMCACHRL0UF  E           K        DISK         KINFSR *PSSR
      **  Funding Gap Accrual History Headers
      *
     F/SPACE 3
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   20  - End of file on LF/MCACHRL0                            *
      *                                                               *
      *****************************************************************
     F/SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  *PSSR - Subroutine to Handle Abnormal Error Condition        *
      *                                                               *
      *****************************************************************
     E/SPACE 3
     E                    CPY@    1   1 80
      *
      **  Array containing Copyright statement
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      **  Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      **  Receive parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           PCHCY   3
     C                     PARM           PCHDT   50
      *
      **  Read first record
      *
     C           *LOVAL    SETLLMCACHRL0
     C                     READ MCACHRL0                 20
      *
     C           *IN20     DOWEQ*OFF
      *
      **  For each record in MCACHRL0 with currency equal to Base
      **  Rate Change Currency
      *
     C           GHCYCD    IFEQ PCHCY
      *
      **  If Base Rate Change Date is less than the Earliest
      **  Date, set Rework Date to Earliest Date
      *
     C           PCHDT     IFLT GHERLD
     C                     Z-ADDGHERLD    GHLRDT
     C                     ELSE
      *
      **  Otherwise, if Base Rate Change Date is less than the Rework
      **  Date, set Rework Date to the Base Rate Change Date
      *
     C           PCHDT     IFLT GHLRDT
     C                     Z-ADDPCHDT     GHLRDT
     C                     ENDIF
     C                     ENDIF
      *
     C                     UPDATMCACHRD0
     C                     ENDIF
      *
     C                     READ MCACHRL0                 20
     C                     ENDDO
      *
      **  Return to calling program
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR - Subroutine to handle abnormal error conditions       *
      *                                                               *
      *  Called by:  Abnormal Termination Processing                  *
      *                                                               *
      *  Calls    :  None                                             *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR **
      *
     C           @RUN      IFEQ *BLANKS
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR                           ** *PSSR **
      *
      *****************************************************************
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
