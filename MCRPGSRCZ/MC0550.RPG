     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MC F/M Accounts Extract')
      *****************************************************************
      *                                                               *
      *  Midas - Management Accounting Module                         *
      *                                                               *
      *  MC0550 - Financial/Management Accounts Extract               *
      *                                                               *
      *  Function:  This program combines Financial accounts and      *
      *             Management accounts into one file.                *
      *                                                               *
      *  Called By: MCC0550 - Financial/Management Accounts Reconc'n  *
      *                       Control Program                         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 124847 *C *CREATE  Date 05Sep97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  124847 - Financial/Management Accounts Reconciliation        *
      *                                                               *
      *****************************************************************
     F*
     FMCACCNL0IF  E           K        DISK         KINFSR *PSSR
     F**  Financial Accounts Master
     F*
     FMCHIBLL0IF  E           K        DISK         KINFSR *PSSR
     F**  Management Accounts Master
     F*
     FMCACHBPDO   E                    DISK         KINFSR *PSSR
     F**  Financial/Management Accounts Extract
     F*
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*   10  - EOF in LF/MCACCNL0                                    *
     F*   11  - EOF in LF/MCHIBLL0                                    *
     F*                                                               *
     F* U7+U8 - Database Error                                        *
     F*                                                               *
     F*****************************************************************
     F/SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  SRACCN - Process Financial Account                           *
     F*  SRHIBL - Process Management Account                          *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E                    CPY@    1   1 80
     E**  Array containing Copyright statement
     E*
     C/EJECT
     C*================================================================
     C*  P R O G R A M   S T A R T                                    *
     C*================================================================
     C*
     C**  Set up copyright parameter
     C*
     C                     MOVEACPY@      CPY2@  80
     C*
     C**  Process all records in Financial account master file
     C*
     C                     EXSR SRACCN
     C*
     C**  Process all records in Management account master file
     C*
     C                     EXSR SRHIBL
     C*
     C**  End Program and Return.
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  SRACCN - Process Financial Account                           *
     C*                                                               *
     C*  Called By:  MAIN   - Main Processing Routine                 *
     C*                                                               *
     C*  Calls    :  -                                                *
     C*                                                               *
     C*****************************************************************
     C*
     C           SRACCN    BEGSR                           ** SRACCN **
     C*
     C**  Process all records in the Financial Account master file.
     C*
     C           *LOVAL    SETLLMCACCNL0
     C                     READ MCACCNL0                 10
     C           *IN10     DOWEQ*OFF
     C                     MOVE BRCA      MCBRCD
     C                     MOVE CCY       MCCYCD
     C                     MOVE ACOD      MCACCD
     C                     MOVE CNUM      MCCNUM
     C                     MOVE ACSQ      MCACSQ
     C                     MOVE *BLANKS   MCPRFC
     C                     Z-ADDLDBL      MCLDBL
     C                     WRITEMCACHBD0
     C                     READ MCACCNL0                 10
     C                     ENDDO
     C*
     C           SRACC9    ENDSR                           ** SRACC9 **
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  SRHIBL - Process Management Account                          *
     C*                                                               *
     C*  Called By:  MAIN   - Main Processing Routine                 *
     C*                                                               *
     C*  Calls    :  -                                                *
     C*                                                               *
     C*****************************************************************
     C*
     C           SRHIBL    BEGSR                           ** SRHIBL **
     C*
     C**  Process all records in the Management Account master file.
     C*
     C           *LOVAL    SETLLMCHIBLL0
     C                     READ MCHIBLL0                 11
     C           *IN11     DOWEQ*OFF
     C                     MOVE HBBRCD    MCBRCD
     C                     MOVE HBCYCD    MCCYCD
     C                     MOVE HBACCD    MCACCD
     C                     MOVE HBCUST    MCCNUM
     C                     MOVE HBACSN    MCACSQ
     C                     MOVE HBPCCD    MCPRFC
     C                     Z-ADDHBPDCB    MCLDBL
     C                     WRITEMCACHBD0
     C                     READ MCHIBLL0                 11
     C                     ENDDO
     C*
     C           SRHIB9    ENDSR                           ** SRHIB9 **
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  *PSSR  - Error control subroutine                            *
     C*                                                               *
     C*  Called By:                                                   *
     C*                                                               *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR                           ** *PSSR  **
     C*
     C**  Check to see that *PSSR has not already been called.
     C*
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     ENDIF
     C*
     C                     RETRN
     C*
     C           WPSS9     ENDSR                           ** WPSS9 **
     C*
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2001
