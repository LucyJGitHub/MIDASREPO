/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas ME SWIFTRef Bank Dir. Plus Chk Rel Date/File')  */
/*********************************************************************/
/*                                                                   */
/*       Midas - Message Management                                  */
/*                                                                   */
/*       MEC001427 - SWIFTRef BANK Dir. Plus Check Release Date/Files*/
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2013           */
/*                                                                   */
/*       Last Amend No. CFT051             Date 02Jun15              */
/*       Prev Amend No. CFT050  *CREATE    Date 03Jun13              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CFT051 - SWIFTRef Directories 2014                          */
/*       CFT050 - SWIFTRef Directories upload                        */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&ERRTYP &INHDR)


/**********  DCL        VAR(&ERRTYP) TYPE(*CHAR) LEN(1)              */                   /*CFT051*/
             DCL        VAR(&ERRTYP) TYPE(*CHAR) LEN(2)                                   /*CFT051*/
             DCL        VAR(&INHDR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&B3) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&CT) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&CU) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&HL) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&TZ) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&SD) TYPE(*CHAR) LEN(1) VALUE(' ')                            /*CFT051*/
             DCL        VAR(&LL) TYPE(*CHAR) LEN(1) VALUE(' ')                            /*CFT051*/
             DCL        VAR(&YY) TYPE(*DEC) LEN(2 0) VALUE(00)
             DCL        VAR(&MM) TYPE(*DEC) LEN(2 0) VALUE(00)
             DCL        VAR(&DD) TYPE(*DEC) LEN(2 0) VALUE(00)
             DCL        VAR(&PRTCD) TYPE(*CHAR) LEN(7) VALUE(' ')                         /*CFT051*/
             DCL        VAR(&CFT051) TYPE(*CHAR) LEN(1) VALUE(' ')                        /*CFT051*/

             DCLF       FILE(METXTFL1)

/* Copyright statement                                               */
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2013')

/* Global Monitor Message                                            */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))

             CHGDTAARA  DTAARA(LDA) VALUE(' ')
             CHGJOB     SWS(XXXXXX00)
             CHGVAR     VAR(&ERRTYP) VALUE('0')
             CHGVAR     VAR(&INHDR) VALUE('      ')

             CLRPFM     METXTFPD

             CALL       PGM(AOSARDR0) PARM(&PRTCD '*VERIFY' 'CFT051')                     /*CFT051*/
             IF         COND(&PRTCD *EQ ' ') THEN(CHGVAR +
                          VAR(&CFT051) VALUE('Y'))                                        /*CFT051*/

/* List all files of QGPL library                                    */
             DSPFD      FILE(QGPL/*ALL) TYPE(*BASATR) +
                          OUTPUT(*OUTFILE) OUTFILE(METXTFPD)

/* Read METXTFL1 file                                                */
 LOOP1:
             RCVF
             MONMSG     MSGID(CPF0864) EXEC(DO)
             IF         COND((&B3 *EQ 'Y') *AND (&CT *EQ 'Y')) +
               THEN(DO)
                 GOTO       CMDLBL(ENDCLPGM)
               ENDDO
             GOTO       CMDLBL(ERROR9)
             ENDDO

/* Filenames starting with B3_                                       */
             IF         COND((&PREF3 *EQ 'B3_') *AND (&DATE6 +
                        *GT &INHDR)) THEN(DO)
               IF       COND(%SST(&ATFILE  9 1) *EQ ' ') THEN(DO)
                  GOTO       CMDLBL(LOOP1)
               ENDDO
               IF       COND(%SST(&ATFILE 10 1) *EQ ' ') THEN(DO)
                 IF     COND((&INHDR *EQ *BLANK) *OR (&DATE6 *GT +
                          &INHDR)) THEN(DO)
                        CHGVAR     VAR(&B3) VALUE(' ')
                        CHGVAR     VAR(&YY) VALUE(%SST(&DATE6 1 2))
                          MONMSG     MSGID(CPF0818) EXEC(DO)
                            GOTO LOOP1
                          ENDDO
                        CHGVAR     VAR(&MM) VALUE(%SST(&DATE6 3 2))
                          MONMSG     MSGID(CPF0818) EXEC(DO)
                            GOTO LOOP1
                          ENDDO
                        CHGVAR     VAR(&DD) VALUE(%SST(&DATE6 5 2))
                          MONMSG     MSGID(CPF0818) EXEC(DO)
                            GOTO LOOP1
                          ENDDO

                        IF         ((&MM *GT 12) *OR (&MM *EQ 00)) +
                          THEN(DO)
                            GOTO       CMDLBL(LOOP1)
                          ENDDO
                        IF         ((&DD *GT 31) *OR (&DD *EQ 00)) +
                          THEN(DO)
                            GOTO       CMDLBL(LOOP1)
                          ENDDO
                        CHGVAR     VAR(&INHDR) VALUE(&DATE6)
                 ENDDO

                 CPYF       FROMFILE(QGPL/&ATFILE) TOFILE(MEDWB3PD) +
                            MBROPT(*REPLACE) FMTOPT(*NOCHK)
                 MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                    EXEC(DO)
                      GOTO       CMDLBL(ERROR2)
                    ENDDO

                 CHGVAR     VAR(&B3) VALUE('Y')
               ENDDO
             ENDDO

             IF         COND(&B3 *NE 'Y') +
               THEN(DO)
                 GOTO       CMDLBL(ERROR1)
               ENDDO

/* Filenames starting with CT_                                       */
             IF         COND((&PREF3 *EQ 'CT_') *AND (&DATE6 *EQ &INHDR)) +
                          THEN(DO)
               IF       COND(%SST(&ATFILE 10 1) *EQ ' ') THEN(DO)

                 CPYF       FROMFILE(QGPL/&ATFILE) TOFILE(MEDWCTPD) +
                            MBROPT(*REPLACE) FMTOPT(*NOCHK)
                 MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                    EXEC(DO)
                      GOTO       CMDLBL(ERROR3)
                    ENDDO

                 CHGVAR     VAR(&CT) VALUE('Y')
               ENDDO
             ENDDO

/* Filenames starting with CU_                                       */
             IF         COND((&PREF3 *EQ 'CU_') *AND (&DATE6 *EQ &INHDR)) +
                          THEN(DO)
               IF       COND(%SST(&ATFILE 10 1) *EQ ' ') THEN(DO)

                 CPYF       FROMFILE(QGPL/&ATFILE) TOFILE(MEDWCUPD) +
                            MBROPT(*REPLACE) FMTOPT(*NOCHK)
                 MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                    EXEC(DO)
                      GOTO       CMDLBL(ERROR4)
                    ENDDO

                 CHGVAR     VAR(&CU) VALUE('Y')
               ENDDO
             ENDDO

/* Filenames starting with HL_                                       */
             IF         COND((&PREF3 *EQ 'HL_') *AND (&DATE6 *EQ &INHDR)) +
                          THEN(DO)
               IF       COND(%SST(&ATFILE 10 1) *EQ ' ') THEN(DO)

                 CPYF       FROMFILE(QGPL/&ATFILE) TOFILE(MEDWHLPD) +
                            MBROPT(*REPLACE) FMTOPT(*NOCHK)
                 MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                    EXEC(DO)
                      GOTO       CMDLBL(ERROR5)
                    ENDDO

                 CHGVAR     VAR(&HL) VALUE('Y')
               ENDDO
             ENDDO

/* Filenames starting with TZ_                                       */
             IF         COND((&PREF3 *EQ 'TZ_') *AND (&DATE6 *EQ &INHDR)) +
                          THEN(DO)
               IF       COND(%SST(&ATFILE 10 1) *EQ ' ') THEN(DO)

                 CPYF       FROMFILE(QGPL/&ATFILE) TOFILE(MEDWTZPD) +
                            MBROPT(*REPLACE) FMTOPT(*NOCHK)
                 MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                    EXEC(DO)
                      GOTO       CMDLBL(ERROR6)
                    ENDDO

                 CHGVAR     VAR(&TZ) VALUE('Y')
               ENDDO
             ENDDO
                                                                                          /*CFT051*/
                                                                                          /*CFT051*/
/* Filenames starting with SD_                                       */                   /*CFT051*/
             IF         COND(&CFT051 *EQ 'Y') THEN(DO)                                    /*CFT051*/
                                                                                          /*CFT051*/
             IF         COND((&PREF3 *EQ 'SD_') *AND (&DATE6 *EQ &INHDR)) +
                          THEN(DO)                                                        /*CFT051*/
               IF       COND(%SST(&ATFILE 10 1) *EQ ' ') THEN(DO)                         /*CFT051*/
                                                                                          /*CFT051*/
                 CPYF       FROMFILE(QGPL/&ATFILE) TOFILE(MEDWSDPD) +
                            MBROPT(*REPLACE) FMTOPT(*NOCHK)                               /*CFT051*/
                 MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                    EXEC(DO)                                                              /*CFT051*/
                      GOTO       CMDLBL(ERROR10)                                          /*CFT051*/
                    ENDDO                                                                 /*CFT051*/
                                                                                          /*CFT051*/
                 CHGVAR     VAR(&SD) VALUE('Y')                                           /*CFT051*/
               ENDDO                                                                      /*CFT051*/
             ENDDO                                                                        /*CFT051*/
                                                                                          /*CFT051*/
             ENDDO                                                                        /*CFT051*/
                                                                                          /*CFT051*/
                                                                                          /*CFT051*/
/* Filenames starting with LL_                                       */                   /*CFT051*/
             IF         COND(&CFT051 *EQ 'Y') THEN(DO)                                    /*CFT051*/
                                                                                          /*CFT051*/
             IF         COND((&PREF3 *EQ 'LL_') *AND (&DATE6 *EQ &INHDR)) +
                          THEN(DO)                                                        /*CFT051*/
               IF       COND(%SST(&ATFILE 10 1) *EQ ' ') THEN(DO)                         /*CFT051*/
                                                                                          /*CFT051*/
                 CPYF       FROMFILE(QGPL/&ATFILE) TOFILE(MEDWLLPD) +
                            MBROPT(*REPLACE) FMTOPT(*NOCHK)                               /*CFT051*/
                 MONMSG     MSGID(CPF2817) CMPDTA(CPF2869) +
                    EXEC(DO)                                                              /*CFT051*/
                      GOTO       CMDLBL(ERROR11)                                          /*CFT051*/
                    ENDDO                                                                 /*CFT051*/
                                                                                          /*CFT051*/
                 CHGVAR     VAR(&LL) VALUE('Y')                                           /*CFT051*/
               ENDDO                                                                      /*CFT051*/
             ENDDO                                                                        /*CFT051*/
                                                                                          /*CFT051*/
             ENDDO                                                                        /*CFT051*/
                                                                                          /*CFT051*/
                                                                                          /*CFT051*/
/* End Loop when All possible entries has been read */

             IF         COND(&CFT051 *EQ 'Y') THEN(DO)                                    /*CFT051*/
             IF         COND((&B3 *EQ 'Y') *AND (&CT *EQ 'Y') *AND +
                          (&CU *EQ 'Y') *AND (&HL *EQ 'Y') *AND (&TZ *EQ 'Y') +
                          *AND (&SD *EQ 'Y') *AND (&LL *EQ 'Y')) +
                          THEN(GOTO CMDLBL(ENDCLPGM))                                     /*CFT051*/
             GOTO       CMDLBL(LOOP1)                                                     /*CFT051*/
             ENDDO                                                                        /*CFT051*/
                                                                                          /*CFT051*/
             ELSE       CMD(DO)                                                           /*CFT051*/
                                                                                          /*CFT051*/
             IF         COND((&B3 *EQ 'Y') *AND (&CT *EQ 'Y') *AND +
                          (&CU *EQ 'Y') *AND (&HL *EQ 'Y') *AND (&TZ *EQ 'Y')) +
                          THEN(GOTO CMDLBL(ENDCLPGM))
             GOTO       CMDLBL(LOOP1)
             ENDDO                                                                        /*CFT051*/

 ERROR1:
             SNDPGMMSG  MSG('No file B3_YYMMDD exists in QGPL.') +
                          TOMSGQ(MOPERQ)
             IF         COND(&INHDR *EQ ' ') THEN(DO)
                CHGVAR     VAR(&INHDR) VALUE('YYMMDD')
             ENDDO
             CHGVAR     VAR(&ERRTYP) VALUE('1')
             GOTO       CMDLBL(ENDCLPGM)

 ERROR2:
             SNDPGMMSG  MSG('BANK DIRECTORY Plus File (B3_' *CAT +
                          &INHDR *CAT ' is empty. Nothing can be +
                          processed.') TOMSGQ(MOPERQ)
             CHGVAR     VAR(&ERRTYP) VALUE('2')
             CLRPFM     FILE(MEDWB3PD)
             GOTO       CMDLBL(ENDCLPGM)

 ERROR3:
             SNDPGMMSG  MSG('BANK DIRECTORY Plus/Country File CT_' *CAT +
                          &INHDR *CAT ' is empty. Nothing can be +
                          processed.') TOMSGQ(MOPERQ)
             CHGVAR     VAR(&ERRTYP) VALUE('3')
             CLRPFM     FILE(MEDWCTPD)
             GOTO       CMDLBL(ENDCLPGM)

 ERROR4:
             SNDPGMMSG  MSG('FYI: BANK DIRECTORY Plus/Currency File +
                          CU_' *CAT &INHDR *CAT ' is empty. No +
                          Currency details will be processed.') +
                          TOMSGQ(MOPERQ)

             CHGVAR     VAR(&ERRTYP) VALUE('4')
             CLRPFM     FILE(MEDWCUPD)
             MONMSG     MSGID(CPF0000)
             GOTO       CMDLBL(LOOP1)

 ERROR5:
             SNDPGMMSG  MSG('FYI: BANK DIRECTORY Plus/Holiday File +
                          HL_' *CAT &INHDR *CAT ' is empty. No +
                          Holiday details will be processed.') +
                          TOMSGQ(MOPERQ)

             CHGVAR     VAR(&ERRTYP) VALUE('5')
             CLRPFM     FILE(MEDWHLPD)
             MONMSG     MSGID(CPF0000)
             GOTO       CMDLBL(LOOP1)

 ERROR6:
             SNDPGMMSG  MSG('FYI: BANK DIRECTORY Plus/Timezone File +
                          TZ_' *CAT &INHDR *CAT ' is empty. No +
                          Timezone Details will be processed.') +
                          TOMSGQ(MOPERQ)

             CHGVAR     VAR(&ERRTYP) VALUE('6')
             CLRPFM     FILE(MEDWTZPD)
             MONMSG     MSGID(CPF0000)
             GOTO       CMDLBL(LOOP1)
                                                                                          /*CFT051*/
 ERROR10:                                                                                 /*CFT051*/
             IF         COND(&CFT051 *EQ 'Y') THEN(DO)                                    /*CFT051*/
             SNDPGMMSG  MSG('FYI: BANK DIRECTORY Plus/Service Direc+
                          tory SD_' *CAT &INHDR *CAT ' is empty. No +
                          Service Directory Details will be processed.') +
                          TOMSGQ(MOPERQ)                                                  /*CFT051*/
                                                                                          /*CFT051*/
             CHGVAR     VAR(&ERRTYP) VALUE('10')                                          /*CFT051*/
             CLRPFM     FILE(MEDWSDPD)                                                    /*CFT051*/
             MONMSG     MSGID(CPF0000)                                                    /*CFT051*/
             GOTO       CMDLBL(LOOP1)                                                     /*CFT051*/
             ENDDO                                                                        /*CFT051*/
                                                                                          /*CFT051*/
                                                                                          /*CFT051*/
 ERROR11:                                                                                 /*CFT051*/
             IF         COND(&CFT051 *EQ 'Y') THEN(DO)                                    /*CFT051*/
             SNDPGMMSG  MSG('FYI: BANK DIRECTORY Plus/Service Direc+
                          tory LL_' *CAT &INHDR *CAT ' is empty. No +
                          Service Directory Details will be processed.') +
                          TOMSGQ(MOPERQ)                                                  /*CFT051*/
                                                                                          /*CFT051*/
             CHGVAR     VAR(&ERRTYP) VALUE('11')                                          /*CFT051*/
             CLRPFM     FILE(MEDWLLPD)                                                    /*CFT051*/
             MONMSG     MSGID(CPF0000)                                                    /*CFT051*/
             GOTO       CMDLBL(LOOP1)                                                     /*CFT051*/
             ENDDO                                                                        /*CFT051*/
                                                                                          /*CFT051*/

 ERROR9:
             SNDPGMMSG  MSG('The file being processed is empty. This +
                          may be due to the following reasons  1.) +
                          File sent is not complete. 2.) B3_ and CT_ +
                          files must exist altogether 3.) Error in +
                          filename used during FTP of transfer +
                          process, filenames should begin with B3_ +
                          or CT_') TOMSGQ(MOPERQ)
             IF         COND(&INHDR *EQ ' ') THEN(DO)
                CHGVAR     VAR(&INHDR) VALUE('YYMMDD')
             ENDDO
             CHGVAR     VAR(&ERRTYP) VALUE('9')
             GOTO       CMDLBL(ENDCLPGM)

/* Abnormal termination */
ABNOR:
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSG('Error during Check of Incoming Data. +
                          Abnormal termination. Ensure that at +
                          Least B3_YYMMDD and CT_YYMMDD files exist +
                          in QGPL with data of File Transfer +
                          (FTP).') TOMSGQ(MOPERQ)
             CHGVAR     VAR(&ERRTYP) VALUE('8')

/* End program */
 ENDCLPGM:

             ENDPGM
