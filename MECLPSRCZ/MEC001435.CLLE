/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas ME SWIFTRef IBAN Plus DIRECTORY Loader')        */
/*********************************************************************/
/*                                                                   */
/*       Midas - Message Management                                  */
/*                                                                   */
/*       MEC001435 - SWIFTREF IBAN Plus DIRECTORY Loader             */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2013           */
/*                                                                   */
/*       Last Amend No. MD023090           Date 11Nov13              */
/*       Prev Amend No. MD023074           Date 27Oct13              */
/*                      CFT050  *CREATE    Date 03Jun13              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD023090 - Incorrect Error Message Displayed in Type of     */
/*                  Upload and Update the BIC IBAN Plus fields       */
/*       MD023074 - Incorrect error messages shown.                  */
/*       CFT050 - SWIFTRef Directories upload                        */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&REFDATE) TYPE(*CHAR) LEN(6)
             DCL        VAR(&ERRTYP)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&YYYYMMDD) TYPE(*CHAR) LEN(8)
             DCL        VAR(&RTCD    ) TYPE(*CHAR) LEN(7)
 
             DCLF       FILE(ME001435DF)
 
/* Copyright statement                                               */
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2013')
 
/* Global Monitor Message                                            */
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) +
                           EXEC(GOTO ABNOR)
             CHGJOB     SWS(00000000)
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)
 
             STRCMTCTL  LCKLVL(*CHG) CMTSCOPE(*JOB)
             MONMSG     MSGID(CPF8351)
 
/* Clear program message queue and initialise return code            */
             RMVMSG     PGMQ(*SAME) CLEAR(*ALL)
 
             RTVDTAARA  DTAARA(RUNDAT (1 7)) RTNVAR(&S0MRDT)
             RTVJOBA    JOB(&S0JOB)  USER(&S0USR)
 
/** STEP 1 - Verify that CFT050 Database is Present - option may run **/
 
             SNDRCVF    RCDFMT(ME001435F0)
             IF         COND(&IN03 *EQ '1') THEN(GOTO CMDLBL(ENDCLPGM))
 
 LOOP:
             CALL       PGM(MEC001426) PARM('3' &S0MSG1 &S0MSG2 &S0MSG3 &S0MSG4 +
                                            &S1ANSW '' '')
 
             IF         COND((&S0MSG1 *NE    ' ')  +
                          *OR  (&S0MSG2 *NE    ' ') +
                          *OR  (&S0MSG3 *NE    ' ') +
                          *OR  (&S0MSG4 *NE    ' ')) THEN(DO)
 
             CHGVAR     VAR(&IN60)  VALUE('1')
             CHGVAR     VAR(&IN61)  VALUE('1')
             CHGVAR     VAR(&IN62)  VALUE('1')
             CHGVAR     VAR(&IN63)  VALUE('1')
             CHGVAR     VAR(&S0MSG5A)  VALUE('Please Ensure with Midas +
                                           Support a correct Delivery.')
 
             SNDRCVF    RCDFMT(ME001435F0)
             IF         COND(&IN03 *EQ '0') THEN(GOTO CMDLBL(LOOP))
             ELSE
             GOTO       CMDLBL(ENDCLPGM)
             ENDDO
 
             ELSE       CMD(DO)
             CHGVAR     VAR(&IN60)  VALUE('0')
             CHGVAR     VAR(&IN61)  VALUE('0')
             CHGVAR     VAR(&IN62)  VALUE('0')
             CHGVAR     VAR(&S0MSG1)   VALUE(' ')
             CHGVAR     VAR(&S0MSG2)   VALUE(' ')
             CHGVAR     VAR(&S0MSG5A)  VALUE(' ')
 
             CHGVAR     VAR(&IN64)  VALUE('1')
             CHGVAR     VAR(&S0MSG6A)  VALUE('Check of the necessary objects +
                                           to run the loader is started.')
 
             SNDF       RCDFMT(ME001435F0)
             DLYJOB     DLY(5)
 
/** STEP 2 - Retrieve data in interim files and detect Release date **/
 
             CALL       PGM(MEC001436) PARM(&ERRTYP &REFDATE)
 
             IF         COND(&ERRTYP *NE '0') THEN(DO)
 
        /* Error message */
             IF         COND(&ERRTYP *EQ '1') THEN(DO)
             CHGVAR     VAR(&IN67)  VALUE('1')
             CHGVAR     VAR(&S2MSG8)  VALUE('No file IB_' *CAT &REFDATE +
                                           *CAT ' exists in QGPL')
             GOTO CMDLBL(ABNOR)
             ENDDO
 
        /* Error message */
             IF         COND(&ERRTYP *EQ '2') THEN(DO)
             CHGVAR     VAR(&IN67)  VALUE('1')
             CHGVAR     VAR(&S2MSG8)  VALUE('IB_' *CAT &REFDATE +
                                           *CAT ' found is empty')
             GOTO CMDLBL(ABNOR)
             ENDDO
 
        /* Error message */
             IF         COND(&ERRTYP *EQ '3') THEN(DO)
             CHGVAR     VAR(&IN67)  VALUE('1')
             CHGVAR     VAR(&S2MSG8)  VALUE('IS_' *CAT &REFDATE +
                                           *CAT ' doesn''t exist or empty')
             GOTO CMDLBL(ABNOR)
             ENDDO
 
        /* Error - Abnormal Termination */
             IF         COND(&ERRTYP *EQ '8') THEN(DO)
             CHGVAR     VAR(&S2MSG8)  VALUE('Abnormal Termination')
             GOTO CMDLBL(ABNOR)
             ENDDO
 
        /* Error message */
             IF         COND(&ERRTYP *EQ '9') THEN(DO)
             CHGVAR     VAR(&IN67)  VALUE('1')
             CHGVAR     VAR(&IN68)  VALUE('1')
             CHGVAR     VAR(&S2MSG8)  VALUE('IB_' *CAT &REFDATE +
                                    *CAT ' and ' *CAT 'IS_' *CAT &REFDATE +
                                    *CAT ', mandatory files')
             CHGVAR     VAR(&S2MSG9)  VALUE(' are not present in QGPL')
             GOTO CMDLBL(ABNOR)
             ENDDO
 
             ENDDO
 
  /** STEP 3 - End-user is choosing the kind of Upload **/
 
 LOOP2:
             CHGVAR     VAR(&S1DATE) VALUE(&REFDATE)
             CHGVAR     VAR(&S1DTYP) VALUE('D')
             CHGVAR     VAR(&S1ANSW) VALUE('Y')
 
  /** Delay 5 seconds **/
/**********  CHGVAR     VAR(&IN64)  VALUE('0')                       */                 /*MD023074*/
/**********  SNDF       RCDFMT(ME001435F0)                           */                 /*MD023074*/
/**********  DLYJOB     DLY(5)                                       */                 /*MD023074*/
             SNDRCVF    RCDFMT(ME001435F0)                                              /*MD023074*/
             IF         COND(&IN03 *EQ '1') THEN(GOTO CMDLBL(ENDCLPGM))                 /*MD023074*/
 LOOP2A:
             SNDRCVF    RCDFMT(ME001435F1)
             CHGVAR     VAR(&IN51)   VALUE('0')
             CHGVAR     VAR(&IN52)   VALUE('0')
             CHGVAR     VAR(&IN55)   VALUE('0')
             CHGVAR     VAR(&IN56)   VALUE('0')
             CHGVAR     VAR(&IN65)   VALUE('0')
             CHGVAR     VAR(&S1MSG5)   VALUE(' ')
             CHGVAR     VAR(&S1MSG6)   VALUE(' ')
             CHGVAR     VAR(&S1MSG7)   VALUE(' ')
             IF         COND(&IN03 *EQ '1') THEN(GOTO CMDLBL(ENDCLPGM))
             ENDDO
 
             IF         COND((&S1DTYP *NE 'D ')  +
                          *AND (&S1DTYP *NE 'F')) THEN(DO)
             CHGVAR     VAR(&IN51)  VALUE('1')
/**********  CHGVAR     VAR(&S1MSG5)   VALUE('Valid value is D or F')*/                 /*MD023074*/
             CHGVAR     VAR(&S1MSG5) VALUE('Type of upload required +
                          value must be either ''D'' (Delta) or')                       /*MD023090*/
             CHGVAR     VAR(&S1MSG6) VALUE('''F'' (Full).')                             /*MD023090*/
/**********               value must be either ''D'' (Delta) or     + **/               /*MD023090*/
/**********               ''F'' (Full).')                             **/      /*MD023074 MD023090*/
             GOTO LOOP2A
             ENDDO
 
             IF         COND((&S1ANSW *NE 'Y ')  +
                          *AND (&S1ANSW *NE 'N')) THEN(DO)
             CHGVAR     VAR(&IN52)  VALUE('1')
/**********  CHGVAR     VAR(&S1MSG5)   VALUE('Valid value is Y or N')*/                 /*MD023074*/
             CHGVAR     VAR(&S1MSG5) VALUE('Update the BIC IBAN Plus +
                          value must be either ''Y'' (Yes) or')                         /*MD023090*/
             CHGVAR     VAR(&S1MSG6) VALUE('''N'' (No).')                               /*MD023090*/
/**********               value must be either ''Y'' (Yes) or ''N'' (No).')**/ /*MD023074 MD023090*/
             GOTO LOOP2A
             ENDDO
 
             IF         COND(&S1DTYP *EQ 'F ') THEN(DO)
                   IF         COND((&S1ANSW *NE 'Y')) THEN(DO)
                   CHGVAR     VAR(&IN52)  VALUE('1')
                   CHGVAR     VAR(&S1MSG5)   VALUE('Update database should be Y,  +
                                                 if Full upload is requested')
                   GOTO LOOP2A
                   ENDDO
             ENDDO
 
             CALL       PGM(MEC001426) PARM('4' &S0MSG1 &S0MSG2 &S0MSG3 &S0MSG4 +
                                            &S1ANSW '' '')
 
             IF         COND(&S0MSG1 *NE    ' ') THEN(DO)
             CHGVAR     VAR(&IN56)   VALUE('1')
             CHGVAR     VAR(&IN65)   VALUE('1')
             CHGVAR     VAR(&S1MSG5)   VALUE(&S0MSG1)
             CHGVAR     VAR(&S1MSG6)   VALUE(&S0MSG2)
             CHGVAR     VAR(&S1MSG7)   VALUE('Please Ensure with Midas Suport a correct +
                                            Delivery.')
             GOTO LOOP2A
             ENDDO
 
             CHGVAR     VAR(&IN55)   VALUE('1')
             CHGVAR     VAR(&IN65)   VALUE('1')
 
 /** STEP4 - Uploading the CFT050 Database **/
 
 /** Converting ASCII to EBCDIC **/
             CHGVAR     VAR(&S1MSG7)   VALUE('Converting Files into Interim files')
             SNDF       RCDFMT(ME001435F1)
             DLYJOB     DLY(5)
             CALL       ME001462 PARM('2')
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
             GOTO       ABNOR
             ENDDO
 
 /** If full upload is requested */
             IF         COND(&S1DTYP *EQ 'F ') THEN(DO)
 
             CHGVAR     VAR(&S1MSG7)   VALUE('Clearing PF/MEIBN3PD rebuilt in full Upload')
             CLRPFM     MEIBN3PD
             MONMSG     MSGID(CPF0000)
             SNDF       RCDFMT(ME001435F1)
             DLYJOB     DLY(1)
 
             CHGVAR     VAR(&S1MSG7)   VALUE('Clearing PF/MEIBNSPD rebuilt in full Upload')
             CLRPFM     MEIBNSPD
             MONMSG     MSGID(CPF0000)
             SNDF       RCDFMT(ME001435F1)
             DLYJOB     DLY(1)
 
             ENDDO
 
 /** Re-output Screen for the CFT050 Database Update after the last CLRPFM one **/
             CHGVAR     VAR(&S1MSG7)   VALUE('Uploading Changes using Delta Version Files.')
             SNDF       RCDFMT(ME001435F1)
             DLYJOB     DLY(5)
 
 /** Override File(s) According type of Upload **/
             IF         COND(&S1DTYP *EQ 'D ') THEN(DO)
             OVRDBF     FILE(METIIBPD) TOFILE(METIIBL0)
             OVRDBF     FILE(METIISPD) TOFILE(METIISL0)
             ENDDO
 
             CHGVAR     VAR(&YYYYMMDD) VALUE('20' *CAT &REFDATE)
 
 /** Call SWIFTRef IBAN PLUS DIRECTORY DB Updater **/
             CALL       ME001463 PARM('2' &YYYYMMDD)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
             DLTOVR     FILE(*ALL)
             GOTO       ABNOR
             ENDDO
             DLTOVR     FILE(*ALL)
 
 /** STEP 5 - Amend the old version files **/
 
             CHGVAR     VAR(&S1MSG7)   VALUE('Amending old Database Files, if asked ...')
             SNDF       RCDFMT(ME001435F1)
             DLYJOB     DLY(5)
 
 /** If Delta Version is asked then the unchanged records are not used (Flag = 'U') **/
             IF         COND(&S1DTYP *EQ 'D ') THEN(DO)
             OVRDBF     FILE(MEIBN3L0) SHARE(*NO)
             OPNQRYF    FILE((MEIBN3L0)) QRYSLT('N3UDAT *EQ ' *BCAT  &YYYYMMDD)
 
             CALL       ME001464 PARM('2' &S1ANSW '' '')
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
             GOTO       ABNOR
             ENDDO
 
             DLTOVR     FILE(MEIBN3L0)
             CLOF       OPNID(MEIBN3L0)
             ENDDO
 
 /** STEP6 - Generation of Search indexes **/
 
 /** Call SWIFTRef BANK DIRECTORY PLUS - Indexes  **/
             CALL       ME1430 PARM(&RTCD)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
             GOTO       ABNOR
             ENDDO
 
             CHGVAR     VAR(&S1MSG7)   VALUE('Generation of search indexes ...')
             SNDF       RCDFMT(ME001435F1)
             DLYJOB     DLY(5)
 
 /** Terminate pgm by the clear of all the working files: MEDWXXPD, METXXXPD **/
             CHGVAR     VAR(&S1MSG7)   VALUE('Clearing PF/MEDWIBPD rebuilt in full Upload')
             CLRPFM     MEDWIBPD
             MONMSG     MSGID(CPF0000)
             SNDF       RCDFMT(ME001435F1)
             DLYJOB     DLY(1)
 
             CHGVAR     VAR(&S1MSG7)   VALUE('Clearing PF/MEDWISPD rebuilt in full Upload')
             CLRPFM     MEDWISPD
             MONMSG     MSGID(CPF0000)
             SNDF       RCDFMT(ME001435F1)
             DLYJOB     DLY(1)
 
             CHGVAR     VAR(&S1MSG7)   VALUE('Clearing PF/METIIBPD rebuilt in full Upload')
             CLRPFM     METIIBPD
             MONMSG     MSGID(CPF0000)
             SNDF       RCDFMT(ME001435F1)
             DLYJOB     DLY(1)
 
             CHGVAR     VAR(&S1MSG7)   VALUE('Clearing PF/METIISPD rebuilt in full Upload')
             CLRPFM     METIISPD
             MONMSG     MSGID(CPF0000)
             SNDF       RCDFMT(ME001435F1)
             DLYJOB     DLY(1)
 
             CHGVAR     VAR(&S1MSG7)   VALUE('Clearing PF/METXT2PD rebuilt in full Upload')
             CLRPFM     METXT2PD
             MONMSG     MSGID(CPF0000)
             SNDF       RCDFMT(ME001435F1)
             DLYJOB     DLY(1)
 
             GOTO       ENDCLPGM
 
 ABNOR:
             CHGJOB     SWS(000XXX11)
             ROLLBACK
             MONMSG     MSGID(CPF0000 MCH0000)
 
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          MEC001435 ended abnormally - see job +
                          log') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
             SNDRCVF    RCDFMT(ME001435F2)
 
 ENDCLPGM:
 
             ENDPGM
