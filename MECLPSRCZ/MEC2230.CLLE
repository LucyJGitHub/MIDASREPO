/*********************************************************************/
/*STD    CLPBASE                                                     */
/*EXI *  TEXT('Midas Incoming MX Messages Purge Phase II of II')     */
/*********************************************************************/
/*                                                                   */
/*       Midas     - Message Management Module                       */
/*                                                                   */
/*       MEC2230  - Incoming MX Purge Phase II of II                 */
/*                                                                   */
/*       (c) Finastra International Limited 2021                     */
/*                                                                   */
/*       Last Amend No. CSW122  *CREATE    Date 04Oct21              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CSW122 - SWIFT ISO20022 Changes                             */
/*                                                                   */
/*********************************************************************/

             PGM        PARM(&COMP_NAME &COMP_SEQ)

             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited +
                          2021')

/** Declare variables */

             DCL        VAR(&RTN_CODE) TYPE(*CHAR) LEN(7)
             DCL        VAR(&COMP_NAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&COMP_SEQ) TYPE(*DEC) LEN(5)
             DCL        VAR(&STATUS) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DATA) TYPE(*CHAR) LEN(50)

/** Global Monitor Message */

             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) +
                           EXEC(GOTO ABNORMAL)

/** Send Message to MRUNQ and standard processing parameters */

             SNDPGMMSG  MSG('MEC2230 - Incoming MX Purge Phase II') +
                          TOMSGQ(MRUNQ)
             CHGJOB     SWS(XXXXX000)
             CHGVAR     VAR(&RTN_CODE) VALUE(' ')

/** Start Commitment Control */

             STRCMTCTL  LCKLVL(*ALL) CMTSCOPE(*JOB)

/** Create data LDA in QTEMP  */

             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)

/** Get run status */

             CHGVAR     VAR(&STATUS) VALUE(' ')
             CALL       PGM(CB0160) PARM(&COMP_NAME &COMP_SEQ &STATUS)

/** Update status to Active */

             CHGVAR     VAR(&STATUS) VALUE('Y')
             CALL       PGM(CB0150) PARM(&COMP_NAME &COMP_SEQ &STATUS)

/** Call Purge II process  */

             CALL       PGM(ME2220) PARM(&RTN_CODE)
             IF         COND((&RTN_CODE *NE '       ') *AND +
                             (&RTN_CODE *NE 'MIN0098')) THEN(GOTO +
                          CMDLBL(DBERR))

/** Update status to Not Active - component finished */

             CHGVAR     VAR(&STATUS) VALUE('N')
             CALL       PGM(CB0150) PARM(&COMP_NAME &COMP_SEQ &STATUS)

             GOTO       CMDLBL(ENDCLPGM)

/** Report Database errors trapped by program */

 DBERR:
             RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&DATA)
             SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&DATA) +
                          TOPGMQ(*EXT) TOMSGQ(MOPERQ)

/** Abnormal termination processing                       */

ABNORMAL:
             ROLLBACK
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             CHGJOB     SWS(XXXXXX11)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             DMPCLPGM
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)

             SNDPGMMSG  MSG('Program MEC2230 ended abnormally') +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          MEC2230 ended abnormally') MSGTYPE(*ESCAPE)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)

 ENDCLPGM:   RCLRSC     LVL(*CALLER)
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Finastra International Limited')
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)

             ENDPGM
