/*********************************************************************/
/*STD    CLPBASEMOD                                                  */
/*EXI *  TEXT('Midas ME Midas/MMM compression server      *MODULE')  */
/*********************************************************************/
/*                                                                   */
/*       Midas SWIFT Direct Link : Midas/MMM interface               */
/*                                                                   */
/*       MEC8200M - Midas/MMM compression server                     */
/*                                                                   */
/*       (c) Finastra International Limited 2002                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/* Midas Release 4.01 -----------------------------------------------*/
/*       Prev Amend No. CSW025  *CREATE    Date 26Mar02              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CSW025 - Midas Message Manager                              */
/*                                                                   */
/*********************************************************************/
 
             PGM        PARM(&SBMJ &ACCP)
 
             DCL        VAR(&SBMJ) TYPE(*CHAR) LEN(6)
             DCL        VAR(&ACCP) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CNUM) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DTQM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTQC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBTYPE) TYPE(*CHAR) LEN(1)
 
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2002')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Create LDA */
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF0000)
 
/* Initialisation */
             RTVJOBA    TYPE(&JOBTYPE)
             RTVJOBA    NBR(&CNUM) TYPE(&JOBTYPE)
             CHGDTAARA  DTAARA(LDA) VALUE(' ')
             CHGJOB     SWS(00000000)
 
/* Derive data queue names */
             CHGVAR     VAR(&DTQM) VALUE('MSQM' *CAT &SBMJ)
             CHGVAR     VAR(&DTQC) VALUE('MSQC' *CAT &SBMJ)
 
/* Override to alternative logical view if required */
             IF         COND(&ACCP *NE ' ') THEN(OVRDBF +
                          FILE(MGOREFL3) TOFILE(&ACCP))
 
/* Call-bound ME8200M communications program */
             QSYS/CALLPRC PRC(ME8200M) PARM(&DTQM &DTQC)
 
/* Database error processing */
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                ROLLBACK
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                GOTO       CMDLBL(END)
             ENDDO
 
/* Normal end */
             GOTO       CMDLBL(END)
 
/* Abnormal end */
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
             IF         COND(&JOBTYPE = '0') THEN(DO)
               SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                            ME8200 ended abnormally - see job log') +
                            TOMSGQ(MOPERQ)
               MONMSG     MSGID(CPF0000 MCH0000)
             ENDDO
 
/* Delete data queue */
 END:        DLTDTAQ    DTAQ(&DTQM)
             MONMSG     MSGID(CPF0000)
             DLTDTAQ    DTAQ(&DTQC)
             MONMSG     MSGID(CPF0000)
 
             ENDPGM
