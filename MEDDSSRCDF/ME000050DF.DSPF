      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME MT94x Production on Request Screen')          *
      *****************************************************************
      *                                                               *
      *  Midas - Messages Management Module                           *
      *                                                               *
      *  DSPF/ME000050DF - MT94x Production on Request                *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL146A            Date 10Jul13               *
      * Bank Fusion Midas 2.1 Base -----------------------------------*
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      *                 CGL013  *CREATE    Date 25Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL146A - MT940/950 Production in Input Cycle                *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
 
     A                                      REF(MFRFL)
     A                                      CA03(03)
     A**Start of change CGL029
     A**********                            DSPSIZ(24 80 *DS3)                                CGL029
     A                                      DSPSIZ(*DS4)                                      CGL029
     A**End of change CGL029
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
 
      *-------------------------
      ** Header
      *-------------------------
 
     A          R ME000050F0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'ME000050'
     A  26                              1 30'MT941/942 Production on Request'
     A                                      DSPATR(HI)
     A  29                              1 30'MT941 Production on Request'
     A                                      DSPATR(HI)
     A N29                              1 30'MT942 Production on Request'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            #0TIME    R        O  2 73REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
 
      *-------------------------
      ** Subfile Record Format
      *-------------------------
 
     A          R ME000050S0
     A                                      SFL
     A  84                                  SFLNXTCHG
 
     A            #0NATY         1   H      TEXT('Network Account Type')
 
     A            #0ANAM        20   H      TEXT('Account Name')
 
     A            #0ACTN         1   B 11  4TEXT('Action Code')
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(HI UL)
 
     A            #0NWRK         6   O 11  7TEXT('Network')
     A                                      DSPATR(HI)
 
     A**Start of change CGL029
     A**********  #0ACCT        18   O 11 15TEXT('Account Number')                            CGL029
     A            #0ACCT        24   O 11 15TEXT('Account Number')                            CGL029
     A**End of change CGL029
     A                                      DSPATR(HI)
 
     A**Start of change CGL029
     A**********  #0ACNO        10   O 11 35TEXT('Retail Account')                            CGL029
     A            #0ACNO        10   O 11 41TEXT('Retail Account')
     A**End of change CGL029
     A                                      DSPATR(HI)
 
     A**Start of change CGL029
     A**********  #0DSTN        11   O 11 47TEXT('MT94x Destination')                         CGL029
     A            #0DSTN        11   O 11 53TEXT('MT94x Destination')
     A**End of change CGL029
     A                                      DSPATR(HI)
 
     A**Start of change CGL029
     A**********  #0MTPY         3   O 11 62TEXT('Message Type')                              CGL029
     A            #0MTPY         3   O 11 68TEXT('Message Type')
     A**End of change CGL029
     A                                      DSPATR(HI)
 
     A                                 12  7'Last Statement Date:'
     A            #0LASD         7   O 12 28TEXT('Last Statement Date')
     A                                      DSPATR(HI)
 
     A                                 12 37'Time:'
     A            #0LAST         8   O 12 43TEXT('Last Statement Time')
     A                                      DSPATR(HI)
 
     A                                 12 53'Page:'
     A            #0LASP         5   O 12 59TEXT('Last Statement Page')
     A                                      DSPATR(HI)
 
      *-------------------------
      ** Subfile Control
      *-------------------------
 
     A          R ME000050C0
     A                                      SFLCTL(ME000050S0)
     A                                      SFLPAG(05) SFLSIZ(06)
     A                                      OVERLAY
     A  80                                  SFLCLR
     A  81                                  SFLDSP
     A                                      SFLDSPCTL
     A N80 81 82                            SFLEND
     A N82                                  ROLLUP(31)
     A  26                                  CA05(05)
     A*/COPY OFCPYSRCZ,CGL146_065                                                            CGL146A
 
     A            #0RLRN         4  0H      SFLRCDNBR
     A                                  4  2'Select on:'
 
     A                                  4 14'Network'
     A            #0NETW         6   B  4 22TEXT('Network')
     A                                      DSPATR(UL HI)
 
     A                                  5 14'Branch'
     A            #0BRCA         3   B  5 22TEXT('Branch')
     A                                      DSPATR(UL HI)
 
     A                                  5 27'Customer'
     A            #0CNUM         6   B  5 36TEXT('Customer')
     A                                      DSPATR(UL HI)
 
     A                                  5 43'Currency'
     A            #0CCY          3   B  5 52TEXT('Currency')
     A                                      DSPATR(UL HI)
 
     A                                  5 57'A/c Code'
     A**Start of change CGL029
     A**********  #0ACCD         4   B  5 66TEXT('Account Code')                              CGL029
     A            #0ACCD        10   B  5 66TEXT('Account Code')                              CGL029
     A**End of change CGL029
     A                                      DSPATR(UL HI)
 
     A**Start of change CGL029
     A**********                        5 71'A/c Seq'                                         CGL029
     A**********  #0ACSQ         2   B  5 79TEXT('Account Sequence')                          CGL029
     A                                  5 77'A/c Seq'                                         CGL029
     A            #0ACSQ         2   B  5 85TEXT('Account Sequence')                          CGL029
     A**End of change CGL029
     A                                      DSPATR(UL HI)
 
     A                                  6 14'Retail account'
     A            #0REAC        10   B  6 29TEXT('Retail Account')
     A                                      DSPATR(UL HI)
 
     A                                  7 14'Message Type'
     A            #0MSGT         3   B  7 27TEXT('Message Type')
     A                                      DSPATR(UL HI)
     A*/COPY OFCPYSRCZ,CGL146_067                                                            CGL146A
 
     A                                  7 32'Destination'
     A            #0DEST        11   B  7 44TEXT('MT94x Destination')
     A                                      DSPATR(UL HI)
 
     A                                  9  7'Network'
     A            #0POS1         6   B 10  7TEXT('Network')
     A                                      DSPATR(UL HI)
 
     A                                  9 15'Account Number'
     A**Start of change CGL029
     A**********  #0POS2        18   B 10 15TEXT('Account Number')                            CGL029
     A            #0POS2        24   B 10 15TEXT('Account Number')                            CGL029
     A**End of change CGL029
     A                                      DSPATR(UL HI)
 
     A**Start of change CGL029
     A**********                        9 35'Retail'                                          CGL029
     A                                  9 41'Retail'
     A**End of change CGL029
 
     A**Start of change CGL029
     A**********                        9 47'Destination'                                     CGL029
     A**********  #0POS3        11   B 10 47TEXT('Destination')                               CGL029
     A                                  9 53'Destination'
     A            #0POS3        11   B 10 53TEXT('Destination')                               CGL029
     A**End of change CGL029
     A                                      DSPATR(UL HI)
 
     A**Start of change CGL029
     A**********                        9 60'Msg Type'                                        CGL029
     A                                  9 68'Msg Type'
     A**End of change CGL029
 
      *------------------
      ** Record Details
      *------------------
 
     A          R ME000050F1
     A                                      OVERLAY
     A  28                                  CA08(08)
     A                                      CA12(12)
 
     A            #1MTPY         3   H      TEXT('Message Type')
 
     A                                  5  3'Network:'
     A            #1NWRK         6   O  5 12TEXT('Network')
     A                                      DSPATR(HI)
 
     A                                  5 21'Account no.:'
     A**Start of change CGL029
     A**********  #1ACCT        22   O  5 34TEXT('Account Number')                            CGL029
     A            #1ACCT        28   O  5 34TEXT('Account Number')                            CGL029
     A**End of change CGL029
     A                                      DSPATR(HI)
 
     A**Start of change CGL029
     A**********                        5 59'Retail:'                                         CGL029
     A**********  #1ACNO        10   O  5 67TEXT('Retail Account')                            CGL029
     A                                  5 65'Retail:'
     A            #1ACNO        10   O  5 73TEXT('Retail Account')
     A**End of change CGL029
     A                                      DSPATR(HI)
 
     A                                  6  3'Type:'
     A                                      DSPATR(ND)
     A            #1NATY         1A  O  6  9TEXT('Network Account Type')
     A                                      DSPATR(ND)
     A                                  6 12'Name:'
     A            #1ANAM        20A  O  6 18TEXT('Account Name')
     A                                      DSPATR(HI)
     A                                  6 41'Destination:'
     A            #1DSTN        11A  O  6 54TEXT('MT94x Destination')
     A                                      DSPATR(HI)
 
     A                                  8 10'Network:'
     A            #1NETW         6   B  8 20TEXT('Network')
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A  28                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
 
     A                                 10 10'Field 21: Related Reference'
     A            #1RREF        16   B 11 10TEXT('Related Reference')
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A  28                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
 
     A                                 13 10'Field 86:'
     A                                 13 20'Information to the Account Owner'
     A            #1MIO1        65   B 14 10TEXT('Info to the a/c owner 1')
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(HI)
     A  28                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
     A            #1MIO2        65   B 15 10TEXT('Info to the a/c owner 2')
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(HI)
     A  28                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
     A            #1MIO3        65   B 16 10TEXT('Info to the a/c owner 3')
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(HI)
     A  28                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
     A            #1MIO4        65   B 17 10TEXT('Info to the a/c owner 4')
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(HI)
     A  28                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
     A            #1MIO5        65   B 18 10TEXT('Info to the a/c owner 5')
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(HI)
     A  28                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
     A            #1MIO6        65   B 19 10TEXT('Info to the a/c owner 6')
     A  48                                  DSPATR(RI PC)
     A N48                                  DSPATR(HI)
     A  28                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
 
      *------------------------
      ** Footer
      *------------------------
 
     A          R ME000050F2
     A                                      OVERLAY
     A  26                             22  2'X=Request Message'
     A                                 23  2'F3=Main Menu'
     A*/COPY OFCPYSRCZ,CGL146_004                                                            CGL146A
     A  26                             23 17'F5=Refresh   Rollup/Rolldown'
     A*/COPY OFCPYSRCZ,CGL146_066                                                            CGL146A
     A  27                             23 17'F12=Previous'
     A  28                             23 17'F8=Submit Request   F12=Previous'
 
      *-------------------------
      ** Message Record Format
      *-------------------------
 
     A          R ME000050S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
 
      *-------------------------
      ** Message Control
      *-------------------------
 
     A          R ME000050C1                TEXT('Program Messages')
     A                                      SFLCTL(ME000050S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
