      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME MT941/MT942 Production Monitor')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  DSPF/ME000055DF - MT941/MT942 Production Monitor             *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. 245711             Date 16Mar07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL013  *CREATE    Date 16Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  245711 - Subfile text is missing in Network Production       *
      *           Monitor.                                            *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      CA03(03)
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
      *
      ** Header
      *
     A          R ME000055F0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'ME000055F0'
      *
     A            #0RUNA    R        O  1 72REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A  21                              2 25'MT941/MT942 Production Monitor'
     A                                      DSPATR(HI)
     A N21                              2 27'Network Production Monitor'
     A                                      DSPATR(HI)
     A                                  2 72TIME
      *
      ** Detail Screen 1
      *
     A          R ME000055F1                OVERLAY
     A                                      PUTOVR
     A                                      CF05
     A                                      CF06
     A                                      CF07
     A                                      CF15
      *
     A                                  5  3'Current Job Status............:'
     A            #0JOBS        20   O  5 35TEXT('Current Job Status')
      *
     A                                  7  3'Running Periodicity...........:'
     A            #0MRPD         2   B  7 35TEXT('Running Periodicity')
     A                                      DSPATR(HI)
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  8  3'Period of Expiring Request....:'
     A            #0PERQ         4   B  8 35TEXT('Period of Expiring +
     A                                      Request')
     A                                      DSPATR(HI)
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 10  5'Current Status:'
      *
     A            #0ACT1         1   B 11  3TEXT('Action Code for +
     A                                      MT941 on Schedule')
     A                                      DSPATR(HI)
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 11  5'MT941 On Schedule...........:'
     A            #0S941         8   O 11 35TEXT('MT941 on Schedule')
      *
     A            #0ACT2         1   B 12  3TEXT('Action Code for +
     A                                      MT942 on Schedule')
     A                                      DSPATR(HI)
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 12  5'MT942 On Schedule...........:'
     A            #0S942         8   O 12 35TEXT('MT942 on Schedule')
      *
     A            #0ACT3         1   B 13  3TEXT('Action Code for +
     A                                      MT941 on Manual Request')
     A                                      DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 13  5'MT941 On Manual Request.....:'
     A            #0M941         8   O 13 35TEXT('MT941 on Manual Request')
      *
     A            #0ACT4         1   B 14  3TEXT('Action Code for +
     A                                      MT942 on Manual Request')
     A                                      DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 14  5'MT942 On Manual Request.....:'
     A            #0M942         8   O 14 35TEXT('MT942 on Manual Request')
      *
     A            #0ACT5         1   B 15  3TEXT('Action Code for +
     A                                      MT941 on MT920 Request')
     A                                      DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 15  5'MT941 On MT920 Request......:'
     A            #0R941         8   O 15 35TEXT('MT941 on MT920 Request')
      *
     A            #0ACT6         1   B 16  3TEXT('Action Code for +
     A                                      MT942 on MT920 Request')
     A                                      DSPATR(HI)
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 16  5'MT942 On MT920 Request......:'
     A            #0R942         8   O 16 35TEXT('MT942 on MT920 Request')
     A*
     A** Subfile Record Format - Detail Screen 2
     A*
     A          R ME000055S0                SFL
     A  23                                  SFLNXTCHG
     A            #0SELE         1A  B  7  2TEXT('Action Code')
     A                                      DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(UL)
      ** Start of Change 245711
     A**********                        7  4'Network:'
     A            #DNWRK         8A  O  7  4TEXT('Network')
      ** End of Change 245711
     A            #0NWRK         6A  O  7 14TEXT('Network')
      ** Start of Change 245711
     A**********                        7 24'Status:'
     A            #DSTAT         7A  O  7 24TEXT('Status')
      ** End of Change 245711
     A            #0STAT         8A  O  7 33TEXT('Status')
      ** Start of Change 245711
     A**********                        7 43'No. of requests...........:'
     A            #DREQS        27A  O  7 43TEXT('No. of requests...........:')
      ** End of Change 245711
     A            #0REQS         6Y 0O  7 72TEXT('No. of Requests')
     A                                      EDTCDE(3)
      ** Start of Change 245711
     A**********                        8  4'No. of waiting requests...:'
     A            #DWAIT        27A  O  8  4TEXT('No. of waiting requests...:')
      ** End of Change 245711
     A            #0WAIT         6Y 0O  8 33TEXT('No. of Waiting Requests')
     A                                      EDTCDE(3)
      ** Start of Change 245711
     A**********                        8 43'No. of duplicated requests:'
     A            #DDUPR        27A  O  8 43TEXT('No. of duplicated requests:')
      ** End of Change 245711
     A            #0DUPR         6Y 0O  8 72TEXT('No. of duplicated requests')
     A                                      EDTCDE(3)
      ** Start of Change 245711
     A**********                        9  4'No. of expired requests...:'
     A            #DEXPR        27A  O  9  4TEXT('No. of expired requests...:')
      ** End of Change 245711
     A            #0EXPR         6Y 0O  9 33TEXT('No. of Expired Requests')
     A                                      EDTCDE(3)
      ** Start of Change 245711
     A**********                        9 43'No. of held requests......:'
     A            #DHLDR        27A  O  9 43TEXT('No. of held requests......:')
      ** End of Change 245711
     A            #0HLDR         6Y 0O  9 72TEXT('No. of duplicated requests')
     A                                      EDTCDE(3)
      ** Start of Change 245711
     A**********                       10  4'No. of submitted requests.:'
     A            #DSUBM        27A  O 10  4TEXT('No. of submitted requests.:')
      ** End of Change 245711
     A            #0SUBM         6Y 0O 10 33TEXT('No. of submitted requests')
     A                                      EDTCDE(3)
      ** Start of Change 245711
     A**********                       10 43'No. of requests in error..:'
     A            #DERRO        27A  O 10 43TEXT('No. of requests in error..:')
      ** End of Change 245711
     A            #0ERRO         6Y 0O 10 72TEXT('No. of requests in error')
     A                                      EDTCDE(3)
      ** Start of Change 245711
     A**********                       11  4'No. of generated requests.:'
     A            #DGENR        27A  O 11  4TEXT('No. of generated requests.:')
      ** End of Change 245711
     A            #0GENR         6  0O 11 33TEXT('No. of generated request')
     A                                      EDTCDE(3)
      ** Start of Change 245711
     A**********                       11 43'No. of req with "No Movts":'
     A            #DMOVT        27A  O 11 43TEXT('No. of req with "No Movts":')
      ** End of Change 245711
     A            #0MOVT         6Y 0O 11 72TEXT('No. of req with "No Movt"')
     A                                      EDTCDE(3)
     A                                 12  2' '
      *
      ** Subfile Control
      *
     A          R ME000055C0                SFLCTL(ME000055S0)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0002)
     A  50                                  SFLCLR
     A N50                                  SFLDSPCTL
     A N50 53                               SFLDSP
     A N50 51 53                            SFLEND(*MORE)
     A N51                                  ROLLUP(54 'Next Page')
     A                                      CF05
     A                                      CF15
      *
     A            #0SFRN         4S 0H      SFLRCDNBR
      *
     A                                  5  4'Position on Network....:'
     A            #0POSN         6   B  5 32TEXT('Position on Network')
     A                                      DSPATR(HI UL)
      *
      ** Footer
      *
     A          R ME000055F2                OVERLAY
     A                                      PUTOVR
      *
     A  21                             22  3'A=Activate D=Deactivate +
     A                                      F3=Main Menu F5=Refresh'
     A  21                             23  3'F6=Activate Job  F7=Deactivate +
     A                                      Job  F15=Network Monitor'
     A N21                             22  3'A=Activate Network    D=Deactivate'
     A N21                             23  3'F3=Main Menu    F5=Refresh     +
     A                                      F15=MT941/MT942 Monitor  +
     A                                      Rollup/Rolldown'
      *
      ** Message Subfile
      *
     A          R ME000055S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #0MSGK                    SFLMSGKEY
     A            #0PGMQ                    SFLPGMQ
      *
      ** Message Subfile Control
      *
     A          R ME000055C1                TEXT('Program messages')
     A                                      SFLCTL(ME000055S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #0PGMQ                    SFLPGMQ
