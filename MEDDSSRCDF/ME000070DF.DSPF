      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Movements Enq by Netwk A/c & Message Type')
      *****************************************************************
      *                                                               *
      *  Midas - Messages Management Module                           *
      *                                                               *
      *  ME000070DF - Movements Enquiry by Network Account and        *
      *               Message Type                                    *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CGL013  *CREATE    Date 04Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      *
     A                                      CF03
     A                                      CF05
      *
      *---------------------------------------------------------------*
      * 1st Screen: Netwk A/c & Msg Type Direct Selection - 1 Format  *
      *---------------------------------------------------------------*
      *
     A          R ME000070F0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A                                      CF15
      *
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                  1  2USER
     A                                  1 13'ME000070'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 24'Movements Enquiry by Message Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
      *
     A                                 16  2'F3=Exit'
     A                                 16 13'F5=Refresh'
     A                                 16 27'F15=Select'
      *
     A                                 18  2'Or Enter:'
      *
     A                                 20  5'Network:'
     A            F0NWRK         6A  B 20 15TEXT('Network')
     A                                      OVRDTA
     A N29                                  DSPATR(HI)
     A  29                                  DSPATR(RI)
     A  29                                  DSPATR(PC)
     A                                 20 51'Posting Date:'
     A            F0PSTD         6A  B 20 65OVRDTA
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
      *
     A                                 21  5'Account:'
     A            F0CNUM        10A  B 21 15TEXT('Customer Number')
     A                                      OVRDTA
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A            F0CCY          3A  B 21 26TEXT('Currency')
     A                                      OVRDTA
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A**Start of change CGL029
     A**********  F0ACCD         4A  B 21 30TEXT('Account Code')                              CGL029
     A            F0ACCD        10A  B 21 30TEXT('Account Code')
     A**End of change CGL029
     A                                      OVRDTA
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A**Start of change CGL029
     A**********  F0ACSQ         2A  B 21 35TEXT('Account Sequence')                          CGL029
     A            F0ACSQ         2A  B 21 41TEXT('Account Sequence')
     A**End of change CGL029
     A                                      OVRDTA
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A**Start of change CGL029
     A**********  F0BRCA         3A  B 21 38TEXT('Branch Code')                               CGL029
     A            F0BRCA         3A  B 21 44TEXT('Branch Code')
     A**End of change CGL029
     A                                      OVRDTA
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A                                 21 51'Destination:'
     A            F0DSTN        11A  B 21 65OVRDTA
     A N28                                  DSPATR(HI)
     A  28                                  DSPATR(RI)
     A  28                                  DSPATR(PC)
      *
      ** N.B.: The following constant description should be changed once
      **       the network account type will no more be hidden
     A                                 22  5'A/c Type:'
     A                                      DSPATR(ND)
      ** N.B.: The following description of F0NATY should be changed once
      **       the network account type will no more be hidden
     A            F0NATY         1A  B 22 15TEXT('Network Account Type')
     A                                      OVRDTA
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
      *N31                                  DSPATR(HI)
      * 31                                  DSPATR(RI PC)
     A                                 22 51'Message type:'
     A            F0MGTP         3A  B 22 65OVRDTA
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
      *
      *---------------------------------------------------------------*
      * 2nd Screen: Netwk A/c & Msg Type Selection Subfile - 3 Formats*
      *---------------------------------------------------------------*
      *
     A          R ME000070S1                SFL
      *
     A  45                                  SFLNXTCHG
      *
     A            S1ATYP         1A  H      TEXT('Account type')
     A            S1LDBL        15S 0H      TEXT('Ledger Balance')
     A            S1CLBL        15S 0H      TEXT('Cleared Balance')
     A            S1ANAM        20A  H      TEXT('Account Name')
      *
     A            S1SELE         1A  B  8  3TEXT('Select Option')
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
     A            S1NWRK         6A  O  8  6TEXT('Network')
     A            S1BRCA         3A  O  8 14TEXT('Account Branch')
     A            S1CNUM         6A  O  8 18TEXT('Account Customer')
     A            S1CCY          3A  O  8 25TEXT('Account Currency')
     A**Start of change CGL029
     A**********  S1ACCD         4A  O  8 29TEXT('Account Code')                              CGL029
     A**********  S1ACSQ         2A  O  8 34TEXT('Account Sequence')                          CGL029
     A            S1ACCD        10A  O  8 29TEXT('Account Code')
     A            S1ACSQ         2A  O  8 40TEXT('Account Sequence')
     A**End of change CGL029
      *
      ** N.B.: The following description of S1NATY should be changed once
      **       the network account type will no more be hidden
     A**Start of change CGL029
     A**********  S1NATY         1A  O  8 38TEXT('Account Type')                              CGL029
     A            S1NATY         1A  O  8 43TEXT('Account Type')
     A**End of change CGL029
     A                                      DSPATR(ND)
     A**Start of change CGL029
     A**********  S1ACNO        10A  O  8 41TEXT('Retail Number')                             CGL029
     A**********  S1DSTN        11A  O  8 52TEXT('MT94x Destination')                         CGL029
     A            S1ACNO        10A  O  8 45TEXT('Retail Number')
     A            S1DSTN        11A  O  8 56TEXT('MT94x Destination')
     A**End of change CGL029
     A            S1G950         1A  O  8 66DSPATR(ND)
     A            S1G940         1A  O  8 70
     A            S1G941         1A  O  8 74
     A            S1G942         1A  O  8 78
      *
     A          R ME000070C1                SFLCTL(ME000070S1)
      *
     A N53                                  ROLLUP(51 'Next page')
     A                                      CF12
      *
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A  50                                  SFLDSP
     A  52                                  SFLDSPCTL
     A N52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
      *
     A            C1SFRN         4S 0H      SFLRCDNBR
      *
     A                                  1  2USER
     A                                  1 13'ME000070'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 24'Movements Enquiry by Message Type'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
      *
     A                                  4 70'Generates'
      *
     A                                  5  2'Sel'
     A                                  5  6'Network'
     A                                  5 14'Account number'
      *
      ** N.B.: The following constant description should be changed once
      **       the network account type will no more be hidden
     A                                  5 37'Typ'
     A                                      DSPATR(ND)
     A**Start of change CGL029
     A**********                        5 41'Retail No.'                                      CGL029
     A**********                        5 52'Destination'                                     CGL029
     A                                  5 45'Retail No.'
     A                                  5 56'Destination'
     A**End of change CGL029
     A                                  5 69'940 941 942'
      *
     A            C1NWRK         6A  B  6  6TEXT('Network')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            C1BRCA         3A  B  6 14TEXT('Account Branch')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            C1CNUM         6A  B  6 18TEXT('Account Customer')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A            C1CCY          3A  B  6 25TEXT('Account Currency')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  C1ACCD         4A  B  6 29TEXT('Account Code')                              CGL029
     A            C1ACCD        10A  B  6 29TEXT('Account Code')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  C1ACSQ         2A  B  6 34TEXT('Account Sequence')                          CGL029
     A            C1ACSQ         2A  B  6 40TEXT('Account Sequence')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      ** N.B.: The following description of C1NATY should be changed once
      **       the network account type will no more be hidden
     A**Start of change CGL029
     A**********  C1NATY         1A  B  6 38TEXT('Account Type')                              CGL029
     A            C1NATY         1A  B  6 43TEXT('Account Type')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A**Start of change CGL029
     A**********  C1ACNO        10A  B  6 41TEXT('Retail Number')                             CGL029
     A            C1ACNO        10A  B  6 45TEXT('Retail Number')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  C1DSTN        11A  B  6 52TEXT('MT94x Destination')                         CGL029
     A            C1DSTN        11A  B  6 56TEXT('MT94x Destination')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A          R ME000070F1
      *
     A                                      BLINK
     A                                      OVERLAY
      *
     A                                 22  2'Selection:'
     A                                 22 13'0=MT940'
     A                                 22 24'1=MT941'
     A                                 22 35'2=MT942'
     A                                 23  2'F3=Exit'
     A                                 23 13'F5=Refresh'
     A                                 23 27'F12=Cancel'
      *
      *---------------------------------------------------------------*
      * 3rd Screen: Movements (until Today) Enquiry - 3 Formats       *
      *---------------------------------------------------------------*
      *
     A          R ME000070S2                SFL
      *
     A            S2PSTD         7A  O 12  3TEXT('Posting Date')
     A            S2VALD         7A  O 12 12TEXT('Value Date')
     A            S2PNAR        30A  O 12 21TEXT('Posting Narrative')
     A            S2PSTA        21A  O 12 53TEXT('Posting Amount')
     A            S2DRCR         2A  O 12 76TEXT('Debit/Credit')
      *
     A          R ME000070C2                SFLCTL(ME000070S2)
      *
     A N53                                  ROLLUP(51 'Next page')
     A  11                                  CF11
     A                                      CF12
      *
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A  50                                  SFLDSP
     A  52                                  SFLDSPCTL
     A N52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
      *
     A            C2SFRN         4S 0H      SFLRCDNBR
      *
     A                                  1  2USER
     A                                  1 13'ME000070'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 21'Movements Enquiry by Message Type:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            C2MGTP         5A  O  2 56TEXT('Message Type')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
      *
     A                                  4  2'Network:'
     A            C2NWRK         6A  O  4 11TEXT('Network')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********                        4 22'Account no:'                                     CGL029
     A**********  C2BRCA         3A  O  4 34TEXT('Account Branch')                            CGL029
     A                                  4 19'Account no:'
     A            C2BRCA         3A  O  4 31TEXT('Account Branch')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  C2CNUM         6A  O  4 38TEXT('Account Customer')                          CGL029
     A            C2CNUM         6A  O  4 35TEXT('Account Customer')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  C2CCY          3A  O  4 45TEXT('Account Currency')                          CGL029
     A            C2CCY          3A  O  4 42TEXT('Account Currency')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  C2ACCD         4A  O  4 49TEXT('Account Code')                              CGL029
     A            C2ACCD        10A  O  4 46TEXT('Account Code')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  C2ACSQ         2A  O  4 54TEXT('Account Sequence')                          CGL029
     A            C2ACSQ         2A  O  4 57TEXT('Account Sequence')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  4 62'Retail:'
     A            C2ACNO        10A  O  4 70TEXT('Retail Number')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  5  2'Account Name:'
     A            C2ANAM        20A  O  5 16TEXT('Account Name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 37'Destination:'
     A            C2DSTN        11A  O  5 50TEXT('MT94x Destination')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      ** N.B.: The following constant description should be changed once
      **       the network account type will no more be hidden
     A                                  5 62'Network Ac Type:'
     A                                      DSPATR(ND)
      *
      ** N.B.: The following description of C2NATY should be changed once
      **       the network account type will no more be hidden
     A            C2NATY         1A  O  5 79TEXT('Account Type')
     A                                      OVRDTA
     A                                      DSPATR(ND)
      *
     A  12                              6  2'Opening Balance:'
     A  12        C2BALA        21A  O  6 19TEXT('Opening Balance')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  12                              6 42'Closing Balance:'
     A  12        C2CLOS        21A  O  6 59TEXT('Closing Balance')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A  15                              7 40'Available Balance:'
     A  15        C2AVBL        21A  O  7 59TEXT('Available Balance')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  8  3'Posting'
     A                                  8 13'Value'
     A                                  8 21'Posting'
      *
     A                                  9  3'Date'
     A                                  9 13'Date'
     A                                  9 21'Narrative'
     A                                  9 53'Amount'
     A                                  9 74'DB/CR'
      *
     A            C2PSTD         6A  B 10  3TEXT('Posting Date')
     A                                      OVRDTA
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
      *
     A          R ME000070F2
      *
     A                                      BLINK
     A                                      OVERLAY
      *
     A                                 23  2'F3=Exit'
     A                                 23 13'F5=Refresh'
     A  11                             23 27'F11=Forward Balances'
     A                                 23 51'F12=Cancel'
      *
      *---------------------------------------------------------------*
      * 4th Screen: Forward Balances Enquiry - 3 Formats              *
      *---------------------------------------------------------------*
      *
     A          R ME000070S3                SFL
      *
     A            S3VALD         7A  O 13  3TEXT('Value Date')
     A            S3PSTD         7A  O 13 12TEXT('Posting Date')
     A            S3PNAR        30A  O 13 21TEXT('Posting Narrative')
     A            S3PSTA        21A  O 13 53TEXT('Posting Amount')
     A            S3DRCR         2A  O 13 76TEXT('Debit/Credit')
      *
     A          R ME000070C3                SFLCTL(ME000070S3)
      *
     A N53                                  ROLLUP(51 'Next page')
     A                                      CF12
      *
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A  50                                  SFLDSP
     A  52                                  SFLDSPCTL
     A N52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
      *
     A            C3SFRN         4S 0H      SFLRCDNBR(*TOP)
      *
     A                                  1  2USER
     A                                  1 13'ME000070'
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
      *
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                      OVRDTA
     A                                  2 21'Movements Enquiry by Message Type:'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            C3MGTP         5A  O  2 56TEXT('Message Type')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
      *
     A                                  3 33'Forward Balances'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
      *
     A                                  5  2'Network:'
     A            C3NWRK         6A  O  5 11TEXT('Network')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********                        5 22'Account no:'                                     CGL029
     A**********  C3BRCA         3A  O  5 34TEXT('Account Branch')                            CGL029
     A                                  5 19'Account no:'
     A            C3BRCA         3A  O  5 31TEXT('Account Branch')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  C3CNUM         6A  O  5 38TEXT('Account Customer')                          CGL029
     A            C3CNUM         6A  O  5 35TEXT('Account Customer')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  C3CCY          3A  O  5 45TEXT('Account Currency')                          CGL029
     A            C3CCY          3A  O  5 42TEXT('Account Currency')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  C3ACCD         4A  O  5 49TEXT('Account Code')                              CGL029
     A            C3ACCD        10A  O  5 46TEXT('Account Code')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  C3ACSQ         2A  O  5 54TEXT('Account Sequence')                          CGL029
     A            C3ACSQ         2A  O  5 57TEXT('Account Sequence')
     A**End of change CGL029
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 62'Retail:'
     A            C3ACNO        10A  O  5 70TEXT('Retail Number')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  6  2'Account Name:'
     A            C3ANAM        20A  O  6 16TEXT('Account Name')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  6 37'Destination:'
     A            C3DSTN        11A  O  6 50TEXT('MT94x Destination')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      ** N.B.: The following constant description should be changed once
      **       the network account type will no more be hidden
     A                                  6 62'Network Ac Type:'
     A                                      DSPATR(ND)
      *
      ** N.B.: The following description of C3NATY should be changed once
      **       the network account type will no more be hidden
     A            C3NATY         1A  O  6 79TEXT('Account Type')
     A                                      OVRDTA
     A                                      DSPATR(ND)
      *
     A                                  7  2'Opening Balance:'
     A            C3BALA        21   O  7 19TEXT('Opening Balance')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  7 42'Closing Balance:'
     A            C3CLOS        21   O  7 59TEXT('Closing Balance')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A  15                              8 40'Available Balance:'
     A  15        C3AVBL        21A  O  8 59TEXT('Available Balance')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A                                  9  4'Value'
     A                                  9 12'Posting'
     A                                  9 21'Posting'
      *
     A                                 10  4'Date'
     A                                 10 12'Date'
     A                                 10 21'Narrative'
     A                                 10 53'Amount'
     A                                 10 74'DB/CR'
      *
     A            C3VALD         6A  B 11  3TEXT('Posting Date')
     A                                      OVRDTA
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
      *
     A          R ME000070F3
      *
     A                                      BLINK
     A                                      OVERLAY
      *
     A                                 23  2'F3=Exit'
     A                                 23 13'F5=Refresh'
     A                                 23 27'F12=Cancel'
      *
      *---------------------------------------------------------------*
      * Error Messages - Displayed on all Screens - 2 Formats         *
      *---------------------------------------------------------------*
      *
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program Message Subfile')
      *
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PSPROCPGM                 SFLPGMQ(10)
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Msg Subfile Control')
      *
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  97
     AON97                                  SFLEND
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
      *
     A            PSPROCPGM                 SFLPGMQ(10)
      *****************************************************************
