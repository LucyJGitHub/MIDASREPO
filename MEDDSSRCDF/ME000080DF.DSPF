      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Incoming MT920 Enquiry')
      *****************************************************************
      *                                                               *
      *   Midas - Message Management Module                           *
      *                                                               *
      *  ME000080DF - Incoming MT920 Enquiry                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL013  *CREATE    Date 10Jun02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03
 
     A          R ME000080F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      OVERLAY
 
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'ME000080DF'
     A            #0RUND    R        O  1 74REFFLD(RUNA)
 
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 35'Incoming MT920s'
     A                                      DSPATR(HI)
     A                                  2 73TIME
 
     A          R ME000080S0                TEXT('Enquiry Screen Subfile')
     A                                      SFL
 
     A            #0ACTN         1A  B  9  2TEXT('Action Code')
     A  10                                  DSPATR(HI)
     A N10                                  DSPATR(ND PR)
     A            #0IMIR        28A     9  5TEXT('Incoming MIR')
     A                                      DSPATR(HI)
     A            #0DATE         7A     9 40TEXT('Received Date')
     A                                      DSPATR(HI)
     A            #0SLSH         1A     9 48TEXT('Slash')
     A                                      DSPATR(HI)
     A            #0TIME         5A     9 50TEXT('Received Time')
     A                                      DSPATR(HI)
     A            #0MSGT         3A    10 18TEXT('Message Type')
     A                                      DSPATR(HI)
     A            #0ACCT        35A    10 25TEXT('Account Requested')
     A                                      DSPATR(HI)
     A            #0MSGG         3A    11 18TEXT('Msg Generated')
     A                                      DSPATR(HI)
     A            #0OTRN        16A    11 25TEXT('Outgoing TRN')
     A                                      DSPATR(HI)
     A            #0NARR        35A    11 45TEXT('Reason why MT995')
     A                                      DSPATR(HI)
 
     A          R ME000080C0                TEXT('Enquiry Screen Control')
     A                                      SFLCTL(ME000080S0)
     A                                      OVERLAY BLINK PUTOVR
     A  02                                  SFLDSP
     A  03                                  SFLDSPCTL
     A  07                                  SFLEND
     A  01                                  SFLCLR
     A                                      SFLSIZ(5)
     A                                      SFLPAG(4)
     A N07                                  ROLLUP(04)
 
     A                                  4 10'Incoming M.I.R.'
     A                                  4 43'Date / Time Received'
     A                                  5  5'Requested:'
     A                                  5 16'Msg Type'
     A                                  5 28'Account'
     A                                  6  5'Generated:'
     A                                  6 16'Msg Type'
     A                                  6 28'Outgoing T.R.N.'
     A                                  6 45'Narrative'
     A            #1IMIR        28A  I  7  5TEXT('Incoming MIR')
     A            #1DATE         7A  I  7 40TEXT('Received Date')
     A            #1SLSH         1A  O  7 48TEXT('Slash')
     A            #1TIME         5A  I  7 50TEXT('Received Time')
     A                                      CHECK(LC)
 
     A          R ME000080F1                TEXT('Enquiry Screen Footer')
     A                                      OVERLAY
 
     A                                 24  2'F3=Exit'
     A  06                             24 11'E=Enquire'
     A  06                             24 22'Rollup/Rolldown'
 
     A          R ME000080F2                TEXT('No Record to Display')
     A                                      OVERLAY
 
     A                                 12 26'*** NO DATA TO DISPLAY ***'
     A                                      DSPATR(HI)
