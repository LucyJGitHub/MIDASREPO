      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas SWIFTRef BANK DIRECTORY PLUS Loader')            *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  ME001425DF - SWIFTRef BANK DIRECTORY PLUS Loader             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. CFT050  *CREATE    Date 03Jun13               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CFT050 - SWIFTRef Directories upload                         *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      INDARA
      ** First screen
     A          R ME001425F0
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2013')
     A                                      CA03(03 'Exit.')
     A                                 23  2'F3=Exit'
     A            S0USR         10A  O  1  2TEXT('*USER')
     A                                      DSPATR(HI)
     A                                  1 13'ME001425'
     A                                      DSPATR(HI)
     A            S0MRDT         7A  O  1 71TEXT('Midas run date')
     A                                      DSPATR(HI)
     A            S0JOB         10A  O  2  2TEXT('*JOB')
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A                                  4 14'This option will generate BANK DIR-
     A                                      ECTORY PLUS Upload.'
     A                                      COLOR(BLU)
     A                                 12 14'Press Enter to continue or F3 to e-
     A                                      xit without update'
     A  60        S0MSG1        60   O 14 14DSPATR(HI)
     A  61        S0MSG2        60   O 15 14DSPATR(HI)
     A  62        S0MSG3        60   O 16 14DSPATR(HI)
     A  63        S0MSG4        60   O 17 14DSPATR(HI)
     A                                  7 14'Please ensure that:'
     A                                  8 20'* B3_YYMMDD AND CT_YYMMDD (YYMMDD -
     A                                      = the Release Date)'
     A                                  9 22'contain the FTP''D'
     A                                  9 40'Data to upload.'
     A                                 10 20'* CU_YYMMDD, HL_YYMMDD and TZ_YYMM-
     A                                      DD may be also provided.'
     A                                  2 28'BANK DIRECTORY PLUS Loader'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  61 62     S0MSG5A       60   O 18 14COLOR(YLW)
     A  64        S0MSG6A       60   O 22 14COLOR(RED)
      ** Second screen
     A          R ME001425F1
     A                                      TEXT('Information Record')
     A                                      CA03(03 'Exit.')
     A                                 23  2'F3=Exit'
     A            S1USR         10A  O  1  2TEXT('*USER')
     A                                      DSPATR(HI)
     A                                  1 13'ME001425'
     A                                      DSPATR(HI)
     A            S1MRDT         7A  O  1 71TEXT('Midas run date')
     A                                      DSPATR(HI)
     A            S1JOB         10A  O  2  2TEXT('*JOB')
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A                                  4 13'This program will update the curre-
     A                                      nt SWIFTRef BANK DIRECTORY'
     A                                  5 13'Plus Database. Press F3 to exit wi-
     A                                      thout update'
     A                                  7 13'The data to be loaded has a releas-
     A                                      e date of:'
     A            S1DATE         6A  O  7 60DSPATR(HI)
     A                                  9 13'The process can perform a Delta or-
     A                                       a Full version of the'
     A                                 10 13'upload of the SWIFTRef BANK DIRECT-
     A                                      ORY Plus Data.'
     A                                 11 13'Type of upload required  (D/F)   .-
     A                                       . . . . :'
     A            S1DTYP         1A  B 11 60DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A N51                                  DSPATR(UL)
     A                                 13 13'You can also use the BANK DIRECTOR-
     A                                      Y Plus data to update'
     A                                 14 13'the previous versions of directori-
     A                                      es on the iSeries.'
     A                                 15 13'Update the BIC Directory? (Y/N)  .-
     A                                       . . . . :'
     A                                 16 13'Update the BIC Database Plus? (Y/N-
     A                                      )? . . . :'
     A                                 17 13'Update the BIC IBAN Plus? (Y/N)  .-
     A                                       . . . . :'
     A            S1ANSW1        1A  B 15 60DSPATR(HI)
     A  52                                  DSPATR(RI)
     A N52                                  DSPATR(UL)
     A  52                                  DSPATR(PC)
     A            S1ANSW2        1A  B 16 60DSPATR(HI)
     A  53                                  DSPATR(RI)
     A N53                                  DSPATR(UL)
     A  53                                  DSPATR(PC)
     A            S1ANSW3        1A  B 17 60DSPATR(HI)
     A  54                                  DSPATR(RI)
     A N54                                  DSPATR(UL)
     A  54                                  DSPATR(PC)
     A            S1MSG5        60A  O 20 13DSPATR(HI)
     A            S1MSG6        60A  O 21 13DSPATR(HI)
     A                                  2 28'BANK DIRECTORY PLUS Loader'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A  65        S1MSG7        60A  O 22 13
     A  55                                  COLOR(RED)
     A  56                                  COLOR(YLW)
      ** Error message screen
     A          R ME001425F2
     A                                      TEXT('Information Record')
     A            S2USR         10A  O  1  2TEXT('*USER')
     A                                      DSPATR(HI)
     A                                  1 13'ME001425'
     A                                      DSPATR(HI)
     A            S2MRDT         7A  O  1 71TEXT('Midas run date')
     A                                      DSPATR(HI)
     A            S2JOB         10A  O  2  2TEXT('*JOB')
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A                                  2 28'BANK DIRECTORY PLUS Loader'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 10 13'This process has terminated abnorm-
     A                                      ally'
     A                                      COLOR(BLU)
     A  67        S2MSG8        60   O 13 13DSPATR(HI)
     A  68        S2MSG9        60   O 14 13DSPATR(HI)
     A  69        S2MSG10       60A  O 15 13DSPATR(HI)
