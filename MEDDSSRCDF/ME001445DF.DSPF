      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas SWIFTRef SSI PLUS TABLE Loader')                 *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  ME001445DF - SWIFTRef SSI TABLES Loader                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. CFT050  *CREATE    Date 03Jun13               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CFT050 - SWIFTRef Directories upload                         *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
      ** First screen
     A          R ME001445F0
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2013')
     A                                      CA03(03 'Exit.')
     A                                 23  2'F3=Exit'
     A            S0USR         10A  O  1  2TEXT('*USER')
     A                                      DSPATR(HI)
     A                                  1 13'ME001445'
     A                                      DSPATR(HI)
     A            S0MRDT         7A  O  1 71TEXT('Midas run date')
     A                                      DSPATR(HI)
     A            S0JOB         10A  O  2  2TEXT('*JOB')
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A                                  4  9'This option will generate SSI PLUS-
     A                                       Upload.'
     A                                      COLOR(BLU)
     A                                 12  9'Press Enter to continue or F3 to e-
     A                                      xit without update'
     A  60        S0MSG1        60A  O 14  9DSPATR(HI)
     A  61        S0MSG2        60A  O 15  9DSPATR(HI)
     A  62        S0MSG3        60A  O 16  9DSPATR(HI)
     A  63        S0MSG4        60A  O 17  9DSPATR(HI)
     A                                  7  9'Please ensure that SI_YYMMDD conta-
     A                                      ins the FTP''D Data to upload.'
     A  61 62     S0MSG5A       60A  O 18  9COLOR(YLW)
     A  64        S0MSG6A       60A  O 22  9COLOR(RED)
     A                                  2 33'SSI PLUS Loader'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 30'(YYMMDD = the Release Date)'
      ** Second screen
     A          R ME001445F1
     A                                      TEXT('Information Record')
     A                                      CA03(03 'Exit.')
     A                                 23  2'F3=Exit'
     A            S1USR         10A  O  1  2TEXT('*USER')
     A                                      DSPATR(HI)
     A                                  1 13'ME001445'
     A                                      DSPATR(HI)
     A            S1MRDT         7A  O  1 71TEXT('Midas run date')
     A                                      DSPATR(HI)
     A            S1JOB         10A  O  2  2TEXT('*JOB')
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A                                  4 13'This program will update the curre-
     A                                      nt SWIFTRef SSI Plus Database'
     A                                  7 13'The data to be loaded has a releas-
     A                                      e date of:'
     A            S1DATE         6A  O  7 60DSPATR(HI)
     A                                  9 13'The process can perform a Delta or-
     A                                       a Full version of the'
     A                                 10 13'upload of the SWIFTRef SSI Plus Da-
     A                                      ta.'
     A                                 11 13'Type of upload required  (D/F)   .-
     A                                       . . . . :'
     A            S1ANSW1        1A  B 11 60DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A N51                                  DSPATR(UL)
     A            S1MSG5        60A  O 20 13DSPATR(HI)
     A            S1MSG6        60A  O 21 13DSPATR(HI)
     A  65        S1MSG7        60   O 22 13
     A  55                                  COLOR(RED)
     A  56                                  COLOR(YLW)
     A                                  2 33'SSI PLUS Loader'
     A                                      DSPATR(HI)
     A                                  5 13'Press F3 to exit without update.'
      ** Error message screen
     A          R ME001445F2
     A            S2USR         10A  O  1  2TEXT('*USER')
     A                                      DSPATR(HI)
     A                                  1 13'ME001445'
     A                                      DSPATR(HI)
     A            S2MRDT         7A  O  1 71TEXT('Midas run date')
     A                                      DSPATR(HI)
     A            S2JOB         10A  O  2  2TEXT('*JOB')
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A                                 10 13'This process has terminated abnorm-
     A                                      ally'
     A                                      COLOR(BLU)
     A  67        S2MSG8        60A  O 13 13DSPATR(HI)
     A  68        S2MSG9        60A  O 14 13DSPATR(HI)
     A  69        S2MSG10       60A  O 15 13DSPATR(HI)
     A                                  2 33'SSI Plus Loader'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
