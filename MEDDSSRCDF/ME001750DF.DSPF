      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Outgoing Message Field Management')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  DSPF/ME001750DF - Outgoing Message Field Management          *
      *                                                               *
      *  (c) Finastra International Limited 2006                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CSW207             Date 07Jul07               *
      *                 BUG14042           Date 24May07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CFT032  *CREATE    Date 11Sep06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW207 - SWIFT 2007 Standard Changes                         *
      *  BUG14042 - CFT032 errors ; applied fix 13798                 *
      *  CFT032 - Account line in field 50X in MT103                  *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R RSFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2006')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(31 'Invalid: SISOC')
     A                                      INDTXT(32 'Invalid: SISOD')
     A                                      INDTXT(33 'Invalid: SSCHT')
     A                                      INDTXT(34 'Invalid: SCSEL')
     A                                      INDTXT(36 'Invalid: SSEEA')
     A  84                                  SFLNXTCHG
     A            SDBRC         66   H      TEXT('Existing record fields')
     A            SACJN         10   H      TEXT('Action Job Name')
     A            SACUN         10   H      TEXT('Action User Name')
     A            SACTM          6  0H      TEXT('Action Time')
     A            SACDA          7   H      TEXT('Action Date')
     A            SACTY          1   H      TEXT('Action Type')
     A            SCSEL          1   B  8  3
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A  50
     AO 51                                  DSPATR(ND PR)
     A** Start of change BUG14042
     A**********  SOMNWRK        6   B  8  6TEXT('Network')                                 BUG14042
     A            SOMNWRK        6   O  8  6TEXT('Network')
     A** End of change BUG14042
     A N31                                  DSPATR(HI)
     A** Start of change BUG14042
     A**20******                                                                            BUG14042
     A***O*21*****                             DSPATR(PR)                                   BUG14042
     A*N20N31***                                                                            BUG14042
     A***AN21N31**                             DSPATR(UL)                                   BUG14042
     A**31******                            DSPATR(RI PC)                                   BUG14042
     A**********  SOMMTPY        3   B  8 14TEXT('Message type')                            BUG14042
     A            SOMMTPY        3   O  8 14TEXT('Message type')
     A** End of change BUG14042
     A N32                                  DSPATR(HI)
     A** Start of change BUG14042
     A**20******                            DSPATR(PR)                                      BUG14042
     A*N20N32***                            DSPATR(UL)                                      BUG14042
     A**32******                            DSPATR(RI PC)                                   BUG14042
     A**********  SOMMTAG        5   B  8 19TEXT('Message Tag')                             BUG14042
     A            SOMMTAG        5   O  8 19TEXT('Message Tag')
     A** End of change BUG14042
     A N33                                  DSPATR(HI)
     A** Start of change BUG14042
     A**20******                            DSPATR(PR)                                      BUG14042
     A*N20N33***                            DSPATR(UL)                                      BUG14042
     A**33******                            DSPATR(RI PC)                                   BUG14042
     A**********  SOMSBRC        3   B  8 28TEXT('Senders branch')                          BUG14042
     A            SOMSBRC        4   O  8 28TEXT('Senders branch')
     A** End of change BUG14042
     A N36                                  DSPATR(HI)
     A** Start of change BUG14042
     A**20******                                                                            BUG14042
     A***ON37*****                             DSPATR(PR)                                   BUG14042
     A*N20N36***                            DSPATR(UL)                                      BUG14042
     A**36******                            DSPATR(RI PC)                                   BUG14042
     A**********  SOMCTRY        2   B  8 35TEXT('Country of location')                     BUG14042
     A            SOMCTRY        4   O  8 35TEXT('Country of location')
     A** End of change BUG14042
     A N38                                  DSPATR(HI)
     A** Start of change BUG14042
     A**20******                            DSPATR(PR)                                      BUG14042
     A*N20N38***                            DSPATR(UL)                                      BUG14042
     A**38******                            DSPATR(RI)                                      BUG14042
     A**********  SOMCURR        3   B  8 43TEXT('Currency')                                BUG14042
     A            SOMCURR        3   O  8 43TEXT('Currency')
     A** End of change BUG14042
     A N38                                  DSPATR(HI)
     A** Start of change BUG14042
     A**20******                            DSPATR(PR)                                      BUG14042
     A*N20N38***                            DSPATR(UL)                                      BUG14042
     A**38******                            DSPATR(RI)                                      BUG14042
     A**********  SOMNOST        2   B  8 53TEXT('Nostro')                                  BUG14042
     A            SOMNOST        2   O  8 53TEXT('Nostro')
     A** End of change BUG14042
     A N38                                  DSPATR(HI)
     A** Start of change BUG14042
     A**20******                            DSPATR(PR)                                      BUG14042
     A*N20N38***                            DSPATR(UL)                                      BUG14042
     A**38******                            DSPATR(RI)                                      BUG14042
     A**********  SOMACTI       10   B  8 59TEXT('Action')                                  BUG14042
     A            SOMACTI       10   O  8 59TEXT('Action')
     A** End of change BUG14042
     A N38                                  DSPATR(HI)
     A** Start of change BUG14042
     A**20******                            DSPATR(PR)                                      BUG14042
     A*N20N38***                            DSPATR(UL)                                      BUG14042
     A**38******                            DSPATR(RI)                                      BUG14042
     A**********  SOMFLAG        1   B  8 74TEXT('Flag')                                    BUG14042
     A            SOMFLAG        1   O  8 74TEXT('Flag')
     A** End of change BUG14042
     A N38                                  DSPATR(HI)
     A** Start of change BUG14042
     A**20******                            DSPATR(PR)                                      BUG14042
     A*N20N38***                            DSPATR(UL)                                      BUG14042
     A**38******                            DSPATR(RI)                                      BUG14042
     A** End of change BUG14042
     A*=========================================================================
     A          R RSFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(RSFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A N40                                  CF09(09 'Add.')
     A                                      CF11(11 'Report')
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            SSFRC          4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                  1 13'ME001750F1'
     A                                  1 38'Midas'
     A            SMRDT          7      1 71TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            SURPT         40      2 25TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            STIME          6  0   2 70TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  3'?'
     A                                  4  6'Network'
     A                                  4 14'Msg'
     A                                  4 19'Message'
     A                                  4 27'Sender'
     A                                  4 35'Ctry'
     A                                  4 41'Currency'
     A                                  4 51'Nostro'
     A                                  4 59'Action'
     A                                  4 72'Flag'
     A                                  5 14'Typ'
     A                                  5 19'Field'
     A                                  5 27'Branch'
     A                                  5 35'Locat.'
     A                                  6 20'Tag'
     A            SEOMNWRK       6   B  7  6TEXT('Network')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A            SEOMMTPY       3   B  7 14TEXT('Message type')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A            SEOMMTAG       5   B  7 19TEXT('Message Tag')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A** Start of change BUG14042
     A**********  SEOMSBRC       3   B  7 28TEXT('Senders branch')                          BUG14042
     A            SEOMSBRC       4   B  7 28TEXT('Senders branch')
     A** End of change BUG14042
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A** Start of change BUG14042
     A**********  SEOMCTRY       2   B  7 35TEXT('Country of location')
     A            SEOMCTRY       4   B  7 35TEXT('Country of location')                     BUG14042
     A** End of change BUG14042
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A            SEOMCURR       3   B  7 43TEXT('Currency')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A            SEOMNOST       2   B  7 53TEXT('Nostro')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A            SEOMACTI      10   B  7 59TEXT('Action')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A            SEOMFLAG       1   B  7 74TEXT('Flag')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A*=========================================================================
     A          R RMNTRCD                   TEXT('Maintenance')
     A                                      CA03(03 'Exit.')
     A** Start of change BUG14042
     A**********                            CF05(05 'Reset.')                          BUG14042
     A** End of change BUG14042
     A                                      CF12(12 'Previ.')
     A                                      OVERLAY
     A            ##USR         10      1  2TEXT('*USER')
     A                                  1 13'ME001750F2'
     A                                  1 38'Midas'
     A            SMRDT          7      1 71TEXT('Midas Run Date')
     A            ##JOB         10      2  2TEXT('*JOB')
     A            SURPT         40      2 25TEXT('User Report Title')
     A                                      DSPATR(HI)
     A            STIME          6  0   2 70TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Action Code . . . :'
     A                                  6  2'Network . . . . . .'
     A                                  7  2'Message type  . . .'
     A                                  8  2'Message Field Tag .'
     A                                  9  2'Sender''s Branch . .'
     A                                 10  2'Country of Location'
     A                                 11  2'Currency'
     A                                 12  2'Nostro'
     A                                 13  2'Action'
     A            SACTCD         1      4 24
     A                                      DSPATR(HI)
     A            SOMNWRK        6   B  6 24
     A N34N31                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A            SOMMTPY        3   B  7 24
     A N34N32                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A            SOMMTAG        5   B  8 24
     A N34N33                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A** Start of change BUG14042
     A**********  SOMSBRC        3   B  9 24                                                BUG14042
     A            SOMSBRC        4   B  9 24
     A** End of change BUG14042
     A N34N36                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  36                                  DSPATR(RI PC)
     A** Start of change BUG14042
     A**********  SOMCTRY        2   B 10 24                                                BUG14042
     A            SOMCTRY        4   B 10 24
     A** End of change BUG14042
     A N34N38                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  38                                  DSPATR(RI PC)
     A            SOMCURR        3   B 11 24
     A N34N39                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  39                                  DSPATR(RI PC)
     A            SOMNOST        2   B 12 24
     A N34N41                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A            SOMACTI       10   B 13 24
     A N34N42                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A
     A                                 23  2'F3=Exit'
     A** Start of change BUG14042
     A**********                       23 16'F5=Refresh'                               BUG14042
     A** End of change BUG14042
     A                                 23 32'F12=Previous'
     A*=========================================================================
     A          R RMN2RCD                   TEXT('Maintenance Screen 2')
     A                                      CA03(03 'Exit.')
     A** Start of change BUG14042
     A**********                            CF05(05 'Reset.')                          BUG14042
     A                                      CF10(10 'Delete')
     A** End of change BUG14042
     A                                      CF12(12 'Previ.')
     A                                      OVERLAY
     A            ##USR         10      1  2TEXT('*USER')
     A                                  1 13'ME001750F2'
     A                                  1 38'Midas'
     A            SMRDT          7      1 71TEXT('Midas Run Date')
     A            ##JOB         10      2  2TEXT('*JOB')
     A            SURPT         40      2 25TEXT('User Report Title')
     A                                      DSPATR(HI)
     A            STIME          6  0   2 70TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Action Code . . . :'
     A                                  6  2'Network . . . . . .'
     A                                  7  2'Message type  . . .'
     A                                  8  2'Message Field Tag .'
     A                                  9  2'Sender''s Branch . .'
     A                                 10  2'Country of Location'
     A                                 11  2'Currency'
     A                                 12  2'Nostro'
     A                                 13  2'Action'
     A            SACTCD         1   O  4 24
     A                                      DSPATR(HI)
     A            SOMNWRK        6   O  6 24
     A                                      DSPATR(HI)
     A            SOMMTPY        3   O  7 24
     A                                      DSPATR(HI)
     A            SOMMTAG        5   O  8 24
     A                                      DSPATR(HI)
     A** Start of change BUG14042
     A**********  SOMSBRC        3   O  9 24                                                BUG14042
     A            SOMSBRC        4   O  9 24
     A** End of change BUG14042
     A                                      DSPATR(HI)
     A** Start of change BUG14042
     A**********  SOMCTRY        2   O 10 24                                                BUG14042
     A            SOMCTRY        4   O 10 24
     A** End of change BUG14042
     A                                      DSPATR(HI)
     A            SOMCURR        3   O 11 24
     A                                      DSPATR(HI)
     A            SOMNOST        2   O 12 24
     A                                      DSPATR(HI)
     A            SOMACTI       10   O 13 24
     A                                      DSPATR(HI)
     A*
     A            SDOMNWRK      30   O  6 40
     A                                      DSPATR(HI)
     A            SDOMMTPY      35   O  7 40
     A                                      DSPATR(HI)
     A            SDOMMTAG      20   O  8 40
     A                                      DSPATR(HI)
     A            SDOMSBRC      30   O  9 40
     A                                      DSPATR(HI)
     A            SDOMCTRY      30   O 10 40
     A                                      DSPATR(HI)
     A            SDOMCURR      14   O 11 40
     A                                      DSPATR(HI)
     A            SDOMNOST      10   O 12 40
     A                                      DSPATR(HI)
     A            SDOMACTI      30   O 13 40
     A                                      DSPATR(HI)
     A                                 15  2'Flag'
     A                                 16  2'Description'
     A            SOMFLAG        1   B 15 27TEXT('Flag')
     A N34N43                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A            SOMDESC       35   B 16 27TEXT('Description')
     A N34                                  DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A                                 18  2'Last Change Information'
     A                                 19  2'User name :'
     A            SOMAUSR       10     19 15TEXT('*USER')
     A                                      DSPATR(HI)
     A                                 19 26'Action Date :'
     A            SOMARDT        7     19 40TEXT('Action Date')
     A                                      DSPATR(HI)
     A                                 19 48'Action Time :'
     A            SOMATIM        6  0  19 62TEXT('*TIME')
     A                                      DSPATR(HI)
     A                                      EDTWRD('  :  :  ')
     A                                 23  2'F3=Exit'
     A** Start of change BUG14042
     A**********                       23 16'F5=Refresh'                               BUG14042
     A  44                             23 16'F10=Delete'
     A                                      DSPATR(HI)
     A** End of change BUG14042
     A                                 23 32'F12=Previous'
      **Start of change CSW207
     A*
     A          R RSFLRC4                   TEXT('Maintenance Screen 4')
     A                                      SFL
     A                                      INDTXT(85 'Subfile next change')
     A*                                     INDTXT(31 'Invalid: SISOC')
     A*                                     INDTXT(32 'Invalid: SISOD')
     A*                                     INDTXT(33 'Invalid: SSCHT')
     A*                                     INDTXT(34 'Invalid: SCSEL')
     A*                                     INDTXT(36 'Invalid: SSEEA')
     A  85                                  SFLNXTCHG
     A            SOM50FS        3   H
     A            SO50F1        15   B 16  2
     A                                      DSPATR(HI)
     A  55                                  DSPATR(RI PC)
     A N34N55                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A            SO50F2        15   B 16 18
     A                                      DSPATR(HI)
     A  56                                  DSPATR(RI PC)
     A N34N56                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A            SO50F3        15   B 16 34
     A                                      DSPATR(HI)
     A  57                                  DSPATR(RI PC)
     A N34N57                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A            SO50F4        15   B 16 50
     A                                      DSPATR(HI)
     A  58                                  DSPATR(RI PC)
     A N34N58                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A            SO50F5        15   B 16 66
     A                                      DSPATR(HI)
     A  59                                  DSPATR(RI PC)
     A N34N59                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A*
     A          R RSFLCT4                   TEXT('Subfile control.')
     A                                      SFLCTL(RSFLRC4)
     A                                      SFLPAG(5) SFLSIZ(6)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(97 'Display subfile')
     A                                      INDTXT(96 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  97                                  SFLCLR
     A N97                                  SFLDSPCTL
     A N97                                  SFLDSP
      *
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  95                                  CF10(10 'Del.')
     A                                      CF12(12 'Prev.')
     A N96                                  ROLLUP(27 'Next page.')
     A            SSRNB          4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A                                  1 13'ME001750F4'
     A                                  1 38'Midas'
     A            SMRDT          7      1 71TEXT('Midas Run Date')
     A            ##JOB         10      2  2TEXT('*JOB')
     A            SURPT         40      2 25TEXT('User Report Title')
     A                                      DSPATR(HI)
     A            STIME          6  0   2 70TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Action Code . . . :'
     A                                  5  2'Network . . . . . .'
     A                                  6  2'Message type  . . .'
     A                                  7  2'Message Field Tag .'
     A                                  8  2'Sender''s Branch . .'
     A                                  9  2'Country of Location'
     A                                 10  2'Currency'
     A                                 11  2'Nostro'
     A                                 12  2'Action'
     A            SACTCD         1   O  4 24
     A                                      DSPATR(HI)
     A            SOMNWRK        6   O  5 24
     A                                      DSPATR(HI)
     A            SOMMTPY        3   O  6 24
     A                                      DSPATR(HI)
     A            SOMMTAG        5   O  7 24
     A                                      DSPATR(HI)
     A            SOMSBRC        4   O  8 24
     A                                      DSPATR(HI)
     A            SOMCTRY        4   O  9 24
     A                                      DSPATR(HI)
     A            SOMCURR        3   O 10 24
     A                                      DSPATR(HI)
     A            SOMNOST        2   O 11 24
     A                                      DSPATR(HI)
     A            SOMACTI       10   O 12 24
     A                                      DSPATR(HI)
     A            SDOMNWRK      30   O  5 40
     A                                      DSPATR(HI)
     A            SDOMMTPY      35   O  6 40
     A                                      DSPATR(HI)
     A            SDOMMTAG      20   O  7 40
     A                                      DSPATR(HI)
     A            SDOMSBRC      30   O  8 40
     A                                      DSPATR(HI)
     A            SDOMCTRY      30   O  9 40
     A                                      DSPATR(HI)
     A            SDOMCURR      14   O 10 40
     A                                      DSPATR(HI)
     A            SDOMNOST      10   O 11 40
     A                                      DSPATR(HI)
     A            SDOMACTI      30   O 12 40
     A                                      DSPATR(HI)
     A                                 13  2'Flag'
     A            SOMFLAG        1   B 13 24TEXT('Flag')
     A N34N43                               DSPATR(UL)
     A  30                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A                                 15  2'Field 1'
     A                                 15 18'Field 2'
     A                                 15 34'Field 3'
     A                                 15 50'Field 4'
     A                                 15 66'Field 5'
      **End of change CSW207
     A*=========================================================================
     A          R RCMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            SCTX1         78     22  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R RCMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            SCTX2         78     23  2TEXT('Command Text 2')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R RMSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R RMSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(RMSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
