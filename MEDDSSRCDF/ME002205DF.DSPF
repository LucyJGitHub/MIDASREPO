      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Network Account Description - Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Messages Management Module                           *
      *                                                               *
      *  DSPF/ME002205DF - Network Account Description Maintenance    *
      *                    (Browse Screen)                            *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 29Nov05               *
      *                 CGL029             Date 01Sep03               *
      *                 CGL013   *CREATE   Date 25Sep02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CA03
     A                                      CA05
     A  01                                  CA09
      *==================================================================*
      * Header Format                                                    *
      *------------------------------------------------------------------*
     A          R ME002205H0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A            AGMRDT         7A  O  1 73
      *
     A            PSJOBNAME     10A  O  2  2
     A                                  2 27'Network Accounts Description'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *==================================================================*
      * Subfile Format                                                   *
      *------------------------------------------------------------------*
     A          R ME002205S0                SFL
     A  24                                  SFLNXTCHG
      *
     A            S0ACTCDE       1A  B  8  3TEXT('Action code')
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A            S0RECI         1   O  8  5
     A                                      DSPATR(BL)
     A**Start of change CGL029
     A**********  S0NWRK         6   O  8 10                                                  CGL029
     A**********  S0BRCH         3   O  8 20                                                  CGL029
     A**********  S0CNUM         6S 0O  8 24                                                  CGL029
     A**********  S0CCY          3   O  8 31                                                  CGL029
     A**********  S0ACOD         4S 0O  8 35                                                  CGL029
     A**********  S0ACSQ         2S 0O  8 40                                                  CGL029
     A            S0NWRK         6   O  8  7
     A            S0BRCH         3   O  8 15
      * Begin /CSD027/
     A**********  S0CNUM         6S 0O  8 19                                                  CSD027
     A            S0CNUM         6A  O  8 19
      * End /CSD027/
     A            S0CCY          3   O  8 26
     A            S0ACOD        10S 0O  8 30
     A            S0ACSQ         2S 0O  8 41
     A**End of change CGL029
     A            S0NATY         1   O  8 44
     A                                      DSPATR(ND)
     A            S0ACNO        10S 0O  8 47
     A            S0G940         1   O  8 62
     A            S0G941         1   O  8 66
     A            S0G942         1   O  8 70
     A            S0G950         1   O  8 74
      *==================================================================*
      * Subfile Control Format                                           *
      *------------------------------------------------------------------*
     A          R ME002205C0                SFLCTL(ME002205S0)
     A                                      OVERLAY PUTOVR
      *
     A N23                                  ROLLUP(25 'Roll up')
      *
     A  22                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N21                                  SFLCLR
     A  23                                  SFLEND(*MORE)
      *
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
      *
     A            C0RRN          4S 0H      SFLRCDNBR
      *
     A                                  4 45'Network'
     A                                      DSPATR(ND)
     A                                  4 62'Generates MT'
      *
     A**Start of change CGL029
     A**********                        5 10'Network'                                         CGL029
     A**********                        5 20'Acount number'                                   CGL029
     A                                  5  7'Network'
     A                                  5 15'Acount number'
     A**End of change CGL029
     A                                  5 42'Type'
     A                                      DSPATR(ND)
     A                                  5 47'Retail no.'
     A                                  5 61'940'
     A                                  5 65'941'
     A                                  5 69'942'
     A                                  5 73'950'
      *
     A**Start of change CGL029
     A**********  C0NWRK         6   B  6 10                                                  CGL029
     A            C0NWRK         6   B  6  7
     A**End of change CGL029
     A                                      DSPATR(HI UL)
     A**Start of change CGL029
     A**********  C0BRCH         3   B  6 20                                                  CGL029
     A            C0BRCH         3   B  6 15
     A**End of change CGL029
     A                                      DSPATR(HI UL)
     A**Start of change CGL029
     A**********  C0CNUM         6   B  6 24DSPATR(HI UL)                                     CGL029
     A**********  C0CCY          3   B  6 31                                                  CGL029
     A            C0CNUM         6   B  6 19DSPATR(HI UL)
     A            C0CCY          3   B  6 26
     A**End of change CGL029
     A                                      DSPATR(HI UL)
     A**Start of change CGL029
     A**********  C0ACOD         4   B  6 35DSPATR(HI UL)                                     CGL029
     A**********  C0ACSQ         2   B  6 40DSPATR(HI UL)                                     CGL029
     A            C0ACOD        10   B  6 30DSPATR(HI UL)
     A            C0ACSQ         2   B  6 41DSPATR(HI UL)
     A**End of change CGL029
     A            C0NATY         1   B  6 44
     A                                      DSPATR(ND PR)
     A            C0ACNO        10   B  6 47DSPATR(HI UL)
     A            C0G940         1   B  6 62
     A                                      DSPATR(HI UL)
     A            C0G941         1   B  6 66
     A                                      DSPATR(HI UL)
     A            C0G942         1   B  6 70
     A                                      DSPATR(HI UL)
     A            C0G950         1   B  6 74
     A                                      DSPATR(HI UL)
      *==================================================================*
      * Error Messages Management                                        *
      *------------------------------------------------------------------*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ(10)
      *==================================================================*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
      *
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
      *
     A            ZAPGM                     SFLPGMQ(10)
      *
     A  02                             22  2'A=Amend'
     A  03                             22 12'E=Enquire'
     A  04                             22 25'D=Delete'
      *
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
     A  01                             23 25'F9=Add'
      *==================================================================*
