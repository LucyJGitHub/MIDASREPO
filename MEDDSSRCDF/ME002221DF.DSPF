      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Network Account Description - Dtls 1')        *
      *****************************************************************
      *                                                               *
      *  Midas - Messages Management Module                           *
      *                                                               *
      *  DSPF/ME002221DF - Network Account Description Maintenance    *
      *                    (Details 1 Screen - MT950)                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. CGL146             Date 10Jul13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 29Nov05               *
      *                 CGL029             Date 01Sep03               *
      *                 CGL013  *CREATE    Date 04Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL146 - MT940/MT950 Production in Input Cycle               *
      *           Added hooks: CGL146_004, CGL146_042                 *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CA03
     A N36                                  CA05
     A  01                                  CA10
     A                                      CA12
      *==================================================================*
      * Header Format                                                    *
      *------------------------------------------------------------------*
     A          R ME002221H0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A            AGMRDT         7A  O  1 73
      *
     A            PSJOBNAME     10A  O  2  2
     A                                  2 28'MT950 Details Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *==================================================================*
      * Key Fields Format                                                *
      *------------------------------------------------------------------*
     A          R ME002221F0                OVERLAY PUTOVR
      *
     A                                  4  2'Network:'
     A            F0NWRK         6   O  4 11OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********                        4 22'Account no:'                                     CGL029
     A**********  F0BRCH         3   O  4 34OVRDTA DSPATR(HI)                                 CGL029
     A**********  F0CNUM         6S 0O  4 38OVRDTA DSPATR(HI)                                 CGL029
     A**********  F0CCY          3   O  4 45OVRDTA DSPATR(HI)                                 CGL029
     A**********  F0ACOD         4S 0O  4 49OVRDTA DSPATR(HI)                                 CGL029
     A**********  F0ACSQ         2S 0O  4 54OVRDTA DSPATR(HI)                                 CGL029
     A                                  4 19'Account no:'
     A            F0BRCH         3   O  4 31OVRDTA DSPATR(HI)
      * Begin /CSD027/
     A**********  F0CNUM         6S 0O  4 35OVRDTA DSPATR(HI)                                 CSD027
     A            F0CNUM         6A  O  4 35OVRDTA DSPATR(HI)
      * End /CSD027/
     A            F0CCY          3   O  4 42OVRDTA DSPATR(HI)
     A            F0ACOD        10S 0O  4 46OVRDTA DSPATR(HI)
     A            F0ACSQ         2S 0O  4 57OVRDTA DSPATR(HI)
     A**End of change CGL029
     A                                  4 62'Retail:'
     A            F0ACNO        10S 0O  4 70
     A                                      OVRDTA DSPATR(HI)
      *
     A                                  5  2'Type:'
     A                                      DSPATR(ND)
     A            F0NATY         1   O  5  8
     A                                      OVRDTA
     A                                      DSPATR(ND)
     A                                  5 12'Name:'
     A            F0ANAM        20   O  5 18
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  02                              5 69'**DELETED**'
     A                                      DSPATR(RI)
      *==================================================================*
      * Details 1 Screen Format (MT950 Details)                          *
      *------------------------------------------------------------------*
     A          R ME002221F1                OVERLAY PUTOVR
      *
     A                                  7  5'Generates MT950?. . . . . . . . .:'
     A            F1G950         1   B  7 40
     A                                      OVRDTA
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                  9  5'Statement Frequency . . . . . . .:'
     A            F15SFQ         1   B  9 40
     A                                      OVRDTA
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A*/COPY OFCPYSRCZ,CGL146_004                                                             CGL146
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A*/COPY OFCPYSRCZ,CGL146_042                                                             CGL146
      *
     A                                 10  5'Statement Day in Month. . . . . .:'
     A            F15SDY         2Y 0B 10 40
     A                                      OVRDTA
     A                                      EDTCDE(4)
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A*/COPY OFCPYSRCZ,CGL146_004                                                             CGL146
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A*/COPY OFCPYSRCZ,CGL146_042                                                             CGL146
      *
     A                                 11  5'Next Statement date . . . . . . .:'
     A            F15NSD         6   B 11 40OVRDTA
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A*/COPY OFCPYSRCZ,CGL146_004                                                             CGL146
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A*/COPY OFCPYSRCZ,CGL146_042                                                             CGL146
      *
     A                                 12  5'Demand Statement Indicator. . . .:'
     A            F15DSI         1   B 12 40
     A                                      OVRDTA
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI PC)
     A*/COPY OFCPYSRCZ,CGL146_004                                                             CGL146
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A*/COPY OFCPYSRCZ,CGL146_042                                                             CGL146
      *==================================================================*
      * Information Screen Format (from the last generated MT950)        *
      *------------------------------------------------------------------*
     A          R ME002221FI                OVERLAY PUTOVR
      *
     A                                 15  5'Last Closing Balance  . . . . . .:'
     A            FI5LCB        18   O 15 40OVRDTA DSPATR(HI)
      *
     A                                 16  5'Last Statement Date . . . . . . .:'
     A            FI5LSD         6   O 16 40OVRDTA DSPATR(HI)
      *
     A                                 17  5'Last Statement Page . . . . . . .:'
     A            FI5LSN         5S 0O 17 40
     A                                      OVRDTA DSPATR(HI)
      *
     A                                 18  5'Last Statement Time . . . . . . .:'
     A            FI5LST         6Y 0O 18 40
     A                                      OVRDTA EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
      *==================================================================*
      * Error Messages Management                                        *
      *------------------------------------------------------------------*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ(10)
      *==================================================================*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A            ZAPGM                     SFLPGMQ(10)
      *
     A                                 23  2'F3=Exit'
     A N36                             23 12'F5=Refresh'
     A  01                             23 25'F10=Delete'
     A                                 23 38'F12=Previous'
      *==================================================================*
