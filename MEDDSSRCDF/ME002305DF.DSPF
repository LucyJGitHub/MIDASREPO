      *==================================================================*
/*STD *  DSPFBASE                                                        *
/*EXI *  TEXT('Midas ME Network A/c MT94x Dtl Desc. - Browse')
      *==================================================================*
      *                                                                  *
      *  Midas - Messages Management Module                              *
      *                                                                  *
      *  DSPF/ME002305DF - Network Account MT94x Details Description     *
      *                    (Browse Screen)                               *
      *                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2002               *
      *                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 29Nov05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CGL013   *CREATE   Date 16Apr02                  *
      *                                                                  *
      *   ------------------------------------------------------------   *
      *                                                                  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL013 - MT94x Messages Generation                              *
      *                                                                  *
      *==================================================================*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CA03
     A                                      CA05
     A  01                                  CA09
     A  12                                  CA12
      *==================================================================*
      * Header Format                                                    *
      *------------------------------------------------------------------*
     A          R ME002305H0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A            AGMRDT         7A  O  1 73
      *
     A            PSJOBNAME     10A  O  2  2
     A                                  2 28'MT94x Details Description'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *==================================================================*
      * Key Fields Format                                                *
      *------------------------------------------------------------------*
     A          R ME002305F0                OVERLAY PUTOVR
      *
     A                                  4  2'Network:'
     A            F0NWRK         6   O  4 11
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********                        4 22'Account no:'                                     CGL029
     A**********  F0BRCA         3   O  4 34                                                  CGL029
     A                                  4 19'Account no:'
     A            F0BRCA         3   O  4 31
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********  F0CNUM         6S 0O  4 38                                                  CGL029
      * Begin /CSD027/
     A**********  F0CNUM         6S 0O  4 35                                                  CSD027
     A            F0CNUM         6A  O  4 35
      * End /CSD027/
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********  F0CCY          3   O  4 45                                                  CGL029
     A            F0CCY          3   O  4 42
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********  F0ACCD         4S 0O  4 49                                                  CGL029
     A            F0ACCD        10S 0O  4 46
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********  F0ACSQ         2S 0O  4 54                                                  CGL029
     A            F0ACSQ         2S 0O  4 57
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A                                  4 62'Retail:'
     A            F0ACNO        10S 0O  4 70
     A                                      OVRDTA DSPATR(HI)
      *
     A                                  5  2'Type:'
     A                                      DSPATR(ND)
     A            F0NATY         1   O  5  8
     A                                      OVRDTA
     A                                      DSPATR(ND)
     A                                  5 12'Name:'
     A            F0ANAM        20   O  5 18
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  13                              5 69'**DELETED**'
     A                                      DSPATR(RI)
      *==================================================================*
      * Subfile Format                                                   *
      *------------------------------------------------------------------*
     A          R ME002305S0                SFL
     A  24                                  SFLNXTCHG
      *
     A            S0WARN1        1A  H      TEXT('Warning 1 already issued')
      *
     A            S0ACTCDE       1A  B 10  3TEXT('Action code')
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A            S0RECI         1   O 10  5
     A                                      DSPATR(BL)
     A            S0DSTN        11   O 10  8
     A            S0DSTD        35   O 10 25
     A            S0G940         1   O 10 66
     A            S0G941         1   O 10 72
     A            S0G942         1   O 10 77
      *==================================================================*
      * Subfile Control Format                                           *
      *------------------------------------------------------------------*
     A          R ME002305C0                SFLCTL(ME002305S0)
     A                                      OVERLAY PUTOVR
      *
     A N23                                  ROLLUP(25 'Roll up')
      *
     A  22                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N21                                  SFLCLR
     A  23                                  SFLEND(*MORE)
      *
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
      *
     A            C0RRN          4S 0H      SFLRCDNBR
      *
     A                                  6 66'Generates'
     A                                  6 76'MT'
      *
     A                                  7  8'Destination'
     A                                  7 25'Description'
     A                                  7 65'940'
     A                                  7 71'941'
     A                                  7 76'942'
      *
     A            C0DSTN        11   B  8  8
     A                                      OVRDTA DSPATR(HI UL)
     A            C0DSTD        35   B  8 25
     A                                      OVRDTA DSPATR(HI UL)
     A            C0G940         1   B  8 66
     A                                      OVRDTA DSPATR(HI UL)
     A            C0G941         1   B  8 72
     A                                      OVRDTA DSPATR(HI UL)
     A            C0G942         1   B  8 77
     A                                      OVRDTA DSPATR(HI UL)
      *==================================================================*
      * Error Messages Management                                        *
      *------------------------------------------------------------------*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ(10)
      *==================================================================*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
      *
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
      *
     A            ZAPGM                     SFLPGMQ(10)
      *
     A  02                             22  2'A=Amend'
     A  03                             22 12'E=Enquire'
     A  04                             22 25'D=Delete'
     A  05                             22 36'0=MT940 Dtls'
     A  06                             22 51'1=MT941 Dtls'
     A  07                             22 66'2=MT942 Dtls'
      *
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
     A  01                             23 25'F9=Add'
     A  12                             23 36'F12=Previous'
      *==================================================================*
