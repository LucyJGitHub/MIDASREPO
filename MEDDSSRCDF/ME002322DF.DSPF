      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Network A/c MT94x Dtl Desc. - Dtls 2')        *
      *****************************************************************
      *                                                               *
      *  Midas - Messages Management Module                           *
      *                                                               *
      *  DSPF/ME002322DF - Network Account MT94x Details Description  *
      *                    (Details 2 Screen - MT940)                 *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL146             Date 10Jul13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 29Nov05               *
      *                 CGL029             Date 01Sep03               *
      *                 CGL013  *CREATE    Date 22Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL146 - MT940/MT950 Production in Input Cycle               *
      *           Added hooks: CGL146_004, CGL146_042                 *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CA03
     A N36                                  CA05
     A  01                                  CA10
     A                                      CA12
      *==================================================================*
      * Header Format                                                    *
      *------------------------------------------------------------------*
     A          R ME002322H0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A            AGMRDT         7A  O  1 73
      *
     A            PSJOBNAME     10A  O  2  2
     A                                  2 24'MT940 Details Account Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *==================================================================*
      * Key Fields Format                                                *
      *------------------------------------------------------------------*
     A          R ME002322F0                OVERLAY PUTOVR
      *
     A                                  4  2'Network:'
     A            F0NWRK         6   O  4 11
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********                        4 22'Account no:'                                     CGL029
     A**********  F0BRCA         3   O  4 34                                                  CGL029
     A                                  4 19'Account no:'
     A            F0BRCA         3   O  4 31
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********  F0CNUM         6S 0O  4 38                                                  CGL029
      * Begin /CSD027/
     A**********  F0CNUM         6S 0O  4 35                                                  CSD027
     A            F0CNUM         6A  O  4 35
      * End /CSD027/
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********  F0CCY          3   O  4 45                                                  CGL029
     A            F0CCY          3   O  4 42
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********  F0ACCD         4S 0O  4 49                                                  CGL029
     A            F0ACCD        10S 0O  4 46
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********  F0ACSQ         2S 0O  4 54                                                  CGL029
     A            F0ACSQ         2S 0O  4 57
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A                                  4 62'Retail:'
     A            F0ACNO        10S 0O  4 70
     A                                      OVRDTA DSPATR(HI)
      *
     A                                  5  2'Type:'
     A                                      DSPATR(ND)
     A            F0NATY         1   O  5  8
     A                                      OVRDTA
     A                                      DSPATR(ND)
     A                                  5 12'Name:'
     A            F0ANAM        20   O  5 18
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 41'Destination:'
     A            F0DSTN        11   O  5 54
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  02                              5 69'**DELETED**'
     A                                      DSPATR(RI)
      *==================================================================*
      * Details 2 Screen Format (MT940 Details)                          *
      *------------------------------------------------------------------*
     A          R ME002322F2                OVERLAY PUTOVR
      *
     A                                  7  5'Statement Frequency:'
     A            F20SFR         1   B  7 26
     A                                      OVRDTA
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A*/COPY OFCPYSRCZ,CGL146_004                                                             CGL146
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A*/COPY OFCPYSRCZ,CGL146_042                                                             CGL146
     A                                  7 47'Statement Day in Month . .:'
     A            F20SDM         2Y 0B  7 75
     A                                      OVRDTA EDTCDE(4)
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A*/COPY OFCPYSRCZ,CGL146_004                                                             CGL146
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A*/COPY OFCPYSRCZ,CGL146_042                                                             CGL146
      *
     A                                  8  5'Next Statement date:'
     A            F20NSD         6   B  8 26OVRDTA
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A*/COPY OFCPYSRCZ,CGL146_004                                                             CGL146
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A*/COPY OFCPYSRCZ,CGL146_042                                                             CGL146
     A                                  8 47'Demand Statement Indicator:'
     A            F20DSI         1   B  8 75
     A                                      OVRDTA
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A*/COPY OFCPYSRCZ,CGL146_004                                                             CGL146
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
     A*/COPY OFCPYSRCZ,CGL146_042                                                             CGL146
      *
     A                                 10  5'Default Information to the Account-
     A                                       Owner:'
      *
     A            F20MI1        65   B 11  8
     A                                      OVRDTA
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F20MI2        65   B 12  8
     A                                      OVRDTA
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F20MI3        65   B 13  8
     A                                      OVRDTA
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F20MI4        65   B 14  8
     A                                      OVRDTA
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F20MI5        65   B 15  8
     A                                      OVRDTA
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F20MI6        65   B 16  8
     A                                      OVRDTA
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 18  5'Last Closing Balance  . . . . :'
     A            F20LCA        18   O 18 37OVRDTA DSPATR(HI)
      *
     A                                 19  5'Last Statement Date . . . . . :'
     A            F20LSD         6   O 19 37OVRDTA DSPATR(HI)
      *
     A                                 20  5'Last Statement Page . . . . . :'
     A            F20LSN         5S 0O 20 37
     A                                      OVRDTA DSPATR(HI)
      *
     A                                 21  5'Last Statement Time . . . . . :'
     A            F20LST         6Y 0O 21 37
     A                                       OVRDTA DSPATR(HI)
     A                                      EDTWRD('  :  :  ')
      *==================================================================*
      * Error Messages Management                                        *
      *------------------------------------------------------------------*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ(10)
      *==================================================================*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A            ZAPGM                     SFLPGMQ(10)
      *
     A                                 23  2'F3=Exit'
     A N36                             23 12'F5=Refresh'
     A  01                             23 25'F10=Delete'
     A                                 23 38'F12=Previous'
      *==================================================================*
