      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Network A/c MT94x Dtl Desc. - Dtls 3')
      *****************************************************************
      *                                                               *
      *  Midas - Messages Management Module                           *
      *                                                               *
      *  DSPF/ME002323DF - Network Account MT94x Details Description  *
      *                    (Details 3 Screen - MT941/1)               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 29Nov05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CGL013  *CREATE    Date 23Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CA03
     A N36                                  CA05
     A  01                                  CA10
     A                                      CA12
      *==================================================================*
      * Header Format                                                    *
      *------------------------------------------------------------------*
     A          R ME002323H0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A            AGMRDT         7A  O  1 73
      *
     A            PSJOBNAME     10A  O  2  2
     A                                  2 23'MT941 Details Account Maintenance -
     A                                      1'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *==================================================================*
      * Key Fields Format                                                *
      *------------------------------------------------------------------*
     A          R ME002323F0                OVERLAY PUTOVR
      *
     A                                  4  2'Network:'
     A            F0NWRK         6   O  4 11
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********                        4 22'Account no:'                                     CGL029
     A**********  F0BRCA         3   O  4 34                                                  CGL029
     A                                  4 19'Account no:'
     A            F0BRCA         3   O  4 31
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********  F0CNUM         6S 0O  4 38                                                  CGL029
      * Begin /CSD027/
     A**********  F0CNUM         6S 0O  4 35                                                  CSD027
     A            F0CNUM         6A  O  4 35
      * End /CSD027/
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********  F0CCY          3   O  4 45                                                  CGL029
     A            F0CCY          3   O  4 42
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********  F0ACCD         4S 0O  4 49                                                  CGL029
     A            F0ACCD        10S 0O  4 46
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********  F0ACSQ         2S 0O  4 54                                                  CGL029
     A            F0ACSQ         2S 0O  4 57
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A                                  4 62'Retail:'
     A            F0ACNO        10S 0O  4 70
     A                                      OVRDTA DSPATR(HI)
      *
     A                                  5  2'Type:'
     A                                      DSPATR(ND)
     A            F0NATY         1   O  5  8
     A                                      OVRDTA
     A                                      DSPATR(ND)
     A                                  5 12'Name:'
     A            F0ANAM        20   O  5 18
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 41'Destination:'
     A            F0DSTN        11   O  5 54
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  02                              5 69'**DELETED**'
     A                                      DSPATR(RI)
      *==================================================================*
      * Details 3 Screen Format (MT941/1 Details)                        *
      *------------------------------------------------------------------*
     A          R ME002323F3                OVERLAY PUTOVR
     A                                  7  5'Optional fields required:'
      *
     A                                  8  8'Date/Time Indicator . . . . . .:'
     A            F31DTI         1   B  8 41
     A                                      OVRDTA
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                  9  8'Opening Balance Information . .:'
     A            F31OBI         1   B  9 41
     A                                      OVRDTA
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 10  8'Number and Sum of Entries (D/C):'
     A            F31LSF         1   B 10 41
     A                                      OVRDTA
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 11  8'Closing Available Balance . . .:'
     A            F31CAB         1   B 11 41
     A                                      OVRDTA
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 12  8'Forward Available Balance . . .:'
     A            F31FAB         1   B 12 41
     A                                      OVRDTA
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 14  5'Default Information to the Account-
     A                                       Owner:'
      *
     A            F31MI1        65   B 15  8
     A                                      OVRDTA
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F31MI2        65   B 16  8
     A                                      OVRDTA
     A N47                                  DSPATR(HI)
     A  47                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F31MI3        65   B 17  8
     A                                      OVRDTA
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F31MI4        65   B 18  8
     A                                      OVRDTA
     A N49                                  DSPATR(HI)
     A  49                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F31MI5        65   B 19  8
     A                                      OVRDTA
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A            F31MI6        65   B 20  8
     A                                      OVRDTA
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *==================================================================*
      * Error Messages Management                                        *
      *------------------------------------------------------------------*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ(10)
      *==================================================================*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A            ZAPGM                     SFLPGMQ(10)
      *
     A                                 23  2'F3=Exit'
     A N36                             23 12'F5=Refresh'
     A  01                             23 25'F10=Delete'
     A                                 23 38'F12=Previous'
      *==================================================================*
