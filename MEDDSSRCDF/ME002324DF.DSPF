      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Network A/c MT94x Dtl Desc. - Dtls 4')
      *****************************************************************
      *                                                               *
      *  Midas - Messages Management Module                           *
      *                                                               *
      *  DSPF/ME002324DF - Network Account MT94x Details Description  *
      *                    (Details 4 Screen - MT941/2)               *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL178             Date 04Aug16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 29Nov05               *
      *                 CGL029             Date 01Sep03               *
      *                 CGL013  *CREATE    Date 23Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL178 - Generate MT942 based on MT950.                      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CA03
     A N36                                  CA05
     A  01                                  CA10
     A                                      CA12
      *==================================================================*
      * Header Format                                                    *
      *------------------------------------------------------------------*
     A          R ME002324H0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A            AGMRDT         7A  O  1 73
      *
     A            PSJOBNAME     10A  O  2  2
     A                                  2 23'MT941 Details Account Maintenance -
     A                                      2'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *==================================================================*
      * Key Fields Format                                                *
      *------------------------------------------------------------------*
     A          R ME002324F0                OVERLAY PUTOVR
      *
     A                                  4  2'Network:'
     A            F0NWRK         6   O  4 11
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********                        4 22'Account no:'                                     CGL029
     A**********  F0BRCA         3   O  4 34                                                  CGL029
     A                                  4 19'Account no:'
     A            F0BRCA         3   O  4 31
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********  F0CNUM         6S 0O  4 38                                                  CGL029
      * Begin /CSD027/
     A**********  F0CNUM         6S 0O  4 35                                                  CSD027
     A            F0CNUM         6A  O  4 35
      * End /CSD027/
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********  F0CCY          3   O  4 45                                                  CGL029
     A            F0CCY          3   O  4 42
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********  F0ACCD         4S 0O  4 49                                                  CGL029
     A            F0ACCD        10S 0O  4 46
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A**Start of change CGL029
     A**********  F0ACSQ         2S 0O  4 54                                                  CGL029
     A            F0ACSQ         2S 0O  4 57
     A**End of change CGL029
     A                                      OVRDTA DSPATR(HI)
     A                                  4 62'Retail:'
     A            F0ACNO        10S 0O  4 70
     A                                      OVRDTA DSPATR(HI)
      *
     A                                  5  2'Type:'
     A                                      DSPATR(ND)
     A            F0NATY         1   O  5  8
     A                                      OVRDTA
     A                                      DSPATR(ND)
     A                                  5 12'Name:'
     A            F0ANAM        20   O  5 18
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5 41'Destination:'
     A            F0DSTN        11   O  5 54
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A  02                              5 69'**DELETED**'
     A                                      DSPATR(RI)
      *==================================================================*
      * Details 4 Screen Format (MT941/2 Details)                        *
      *------------------------------------------------------------------*
     A          R ME002324F4                OVERLAY PUTOVR
      *
     A                                  7  5'Generation if no Movement . . . . -
     A                                      . . .:'
     A            F41GNM         1   B  7 46
     A                                      OVRDTA
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                  8  5'Allow Generation on External Reque-
     A                                      st. .:'
     A            F41AER         1   B  8 46
     A                                      OVRDTA
     A N42                                  DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A N36N03                               DSPATR(UL)
     A  03
     AO 36                                  DSPATR(PR)
      *
     A                                  9  5'Allow Generation on Schedule. . . -
     A                                      . . .:'
     A            F41AGS         1   B  9 46
     A                                      OVRDTA
     A N43                                  DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N36N04                               DSPATR(UL)
     A  04
     AO 36                                  DSPATR(PR)
      *
     A                                 10  5'Allow Generation on Manual Request-
     A                                      . . .:'
     A            F41AMR         1   B 10 46
     A                                      OVRDTA
     A N44                                  DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A N36N05                               DSPATR(UL)
     A  05
     AO 36                                  DSPATR(PR)
      *
      ** Start of change CGL178
     A**********                       11  5'Re-Extract all Postings from Last -
     A**********                            MT940:'
     A N49                             11  5'Re-Extract all Postings from Last -
     A                                      MT940:'
     A  49                             11  5'Re-Extract all Postings from Last -
     A                                      MT950:'
      ** End of change CGL178
     A            F41RPL         1   B 11 46
     A                                      OVRDTA
     A N45                                  DSPATR(HI)
     A  45                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 12  5'Re-Extract all Shadow Postings. . -
     A                                      . . .:'
     A            F41RAS         1   B 12 46
     A                                      OVRDTA
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A  36                                  DSPATR(PR)
      *
     A                                 15  5'Next Opening Balance. . . . . . . -
     A                                      . . .:'
     A            F41LCB        18A  O 15 46OVRDTA DSPATR(HI)
      *
     A                                 16  5'Last Statement Date . . . . . . . -
     A                                      . . .:'
     A            F41LSD         6A  O 16 46OVRDTA DSPATR(HI)
      *
     A                                 17  5'Last Statement Page . . . . . . . -
     A                                      . . .:'
     A            F41LSP         5S 0O 17 46
     A                                      OVRDTA DSPATR(HI)
      *
     A                                 18  5'Last Statement Time . . . . . . . -
     A                                      . . .:'
     A            F41LST         6Y 0O 18 46
     A                                      OVRDTA DSPATR(HI)
     A                                      EDTWRD('  :  :  ')
      *
     A                                 19  5'Last Sum of Debit Items / Amount. -
     A                                      . . .:'
     A            F41LDN         5Y 0O 19 46
     A                                      OVRDTA DSPATR(HI) EDTCDE(3)
     A            F41LDA        18A  O 19 52OVRDTA DSPATR(HI)
      *
     A                                 20  5'Last Sum of Credit Items / Amount -
     A                                      . . .:'
     A            F41LCN         5Y 0O 20 46
     A                                      OVRDTA DSPATR(HI) EDTCDE(3)
     A            F41LCA        18A  O 20 52OVRDTA DSPATR(HI)
      *==================================================================*
      * Error Messages Management                                        *
      *------------------------------------------------------------------*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ(10)
      *==================================================================*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A            ZAPGM                     SFLPGMQ(10)
      *
     A                                 23  2'F3=Exit'
     A N36                             23 12'F5=Refresh'
     A  01                             23 25'F10=Delete'
     A                                 23 38'F12=Previous'
      *==================================================================*
