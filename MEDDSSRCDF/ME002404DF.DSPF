      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME MT941/2 Generation Schedules- Browse Dest')
      *****************************************************************
      *                                                               *
      *  Midas - Messages Management Module                           *
      *                                                               *
      *  DSPF/ME002404DF - MT941/2 Generation Schedules Description   *
      *                    (1st Browse Screen: Destination Selection) *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 29Nov05               *
      *                 CGL029             Date 01Sep03               *
      *                 CGL013   *CREATE   Date 24Jun02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT INDARA
      *
     A                                      CA03
     A                                      CA05
      *==================================================================*
      * Header Format                                                    *
      *------------------------------------------------------------------*
     A          R ME002404H0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A            PSUSER        10A  O  1  2
     A            ZAPGM         10A  O  1 13
     A            AGMRDT         7A  O  1 73
      *
     A            PSJOBNAME     10A  O  2  2
     A                                  2 25'Generation Schedules Maintenance'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *
     A                                  3 30'Destination Selection'
     A                                      DSPATR(HI UL)
      *==================================================================*
      * Subfile Format                                                   *
      *------------------------------------------------------------------*
     A          R ME002404S0                SFL
     A  24                                  SFLNXTCHG
      *
     A            S0ACTCDE       1A  B  8  3TEXT('Action code')
     A N41                                  DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A                                      DSPATR(UL)
     A            S0RECI         1   O  8  5
     A                                      DSPATR(BL)
     A            S0NWRK         6   O  8  7
     A            S0BRCH         3   O  8 16
      * Begin /CSD027/
     A**********  S0CNUM         6S 0O  8 20                                                  CSD027
     A            S0CNUM         6A  O  8 20
      * End /CSD027/
     A            S0CCY          3   O  8 27
     A**Start of change CGL029
     A**********  S0ACOD         4S 0O  8 31                                                  CGL029
     A**********  S0ACSQ         2S 0O  8 36                                                  CGL029
     A**********  S0NATY         1   O  8 42                                                  CGL029
     A            S0ACOD        10S 0O  8 31
     A            S0ACSQ         2S 0O  8 42
     A            S0NATY         1   O  8 44
     A**End of change CGL029
     A                                      DSPATR(ND)
     A            S0ACNO        10S 0O  8 46
     A            S0DSTN        11   O  8 58
     A            S01AGS         1   O  8 73
     A            S02AGS         1   O  8 78
      *==================================================================*
      * Subfile Control Format                                           *
      *------------------------------------------------------------------*
     A          R ME002404C0                SFLCTL(ME002404S0)
     A                                      OVERLAY PUTOVR
      *
     A N23                                  ROLLUP(25 'Roll up')
      *
     A  22                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N21                                  SFLCLR
     A  23                                  SFLEND(*MORE)
      *
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
      *
     A            C0RRN          4S 0H      SFLRCDNBR
      *
     A                                  4 71'Schedules'
      *
     A                                  5  7'Network'
     A                                  5 16'Acount number'
     A                                  5 40'Type'
     A                                      DSPATR(ND)
     A                                  5 46'Retail no.'
     A                                  5 58'Destination'
     A                                  5 71'MT941/942'
      *
     A            C0NWRK         6   B  6  7
     A                                      DSPATR(HI UL)
     A            C0BRCH         3   B  6 16
     A                                      DSPATR(HI UL)
     A            C0CNUM         6A  B  6 20DSPATR(HI UL)
     A            C0CCY          3   B  6 27
     A                                      DSPATR(HI UL)
     A**Start of change CGL029
     A**********  C0ACOD         4A  B  6 31DSPATR(HI UL)                                     CGL029
     A**********  C0ACSQ         2A  B  6 36DSPATR(HI UL)                                     CGL029
     A**********  C0NATY         1   B  6 42                                                  CGL029
     A            C0ACOD        10A  B  6 31DSPATR(HI UL)
     A            C0ACSQ         2A  B  6 42DSPATR(HI UL)
     A            C0NATY         1   B  6 44
     A**End of change CGL029
     A                                      DSPATR(ND PR)
     A            C0ACNO        10A  B  6 46DSPATR(HI UL)
     A            C0DSTN        11   B  6 58
     A                                      DSPATR(HI UL)
     A            C01AGS         1   B  6 73
     A                                      DSPATR(HI UL)
     A            C02AGS         1   B  6 78
     A                                      DSPATR(HI UL)
      *==================================================================*
      * Error Messages Management                                        *
      *------------------------------------------------------------------*
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            ZAPGM                     SFLPGMQ(10)
      *==================================================================*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
      *
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  23
     AON23                                  SFLEND
      *
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
      *
     A            ZAPGM                     SFLPGMQ(10)
      *
     A  02                             22  2'A=Amend'
     A  03                             22 12'E=Enquire'
     A  06                             22 24'1=MT941 Schedules'
     A  07                             22 44'2=MT942 Schedules'
      *
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
      *==================================================================*
