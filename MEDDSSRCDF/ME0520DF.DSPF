      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Outgoing messages by destination')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MESSAGE MANAGEMENT                                   *
     A*                                                               *
     A*  ME0520DF - OUTGOING MESSAGES BY DESTINATION SUMMARY          *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
     A*  Last Amend No. 192138             DATE 11Jul01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. 186033             Date 23Feb01               *
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 105621             Date 22Sep97               *
      *                 112563             Date 10Feb97               *
      *                 077219             Date 11Oct94               *
     A*                 059805             DATE 17FEB94               *
     A*                 E39162             DATE 18JUN92               *
     A*                 E32330             DATE 18NOV91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  192138 - Amend TRN field on screen to 17A to allow for the   *
     A*           fact that this is indented by 1 if part of a        *
     A*           multiple message.                                   *
      *  186033 - Position cursor at error subfile record             *
     A*  105621 - Change SFLEND to SFLEND(*MORE).                     *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  077219 - Specify correct folder for on-line help.            *
     A*  059805 - Indent parts of multiple messages                   *
     A*           (change TRNO field to DTRNO)                        *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*  E32330 - 'TODAY'S WORK' AMENDMENT                            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
     A*                                              Begin 077219
     A*****************                     C10')
     A*                                              End   077219
     A          R ME0520F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            USRID         10A  O  1  2
     A                                  1 13'ME0520'
     A                                  1 20'OUTGOING MESSAGES BY DESTINATION +
     A                                      SUMMARY'
     A                                      DSPATR(UL HI)
     A            RUNA           7A  O  1 72
     A            WSID          10   O  2  2
     A                                  2 71TIME
     A          R ME0520F2
     A                                      OVERLAY
      *
      *
      *
      *
     A  41                              3  2'Branch'
     A  41        SBCH           3   B  3  9
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A                                  3 13'Destination customer'
     A            SCUS          10   B  3 34
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A                                  3 45'or address'
     A            SADR          20   B  3 61
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A                                  4  2'Module'
     A            SMOD           2   B  4  9
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,ME0520DF01
     A                                  4 13'Start date'
     A*/COPY WNCPYSRC,ME0520DF02
     A            SDAT           6   B  4 34
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A                                  4 45'External system'
     A            SSYS           6   B  4 61
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI PC)
     A                                  5  2'Status'
     A            STS1           1   B  5  9
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI PC)
     A                                  5 13'Unreleased'
     A            STS2           1   B  6  9
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI PC)
     A                                  6 13'Released incomplete'
     A            STS3           1   B  7  9
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI PC)
     A                                  7 13'Released complete'
     A            STS4           1   B  8  9
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI PC)
     A                                  8 13'Cancelled/released/undelivered'
     A**                                             Begin /112563/
     A***********                      23  2'F3-Exit'
     A                                 23  2'F3=Exit'
     A**                                             End   /112563/
      *
     A          R ME0520F3
     A                                      OVERLAY
     A  41                              3  2'Branch'
     A  41        SBCH           3A  O  3  9
     A  41                                  DSPATR(HI)
     A                                  3 13'Destination customer'
     A            SCUS          10A  O  3 34
     A                                      DSPATR(HI)
     A                                  3 45'or address'
     A            SADR          20A  O  3 61
     A                                      DSPATR(HI)
     A                                  4  2'Module'
     A            SMOD           2A  O  4  9
     A                                      DSPATR(HI)
      * Start of E32330
     A***********                       4 13'Start date'
     A*********** SDAT           6A  O  4 34
     A***********                           DSPATR(HI)
     A*/COPY WNCPYSRC,ME0520DF03
     A N39                              4 13'Start date'
     A N39        SDAT           6A  O  4 34
     A N39                                  DSPATR(HI)
     A  39                              4 13'Today''s work only'
     A*/COPY WNCPYSRC,ME0520DF04
      * End of E32330
     A                                  4 45'External system'
     A            SSYS           6A  O  4 61
     A                                      DSPATR(HI)
     A                                  5  2'Status'
     A            SSTS          35A  O  5  9
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,ME0520DF05
     A                                  6 26'Msg'
     A  25                              6 73'C/P'
     A  25                              6 77'Brk'
     A** Begin /192138/
     A**********                        7  9'T.R.N.'
     A  41                              7  4'Brc'
     A                                  7  8'T.R.N.'
     A** End   /192138/
     A                                  7 25' typ'
     A                                  7 30'Message status'
     A                                  7 65' Netwrk'
     A  25                              7 73'Cnf'
     A  25                              7 77'Cnf'
     A** Begin /192138/
     A**41******                        7  4'Brch'
     A** End   /192138/
     A*/COPY WNCPYSRC,ME0520DF06
      *
     A          R ME0520F4                  SFL
     A  24                                  SFLNXTCHG
     A            SELECT         1A  B  8  2
      *                                               Begin /186033/
     A**38******                            DSPATR(RI)
     A  38                                  DSPATR(RI PC)
      *                                               End   /186033/
     A*********              Start of Change               /059805/
     A*********   TRNO          16A  O  8  9
     A** Begin /192138/
     A**********  DTRNO         16A  O  8  9
     A            DTRNO         17A  O  8  8
     A** End   /192138/
     A*                      End of Change                 /059805/
     A            MTPY           3A  O  8 26
     A            MSTT          35A  O  8 30
     A            NWRK           6A  O  8 66
     A            CFCPY          2A  O  8 73
     A            CFBRK          2A  O  8 77
     A            BRCA           3A  O  8  4
     A N41                                  DSPATR(ND)
     A          R ME0520F5                  SFLCTL(ME0520F4)
     A*  91/06/07  11:57:42    FBANK       REL-R03M00  5728-PW1
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CA05
     A                                      CA12
     A  20                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N20N21                               SFLCLR
     A*********              Start of Change               /105621/
     A**22*****                             SFLEND
     A  22                                  SFLEND(*MORE)
     A*                      End of Change                 /105621/
     A N22                                  ROLLUP(23)
     A                                      OVERLAY
     A*                                              Begin 077219
     A************                          C10')
     A*                                              End   077219
     A            DSPREC         4S 0H      SFLRCDNBR
     A**                                             Begin /112563/
     A***********                      23  2'F3-Exit'
     A***********                      23 11'F12-Prev'
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Prev'
     A**                                             End   /112563/
     A*/COPY WNCPYSRC,ME0520DF08
     A                                 23 33'Rollup/Rolldown'
     A                                 23 50'Valid action codes'
     A*/COPY WNCPYSRC,ME0520DF09
     A            SATN           3A  O 23 69
     A  45                             23 73'R(eset)'
     A**                                             Begin /112563/
     A***********                      23 21'F5-Refresh'
     A                                 23 21'F5=Refresh'
     A**                                             End   /112563/
     A*/COPY WNCPYSRC,ME0520DF07
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
      *
      **  Message Subfile Control Record format
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
