      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Outgoing messages by transaction')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MESSAGE MANAGEMENT
     A*                                                               *
     A*   ME0530DF - OUTGOING MESSAGES BY TRANSACTION SUMMARY         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. 186033             Date 23Feb01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 112563             Date 10Feb97               *
      *                 077219             Date 11Oct94               *
     A*                 E39162             DATE 18JUN92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  186033 - Position the cursor on the error subfile record     *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  077219 - Specify correct folder for on-line help.            *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
     A*                                              Begin 077219
     A************                          C10')
     A*                                              End   077219
     A          R ME0530F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            USRID         10A  O  1  2
     A                                  1 13'ME0530'
     A            RUNA           7A  O  1 72
     A            WSID          10A  O  2  2
     A                                  1 23'OUTGOING MESSAGES BY TRANSACTION S-
     A                                      UMMARY'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A          R ME0530F2                  OVERLAY
     A                                  3  2'Module id'
     A            SMOD           2   B  3 12
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A                                  3 23'Transaction'
     A            STRN          15   B  3 35
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A**                                             Begin /112563/
     A***********                      23  2'F3-Exit'
     A                                 23  2'F3=Exit'
     A**                                             End   /112563/
      *
     A          R ME0530F3
     A                                      OVERLAY
     A                                  3  2'Module id'
     A            SMOD           2A  O  3 12
     A                                      DSPATR(HI)
     A                                  3 23'Transaction'
     A            STRN          15A  O  3 35
     A                                      DSPATR(HI)
     A                                  5 44'Msg'
     A  25                              5 71'C/p'
     A  25                              5 75' Brk  '
     A                                  6 26'T.R.N.'
     A                                  6 44'type'
     A                                  6 49'Msg status'
     A                                  6 63'Network'
     A  25                              6 71'cnf '
     A  25                              6 76'cnf '
     A  41                              6  4'Brch'
     A                                  5  9'Transaction'
     A                                  6  9'no.'
     A          R ME0530F4                  SFL
     A  24                                  SFLNXTCHG
     A            SELECT         1A  B  7  2
      *                                               Begin /186033/
     A**38******                            DSPATR(RI)
     A  38                                  DSPATR(RI PC)
      *                                               End   /186033/
     A            BRCA           3A  O  7  4
     A N41                                  DSPATR(ND)
     A            MTRN          15A  O  7  9
     A            TRNO          16A  O  7 26
     A            MTPY           3A  O  7 44
     A            MSTT          12A  O  7 49
     A            NWRK           6A  O  7 63
     A            CFCPY          2A  O  7 71
     A            CFBRK          2A  O  7 76
     A          R ME0530F5                  SFLCTL(ME0530F4)
     A*  91/06/07  11:57:42    FBANK       REL-R03M00  5728-PW1
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CA05
     A                                      CA12
     A  20                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N20N21                               SFLCLR
     A  22                                  SFLEND
     A N22                                  ROLLUP(23)
     A                                      OVERLAY
     A*                                              Begin 077219
     A************                          C10')
     A*                                              End   077219
     A            DSPREC         4S 0H      SFLRCDNBR
     A**                                             Begin /112563/
     A***********                      23  2'F3-Exit'
     A***********                      23 11'F12-Previous'
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Previous'
     A**                                             End   /112563/
     A*/COPY WNCPYSRC,ME0530DF01
     A                                 23 37'Rollup/Rolldown'
     A                                 23 54'Possible action codes EAP'
     A*/COPY WNCPYSRC,ME0530DF02
     A**                                             Begin /112563/
     A***********                      23 25'F5-Refresh'
     A                                 23 25'F5=Refresh'
     A**                                             End   /112563/
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     *
     **  Message Subfile Control Record format
     *
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
      *
