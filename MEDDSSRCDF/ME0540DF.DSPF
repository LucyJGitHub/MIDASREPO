      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Outgoing payment messages by nostro')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MESSAGE MANAGEMENT                               *
     A*                                                               *
     A*  ME0540DF - OUTGOING PAYMENT MESSAGES BY NOSTRO SUMMARY       *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. 186033             Date 23Feb01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 105621             Date 22Sep97               *
      *                 112563             Date 10Feb97               *
      *                 077219             Date 11Oct94               *
     A*                 057344             DATE 04NOV93               *
     A*                 E39162             DATE 18JUN92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  186033 - Position the cursor at error subfile record         *
     A*  105621 - Change SFLEND to SFLEND(*MORE).                     *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  077219 - Specify correct folder for on-line help.            *
     A*  057344 - Indent parts of multiple messages.                  *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
     A*                                              Begin 077219
     A************                          C10')
     A*                                              End   077219
     A          R ME0540F1
     A*  91/02/21  14:32:43    F7956       REL-R02M00  5728-PW1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            USRID         10A  O  1  2
     A                                  1 13'ME0540'
     A                                  1 22'OUTGOING PAYMENT MESSAGES BY NOSTR-
     A                                      O SUMMARY'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            RUNA           7A  O  1 72
     A            WSID          10A  O  2  2
     A                                  2 71TIME
     A          R ME0540F2
     A*  91/02/25  12:33:18    F7956       REL-R02M00  5728-PW1
     A                                      OVERLAY
     A*
     A*
     A*
     A  41                              3  2'Branch'
     A  41        SBCH           3A  B  3  9
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A            SCCY           3A  B  3 51
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A            SNOS           2   B  3 55
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A            SMOD           2A  B  3 71
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A                                  4 40'Value date'
     A            SDAT           6A  B  4 51
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                  4 60'Network'
     A            SNWK           6A  B  4 71
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A            STS1           1A  B  4  9
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A                                  4 11'Unreleased'
     A            STS2           1A  B  5  9
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A                                  5 11'Released incomplete'
     A            STS3           1A  B  6  9
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A                                  6 11'Released complete'
     A            STS4           1A  B  7  9
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A                                  7 11'Cancelled/released/undelivered'
      *
     A**                                             Begin /112563/
     A***********                      23  2'F3-Exit'
     A                                 23  2'F3=Exit'
     A**                                             End   /112563/
     A                                  3 40'Nostro'
     A                                  3 60'Module'
     A                                  3 67'id'
     A                                  4  2'Status'
     A          R ME0540F3
     A*  91/06/13  16:21:38    FBANK       REL-R03M00  5728-PW1
     A                                      OVERLAY
     A  41                              3  2'Branch'
     A  41        SBCH           3A  O  3  9
     A  41                                  DSPATR(HI)
     A            SMOD           2A  O  3 70DSPATR(HI)
     A                                  4 41'Value date'
     A            SDAT           6A  O  4 52DSPATR(HI)
     A                                  4 60'Network'
     A            SNWK           6A  O  4 70DSPATR(HI)
     A                                  4  2'Status'
     A            SSTS          30A  O  4  9DSPATR(HI)
     A                                  6 32'Msg'
     A  25                              6 73'C/P'
     A  25                              6 77'Brk'
     A                                  7 15'T.R.N.'
     A                                  7 31' typ'
     A  25                              7 73'Cnf'
     A  25                              7 77'Cnf'
     A  41                              7  4'Br.'
     A            SCCY           3A  O  3 52DSPATR(HI)
     A            SNOS           2A  O  3 56DSPATR(HI)
     A                                  3 60'Module'
     A                                  3 67'id'
     A                                  3 45'Nostro'
     A                                  6 49'Value'
     A                                  7 36'Msg status'
     A                                  7 49'Date'
     A                                  7 56'Amount'
     A                                  7  8'Nostro'
     A          R ME0540F4                  SFL
     A*  91/06/13  16:21:38    FBANK       REL-R03M00  5728-PW1
     A  24                                  SFLNXTCHG
     A            SELECT         1A  B  8  2
      *                                               Begin /186033/
     A**38******                            DSPATR(RI)
     A  38                                  DSPATR(RI PC)
      *                                               Begin /186033/
     A            BRCA           3A  O  8  4
     A N41                                  DSPATR(ND)
     A*********              Start of change              /057344/
     A*********   TRNO          16A  O  8 15
     A            DTRNO         16A  O  8 15
     A*                      End of change                /057344/
     A            MTPY           3A  O  8 32
     A            MSTT          12A  O  8 36
     A            SSVDT          6A  O  8 49
     A            SAMTS         17A  O  8 56
     A            CFCPY          2A  O  8 74
     A            CFBRK          2A  O  8 77
     A            CCY            3   O  8  8
     A            SNSNO          2   O  8 12
     A          R ME0540F5                  SFLCTL(ME0540F4)
     A*  91/06/13  16:21:38    FBANK       REL-R03M00  5728-PW1
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CA05
     A                                      CA12
     A  20                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N20N21                               SFLCLR
     A*********              Start of Change               /105621/
     A**22*****                             SFLEND
     A  22                                  SFLEND(*MORE)
     A*                      End of Change                 /105621/
     A N22                                  ROLLUP(23)
     A                                      OVERLAY
     A*                                              Begin 077219
     A************                          C10')
     A*                                              End   077219
     A            DSPREC         4S 0H      SFLRCDNBR
     A**                                             Begin /112563/
     A***********                      23  2'F3-Exit'
     A***********                      23 11'F12-Previous'
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Previous'
     A**                                             End   /112563/
     A*/COPY WNCPYSRC,ME0540DF01
     A                                 23 37'Rollup/Rolldown'
     A                                 23 54'Valid action codes'
     A*/COPY WNCPYSRC,ME0540DF02
     A            SATN           3A  O 23 73
     A**                                             Begin /112563/
     A***********                      23 25'F5-Refresh'
     A                                 23 25'F5=Refresh'
     A**                                             End   /112563/
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     *
     **  Message Subfile Control Record format
     *
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
      *
