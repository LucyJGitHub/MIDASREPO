      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Outgoing messages status review')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MESSAGE MANAGEMENT                               *
     A*                                                               *
     A*   ME0550DF - OUTGOING MESSAGES STATUS REVIEW SUMMARY          *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. 186033             Date 23Feb01               *
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 112563             Date 10Feb97               *
      *                 077219             Date 11Oct94               *
     A*                 E39162             DATE 18JUN92               *
     A*                 E29588                  25NOV91               *
     A*                 E32330                  19NOV91               *
     A*                                                               *
     A********************************************************************
     A*                                                               *
      *  186033 - Position cursor at error subfile record             *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  077219 - Specify correct folder for on-line help.            *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*   E29588 - POST COLLECTION SWIFT RATIONALISATION AMENDMENTS   *
     A*             o Indent parts of multiples                       *
     A*   E32330 - BHF IMPLEMENTATION SPECIFIC CHANGES                *
     A*             o 'Today's work' amendment                        *
     A*             o limit users to specific modules                 *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
     A*                                              Begin 077219
     A************                          C10')
     A*                                              End   077219
     A          R ME0550F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            USRID         10A  O  1  2
     A                                  1 13'ME0550'
     A                                  1 24'OUTGOING MESSAGES STATUS REVIEW SU-
     A                                      MMARY'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            RUNA           7A  O  1 72
     A            WSID          10A  O  2  2
     A                                  2 71TIME
     A          R ME0550F2
     A                                      OVERLAY
     A*
     A*
     A*
     A  41                              3  2'Branch'
     A  41        SBCH           3A  B  3  9
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A                                  4 42'Module'
     A            SMOD           2A  B  4 52
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A*                                                    /*E29588*/
     A N40                                  DSPATR(UL)
     A  40                                  DSPATR(PR)
     A*                                                    /*E29588*/
     A                                  4 56'Start date'
     A            SDAT           6A  B  4 67
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                  3 42'External system'
     A            SSYS           6A  B  3 58
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A*/COPY WNCPYSRC,ME0550DF01
     A                                  4  2'Status'
     A            STS1           1A  B  4  9
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A                                  4 11'Unreleased'
     A            STS2           1A  B  5  9
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A                                  5 11'Released incomplete'
     A            STS3           1A  B  6  9
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A                                  6 11'Released complete'
     A            STS4           1A  B  7  9
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A                                  7 11'Cancelled/released/undelivered'
      *
     A**                                             Begin /112563/
     A***********                      23  2'F3-Exit'
     A                                 23  2'F3=Exit'
     A**                                             End   /112563/
     A            SNWK           6A  B  3 30
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A            SPRY           1A  B  4 78
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                  3 22'Network'
     A                                  4 49'id'
     A                                  4 75'Pr'
     A          R ME0550F3
     A*  91/02/22  09:42:47    F7956       REL-R02M00  5728-PW1
     A                                      OVERLAY
     A  41                              3  2'Branch'
     A  41        SBCH           3A  O  3  9
     A  41                                  DSPATR(HI)
     A            SNWK           6A  O  3 28DSPATR(HI)
     A                                  4 43'Module'
     A            SMOD           2A  O  4 53DSPATR(HI)
      * Start of E32330
     A***********                       4 57'Start date'
     A*********** SDAT           6A  O  4 68DSPATR(HI)
     A N39                              4 57'Start date'
     A N39        SDAT           6A  O  4 68DSPATR(HI)
     A  39                              4 57'Today''s work only'
      * End of E32330
     A            SPRY           1A  O  4 79DSPATR(HI)
     A                                  3 43'External system'
     A            SSYS           6A  O  3 59DSPATR(HI)
     A                                  4  2'Status'
     A            SSTS          30A  O  4  9DSPATR(HI)
     A*/COPY WNCPYSRC,ME0550DF02
     A  25                              6 73'C/P'
     A  25                              6 77'Brk'
     A  25                              7 73'Cnf'
     A  25                              7 77'Cnf'
     A  41                              7  4'Brch'
     A                                  3 20'Network'
     A                                  4 50'id'
     A                                  4 76'Pr'
     A                                  6 10'Creation'
     A                                  7 10'Date'
     A                                  7 19'T.R.N.'
     A                                  6 37'Msg'
     A                                  7 37'Typ '
     A                                  7 42'Pr'
     A                                  6 46'Del'
     A                                  7 46'Not'
     A                                  6 50'Non'
     A                                  7 50'Del'
     A                                  7 55'Message'
     A                                  7 63'status'
     A                                  7 71'Network'
     A          R ME0550F4                  SFL
     A*  91/02/22  09:42:47    F7956       REL-R02M00  5728-PW1
     A  24                                  SFLNXTCHG
     A            SELECT         1A  B  8  2
      *                                              Begin /186033/
     A**38******                            DSPATR(RI)
     A  38                                  DSPATR(RI PC)
      *                                              End   /186033/
     A            BRCA           3A  O  8  4
     A N41                                  DSPATR(ND)
     A            MGDEF          6A  O  8 10
      * Start of E29588
     A************TRNO          16A  O  8 19
     A            DTRNO         17A  O  8 19
      * End of E29588
     A            MTPY           3A  O  8 37
     A            DLNOT          1A  O  8 47
     A            NDELW          1A  O  8 51
     A            MSTT          12A  O  8 55
     A            NWRKF          6A  O  8 71
     A            MPRY           1A  O  8 42
     A          R ME0550F5                  SFLCTL(ME0550F4)
     A*  91/06/07  11:57:42    FBANK       REL-R03M00  5728-PW1
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CA05
     A                                      CA12
     A  20                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N20N21                               SFLCLR
     A  22                                  SFLEND
     A N22                                  ROLLUP(23)
     A                                      OVERLAY
     A*                                              Begin 077219
     A************                          C10')
     A*                                              End   077219
     A            DSPREC         4S 0H      SFLRCDNBR
     A**                                             Begin /112563/
     A***********                      23  2'F3-Exit'
     A***********                      23 11'F12-Previous'
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Previous'
     A**                                             End   /112563/
     A*/COPY WNCPYSRC,ME0550DF03
     A                                 23 37'Rollup/Rolldown'
     A                                 23 54'Valid action codes'
     A*/COPY WNCPYSRC,ME0550DF04
     A            SATN           3A  O 23 73
     A**                                             Begin /112563/
     A***********                      23 25'F5-Refresh'
     A                                 23 25'F5=Refresh'
     A**                                             End   /112563/
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     *
     **  Message Subfile Control Record format
     *
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
      *
