      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Outgoing messages by TRN')
     A*  91/06/07  11:57:42    FBANK       REL-R03M00  5728-PW1
     A*****************************************************************
     A*                                                               *
     A*  Midas - MESSAGE MANAGEMENT
     A*                                                               *
     A*   ME0570DF - OUTGOING MESSAGES BY T.R.N.                      *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. 186033             Date 23Feb01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 112563             Date 10Feb97               *
      *                 E39162             Date 18Jun92               *
     A*                 E00000                  DDMMMYY               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  186033 - Position the cursor on the error subfile record.    *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*      LAST AMEND NO. S01342           DATE 09AUG91             *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*      S01342 - RELEASE 10 SD HELP INCORPORATION                *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
     A*                                                    /*S01342*/
     A*                                                    /*S01342*/
     A          R ME0570F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            USRID         10A  O  1  2
     A                                  1 13'ME0570'
     A            RUNA           7A  O  1 72
     A            WSID          10A  O  2  2
     A                                  1 28'OUTGOING MESSAGES BY T.R.N'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A          R ME0570F2                  OVERLAY
     A*                                                    /*S01342*/
     A*                                                    /*S01342*/
     A                                  3  2'Transaction reference number'
     A            STRN          16   B  3 31
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A**                                             Begin /112563/
     A***********                      23  2'F3-Exit'
     A                                 23  2'F3=Exit'
     A**                                             End   /112563/
      *
     A          R ME0570F3
     A                                      OVERLAY
     A                                  3  2'Transaction reference number'
     A            STRN          16A  O  3 31
     A                                      DSPATR(HI)
     A                                  6  9'T.R.N.'
     A                                  5 27'Transaction'
     A                                  6 27'no.'
     A                                  5 44'Msg'
     A  25                              5 71'C/p'
     A  25                              5 75' Brk  '
     A                                  6 44'type'
     A                                  6 49'Msg status'
     A                                  6 63'Network'
     A  25                              6 71'cnf '
     A  25                              6 76'cnf '
     A  41                              6  4'Brch'
     A          R ME0570F4                  SFL
     A  24                                  SFLNXTCHG
     A            SELECT         1A  B  7  2
      *                                               Begin /186033/
     A**38******                            DSPATR(RI)
     A  38                                  DSPATR(RI PC)
      *                                               End   /186033/
     A            BRCA           3A  O  7  4
     A N41                                  DSPATR(ND)
     A            TRNO          16A  O  7  9
     A            MTRN          15A  O  7 27
     A            MTPY           3A  O  7 44
     A            MSTT          12A  O  7 49
     A            NWRK           6A  O  7 63
     A            CFCPY          2A  O  7 71
     A            CFBRK          2A  O  7 76
     A          R ME0570F5                  SFLCTL(ME0570F4)
     A*  91/06/07  11:57:42    FBANK       REL-R03M00  5728-PW1
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CA05
     A                                      CA12
     A  20                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N20N21                               SFLCLR
     A  22                                  SFLEND
     A N22                                  ROLLUP(23)
     A                                      OVERLAY
     A*                                                    /*S01342*/
     A*                                                    /*S01342*/
     A            DSPREC         4S 0H      SFLRCDNBR
     A**                                             Begin /112563/
     A***********                      23  2'F3-Exit'
     A***********                      23 11'F12-Previous'
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Previous'
     A**                                             End   /112563/
     A*/COPY WNCPYSRC,ME0570DF02
     A                                 23 37'Rollup/Rolldown'
     A*/COPY WNCPYSRC,ME0570DF01
     A                                 23 54'Possible action codes EAP'
     A*/COPY WNCPYSRC,ME0570DF03
     A**                                             Begin /112563/
     A***********                      23 25'F5-Refresh'
     A                                 23 25'F5=Refresh'
     A**                                             End   /112563/
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     *
     **  Message Subfile Control Record format
     *
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
      *
