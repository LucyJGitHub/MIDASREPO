      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Outgoing message enquiry/maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - MESSAGE MANAGEMENT                                   *
      *                                                               *
      *   ME0600DF - OUTGOING MESSAGES MAINTENANCE                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  LAST AMEND NO. 104633             DATE 16JUL96               *
      *  PREV AMEND NO. E39162             DATE 18JUN92               *
      *                 E00000                  DDMMMYY               *
      *                                                               *
     A*****************************************************************
     A*                                                               *
     A*  104633 - GUI Amendments to DSPF with non-standard footers    *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*                                                               *
     A*****************************************************************
      *                                                               *
      *         /////////////////////////////////////////             *
      *        // WARNING: When creating this display //              *
      *       //   file, take options RSTDSP(*YES)   //               *
      *      //                       SHARE (*YES)  //                *
      *     /////////////////////////////////////////                 *
      *                                                               *
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *****************************************************************
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA12
      *
     A          R ME0600F1                  OVERLAY
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            USRID         10A  O  1  2
     A                                  1 13'ME0600'
     A  70                              1 22'OUTGOING MESSAGE DETAILS +
                                            MAINTENANCE'
     A  70                                  DSPATR(UL HI)
     A N70                              1 22'OUTGOING MESSAGE DETAILS +
                                            ENQUIRY'
     A N70                                  DSPATR(UL HI)
     A            RUNA           7A  O  1 72
     A            WSID          10   O  2  2
     A                                  2 71TIME
      *
     A          R ME0600F2                  SFL
     A            RCD           78A  O  3  2
      *
     A          R ME0600F3                  SFLCTL(ME0600F2)
     A  20                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N20N21                               SFLCLR
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0018)
     A  22                                  SFLEND
     A                                      VLDCMDKEY(31)
     A                                      OVERLAY
     A  23                                  CA08
     A  24                                  CA09
     A  25                                  CA10
     A  26                                  CA20
     A  27                                  CA24
      *
      *
     A*/COPY WNCPYSRC,ME0600DF04
     A  28                             21  2'TRN of message or part to delete'
     A  28        STRN          16A  B 21 35
     A N40                                  DSPATR(HI)
     A  40                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,ME0600DF05
      *
     A  29                             21  2'Network'
     A  29        SNWK           6A  B 21 10
     A N41 29                               DSPATR(HI)
     A  41 29                               DSPATR(RI PC)
     A  29 62                          21 17'Pty'
     A  29 62     SPTY           1A  B 21 21
     A N42 29 62                            DSPATR(HI)
     A  42 29 62                            DSPATR(RI PC)
     A  29 62                          21 23'Delivery'
     A  29 62     SDEL           1A  B 21 32
     A N43 29 62                            DSPATR(HI)
     A  43 29 62                            DSPATR(RI PC)
     A  29 62                          21 34'Non delivery'
     A  29 62     SNDL           1A  B 21 47
     A N44 29 62                            DSPATR(HI)
     A  44 29 62                            DSPATR(RI PC)
      *
     A*/COPY WNCPYSRC,ME0600DF02
     A  30 60                          21  2'Verification Date'
     A*/COPY WNCPYSRC,ME0600DF03
     A  30 60     SDAT           6A  B 21 20
     A N45 30 60                            DSPATR(HI)
     A  45 30 60                            DSPATR(RI PC)
     A  30 60                          21 27'Ccy'
     A  30 60     SCCY           3A  B 21 31
     A N45 30 60                            DSPATR(HI)
     A  45 30 60                            DSPATR(RI)
     A  30 60                          21 35'Amt'
     A  30 60     SAMT          15A  B 21 39
     A N45 30 60                            DSPATR(HI)
     A  45 30 60                            DSPATR(RI)
     A  30 61                          21 55'Testkey'
     A  30 61     STKY          15A  B 21 63
     A N46 30 61                            DSPATR(HI)
     A  46 30 61                            DSPATR(RI)
     A*/COPY WNCPYSRC,ME0600DF01
      *
      **                                            Start   /104633/
     A*****                            23  2'F12-Previous'
     A**23*                            23 16'F8-Next Msg'
     A**24*                            23 29'F9-Previous Msg'
     A**25*                            23 46'F10-Delete'
     A**26*                            23 58'F20-Amend'
     A**27*                            23 69'F24-Release'
     A                                 23  2'F12=Previous'
     A  23                             23 16'F8=Next Msg'
     A  24                             23 29'F9=Previous Msg'
     A  25                             23 46'F10=Delete'
     A  26                             23 58'F20=Amend'
     A  27                             23 69'F24=Release'
      **                                            End     /104633/
      *
      **  Message subfile Record format
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
      *
      **  Message Subfile Control Record format
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
