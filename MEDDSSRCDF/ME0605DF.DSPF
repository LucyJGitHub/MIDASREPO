      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Outgoing Messages release by range')
     A*****************************************************************
     A*                                                               *
     A*  Midas        MESSAGE MANAGEMENT
     A*                                                               *
     A*  ME0605DF - Outgoing Messages release by range                *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. E39162             DATE 18JUN92               *
     A*  PREV AMEND NO. E00000                  DDMMMYY               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
     A                                      CA12
      *
      *
     A          R INITSCN                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
      *
      *
     A            PGMUSR        10   O  1  2
     A                                  1 13'ME0605'
     A                                  1 27'OUTGOING MESSAGES RELEASE BY -
     A                                      RANGE'
     A                                      DSPATR(HI UL)
     A            RDAT           7   O  1 72
     A            PGMDEV        10   O  2  2
     A                                  2 72TIME
      *
     A                                  4  7'Network'
     A            SNWRK          6   B  4 18
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL)
      *
     A                                  6  7'Transaction reference number'
     A                                  6 36'range - from'
     A            SFTRNO        16   B  6 50
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL)
     A                                  7 45'to'
     A            STTRNO        16   B  7 50
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL)
      *
     A                                 23  2'F3-Exit   F12-Previous'
      *
      **  Message subfile Record format
      *
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     *
     **  Message Subfile Control Record format
     *
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
      *
