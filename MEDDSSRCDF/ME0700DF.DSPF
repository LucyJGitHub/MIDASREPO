      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Free format message maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MESSAGE MANAGEMENT                               *
     A*                                                               *
     A*  ME0700DF - FREE FORMAT MESSAGE MAINTENANCE                   *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 093907             Date 26Jul96               *
      *                 034540             Date 29Mar94               *
     A*                 E39162             DATE 18JUN92               *
     A*                 E33164             DATE 06FEB92               *
     A*                 E32501             DATE 29NOV91               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  093907 - 'Position Cursor' not working correctly.  Fix is to *
     A*           condition 'PC' on *IN67 on second screen.           *
     A*  034540 - After typing the first page of a free format mes-   *
     A*           sage, when 'PAGE DOWN' is entered, the cursor shld  *
     A*           return to the top of the text area.                 *
     A*         - Conditioned 'PC' on a different indicator and added *
     A*           a SFLRCDNBR keyword.                                *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*  E33164 - CHANGE LENGTH OF INPUT RECORD TO 35*50              *
     A*  E32501 - DISPLAY TRN OF MESSAGE BEING INSERTED               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
      *
      *
     A          R ME0700F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            USRID         10   O  1  2
     A                                  1 13'ME0700'
     A                                  1 25'FREE FORMAT MESSAGE MAINTENANCE'
     A                                      DSPATR(UL HI)
     A            RUNA           7   O  1 72
     A            WSID          10   O  2  2
     A                                  2 71TIME
      *
     A          R ME0700F2
     A                                      OVERLAY
      *
      *
      *
     A                                  3 02'Action'
     A            SATN           1   B  3 09
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A                                  3 11'TRN'
     A            STRN          16   B  3 15
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A                                 23  2'F3-Exit'
      *
     A          R ME0700F3                  SFL
     A  24                                  SFLNXTCHG
     A*/COPY WNCPYSRC,ME0700DF01
      * START E33164
     A*********** RCD           35   B  7 23
     A            RCD           50   B  7 15
     A*/COPY WNCPYSRC,ME0700DF02
      * END E33164
     A N38                                  DSPATR(HI)
     A*********              Start of Change               /034540/
     A**38*****                             DSPATR(RI PC)
     A  38                                  DSPATR(RI)
     A*                      End of Change                 /034540/
     A N43                                  DSPATR(UL)
     A  43                                  DSPATR(PR)
     A*********              Start of Change               /034540/
     A N67                                  DSPATR(PC)
     A*                      End of Change                 /034540/
      *
     A          R ME0700F4                  SFLCTL(ME0700F3)
     A                                      CA12
     A                                      CA10
     A  20                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N20N21                               SFLCLR
     A  23                                  SFLINZ
     A                                      SFLSIZ(0035)
     A*/COPY WNCPYSRC,ME0700DF03
     A                                      SFLPAG(0015)
     A*/COPY WNCPYSRC,ME0700DF04
     A  22                                  SFLEND
     A                                      OVERLAY
     A*/COPY WNCPYSRC,ME0700DF05
      *
     A*/COPY WNCPYSRC,ME0700DF06
      *
      *
      *
      *
     A  41                              4  2'Branch'
     A  41        SBCH           3   B  4  9
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A  42                                  DSPATR(UL)
     A N42                                  DSPATR(PR)
      *
     A                                  4 13'Address'
     A            SADR          20   B  4 21
     A N35                                  DSPATR(HI)
     A*********              Start of Change               /093907/
     A**35*************                     DSPATR(RI PC)
     A  67                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A*********              END of Change                 /093907/
     A  42                                  DSPATR(UL)
     A N42                                  DSPATR(PR)
      *
     A  46                              4 42'or destination customer'
     A  46        SCUS          10   B  4 66
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI PC)
     A  42                                  DSPATR(UL)
     A N42                                  DSPATR(PR)
      *
     A                                 05 13'Network'
     A            SNWK           6   B 05 21
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI PC)
     A  42                                  DSPATR(UL)
     A N42                                  DSPATR(PR)
      *
     A                                 05 28'Message type'
     A            STYP           3   B 05 41
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI PC)
     A  42                                  DSPATR(UL)
     A N42                                  DSPATR(PR)
     A*********              Start of Change               /034540/
     A            SRRN           4S 0H      SFLRCDNBR(CURSOR)
     A*                      End of Change                 /034540/
      *
      * Start of E32501
     A     55                          05 45'TRN'
     A     55     STRN          16   B 05 49
     A N33 55                               DSPATR(HI)
     A  33 55                               DSPATR(RI PC)
     A  42 55                               DSPATR(UL)
     A N42 55                               DSPATR(PR)
      * End of E32501
      *
     A*/COPY WNCPYSRC,ME0700DF07
     A          R ME0700F5
     A                                      OVERLAY
     A                                 23  2'F3-Exit F12-Previous'
     A  22                             23 24'Rollup/Rolldown'
      *
      **  Message subfile Record format
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     *
     **  Message Subfile Control Record format
     *
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
      *
