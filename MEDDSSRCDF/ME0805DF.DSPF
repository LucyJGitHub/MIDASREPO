      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Outgoing message details report')
     A*****************************************************************
     A*                                                               *
     A*  Midas        MESSAGE MANAGEMENT
     A*                                                               *
     A*  ME0805DF - Outgoing message details report                   *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. E39162             Date 18Jun92               *
      *                                    Date                       *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*                                                               *
     A*****************************************************************
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
     A                                      CA12
      *
      *
     A          R INITSCN                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
      *
      *
     A            PGMUSR        10   O  1  2
     A                                  1 13'ME0805'
     A            RDAT           7   O  1 72
     A            PGMDEV        10   O  2  2
     A                                  1 27'OUTGOING MESSAGES DETAIL REPORT'
     A                                      DSPATR(HI UL)
     A                                  2 72TIME
      *
     A                                  4  7'Module id'
     A            SMODID         2   B  4 18
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL)
      *
     A                                  4 57'External system'
     A            SEXSYI         6   B  4 73
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL)
      *
     A                                  6  7'Network'
     A            SNTWRK         6   B  6 18
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL)
      *
     A                                  6 57'Message group'
     A            SMSGRP         3   B  6 73
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL)
      *
     A                                  8  7'Status'
     A            SURLST         1   B  8 18
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
      *
     A                                  8 20'unreleased'
     A                                  8 57'Priority'
     A            SPTY           1   B  8 73
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL)
      *
     A            SRICST         1   B  9 18
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL)
     A                                  9 20'released incomplete'
      *
     A            SCRLST         1   B 10 18
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL)
     A                                 10 20'released complete'
      *
     A            SRCMST         1   B 11 18
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL)
     A                                 11 20'cancelled/released/undelivered'
      *
     A                                 13  7'Creation date range'
     A                                 13 42'- from'
     A            SFMDAT         6   B 13 49
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL)
      *
     A                                 14 44'to'
     A            STODAT         6   B 14 49
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL)
      *
     A                                 16  7'Transaction reference number range'
     A                                 16 42'- from'
     A            SFMTRN        16   B 16 49
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(UL)
     A                                 17 44'to'
     A            STOTRN        16   B 17 49
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(UL)
      *
     A          R FOOTER                    OVERLAY
     A                                 23  2'F3=Exit   F12=Previous'
      *
      **  Message subfile Record format
      *
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     *
     **  Message Subfile Control Record format
     *
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
      *
