      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Outgoing payment messages by nostro')
     A*****************************************************************
     A*                                                               *
     A*  Midas MESSAGE MANAGEMENT                                 *
     A*                                                               *
     A*  MS0815DF - Outgoing payment messages by nostro report        *
     A*             Prompt screen                                     *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. E39162             DATE 18JUN92               *
     A*  PREV AMEND NO. E00000                  DDMMMYY               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
     A                                      CA12
      *
      *
     A          R INITSCN                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
      *
      *
     A            PGMUSR        10A  O  1  2
     A                                  1 13'ME0815'
     A            RDAT           7A  O  1 72
     A            PGMDEV        10A  O  2  2
     A                                  1 27'OUTGOING PAYMENT MESSAGES BY NOST-
     A                                      RO'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  2 72TIME
     A                                  4  7'Module id'
     A            SMODID         2A  B  4 18
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A                                  6  7'Nostro'
     A            SCCY           3A  B  6 18
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A            SNOSN          2A  B  6 22
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(UL)
     A                                  8  7'Network'
     A            SNTWRK         6A  B  8 18
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(UL)
     A                                 10  7'Value date range'
     A                                 10 24'- from'
     A            SFMDAT         6A  B 10 31
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N41                                  DSPATR(UL)
     A                                 11 26'to'
     A            STODAT         6A  B 11 31
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N42                                  DSPATR(UL)
     A                                 23  2'F3=Exit   F12=Previous'
      *
      **  Message subfile Record format
      *
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     *
     **  Message Subfile Control Record format
     *
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
      *
