      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Outgoing messages by transaction')
     A*****************************************************************
     A*                                                               *
     A*  Midas        MESSAGE MANAGEMENT
     A*                                                               *
     A*  ME0825DF - Outgoing by transaction report                   *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. E39162             Date 18Jun92               *
      *                                    Date                       *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
     A                                      CA12
      *
      *
     A          R INITSCN                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
      *
      *
     A            PGMUSR        10   O  1  2
     A                                  1 13'ME0825'
     A                                  1 27'OUTGOING MESSAGES BY TRANSACTION'
     A                                      DSPATR(HI UL)
     A            RDAT           7   O  1 72
     A            PGMDEV        10   O  2  2
     A                                  2 72TIME
      *
     A                                  4  7'Module id'
     A            SMODID         2   B  4 18
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL)
      *
     A                                  6  7'Midas transaction number range'
     A                                  6 38'- from'
     A            SFMTRN        15   B  6 45
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL)
     A                                  7 40'to'
     A            STOTRN        15   B  7 45
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL)
      *
     A                                 23  2'F3=Exit   F12=Previous'
      *
      **  Message subfile Record format
      *
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     *
     **  Message Subfile Control Record format
     *
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
      *
