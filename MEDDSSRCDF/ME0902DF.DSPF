      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MG MT970 Generation Request')                    *
      *****************************************************************
      *                                                               *
      *  Midas - ME Module                                            *
      *                                                               *
      *  DSPF/ME0902DF - MT970 Generation Request                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      *  Last Amend No. AR689705           Date 03Jan11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSW099  *CREATE    Date 28Feb05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  AR689705 - Nominal and value settled were missing after      *
      *             enter. Applied fix AR656294 (Child: AR689706)     *
      *  AR656294 - HATS issue                                        *
      *  CSW099 - Creation of MT970 Netting Statements                *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
     A                                      HELP
      *
     A          R ME0902F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
      *                                     CF03
      **Start of change AR656294
     A                                      CF03
     A  13                                  CF10
     A  20                                  CF12
      **End of change AR656294
     A                                      FRCDTA
     A                                      OVERLAY BLINK PUTOVR
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'ME0902'
     A                                  1 22'MT970 Generation Request'
     A                                      DSPATR(UL HI)
     A            BJMRDT         7A  O  1 73TEXT('Midas Rundate')
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
      *
     A            STIME          6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  5  5'Value Date      :'
     A            LVDTE          6A  B  5 23
     A  10                                  DSPATR(RI PC)
     A N10N15                               DSPATR(UL)
     A  15                                  DSPATR(PR)
     A                                      OVRDTA
      *
     A          R ME0902F1                  TEXT('Screen Footer')
     A                                      CF03
     A  13                                  CF10
     A  20                                  CF12
     A                                      OVERLAY
     A                                 23  3'F3=End'
     A  20                             23 12'F12=Cancel'
      *
     A          R ME0902S1                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            SMSGKY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R ME0902C1                  TEXT('Program messages')
     A                                      SFLCTL(ME0902S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
