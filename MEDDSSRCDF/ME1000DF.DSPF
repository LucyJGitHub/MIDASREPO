      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Display Incoming Messages')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module
     A*                                                            *
     A*   DSPF/ME1000DF - Display Incoming Messages Display file   *
     A*                                                            *
     A*  (c) Finastra International Limited 2001                   *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. 090199             Date 12JUL95               *
     A*    PREV AMEND NO. 069867       DATE 22APR94                *
     A*         AMEND NO. S01435       DATE 29JUL93                *
     A*                                                            *
     A**************************************************************
     A*                                                            *
     A*  MD046248 - Finastra Rebranding                               *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*    069867 - File was collected with a keyword option which *
     A*             is not supported at OS/400 releases before     *
     A*             V2R2M0.  As we must support V2R1M1 at this     *
     A*             time, this part of the change must be reversed *
     A*             out.                                           *
     A*    S01435 - Incoming Message Management                    *
     A*                                                            *
     A**************************************************************
     A*                                                               *
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
      *================================================================
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1MSGR         8  0H      TEXT('Msg Unique Reference')
     A            #1TRNO        16   H      TEXT('Transaction reference no.')
     A            #1CCYP         3   H      TEXT('Currency Code      (Part)')
     A            #1VDTP         6   H      TEXT('Value date  YYMMDD (Part)')
     A            #1AMTP        17   H      TEXT('Payment Amount     (Part)')
     A            #1INBK       100   H      TEXT('Intermediary')
     A            #1ACIN       100   H      TEXT('A/c with Institution')
     A            #1BENF       100   H      TEXT('Beneficiary')
     A            #1KPRT         3  0H      TEXT('Part Number (Key)')
     A            #1BRCP         3   H      TEXT('Branch Code        (Part)')
     A            #1INFG         7   H      TEXT('Incoming Flag')
     A            #1IPRF        15   H      TEXT('Incoming Payment Ref.')
     A            #1OTFG         7   H      TEXT('Outgoing Flag')
     A            #1OPRF        15   H      TEXT('Outgoing Payment Ref.')
     A            #1FRGL        18   H      TEXT('Funds From')
     A            #1TOGL        18   H      TEXT('Funds To')
     A            #1SYTM         6   H      TEXT('System')
     A            #1NWRK         6   H      TEXT('Network')
     A            #1SNDR        20   H      TEXT('Sender')
     A            #1MIR         28   H      TEXT('Message Input reference')
     A            #1MOR         28   H      TEXT('Message Output reference')
     A            #1MTPY         3   H      TEXT('Message type')
     A            #1MPRY         1   H      TEXT('Message Priority')
     A            #1MODE         6  0H      TEXT('Message Received Date')
     A            #1MOTM         4  0H      TEXT('Message Received Time')
     A            #1SNTD         6  0H      TEXT('Message Sent Date')
     A            #1SNTT         4  0H      TEXT('Message Sent Time')
     A            #1CYCD         3   H      TEXT('Currency Code       (key)')
     A            #1SVDT         6   H      TEXT('Value date  YYMMDD format')
     A            #1AMTS        17   H      TEXT('Payment Amount')
     A            #1CRCY         3   H      TEXT('Currency Code       (CR)')
     A            #1CRDT         6   H      TEXT('Value date  YYMMDD  (CR)')
     A            #1CRPA        17   H      TEXT('Payment Amount      (CR)')
     A            #1BRCA         3   H      TEXT('Branch Code')
     A            #1DUPF         3   H      TEXT('Duplication Flag')
     A            #1NPRT         3  0H      TEXT('Number of Parts')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A*/COPY WNCPYSRC,ME1000DF01
     A            #1JOB         10   H      TEXT('Job name')
     A            #1USR         10   H      TEXT('User name')
     A            #1JNO          6  0H      TEXT('*Job number')
     A            #1SEL          1   B  6  2
     A  79                                  DSPATR(PR)
     A N79N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A            #RNT76        76      6  4TEXT('Narrative Text 76')
     A  33                                  DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(15) SFLSIZ(16)
     A                                      BLINK OVERLAY
     A                                      KEEP
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
      *
      *                                              Begin /069867/
      ** The SFLEND(*MORE) option has been removed because it is not
      ** supported at releases of OS/400 prior to V2R2M0.  At a later date,
      ** when support for V2R1M1 is not required, this option should be
      ** reinstated.
      ***If*version*2.1*or*lessof*OS/400*reinstate*next*line****************
      *
     A*N80*81*82****************************SFLEND**************************
     A*N80*81*82*                           SFLEND(*MORE)
     A N80 81 82                            SFLEND
      *                                                End /069867/
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A  77                                  CF12(12 'CF12 Previous')
     A                                      CF17(17 'CF17 Selection')
     A  78                                  CF20(20 'CF20 Return with select -
     A                                       ion')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #CNR78')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'ME1000F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  81        #CNR78        78      4  2TEXT('Narrative 78')
     A N25                                  OVRDTA
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              5  2'?'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81        #CNR76        76      5  4TEXT('Narrative 76')
     A N25                                  OVRDTA
     A                                      DSPATR(HI) COLOR(WHT)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
