      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Display incoming message')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module
     A*                                                            *
     A*   DSPF/ME1060DF - Display Incoming Messages Display trans. *
     A*                                                            *
      *  (c) Finastra International Limited 2001                      *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CFT006             Date 22Nov99               *
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CFT003             Date 20Nov98               *
     A*                   090199       Date 12Jul95                *
     A*                   069867       DATE 22APR94                *
     A*                   S01435       DATE 29JUL93                *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  MD046248 - Finastra Rebranding                               *
      *    CFT006 - Funds Transfer addition of MT101 and MT102 msgs*
     A*    CFT003 - FT Batch Authorisation                         *
     A*    090199 - Recompile to include new Multi-Language Suport *
     A*             messages IDs from 10.6 packaging into core.    *
     A*    069867 - File was collected with a keyword option which *
     A*             is not supported at OS/400 releases before     *
     A*             V2R2M0.  As we must support V2R1M1 at this     *
     A*             time, this part of the change must be reversed *
     A*             out.                                           *
     A*    S01435 - Incoming Message Management                    *
     A*                                                            *
     A**************************************************************
     A*                                                               *
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
      *================================================================
      *================================================================
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SFL
     A  84                                  SFLNXTCHG
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(31 'Invalid: #1SEL')
     A            #1MSGR         8  0H      TEXT('Msg Unique Reference')
     A            #1SEQN         3  0H      TEXT('Sequence Number')
     A            #1MTG          5   H      TEXT('Message Tag')
     A            #1PART         3  0H      TEXT('Part Number')
     A            #1FPID         1   H      TEXT('First Part Identifier')
     A            #1MFLD       100   H      TEXT('Message detail')
     A            #1SEL          1   B  9  2
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N79N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A            #RNT76        76      9  4TEXT('Narrative Text 76')
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(12) SFLSIZ(13)
     A                                      BLINK OVERLAY
     A                                      KEEP
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
      *                                              Begin /069867/
      ** The SFLEND(*MORE) option has been removed because it is not
      ** supported at releases of OS/400 prior to V2R2M0.  At a later date,
      ** when support for V2R1M1 is not required, this option should be
      ** reinstated.
     A*N25*******
      *
      ***If*version*2.1*or*lessof*OS/400*reinstate*next*line****************
      *
     A*AN80*****************************************************************
     A*A*81**********************************SFLDSP*SFLEND******************
     A*AN80******
     A*A*81******                            SFLDSP SFLEND(*MORE)
     A N25
     AAN80
     AA 81                                  SFLDSP SFLEND
      *                                                End /069867/
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'CF12 Previous')
     A  76                                  CF10(10 'CF10 Ignore')
     A  77                                  CF14(14 'CF14 FT Incoming')
     A  78                                  CF18(18 'CF18 FT Outgoing')
     A*                                                    /CFT006/
     A  74                                  CF19
     A  75                                  CF20
     A*                                                    /CFT006/
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR(CURSOR)
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'ME1060F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2MSGR         8  0H      TEXT('Msg Unique Reference')
     A            #2SYTM         6   H      TEXT('System')
     A            #2CYCD         3   H      TEXT('Currency Code       (key)')
     A            #2SVDT         6   H      TEXT('Value date  YYMMDD format')
     A            #2AMTS        17   H      TEXT('Payment Amount')
     A            #2PART         3  0H      TEXT('Part Number')
     A N88                              4  2'Network'
     A N88        #2NWRK         6      4 10TEXT('Network')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #2SEL          1   B  4 17
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N79N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRDTA
     A N88                              4 19'Sender'
     A N88        #2SNDR        20      4 26TEXT('Sender')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,ME1060DF04
     A N70
     AAN88                              4 54'Branch'
     A N70
     AAN88        #2BRCA         3      4 61TEXT('Branch Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,ME1060DF03
     A N88                              5  2'Msg Input Ref.'
     A N88        #2MIR         28      5 18TEXT('Message Input reference')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N88                              5 48'Message type'
     A N88        #2MTPY         3      5 61TEXT('Message type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N88                              5 65'Priority'
     A N88        #2MPRY         1      5 74TEXT('Message Priority')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N88                              6  2'Msg Output Ref.'
     A N88        #2MOR         28      6 18TEXT('Message Output reference')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N88                              6 48'Number of Parts'
     A N88        #2NPRT         3  0   6 64TEXT('Number of Parts')
     A                                      EDTCDE(2)
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N88                              7  2'Received Date'
     A N88        #2MODE         6  0   7 16TEXT('Message Received Date')
     A                                      EDTWRD('  /  /  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N88        #2MOTM         4  0   7 26TEXT('Message Received Time')
     A                                      EDTWRD('  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N88                              7 33'Sent Date'
     A N88        #2SNTD         6  0   7 43TEXT('Message Sent Date')
     A                                      EDTWRD('  /  /  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N88        #2SNTT         4  0   7 53TEXT('Message Sent Time')
     A                                      EDTWRD('  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A N88                              7 60'Duplication Flag'
     A N88        #2DUPF         3      7 77TEXT('Duplication Flag')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,ME1060DF02
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,ME1060DF01
      *                                                     Str CFT003
     A*=====================================================================
     A*  Format #WINDOW provides the Changes reminder display.             =
     A*=====================================================================
     A          R #WINDOW                   TEXT('Window definition.')
     A*
     A                                      WINDOW(11 2  5 75)
     A*
     A* Command keys............................................................
     A                                      CF10(10 'Confirm changes.' )
     A                                      CF12(12 'Previous.')
     A                                      VLDCMDKEY(29 'Valid comman d key.')
     A* SETOFFS.................................................................
     A            #1WARN        70      2  2TEXT('Warning message.')
     A                                      DSPATR(HI)
     A            #1WAR2        70      3  2TEXT('Warning message.')
     A                                      DSPATR(HI)
     A            #1CMDW        70      4  2TEXT('Command Keys Window')
     A                                      DSPATR(HI)
     A*=====================================================================
     A*  Format DUMMY is not used. It contains the ASSUME keyword          =
     A*   which must be in at least one format when the WINDOW             =
     A*   keyword is used.                                                 =
     A*=====================================================================
     A          R DUMMY                      TEXT('Dummy format.')
     A*
     A                                       OVERLAY PUTOVR ASSUME
     A            DUMMYF         1A  O  1 80 DSPATR(ND)
     A*=====================================================================
      *                                                     End CFT003
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
