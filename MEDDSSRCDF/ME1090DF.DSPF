      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Incoming Message Routing Table')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module
     A*                                                            *
     A*   DSPF/ME1090DF - Display Valid Msg Types   Display file   *
     A*                                                            *
     A*  (c) Finastra International Limited 2001                   *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. 090199             Date 12JUL95               *
     A*    PREV AMEND NO. 069867       DATE 22APR94                *
     A*         AMEND NO. S01435       DATE 29JUL93                *
     A*                                                            *
     A**************************************************************
     A*                                                            *
     A*  MD046248 - Finastra Rebranding                            *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*    069867 - File was collected with a keyword option which *
     A*             is not supported at OS/400 releases before     *
     A*             V2R2M0.  As we must support V2R1M1 at this     *
     A*             time, this part of the change must be reversed *
     A*             out.                                           *
     A*    S01435 - Incoming Message Management                    *
     A*                                                            *
     A**************************************************************
     A*                                                               *
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
      *================================================================
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(38 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1NARR        30   H      TEXT('Narrative')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A            #1SEL          1   B 10  2
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A            #1NWRK         6     10  5TEXT('Network')
     A                                      DSPATR(HI)
     A            #1MTPY         3     10 14TEXT('Message type')
     A                                      DSPATR(HI)
     A            #1SNDK        20     10 19TEXT('Sender (key)')
     A                                      DSPATR(HI)
     A            #1ACPT         7     10 41TEXT('Access Point')
     A                                      DSPATR(HI)
     A            #1INEX         1     10 53TEXT('Include/Exclude')
     A                                      DSPATR(HI)
     A            #1PMFG         1     10 63TEXT('Payment Message Flag')
     A                                      DSPATR(HI)
     A            #1BRCA         3     10 70TEXT('Branch Code')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(ND)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(11) SFLSIZ(12)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
      *
      *                                              Begin /069867/
      ** The SFLEND(*MORE) option has been removed because it is not
      ** supported at releases of OS/400 prior to V2R2M0.  At a later date,
      ** when support for V2R1M1 is not required, this option should be
      ** reinstated.
      ***If*version*2.1*or*lessof*OS/400*reinstate*next*line****************
      *
     A*N80*81*82****************************SFLEND**************************
     A*N80*81*82*                           SFLEND(*MORE)
     A N80 81 82                            SFLEND
      *                                                End /069867/
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2NWRK')
     A                                      SETOF(32 'Invalid: #2MTPY')
     A                                      SETOF(33 'Invalid: #2SNDK')
     A                                      SETOF(34 'Invalid: #2ACPT')
     A                                      SETOF(35 'Invalid: #2INEX')
     A                                      SETOF(36 'Invalid: #2BRCA')
     A                                      SETOF(37 'Invalid: #2PMFG')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'ME1090F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
     A                                  4  2'Position on.:'
     A                                  4 16'Network'
     A            #2NWRK         6   B  4 24TEXT('Network')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  4 32'Message type'
     A            #2MTPY         3   B  4 45TEXT('Message type')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  4 50'Sender'
     A            #2SNDK        20   B  4 57TEXT('Sender (key)')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5  2'Select on...:'
     A                                  5 16'Access Point'
     A            #2ACPT         7   B  5 29TEXT('Access Point')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5 38'Incl/Excl'
     A            #2INEX         1   B  5 48TEXT('Include/Exclude')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5 51'Pay Msg Flag'
     A            #2PMFG         1   B  5 64TEXT('Payment Message Flag')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A N70                              5 66'Branch'
     A N70        #2BRCA         3   B  5 73TEXT('Branch Code')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  81                              7  2'?'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7  5'Network'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 14'Msg'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 19'Sender'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 41'Access'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 50'Include/'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              7 60'Payment'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81N70                           7 69'Branch'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 14'Typ'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 41'Point'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 50'Exclude'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 60'Message'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81N70                           8 69' Code'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 62'Flag'
     A                                      DSPATR(HI) COLOR(WHT)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
