      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME FT Message Translation Table')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module
     A*                                                            *
     A*   DSPF/ME1150DF - Display Message Trans.    Display file   *
     A*                                                            *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                            *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. BUG24795           Date 07Jul09               *
      *  Prev Amend No. CSW209             Date 01Apr09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 090199             Date 12Jul95               *
     A*                   069867       DATE 22APR94                *
     A*         AMEND NO. S01435       DATE 29JUL93                *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  BUG24795 -  Field Sequence is missing                      *
      *  CSW209  -  SWIFT 2009 Changes                             *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*    069867 - File was collected with a keyword option which *
     A*             is not supported at OS/400 releases before     *
     A*             V2R2M0.  As we must support V2R1M1 at this     *
     A*             time, this part of the change must be reversed *
     A*             out.                                           *
     A*    S01435 - Incoming Message Management                    *
     A*                                                            *
     A**************************************************************
     A*                                                               *
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
      *================================================================
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(36 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1NARR        30   H      TEXT('Narrative')
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
      ** Start of Change CSW209
     A**********  #1SEL          1   B 12 15                                                  CSW209
     A            #1SEL          1   B 12 11
      ** End of Change CSW209
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
      ** Start of Change CSW209
     A**********  #1NWRK         6     12 18TEXT('Network')                                   CSW209
     A            #1NWRK         6     12 14TEXT('Network')
      ** End of Change CSW209
     A                                      DSPATR(HI)
      ** Start of Change CSW209
     A**********  #1MTPY         3     12 27TEXT('Message type')                              CSW209
     A            #1MTPY         3     12 23TEXT('Message type')
      ** End of Change CSW209
     A                                      DSPATR(HI)
      ** Start of Change CSW209
     A            #1MSEQ         3     12 29TEXT('Message Field Sequence')
     A                                      DSPATR(HI)
     A  37                                  DSPATR(ND)
     A**********  #1MTAG         5     12 33TEXT('Message Field Tag')                         CSW209
     A            #1MTAG         5     12 36TEXT('Message Field Tag')
      ** End of Change CSW209
     A                                      DSPATR(HI)
      ** Start of Change CSW209
     A**********  #1FTFD        10     12 41TEXT('FT Field Name')                             CSW209
     A            #1FTFD        10     12 43TEXT('FT Field Name')
      ** End of Change CSW209
     A                                      DSPATR(HI)
      ** Start of Change CSW209
     A**********  #1FTSP        10     12 53TEXT('Special Instructions')                      CSW209
     A            #1FTSP        10     12 55TEXT('Special Instructions')
      ** End of Change CSW209
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(09) SFLSIZ(10)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
      *
      *                                              Begin /069867/
      ** The SFLEND(*MORE) option has been removed because it is not
      ** supported at releases of OS/400 prior to V2R2M0.  At a later date,
      ** when support for V2R1M1 is not required, this option should be
      ** reinstated.
      ***If*version*2.1*or*lessof*OS/400*reinstate*next*line****************
      *
     A*N80*81*82****************************SFLEND**************************
     A*N80*81*82*                           SFLEND(*MORE)
     A N80 81 82                            SFLEND
      *                                                End /069867/
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2NWRK')
     A                                      SETOF(32 'Invalid: #2MTPY')
     A                                      SETOF(33 'Invalid: #2MTAG')
     A                                      SETOF(34 'Invalid: #2FTFD')
     A                                      SETOF(35 'Invalid: #2FTSP')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'ME1150F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
     A                                  4  2'Position on.:'
     A                                  4 16'Network'
     A            #2NWRK         6   B  4 24TEXT('Network')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      ** Start of Change CSW209
     A**********                        5 11'Message type'                                    CSW209
     A                                  4 32'Message type'
     A**********  #2MTPY         3   B  5 24TEXT('Message type')                              CSW209
     A            #2MTPY         3   B  4 45TEXT('Message type')
      ** End of Change CSW209
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
      ** Start of Change CSW209
     A                                  5 11'Field Seq'
     A  37                                  DSPATR(ND)
     A            #2MSEQ         3   B  5 21TEXT('Field Seq')
      ** Start of Change BUG24795
     A**37******                            DSPATR(ND)                                      BUG24795
     A  37                                  DSPATR(ND PR)
      ** End of Change BUG24795
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A**********                        5 29'Message Field Tag'                               CSW209
     A                                  5 26'Field Tag'
     A**********  #2MTAG         5   B  5 47TEXT('Message Field Tag')                         CSW209
     A            #2MTAG         5   B  5 36TEXT('Message Field Tag')
      ** End of Change CSW209
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
      ** Start of Change CSW209
     A**********                        5 54'FT Field Name'                                   CSW209
     A                                  5 43'FT Field Name'
     A**********  #2FTFD        10   B  5 68TEXT('FT Field Name')                             CSW209
     A            #2FTFD        10   B  5 57TEXT('FT Field Name')
      ** End of Change CSW209
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  7  2'Select on..:'
     A                                  7 16'Special Instructions'
     A            #2FTSP        10   B  7 37TEXT('Special Instructions')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
      ** Start of Change CSW209
     A**81******                        9 15'?'                                               CSW209
     A  81                              9 11'?'
      ** End of Change CSW209
     A                                      DSPATR(HI) COLOR(WHT)
      ** Start of Change CSW209
     A**81******                        9 18'Network'                                         CSW209
     A  81                              9 14'Network'
      ** End of Change CSW209
     A                                      DSPATR(HI) COLOR(WHT)
      ** Start of Change CSW209
     A**81******                        9 27'Msg'                                             CSW209
     A  81                              9 23'Msg'
      ** End of Change CSW209
     A                                      DSPATR(HI) COLOR(WHT)
      ** Start of Change CSW209
     A**81******                        9 32'Message'                                         CSW209
     A  81                              9 31'Message'
      ** End of Change CSW209
     A                                      DSPATR(HI) COLOR(WHT)
      ** Start of Change CSW209
     A**81******                        9 41'FT Field'                                        CSW209
     A  81                              9 43'FT Field'
      ** End of Change CSW209
     A                                      DSPATR(HI) COLOR(WHT)
      ** Start of Change CSW209
     A**81******                        9 53'Special'                                         CSW209
     A  81                              9 55'Special'
      ** End of Change CSW209
     A                                      DSPATR(HI) COLOR(WHT)
      ** Start of Change CSW209
     A**81******                       10 27'Typ'                                             CSW209
     A  81                             10 23'Typ'
      ** End of Change CSW209
     A                                      DSPATR(HI) COLOR(WHT)
      ** Start of Change CSW209
     A**81******                       10 32' Field'                                          CSW209
     A  81                             10 31' Field'
      ** End of Change CSW209
     A                                      DSPATR(HI) COLOR(WHT)
      ** Start of Change CSW209
     A**81******                       10 41'Name'                                            CSW209
     A  81                             10 43'Name'
      ** End of Change CSW209
     A                                      DSPATR(HI) COLOR(WHT)
      ** Start of Change CSW209
     A**81******                       10 53'Instructions'                                    CSW209
     A  81                             10 55'Instructions'
      ** End of Change CSW209
     A                                      DSPATR(HI) COLOR(WHT)
      ** Start of Change CSW209
     A  81                             11 29'Seq'
     A  37                                  DSPATR(ND)
     A                                      DSPATR(HI) COLOR(WHT)
     A**81******                       11 32'  Tag'                                           CSW209
     A  81                             11 34'  Tag'
      ** End of Change CSW209
     A                                      DSPATR(HI) COLOR(WHT)
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
