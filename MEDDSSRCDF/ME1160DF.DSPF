      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Edit Message Translation')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module
     A*                                                            *
     A*   DSPF/ME1160DF - Edit Message Translation  Edit rd(1scrn) *
     A*                                                            *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                            *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CSW209             Date 01Apr09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. 090199             Date 12JUL95               *
     A*                   S01435       DATE 29JUL93                *
     A*                                                            *
     A**************************************************************
     A*                                                            *
      *  CSW209  - SWIFT 2009 Changes                              *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*    S01435 - Incoming Message Management                    *
     A*                                                            *
     A**************************************************************
     A*                                                               *
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
      *================================================================
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(31 'Invalid: #1NWRK')
     A                                      SETOF(32 'Invalid: #1MTPY')
      ** Start of Change CSW209
     A                                      SETOF(35 'Invalid: #1MSEQ')
      ** End of Change CSW209
     A                                      SETOF(33 'Invalid: #1MTAG')
     A                                      SETOF(34 'Invalid: #1FTFD')
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'ME1160F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code . . . :'
     A            #PACTC         1      4 24TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Network . . . . . .'
     A            #1NWRK         6   B  6 24TEXT('Network')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  7  2'Message type  . . .'
     A            #1MTPY         3   B  7 24TEXT('Message type')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
      ** Start of Change CSW209
     A                                  8  2'Message Field Seq .'
     A  36                                  DSPATR(ND)
     A            #1MSEQ         3   B  8 24TEXT('Message Field Seq')
     A N25                                  OVRDTA
     A  36                                  DSPATR(ND PR)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A**********                        8  2'Message Field Tag .'                             CSW209
     A**********  #1MTAG         5   B  8 24TEXT('Message Field Tag')                         CSW209
     A                                  9  2'Message Field Tag .'
     A            #1MTAG         5   B  9 24TEXT('Message Field Tag')
      ** End of Change CSW209
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
      ** Start of Change CSW209
     A**********                        9  2'FT Field Name . . .'                             CSW209
     A**********  #1FTFD        10   B  9 24TEXT('FT Field Name')                             CSW209
     A                                 10  2'FT Field Name . . .'
     A            #1FTFD        10   B 10 24TEXT('FT Field Name')
      ** End of Change CSW209
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Previous')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1FTSP')
     A                                      SETOF(32 'Invalid: #1NARR')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'ME1160F2'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A                                  4  2'Action Code  . . . . :'
     A            #PACTC         1      4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Network  . . . . . . :'
     A            #1NWRK         6      6 27TEXT('Network')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1NETD        30      6 40TEXT('Network Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Message type . . . . :'
     A            #1MTPY         3      7 27TEXT('Message type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1MTPD        35      7 40TEXT('Message Type Descr.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      ** Start of Change CSW209
     A                                  8  2'Message Field Seq. . :'
     A  36                                  DSPATR(ND)
     A            #1MSEQ         3      8 27TEXT('Message Field Seq')
     A N25                                  OVRDTA
     A  36                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A**********                        8  2'Message Field Tag  . :'                          CSW209
     A**********  #1MTAG         5      8 27TEXT('Message Field Tag')                         CSW209
     A                                  9  2'Message Field Tag  . :'
     A            #1MTAG         5      9 27TEXT('Message Field Tag')
      ** End of Change CSW209
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      ** Start of Change CSW209
     A**********  #1SCFD        20      8 40TEXT('Message Tag Descr.')                        CSW209
     A            #1SCFD        20      9 40TEXT('Message Tag Descr.')
      ** End of Change CSW209
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      ** Start of Change CSW209
     A**********                        9  2'FT Field Name  . . . :'                          CSW209
     A**********  #1FTFD        10      9 27TEXT('FT Field Name')                             CSW209
     A                                 10  2'FT Field Name  . . . :'
     A            #1FTFD        10     10 27TEXT('FT Field Name')
      ** End of Change CSW209
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      ** Start of Change CSW209
     A**********  #1FTFN        40      9 40TEXT('FT Field Descr.')                           CSW209
     A            #1FTFN        40     10 40TEXT('FT Field Descr.')
      ** End of Change CSW209
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      ** Start of Change CSW209
     A*******N79                       11  2'Special Instructions .'                          CSW209
     A********79                       11  2'Special Instructions :'                          CSW209
     A       N79                       12  2'Special Instructions .'
     A        79                       12  2'Special Instructions :'
     A**********  #1FTSP        10   B 11 27TEXT('Special Instructions')                      CSW209
     A            #1FTSP        10   B 12 27TEXT('Special Instructions')
      ** End of Change CSW209
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N79N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
      ** Start of Change CSW209
     A*******N79                       12  2'Narrative  . . . . . .'                          CSW209
     A********79                       12  2'Narrative  . . . . . :'                          CSW209
     A**********  #1NARR        30   B 12 27TEXT('Narrative')                                 CSW209
     A       N79                       13  2'Narrative  . . . . . .'
     A        79                       13  2'Narrative  . . . . . :'
     A            #1NARR        30   B 13 27TEXT('Narrative')
      ** End of Change CSW209
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N79N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 17  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL)
     A N25                                  OVRATR
     A                                 18  2'User name :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1AUSR        10     18 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 18 25'Action Date :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ARDT         7     18 39TEXT('Action Date')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 18 47'Action Time :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATIM         6  0  18 61TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
