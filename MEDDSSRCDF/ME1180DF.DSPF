      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Input Date Range')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module
     A*                                                            *
     A*   DSPF/ME1180DF - Input Date Range                         *
     A*                                                            *
     A*  (c) Finastra International Limited 2001                   *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. 090199             Date 12JUL95               *
     A*                   S01435       DATE 29JUL93                *
     A*                                                            *
     A**************************************************************
     A*                                                            *
     A*  MD046248 - Finastra Rebranding                               *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*    S01435 - Incoming Message Management                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* COMMAND KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #DFDTE')
     A                                      SETOF(32 'Invalid: #DTDTE')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'ME1180F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  2 36'Input Date Range'
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PRSEQ         5   H      TEXT('RCF Sequence')
     A            #PRLVL         1   H      TEXT('RCF Level')
     A            #PRENT         3   H      TEXT('RCF Entity')
     A            #PRACT         1   H      TEXT('RCF Action')
     A            #PRFKY         1   H      TEXT('RCF F key pressed')
     A            #PRPRM       100   H      TEXT('RCF Selection Parameters')
     A                                  6 26'Date selection criteria'
     A                                      DSPATR(UL)
     A                                  8 26'From date . .'
     A            #DFDTE         6   B  8 42TEXT('From date')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10 26'To date . . .'
     A            #DTDTE         6   B 10 42TEXT('To date')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
