      *****************************************************************
/*STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  ME1210#1 - Extension details for Routing                     *
      *                                                               *
      *  (C) Copyright Misys International Banking Systems Ltd 2003   *
      *                                                               *
      *  Last Amend No. MD31937            Date 13Jan15            *
      *  Last Amend No. AR856737           Date 13Sep11               *
      *  Last Amend No. ESL038             Date 26Nov2004             *
      *  Prev Amend No. ERN056             Date 09Aug2004             *
      *  Prev Amend No. EEE065             Date 06Apr2004             *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD31937  - Upgrade STP enhancements to BF Midas 2.1       *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  ESL038 - Funds Transfer STP Enhancements                     *
      *  ERN056 - FT Rocket Payment Messages                          *
      *  EEE065 - FT Payment Routing                                  *
      *                                                               *
      *****************************************************************
     A                                      PRINT
     A                                      INDARA
     A                                      CA03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A                                      CF12(12 'Previous')
     A          R ME1210W#1                 TEXT('(C) Misys IBS +
     A                                      Ltd. 2004')
      *
     A                                      WINDOW(&SWROW &SWCOL 12 60)
      *
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A            ##PGM         10A  H
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
      *----------------------------------------------------------------
      *
     A                                  2  1'Display pay route .............:'
     A                                      OVRATR
     A            SDSPR          1A  B  2 35OVRDTA
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  3  1'MC messageg (Y/N) .............:'
     A                                      OVRATR
     A            SMCMG          1A  B  3 35OVRDTA
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  4  1'MC Midas Suffix ...............:'
     A                                      OVRATR
     A            SMCSX          2A  B  4 35OVRDTA
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *>> Start ERN056
     A***********                       5  1'MC 281 Ack issued immediately..:'
     A***********                           OVRATR
     A*********** SM281          1A  B  5 35OVRDTA
     A*N54*******                           DSPATR(HI)
     A**54*******                           DSPATR(RI PC)
     A**20*******                           DSPATR(PR)
     A*N20*******                           DSPATR(UL)
     A***********                       6  1'MC 291 Reject issued on Ignore.:'
     A***********                           OVRATR
     A*********** SM291          1A  B  6 35OVRDTA
     A*N55*******                           DSPATR(HI)
     A**55*******                           DSPATR(RI PC)
     A**20*******                           DSPATR(PR)
     A*N20*******                           DSPATR(UL)
     A                                  5  1'MC Ack issued immediately..:'
     A                                      OVRATR
     A            SMACK          1A  B  5 35OVRDTA
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  5 37'Type'
     A                                      OVRATR
     A            SMACM          3A  B  5 42OVRDTA
     A N58                                  DSPATR(HI)
     A  58                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  6  1'MC Reject issued on Ignore.:'
     A                                      OVRATR
     A            SMNCK          1A  B  6 35OVRDTA
     A N55                                  DSPATR(HI)
     A  55                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  6 37'Type'
     A                                      OVRATR
     A            SMNCM          3A  B  6 42OVRDTA
     A N59                                  DSPATR(HI)
     A  59                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *>> End ERN056
     A                                  7  1'MC Import accounting...........:'
     A                                      OVRATR
     A            SIMPA          1A  B  7 35OVRDTA
     A N56                                  DSPATR(HI)
     A  56                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  8  1'MC Reversal accounting on Ign.:'
     A                                      OVRATR
     A            SREVA          1A  B  8 35OVRDTA
     A N57                                  DSPATR(HI)
     A  57                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                 11  1'F3=Exit         F12=Cancel-
      *
     A                                                              '
      *
      *  Start of changes for ESL038
     A          R ME1210W#2                 TEXT('(C) Misys IBS +
     A                                      Ltd. 2004')
      *
     A                                      WINDOW(&SWROW &SWCOL 9 60)
      *
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
     A                                      SETOF(57)
     A                                      SETOF(58)
     A            ##PGM         10A  H
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
      *----------------------------------------------------------------
      *
     A                                  2  1'Display PCAT on Incoming (Y/N).:'
     A                                      OVRATR
     A            SDSPI          1A  B  2 35OVRDTA
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  3  1'Display PCAT on Outgoing (O/B/N)'
     A                                      OVRATR
     A            SDSPO          1A  B  3 35OVRDTA
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  4  1'Display pay route .............:'
     A                                      OVRATR
     A            SDSPR          1A  B  4 35OVRDTA
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  5  1'External System (Y/N)..........:'
     A                                      OVRATR
     A            SMCMG          1A  B  5 35OVRDTA
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  6  1'Disable Input Correction (Y/N).:'
     A                                      OVRATR
     A            SDINC          1A  B  6 35OVRDTA
     A N55                                  DSPATR(HI)
     A  55                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
     A                                  8  1'F3=Exit         F12=Cancel'
      *  End of changes for ESL038
      *
      *
     A*=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
