      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME IMM Status Enquiry')
     A**************************************************************
     A*                                                            *
     A*  Midas - Message Management Module
     A*                                                            *
     A*   DSPF/ME1260DF - Incoming Message Management Status Enq.  *
     A*                                                            *
     A*  (c) Finastra International Limited 2001                   *
     A*                                                            *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. 090199             Date 12JUL95               *
     A*                   S01435       DATE 31DEC93                *
     A*                                                            *
     A**************************************************************
     A*                                                            *
     A*  MD046248 - Finastra Rebranding                               *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*    S01435 - Incoming Message Management                    *
     A*                                                            *
     A**************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* COMMAND KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #DFDTE')
     A                                      SETOF(32 'Invalid: #DTDTE')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'ME1260F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  74                              4  1'SWIFT Direct Link Communication'
     A                                      DSPATR(UL)
     A  74                              4 42'Status..:'
     A  74        ##SST1        10      4 52TEXT('MSDL Status')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  74        ##SST3         4      4 63TEXT('MSDL Action')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  74        ##SST2        10      4 68TEXT('MSDL Sub Status')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  74                              5  1'Network'
     A  74                              5 11'Sender Reference'
     A  74                              5 42'Date'
     A  74                              5 49'   O.S.N.   '
     A  74        ##SLN1        79      6  1TEXT('Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  1'Incoming Message Extraction'
     A                                      DSPATR(UL)
     A                                  8 42'Status..:'
     A            ##IST1        10      8 52TEXT('Incoming Status')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##IST3         4      8 63TEXT('Incoming Action')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##IST2        10      8 68TEXT('Incoming Sub Status')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  1'Network'
     A                                  9 11'Sender Reference'
     A                                  9 42'Date'
     A                                  9 49'   O.S.N.   '
     A            ##ILN1        79     10  1TEXT('Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ILN2        79     11  1TEXT('Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ILN3        79     12  1TEXT('Line 3')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  75                             14  1'Auto Generation Information'
     A                                      DSPATR(UL)
     A  75                             14 42'Status..:'
     A  75        ##AST1        10     14 52TEXT('FT Auto Gen Status')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  75        ##AST3         4     14 63TEXT('FT Auto Gen Action')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  75        ##AST2        10     14 68TEXT('FT Auto Gen Sub Status')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  75                             15  1'Network'
     A  75                             15 11'Sender Reference'
     A  75                             15 42'Date'
     A  75                             15 49'   O.S.N.   '
     A  75                             15 63'FT Reference'
     A  75        ##AIL1        79     16  1TEXT('Line 4')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  75        ##AOL1        79     17  1TEXT('Line 5')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  75        ##AIL2        79     18  1TEXT('Line 6')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  75        ##AOL2        79     19  1TEXT('Line 7')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  75        ##AIL3        79     20  1TEXT('Line 8')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A  75        ##AOL3        79     21  1TEXT('Line 9')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*                                20  1'Re-display after'
     A*           #DRDSP         2   B 20 18TEXT('Redisplay Time')
     A*N25                                  OVRDTA
     A* 31                                  DSPATR(RI PC)
     A*N31                                  DSPATR(UL HI)
     A*N25                                  OVRATR
     A*                                20 21'Seconds'
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
