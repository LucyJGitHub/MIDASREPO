     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Sub-Line Account Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Message Management Module                            *
     A*                                                               *
     A*  DSPF/ME1320DF - Sub-Line Account Code Maintenance            *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
     A*  Last Amend No. CFT006  *CREATE    Date 03Nov99               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CFT006 - MT101/MT102                                         *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*=========================================================================
     A          R #MSGRCD                   SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
     A*=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            ##PGM                     SFLPGMQ(10)
     A*=========================================================================
     A*
     A          R ME1320S1                  SFL
     A  53                                  SFLNXTCHG
     A            SSEL           1A  B  8 11
     A N60                                  DSPATR(HI)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A
     A            SFTFLD        10   O  8 16DSPATR(HI)
     A            SFTDSC        40   O  8 29DSPATR(HI)
     A*
     A          R ME1320C1                  SFLCTL(ME1320S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CA03
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLCSRRRN(&@@RNS1)
     A  52                                  SFLDSP
     A                                      SFLDSPCTL
     A  54                                  SFLINZ
     A  51                                  SFLCLR
     A  50                                  SFLEND
     A            @@RNC1         4S 0H      SFLRCDNBR
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A            @@RNS1         5S 0H
     A                                  1 13'ME1320F1'
     A                                  5 29'Description'
     A                                  1 33'Select FT field'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 16'FT field'
     A                                  7 10'1=Sel'
     A*
     A          R ME1320F1
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'F3=Exit'
