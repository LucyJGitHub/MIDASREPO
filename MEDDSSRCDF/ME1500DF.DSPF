      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Display message translation table')
      *****************************************************************
      *                                                               *
      *   Midas - Message Management Module                           *
      *                                                               *
      *   DSPF/ME1500DF - Display Message Trans.  Display file        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPK010             Date 20Oct99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CFT002  *CREATE    Date 22Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *    CPK010 - Old BIS reference removed                         *
      *    CFT002 - Straight Through Processing Phase 1               *
      *             Based on ME1500DF original references             *
      *             HUB001/S01489                                     *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *=========================================================================
     A          R #SFLRC1                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(36 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1RECI         1A  H      TEXT('Record Identifier')
     A            #1SQNO         5S 0H      TEXT('Sequence Number')
     A            #1AJOB        10A  H      TEXT('Job name')
     A            #1AUSR        10A  H      TEXT('User name')
     A            #1ATIM         6S 0H      TEXT('Action Time')
     A            #1ARDT         7A  H      TEXT('Action Date')
     A            #1AACT         1A  H      TEXT('Action Type')
     A            #1RDNB         5S 0H      TEXT('Run day number')
     A            #1IND1        35   H      TEXT('Text Line 1')
     A            #1IND2        35   H      TEXT('Text Line 2')
     A            #1SEL          1A  B 11  4
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A            #1NWRK         6A  O 11  6TEXT('Network')
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  #1CYCD         3A  O 11 31TEXT('Currency Code')                             CGL029
     A            #1CYCD         3A  O 11 29TEXT('Currency Code')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  #1SNDR        20A  O 11 37TEXT('Sender')                                    CGL029
     A            #1SNDR        20A  O 11 34TEXT('Sender')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  #1MTPY         3A  O 11 25TEXT('Message type')                              CGL029
     A            #1MTPY         3A  O 11 23TEXT('Message type')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A            #1MTAG         5A  O 11 15TEXT('Message Field Tag')
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  #1MDEQ        18A  O 11 60TEXT('MIDAS Equivalent')                          CGL029
     A            #1MDEQ        24A  O 11 56TEXT('MIDAS Equivalent')
     A**End of change CGL029
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #SFLCT1                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRC1)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(10) SFLSIZ(11)
     A                                      BLINK OVERLAY
     A                                      KEEP
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
      *-------------------------------------------------------------------------
      *  If version 2.1 or lessof OS/400 reinstate next line
     A N80 81 82                            SFLEND(*MORE)
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A                                      CF18(18 'CF18 View 2')
     A                                      CF19(19 'CF19 View 3')
     A N82                                  ROLLUP(27 'Next page.')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2NWRK')
     A                                      SETOF(32 'Invalid: #2MTAG')
     A                                      SETOF(33 'Invalid: #2MTPY')
     A                                      SETOF(34 'Invalid: #2CYCD')
     A                                      SETOF(35 'Invalid: #2SNDR')
     A                                      SETOF(37 'Invalid: #SNWRK')
     A                                      SETOF(38 'Invalid: #SMTAG')
     A                                      SETOF(39 'Invalid: #SMTPY')
     A                                      SETOF(40 'Invalid: #SCYCD')
     A                                      SETOF(41 'Invalid: #SSNDR')
      *-------------------------------------------------------------------------
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*                                     Start of Change /CPK010/
     A**********************************1 26'B I S  ¦  M i d a s  ¦  A B S'
     A*                                       End of Change /CPK010/
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1A  H      TEXT('Action Code')
     A                                  4  2'Position on :'
     A                                  4 16'Network'
     A            #2NWRK         6A  B  4 24TEXT('Network')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  4 33'Message field Tag'
     A            #2MTAG         5A  B  4 51TEXT('Message Field Tag')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  4 59'Message type'
     A            #2MTPY         3A  B  4 72TEXT('Message type')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  5 16'Currency code'
     A            #2CYCD         3A  B  5 30TEXT('Currency Code')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  5 36'Sender'
     A            #2SNDR        20A  B  5 43TEXT('Sender')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  7  3'Select on  :'
     A                                  7 16'Network'
     A            #2SNWR         6A  B  7 24TEXT('Network')
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N37                                  DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  7 33'Message field Tag'
     A            #2SMTA         5A  B  7 51TEXT('Message Field Tag')
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A N38                                  DSPATR(UL)
     A N38                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  7 59'Message type'
     A            #2SMTP         3A  B  7 72TEXT('Message type')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A N39                                  DSPATR(UL)
     A N39                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  8 16'Currency code'
     A            #2SCYC         3A  B  8 30TEXT('Currency Code')
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N40                                  DSPATR(UL)
     A N40                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  8 36'Sender'
     A            #2SSND        20A  B  8 43TEXT('Sender')
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N41                                  DSPATR(UL)
     A N41                                  DSPATR(HI)
     A N25                                  OVRATR
     A**Start of change CGL029
     A**81******                        9  4'? Network  Msg Fld   Msg   Ccy   S-              CGL029
     A**********                            ender                 MIDAS        -              CGL029
     A**********                                 '                                            CGL029
     A  81                              9  4'? Network  Msg Fld Msg   Ccy  Send-
     A                                      er                 MIDAS'
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**81******                       10  4'           Tag       Type  Code   -              CGL029
     A**********                                                  Equivalent   -              CGL029
     A**********                                 '                                            CGL029
     A  81                             10  4'           Tag     Type  Code     -
     A                                                        Equivalent'
     A**End of change CGL029
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #SFLRC2                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(36 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1RECI         1A  H      TEXT('Record Identifier')
     A            #1SQNO         5S 0H      TEXT('Sequence Number')
     A            #1AJOB        10A  H      TEXT('Job name')
     A            #1AUSR        10A  H      TEXT('User name')
     A            #1ATIM         6S 0H      TEXT('Action Time')
     A            #1ARDT         7A  H      TEXT('Action Date')
     A            #1AACT         1A  H      TEXT('Action Type')
     A            #1RDNB         5S 0H      TEXT('Run day number')
     A            #1IND2        35   H      TEXT('Text Line 2')
     A            #1SNDR        20A  H      TEXT('Sender')
     A            #1MTPY         3A  H      TEXT('Message type')
     A            #1MTAG         5A  H      TEXT('Message Field Tag')
     A            #1SEL          1A  B 11  4
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A**Start of change CGL029
     A**********  #1MDEQ        18A  O 11  6TEXT('MIDAS Equivalent')                          CGL029
     A            #1MDEQ        24A  O 11  6TEXT('MIDAS Equivalent')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  #1NWRK         6A  O 11 25TEXT('Network')                                   CGL029
     A            #1NWRK         6A  O 11 31TEXT('Network')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  #1CYCD         3A  O 11 33TEXT('Currency Code')                             CGL029
     A            #1CYCD         3A  O 11 39TEXT('Currency Code')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  #1IND1        35   O 11 38TEXT('Text Line 1')                               CGL029
     A            #1IND1        35   O 11 44TEXT('Text Line 1')
     A**End of change CGL029
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #SFLCT2                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRC2)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(10) SFLSIZ(11)
     A                                      BLINK OVERLAY
     A                                      KEEP
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
      *-------------------------------------------------------------------------
      *  If version 2.1 or lessof OS/400 reinstate next line
     A N80 81 82                            SFLEND(*MORE)
      *-------------------------------------------------------------------------
      * Command keys                                                           .
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A                                      CF18(18 'CF18 View 1')
     A                                      CF19(19 'CF19 View 3')
     A N82                                  ROLLUP(27 'Next page.')
      *-------------------------------------------------------------------------
      * Setoffs                                                                .
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2MDEQ')
     A                                      SETOF(32 'Invalid: #SMDEQ')
     A                                      SETOF(33 'Invalid: #SNWRK')
     A                                      SETOF(34 'Invalid: #SMTAG')
     A                                      SETOF(35 'Invalid: #SMTPY')
     A                                      SETOF(37 'Invalid: #SCYCD')
     A                                      SETOF(38 'Invalid: #SSNDR')
      *-------------------------------------------------------------------------
      *
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*                                     Start of Change /CPK010/
     A**********************************1 26'B I S  ¦  M i d a s  ¦  A B S'
     A*                                       End of Change /CPK010/
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1A  H      TEXT('Action Code')
     A                                  4  2'Position on :'
     A                                  4 16'MIDAS Equivalent'
     A**Start of change CGL029
     A**75******  #2MDEQ        18A  B  4 33TEXT('Midas Equivalent')                          CGL029
     A  75        #2MDEQ        24A  B  4 33TEXT('Midas Equivalent')
     A**End of change CGL029
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A**Start of change CGL029
     A*N75******  #2MDE1        15A  B  4 33TEXT('Midas Equivalent')                          CGL029
     A N75        #2MDE1        21A  B  4 33TEXT('Midas Equivalent')
     A**End of change CGL029
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  6  3'Select on  :'
     A                                  6 16'MIDAS Equivalent'
     A**Start of change CGL029
     A**75******  #SMDEQ        18A  B  6 33TEXT('Midas Equivalent')                          CGL029
     A  75        #SMDEQ        24A  B  6 33TEXT('Midas Equivalent')
     A**End of change CGL029
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A**Start of change CGL029
     A*N75******  #SMDE1        15A  B  6 33TEXT('Midas Equivalent')                          CGL029
     A N75        #SMDE1        21A  B  6 33TEXT('Midas Equivalent')
     A**End of change CGL029
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  7 16'Network'
     A            #2SNWR         6A  B  7 24TEXT('Network')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  7 33'Message field Tag'
     A            #2SMTA         5A  B  7 51TEXT('Message Field Tag')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  7 59'Message type'
     A            #2SMTP         3A  B  7 72TEXT('Message type')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  8 16'Currency code'
     A            #2SCYC         3A  B  8 30TEXT('Currency Code')
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N37                                  DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  8 36'Sender'
     A            #2SSND        20A  B  8 43TEXT('Sender')
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A N38                                  DSPATR(UL)
     A N38                                  DSPATR(HI)
     A N25                                  OVRATR
     A**Start of change CGL029
     A**81******                        9  4'? MIDAS              Network Ccy  -              CGL029
     A**********                            Contents of Message Tag            -              CGL029
     A  81                              9  4'? MIDAS                    Network-
     A                                       Ccy  Contents of Message Tag      -
     A**End of change CGL029
     A                                           '
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**81******                       10  4'  Equivalent                 Code -              CGL029
     A**********                                                               -              CGL029
     A  81                             10  4'  Equivalent                      -
     A                                       Code                              -
     A**End of change CGL029
     A                                           '
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #SFLRC3                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(36 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1RECI         1A  H      TEXT('Record Identifier')
     A            #1SQNO         5S 0H      TEXT('Sequence Number')
     A            #1AJOB        10A  H      TEXT('Job name')
     A            #1AUSR        10A  H      TEXT('User name')
     A            #1ATIM         6S 0H      TEXT('Action Time')
     A            #1ARDT         7A  H      TEXT('Action Date')
     A            #1AACT         1A  H      TEXT('Action Type')
     A            #1RDNB         5S 0H      TEXT('Run day number')
     A            #1IND2        35   H      TEXT('Text Line 2')
     A            #1SNDR        20A  H      TEXT('Sender')
     A            #1MTPY         3A  H      TEXT('Message type')
     A            #1MTAG         5A  H      TEXT('Message Field Tag')
     A            #1SEL          1A  B 11  4
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A            #1IND1        35   O 11  6TEXT('Text Line 1')
     A                                      DSPATR(HI)
     A            #1NWRK         6A  O 11 42TEXT('Network')
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  #1MDEQ        18A  O 11 50TEXT('MIDAS Equivalent')                          CGL029
     A            #1MDEQ        24A  O 11 50TEXT('MIDAS Equivalent')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  #1CYCD         3A  O 11 69TEXT('Currency Code')                             CGL029
     A            #1CYCD         3A  O 11 75TEXT('Currency Code')
     A**End of change CGL029
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #SFLCT3                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRC3)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(10) SFLSIZ(11)
     A                                      BLINK OVERLAY
     A                                      KEEP
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
      *----------------------------------------------------------------
      *  If version 2.1 or lessof OS/400 reinstate next line
     A N80 81 82                            SFLEND(*MORE)
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A                                      CF18(18 'CF18 View 1')
     A                                      CF19(19 'CF19 View 2')
     A N82                                  ROLLUP(27 'Next page.')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2IND1')
     A                                      SETOF(32 'Invalid: #SIND1')
     A                                      SETOF(33 'Invalid: #SNWRK')
     A                                      SETOF(34 'Invalid: #SMTPY')
     A                                      SETOF(35 'Invalid: #SMTAG')
     A                                      SETOF(37 'Invalid: #SCYCD')
     A                                      SETOF(38 'Invalid: #SSNDR')
      *-------------------------------------------------------------------------
      *
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*                                     Start of Change /CPK010/
     A**********************************1 26'B I S  ¦  M i d a s  ¦  A B S'
     A*                                       End of Change /CPK010/
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1A  H      TEXT('Action Code')
     A                                  4  2'Position on :'
     A                                  4 16'Text'
     A            #2IND1        35A  B  4 24TEXT('Text')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  6  3'Select on  :'
     A                                  6 16'Text'
     A            #SIND1        35A  B  6 24TEXT('Text')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  7 16'Network'
     A            #2SNWR         6A  B  7 24TEXT('Network')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  7 33'Message field Tag'
     A            #2SMTA         5A  B  7 51TEXT('Message Field Tag')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  7 59'Message type'
     A            #2SMTP         3A  B  7 72TEXT('Message type')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  8 16'Currency code'
     A            #2SCYC         3A  B  8 30TEXT('Currency Code')
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N37                                  DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  8 36'Sender'
     A            #2SSND        20A  B  8 43TEXT('Sender')
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A N38                                  DSPATR(UL)
     A N38                                  DSPATR(HI)
     A N25                                  OVRATR
     A  81                              9  4'? Contents of Field               -
     A**Start of change CGL029
     A**********                                Network MIDAS              Ccy -              CGL029
     A**********                                 '                                            CGL029
     A                                          Network MIDAS                  -
     A                                        Ccy'
     A**End of change CGL029
     A                                      DSPATR(HI)
     A  81                             10  4'                                  -
     A**Start of change CGL029
     A**********                                        Equivalent         Code-              CGL029
     A**********                                 '                                            CGL029
     A                                                  Equivalent             -
     A                                        Code'
     A**End of change CGL029
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
