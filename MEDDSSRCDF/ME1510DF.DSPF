      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Edit message translation')
      *****************************************************************
      *                                                               *
      *   Midas - Message Management Module                           *
      *                                                               *
      *   DSPF/ME1510DF - Edit Message Translation  Edit rd (1scrn)   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPK010             Date 20Oct99               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 162075             Date 27Aug99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                  162079          Date 09Jun99                 *
      *                  CFT002 *CREATE  Date 23Apr98                 *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *    CPK010 - Old BIS reference removed                         *
      *    162075 - Correct cursor positioning                        *
      *    162079 - Cross validity check between Currencies           *
      *    CFT002 - Straight Through Processing Phase 1               *
      *             Based on ME1510DF original references             *
      *             HUB001/S01489                                     *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *=========================================================================
     A          R #RCDKEY                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(31 'Invalid: #1NWRK')
     A                                      SETOF(32 'Invalid: #1MTPY')
     A                                      SETOF(33 'Invalid: #1MTAG')
     A                                      SETOF(34 'Invalid: #1CYCD')
     A                                      SETOF(35 'Invalid: #1SNDR')
      *                                               Begin /162075/
     A                                      SETOF(36 'Position cursor #1MTAG')
     A                                      SETOF(37 'Position cursor #1MTPY')
     A                                      SETOF(38 'Position cursor #1CYCD')
     A                                      SETOF(39 'Position cursor #1NWRK')
      *                                                 End /162075/
     A                                      SETOF(99 'Global error flag')
      *----------------------------------------------------------------
      *
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*                                     Start of Change /CPK010/
     A**********************************1 26'B I S  ¦  M i d a s  ¦  A B S'
     A*                                       End of Change /CPK010/
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code  . . . . :'
     A            #PACTC         1A  O  4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Network  . . . . . . :'
     A            #1NWRK         6A  B  6 27TEXT('Network')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
      *                                               Begin /162075/
     A**31**********************************DSPATR(PC)
     A  39
     AO 31                                  DSPATR(PC)
      *                                                 End /162075/
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  7  2'Message Field Tag  . :'
     A            #1MTAG         5A  B  7 27TEXT('Message Field Tag')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI)
      *                                               Begin /162075/
     A**33**********************************DSPATR(PC)
     A  36
     AO 33                                  DSPATR(PC)
      *                                                 End /162075/
     A N33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  8  2'Message type . . . . :'
     A            #1MTPY         3A  B  8 27TEXT('Message type')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
      *                                               Begin /162075/
     A**32**********************************DSPATR(PC)
     A  37
     AO 32                                  DSPATR(PC)
      *                                                 End /162075/
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  9  2'Currency . . . . . . :'
     A            #1CYCD         3A  B  9 27TEXT('Currency')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI)
      *                                               Begin /162075/
     A**34**********************************DSPATR(PC)
     A  38
     AO 34                                  DSPATR(PC)
      *                                                 End /162075/
     A N34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 10  2'Sender . . . . . . . :'
     A            #1SNDR        20A  B 10 27TEXT('Sender')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #RCDDTL1
      *
     A                                      TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Previous')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1IND*')
     A                                      SETOF(32 'Invalid: #1MDEQ')
      *-------------------------------------------------------------------------
      *
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*                                     Start of Change /CPK010/
     A**********************************1 26'B I S  ¦  M i d a s  ¦  A B S'
     A*                                       End of Change /CPK010/
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1RECI         1A  H      TEXT('Record Identifier')
     A            #1AJOB        10A  H      TEXT('Job name')
     A            #1AACT         1A  H      TEXT('Action Type')
     A            #1RDNB         5S 0H      TEXT('Run day number')
     A                                  4  2'Action Code  . . . . :'
     A            #PACTC         1A  O  4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Network  . . . . . . :'
     A            #1NWRK         6A  O  6 27TEXT('Network')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1NETD        30A  O  6 40TEXT('Network Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Message Field Tag  . :'
     A            #1MTAG         5A  O  7 27TEXT('Message Field Tag')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1SCFD        20A  O  7 40TEXT('Message Tag Descr.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  8  2'Message type . . . . :'
     A            #1MTPY         3A  O  8 27TEXT('Message type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1MTPD        35A  O  8 40TEXT('Message Type Descr.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'Currency . . . . . . :'
     A            #1CYCD         3A  O  9 27TEXT('Currency')
     A N25                                  OVRDTA
      *                                                                   /*Begin 162079*/
     A**************************************DSPATR(HI)
     A  34                                  DSPATR(RI)
     A N34                                  DSPATR(HI)
      *                                                                   /*End   162079*/
     A            #1CYNM        14A  O  9 40TEXT('Currency Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 10  2'Sender . . . . . . . :'
     A            #1SNDR        20A  O 10 27TEXT('Sender')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 12  2'Incoming Field Data. .'
     A            #1IND1        35A  B 12 27TEXT('Incoming Field Data Line 1')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N79N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1IND2        35A  B 13 27TEXT('Incoming Field Data Line 2')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N79N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1IND3        35A  B 14 27TEXT('Incoming Field Data Line 3')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N79N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1IND4        35A  B 15 27TEXT('Incoming Field Data Line 4')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N79N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A            #1IND5        35A  B 16 27TEXT('Incoming Field Data Line 4')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N79N31                               DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 17  2'MIDAS Equivalent . . .'
     A**Start of change CGL029
     A**75******  #1MDEQ        18A  B 17 27TEXT('MIDAS Equivalent')                          CGL029
     A  75        #1MDEQ        24A  B 17 27TEXT('MIDAS Equivalent')
     A**End of change CGL029
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32
     AON52N79                               DSPATR(PC)
     A N79N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A**Start of change CGL029
     A*N75******  #1MDE1        15A  B 17 27TEXT('MIDAS Equivalent')                          CGL029
     A N75        #1MDE1        21A  B 17 27TEXT('MIDAS Equivalent')
     A**End of change CGL029
     A N25                                  OVRDTA
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32
     AON52N79                               DSPATR(PC)
     A N79N32                               DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 19  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL)
     A N25                                  OVRATR
     A                                 20  2'User name :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1AUSR        10A  O 20 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 25'Action Date :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ARDT         7A  O 20 39TEXT('Action Date')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 47'Action Time :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATIM         6Y 0O 20 61TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
