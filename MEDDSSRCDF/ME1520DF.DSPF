      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Display message tag trans. x-ref')
      *****************************************************************
      *                                                               *
      *   Midas - Message Management Module                           *
      *                                                               *
      *   DSPF/ME1520DF - Display Message Tag Translation X-ref       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *   Last Amend No. CPK010           Date 20Oct99                *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *   Prev Amend No. CFT002  *CREATE  Date 23Apr98                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *    CPK010 - Old BIS reference removed                         *
      *    CFT002 - Straight Through Processing Phase 1               *
      *             Based on ME1520DF original references             *
      *             HUB001/S01489                                     *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
      *=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(36 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AUSR        10   H      TEXT('User name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ARDT         7   H      TEXT('Action Date')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A            #1SEL          1   B 11  9
     A  36N76                               DSPATR(RI)
     A  36N76                               DSPATR(PC)
     A N36N76                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A     76                               DSPATR(PR BL)
     A            #1MTPY         3   O 11 12TEXT('Message Type')
     A                                      DSPATR(HI)
     A            #1MTAG         5   O 11 19TEXT('Message Tag')
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control')
     A                                      SFLCTL(#SFLRCD)
     A                                      SFLLIN(7)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(27) SFLSIZ(28)
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
      *-------------------------------------------------------------------------
      * If version 2.1 or less of OS/400 reinstate next line
     A N80 81 82                            SFLEND(*MORE)
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  77                                  CF09(09 'CF09 Add')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2MTPY')
     A                                      SETOF(32 'Invalid: #2MTAG')
      *-------------------------------------------------------------------------
      *
     A            ##SFRC         4S 0H      SFLRCDNBR
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*                                     Start of Change /CPK010/
     A**********************************1 26'B I S  ¦  M i d a s  ¦  A B S'
     A*                                       End of Change /CPK010/
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1A  H      TEXT('Action Code')
     A                                  4  2'Position on:'
     A                                  4 15'Message type'
     A            #2MTPY         3A  B  4 28TEXT('Message type')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  5  2'Select on  :'
     A                                  5 15'Message tag'
     A            #2MTAG         5A  B  5 28TEXT('Message tag')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  81                              8 12'Msg'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 20'Msg'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 34'Msg'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 42'Msg'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 56'Msg'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              8 64'Msg'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9  9'?'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 12'Type'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 20'Tag'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 31'?'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 34'Type'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 42'Tag'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 53'?'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 56'Type'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              9 64'Tag'
     A                                      DSPATR(HI) COLOR(WHT)
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
