      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Message tag translation x-ref maint.')
      *****************************************************************
      *                                                               *
      *   Midas - Message Management Module                           *
      *                                                               *
      *   DSPF/ME1530DF - Message Tag Translation X-ref Maintenance   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CPK010             Date 20Oct99               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 162075             Date 02Sep99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                  162671           Date 23Jun99                *
      *                  CFT002  *CREATE  Date 23Apr98                *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *    CPK010 - Old BIS reference removed                         *
      *    162075 - Correct cursor positioning                        *
      *    162671 - Default Translation                               *
      *    CFT002 - Straight Through Processing Phase 1               *
      *             Based on ME1530DF original references             *
      *             HUB001/S01489                                     *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
      *=========================================================================
     A          R #RCDKEY                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      KEEP
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'CF12 Previous')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(31 'Invalid: #1MTPY')
     A                                      SETOF(32 'Invalid: #1MTAG')
      *                                                                   /*Begin 162671*/
     A                                      SETOF(34 'Invalid: #1DFLT')
      *                                                                   /*Begin 162075*/
     A                                      SETOF(35 'Position cursor #1MTPY')
     A                                      SETOF(36 'Position cursor #1MTAG')
      *                                                                   /*End   162075*/
      *                                                                   /*End   162671*/
     A                                      SETOF(99 'Global error flag')
      *-------------------------------------------------------------------------
      *
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*                                     Start of Change /CPK010/
     A**********************************1 26'B I S  ¦  M i d a s  ¦  A B S'
     A*                                       End of Change /CPK010/
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONKY        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code . . . :'
     A            #PACTC         1      4 24TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *                                                                   /*Begin 162671*/
     A**********************************9  2'Message type  . . .'
     A                                  9  2'Message type  . . . .'
      *                                                                   /*End   162671*/
     A            #1MTPY         3   B  9 24TEXT('Message type')
     A N25                                  OVRDTA
      *                                                                   /*Begin 162075*/
     A**31**********************************DSPATR(RI PC)
     A  31                                  DSPATR(RI)
     A  31
     AO 35                                  DSPATR(PC)
      *                                                                   /*End   162075*/
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *                                                                   /*Begin 162671*/
     A*********************************11  2'Message Field Tag .'
     A                                 11  2'Message Field Tag . .'
      *                                                                   /*End   162671*/
     A            #1MTAG         5   B 11 24TEXT('Message Field Tag')
     A N25                                  OVRDTA
      *                                                                   /*Begin 162075*/
     A**32**********************************DSPATR(RI PC)
     A  32                                  DSPATR(RI)
     A  32
     AO 36                                  DSPATR(PC)
      *                                                                   /*End   162075*/
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *                                                                   /*Begin 162671*/
     A                                 13  2'Default Translation .'
     A            #1DFLT         1   B 13 24
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *                                                                   /*End   162671*/
      *=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            #KCMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #KCMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #RCDDTL1                  TEXT('Details')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
      *-------------------------------------------------------------------------
      * Command keys
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF12(12 'Previous')
     A                                      CF05(05 'Reset.')
     A  77                                  CF10(10 'Delete.')
      *-------------------------------------------------------------------------
      * Setoffs
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #1FTSP')
     A                                      SETOF(32 'Invalid: #1NARR')
      *-------------------------------------------------------------------------
      *
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*                                     Start of Change /CPK010/
     A**********************************1 26'B I S  ¦  M i d a s  ¦  A B S'
     A*                                       End of Change /CPK010/
     A            ##MRDT         7A  O  1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONDT        40A  O  2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6Y 0O  2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Action Code  . . . . :'
     A            #1RECI         1   H      TEXT('Record Identifier')
     A            #1AJOB        10   H      TEXT('Job name')
     A            #1AACT         1   H      TEXT('Action Type')
     A            #1RDNB         5  0H      TEXT('Run day number')
     A            #PACTC         1A  O  4 27TEXT('Action Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  9  2'Message type . . . . :'
     A            #1MTPY         3A  O  9 27TEXT('Message type')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1MTPD        35A  O  9 40TEXT('Message Type Descr.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 11  2'Message Field Tag  . :'
     A            #1MTAG         5A  O 11 27TEXT('Message Field Tag')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #1SCFD        20A  O 11 40TEXT('Message Tag Descr.')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *                                                                   /*Begin 162671*/
     A                                 13  2'Default Translation  :'
     A            #1DFLT         1   O 13 27DSPATR(HI)
     A N25                                  OVRDTA
      *                                                                   /*End   162671*/
     A                                 19  2'Last Change Information'
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(UL)
     A N25                                  OVRATR
     A                                 20  2'User name :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1AUSR        10A  O 20 14TEXT('User name')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 25'Action Date :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ARDT         7A  O 20 39TEXT('Action Date')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 20 47'Action Time :'
     A  78                                  DSPATR(ND)
     A N25                                  OVRATR
     A            #1ATIM         6Y 0O 20 61TEXT('Action Time')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A  78                                  DSPATR(ND)
     A N78                                  DSPATR(HI)
     A N25                                  OVRATR
      *=========================================================================
     A          R #CMDTXT2                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *=========================================================================
