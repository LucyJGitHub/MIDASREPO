      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Extension details for Routing')               *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  ME1540#1 - Extension details for Routing                     *
      *                                                               *
      *  (C) Copyright Misys International Banking Systems Ltd 2003   *
      *                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 13Sep11               *
      *                 ESL038             Date 01Oct2004             *
      *                 EEE065             Date 01Jan2004             *
      *                 ERN054             Date 15Jul2003             *
      *                 EEE058 *Create     Date 26Mar2003             *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  ESL038 - ING STP Development                                 *
      *  EEE065 - FT Payment Routing                                  *
      *  ERN054 - Agent Message Changes                               *
      *  EEE058 - Incoming STP                                        *
      *                                                               *
      *****************************************************************
     A                                      PRINT
     A                                      INDARA
     A                                      CA03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A                                      CF12(12 'Previous')
     A          R ME1540W#1                 TEXT('(C) Misys IBS +
     A                                      Ltd. 2003')
      * Start ERN054
     A***************                       WINDOW(&SWROW &SWCOL 10 60)
      * Start EEE065
     A***************                       WINDOW(&SWROW &SWCOL 11 60)
     A                                      WINDOW(&SWROW &SWCOL 12 60)
      * End   EEE065
      * End   ERN054
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SETOF(51)
     A                                      SETOF(52)
     A                                      SETOF(53)
     A                                      SETOF(54)
     A                                      SETOF(55)
     A                                      SETOF(56)
      * Start ERN054
     A                                      SETOF(57)
      * End ERN054
      * Start EEE065
     A                                      SETOF(58)
      * End EEE065
     A            ##PGM         10A  H
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
      *----------------------------------------------------------------
      *
     A                                  2  1'Allow Back value Authorisation.:'
     A                                      OVRATR
     A            SBAUT          1A  B  2 35OVRDTA
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  3  1'Pay date must be today.........:'
     A                                      OVRATR
     A            SPDTD          1A  B  3 35OVRDTA
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  4  1'Mt191 required.................:'
     A                                      OVRATR
     A            SM191          1A  B  4 35OVRDTA
     A N53                                  DSPATR(HI)
     A  53                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  5  1'Mt900 required.................:'
     A                                      OVRATR
     A            SM900          1A  B  5 35OVRDTA
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  6  1'Mt910 required.................:'
     A                                      OVRATR
     A            SM910          1A  B  6 35OVRDTA
     A N55                                  DSPATR(HI)
     A  55                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                  7  1'STP Queue name ................:'
     A                                      OVRATR
     A            SSTPQ         20A  B  7 35OVRDTA
     A N56                                  DSPATR(HI)
     A  56                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      * Start ERN054
     A                                  8  1'Settle date must be today.....:'
     A                                      OVRATR
     A            SSLTD          1A  B  8 35OVRDTA
     A N57                                  DSPATR(HI)
     A  57                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
      * End   ERN054
      * Start EEE065
     A                                  9  1'Apply Payment Routing ........:'
     A                                      OVRATR
     A            SAPPR          1A  B  9 35OVRDTA
     A N58                                  DSPATR(HI)
     A  58                                  DSPATR(RI PC)
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
      *
      * End   EEE065
      * Start ERN054
     A*********************             9  1'F3=Exit         F12=Cancel-
      * Start EEE065
     A*********************            10  1'F3=Exit         F12=Cancel-
     A                                 11  1'F3=Exit         F12=Cancel-
      * End   EEE065
      * End   ERN054
     A                                                              '
      *
      *
     A*=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
