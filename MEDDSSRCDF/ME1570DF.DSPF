     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Default Charge Code Overrides Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Message Management Module                            *
     A*                                                               *
     A*  DSPF/ME1570DF - Default Charge Code Overrides Maintenance    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
     A*  Last Amend No. CFT009  *CREATE    Date 05Jun00               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*    CFT009 - FT Fees and Charges                               *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CA03(03)
     A N30                                  CF05(05)
     A                                      CA12(12)
     A*
     A*=========================================================================
     A          R ME1570F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                      DSPATR(HI)
     A                                  1 15'ME1570'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A                                      DSPATR(HI)
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A                                      DSPATR(HI)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                      DSPATR(HI)
     A                                  2 73TIME
     A                                      DSPATR(HI)
     A                                  2 22'Generation Criteria +
     A                                       Table Maintenance'
     A                                      DSPATR(HI)
     A                                  3 28'Default Charge Overrides'
     A                                      DSPATR(HI)
     A                                  5  6'Message Type........:'
     A            SMTPY          3A  O  5 28
     A                                  6  6'Set Number..........:'
     A            SMSET          3A  O  6 28
     A                                  8 28'Charge Code Overrides'
     A                                      DSPATR(HI)
     A                                 10  2'Payment date offset code'
     A            SPDOCD         5A  B 10 28
     A N31                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  31                                  DSPATR(RI PC)
     A N30N31                               DSPATR(UL)
     A                                 10 43'FX spread code'
     A            SFXSCD         5A  B 10 73
     A N32                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N30N32                               DSPATR(UL)
     A                                 12  2'S.W.I.F.T. standard +
     A                                       charge code'
     A            SSWSCD         5A  B 12 36
     A N33                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N30N33                               DSPATR(UL)
     A                                 12 43'Advice charge code'
     A            SADVCD         5A  B 12 73
     A N34                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N30N34                               DSPATR(UL)
     A                                 13  2'S.W.I.F.T. priority +
     A                                       charge code'
     A            SSWPCD         5A  B 13 36
     A N35                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N30N35                               DSPATR(UL)
     A                                 13 43'Costs abroad charge code'
     A            SCABCD         5A  B 13 73
     A N36                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N30N36                               DSPATR(UL)
     A                                 14  2'Telex charge code'
     A            STLXCD         5A  B 14 36
     A N37                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N30N37                               DSPATR(UL)
     A                                 14 43'Cheque charge code'
     A            SCHQCD         5A  B 14 73
     A N38                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A N30N38                               DSPATR(UL)
     A                                 15  2'Telephone charge code'
     A            STPHCD         5A  B 15 36
     A N39                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A N30N39                               DSPATR(UL)
     A                                 15 43'Miscellaneous charge code'
     A            SMI0CD         5A  B 15 73
     A N40                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  40                                  DSPATR(RI PC)
     A N30N40                               DSPATR(UL)
     A                                 16  2'Collection charge code'
     A            SCOLCD         5A  B 16 36
     A N41                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  41                                  DSPATR(RI PC)
     A N30N41                               DSPATR(UL)
     A                                 16 43'Miscellaneous charge +
     A                                       code 1'
     A            SMI1CD         5A  B 16 73
     A N42                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  42                                  DSPATR(RI PC)
     A N30N42                               DSPATR(UL)
     A                                 17  2'Correspondent''s charge +
     A                                       code'
     A            SCORCD         5A  B 17 36
     A N43                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  43                                  DSPATR(RI PC)
     A N30N43                               DSPATR(UL)
     A                                 17 43'Miscellaneous charge +
     A                                       code 2'
     A            SMI2CD         5A  B 17 73
     A N44                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  44                                  DSPATR(RI PC)
     A N30N44                               DSPATR(UL)
     A                                 18  2'Deductions charge code'
     A            SDEDCD         5A  B 18 36
     A N45                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  45                                  DSPATR(RI PC)
     A N30N45                               DSPATR(UL)
     A                                 18 43'Miscellaneous charge +
     A                                       code 3'
     A            SMI3CD         5A  B 18 73
     A N46                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  46                                  DSPATR(RI PC)
     A N30N46                               DSPATR(UL)
     A                                 19  2'FX/Change commission +
     A                                       charge code'
     A            SFCCCD         5A  B 19 36
     A N47                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  47                                  DSPATR(RI PC)
     A N30N47                               DSPATR(UL)
     A                                 19 43'Miscellaneous charge +
     A                                       code 4'
     A            SMI4CD         5A  B 19 73
     A N48                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  48                                  DSPATR(RI PC)
     A N30N48                               DSPATR(UL)
     A                                 20  2'Transfer commission +
     A                                       charge code'
     A            STCOCD         5A  B 20 36
     A N49                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  49                                  DSPATR(RI PC)
     A N30N49                               DSPATR(UL)
     A                                 20 43'Miscellaneous charge +
     A                                       code 5'
     A            SMI5CD         5A  B 20 73
     A N50                                  DSPATR(HI)
     A  30                                  DSPATR(PR)
     A  50                                  DSPATR(RI PC)
     A N30N50                               DSPATR(UL)
     A                                 23  2'F3=Exit'
     A N30                             23 11'F5=Refresh'
     A                                 23 23'F12=Previous'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
