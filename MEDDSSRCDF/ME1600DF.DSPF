     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Message repair')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Message Management Module                            *
     A*                                                               *
     A*  DSPF/ME1600DF - Message Repair Maintenance Display           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CSW209             Date 01Apr09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CFT006             Date 16DEC99               *
      * Midas DBA 3.01 -----------------------------------------------*
      *                 162075             Date 26Aug99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 162562             Date 17Jun99               *
     A*                 162233             Date 14Jun99               *
     A*                 162073             Date 08Jun99               *
     A*                 CFT002  *CREATE    Date 25Mar99               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *    CSW209 - SWIFT 2009 Changes                                *
      *    CFT006 - FT addition of MT101 and MT102 messages           *
      *    162075 - Correct cursor positioning                        *
     A*    162562 - Repair Sequence number input field seems to be 6  *
     A*             long and not 5                                    *
     A*    162233 - Rename Sequence number to Repair Sequence number  *
     A*    162073 - Error fix : no F12 in the subfile                 *
     A*    CFT002 - Straight Through Processing Phase 1               *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CA03(03)
     A*
     A*=========================================================================
     A          R HEADER
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                      DSPATR(HI)
     A                                  1 15'ME1600'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A                                      DSPATR(HI)
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A                                      DSPATR(HI)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                      DSPATR(HI)
     A                                  2 67'TIME '
     A                                      DSPATR(HI)
     A                                  2 73TIME
     A                                      DSPATR(HI)
     A                                  2 28'Message Repair Details'
     A                                      DSPATR(HI)
     A  50
     AO 51                              4  6'Action Code (I,A,D).:'
     A
     A  50
     AO 51        SACTC          1A  O  4 28
     A                                      DSPATR(HI)
     A                                  6  6'Message Type........:'
     A            SMTPY          3A  O  6 28
     A                                      DSPATR(HI)
     A                                  7  6'Set Number..........:'
     A            SMSET          3A  O  7 28
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R REPDTL                    SFL
     A  30                                  SFLNXTCHG
     A            S1AMD          1A  B 11 11
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A            S1SENO         5A  O 11 20
     A            S1PVAL         5A  O 11 35
     A            S1ITAG         5A  O 11 48
      ** Start of Change CSW209
     A            S1IMSQ         3A  O 11 54
     A  56
     AON56N31                               DSPATR(ND)
      ** End of Change CSW209
     A            S1RTAG         5A  O 11 62
      ** Start of Change CSW209
     A            S1RMSQ         3A  O 11 68
     A  56
     AON56N31                               DSPATR(ND)
      ** End of Change CSW209
      *                                                           /*Begin CFT006*/
     A            H1ITGH         1   H
     A            H1RTGH         1   H
      *                                                           /* End  CFT006*/
     A*=========================================================================
     A          R RDCTL                     SFLCTL(REPDTL)
     A N52                                  CF05(05)
     A                                      CF09(09)
      *                                                           /*Begin 162073*/
     A                                      CF12(12)
      *                                                           /* End  162073*/
     A                                      OVERLAY
     A  26                                  SFLDSP
     A  27                                  SFLDSPCTL
     A  28                                  SFLCLR
     A  29                                  SFLEND
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                  9 11'?'
     A                                      DSPATR(HI)
     A*                                                           /*Begin 162233*/
     A**********************************9 17'Sequence No.'
     A                                  9 17'Repair Seq No'
     A*                                                           /* End  162233*/
     A                                      DSPATR(HI)
     A                                  9 34'Process'
     A                                      DSPATR(HI)
     A                                  9 46'Input Tag'
     A                                      DSPATR(HI)
      ** Start of Change CSW209
     A                                  9 56'/Seq'
     A  56
     AON56N31                               DSPATR(ND)
     A                                      DSPATR(HI)
     A**********                        9 60'Repair Tag'                                      CSW209
     A                                  9 62'Repair Tag'
      ** End of Change CSW209
     A                                      DSPATR(HI)
      ** Start of Change CSW209
     A                                  9 73'/Seq'
     A  56
     AON56N31                               DSPATR(ND)
     A                                      DSPATR(HI)
      ** End of Change CSW209
     A            REC            3S 0H      SFLRCDNBR
     A*=========================================================================
     A          R REPDT2
     A N50                                  CF05(05)
     A  50                                  CF10(10)
     A                                      CF12(12)
     A                                      OVERLAY
      *
      *-------------------------------------------------------------------------
      *
     A*                                                           /*Begin 162233*/
     A********************************* 9 10'Sequence no:'
     A*********************************11 10'Process    :'
     A*********************************13 10'Input Tag  :'
     A*********************************15 10'Repair Tag :'
     A                                  9 10'Repair Seq no:'
     A                                 11 10'Process      :'
     A                                 13 10'Input Tag    :'
      ** Start of Change CSW209
     A                                 13 35'Sequence  :'
     A  56
     AON56N31                               DSPATR(ND)
      ** End of Change CSW209
     A                                 15 10'Repair Tag   :'
      ** Start of Change CSW209
     A                                 15 35'Sequence  :'
     A  56
     AON56N31                               DSPATR(ND)
      ** End of Change CSW209
      *
     A************S2SENO*********5  0B  9 24
     A            S2SENO         5  0B  9 26
     A*                                                           /* End  162233*/
     A N61                                  DSPATR(HI)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A  49
     AO 50                                  DSPATR(PR)
     A N49
     AAN50                                  DSPATR(UL)
     A*                                                           /*Begin 162562*/
     A                                      EDTCDE(4)
     A*                                                           /* End  162562*/
     A*                                                           /*Begin 162233*/
     A************S2PVAL*********5A  B 11 24
     A            S2PVAL         5A  B 11 26
     A*                                                           /* End  162233*/
     A N62                                  DSPATR(HI)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(UL)
     A*                                                           /*Begin 162233*/
     A************S2ITAG*********5A  B 13 24
     A            S2ITAG         5A  B 13 26
     A*                                                           /* End  162233*/
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(RI)
      *                                                           /*Begin 162075*/
     A  67
     A**63**********************************DSPATR(PC)
     AO 63                                  DSPATR(PC)
      *                                                           /*End   162075*/
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(UL)
      ** Start of Change CSW209
     A            S2IMSQ         3A  B 13 48
     A  56
     AON56N31                               DSPATR(ND)
     A N63                                  DSPATR(HI)
     A  63                                  DSPATR(RI)
     A  67
     AO 63                                  DSPATR(PC)
     A  56
     AON56N31
     AO 50                                  DSPATR(PR)
     A N50                                  DSPATR(UL)
      ** End of Change CSW209
     A*                                                           /*Begin 162233*/
     A************S2RTAG*********5A  B 15 24
     A            S2RTAG         5A  B 15 26
     A*                                                           /* End  162233*/
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(RI)
      *                                                           /*Begin 162075*/
     A  68
     A**64**********************************DSPATR(PC)
     AO 64                                  DSPATR(PC)
      *                                                           /*End   162075*/
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(UL)
      ** Start of Change CSW209
     A            S2RMSQ         3A  B 15 48
     A  56
     AON56N31                               DSPATR(ND)
     A N64                                  DSPATR(HI)
     A  64                                  DSPATR(RI)
     A  68
     AO 64                                  DSPATR(PC)
     A  56
     AON56N31
     AO 50                                  DSPATR(PR)
     A N50                                  DSPATR(UL)
      ** End of Change CSW209
     A*                                                           /*Begin CFT006*/
     A                                 13 35'Header (Y/N) : '
     A  31                                  DSPATR(ND)
     A            S2ITGH         1A  B 13 51
     A  31                                  DSPATR(ND)
     A N69N31                               DSPATR(HI)
     A  69N31                               DSPATR(RI)
     A  69N31                               DSPATR(PC)
     A  31
     AO 50                                  DSPATR(PR)
     A N50N31                               DSPATR(UL)
     A                                 15 35'Header (Y/N) : '
     A  31                                  DSPATR(ND)
     A            S2RTGH         1A  B 15 51
     A  31                                  DSPATR(ND)
     A N70N31                               DSPATR(HI)
     A  70N31                               DSPATR(RI)
     A  70N31                               DSPATR(PC)
     A  31
     AO 50                                  DSPATR(PR)
     A N50N31                               DSPATR(UL)
     A*                                                           /*End CFT006*/
     A*=========================================================================
     A          R CMDKEY
     A                                      OVERLAY
     A                                 23  6'F3=Exit'
     A                                      DSPATR(HI)
     A N50N52                          23 17'F5=Refresh'
     A                                      DSPATR(HI)
     A  50                             23 17'F10=Delete'
     A                                      DSPATR(HI)
     A  51                             23 31'F12=Previous'
     A                                      DSPATR(HI)
     A N50N51                          23 31'F9=Add'
     A                                      DSPATR(HI)
     A N50N51N52                       23 41'A=Amend'
     A                                      DSPATR(HI)
     A N50N51N52                       23 52'D=Delete'
     A                                      DSPATR(HI)
      *                                                           /*Begin 162073*/
     A N50N51                          23 62'F12=Previous'
      *                                                           /*End   162073*/
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
