      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME BIC ZIP and clearing prefix config. maint')   *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  ME1620DF - BIC ZIP and Clearing Prefix Configuration Maint.  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. MD021483           Date 22Aug13               *
      *  Prev Amend No. CFT050             Date 03Jun13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CFT007  *CREATE    Date 18Oct99               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD021483 - "?" Character is present beside SWIFT Country     *
      *             Label                                             *
      *  CFT050 - SWIFTRef Directories upload                         *
      *  CFT007 - BIC Database Plus Integration                       *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SBFLRCD                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLNXTCHG
     A            DDOPTN         1   B  6  2
     A N83                                  DSPATR(HI UL)
     A  83                                  DSPATR(ND PR)
     A            DDCNTY        70   O  6  4TEXT('S.W.I.F.T. Country')
     A N83                                  DSPATR(HI)
     A  83                                  DSPATR(ND)
     A            DDZLST         1   O  7 14TEXT('ZIP Located Last')
     A N83                                  DSPATR(HI)
     A  83                                  DSPATR(ND)
     A            DDCPFX         4   O  7 32TEXT('Clearing Code Prefix')
     A N83                                  DSPATR(HI)
     A  83                                  DSPATR(ND)
     A*=========================================================================
     A          R #SBFLCTL                  TEXT('Subfile control.')
     A                                      SFLCTL(#SBFLRCD)
     A                                      SFLPAG(8) SFLSIZ(11)
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A  80                                  SFLCLR
     A  81                                  SFLDSP
     A  81                                  SFLDSPCTL
     A  81 96                               SFLEND
     A* Command keys............................................................
     A                                      CF03(03 'Exit')
     A                                      CF09(09 'Insert')
     A                                      CF12(12 'Previous')
     A*.........................................................................
     A            @SFREC         4  0H      SFLRCDNBR(*TOP)
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A                                      OVRDTA
     A            DDMRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
      **Start of change CFT050
     A**********                        1 24'BIC ZIP Code +                                   CFT050
     A**********                            Clearing Prefix'                                  CFT050
     A                                  1 24'SWIFT Dir.plus zip +
     A                                      placem./clearing prefix'
      **End of change CFT050
     A                                      DSPATR(HI UL)
     A                                  2 71TIME EDTWRD('  :  :  ')
      **Start of Change MD021483
     A**********                        4  2'?'                                             MD021483
      **End of Change MD021483
     A                                  4  4'S.W.I.F.T. Country'
     A                                  5  7'ZIP Located Last'
     A                                  5 27'Clearing Code Prefix'
     A*=========================================================================
     A          R #DETAIL                   TEXT('DETAIL')
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                      CF03(03 'Exit')
     A  72                                  CF10(10 'DELETE')
     A                                      CF12(12 'Previous')
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A                                      OVRDTA
     A            DDMRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
      **Start of change CFT050
     A**********                        1 26'BIC ZIP Code and +                               CFT050
     A**********                            Clearing Prefix'                                  CFT050
     A                                  1 26'SWIFT Dir.plus zip +
     A                                      placem./clearing prefix'
      **End of change CFT050
     A                                      DSPATR(HI UL)
     A                                  2 71TIME EDTWRD('  :  :  ')
     A                                  4  4'S.W.I.F.T. Country'
     A            DDCNTY        70   B  5  4TEXT('S.W.I.F.T. Country')
     A N50                                  DSPATR(HI)
     A N51N52N71
     AO 50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A N70N50
     AAN71                                  DSPATR(UL)
        70
     AO 71                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7  4'Place ZIP/Postal +
     A                                      code after +
     A                                      city/state/county. :'
     A            DDZLST         1   B  7 54TEXT('ZIP Code')
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI)
       N71N52
     AO 51                                  DSPATR(PC)
     A N70N51                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9  4'Clearing code prefix +
     A                                      . . . . . . . . . +
     A                                      . . . . :'
     A            DDCPFX         4   B  9 54TEXT('Clearing Code')
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(PC RI)
     A N70N52                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A*=========================================================================
     A          R #SBFLRCB                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLNXTCHG
     A            DDSELC         1   B  5  2
     A N83                                  DSPATR(HI UL)
     A  83                                  DSPATR(ND PR)
     A            DDCNTY        70   O  5  4TEXT('S.W.I.F.T. Country')
     A N83                                  DSPATR(HI)
     A  83                                  DSPATR(ND)
     A*=========================================================================
     A          R #SBFLCTB                  TEXT('Subfile control.')
     A                                      SFLCTL(#SBFLRCB)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(17) SFLSIZ(18)
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A  80                                  SFLCLR
     A  81                                  SFLDSP
     A  81                                  SFLDSPCTL
     A  81 96                               SFLEND
     A* Command keys............................................................
     A                                      CF03(03 'Exit')
     A                                      CF12(12 'Previous')
     A*.........................................................................
     A            @SFREB         4  0H      SFLRCDNBR(*TOP)
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A                                      OVRDTA
     A            DDMRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  1 24'S.W.I.F.T. Country +
     A                                      Code Selection'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME EDTWRD('  :  :  ')
     A                                  4  2'?'
     A                                  4  4'S.W.I.F.T. Country'
     A*=========================================================================
     A          R #FOOTER                   TEXT('SCREEN FOOTER')
     A                                      OVERLAY
     A                                      PUTOVR
     A            FOOTR1        50     23  2OVRDTA
     A            FOOTR2        50     24  2OVRDTA
     A*=========================================================================
     A          R #MESSAGE                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(22)
     A            MSGKEY                    SFLMSGKEY
     A            DDPGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MESSAGE)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A N25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
