      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME BIC repair excluded phrases maintenance')     *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  ME1630DF - Midas ME excluded phrases maintenance             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD035218           Date 29Jul15               *
      *                 MD021482           Date 22Aug13               *
      *                 CFT050             Date 03Jun13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CFT007  *CREATE    Date 27Oct99               *
      *                                                               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD035218 - Incorrect message on SWIFT/SWIFTRef 2014          *
      *             related screens (Recompile)                       *
      *  MD021482 - "?" Character is present beside Excluded Phrase   *
      *             Label                                             *
      *  CFT050 - SWIFTRef Directories upload                         *
      *  CFT007 - BIC Database Plus Integration                       *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SBFLRCD                  TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLNXTCHG
     A            DDOPTN         1   B  5  2
     A N83                                  DSPATR(HI UL)
     A  83                                  DSPATR(ND PR)
     A            DDPHRS        20   O  5  4TEXT('Excluded phrase')
     A N83                                  DSPATR(HI)
     A  83                                  DSPATR(ND)
     A*=========================================================================
     A          R #SBFLCTL                  TEXT('Subfile control.')
     A                                      SFLCTL(#SBFLRCD)
     A                                      SFLPAG(17) SFLSIZ(18)
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A  80                                  SFLCLR
     A  81                                  SFLDSP
     A  81                                  SFLDSPCTL
     A  81 96                               SFLEND
     A* Command keys............................................................
     A                                      CF03(03 'Exit')
     A                                      CF09(09 'Insert')
     A                                      CF12(12 'Previous')
     A*.........................................................................
     A            @SFREC         4  0H      SFLRCDNBR(*TOP)
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A                                      OVRDTA
     A            DDMRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
      **Start of change CFT050
     A**********                        1 28'Excluded Search Phrases +                        CFT050
     A**********                            for'                                              CFT050
     A**********                            DSPATR(HI UL)                                     CFT050
     A**********                        2 33'BIC Database Plus'                               CFT050
     A                                  1 31'SWIFT Dir. Repair'
     A                                      DSPATR(HI UL)
     A                                  2 28'Excluded Phrases +
     A                                       Maint.'
      **End of change CFT050
     A                                      DSPATR(HI UL)
     A                                  2 71TIME EDTWRD('  :  :  ')
      **Start of Change MD021482
     A**********                        4  2'?'                                             MD021482
      **End of Change MD021482
     A                                  4  4'Excluded phrase'
     A*=========================================================================
     A          R #DETAIL                   TEXT('DETAIL')
     A*                                     OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                      CF03(03 'Exit')
     A  72                                  CF10(10 'DELETE')
     A                                      CF12(12 'Previous')
     A            ##USR         10      1  2TEXT('*USER')
     A                                      OVRDTA
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A                                      OVRDTA
     A            DDMRDT         7      1 72TEXT('Midas Run Date')
     A                                      OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                      OVRDTA
     A                                  1 24'Excluded Search Phrases'
     A                                      DSPATR(HI UL)
     A                                  2 71TIME EDTWRD('  :  :  ')
     A                                  5  4'Phrase to be excluded:'
     A            DDPHRS        20   B  5 27TEXT('Phrase to be excluded')
     A N50                                  DSPATR(HI)
     A  50                                  DSPATR(PC RI)
     A N70N50                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A*=========================================================================
     A          R #FOOTER                   TEXT('SCREEN FOOTER')
     A                                      OVERLAY
     A                                      PUTOVR
     A            FOOTR1        50     23  2OVRDTA
     A            FOOTR2        50     24  2OVRDTA
     A*=========================================================================
     A          R #MESSAGE                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(22)
     A            MSGKEY                    SFLMSGKEY
     A            DDPGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MESSAGE)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A N25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
