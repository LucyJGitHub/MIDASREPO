      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Bilateral Agreements subfile')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  DSPF/ME1710DF - ME Bilateral Agreements display file         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CFT014  *CREATE    Date 25May00               *
      *                 CFT013  *Create    Date 25May00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFT014 - Straight-through Processing Phase 2 MT103 Messages  *
      *           Generation for FT.                                  *
      *  CFT013 - Straight-through Processing Phase 2 MT103 Messages  *
      *           Generation for non-FT.                              *
      *                                                               *
      *****************************************************************
      *                                                                =========
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A          R RADDMD
     A                                      OVERLAY
     A                                      INDTXT(32 'Invalid: SBATY')
     A                                      INDTXT(33 'Invalid: SBRCA')
     A                                      INDTXT(34 'Invalid: SOTHP')
     A                                      INDTXT(35 'Invalid: SBAGD')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A                                      CF12(12 'Previous.')
     A            ##USR         10      1  2TEXT('*USER')
     A                                  1 13'ME1710F2'
     A            SMRDT          7      1 71TEXT('Midas Run Date')
     A            ##JOB         10      2  2TEXT('*JOB')
     A            SURPT         40      2 27TEXT('User Report Title')
     A                                      DSPATR(HI)
     A            STIME          6  0   2 70TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A                                  5  3'Agreement Type . . :'
     A            SBATY         10   B  5 25
     A N32                                  DSPATR(HI UL)
     A  32                                  DSPATR(RI PC)
     A                                  6  3'Branch . . . . . . :'
     A            SBRCA          3   B  6 25
     A N33                                  DSPATR(HI UL)
     A  33                                  DSPATR(RI PC)
     A                                  7  3'Other Party. . . . :'
     A            SOTHP         11   B  7 25
     A N34                                  DSPATR(HI UL)
     A  34                                  DSPATR(RI PC)
     A                                  9  3'Description. . . . :'
     A            SBAGD         35   B  9 25
     A N35                                  DSPATR(HI UL)
     A  35                                  DSPATR(RI PC)
      *
     A          R RSFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(31 'Invalid: SCSEL')
     A  84                                  SFLNXTCHG
     A            SDBRC         74   H      TEXT('Existing record fields')
     A            SACTM          6  0H      TEXT('Action Time')
     A            SACDA          7   H      TEXT('Action Date')
     A            SLCD           5  0H      TEXT('Last Change Date')
     A            SACTY          1   H      TEXT('Action Type')
     A            SCSEL          1   B  9  3
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A  51                                  DSPATR(ND PR)
     A            SBATY         10   O  9  8TEXT('Bilateral Agreement Type')
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI)
     A            SBRCA          3   O  9 22TEXT('Branch')
     A N37                                  DSPATR(HI)
     A  37                                  DSPATR(RI)
     A            SOTHP         11   O  9 29TEXT('Other Party')
     A N38                                  DSPATR(HI)
     A  38                                  DSPATR(RI)
     A            SBAGD         35   B  9 44TEXT('Bilateral Agreement +
     A                                      Description')
     A N39                                  DSPATR(HI)
     A N20N39                               DSPATR(UL)
     A  20                                  DSPATR(PR)
     A  39                                  DSPATR(RI)
     A*=========================================================================
     A          R RSFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(RSFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(11) SFLSIZ(12)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N40                                  CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A N40                                  CF09(09 'Add.')
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            SSFRC          4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                  1 13'ME1710F1'
     A            SMRDT          7      1 71TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            SURPT         40      2 27TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            STIME          6  0   2 70TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  5  8'Agreement'
     A                                  6 10'Type'
     A                                  6 21'Branch'
     A                                  6 29'Other Party'
     A                                  6 44'Description'
     A            SEBATY        10   B  7  8TEXT('Bilateral Agreement Type')
     A                                      DSPATR(UL HI)
     A            SEBRCA         3   B  7 22TEXT('Branch')
     A                                      DSPATR(UL HI)
     A            SEOTHP        11   B  7 29TEXT('Other Party')
     A                                      DSPATR(UL HI)
     A*=========================================================================
     A          R RCMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            SCTX1         78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R RMSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R RMSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(RMSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
