      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Outgoing Message Field Management')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  DSPF/ME1750DF - Outgoing Message Field Management            *
      *                                                               *
      *  (c) Finastra International Limited 2006                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CFT032  *CREATE    Date 11Sep06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFT032 - Account line in field 50X in MT103                  *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R RSFLRCD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2006')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(31 'Invalid: SISOC')
     A                                      INDTXT(32 'Invalid: SISOD')
     A                                      INDTXT(33 'Invalid: SSCHT')
     A                                      INDTXT(34 'Invalid: SCSEL')
     A                                      INDTXT(36 'Invalid: SSEEA')
     A  84                                  SFLNXTCHG
     A            SDBRC         66   H      TEXT('Existing record fields')
     A            SACJN         10   H      TEXT('Action Job Name')
     A            SACUN         10   H      TEXT('Action User Name')
     A            SACTM          6  0H      TEXT('Action Time')
     A            SACDA          7   H      TEXT('Action Date')
     A            SACTY          1   H      TEXT('Action Type')
     A            SCSEL          1   B  8  3
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A  50
     AO 51                                  DSPATR(ND PR)
     A            SOMNWRK        6   B  8  6TEXT('Network')
     A N31                                  DSPATR(HI)
     A  20
     AO 21                                  DSPATR(PR)
     A N20N31
     AAN21N31                               DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A            SOMMTPY        3   B  8 14TEXT('Message type')
     A N32                                  DSPATR(HI)
     A  20                                  DSPATR(PR)
     A N20N32                               DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A            SOMMTAG        5   B  8 19TEXT('Message Tag')
     A N33                                  DSPATR(HI)
     A  20                                  DSPATR(PR)
     A N20N33                               DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A            SOMSBRC        4   B  8 28TEXT('Senders branch')
     A N36                                  DSPATR(HI)
     A  20
     AON37                                  DSPATR(PR)
     A N20N36                               DSPATR(UL)
     A  36                                  DSPATR(RI PC)
     A            SOMCTRY        4   B  8 35TEXT('Country of location')
     A N38                                  DSPATR(HI)
     A  20                                  DSPATR(PR)
     A N20N38                               DSPATR(UL)
     A  38                                  DSPATR(RI)
     A            SOMCURR        2   B  8 43TEXT('Currency')
     A N38                                  DSPATR(HI)
     A  20                                  DSPATR(PR)
     A N20N38                               DSPATR(UL)
     A  38                                  DSPATR(RI)
     A            SOMNOST        2   B  8 53TEXT('Nostro')
     A N38                                  DSPATR(HI)
     A  20                                  DSPATR(PR)
     A N20N38                               DSPATR(UL)
     A  38                                  DSPATR(RI)
     A            SOMACTI       10   B  8 59TEXT('Action')
     A N38                                  DSPATR(HI)
     A  20                                  DSPATR(PR)
     A N20N38                               DSPATR(UL)
     A  38                                  DSPATR(RI)
     A            SOMFLAG        1   B  8 74TEXT('Flag')
     A N38                                  DSPATR(HI)
     A  20                                  DSPATR(PR)
     A N20N38                               DSPATR(UL)
     A  38                                  DSPATR(RI)
     A*=========================================================================
     A          R RSFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(RSFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N40                                  CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A N40                                  CF09(09 'Add.')
     A                                      CF11(11 'Report')
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            SSFRC          4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                  1 13'ME1750F1'
     A                                  1 38'Midas'
     A            SMRDT          7      1 71TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A            SURPT         30      2 25TEXT('User Report Title')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            STIME          6  0   2 70TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  4  3'?'
     A                                  4  6'Network'
     A                                  4 14'Msg'
     A                                  4 19'Message'
     A                                  4 27'Sender'
     A                                  4 35'Ctry'
     A                                  4 41'Currency'
     A                                  4 51'Nostro'
     A                                  4 59'Action'
     A                                  4 72'Flag'
     A                                  5 14'Typ'
     A                                  5 19'Field'
     A                                  5 27'Branch'
     A                                  5 38'Locat.'
     A                                  6 20'Tag'
     A            SEOMNWRK       6   B  7  6TEXT('Network')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A            SEOMMTPY       3   B  7 14TEXT('Message type')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A            SEOMMTAG       5   B  7 19TEXT('Message Tag')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A            SEOMSBRC       4   B  7 28TEXT('Senders branch')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A            SEOMCTRY       4   B  7 35TEXT('Country of location')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A            SEOMCURR       2   B  7 43TEXT('Currency')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A            SEOMNOST       2   B  7 53TEXT('Nostro')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A            SEOMACTI      10   B  7 59TEXT('Action')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A            SEOMFLAG       1   B  7 74TEXT('Flag')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A  50                                  DSPATR(ND PR)
     A*=========================================================================
     A          R RCMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            SCTX1         78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A          R RMSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R RMSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(RMSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
