      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Fontis TPP nostro defaults')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  DSPF/ME2006DF - Midas-Fontis TPP Nostro Defaults             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. 175469             Date 17Jan00               *
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CGL009  *CREATE    Date 18May99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  175469 - Euro nostro equivalent                              *
      *  CGL009 - Midas-Fontis Inter-Account Transfers (IAT) and      *
      *           Third Party Payments (TPP) Interface                *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      *--------------------------*
      ***  Subfile Record Format *
      *--------------------------*
      *
     A          R #SFLRCD                   SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(87 'Enable key screen')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A                                      INDTXT(33 'Invalid: #RLCFF')
     A                                      INDTXT(34 'Invalid: #1LCNM')
     A                                      INDTXT(35 'Invalid: #1CNCD')
     A*                                                               /*Beg 175469*/
     A                                      INDTXT(36 'Invalid: #1EUNO')
     A*                                                               /*End 175469*/
     A  84                                  SFLNXTCHG
     A*                                                               /*Beg 175469*/
     A************#1DBRC        40   H      TEXT('Existing record fields')
     A            #1DBRC        42   H      TEXT('Existing record fields')
     A*                                                               /*End 175469*/
     A            #1RECI         1   H      TEXT('Record Id')
     A            #1LCCD         3   H      TEXT('Currency Code')
     A            #1AJOB        10   H      TEXT('Action Job Name')
     A            #1AUSR        10   H      TEXT('Action User Name')
     A            #1ATIM         6  0H      TEXT('Action Time')
     A            #1ADTE         7   H      TEXT('Action Date')
     A            #1CHTP         1   H      TEXT('Action Type')
     A            #1SEL          1A  B  8  2VALUES('D')
     A                                      CHECK(AB)
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(UL)
     A N79N32                               DSPATR(HI)
     A            #RLCFF         3A  B  8 13TEXT('Currency of Payment')
     A  78                                  DSPATR(PR)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N78N33                               DSPATR(UL)
     A N33                                  DSPATR(HI)
     A            #1LCNM         2A  B  8 28TEXT('Nostro Number')
     A  87                                  DSPATR(PR)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N87N34                               DSPATR(UL)
     A N34                                  DSPATR(HI)
     A            #1CNCD         1A  B  8 40TEXT('Euro Payment')
     A N50                                  DSPATR(ND)
     A N50
     AO 87                                  DSPATR(PR)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N87N35                               DSPATR(UL)
     A N35                                  DSPATR(HI)
     A*                                                               /*Beg 175469*/
     A            #1EUNO         2A  B  8 54TEXT('Euro Nostro')
     A N50                                  DSPATR(ND)
     A N50
     AO 87                                  DSPATR(PR)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N87N36                               DSPATR(UL)
     A N36                                  DSPATR(HI)
     A*                                                               /*End 175469*/
      *
      *--------------------*
      ***  Subfile Control *
      *--------------------*
      *
     A          R #SFLCTL                   SFLCTL(#SFLRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A                                      INDTXT(89 '89')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25N80 81                            SFLDSP
     A N80 81 82                            SFLEND
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A N82                                  ROLLUP(27 'Next page.')
     A                                      CF05(05 'Reset.')
     A                                      CF09(09 'Change mode.')
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2LCCD')
     A            ##SFRC         4S 0H      SFLRCDNBR(CURSOR)
     A            ##USR         10A  O  1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'ME2006DF'
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 19'Midas-Fontis TPP Nostro Defaults M-
     A                                      aintenance'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4 10'Currency of     Nostro'
     A                                  5 12'Payment       Number'
     A  50                              4 39'Euro'
     A  50                              5 37'Payment?'
     A*                                                               /*Beg 175469*/
     A  50                              4 50'Euro Nostro'
     A  50                              5 52'Number'
     A*                                                               /*End 175469*/
     A N89        #2LCCD         3A  B  6 13TEXT('Currency Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
      *
      *-----------------*
      ***  Command Line *
      *-----------------*
      *
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A  89                             23  2'F3=Main menu   F5=Refresh   F9=Go-
     A                                       to ''Change'' mode'
     A N89                             23  2'D=Delete   F3=Main menu   F5=Refr-
     A                                      esh   F9=Go to ''Add'' mode'
      *
      *--------------------------*
      ***  Message Record Format *
      *--------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *--------------------*
      ***  Message Control *
      *--------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
