      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Work Midas Message Mgr Control Info')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  DSPF/ME9970DF- Midas Message Manager Control Info            *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG7413  *CREATE   Date 02Jun05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG7413 - ICD Midas Message Manager ends in error            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      CA03
     A                                      CA05
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
     A          R ME9970H0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      PUTOVR
     A                                      OVERLAY
      *
     A            #0USER        10A  O  1  2OVRDTA
     A            #0PGM         10A  O  1 13
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            #0WID         10A  O  2  2OVRDTA
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Type choices, press Enter.'
     A                                  1 25'Work Midas Message Manager Control-
     A                                       Info'
      *
     A          R ME9970D0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      OVERLAY
     A                                  6  2'Meridian Link database name  . .'
     A            #0LKDB        10A  B  6 37
     A N21                                  DSPATR(UL)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A                                  7  2'Meridian Server name . . . . . .'
     A            #0SVNM        10A  B  7 37
     A N22                                  DSPATR(UL)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A                                  8  2'Managing Midas system? . . . . .'
     A            #0MSYS         4A  B  8 37
     A N23                                  DSPATR(UL)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A  24                              9  2'Meridian managing s/w library  .'
     A  24N25     #0MLIB        10A  B  9 37DSPATR(UL)
     A                                      DSPATR(HI)
     A  24                             10  2'Meridian Link to CAS library . .'
     A  24N26     #0MCAS        10A  B 10 37DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  6 51'Name'
     A                                  7 51'Name'
     A                                  8 51'*YES, *NO'
     A  24                              9 51'Name'
     A  24                             10 51'Name'
      *
     A          R ME9970F0
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 22  2'F3=Exit'
     A                                 22 12'F5=Refresh'
      *
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
      *
     A            MSGKEY                    SFLMSGKEY
     A            #0PGM                     SFLPGMQ(10)
      *
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  35
     AON35                                  SFLEND
      *
     A            #0PGM                     SFLPGMQ(10)
