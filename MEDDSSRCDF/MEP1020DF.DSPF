      *****************************************************************
/*STD *  DSPFBASE                                                     *
      **************************************************************
      *                                                            *
      *  Midas - Message Management Module
      *                                                            *
      *   DSPF/MEP1020DF - Display Incoming Messages Display trans.*
      *                                                            *
      *  (c) Misys International Banking Systems Ltd. 2004         *
      *                                                            *
      *  Last Amend No. MD31937            Date 13Jan15            *
      *  Last Amend No. AR856737           Date 13Sep11            *
      *  Last Amend No. ESL038             Date 01Oct2004          *
      * Midas Release 4 --------------- Base ----------------------*
      *                                                            *
      **************************************************************
      *                                                            *
      *  MD31937  - Upgrade STP enhancements to BF Midas 2.1       *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level   *
      *  ESL038 - ING STP Development                              *
      *           Originally ME1020DF                              *
      *                                                            *
      **************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A                                      INDTXT(32 'Invalid: #1SEL')
     A  84                                  SFLNXTCHG
     A            #1DATA       512   H      TEXT('Record Datar')
     A            #1SEL          1   B  8  2
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
      *                                                  Begin /176583/
     A            #SDTAC         6      8  4TEXT('Date of Action')
     A                                      DSPATR(HI)
     A            #STIMA         6  0   8 11TEXT('Action Time')
     A                                      DSPATR(HI)
     A            #STEXT        62      8 18TEXT('Text')
     A                                      DSPATR(HI)
      *                                                  End   /176583/
     A*=========================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND(*MORE)
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  78                                  CF12(12 'CF12 Previous')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #CDTAC')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'MEP1020D1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            #PACTC         1   H      TEXT('Action Code')
     A  81                              5  2'?'
     A                                      DSPATR(HI) COLOR(WHT)
      *                                                  Begin /176583/
     A  81                              5  4'Action'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              6  4' Date '
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              6 18'Description'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              5 11'Action'
     A                                      DSPATR(HI) COLOR(WHT)
     A  81                              6 11' Time '
     A                                      DSPATR(HI) COLOR(WHT)
      *                                                  End   /176583/
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
