      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME Message Selection Criteria Prompt')           *
      **************************************************************
      *                                                            *
      *  Midas - Message Management Module                         *
      *                                                            *
      *   DSPF/ME1240DF - Message Selection Criteria Prompt        *
      *                                                            *
      *  (c) Misys International Banking Systems Ltd. 2003         *
      *                                                            *
      *  Last Amend No. MD031937           Date 13Jan15            *
      *  Prev Amend No. AR856737           Date 13Sep11            *
      *                 ESL038             Date 01Oct2004          *
      *                 EEE065             Date 07Sep2004          *
      *                 EEE058   *CREATE   Date 14Feb2003          *
      *                                                            *
      **************************************************************
      *                                                            *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1       *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level   *
      *  ESL038 - ING STP Development                              *
      *  EEE065 - FT Payment Routing                               *
      *  EEE058 - Incoming STP                                     *
      *                                                            *
      **************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDDTL1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* COMMAND KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A  79                                  CF12(12 'Previous')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #DPNWK')
     A                                      SETOF(32 'Invalid: #DPSND')
     A                                      SETOF(34 'Invalid: #1NWRK')
     A                                      SETOF(35 'Invalid: #1SNDR')
     A                                      SETOF(36 'Invalid: #1MIR')
     A                                      SETOF(37 'Invalid: #1MOR')
     A                                      SETOF(38 'Invalid: #1TRNO')
     A                                      SETOF(39 'Invalid: #1MTPY')
     A                                      SETOF(40 'Invalid: #1MPRY')
     A                                      SETOF(41 'Invalid: #1MODE')
     A                                      SETOF(42 'Invalid: #1CYCD')
     A                                      SETOF(43 'Invalid: #1SVDT')
     A                                      SETOF(44 'Invalid: #1TVDT')
     A                                      SETOF(45 'Invalid: #1BRCA')
     A                                      SETOF(46 'Invalid: #1DUPF')
     A                                      SETOF(50 'Invalid: #1STPQ')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'ME1240F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  4  2'Position on .'
     A  70                              5  2'Network . . . . . . . . . .'
     A  70        #DPNWK         6   B  5 32TEXT('Network (Position)')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  70                              5 43'Sender . .'
     A  70        #DPSND        20   B  5 56TEXT('Sender (Position)')
     A************                          CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  71                              5  2'Currency  . . . . . . . . .'
     A  71        #DPCCY         3   B  5 32TEXT('Ccy (Position)')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  71                              5 43'Trans. Ref'
     A  71        #DPTRN        16   B  5 56TEXT('Trans. (Position)')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  72                              5  2'FT Transaction reference  .'
     A  72        #DPPRF        15   B  5 32TEXT('Ccy (Position)')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  73N74                           5  2'No Positioning is available'
     A  73 74                           5  2'Received date . . . . . . .'
     A  73 74     #DPDAT         6  0B  5 32TEXT('Date (Position)')
     A                                      EDTCDE(X)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  73 74                           5 43'Time. . .'
     A  73 74     #DPTIM         4  0B  5 56TEXT('Time (Position)')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  7  2'Select on....'
     A                                  7 21'Queue.....'
     A            #1STPQ        20   B  7 32TEXT('STP Queue')
     A N25                                  OVRDTA
     A  50                                  DSPATR(RI PC)
     A N50                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *>> Start EEE065
     A                                  8 21'Status....'
     A            #1STAT         4   B  8 32TEXT('Status')
     A N25                                  OVRDTA
     A  51                                  DSPATR(RI PC)
     A N51                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *<< End EEE065
     A                                  9  2'Network  .'
     A            #1NWRK         6   B  9 32TEXT('Network')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10  2'Sender  . . . . . . . . . .'
     A            #1SNDR        20   B 10 32TEXT('Sender')
     A************                          CHECK(LC)
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*/COPY WNCPYSRC,ME1240DF01
     A                                 12  2'Message Input reference . .'
     A            #1MIR         28   B 12 32TEXT('Message Input reference')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 13  2'Message Output reference  .'
     A            #1MOR         28   B 13 32TEXT('Message Output reference')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 14  2'Transaction reference . . .'
     A            #1TRNO        16   B 14 32TEXT('Transaction Reference')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 15  2'Message type  . . . . . . .'
     A            #1MTPY         3   B 15 32TEXT('Message type')
     A N25                                  OVRDTA
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 15 43'Priority  .'
     A            #1MPRY         1   B 15 57TEXT('Message Priority')
     A N25                                  OVRDTA
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 16  2'Message Received Date . . .'
     A            #1MODE         6   B 16 32TEXT('Message Received Date')
     A N25                                  OVRDTA
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*/COPY WNCPYSRC,ME1240DF02
     A                                 18  2'Currency  . . . . . . . . .'
     A            #1CYCD         3   B 18 32TEXT('Currency Code       (key)')
     A N25                                  OVRDTA
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 18 43'Value date .(from) .'
     A            #1SVDT         6   B 18 65TEXT('Value date  YYMMDD format')
     A N25                                  OVRDTA
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 19 43'Value date .(To) . .'
     A            #1TVDT         6   B 19 65TEXT('Value date  (To)')
     A N25                                  OVRDTA
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A  78                             20 43'Branch Code . .'
     A  78        #1BRCA         3   B 20 60TEXT('Branch Code')
     A N25                                  OVRDTA
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 20  2'Duplication Flag  . . . . .'
     A            #1DUPF         3   B 20 32TEXT('Duplication Flag')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
