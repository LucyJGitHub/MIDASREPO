      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME MTn92 Messages Maintenance')                  *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management                                   *
      *                                                               *
      *  MEP720DF - MTn92 Messages Maintenance                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 13Sep11               *
      *                 EEE065             Date 01Oct2004             *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  EEE065 - SWIFT MT192/292 Message Maintenance.                *
      *                                                               *
      *****************************************************************
      *
     A                                      PRINT(MPRINT)
     A                                      CA03
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      * ----------------------------------------------------------------------
      *
     A          R MEP720X1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A            SUSER     R   10   O  1  2REFFLD(USER)
     A            SPGM      R   10   O  1 13REFFLD(JOPGM)
     A            SRUNA     R    7   O  1 72REFFLD(RUNA)
     A            SWSID     R   10   O  2  2REFFLD(JOJOB)
     A            X1MTPY         5   O  1 27TEXT('Message Type')
     A                                      DSPATR(HI)
     A                                  1 33'MESSAGE MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  2 71TIME
      *
      * ----------------------------------------------------------------------
      *
     A          R MEP720X2                  OVERLAY
      *
     A                                  3 02'Action'
     A            X2ACTN         1   B  3 09
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(RI PC)
     A                                  3 11'TRN'
     A            X2TRN         16   B  3 15
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A                                  4 02'Related Reference'
     A            X2F21         16   B  4 20
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A                                  5 02'Include field 79: Narrative Descri-
     A                                      ption of the Original Message (Y/N)'
     A            X2I79          1   B  5 72
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(RI PC)
      *
     A                                 23  2'F3-Exit'
      *
      * ----------------------------------------------------------------------
      *
     A          R MEP720X3                  OVERLAY
      *
     A  15N14
     AON11N12                          23  2'F3-Exit  F12-Cancel'
     A  11N14                          23  2'F3-Exit  F10-Continue  F12-Cancel'
     A  11 14                          23  2'F3-Exit  F10-Delete  F12-Cancel'
     A N11 12                          23  2'F3-Exit  F9-Add  F12-Cancel'
     A  13                             23 73'DELETED'
     A                                      DSPATR(RI)
      *
      * ----------------------------------------------------------------------
      *
     A          R MEP720S0                  TEXT('Field 79')
     A                                      SFL
     A  24                                  SFLNXTCHG
     A            S0F79         50   B 09 15TEXT('Field 79')
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(RI PC)
     A  48                                  DSPATR(UL)
     A N48                                  DSPATR(PR)
      *
      * ----------------------------------------------------------------------
      *
     A          R MEP720C0                  TEXT('Field 79 subfile control')
     A                                      CA12
     A  11                                  CA10
     A  12                                  CA09
     A                                      SFLCTL(MEP720S0)
     A                                      SFLPAG(13)
     A                                      SFLSIZ(35)
     A                                      OVERLAY
     A N31                                  SFLDSP SFLDSPCTL
     A  31                                  SFLCLR
     A  60
     AON60                                  SFLEND
      *
     A                                  4  2'Branch'
     A            C0BRCD         3   O  4  9DSPATR(HI)
     A                                  4 13'Address'
     A            C0ADDR        20   O  4 21 DSPATR(HI)
     A                                  4 42'or destination customer'
     A            C0CUST        10   O  4 66 DSPATR(HI)
     A                                 05 13'Network'
     A            C0NTWK         6   O 05 21 DSPATR(HI)
     A                                  5 45':20: TRN'
     A            X2TRN         16   O  5 54 DSPATR(HI)
     A                                  6  2':21: Related Reference'
     A            X2F21         16   O  6 25DSPATR(HI)
     A                                  7  2':11S: Type and Date of Original Me-
     A                                      ssage'
     A            C0F111         3   O  7 42TEXT('Original Message Type')
     A                                      DSPATR(HI)
     A            C0F112         6   O  7 46TEXT('Original Message Date')
     A                                      DSPATR(HI)
     A            C0F113        10   O  7 53TEXT('Original Msg Input Seq.no')
     A                                      DSPATR(HI)
      *
     A                                 08 02':79: Narrative Description of the -
     A                                      Original Message'
      *
      * ----------------------------------------------------------------------
      *
     A          R MEP720S1                  TEXT('Copy of orig.msg fields')
     A                                      SFL
     A  25                                  SFLNXTCHG
      *
     A            S1MTAG         5   H      TEXT('MTAG')
     A            S1SEL          1   B 09  2TEXT('Select field')
     A  15
     AON48                                  DSPATR(ND PR)
     A  25N15
     AO 48N15                               DSPATR(PC)
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(HI)
     A  48                                  DSPATR(UL)
     A            S1FLD         77   O 09  4TEXT('Any field')
     A  30                                  DSPATR(HI)
      *
      * ----------------------------------------------------------------------
      *
     A          R MEP720C1                  TEXT('Subfile control')
     A                                      SFLCTL(MEP720S1)
     A                                      SFLPAG(13)
     A                                      SFLSIZ(14)
     A                                      OVERLAY
     A N31                                  SFLDSP SFLDSPCTL
     A  31                                  SFLCLR
     A  60
     AON60                                  SFLEND
     A                                      CA12
     A  11                                  CA10
     A  12N15                               CA09
      *
     A                                  4  2'Branch'
     A            C0BRCD         3   O  4  9DSPATR(HI)
     A                                  4 13'Address'
     A            C0ADDR        20   O  4 21DSPATR(HI)
     A                                  4 42'or destination customer'
     A            C0CUST        10   O  4 66DSPATR(HI)
     A                                 05 13'Network'
     A            C0NTWK         6   O 05 21DSPATR(HI)
     A                                  5 45':20: TRN'
     A            X2TRN         16   O  5 54DSPATR(HI)
     A                                  6  2':21: Related Reference'
     A            X2F21         16   O  6 25DSPATR(HI)
     A                                  7  2':11S: Type and Date of Original Me-
     A                                      ssage'
     A            C0F111         3   O  7 42TEXT('Original Message Type')
     A                                      DSPATR(HI)
     A            C0F112         6   O  7 46TEXT('Original Message Date')
     A                                      DSPATR(HI)
     A            C0F113        10   O  7 53TEXT('Original Msg Input Seq.no')
     A                                      DSPATR(HI)
      *
     A                                 08 02'------------------------ Copy of o-
     A                                      riginal message fields -------------
     A                                      -------'
      *
      * ----------------------------------------------------------------------
      *
     A          R MEP720S2                  TEXT('Additional fields')
     A                                      SFL
     A  25                                  SFLNXTCHG
      *
     A            S2MTAG         5   H      TEXT('MTAG')
     A            S2SEL          1   B 09  2TEXT('Select field')
     A N48                                  DSPATR(ND)
     A  25
     AO 48                                  DSPATR(PC)
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(HI)
     A  48                                  DSPATR(UL)
     A N48                                  DSPATR(PR)
     A            S2FLD         77   O 09  4TEXT('Any field')
     A  30                                  DSPATR(HI)
      *
      * ----------------------------------------------------------------------
      *
     A          R MEP720C2                  TEXT('Subfile control')
     A                                      SFLCTL(MEP720S2)
     A                                      SFLPAG(13)
     A                                      SFLSIZ(14)
     A                                      OVERLAY
     A N31                                  SFLDSP SFLDSPCTL
     A  31                                  SFLCLR
     A  60
     AON60                                  SFLEND
     A                                      CA12
      *
     A                                  4  2'Branch'
     A            C0BRCD         3   O  4  9DSPATR(HI)
     A                                  4 13'Address'
     A            C0ADDR        20   O  4 21DSPATR(HI)
     A                                  4 42'or destination customer'
     A            C0CUST        10   O  4 66DSPATR(HI)
     A                                 05 13'Network'
     A            C0NTWK         6   O 05 21DSPATR(HI)
     A                                  5 45':20: TRN'
     A            X2TRN         16   O  5 54DSPATR(HI)
     A                                  6  2':21: Related Reference'
     A            X2F21         16   O  6 25DSPATR(HI)
     A                                  7  2':11S: Type and Date of Original Me-
     A                                      ssage'
     A            C0F111         3   O  7 42TEXT('Original Message Type')
     A                                      DSPATR(HI)
     A            C0F112         6   O  7 46TEXT('Original Message Date')
     A                                      DSPATR(HI)
     A            C0F113        10   O  7 53TEXT('Original Msg Input Seq.no')
     A                                      DSPATR(HI)
      *
     A                                 08 02'---------------------- Select opti-
     A                                      onal fields to include -------------
     A                                      -------'
      *
      * ----------------------------------------------------------------------
      *
      **  Message subfile Record format
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
      * ----------------------------------------------------------------------
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  60
     AON60                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
