      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_RU                                                        *
/*EXI *  TEXT('Midas Incoming Message FT Data')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Message Management Module
     A*                                                               *
     A*  PF/MEINFTPD - Incoming Message FT Data                       *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 13Sep11               *
      *                 247197             Date 21Aug07               *
      *                 CSW220             Date 09Mar20               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01435             Date 29Jul93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level      *
      *  247197 - Add field FTSTPF (STPed Flag).                      *
      *  CSW220 - SWIFT Changes 2020                                  *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  S01435 - Incoming Message Management                         *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A*=====================================================================
     A          R @INFTPD                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*---------------------------------------------------------------------
     A            FTMSGR    R               TEXT('Msg Unique Reference')
     A                                      COLHDG('Msg Unique' +
     A                                             'Reference')
     A                                      REFFLD(MSGR)
     A            FTKPRT    R               TEXT('Part Number (Key)')
     A                                      COLHDG('Part Number' +
     A                                             '(Key)')
     A                                      REFFLD(KPRT)
     A            FTBRCA    R               TEXT('Branch Code')
     A                                      COLHDG('Branch' +
     A                                             'Code')
     A                                      REFFLD(BRCA)
     A            FTINFG    R               TEXT('Incoming Flag')
     A                                      COLHDG('Incoming' +
     A                                             'Flag')
     A                                      REFFLD(MSID)
     A            FTIPRF    R               TEXT('Incoming Payment Ref.')
     A                                      COLHDG('Incoming' +
     A                                             'Payment Ref.')
     A                                      REFFLD(PREF)
     A            FTOTFG    R               TEXT('Outgoing Flag')
     A                                      COLHDG('Outgoing' +
     A                                             'Flag')
     A                                      REFFLD(MSID)
     A            FTOPRF    R               TEXT('Outgoing Payment Ref.')
     A                                      COLHDG('Outgoing' +
     A                                             'Payment Ref.')
     A                                      REFFLD(PREF)
     A**Start of change CGL029
     A**********  FTFRGL    R               TEXT('Funds From')                                CGL029
     A**********                            COLHDG('Funds' +                                  CGL029
     A**********                                   'From')                                    CGL029
     A**********                            REFFLD(SEAC)                                      CGL029
     A            QQFRGL        18A
     A**********  FTTOGL    R               TEXT('Funds To')                                  CGL029
     A**********                            COLHDG('Funds To')                                CGL029
     A**********                            REFFLD(SEAC)                                      CGL029
     A            QQTOGL        18A
     A**End of change CGL029
     A            FTAJOB    R               TEXT('Job name')
     A                                      COLHDG('Job name')
     A                                      REFFLD(JOJOB)
     A            FTAUSR    R               TEXT('User name')
     A                                      COLHDG('User name')
     A                                      REFFLD(JOUSER)
     A            FTATIM    R     P         TEXT('Action Time')
     A                                      COLHDG('Action' +
     A                                             'Time')
     A                                      REFFLD(MTIME)
     A            FTARDT    R               TEXT('Action Date')
     A                                      COLHDG('Action' +
     A                                             'Date')
     A                                      REFFLD(RUNA)
     A            FTAACT    R               TEXT('Action Type')
     A                                      COLHDG('Action' +
     A                                             'Type')
     A                                      REFFLD(LCTP)
     A            FTRDNB    R               TEXT('Run day number')
     A                                      COLHDG('Run day' +
     A                                             'number')
     A                                      REFFLD(RUND)
     A*=====================================================================
     A**Start of change CGL029
     A            FTFRGL        24A         TEXT('Funds From')
     A            FTTOGL        24A         TEXT('Funds To')
     A**End of change CGL029
     A*=====================================================================
     A**Start of change CSW220
     A            FTM199         1A         TEXT('MT199 Flag')
     A**End of change CSW220
      * Start of Change 247197
     A            FTSTPF         1A         TEXT('STPed Flag')
     A                                      COLHDG('STPed Flag')
      * End of Change 247197
