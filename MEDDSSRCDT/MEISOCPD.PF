      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas ME SWIFT ISO country codes file')                *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  PF/MEISOCPD - SWIFT ISO Country Codes File                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CSW213             Date 03Jun13               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CSW209             Date 01Apr09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CSW207             Date 07Jul07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CFT032             Date 11Sep06               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CFT017             Date 12Nov01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CFT014  *CREATE    Date 25May00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW213 - SWIFT Changes 2013                                  *
      *  CSW209 - SWIFT 2009 Changes                                  *
      *  CSW207 - SWIFT 2007 Standard Changes                         *
      *  CFT032 - Account line in field 50X in MT103                  *
      *  CFT017 - Multilateral Inter-bank Exchange Fee.               *
      *  CFT014 - Straight-through Processing Phase 2 MT103           *
      *           Messages Generation for FT.                         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R MEISOCD0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            M3ISOC         2A         TEXT('ISO Country Code')
     A                                      COLHDG('ISO Country Code')
     A            M3ISOD        30A         TEXT('ISO Country Code +
     A                                      Description')
     A                                      COLHDG('ISO Country Code' +
     A                                      'Description')
     A            M3SCHT         1A         TEXT('Supports Charge +
     A                                      Transparency?')
     A                                      COLHDG('Supports Charge' +
     A                                      'Transparency?')
     A            M3ACJN        10A         TEXT('Action Job Name')
     A                                      COLHDG('Action Job Name')
     A            M3ACUN        10A         TEXT('Action User Name')
     A                                      COLHDG('Action User Name')
     A            M3ACTM         6P 0       TEXT('Action Time')
     A                                      COLHDG('Action Time')
     A            M3ACDA         7A         TEXT('Action Date')
     A                                      COLHDG('Action Date')
     A            M3ACTY         1A         TEXT('Action Type')
     A                                      COLHDG('Action Type')
      **Start of Change CFT017
     A            M3EEAM         1A         TEXT('EEA Member')
     A                                      COLHDG('EEA Member')
      **End of Change CFT017
      ** Start of change CFT032
     A            M3FATF         1A         TEXT('Support FATF')
     A                                      COLHDG('Support FATF')
      ** End of change CFT032
      **Start of change CSW207
     A            M3MFID         1A         TEXT('Support MiFID')
     A                                      COLHDG('Support MiFID')
      **End of change CSW207
      ** Start of Change CSW209
     A            M3M205         1A         TEXT('Support SWIFT MT205')
     A                                      COLHDG('Support MT205')
      ** End of Change CSW209
      **Start of change CSW213
     A            M3SEPA         1A         TEXT('Supports SEPA')
     A                                      COLHDG('Supports SEPA')
     A            M3IBAC         2A         TEXT('IBAN Country Code')
     A                                      COLHDG('IBAN Country Code')
     A            M3IBAR         1A         TEXT('IBAN Required in +
     A                                       MT102+ / MT103+')
     A                                      COLHDG('MT102/MT103 Req')
     A            M3OCDT         5S 0       TEXT('Optional Commence +
     A                                       Date')
     A                                      COLHDG('Opt Comm Date')
     A            M3MCDT         5S 0       TEXT('Mandatory Commence +
     A                                            Date')
     A                                      COLHDG('Mand Com Date')
     A            M3CCSP         2S 0       TEXT('Country Code Start +
     A                                            Position')
     A                                      COLHDG('Cntry Code StrPos')
     A            M3CCLN         1S 0       TEXT('Country Code Length')
     A                                      COLHDG('Cntry Code Length')
     A            M3ICSP         2S 0       TEXT('IBAN Check Digit +
     A                                            Start Position')
     A                                      COLHDG('IBAN ChkDig +
     A                                            StrPos')
     A            M3ICLN         2S 0       TEXT('IBAN Check Digit +
     A                                            Length')
     A                                      COLHDG('IBAN ChkDig Len')
     A            M3BBSP         2S 0       TEXT('BBAN Start +
     A                                           Position')
     A                                      COLHDG('BBAN StrPos')
     A            M3BBLN         2S 0       TEXT('BBAN Length')
     A                                      COLHDG('BBAN Len')
     A            M3BISP         2S 0       TEXT('Bank ID Start +
     A                                            Position')
     A                                      COLHDG('Bnk ID StrPos')
     A            M3BILN         2S 0       TEXT('Bank ID Length')
     A                                      COLHDG('Bnk ID Len')
     A            M3BRSP         2S 0       TEXT('Branch ID Start +
     A                                           Position')
     A                                      COLHDG('Brch ID StrPos')
     A            M3BRLN         2S 0       TEXT('Branch ID Length')
     A                                      COLHDG('Brch ID Len')
     A            M3INLN         2S 0       TEXT('IBAN National ID +
     A                                            Length')
     A                                      COLHDG('IBAN Nat ID Len')
     A            M3ACSP         2S 0       TEXT('Account Number +
     A                                            Position Start +
     A                                            Position')
     A                                      COLHDG('A/C No Pos StrPos')
     A            M3ACLN         2S 0       TEXT('Account Number +
     A                                            Position Length')
     A                                      COLHDG('A/C No Pos Len')
     A            M3TBLN         2S 0       TEXT('Total Bank Length')
     A                                      COLHDG('Tot Bnk Len')
     A            M3TILN         2S 0       TEXT('Total IBAN Length')
     A                                      COLHDG('Tot IBAN Len')
      **End of change CSW213
      *
      *
