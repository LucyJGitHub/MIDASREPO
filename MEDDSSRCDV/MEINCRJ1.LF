      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas ME I.M.Control  Message Control Info.')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Message Management Module
     A*                                                               *
     A*  LF/MEINCRJ1 - Incoming Message Control  Message Control Info.*
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 124673             Date 05Jun98               *
      *                 S01435             Date 29Jul93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  124673 - Add Century fields for Year 2000.                   *
     A*  S01435 - Incoming Message Management                         *
     A*                                                               *
     A*****************************************************************
     A                                      JDFTVAL
     A*=====================================================================
     A          R @INCRJ1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      JFILE(MEINCRPD +
     A                                            MEINMPPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from Incoming Message Control to Incoming Message Part Dtl
     A          J                           JOIN(1 2)
     A                                      JFLD(CRMSGR MPMSGR)
     A*---------------------------------------------------------------------
     A            CRMSGR                    TEXT('Msg Unique Reference')
     A                                      JREF(1)
     A            CRKPRT                    TEXT('Part number.')
     A                                      JREF(2)
     A                                      RENAME(MPKPRT)
     A            CRTRNO                    TEXT('Transaction reference no.')
     A                                      JREF(2)
     A                                      RENAME(MPTRNO)
     A            CRCCYP                    TEXT('Currency Code      (Part)')
     A                                      JREF(2)
     A                                      RENAME(MPCYCD)
      *                                              Begin /124673/
     A            CRVDTC                    TEXT('Century of Value date')
     A                                      JREF(2)
     A                                      RENAME(MPSVDC)
      *                                              End   /124673/
     A            CRVDTP                    TEXT('Value date  YYMMDD (Part)')
     A                                      JREF(2)
     A                                      RENAME(MPSVDT)
     A            CRAMTP                    TEXT('Payment Amount     (Part)')
     A                                      JREF(2)
     A                                      RENAME(MPAMTS)
     A            CRINBK                    TEXT('Intermediary')
     A                                      JREF(2)
     A                                      RENAME(MPINBK)
     A            CRACIN                    TEXT('A/c with Institution')
     A                                      JREF(2)
     A                                      RENAME(MPACIN)
     A            CRBENF                    TEXT('Beneficiary')
     A                                      JREF(2)
     A                                      RENAME(MPBENF)
     A            CRPYAM                    TEXT('Payment Amount')
     A                                      JREF(2)
     A                                      RENAME(MPPYAM)
     A            CRSYTM                    TEXT('System')
     A                                      JREF(1)
     A            CRNWRK                    TEXT('Network')
     A                                      JREF(1)
     A            CRSNDR                    TEXT('Sender')
     A                                      JREF(1)
     A            CRMIR                     TEXT('Message Input reference')
     A                                      JREF(1)
     A            CRMOR                     TEXT('Message Output reference')
     A                                      JREF(1)
     A            CRMTPY                    TEXT('Message type')
     A                                      JREF(1)
     A            CRMPRY                    TEXT('Message Priority')
     A                                      JREF(1)
      *                                              Begin /124673/
     A            CRMDEC                    TEXT('Message Received Century')
     A                                      JREF(1)
      *                                              End   /124673/
     A            CRMODE                    TEXT('Message Received Date')
     A                                      JREF(1)
     A            CRMOTM                    TEXT('Message Received Time')
     A                                      JREF(1)
      *                                              Begin /124673/
     A            CRSTDC                    TEXT('Message Sent Century')
     A                                      JREF(1)
      *                                              End   /124673/
     A            CRSNTD                    TEXT('Message Sent Date')
     A                                      JREF(1)
     A            CRSNTT                    TEXT('Message Sent Time')
     A                                      JREF(1)
     A            CRCRCY                    TEXT('Currency Code       (key)')
     A                                      JREF(1)
     A                                      RENAME(CRCYCD)
      *                                              Begin /124673/
     A            CRSVDC                    TEXT('Century of Value date')
     A                                      JREF(1)
      *                                              End   /124673/
     A            CRCRDT                    TEXT('Value date  YYMMDD format')
     A                                      JREF(1)
     A                                      RENAME(CRSVDT)
     A            CRCRPA                    TEXT('Payment Amount')
     A                                      JREF(1)
     A                                      RENAME(CRAMTS)
     A            CRBRCA                    TEXT('Branch Code')
     A                                      JREF(1)
     A            CRDUPF                    TEXT('Duplication Flag')
     A                                      JREF(1)
     A*/COPY WNCPYSRC,MEINCRJ101
     A            CRNPRT                    TEXT('Number of Parts')
     A                                      JREF(1)
     A            CRAJOB                    TEXT('Job name')
     A                                      JREF(1)
     A            CRAUSR                    TEXT('User name')
     A                                      JREF(1)
     A            CRATIM                    TEXT('Action Time')
     A                                      JREF(1)
     A            CRARDT                    TEXT('Action Date')
     A                                      JREF(1)
     A            CRAACT                    TEXT('Action Type')
     A                                      JREF(1)
     A            CRRDNB                    TEXT('Run day number')
     A                                      JREF(1)
     A*.....................................................................
     A* Key fields
     A          K CRNWRK
     A          K CRSNDR
      *                                              Begin /124673/
     A          K CRSVDC
      *                                              End   /124673/
     A          K CRCRDT
     A          K CRCRCY
     A          K CRCRPA
     A          K CRMIR
     A          K CRMSGR
     A*=====================================================================
