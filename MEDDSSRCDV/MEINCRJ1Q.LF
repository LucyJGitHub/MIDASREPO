      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas ME I.M.Control  Message Control Info.')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module
      *                                                               *
      *  LF/MEINCRJ1 - Incoming Message Control  Message Control Info.*
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 15Jul2011             *
      *                 EEE065             Date 07Sep2004             *
      *                 ESL038             Date 01Oct2004             *
      *                 EEE058   *CREATE   Date 14Feb2003             *
      * Midas Release 4 --------------- Base -------------------------*
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level.     *
      *  EEE065 - FT Payment Routing                                  *
      *  ESL038 - ING STP Development                                 *
      *  EEE058 - Incoming STP                                        *
      *  124673 - Add Century fields for Year 2000.                   *
      *  S01435 - Incoming Message Management                         *
      *                                                               *
      *****************************************************************
     A                                      JDFTVAL
     A*=====================================================================
     A          R @INCRJ1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      JFILE(FTMESQP0 +
     A                                            MEINCRPD +
     A                                            MEINMPPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from STP Queues to FT data
     A          J                           JOIN(1 2)
     A                                      JFLD(SQMSGR CRMSGR)
     A*---------------------------------------------------------------------
     A* Join from Incoming Message Control to Incoming Message Part Dtl
     A          J                           JOIN(1 3)
     A                                      JFLD(SQMSGR MPMSGR)
     A*---------------------------------------------------------------------
     A            CRMSGR                    TEXT('Msg Unique Reference')
     A                                      JREF(1)
     A                                      RENAME(SQMSGR)
     A            CRKPRT                    TEXT('Part number.')
     A                                      JREF(1)
     A                                      RENAME(SQKPRT)
     A            CRTRNO                    TEXT('Transaction reference no.')
     A                                      JREF(3)
     A                                      RENAME(MPTRNO)
     A            CRCCYP                    TEXT('Currency Code      (Part)')
     A                                      JREF(3)
     A                                      RENAME(MPCYCD)
      *                                              Begin /124673/
     A            CRVDTC                    TEXT('Century of Value date')
     A                                      JREF(3)
     A                                      RENAME(MPSVDC)
      *                                              End   /124673/
     A            CRVDTP                    TEXT('Value date  YYMMDD (Part)')
     A                                      JREF(3)
     A                                      RENAME(MPSVDT)
     A            CRAMTP                    TEXT('Payment Amount     (Part)')
     A                                      JREF(3)
     A                                      RENAME(MPAMTS)
     A            CRINBK                    TEXT('Intermediary')
     A                                      JREF(3)
     A                                      RENAME(MPINBK)
     A            CRACIN                    TEXT('A/c with Institution')
     A                                      JREF(3)
     A                                      RENAME(MPACIN)
     A            CRBENF                    TEXT('Beneficiary')
     A                                      JREF(3)
     A                                      RENAME(MPBENF)
     A            CRPYAM                    TEXT('Payment Amount')
     A                                      JREF(3)
     A                                      RENAME(MPPYAM)
     A            CRSYTM                    TEXT('System')
     A                                      JREF(2)
     A            CRNWRK                    TEXT('Network')
     A                                      JREF(1)
     A            CRSNDR                    TEXT('Sender')
     A                                      JREF(1)
     A            CRMIR                     TEXT('Message Input reference')
     A                                      JREF(1)
     A            CRMOR                     TEXT('Message Output reference')
     A                                      JREF(2)
     A            CRMTPY                    TEXT('Message type')
     A                                      JREF(2)
     A            CRMPRY                    TEXT('Message Priority')
     A                                      JREF(2)
      *                                              Begin /124673/
     A            CRMDEC                    TEXT('Message Received Century')
     A                                      JREF(2)
      *                                              End   /124673/
     A            CRMODE                    TEXT('Message Received Date')
     A                                      JREF(2)
     A            CRMOTM                    TEXT('Message Received Time')
     A                                      JREF(2)
      *                                              Begin /124673/
     A            CRSTDC                    TEXT('Message Sent Century')
     A                                      JREF(2)
      *                                              End   /124673/
     A            CRSNTD                    TEXT('Message Sent Date')
     A                                      JREF(2)
     A            CRSNTT                    TEXT('Message Sent Time')
     A                                      JREF(2)
     A            CRCRCY                    TEXT('Currency Code       (key)')
     A                                      JREF(1)
     A                                      RENAME(CRCYCD)
      *                                              Begin /124673/
     A            CRSVDC                    TEXT('Century of Value date')
     A                                      JREF(1)
      *                                              End   /124673/
     A            CRCRDT                    TEXT('Value date  YYMMDD format')
     A                                      JREF(1)
     A                                      RENAME(CRSVDT)
     A            CRCRPA                    TEXT('Payment Amount')
     A                                      JREF(1)
     A                                      RENAME(CRAMTS)
     A            CRBRCA                    TEXT('Branch Code')
     A                                      JREF(2)
     A            CRDUPF                    TEXT('Duplication Flag')
     A                                      JREF(2)
     A*/COPY WNCPYSRC,MEINCRJ101
     A            CRNPRT                    TEXT('Number of Parts')
     A                                      JREF(2)
     A            CRAJOB                    TEXT('Job name')
     A                                      JREF(2)
     A            CRAUSR                    TEXT('User name')
     A                                      JREF(2)
     A            CRATIM                    TEXT('Action Time')
     A                                      JREF(2)
     A            CRARDT                    TEXT('Action Date')
     A                                      JREF(2)
     A            CRAACT                    TEXT('Action Type')
     A                                      JREF(2)
     A            CRRDNB                    TEXT('Run day number')
     A                                      JREF(2)
     A            SQSTPQ                    TEXT('STP Queue Name')
     A                                      JREF(1)
      *>> Start EEE065
     A            SQSTAT                    TEXT('STP Queue Status')
     A                                      JREF(1)
      *>> End EEE065
     A*.....................................................................
     A* Key fields
     A          K SQSTPQ
     A          K CRNWRK
     A          K CRSNDR
      *                                              Begin /124673/
     A          K CRSVDC
      *                                              End   /124673/
     A          K CRCRDT
     A          K CRCRCY
     A          K CRCRPA
     A          K CRMIR
     A          K CRMSGR
     A*=====================================================================
