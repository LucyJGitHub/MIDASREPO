      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas ME I.M Control  Msg Managemnt all data')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Message Management Module
     A*                                                               *
     A*  LF/MEINCRM1 - Incoming Message Control  Msg Management alldt *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. CSW220             Date 09Mar20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CSW209             Date 01Apr09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 124673             Date 04Jun98               *
      *                 S01435             Date 29Jul93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CSW220 - SWIFT Changes 2020 (Recompile)                      *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW209 - SWIFT 2009 Changes                                  *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
     A*  124673 - Add Century fields for Year 2000.                   *
     A*  S01435 - Incoming Message Management                         *
     A*                                                               *
     A*****************************************************************
     A          R @INCRM1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      PFILE(MEINCRPD)
     A*---------------------------------------------------------------------
     A            CRMSGR                    TEXT('Msg Unique Reference')
     A            CRSYTM                    TEXT('System')
     A            CRNWRK                    TEXT('Network')
     A            CRSNDR                    TEXT('Sender')
     A            CRMIR                     TEXT('Message Input reference')
     A            CRMOR                     TEXT('Message Output reference')
     A            CRMTPY                    TEXT('Message type')
     A            CRMPRY                    TEXT('Message Priority')
      *                                              Begin /124673/
     A            CRMDEC                    TEXT('Century of Msg Received Date')
      *                                              End   /124673/
     A            CRMODE                    TEXT('Message Received Date')
     A            CRMOTM                    TEXT('Message Received Time')
      *                                              Begin /124673/
     A            CRSTDC                    TEXT('Century of Msg Sent Date')
      *                                              End   /124673/
     A            CRSNTD                    TEXT('Message Sent Date')
     A            CRSNTT                    TEXT('Message Sent Time')
     A            CRCYCD                    TEXT('Currency Code       (key)')
      *                                              Begin /124673/
     A            CRSVDC                    TEXT('Century of Value Date')
      *                                              End   /124673/
     A            CRSVDT                    TEXT('Value date  YYMMDD format')
     A            CRAMTS                    TEXT('Payment Amount')
     A            CRBRCA                    TEXT('Branch Code')
     A            CRDUPF                    TEXT('Duplication Flag')
     A*/COPY WNCPYSRC,MEINCRM101
     A            CRNPRT                    TEXT('Number of Parts')
     A            CRAJOB                    TEXT('Job name')
     A            CRAUSR                    TEXT('User name')
     A            CRATIM                    TEXT('Action Time')
     A            CRARDT                    TEXT('Action Date')
     A            CRAACT                    TEXT('Action Type')
     A            CRRDNB                    TEXT('Run day number')
     A*.....................................................................
     A* Key fields
     A          K CRMSGR
     A*=====================================================================
     A          R @INMPM1                   TEXT('Incoming Message Part Dtl')
     A                                      PFILE(MEINMPPD)
     A*---------------------------------------------------------------------
     A            MPMSGR                    TEXT('Msg Unique Reference')
     A            MPKPRT                    TEXT('Part Number (Key)')
     A            MPTRNO                    TEXT('Transaction reference no.')
     A            MPCYCD                    TEXT('Currency Code       (key)')
     A            MPSVDT                    TEXT('Value date  YYMMDD format')
     A            MPAMTS                    TEXT('Payment Amount')
     A            MPINBK                    TEXT('Intermediary')
     A            MPACIN                    TEXT('A/c with Institution')
     A            MPBENF                    TEXT('Beneficiary')
     A            MPPYAM                    TEXT('Payment Amount')
     A*.....................................................................
     A* Key fields
     A          K MPMSGR
     A          K *NONE
     A          K MPKPRT
     A*=====================================================================
     A          R @INFTM1                   TEXT('Incoming Message FT Data')
     A                                      PFILE(MEINFTPD)
     A*---------------------------------------------------------------------
     A            FTMSGR                    TEXT('Msg Unique Reference')
     A            FTKPRT                    TEXT('Part Number (Key)')
     A            FTBRCA                    TEXT('Branch Code')
     A            FTINFG                    TEXT('Incoming Flag')
     A            FTIPRF                    TEXT('Incoming Payment Ref.')
     A            FTOTFG                    TEXT('Outgoing Flag')
     A            FTOPRF                    TEXT('Outgoing Payment Ref.')
     A            FTFRGL                    TEXT('Funds From')
     A            FTTOGL                    TEXT('Funds To')
     A            FTAJOB                    TEXT('Job name')
     A            FTAUSR                    TEXT('User name')
     A            FTATIM                    TEXT('Action Time')
     A            FTARDT                    TEXT('Action Date')
     A            FTAACT                    TEXT('Action Type')
     A            FTRDNB                    TEXT('Run day number')
     A*.....................................................................
     A* Key fields
     A          K FTMSGR
     A          K *NONE
     A          K FTKPRT
     A*=====================================================================
     A          R @INDTM1                   TEXT('Incoming Message Detail')
     A                                      PFILE(MEINDTPD)
     A*---------------------------------------------------------------------
     A            DTMSGR                    TEXT('Msg Unique Reference')
     A            DTSEQN                    TEXT('Sequence Number')
     A            DTMTG                     TEXT('Message Tag')
     A            DTPART                    TEXT('Part Number')
     A            DTFPID                    TEXT('First Part Identifier')
     A            DTMFLD                    TEXT('Message detail')
      ** Start of Change CSW209
     A            DTMSEQ                    TEXT('Message Sequence')
      ** End of Change CSW209
     A*.....................................................................
     A* Key fields
     A          K DTMSGR
     A          K DTSEQN
     A          K DTPART
     A*=====================================================================
     A          R @IN35M1                   TEXT('Incoming Message Det 35')
     A                                      PFILE(MEIN35PD)
     A*---------------------------------------------------------------------
     A            DSMSGR                    TEXT('Msg Unique Reference')
     A            DSSEQN                    TEXT('Sequence Number')
     A            DSMTG                     TEXT('Message Tag')
     A            DSPART                    TEXT('Part Number')
     A            DSFPID                    TEXT('First Part Identifier')
     A            DSMF35                    TEXT('Message detail (35 chars)')
      ** Start of Change CSW209
     A            DSMSEQ                    TEXT('Message Sequence')
      ** End of Change CSW209
     A*.....................................................................
     A* Key fields
     A          K DSMSGR
     A          K DSSEQN
     A          K DSPART
     A*=====================================================================
     A          R @INLFM1                   TEXT('I.M.M. Log File')
     A                                      PFILE(MEINLFPD)
     A*---------------------------------------------------------------------
     A            LFMSGR                    TEXT('Msg Unique Reference')
     A            LFKPRT                    TEXT('Part Number (Key)')
     A            LFMSID                    TEXT('Action (Msg id.)')
     A            LFMSGF                    TEXT('Action Msg File')
     A            LFMSDT                    TEXT('Action data')
     A            LFARSM                    TEXT('Action Result')
     A            LFARSD                    TEXT('Action Result Data')
     A            LFCPGM                    TEXT('Calling Process')
     A            LFRPGM                    TEXT('Called Process')
     A            LFAJOB                    TEXT('Job name')
     A            LFAUSR                    TEXT('User name')
     A            LFATIM                    TEXT('Action Time')
     A            LFARDT                    TEXT('Action Date')
     A            LFAACT                    TEXT('Action Type')
     A            LFRDNB                    TEXT('Run day number')
     A*.....................................................................
     A* Key fields
     A          K LFMSGR
     A          K *NONE
     A          K LFKPRT
     A*=====================================================================
