      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas I.M Control  Details (both)')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Message Management Module
     A*                                                               *
     A*  LF/MEINDTS1 - Incoming Message Control  Details (both)       *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CSW209             Date 01Apr09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01435             Date 29Jul93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CSW209 - SWIFT 2009 Changes                                  *
     A*  S01435 - Incoming Message Management                         *
     A*                                                               *
     A*****************************************************************
     A          R @INDTS1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PFILE(MEINDTPD)
     A*---------------------------------------------------------------------
     A            DTMSGR                    TEXT('Msg Unique Reference')
     A            DTSEQN                    TEXT('Sequence Number')
     A            DTMTG                     TEXT('Message Tag')
     A            DTPART                    TEXT('Part Number')
     A            DTMFLD                    TEXT('Message detail')
     A            DTFPID                    TEXT('First Part Identifier')
      ** Start of Change CSW209
     A            DTMSEQ                    TEXT('Message Sequence')
      ** End of Change CSW209
     A*.....................................................................
     A* Key fields
     A          K DTMSGR
     A          K DTSEQN
     A*=====================================================================
     A          R @IN35S1                   TEXT('Incoming Message Det 35')
     A                                      PFILE(MEIN35PD)
     A*---------------------------------------------------------------------
     A            DSMSGR                    TEXT('Msg Unique Reference')
     A            DSSEQN                    TEXT('Sequence Number')
     A            DSMTG                     TEXT('Message Tag')
     A            DSPART                    TEXT('Part Number')
     A            DSMF35                    TEXT('Message detail (35 chars)')
     A            DSFPID                    TEXT('First Part Identifier')
      ** Start of Change CSW209
     A            DSMSEQ                    TEXT('Message Sequence')
      ** End of Change CSW209
     A*.....................................................................
     A* Key fields
     A          K DSMSGR
     A          K DSSEQN
     A*=====================================================================
