      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas IMM FT Incoming Transactions')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Message Management Module
     A*                                                               *
     A*  LF/MEINFTJ2 - Incoming Message FT Data  Incoming Transactions*
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 124673             Date 08Jun98               *
      *                 S01435             Date 29Jul93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
     A*  124673 - Add Century fields for Year 2000.                   *
     A*  S01435 - Incoming Message Management                         *
     A*                                                               *
     A*****************************************************************
     A                                      JDFTVAL
     A                                      DYNSLT
     A*=====================================================================
     A          R @INFTJ2
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      JFILE(MEINFTPD +
     A                                            MEINCRPD +
     A                                            MEINMPPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from Incoming Message FT Data to Incoming Message Control
     A          J                           JOIN(1 2)
     A                                      JFLD(FTMSGR CRMSGR)
     A*---------------------------------------------------------------------
     A* Join from Incoming Message FT Data to Incoming Message Part Dtl
     A          J                           JOIN(1 3)
     A                                      JFLD(FTMSGR MPMSGR)
     A                                      JFLD(FTKPRT MPKPRT)
     A*---------------------------------------------------------------------
     A            INMSGR                    TEXT('Msg Unique Reference')
     A                                      JREF(1)
     A                                      RENAME(FTMSGR)
     A            INSYTM                    TEXT('System')
     A                                      JREF(2)
     A                                      RENAME(CRSYTM)
     A            INNWRK                    TEXT('Network')
     A                                      JREF(2)
     A                                      RENAME(CRNWRK)
     A            INSNDR                    TEXT('Sender')
     A                                      JREF(2)
     A                                      RENAME(CRSNDR)
     A            INMIR                     TEXT('Message Input reference')
     A                                      JREF(2)
     A                                      RENAME(CRMIR)
     A            INMOR                     TEXT('Message Output reference')
     A                                      JREF(2)
     A                                      RENAME(CRMOR)
     A            INMTPY                    TEXT('Message type')
     A                                      JREF(2)
     A                                      RENAME(CRMTPY)
     A            INMPRY                    TEXT('Message Priority')
     A                                      JREF(2)
     A                                      RENAME(CRMPRY)
      *                                              Begin /124673/
     A            INMDEC                    TEXT('Message Received Century')
     A                                      JREF(2)
     A                                      RENAME(CRMDEC)
      *                                              End   /124673/
     A            INMODE                    TEXT('Message Received Date')
     A                                      JREF(2)
     A                                      RENAME(CRMODE)
     A            INMOTM                    TEXT('Message Received Time')
     A                                      JREF(2)
     A                                      RENAME(CRMOTM)
      *                                              Begin /124673/
     A            INSTDC                    TEXT('Message Sent Century')
     A                                      JREF(2)
     A                                      RENAME(CRSTDC)
      *                                              End   /124673/
     A            INSNTD                    TEXT('Message Sent Date')
     A                                      JREF(2)
     A                                      RENAME(CRSNTD)
     A            INSNTT                    TEXT('Message Sent Time')
     A                                      JREF(2)
     A                                      RENAME(CRSNTT)
     A            INCRCY                    TEXT('Currency Code       (CR)')
     A                                      JREF(2)
     A                                      RENAME(CRCYCD)
      *                                              Begin /124673/
     A            INSVDC                    TEXT('Century of Value date')
     A                                      JREF(2)
     A                                      RENAME(CRSVDC)
      *                                              End   /124673/
     A            INCRDT                    TEXT('Value date  YYMMDD  (CR)')
     A                                      JREF(2)
     A                                      RENAME(CRSVDT)
     A            INCRPA                    TEXT('Payment Amount      (CR)')
     A                                      JREF(2)
     A                                      RENAME(CRAMTS)
     A            INBRCH                    TEXT('Branch Code')
     A                                      JREF(2)
     A                                      RENAME(CRBRCA)
     A            INDUPF                    TEXT('Duplication Flag')
     A                                      JREF(2)
     A                                      RENAME(CRDUPF)
     A            INNPRT                    TEXT('Number of Parts')
     A                                      JREF(2)
     A                                      RENAME(CRNPRT)
     A            INKPRT                    TEXT('Part Number (Key)')
     A                                      JREF(1)
     A                                      RENAME(FTKPRT)
     A            INTRNO                    TEXT('Transaction reference no.')
     A                                      JREF(3)
     A                                      RENAME(MPTRNO)
     A            INCCYP                    TEXT('Currency Code      (Part)')
     A                                      JREF(3)
     A                                      RENAME(MPCYCD)
      *                                              Begin /124673/
     A            INVDTC                    TEXT('Century of Value date')
     A                                      JREF(3)
     A                                      RENAME(MPSVDC)
      *                                              End   /124673/
     A            INVDTP                    TEXT('Value date  YYMMDD (Part)')
     A                                      JREF(3)
     A                                      RENAME(MPSVDT)
     A            INAMTP                    TEXT('Payment Amount     (Part)')
     A                                      JREF(3)
     A                                      RENAME(MPAMTS)
     A            ININBK                    TEXT('Intermediary')
     A                                      JREF(3)
     A                                      RENAME(MPINBK)
     A            INACIN                    TEXT('A/c with Institution')
     A                                      JREF(3)
     A                                      RENAME(MPACIN)
     A            INBENF                    TEXT('Beneficiary')
     A                                      JREF(3)
     A                                      RENAME(MPBENF)
     A            INPMFG                    TEXT('Payment Message Flag')
     A                                      JREF(3)
     A                                      RENAME(MPPMFG)
     A            INPYAM                    TEXT('Payment Amount')
     A                                      JREF(3)
     A                                      RENAME(MPPYAM)
     A            INBRCA                    TEXT('Branch Code        (Part)')
     A                                      JREF(1)
     A                                      RENAME(FTBRCA)
     A            ININFG                    TEXT('Incoming Flag')
     A                                      JREF(1)
     A                                      RENAME(FTINFG)
     A            INIPRF                    TEXT('Incoming Payment Ref.')
     A                                      JREF(1)
     A                                      RENAME(FTIPRF)
     A            INOTFG                    TEXT('Outgoing Flag')
     A                                      JREF(1)
     A                                      RENAME(FTOTFG)
     A            INOPRF                    TEXT('Outgoing Payment Ref.')
     A                                      JREF(1)
     A                                      RENAME(FTOPRF)
     A            INFRGL                    TEXT('Funds From')
     A                                      JREF(1)
     A                                      RENAME(FTFRGL)
     A            INTOGL                    TEXT('Funds To')
     A                                      JREF(1)
     A                                      RENAME(FTTOGL)
     A            INAJOB                    TEXT('Job name')
     A                                      JREF(1)
     A                                      RENAME(FTAJOB)
     A            INAUSR                    TEXT('User name')
     A                                      JREF(1)
     A                                      RENAME(FTAUSR)
     A            INATIM                    TEXT('Action Time')
     A                                      JREF(1)
     A                                      RENAME(FTATIM)
     A            INARDT                    TEXT('Action Date')
     A                                      JREF(1)
     A                                      RENAME(FTARDT)
     A            INAACT                    TEXT('Action Type')
     A                                      JREF(1)
     A                                      RENAME(FTAACT)
     A            INRDNB                    TEXT('Run day number')
     A                                      JREF(1)
     A                                      RENAME(FTRDNB)
     A*.....................................................................
     A* Key fields
     A          K INIPRF
     A*.....................................................................
     A* SELECT: Incomings Only
     A* Field    : Incoming Payment Ref.
     A* Condition: Not Blank
     A          S INIPRF                    COMP(NE ' ')
     A*=====================================================================
