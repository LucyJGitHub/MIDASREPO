      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas IMM FT Incoming Transactions')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module
      *                                                               *
      *  LF/MEINFTJ2 - Incoming Message FT Data  Incoming Transactions*
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
     A*  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 15Jul2011             *
      *                 EEE065             Date 07Sep2004             *
      *                 ESL038             Date 01Oct2004             *
      *                 EEE058   *CREATE   Date 14Feb2003             *
      * Midas Release 4 --------------- Base -------------------------*
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level.     *
      *  EEE065 - FT Payment Routing                                  *
      *  ESL038 - ING STP Development                                 *
      *  EEE058 - Incoming STP                                        *
      *                                                               *
      *****************************************************************
     A                                      JDFTVAL
     A                                      DYNSLT
     A*=====================================================================
     A          R @INFTJ2
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      JFILE(FTMESQP0 +
     A                                            MEINFTPD +
     A                                            MEINCRPD +
     A                                            MEINMPPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from STP Queues to FT data
     A          J                           JOIN(1 2)
     A                                      JFLD(SQMSGR FTMSGR)
     A*---------------------------------------------------------------------
     A* Join from Incoming Message FT Data to Incoming Message Control
     A          J                           JOIN(1 3)
     A                                      JFLD(SQMSGR CRMSGR)
     A*---------------------------------------------------------------------
     A* Join from Incoming Message FT Data to Incoming Message Part Dtl
     A          J                           JOIN(1 4)
     A                                      JFLD(SQMSGR MPMSGR)
     A                                      JFLD(SQKPRT MPKPRT)
     A*---------------------------------------------------------------------
     A            INMSGR                    TEXT('Msg Unique Reference')
     A                                      JREF(1)
     A                                      RENAME(SQMSGR)
     A            INSYTM                    TEXT('System')
     A                                      JREF(3)
     A                                      RENAME(CRSYTM)
     A            INNWRK                    TEXT('Network')
     A                                      JREF(3)
     A                                      RENAME(CRNWRK)
     A            INSNDR                    TEXT('Sender')
     A                                      JREF(3)
     A                                      RENAME(CRSNDR)
     A            INMIR                     TEXT('Message Input reference')
     A                                      JREF(3)
     A                                      RENAME(CRMIR)
     A            INMOR                     TEXT('Message Output reference')
     A                                      JREF(3)
     A                                      RENAME(CRMOR)
     A            INMTPY                    TEXT('Message type')
     A                                      JREF(3)
     A                                      RENAME(CRMTPY)
     A            INMPRY                    TEXT('Message Priority')
     A                                      JREF(3)
     A                                      RENAME(CRMPRY)
      *                                              Begin /124673/
     A            INMDEC                    TEXT('Message Received Century')
     A                                      JREF(3)
     A                                      RENAME(CRMDEC)
      *                                              End   /124673/
     A            INMODE                    TEXT('Message Received Date')
     A                                      JREF(3)
     A                                      RENAME(CRMODE)
     A            INMOTM                    TEXT('Message Received Time')
     A                                      JREF(3)
     A                                      RENAME(CRMOTM)
      *                                              Begin /124673/
     A            INSTDC                    TEXT('Message Sent Century')
     A                                      JREF(3)
     A                                      RENAME(CRSTDC)
      *                                              End   /124673/
     A            INSNTD                    TEXT('Message Sent Date')
     A                                      JREF(3)
     A                                      RENAME(CRSNTD)
     A            INSNTT                    TEXT('Message Sent Time')
     A                                      JREF(3)
     A                                      RENAME(CRSNTT)
     A            INCRCY                    TEXT('Currency Code       (CR)')
     A                                      JREF(3)
     A                                      RENAME(CRCYCD)
      *                                              Begin /124673/
     A            INSVDC                    TEXT('Century of Value date')
     A                                      JREF(3)
     A                                      RENAME(CRSVDC)
      *                                              End   /124673/
     A            INCRDT                    TEXT('Value date  YYMMDD  (CR)')
     A                                      JREF(3)
     A                                      RENAME(CRSVDT)
     A            INCRPA                    TEXT('Payment Amount      (CR)')
     A                                      JREF(3)
     A                                      RENAME(CRAMTS)
     A            INBRCH                    TEXT('Branch Code')
     A                                      JREF(3)
     A                                      RENAME(CRBRCA)
     A            INDUPF                    TEXT('Duplication Flag')
     A                                      JREF(3)
     A                                      RENAME(CRDUPF)
     A            INNPRT                    TEXT('Number of Parts')
     A                                      JREF(3)
     A                                      RENAME(CRNPRT)
     A            INKPRT                    TEXT('Part Number (Key)')
     A                                      JREF(1)
     A                                      RENAME(SQKPRT)
     A            INTRNO                    TEXT('Transaction reference no.')
     A                                      JREF(4)
     A                                      RENAME(MPTRNO)
     A            INCCYP                    TEXT('Currency Code      (Part)')
     A                                      JREF(4)
     A                                      RENAME(MPCYCD)
      *                                              Begin /124673/
     A            INVDTC                    TEXT('Century of Value date')
     A                                      JREF(4)
     A                                      RENAME(MPSVDC)
      *                                              End   /124673/
     A            INVDTP                    TEXT('Value date  YYMMDD (Part)')
     A                                      JREF(4)
     A                                      RENAME(MPSVDT)
     A            INAMTP                    TEXT('Payment Amount     (Part)')
     A                                      JREF(4)
     A                                      RENAME(MPAMTS)
     A            ININBK                    TEXT('Intermediary')
     A                                      JREF(4)
     A                                      RENAME(MPINBK)
     A            INACIN                    TEXT('A/c with Institution')
     A                                      JREF(4)
     A                                      RENAME(MPACIN)
     A            INBENF                    TEXT('Beneficiary')
     A                                      JREF(4)
     A                                      RENAME(MPBENF)
     A            INPMFG                    TEXT('Payment Message Flag')
     A                                      JREF(4)
     A                                      RENAME(MPPMFG)
     A            INPYAM                    TEXT('Payment Amount')
     A                                      JREF(4)
     A                                      RENAME(MPPYAM)
     A            INBRCA                    TEXT('Branch Code        (Part)')
     A                                      JREF(2)
     A                                      RENAME(FTBRCA)
     A            ININFG                    TEXT('Incoming Flag')
     A                                      JREF(2)
     A                                      RENAME(FTINFG)
     A            INIPRF                    TEXT('Incoming Payment Ref.')
     A                                      JREF(1)
     A                                      RENAME(FTIPRF)
     A            INOTFG                    TEXT('Outgoing Flag')
     A                                      JREF(2)
     A                                      RENAME(FTOTFG)
     A            INOPRF                    TEXT('Outgoing Payment Ref.')
     A                                      JREF(2)
     A                                      RENAME(FTOPRF)
     A            INFRGL                    TEXT('Funds From')
     A                                      JREF(2)
     A                                      RENAME(FTFRGL)
     A            INTOGL                    TEXT('Funds To')
     A                                      JREF(2)
     A                                      RENAME(FTTOGL)
     A            INAJOB                    TEXT('Job name')
     A                                      JREF(2)
     A                                      RENAME(FTAJOB)
     A            INAUSR                    TEXT('User name')
     A                                      JREF(2)
     A                                      RENAME(FTAUSR)
     A            INATIM                    TEXT('Action Time')
     A                                      JREF(2)
     A                                      RENAME(FTATIM)
     A            INARDT                    TEXT('Action Date')
     A                                      JREF(2)
     A                                      RENAME(FTARDT)
     A            INAACT                    TEXT('Action Type')
     A                                      JREF(2)
     A                                      RENAME(FTAACT)
     A            INRDNB                    TEXT('Run day number')
     A                                      JREF(2)
     A                                      RENAME(FTRDNB)
     A            SQSTPQ                    TEXT('STP Queue Name')
     A                                      JREF(1)
      *>> Start EEE065
     A            SQSTAT                    TEXT('STP Queue Status')
     A                                      JREF(1)
      *>> End EEE065
     A*.....................................................................
     A* Key fields
     A          K SQSTPQ
     A          K INIPRF
     A*.....................................................................
     A* SELECT: Incomings Only
     A* Field    : Incoming Payment Ref.
     A* Condition: Not Blank
     A          S INIPRF                    COMP(NE ' ')
     A*=====================================================================
