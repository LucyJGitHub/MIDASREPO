      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas IMM FT Outgoing Transactions')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Message Management Module
     A*                                                               *
     A*  LF/MEINFTJ3 - Incoming Message FT Data  Outgoing Transactions*
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 124673             Date 08Jul98               *
      *                 S01435             Date 29Jul93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
     A*  124673 - Add Century fields for Year 2000.                   *
     A*  S01435 - Incoming Message Management                         *
     A*                                                               *
     A*****************************************************************
     A                                      JDFTVAL
     A                                      DYNSLT
     A*=====================================================================
     A          R @OPFTJ3
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      JFILE(MEINFTPD +
     A                                            MEINCRPD +
     A                                            MEINMPPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* JoOP from OPcomOPg Message FT Data to OPcoming Message Control
     A          J                           JOIN(1 2)
     A                                      JFLD(FTMSGR CRMSGR)
     A*---------------------------------------------------------------------
     A* JoOP from OPcomOPg Message FT Data to OPcoming Message Part Dtl
     A          J                           JOIN(1 3)
     A                                      JFLD(FTMSGR MPMSGR)
     A                                      JFLD(FTKPRT MPKPRT)
     A*---------------------------------------------------------------------
     A            OPMSGR                    TEXT('Msg Unique Reference')
     A                                      JREF(1)
     A                                      RENAME(FTMSGR)
     A            OPSYTM                    TEXT('System')
     A                                      JREF(2)
     A                                      RENAME(CRSYTM)
     A            OPNWRK                    TEXT('Network')
     A                                      JREF(2)
     A                                      RENAME(CRNWRK)
     A            OPSNDR                    TEXT('Sender')
     A                                      JREF(2)
     A                                      RENAME(CRSNDR)
     A            OPMIR                     TEXT('Message Input reference')
     A                                      JREF(2)
     A                                      RENAME(CRMIR)
     A            OPMOR                     TEXT('Message Output reference')
     A                                      JREF(2)
     A                                      RENAME(CRMOR)
     A            OPMTPY                    TEXT('Message type')
     A                                      JREF(2)
     A                                      RENAME(CRMTPY)
     A            OPMPRY                    TEXT('Message Priority')
     A                                      JREF(2)
     A                                      RENAME(CRMPRY)
      *                                              Begin /124673/
     A            OPMDEC                    TEXT('Message Received Century')
     A                                      JREF(2)
     A                                      RENAME(CRMDEC)
      *                                              End   /124673/
     A            OPMODE                    TEXT('Message Received Date')
     A                                      JREF(2)
     A                                      RENAME(CRMODE)
     A            OPMOTM                    TEXT('Message Received Time')
     A                                      JREF(2)
     A                                      RENAME(CRMOTM)
      *                                              Begin /124673/
     A            OPSTDC                    TEXT('Message Sent Century')
     A                                      JREF(2)
     A                                      RENAME(CRSTDC)
      *                                              End   /124673/
     A            OPSNTD                    TEXT('Message Sent Date')
     A                                      JREF(2)
     A                                      RENAME(CRSNTD)
     A            OPSNTT                    TEXT('Message Sent Time')
     A                                      JREF(2)
     A                                      RENAME(CRSNTT)
     A            OPCRCY                    TEXT('Currency Code       (CR)')
     A                                      JREF(2)
     A                                      RENAME(CRCYCD)
      *                                              Begin /124673/
     A            OPSVDC                    TEXT('Century of Value date')
     A                                      JREF(2)
     A                                      RENAME(CRSVDC)
      *                                              End   /124673/
     A            OPCRDT                    TEXT('Value date  YYMMDD  (CR)')
     A                                      JREF(2)
     A                                      RENAME(CRSVDT)
     A            OPCRPA                    TEXT('Payment Amount      (CR)')
     A                                      JREF(2)
     A                                      RENAME(CRAMTS)
     A            OPBRCH                    TEXT('Branch Code')
     A                                      JREF(2)
     A                                      RENAME(CRBRCA)
     A            OPDUPF                    TEXT('Duplication Flag')
     A                                      JREF(2)
     A                                      RENAME(CRDUPF)
     A            OPNPRT                    TEXT('Number of Parts')
     A                                      JREF(2)
     A                                      RENAME(CRNPRT)
     A            OPKPRT                    TEXT('Part Number (Key)')
     A                                      JREF(1)
     A                                      RENAME(FTKPRT)
     A            OPTRNO                    TEXT('Transaction reference no.')
     A                                      JREF(3)
     A                                      RENAME(MPTRNO)
     A            OPCCYP                    TEXT('Currency Code      (Part)')
     A                                      JREF(3)
     A                                      RENAME(MPCYCD)
      *                                              Begin /124673/
     A            OPVDTC                    TEXT('Century of Value date')
     A                                      JREF(3)
     A                                      RENAME(MPSVDC)
      *                                              End   /124673/
     A            OPVDTP                    TEXT('Value date  YYMMDD (Part)')
     A                                      JREF(3)
     A                                      RENAME(MPSVDT)
     A            OPAMTP                    TEXT('Payment Amount     (Part)')
     A                                      JREF(3)
     A                                      RENAME(MPAMTS)
     A            OPINBK                    TEXT('Intermediary')
     A                                      JREF(3)
     A                                      RENAME(MPINBK)
     A            OPACIN                    TEXT('A/c with Institution')
     A                                      JREF(3)
     A                                      RENAME(MPACIN)
     A            OPBENF                    TEXT('Beneficiary')
     A                                      JREF(3)
     A                                      RENAME(MPBENF)
     A            OPPMFG                    TEXT('Payment Message Flag')
     A                                      JREF(3)
     A                                      RENAME(MPPMFG)
     A            OPPYAM                    TEXT('Payment Amount')
     A                                      JREF(3)
     A                                      RENAME(MPPYAM)
     A            OPBRCA                    TEXT('Branch Code        (Part)')
     A                                      JREF(1)
     A                                      RENAME(FTBRCA)
     A            OPINFG                    TEXT('Incoming Flag')
     A                                      JREF(1)
     A                                      RENAME(FTINFG)
     A            OPIPRF                    TEXT('Incoming Payment Ref.')
     A                                      JREF(1)
     A                                      RENAME(FTIPRF)
     A            OPOTFG                    TEXT('Outgoing Flag')
     A                                      JREF(1)
     A                                      RENAME(FTOTFG)
     A            OPOPRF                    TEXT('Outgoing Payment Ref.')
     A                                      JREF(1)
     A                                      RENAME(FTOPRF)
     A            OPFRGL                    TEXT('Funds From')
     A                                      JREF(1)
     A                                      RENAME(FTFRGL)
     A            OPTOGL                    TEXT('Funds To')
     A                                      JREF(1)
     A                                      RENAME(FTTOGL)
     A            OPAJOB                    TEXT('Job name')
     A                                      JREF(1)
     A                                      RENAME(FTAJOB)
     A            OPAUSR                    TEXT('User name')
     A                                      JREF(1)
     A                                      RENAME(FTAUSR)
     A            OPATIM                    TEXT('Action Time')
     A                                      JREF(1)
     A                                      RENAME(FTATIM)
     A            OPARDT                    TEXT('Action Date')
     A                                      JREF(1)
     A                                      RENAME(FTARDT)
     A            OPAACT                    TEXT('Action Type')
     A                                      JREF(1)
     A                                      RENAME(FTAACT)
     A            OPRDNB                    TEXT('Run day number')
     A                                      JREF(1)
     A                                      RENAME(FTRDNB)
     A*.....................................................................
     A* Key fields
     A          K OPOPRF
     A*.....................................................................
     A* SELECT: Outgoings Only
     A* Field    : Outgoing Payment Ref.
     A* Condition: Not Blank
     A          S OPOPRF                    COMP(NE ' ')
     A*=====================================================================
