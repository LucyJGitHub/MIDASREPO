      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas IMM FT Outgoing Transactions')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module
      *                                                               *
      *  LF/MEINFTJ3 - Incoming Message FT Data  Outgoing Transactions*
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 15Jul2011             *
      *                 EEE065             Date 07Sep2004             *
      *                 ESL038             Date 01Oct2004             *
      *                 EEE058             Date 14Feb2003             *
      * Midas Release 4 --------------- Base -------------------------*
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level.     *
      *  EEE065 - FT Payment Routing                                  *
      *  ESL038 - ING STP Development                                 *
      *  EEE058 - Incoming STP                                        *
      *                                                               *
      *****************************************************************
     A                                      JDFTVAL
     A                                      DYNSLT
     A*=====================================================================
     A          R @OPFTJ3
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      JFILE(FTMESQP0 +
     A                                            MEINFTPD +
     A                                            MEINCRPD +
     A                                            MEINMPPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from STP Queues to FT data
     A          J                           JOIN(1 2)
     A                                      JFLD(SQMSGR FTMSGR)
     A*---------------------------------------------------------------------
     A* JoOP from OPcomOPg Message FT Data to OPcoming Message Control
     A          J                           JOIN(1 3)
     A                                      JFLD(SQMSGR CRMSGR)
     A*---------------------------------------------------------------------
     A* JoOP from OPcomOPg Message FT Data to OPcoming Message Part Dtl
     A          J                           JOIN(1 4)
     A                                      JFLD(SQMSGR MPMSGR)
     A                                      JFLD(SQKPRT MPKPRT)
     A*---------------------------------------------------------------------
     A            OPMSGR                    TEXT('Msg Unique Reference')
     A                                      JREF(1)
     A                                      RENAME(SQMSGR)
     A            OPSYTM                    TEXT('System')
     A                                      JREF(3)
     A                                      RENAME(CRSYTM)
     A            OPNWRK                    TEXT('Network')
     A                                      JREF(3)
     A                                      RENAME(CRNWRK)
     A            OPSNDR                    TEXT('Sender')
     A                                      JREF(3)
     A                                      RENAME(CRSNDR)
     A            OPMIR                     TEXT('Message Input reference')
     A                                      JREF(3)
     A                                      RENAME(CRMIR)
     A            OPMOR                     TEXT('Message Output reference')
     A                                      JREF(3)
     A                                      RENAME(CRMOR)
     A            OPMTPY                    TEXT('Message type')
     A                                      JREF(3)
     A                                      RENAME(CRMTPY)
     A            OPMPRY                    TEXT('Message Priority')
     A                                      JREF(3)
     A                                      RENAME(CRMPRY)
      *                                              Begin /124673/
     A            OPMDEC                    TEXT('Message Received Century')
     A                                      JREF(3)
     A                                      RENAME(CRMDEC)
      *                                              End   /124673/
     A            OPMODE                    TEXT('Message Received Date')
     A                                      JREF(3)
     A                                      RENAME(CRMODE)
     A            OPMOTM                    TEXT('Message Received Time')
     A                                      JREF(3)
     A                                      RENAME(CRMOTM)
      *                                              Begin /124673/
     A            OPSTDC                    TEXT('Message Sent Century')
     A                                      JREF(3)
     A                                      RENAME(CRSTDC)
      *                                              End   /124673/
     A            OPSNTD                    TEXT('Message Sent Date')
     A                                      JREF(3)
     A                                      RENAME(CRSNTD)
     A            OPSNTT                    TEXT('Message Sent Time')
     A                                      JREF(3)
     A                                      RENAME(CRSNTT)
     A            OPCRCY                    TEXT('Currency Code       (CR)')
     A                                      JREF(3)
     A                                      RENAME(CRCYCD)
      *                                              Begin /124673/
     A            OPSVDC                    TEXT('Century of Value date')
     A                                      JREF(3)
     A                                      RENAME(CRSVDC)
      *                                              End   /124673/
     A            OPCRDT                    TEXT('Value date  YYMMDD  (CR)')
     A                                      JREF(3)
     A                                      RENAME(CRSVDT)
     A            OPCRPA                    TEXT('Payment Amount      (CR)')
     A                                      JREF(3)
     A                                      RENAME(CRAMTS)
     A            OPBRCH                    TEXT('Branch Code')
     A                                      JREF(3)
     A                                      RENAME(CRBRCA)
     A            OPDUPF                    TEXT('Duplication Flag')
     A                                      JREF(3)
     A                                      RENAME(CRDUPF)
     A            OPNPRT                    TEXT('Number of Parts')
     A                                      JREF(3)
     A                                      RENAME(CRNPRT)
     A            OPKPRT                    TEXT('Part Number (Key)')
     A                                      JREF(1)
     A                                      RENAME(SQKPRT)
     A            OPTRNO                    TEXT('Transaction reference no.')
     A                                      JREF(4)
     A                                      RENAME(MPTRNO)
     A            OPCCYP                    TEXT('Currency Code      (Part)')
     A                                      JREF(4)
     A                                      RENAME(MPCYCD)
      *                                              Begin /124673/
     A            OPVDTC                    TEXT('Century of Value date')
     A                                      JREF(4)
     A                                      RENAME(MPSVDC)
      *                                              End   /124673/
     A            OPVDTP                    TEXT('Value date  YYMMDD (Part)')
     A                                      JREF(4)
     A                                      RENAME(MPSVDT)
     A            OPAMTP                    TEXT('Payment Amount     (Part)')
     A                                      JREF(4)
     A                                      RENAME(MPAMTS)
     A            OPINBK                    TEXT('Intermediary')
     A                                      JREF(4)
     A                                      RENAME(MPINBK)
     A            OPACIN                    TEXT('A/c with Institution')
     A                                      JREF(4)
     A                                      RENAME(MPACIN)
     A            OPBENF                    TEXT('Beneficiary')
     A                                      JREF(4)
     A                                      RENAME(MPBENF)
     A            OPPMFG                    TEXT('Payment Message Flag')
     A                                      JREF(4)
     A                                      RENAME(MPPMFG)
     A            OPPYAM                    TEXT('Payment Amount')
     A                                      JREF(4)
     A                                      RENAME(MPPYAM)
     A            OPBRCA                    TEXT('Branch Code        (Part)')
     A                                      JREF(2)
     A                                      RENAME(FTBRCA)
     A            OPINFG                    TEXT('Incoming Flag')
     A                                      JREF(2)
     A                                      RENAME(FTINFG)
     A            OPIPRF                    TEXT('Incoming Payment Ref.')
     A                                      JREF(2)
     A                                      RENAME(FTIPRF)
     A            OPOTFG                    TEXT('Outgoing Flag')
     A                                      JREF(2)
     A                                      RENAME(FTOTFG)
     A            OPOPRF                    TEXT('Outgoing Payment Ref.')
     A                                      JREF(1)
     A                                      RENAME(FTOPRF)
     A            OPFRGL                    TEXT('Funds From')
     A                                      JREF(2)
     A                                      RENAME(FTFRGL)
     A            OPTOGL                    TEXT('Funds To')
     A                                      JREF(2)
     A                                      RENAME(FTTOGL)
     A            OPAJOB                    TEXT('Job name')
     A                                      JREF(2)
     A                                      RENAME(FTAJOB)
     A            OPAUSR                    TEXT('User name')
     A                                      JREF(2)
     A                                      RENAME(FTAUSR)
     A            OPATIM                    TEXT('Action Time')
     A                                      JREF(2)
     A                                      RENAME(FTATIM)
     A            OPARDT                    TEXT('Action Date')
     A                                      JREF(2)
     A                                      RENAME(FTARDT)
     A            OPAACT                    TEXT('Action Type')
     A                                      JREF(2)
     A                                      RENAME(FTAACT)
     A            OPRDNB                    TEXT('Run day number')
     A                                      JREF(2)
     A                                      RENAME(FTRDNB)
     A            SQSTPQ                    TEXT('STP Queue Name')
     A                                      JREF(1)
      *>> Start EEE065
     A            SQSTAT                    TEXT('STP Queue Status')
     A                                      JREF(1)
      *>> End EEE065
     A*.....................................................................
     A* Key fields
     A          K SQSTPQ
     A          K OPOPRF
     A*.....................................................................
     A* SELECT: Outgoings Only
     A* Field    : Outgoing Payment Ref.
     A* Condition: Not Blank
     A          S OPOPRF                    COMP(NE ' ')
     A*=====================================================================
