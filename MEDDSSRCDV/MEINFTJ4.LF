      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas ME Incoming message FT data, all MT103')         *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  LF/MEINFTJ4 - Incoming Message FT Data, all MT103            *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. CSW220 *CREATE     Date 09Mar20               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSW220 - SWIFT Changes 2020                                  *
      *                                                               *
      *****************************************************************
     A          R @INFTJ4                   JFILE(MEINFTPD MEINCRPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2020')
      *
     A          J                           JOIN(1 2)
     A                                      JFLD(FTMSGR CRMSGR)

     A            FTMSGR                    TEXT('Msg Unique Reference')
     A                                      JREF(1)
     A            FTKPRT                    TEXT('Part Number Key')
     A                                      JREF(1)
     A            FTIPRF                    TEXT('Incoming Payment Ref.')
     A                                      JREF(1)
     A            FTOPRF                    TEXT('Outgoing Payment Ref.')
     A                                      JREF(1)
     A            FTM199                    TEXT('MT199')
     A                                      JREF(1)
     A            CRMODE                    TEXT('Msg Received Date')
     A                                      JREF(2)
     A            CRMTPY                    TEXT('Msg Type')
     A                                      JREF(2)

      * Key fields
     A          K FTMSGR
      *
     A          S CRMTPY                    COMP(EQ '103')
