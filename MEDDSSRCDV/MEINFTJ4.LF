/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas ME Incoming message FT data, all MT103')         *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  LF/MEINFTJ4 - Incoming Message FT Data, all MT103            *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. CSW220 *CREATE     Date 09Mar20               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSW220 - SWIFT Changes 2020                                  *
      *                                                               *
      *****************************************************************
     A          R @INFTJ4                   JFILE(MEINFTPD +
     A                                            MEINCRPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2020')
      *
      ** Join from Incoming Message Control to Incoming Message Part Dtl
     A          J                           JOIN(1 2)
     A                                      JFLD(FTMSGR CRMSGR)

     A            FTMSGR                    TEXT('Msg Unique Reference')
     A                                      JREF(1)
     A            FTINFG                    TEXT('Incoming Flag')
     A                                      JREF(1)
     A            FTIPRF                    TEXT('Incoming Payment Ref.')
     A                                      JREF(1)
     A            FTOPRF                    TEXT('Outgoing Payment Ref.')
     A                                      JREF(1)
     A            FTM199                    TEXT('MT199')
     A                                      JREF(1)
     A            CRSVDT                    TEXT('Value Date YYMMDD')
     A                                      JREF(2)
     A            CRMODE                    TEXT('Msg Received Date')
     A                                      JREF(2)
     A            CRMTPY                    TEXT('Msg Type')
     A                                      JREF(2)

      * Key fields
     A          K FTMSGR
      *
     A          S CRMTPY                    COMP(EQ '103')
     A            FTM199                    COMP(EQ ' ')
