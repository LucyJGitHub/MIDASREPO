      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas Incoming Message FT Data')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Message Management Module
     A*                                                               *
     A*  LF/MEINFTL3 - Incoming Message FT Data  (To be processed)    *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 15Jul2011             *
      *                 247197             Date 21Aug07               *
      *                 CSW220             Date 09Mar20               *
      *                 MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01435             Date 20Jan94               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level.     *
      *  247197 - Add field FTSTPF (STPed Flag). Applied fix 238276.  *
      *  CSW220 - SWIFT Changes 2020 (Recompile)                      *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
     A*  S01435 - Incoming Message Management                         *
     A*                                                               *
     A*****************************************************************
     A          R @INFTL3
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      PFILE(MEINFTPD)
     A*---------------------------------------------------------------------
     A            FTMSGR                    TEXT('Msg Unique Reference')
     A            FTKPRT                    TEXT('Part Number (Key)')
     A            FTBRCA                    TEXT('Branch Code')
     A            FTINFG                    TEXT('Incoming Flag')
     A            FTIPRF                    TEXT('Incoming Payment Ref.')
     A            FTOTFG                    TEXT('Outgoing Flag')
     A            FTOPRF                    TEXT('Outgoing Payment Ref.')
     A            FTFRGL                    TEXT('Funds From')
     A            FTTOGL                    TEXT('Funds To')
     A            FTAJOB                    TEXT('Job name')
     A            FTAUSR                    TEXT('User name')
     A            FTATIM                    TEXT('Action Time')
     A            FTARDT                    TEXT('Action Date')
     A            FTAACT                    TEXT('Action Type')
     A            FTRDNB                    TEXT('Run day number')
      * Start of Change 247197
     A            FTSTPF                    TEXT('STPed Flag')
      * End of Change 247197
     A*.....................................................................
     A* Key fields
     A          K FTMSGR
     A          K FTKPRT
     A*.....................................................................
     A*   OMIT: Omit all Incoming Transactions already in FT
     A* Field    : Incoming Payment Ref.
     A* Condition: Not Blank
     A          O FTIPRF                    COMP(NE ' ')
     A*.....................................................................
     A*   OMIT: Omit all Outgoing Transactions already in FT
     A* Field    : Outgoing Payment Ref.
     A* Condition: Not Blank
     A          O FTOPRF                    COMP(NE ' ')
     A*.....................................................................
     A*   OMIT: Omit if ignored
     A* Field    : Incoming Flag
     A* Condition: Not Blank
     A          O FTINFG                    COMP(NE ' ')
     A*=====================================================================
