      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Incoming Msg FT Data, MT199 message type')             *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  LF/MEINFTL6 - Incoming Message FT Data  (For MT199)          *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. CSW220  *CREATE    Date 09Mar20               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSW220 - SWIFT Changes 2020                                  *
      *                                                               *
      *****************************************************************
     A          R @INFTL6
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2020')
     A                                      PFILE(MEINFTPD)
     A*---------------------------------------------------------------------
     A            FTMSGR                    TEXT('Msg Unique Reference')
     A            FTKPRT                    TEXT('Part Number (Key)')
     A            FTBRCA                    TEXT('Branch Code')
     A            FTINFG                    TEXT('Incoming Flag')
     A            FTIPRF                    TEXT('Incoming Payment Ref.')
     A            FTOTFG                    TEXT('Outgoing Flag')
     A            FTOPRF                    TEXT('Outgoing Payment Ref.')
     A            FTFRGL                    TEXT('Funds From')
     A            FTTOGL                    TEXT('Funds To')
     A            FTAJOB                    TEXT('Job name')
     A            FTAUSR                    TEXT('User name')
     A            FTATIM                    TEXT('Action Time')
     A            FTARDT                    TEXT('Action Date')
     A            FTAACT                    TEXT('Action Type')
     A            FTRDNB                    TEXT('Run day number')
     A            FTM199                    TEXT('MT199 Flag')
     A*.....................................................................
     A* Key fields
     A          K FTMSGR
     A          K FTKPRT
     A*.....................................................................
     A*   SELECT: Select Incoming Transactions flagged 'A' 'R' 'T' or 'H' for MT199 generation
     A* Field    : MT199 Flag
     A* Condition: FTM199 IN('A','R','T','H')
     A          S FTM199                    VALUES('A' 'R' 'T' 'H')
     A*=====================================================================
