      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas Incoming Message FT Data')                       *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  LF/MEINFTV2 - Incoming Message FT Data - Cash Management     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. ME056975           Date 09Oct20               *
      *  Prev Amend No. UUC104   *Create   Date 12Oct20               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD056975-SWIFT Changes 2020 AND UUC104CC1 (Recompile)        *
      *  UUC104 - MPM Integration (Upgrade of 1.4.11.06 CAR UUC069)   *
      *                                                               *
      *****************************************************************
     A          R @INFTV2
     A                                      PFILE(MEINFTPD)
      *---------------------------------------------------------------------
     A            FTMSGR                    TEXT('Msg Unique Reference')
     A            FTKPRT                    TEXT('Part Number (Key)')
     A            FTBRCA                    TEXT('Branch Code')
     A            FTINFG                    TEXT('Incoming Flag')
     A            FTIPRF                    TEXT('Incoming Payment Ref.')
     A            FTOTFG                    TEXT('Outgoing Flag')
     A            FTOPRF                    TEXT('Outgoing Payment Ref.')
     A            FTFRGL                    TEXT('Funds From')
     A            FTTOGL                    TEXT('Funds To')
     A            FTAJOB                    TEXT('Job name')
     A            FTAUSR                    TEXT('User name')
     A            FTATIM                    TEXT('Action Time')
     A            FTARDT                    TEXT('Action Date')
     A            FTAACT                    TEXT('Action Type')
     A            FTRDNB                    TEXT('Run day number')
      *.....................................................................
      * Key fields
     A          K FTMSGR
     A          K FTKPRT
     A*.....................................................................
      *   Select Cash Management transactions
      * Condition: Cash Management processed
     A          S FTINFG                    VALUES('MIN0508' 'MIN0509' +
     A                                             'MIN0510')
      * Condition: Not processed
     A            FTAJOB                    COMP(NE 'SWEEP')
     A*=====================================================================
