      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas ME Incoming Message FT Data  (To be pro)')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module
      *                                                               *
      *  LF/MEINFTV3 - Incoming Message FT Data  (To be processed)    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 15Jul2011             *
      *                 MMFIX              Date 08Oct2008             *
      *                 ESL038             Date 01Oct2004             *
      *                 EEE058   *CREATE   Date 23Apr2003             *
      * Midas Release 4 --------------- Base -------------------------*
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level.     *
      *  MMFIX  - Fix 247197 not completed. FTSTPF flag missing.      *
      *  ESL038 - ING STP Development                                 *
      *  EEE058 - Incoming STP                                        *
      *                                                               *
      *****************************************************************
     A          R @INFTV3
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      PFILE(MEINFTPD)
     A*---------------------------------------------------------------------
     A            FTMSGR                    TEXT('Msg Unique Reference')
     A            FTKPRT                    TEXT('Part Number (Key)')
     A            FTBRCA                    TEXT('Branch Code')
     A            FTINFG                    TEXT('Incoming Flag')
     A            FTIPRF                    TEXT('Incoming Payment Ref.')
     A            FTOTFG                    TEXT('Outgoing Flag')
     A            FTOPRF                    TEXT('Outgoing Payment Ref.')
     A            FTFRGL                    TEXT('Funds From')
     A            FTTOGL                    TEXT('Funds To')
     A            FTAJOB                    TEXT('Job name')
     A            FTAUSR                    TEXT('User name')
     A            FTATIM                    TEXT('Action Time')
     A            FTARDT                    TEXT('Action Date')
     A            FTAACT                    TEXT('Action Type')
     A            FTRDNB                    TEXT('Run day number')
     A* Start of MMFIX
     A            FTSTPF                    TEXT('STPed Flag')
     A* Endrt of MMFIX
     A*.....................................................................
     A* Key fields
     A          K FTMSGR
     A          K FTKPRT
     A*.....................................................................
     A*   OMIT: Omit all Incoming Transactions already in FT
     A* Field    : Incoming Payment Ref.
     A* Condition: Not Blank
     A          O FTIPRF                    COMP(NE ' ')
     A*.....................................................................
     A*   OMIT: Omit all Outgoing Transactions already in FT
     A* Field    : Outgoing Payment Ref.
     A* Condition: Not Blank
     A          O FTOPRF                    COMP(NE ' ')
     A*.....................................................................
     A*   OMIT: Omit if ignored
     A* Field    : Incoming Flag
     A* Condition: Not Blank
     A          O FTINFG                    COMP(NE ' ')
     A* Field    : Incoming Flag
     A* Condition: Failed Auto Gen
     A            FTINFG                    COMP(NE 'MIN0083')
     A*=====================================================================
