      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module
      *                                                               *
      *  LF/MEINLFV3 - Incoming Message Log File                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      *  Last Amend No. MD31937            Date 13Jan15               *
      *  Last Amend No. AR856737           Date 15Jul2011             *
      *  Last Amend No. ESL038             Date 01Oct2004             *
      * Midas Release 4 --------------- Base -------------------------*
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD31937  - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level.     *
      *  ESL038 - ING STP Development                                 *
      *                                                               *
      *****************************************************************
     A                                      JDFTVAL
     A*=====================================================================
     A          R @INLFV3
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      JFILE(MEINLFPD +
     A                                            MEINCRPD +
     A                                            MEINMPPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from I.M.M. Log File to Incoming Message Control
     A          J                           JOIN(1 2)
     A                                      JFLD(LFMSGR CRMSGR)
     A*---------------------------------------------------------------------
     A* Join from I.M.M. Log File to Incoming Message Part Dtl
     A          J                           JOIN(1 3)
     A                                      JFLD(LFMSGR MPMSGR)
     A                                      JFLD(LFKPRT MPKPRT)
     A*---------------------------------------------------------------------
     A            LFMSGR                    TEXT('Msg Unique Reference')
     A                                      JREF(1)
     A            LFSYTM                    TEXT('System')
     A                                      JREF(2)
     A                                      RENAME(CRSYTM)
     A            LFNWRK                    TEXT('Network')
     A                                      JREF(2)
     A                                      RENAME(CRNWRK)
     A            LFSNDR                    TEXT('Sender')
     A                                      JREF(2)
     A                                      RENAME(CRSNDR)
     A            LFMIR                     TEXT('Message Input reference')
     A                                      JREF(2)
     A                                      RENAME(CRMIR)
     A            LFMOR                     TEXT('Message Output reference')
     A                                      JREF(2)
     A                                      RENAME(CRMOR)
     A            LFMTPY                    TEXT('Message type')
     A                                      JREF(2)
     A                                      RENAME(CRMTPY)
     A            LFMPRY                    TEXT('Message Priority')
     A                                      JREF(2)
     A                                      RENAME(CRMPRY)
      *                                              Begin /124673/
     A            LFMDEC                    TEXT('Message Received Century')
     A                                      JREF(2)
     A                                      RENAME(CRMDEC)
      *                                              End   /124673/
     A            LFMODE                    TEXT('Message Received Date')
     A                                      JREF(2)
     A                                      RENAME(CRMODE)
     A            LFMOTM                    TEXT('Message Received Time')
     A                                      JREF(2)
     A                                      RENAME(CRMOTM)
      *                                              Begin /124673/
     A            LFSTDC                    TEXT('Message Sent Century')
     A                                      JREF(2)
     A                                      RENAME(CRSTDC)
      *                                              End   /124673/
     A            LFSNTD                    TEXT('Message Sent Date')
     A                                      JREF(2)
     A                                      RENAME(CRSNTD)
     A            LFSNTT                    TEXT('Message Sent Time')
     A                                      JREF(2)
     A                                      RENAME(CRSNTT)
     A            LFCYCD                    TEXT('Currency Code       (key)')
     A                                      JREF(2)
     A                                      RENAME(CRCYCD)
      *                                              Begin /124673/
     A            LFSVDC                    TEXT('Century of Value date')
     A                                      JREF(2)
     A                                      RENAME(CRSVDC)
      *                                              End   /124673/
     A            LFSVDT                    TEXT('Value date  YYMMDD format')
     A                                      JREF(2)
     A                                      RENAME(CRSVDT)
     A            LFAMTS                    TEXT('Payment Amount')
     A                                      JREF(2)
     A                                      RENAME(CRAMTS)
     A            LFBRCA                    TEXT('Branch Code')
     A                                      JREF(2)
     A                                      RENAME(CRBRCA)
     A            LFDUPF                    TEXT('Duplication Flag')
     A                                      JREF(2)
     A                                      RENAME(CRDUPF)
     A            LFNPRT                    TEXT('Number of Parts')
     A                                      JREF(2)
     A                                      RENAME(CRNPRT)
     A            LFCJOB                    TEXT('Job name')
     A                                      JREF(2)
     A                                      RENAME(CRAJOB)
     A            LFCUSR                    TEXT('User name')
     A                                      JREF(2)
     A                                      RENAME(CRAUSR)
     A            LFCTIM                    TEXT('Action Time')
     A                                      JREF(2)
     A                                      RENAME(CRATIM)
     A            LFCRDT                    TEXT('Action Date')
     A                                      JREF(2)
     A                                      RENAME(CRARDT)
     A            LFCACT                    TEXT('Action Type')
     A                                      JREF(2)
     A                                      RENAME(CRAACT)
     A            LFCRND                    TEXT('Run day number')
     A                                      JREF(2)
     A                                      RENAME(CRRDNB)
     A            LFKPRT                    TEXT('Part Number (Key)')
     A                                      JREF(1)
     A            LFTRNO                    TEXT('Transaction reference no.')
     A                                      JREF(3)
     A                                      RENAME(MPTRNO)
     A            LFPCCY                    TEXT('Currency Code       (key)')
     A                                      JREF(3)
     A                                      RENAME(MPCYCD)
      *                                              Begin /124673/
     A            LFVDTC                    TEXT('Century of Value date')
     A                                      JREF(3)
     A                                      RENAME(MPSVDC)
      *                                              End   /124673/
     A            LFPVDT                    TEXT('Value date  YYMMDD format')
     A                                      JREF(3)
     A                                      RENAME(MPSVDT)
     A            LFPAMT                    TEXT('Payment Amount')
     A                                      JREF(3)
     A                                      RENAME(MPAMTS)
     A            LFINBK                    TEXT('Intermediary')
     A                                      JREF(3)
     A                                      RENAME(MPINBK)
     A            LFACIN                    TEXT('A/c with Institution')
     A                                      JREF(3)
     A                                      RENAME(MPACIN)
     A            LFBENF                    TEXT('Beneficiary')
     A                                      JREF(3)
     A                                      RENAME(MPBENF)
     A            LFPMFG                    TEXT('Payment Message Flag')
     A                                      JREF(3)
     A                                      RENAME(MPPMFG)
     A            LFPYAM                    TEXT('Payment Amount')
     A                                      JREF(3)
     A                                      RENAME(MPPYAM)
     A            LFMSID                    TEXT('Action (Msg id.)')
     A                                      JREF(1)
     A            LFMSGF                    TEXT('Action Msg File')
     A                                      JREF(1)
     A            LFMSDT                    TEXT('Action data')
     A                                      JREF(1)
     A            LFARSM                    TEXT('Action Result')
     A                                      JREF(1)
     A            LFARSD                    TEXT('Action Result Data')
     A                                      JREF(1)
     A            LFCPGM                    TEXT('Calling Process')
     A                                      JREF(1)
     A            LFRPGM                    TEXT('Called Process')
     A                                      JREF(1)
     A            LFAJOB                    TEXT('Job name')
     A                                      JREF(1)
     A            LFAUSR                    TEXT('User name')
     A                                      JREF(1)
     A            LFATIM                    TEXT('Action Time')
     A                                      JREF(1)
     A            LFARDT                    TEXT('Action Date')
     A                                      JREF(1)
     A            LFAACT                    TEXT('Action Type')
     A                                      JREF(1)
     A            LFRDNB                    TEXT('Run day number')
     A                                      JREF(1)
     A*.....................................................................
     A* Key fields
     A          K LFMSGR
     A          K LFKPRT
     A          K LFRDNB
     A          K LFATIM
     A*=====================================================================
