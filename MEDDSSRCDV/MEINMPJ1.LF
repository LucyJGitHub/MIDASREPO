      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas Part Dtl Join Parts to FT Parts')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Message Management Module
     A*                                                               *
     A*  LF/MEINMPJ1 - Incoming Message Part Dtl Join Parts to FT Part*
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. CSW220             Date 09Mar20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 124673             Date 08Jun98               *
      *                 S01435             Date 29Jul93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CSW220 - SWIFT Changes 2020 (Recompile)                      *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
     A*  124673 - Add Century fields for Year 2000.                   *
     A*  S01435 - Incoming Message Management                         *
     A*                                                               *
     A*****************************************************************
     A                                      JDFTVAL
     A                                      DYNSLT
     A*=====================================================================
     A          R @INMPJ1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      JFILE(MEINMPPD +
     A                                            MEINCRPD +
     A                                            MEINMRPD +
     A                                            MEINFTPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from Incoming Message Part Dtl to Incoming Message Control
     A          J                           JOIN(1 2)
     A                                      JFLD(MPMSGR CRMSGR)
     A*---------------------------------------------------------------------
     A* Join from Incoming Message Part Dtl to Midas Reservation
     A          J                           JOIN(1 3)
     A                                      JFLD(MPMSGR MRMSGR)
     A                                      JFLD(MPKPRT MRKPRT)
     A*---------------------------------------------------------------------
     A* Join from Incoming Message Part Dtl to Incoming Message FT Data
     A          J                           JOIN(1 4)
     A                                      JFLD(MPMSGR FTMSGR)
     A                                      JFLD(MPKPRT FTKPRT)
     A*---------------------------------------------------------------------
     A            MPMSGR                    TEXT('Msg Unique Reference')
     A                                      JREF(1)
     A            MPSYTM                    TEXT('System')
     A                                      JREF(2)
     A                                      RENAME(CRSYTM)
     A            MPNWRK                    TEXT('Network')
     A                                      JREF(2)
     A                                      RENAME(CRNWRK)
     A            MPSNDR                    TEXT('Sender')
     A                                      JREF(2)
     A                                      RENAME(CRSNDR)
     A            MPMIR                     TEXT('Message Input reference')
     A                                      JREF(2)
     A                                      RENAME(CRMIR)
     A            MPMOR                     TEXT('Message Output reference')
     A                                      JREF(2)
     A                                      RENAME(CRMOR)
     A            MPMTPY                    TEXT('Message type')
     A                                      JREF(2)
     A                                      RENAME(CRMTPY)
     A            MPMPRY                    TEXT('Message Priority')
     A                                      JREF(2)
     A                                      RENAME(CRMPRY)
      *                                              Begin /124673/
     A            MPMDEC                    TEXT('Message Received Century')
     A                                      JREF(2)
     A                                      RENAME(CRMDEC)
      *                                              End   /124673/
     A            MPMODE                    TEXT('Message Received Date')
     A                                      JREF(2)
     A                                      RENAME(CRMODE)
     A            MPMOTM                    TEXT('Message Received Time')
     A                                      JREF(2)
     A                                      RENAME(CRMOTM)
      *                                              Begin /124673/
     A            MPSTDC                    TEXT('Message Sent Century')
     A                                      JREF(2)
     A                                      RENAME(CRSTDC)
      *                                              End   /124673/
     A            MPSNTD                    TEXT('Message Sent Date')
     A                                      JREF(2)
     A                                      RENAME(CRSNTD)
     A            MPSNTT                    TEXT('Message Sent Time')
     A                                      JREF(2)
     A                                      RENAME(CRSNTT)
     A            MPCRCY                    TEXT('Currency Code        (CR)')
     A                                      JREF(2)
     A                                      RENAME(CRCYCD)
      *                                              Begin /124673/
     A            MPSVDC                    TEXT('Century of Value date')
     A                                      JREF(2)
     A                                      RENAME(CRSVDC)
      *                                              End   /124673/
     A            MPCRDT                    TEXT('Value date  YYMMDD   (CR)')
     A                                      JREF(2)
     A                                      RENAME(CRSVDT)
     A            MPCRPA                    TEXT('Payment Amount       (CR)')
     A                                      JREF(2)
     A                                      RENAME(CRAMTS)
     A            MPBRCA                    TEXT('Branch Code')
     A                                      JREF(2)
     A                                      RENAME(CRBRCA)
     A            MPDUPF                    TEXT('Duplication Flag')
     A                                      JREF(2)
     A                                      RENAME(CRDUPF)
     A            MPNPRT                    TEXT('Number of Parts')
     A                                      JREF(2)
     A                                      RENAME(CRNPRT)
     A            MPKPRT                    TEXT('Part Number (Key)')
     A                                      JREF(1)
     A            MPJOB                     TEXT('Job name')
     A                                      JREF(3)
     A                                      RENAME(MRJOB)
     A            MPUSR                     TEXT('User name')
     A                                      JREF(3)
     A                                      RENAME(MRUSR)
     A            MPJNO                     TEXT('*Job number')
     A                                      JREF(3)
     A                                      RENAME(MRJNO)
     A            MPINFG                    TEXT('Incoming Flag')
     A                                      JREF(4)
     A                                      RENAME(FTINFG)
     A            MPIPRF                    TEXT('Incoming Payment Ref.')
     A                                      JREF(4)
     A                                      RENAME(FTIPRF)
     A            MPOTFG                    TEXT('Outgoing Flag')
     A                                      JREF(4)
     A                                      RENAME(FTOTFG)
     A            MPOPRF                    TEXT('Outgoing Payment Ref.')
     A                                      JREF(4)
     A                                      RENAME(FTOPRF)
     A            MPFRGL                    TEXT('Funds From')
     A                                      JREF(4)
     A                                      RENAME(FTFRGL)
     A            MPTOGL                    TEXT('Funds To')
     A                                      JREF(4)
     A                                      RENAME(FTTOGL)
     A            MPTRNO                    TEXT('Transaction reference no.')
     A                                      JREF(1)
     A            MPCCYP                    TEXT('Currency Code      (Part)')
     A                                      JREF(1)
     A                                      RENAME(MPCYCD)
      *                                              Begin /124673/
     A            MPVDTC                    TEXT('Century of Value date')
     A                                      JREF(1)
     A                                      RENAME(MPSVDC)
      *                                              End   /124673/
     A            MPVDTP                    TEXT('Value date  YYMMDD (Part)')
     A                                      JREF(1)
     A                                      RENAME(MPSVDT)
     A            MPAMTP                    TEXT('Payment Amount     (Part)')
     A                                      JREF(1)
     A                                      RENAME(MPAMTS)
     A            MPINBK                    TEXT('Intermediary')
     A                                      JREF(1)
     A            MPACIN                    TEXT('A/c with Institution')
     A                                      JREF(1)
     A            MPBENF                    TEXT('Beneficiary')
     A                                      JREF(1)
     A            MPPMFG                    TEXT('Payment Message Flag')
     A                                      JREF(1)
     A            MPPYAM                    TEXT('Payment Amount')
     A                                      JREF(1)
     A*.....................................................................
     A* Key fields
     A          K MPMSGR
     A          K MPKPRT
     A*.....................................................................
     A*   OMIT: Omit all Incoming Transactions already in FT
     A* Field    : Incoming Payment Ref.
     A* Condition: Not Blank
     A          O MPIPRF                    COMP(NE ' ')
     A*.....................................................................
     A*   OMIT: Omit all Outgoing Transactions already in FT
     A* Field    : Outgoing Payment Ref.
     A* Condition: Not Blank
     A          O MPOPRF                    COMP(NE ' ')
     A*.....................................................................
     A*   OMIT: Omit if ignored
     A* Field    : Incoming Flag
     A* Condition: Not Blank
     A          O MPINFG                    COMP(NE ' ')
     A* Field    : Incoming Flag
     A* Condition: Failed Auto Gen
     A            MPINFG                    COMP(NE 'MIN0083')
     A*.....................................................................
     A*   OMIT: Omit if not payment message
     A* Field    : Payment Message Flag
     A* Condition: Payment Message
     A          O MPPMFG                    COMP(NE 'P')
     A*=====================================================================
