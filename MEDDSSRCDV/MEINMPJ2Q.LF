      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module
      *                                                               *
      *  LF/MEINMPJ2 - Incoming Message Part Dtl FT Parts by TRNO     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2003            *
      *                                                               *
      *  Last Amend No. MD31937            Date 13Jan15               *
      *  Last Amend No. AR856737           Date 15Jul2011             *
      *  Last Amend No. EEE065             Date 07Sep2004             *
      *  Prev Amend No. ESL038             Date 01Oct2004             *
      *                 EEE058             Date 14Feb2003             *
      * Midas Release 4 --------------- Base -------------------------*
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD31937  - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level.     *
      *  EEE065 - FT Payment Routing                                  *
      *  ESL038 - ING STP Development                                 *
      *  EEE058 - Incoming STP                                        *
      *                                                               *
      *****************************************************************
     A                                      JDFTVAL
     A*=====================================================================
     A          R @INMPJ2
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2003')
     A                                      JFILE(FTMESQP0 +
     A                                            MEINMPPD +
     A                                            MEINCRPD +
     A                                            MEINFTPD +
     A                                           )
     A*---------------------------------------------------------------------
     A* Join from Queue Definitions to Parts
     A          J                           JOIN(1 2)
     A                                      JFLD(MPMSGR MPMSGR)
     A*---------------------------------------------------------------------
     A* Join from Incoming Message Part Dtl to Incoming Message Control
     A          J                           JOIN(1 3)
     A                                      JFLD(MPMSGR CRMSGR)
     A*---------------------------------------------------------------------
     A* Join from Incoming Message Part Dtl to Incoming Message FT Data
     A          J                           JOIN(1 4)
     A                                      JFLD(MPMSGR FTMSGR)
     A                                      JFLD(MPKPRT FTKPRT)
     A*---------------------------------------------------------------------
     A            MPMSGR                    TEXT('Msg Unique Reference')
     A                                      JREF(1)
     A                                      RENAME(SQMSGR)
     A            MPSYTM                    TEXT('System')
     A                                      JREF(3)
     A                                      RENAME(CRSYTM)
     A            MPNWRK                    TEXT('Network')
     A                                      JREF(3)
     A                                      RENAME(CRNWRK)
     A            MPSNDR                    TEXT('Sender')
     A                                      JREF(3)
     A                                      RENAME(CRSNDR)
     A            MPMIR                     TEXT('Message Input reference')
     A                                      JREF(3)
     A                                      RENAME(CRMIR)
     A            MPMOR                     TEXT('Message Output reference')
     A                                      JREF(3)
     A                                      RENAME(CRMOR)
     A            MPMTPY                    TEXT('Message type')
     A                                      JREF(3)
     A                                      RENAME(CRMTPY)
     A            MPMPRY                    TEXT('Message Priority')
     A                                      JREF(3)
     A                                      RENAME(CRMPRY)
      *                                              Begin /124673/
     A            MPMDEC                    TEXT('Message Received Century')
     A                                      JREF(3)
     A                                      RENAME(CRMDEC)
      *                                              End   /124673/
     A            MPMODE                    TEXT('Message Received Date')
     A                                      JREF(3)
     A                                      RENAME(CRMODE)
     A            MPMOTM                    TEXT('Message Received Time')
     A                                      JREF(3)
     A                                      RENAME(CRMOTM)
      *                                              Begin /124673/
     A            MPSTDC                    TEXT('Message Sent Century')
     A                                      JREF(3)
     A                                      RENAME(CRSTDC)
      *                                              End   /124673/
     A            MPSNTD                    TEXT('Message Sent Date')
     A                                      JREF(3)
     A                                      RENAME(CRSNTD)
     A            MPSNTT                    TEXT('Message Sent Time')
     A                                      JREF(3)
     A                                      RENAME(CRSNTT)
     A            MPCRCY                    TEXT('Currency Code        (CR)')
     A                                      JREF(3)
     A                                      RENAME(CRCYCD)
      *                                              Begin /124673/
     A            MPSVDC                    TEXT('Century of Value date')
     A                                      JREF(3)
     A                                      RENAME(CRSVDC)
      *                                              End   /124673/
     A            MPCRDT                    TEXT('Value date  YYMMDD   (CR)')
     A                                      JREF(3)
     A                                      RENAME(CRSVDT)
     A            MPCRPA                    TEXT('Payment Amount       (CR)')
     A                                      JREF(3)
     A                                      RENAME(CRAMTS)
     A            MPBRCA                    TEXT('Branch Code')
     A                                      JREF(3)
     A                                      RENAME(CRBRCA)
     A            MPDUPF                    TEXT('Duplication Flag')
     A                                      JREF(3)
     A                                      RENAME(CRDUPF)
     A            MPNPRT                    TEXT('Number of Parts')
     A                                      JREF(3)
     A                                      RENAME(CRNPRT)
     A            MPKPRT                    TEXT('Part Number (Key)')
     A                                      JREF(1)
     A                                      RENAME(SQKPRT)
     A            MPINFG                    TEXT('Incoming Flag')
     A                                      JREF(4)
     A                                      RENAME(FTINFG)
     A            MPIPRF                    TEXT('Incoming Payment Ref.')
     A                                      JREF(4)
     A                                      RENAME(FTIPRF)
     A            MPOTFG                    TEXT('Outgoing Flag')
     A                                      JREF(4)
     A                                      RENAME(FTOTFG)
     A            MPOPRF                    TEXT('Outgoing Payment Ref.')
     A                                      JREF(4)
     A                                      RENAME(FTOPRF)
     A            MPFRGL                    TEXT('Funds From')
     A                                      JREF(4)
     A                                      RENAME(FTFRGL)
     A            MPTOGL                    TEXT('Funds To')
     A                                      JREF(4)
     A                                      RENAME(FTTOGL)
     A            MPTRNO                    TEXT('Transaction reference no.')
     A                                      JREF(1)
     A            MPCCYP                    TEXT('Currency Code      (Part)')
     A                                      JREF(1)
     A                                      RENAME(MPCYCD)
      *                                              Begin /124673/
     A            MPVDTC                    TEXT('Century of Value date')
     A                                      JREF(2)
     A                                      RENAME(MPSVDC)
      *                                              End   /124673/
     A            MPVDTP                    TEXT('Value date  YYMMDD (Part)')
     A                                      JREF(2)
     A                                      RENAME(MPSVDT)
     A            MPAMTP                    TEXT('Payment Amount     (Part)')
     A                                      JREF(2)
     A                                      RENAME(MPAMTS)
     A            MPINBK                    TEXT('Intermediary')
     A                                      JREF(2)
     A            MPACIN                    TEXT('A/c with Institution')
     A                                      JREF(2)
     A            MPBENF                    TEXT('Beneficiary')
     A                                      JREF(2)
     A            MPPMFG                    TEXT('Payment Message Flag')
     A                                      JREF(2)
     A            MPPYAM                    TEXT('Payment Amount')
     A                                      JREF(2)
     A            SQSTPQ                    TEXT('STP Queue Name')
     A                                      JREF(1)
      *>> Start EEE065
     A            SQSTAT                    TEXT('STP Queue Status')
     A                                      JREF(1)
      *>> End EEE065
     A*.....................................................................
     A* Key fields
     A          K SQSTPQ
     A          K MPCCYP
     A          K MPTRNO
     A          K MPMSGR
     A          K MPKPRT
     A*=====================================================================
