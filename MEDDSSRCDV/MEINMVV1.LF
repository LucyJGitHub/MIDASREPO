      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas ME Message Routing Extension')                   *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  LF/MEINMVV1 - Message Routing Extension                      *
      *                                                               *
      *  (C) Copyright Misys IBS Ltd. 2004                            *
      *                                                               *
      *  Last Amend No. MD031937           Date 13Jan15               *
      *  Prev Amend No. AR856737           Date 15Jul2011             *
      *                 ESL038             Date 03Aug2004             *
      *                 ERN056             Date 03Aug2004             *
      *                 EEE065   *CREATE   Date 01Jan2004             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD031937 - Upgrade STP enhancements to BF Midas 2.1          *
      *  AR856737 - Upgrade STP enhancements to Midas Plus level.     *
      *  ESL038 - Funds Transfer STP Enhancements                     *
      *  ERN056 - FT Rocket Payment Messages                          *
      *  EEE065 - FT Payment Routing                                  *
      *                                                               *
      *****************************************************************
     A                                      DYNSLT
      *
     A          R @INMVV1                   TEXT('(C) Misys IBS  +
     A                                      . 2004')
     A                                      PFILE(MEINMVP0)
      *---------------------------------------------------------------------
     A            MVNWRK                    TEXT('Network')
     A            MVMTPY                    TEXT('Message type')
     A            MVSNDK                    TEXT('Sender (key)')
     A            MVRECI                    TEXT('Record Identifier')
     A            MVDSPR                    TEXT('Display Payment Route')
      * ERN056 Rocket Payment Messages
     A            MVMCMG                    TEXT('MC Message (Y/N)')
     A            MVMCSX                    TEXT('MC Midas Suffix')
     A            MVMACK                    TEXT('MC Ack Immed')
     A            MVMACM                    TEXT('MC Ack Msg')
     A            MVMNCK                    TEXT('MC Reject')
     A            MVMNCM                    TEXT('MC Reject Msg')
     A            MVIMPA                    TEXT('MC Import Accounting')
     A            MVREVA                    TEXT('MC Reversal Accounting')
      * ERN056 Rocket Payment Messages
      *
      * Start of ESL038 changes
     A            MVDSPI                    TEXT('Display PCAT on Incoming')
     A            MVDSPO                    TEXT('Display PCAT on Outgoing')
     A            MVDINC                    TEXT('Disable Input Correction')
      * End of ESL038 changes
      *
      *....................
      * Key fields
     A          K MVNWRK
     A          K MVMTPY
     A          K MVSNDK
     A*.....................................................................
     A* SELECT: Live
     A* Field    : Record Identifier
     A* Condition: Live
     A          S MVRECI                    COMP(EQ 'D')
     A*=====================================================================
