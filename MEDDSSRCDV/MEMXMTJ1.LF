      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IMIP                                                      *
/*EXI *  TEXT('Midas MX Join to MT FT Msgs')                          *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  LF/MEMXMTJ1 - ME Incoming MX Join to MT FT Msgs              *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. MD060246           Date 21Jul22               *
      *  Prev Amend No. CSW122  *CREATE    Date 04Oct21               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD060246 - Increase field length value for MX Element Value  *
      *             changed from 120A to 500A. (Recompile)            *
      *  CSW122 - SWIFT ISO 20022 Changes                             *
      *                                                               *
      *****************************************************************

     A          R @MXMTJ1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2021')
     A                                      JFILE(MEISODPD +
     A                                            MEINDTPD  +
     A                                            MEINFTPD
     A                                           )
     A          J                           JOIN(1 2)
     A                                      JFLD(MEUETR DTMFLD)
     A          J                           JOIN(2 3)
     A                                      JFLD(DTMSGR FTMSGR)
     A            MEMSGR                    TEXT('Msg Unique Reference')
     A                                      JREF(1)
     A            DTMSGR                    TEXT('FT Msg Unique Ref')
     A                                      COLHDG('FT Msg Unique Ref')
     A                                      JREF(2)
     A            MEMSID                    TEXT('MsgID - Hdr')
     A                                      JREF(1)
     A            MEUETR                    TEXT('UETR')
     A                                      JREF(1)
     A            MESEQN                    TEXT('Sequence Number')
     A                                      JREF(1)
     A            MEENAM                    TEXT('Element Name')
     A                                      JREF(1)
     A            MEEVAL                    TEXT('Element value')
     A                                      JREF(1)
      *
     A          K MEMSID
     A          K MEUETR
