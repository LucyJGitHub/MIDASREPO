      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas ME Results of DSPFD')                            *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  LF/METXTFL1 - Midas ME Results of DSPFD                      *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CFT051             Date 02Jun15               *
      *                 CFT050  *CREATE    Date 03Jun13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFT051 - SWIFTRef Directories 2014                           *
      *  CFT050 - SWIFTRef Directories upload                         *
      *                                                               *
      *****************************************************************
      *
     A          R QWHFDBAS                  PFILE(METXTFPD)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited 2013')
      *
     A            ATFILE
     A            ATLIB
     A            ATFCDT
     A            ATFCTM
      *
     A            PREF3              I      SST(ATFILE 1 3)
     A            DATE6              I      SST(ATFILE 4 6)
      *
      ** Release Date based on filename 'B3_YYMMDD'
      ** - The Higher date is assumed to be taken
      *
     A          K PREF3
     A          K DATE6                     DESCEND
      *
     A          S PREF3                     CMP(EQ 'B3_')
     A          S PREF3                     CMP(EQ 'CT_')
     A          S PREF3                     CMP(EQ 'CU_')
     A          S PREF3                     CMP(EQ 'HL_')
     A          S PREF3                     CMP(EQ 'TZ_')
      *
      **Start of change CFT051
      *
     A          S PREF3                     CMP(EQ 'SD_')
     A          S PREF3                     CMP(EQ 'LL_')
      *
      **End of change CFT051
