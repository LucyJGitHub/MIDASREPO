      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MS MT942 Automatic Generation Audit Report.')
      *****************************************************************
      *                                                               *
      *  Midas - SWIFT Direst Link Module                             *
      *                                                               *
      *  PRTF/ME000060AU - Automatic Generation of MT941 and MT942 on *
      *                    MT920 Request Audit Report.                *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL013  *CREATE    Date 13May02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL013 - MT941/MT942 Production on MT920 Request.            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE ME000060AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    52'Automatic Generation of MT941/2'
     A                                      SPACEB(1)
      *
     A                                    51'following MT920 Request - AUDIT'
     A                                      SPACEB(1)
      *
     A                                    51'-------------------------------'
     A                                      SPACEB(1)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    58'CALCULATED'
     A                                      SPACEB(3)
     A                                    58'----------'
     A                                      SPACEB(1)
      *
     A                                     6'Requests on MT920 +
     A                                      - PF/MSMSI2L7'
     A                                      SPACEB(3)
      *
     A            AU942R         5  0     60EDTCDE(3)
      *
     A                                     6'------------------------+
     A                                      -------------'
     A                                      SPACEB(1)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
