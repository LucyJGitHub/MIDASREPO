      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MS MT942 Automatic Generation Report.')
      *****************************************************************
      *                                                               *
      *  Midas - SWIFT Direct Link Module                             *
      *                                                               *
      *  PRTF/ME000060P1 - Automatic Generation of MT941 and MT942    *
      *                    on MT920 Request.                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL013  *CREATE    Date 13May02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL013 - MT941/2 Production on MT920 Request.                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      **  Report Header Record Format  *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE ME000060P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101'TIME'
     A                                      SPACEB(1)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    52'Automatic Generation of MT941/2'
     A                                      SPACEB(1)
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
     A                                   132' '
      *
     A                                    55'following MT920 Request'
     A                                      SPACEB(1)
      *
     A                                    55'-----------------------'
     A                                      SPACEB(1)
      *
     A                                     6'Received Date :'
     A                                      SPACEB(2)
     A            P1MODE         7A  O    22
      *
     A                                     6'Received Time :'
     A                                      SPACEB(1)
     A            P1MOTM         5A  O    22
      *
     A                                     6'Sender  . . . :'
     A                                      SPACEB(1)
     A            P1STID        12A  O    22
     A            P1SCNO         6A  O    36
     A            P1SCSN        10A  O    44
      *
     A                                     6'Incoming MIR  :'
     A                                      SPACEB(1)
     A            P1IMIR        28A  O    22
      *
     A                                     6'Incoming TRN  :'
     A                                      SPACEB(1)
     A            P1ITRN        16A  O    22
      *
     A                                     1'Message '
     A                                      SPACEB(003)
     A                                    10'Account requested'
     A  61                                35'Retail No.'
     A                                    47'Action taken'
      *
     A                                     2
     A                                      'Type'
     A                                      SPACEB(1)
      *
      *-------------------------*
      **  Detail Record Format  *
      *-------------------------*
      *
     A          R DETAIL1
      *
     A                                      SPACEB(1)
     A            P1MTYP         3A  O     3
     A            P1ACCN        35A  O    10
     A            P1ACTN        80A  O    47
      *
      *-------------------*
      **  Report Trailer  *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                50'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                55'***  END OF REPORT  ***'
     A                                      SPACEB(3)
