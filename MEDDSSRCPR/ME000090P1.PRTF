      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas ME Incoming MT920s Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  PRTF/ME000080P1 - Incoming MT920s Report (Detail)            *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL013  *CREATE    Date 10Jun02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL013 - MT94x Messages Generation                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE ME000090P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    47'INCOMING MT920'
     A                                      SPACEA(1)
      *
     A                                    47'---------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     5'T.R.N.'
     A                                    25'M.I.R.'
     A                                    54'RECEIVED DATE/TIME'
     A                                      SPACEA(1)
     A                                     5'REQUESTED MSG TYPE'
     A                                    25'REQUESTED ACCOUNT'
     A                                    54'GENERATED MSG TYPE'
     A                                    74'OUTGOING T.R.N.'
     A                                    95'NARRATIVE'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RTRNO         16         5
     A            RMIR          28        25
     A            RRCVD          7        56
     A                                    64'/'
     A            RTIME          6S 0     66EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A            RMTYP          5        10
     A            RACC          35        25
     A            RGMTP          5        64
     A            RTRN          16        74
     A            RNARR         35        95
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  Report Trailer  *
      *--------------------*
      *
     A          R TRAIL
     A                                    51'***  END OF REPORT ***'
     A                                      SPACEB(3)
      *
