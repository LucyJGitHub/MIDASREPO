      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas ME Network Accounts List')
      *****************************************************************
      *                                                               *
      *  Midas - Messages Management Module                           *
      *                                                               *
      *  PRTF/ME002203P1 - Network Accounts List                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CGL013  *CREATE    Date 16Apr02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL013 - MT94x Message Generation                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SKIPB(003)
      *
     A                                     1
     A                                      'REFERENCE ME002203P1'
     A            BJURPT    R             35
     A                                      REFFLD(URPT)
     A                                   102
     A                                      'DATE'
     A            BJMRDT    R            107
     A                                      REFFLD(MRDT)
     A                                   123
     A                                      'PAGE'
     A                                   128
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   102
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   107
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    45
     A                                      'NETWORK ACCOUNTS DESCRIPTION LIST'
     A                                      UNDERLINE
     A                                      SPACEB(001)
      *
     A  37                                89
     A                                      'BRANCH -'
     A  37        PBRCH     R             98
     A                                      REFFLD(BRCA)
     A  37        PBRNM     R            102
     A                                      REFFLD(BRNM)
     A N37                               120
     A                                      'ALL BRANCHES'
      *
      *-------------------------*
      *** Detail Record Formats *
      *-------------------------*
      *
     A          R NETWORK
     A                                      SPACEB(003)
      *
     A                                     1
     A                                      'NETWORK:'
     A            PNWRK          6A       10
     A                                    22
     A                                      'ACCOUNT:'
     A            PBRCH          3A       31
     A                                    34
     A                                      '-'
      * Begin /CSD027/
     A**********  PCNUM          6S 0     35                                                  CSD027
     A            PCNUM          6A       35
      * End /CSD027/
     A                                    41
     A                                      '-'
     A            PCCY           3A       42
     A                                    45
     A                                      '-'
     A**Start of change CGL029
     A**********  PACOD          4S 0     46                                                  CGL029
     A**********                          50                                                  CGL029
     A**********                            '-'                                               CGL029
     A**********  PACSQ          2S 0     51                                                  CGL029
     A**********                          59                                                  CGL029
     A**********                            'RETAIL NUMBER:'                                  CGL029
     A**********  PRANO         10A       74                                                  CGL029
     A            PACOD         10S 0     46
     A                                    56
     A                                      '-'
     A            PACSQ          2S 0     57
     A                                    62
     A                                      'RETAIL NUMBER:'
     A            PRANO         10A       77
     A**End of change CGL029
     A                                    90
     A                                      'TYPE:'
     A            PNATY          1A       96
     A                                   103
     A                                      'NAME:'
     A            PANAM         20A  O   109
      *
     A                                     1
     A                                      'Generates MT950:'
     A                                      SPACEB(001)
     A            PG950          1A       18
     A  38                                22
     A                                      'Last Change:'
     A  38                                35
     A                                      'Date'
     A  38        PALCDT         7A       40
     A  38                                50
     A                                      'Type'
     A  38        PACHTP         1A       55
     A  38                                59
     A                                      'User'
     A  38        PAUSER        10A       64
      *--------------------------------*
     A          R MT950DTL
     A                                      SPACEB(002)
      *
     A                                     1
     A                                      'MT950 DETAILS:'
      *
     A                                     2
     A                                      'Statement:'
     A                                      SPACEB(001)
     A                                    18
     A                                      'Frequency'
     A            P5SFQ          1A       28
     A                                    32
     A                                      'Day in Month'
     A            P5SDY          2S 0     45
     A                                    50
     A                                      'Next Date'
     A            P5NSD          7A       60
     A                                    70
     A                                      'Demand Indicator'
     A            P5DSI          1A       87
      *
     A                                     2
     A                                      'Last Statement:'
     A                                      SPACEB(001)
     A                                    18
     A                                      'Balance'
     A            P5LCB         18A       26
     A                                    47
     A                                      'Date'
     A            P5LSD          7A       52
     A                                    62
     A                                      'Time'
     A            P5LST          6S 0     67
     A                                      EDTWRD('  :  :  ')
     A                                    78
     A                                      'Page'
     A            P5LSN          5S 0     83
      *--------------------------------*
     A          R DEST
     A                                      SPACEB(003)
      *
     A                                     1
     A                                      'DESTINATION:'
     A            PDSTN         11A       14
     A                                    31
     A                                      'DESCRIPTION:'
     A            PDSTD         35A       44
      *
     A                                     1
     A                                      'Generates:'
     A                                      SPACEB(001)
     A                                    12
     A                                      'MT940'
     A            PG940          1A       18
     A                                    22
     A                                      'MT941'
     A            PG941          1A       28
     A                                    32
     A                                      'MT942'
     A            PG942          1A       38
     A  38                                42
     A                                      'Last Change:'
     A  38                                55
     A                                      'Date'
     A  38        P4LCDT         7A       60
     A  38                                70
     A                                      'Type'
     A  38        P4CHTP         1A       75
     A  38                                79
     A                                      'User'
     A  38        P4USER        10A       84
      *--------------------------------*
     A          R MT940DTL
     A                                      SPACEB(002)
      *
     A                                     1
     A                                      'MT940 DETAILS:'
      *
     A                                     2
     A                                      'Statement:'
     A                                      SPACEB(001)
     A                                    18
     A                                      'Frequency'
     A            P0SFR          1A       28
     A                                    32
     A                                      'Day in Month'
     A            P0SDM          2S 0     45
     A                                    50
     A                                      'Next Date'
     A            P0NSD          7A       60
     A                                    70
     A                                      'Demand Indicator'
     A            P0DSI          1A       87
      *
     A                                     2
     A                                      'Default Information to the Account-
     A                                       Owner:'
     A                                      SPACEB(001)
      *
     A            P0MI1         65A        5
     A                                      SPACEB(001)
      *
     A            P0MI2         65A        5
     A                                      SPACEB(001)
      *
     A            P0MI3         65A        5
     A                                      SPACEB(001)
      *
     A            P0MI4         65A        5
     A                                      SPACEB(001)
      *
     A            P0MI5         65A        5
     A                                      SPACEB(001)
      *
     A            P0MI6         65A        5
     A                                      SPACEB(001)
      *
     A                                     2
     A                                      'Last Statement:'
     A                                      SPACEB(001)
     A                                    18
     A                                      'Balance'
     A            P0LCA         18A       26
     A                                    47
     A                                      'Date'
     A            P0LSD          7A       52
     A                                    62
     A                                      'Time'
     A            P0LST          6S 0     67
     A                                      EDTWRD('  :  :  ')
     A                                    78
     A                                      'Page'
     A            P0LSN          5S 0     83
      *--------------------------------*
     A          R MT941DTL
     A                                      SPACEB(002)
      *
     A                                     1
     A                                      'MT941 DETAILS:'
      *
     A                                     2
     A                                      'Date/Time indicator:'
     A                                      SPACEB(001)
     A            P1DTI          1A       23
     A                                    27
     A                                      'Opening Balance Info:'
     A            P1OBI          1A       49
     A                                    53
     A                                      'Number and Sum of Entries:'
     A            P1LSF          1A       80
      *
     A                                     2
     A                                      'Available Balance:'
     A                                      SPACEB(001)
     A                                    21
     A                                      'Closing'
     A            P1CAB          1A       29
     A                                    33
     A                                      'Forward'
     A            P1FAB          1A       41
      *
     A                                     2
     A                                      'Default Information to the Account-
     A                                       Owner:'
     A                                      SPACEB(001)
      *
     A            P1MI1         65A        5
     A                                      SPACEB(001)
      *
     A            P1MI2         65A        5
     A                                      SPACEB(001)
      *
     A            P1MI3         65A        5
     A                                      SPACEB(001)
      *
     A            P1MI4         65A        5
     A                                      SPACEB(001)
      *
     A            P1MI5         65A        5
     A                                      SPACEB(001)
      *
     A            P1MI6         65A        5
     A                                      SPACEB(001)
      *
     A                                     2
     A                                      'Allow Generation on:'
     A                                      SPACEB(001)
     A                                    23
     A                                      'Ext Request'
     A            P1AER          1A       35
     A                                    39
     A                                      'Schedule'
     A            P1AGS          1A       48
     A                                    52
     A                                      'Manual Request'
     A            P1AMR          1A       67
      *
     A                                     2
     A                                      'Generation if no Movement:'
     A                                      SPACEB(001)
     A            P1GNM          1A       29
     A                                    33
     A                                      'Re-Extract Postings:'
     A                                    54
     A                                      'From Last MT940'
     A            P1RPL          1A  O    70
     A                                    74
     A                                      'Shadow'
     A            P1RAS          1A  O    81
      *
     A                                     2
     A                                      'Next Opening Balance:'
     A                                      SPACEB(001)
     A            P1LCB         18A       24
     A                                    44
     A                                      'Last Statement:'
     A                                    60
     A                                      'Date'
     A            P1LSD          7A       65
     A                                    74
     A                                      'Time'
     A            P1LST          6S 0     79
     A                                      EDTWRD('  :  :  ')
     A                                    89
     A                                      'Page'
     A            P1LSP          5S 0     94
      *
     A                                     2
     A                                      'Last Sum:'
     A                                      SPACEB(001)
     A                                    18
     A                                      'DB No'
     A            P1LDN          5S 0     24
     A                                      EDTCDE(4)
     A                                    32
     A                                      'DB Amt'
     A            P1LDA         18A       39
     A                                    60
     A                                      'CR No'
     A            P1LCN          5S 0     66
     A                                      EDTCDE(4)
     A                                    74
     A                                      'CR Amt'
     A            P1LCA         18A       81
      *--------------------------------*
     A          R MT942DTL
     A                                      SPACEB(002)
      *
     A                                     1
     A                                      'MT942 DETAILS:'
      *
     A                                     2
     A                                      'Number and Sum of Entries:'
     A                                      SPACEB(001)
     A            P2LSF          1A       29
     A                                    33
     A                                      'Floor Limit:'
     A                                    46
     A                                      'DB'
     A            P2FLD         16        49
     A                                    68
     A                                      'CR'
     A            P2FLC         16        71
      *
     A                                     2
     A                                      'Default Information to the Account-
     A                                       Owner:'
     A                                      SPACEB(001)
      *
     A            P2MI1         65A        5
     A                                      SPACEB(001)
      *
     A            P2MI2         65A        5
     A                                      SPACEB(001)
      *
     A            P2MI3         65A        5
     A                                      SPACEB(001)
      *
     A            P2MI4         65A        5
     A                                      SPACEB(001)
      *
     A            P2MI5         65A        5
     A                                      SPACEB(001)
      *
     A            P2MI6         65A        5
     A                                      SPACEB(001)
      *
     A                                     2
     A                                      'Allow Generation on:'
     A                                      SPACEB(001)
     A                                    23
     A                                      'Ext Request'
     A            P2AER          1A       35
     A                                    39
     A                                      'Schedule'
     A            P2AGS          1A       48
     A                                    52
     A                                      'Manual Request'
     A            P2AMR          1A       67
      *
     A                                     2
     A                                      'Generation if no Movement:'
     A                                      SPACEB(001)
     A            P2GNM          1A       29
     A                                    33
     A                                      'Re-Extract Postings:'
     A                                    54
     A                                      'From Last MT940'
     A            P2RPL          1A       70
     A                                    74
     A                                      'Shadow'
     A            P2RAS          1A       81
      *
     A                                     2
     A                                      'Last Statement:'
     A                                      SPACEB(001)
     A                                    18
     A                                      'Date'
     A            P2LSD          7A       23
     A                                    33
     A                                      'Time'
     A            P2LST          6S 0     38
     A                                      EDTWRD('  :  :  ')
     A                                    49
     A                                      'Page'
     A            P2LSP          5S 0     54
      *
     A                                     2
     A                                      'Last Sum:'
     A                                      SPACEB(001)
     A                                    18
     A                                      'DB No'
     A            P2LDN          5S 0     24
     A                                      EDTCDE(4)
     A                                    32
     A                                      'DB Amt'
     A            P2LDA         18A       39
     A                                    60
     A                                      'CR No'
     A            P2LCN          5S 0     66
     A                                      EDTCDE(4)
     A                                    74
     A                                      'CR Amt'
     A            P2LCA         18A       81
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILER
     A                                      SPACEB(003)
     A  37                                48
     A                                      '***  END OF REPORT FOR BRANCH'
     A  37        PBRCH     R             78
     A                                      REFFLD(BRCA)
     A  37                                83
     A                                      '***'
     A N37                                55
     A                                      '***  END OF REPORT  ***'
