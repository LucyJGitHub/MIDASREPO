      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas ME Outgoing messages release by range')
     A*********************************************************************
     A*                                                                   *
     A*  Midas        MESSAGE MANAGEMENT
     A*                                                                   *
     A*   ME0610P1 - Outgoing message release by range                    *
     A*                                                                   *
     A*      (c) Finastra International Limited 2001                      *
     A*                                                                   *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.03 ----------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    PREV AMEND NO. 057127             DATE 11JUN93                 *
     A*                   E39473             DATE 01MAY92                 *
     A*                   E38623             DATE 15APR92                *
     A*                   E38493             DATE 15APR92                *
     A*                                                                   *
     A*------------------------------------------------------------------*
     A*                                                                   *
     A*    MD046248 - Finastra Rebranding                                 *
     A*    057127  -  OVERPRINTING OF HEADINGS IF SINGLE BRANCH SYSTEM    *
     A*               BECAUSE SPACEB(2) CONDITIONED ON MULTIBRANCH FLAG   *
     A*    E39473  -  SHOULD NOT BE FILE SEPARATORS                       *
     A*    E38623  -  REFERENCE SHOULD INCLUDE FULL NAME OF PRINTER FILE  *
     A*    E38493  -  SUPRESS ALL BRANCH INFO WHEN SINGLE BRANCHING      *
     A*                                                                   *
     A*********************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                          *
     A*                                                                   *
     A*********************************************************************
      *
     A                                      REF(MFRFL)
     A          R R01                       TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(003)
     A                                      SPACEA(002)
     A                                     2
     A*                                                   /E38623/
     A**********                            DFT('Reference ME0610')
     A                                      DFT('Reference ME0610P1')
     A*                                                   /E38623/
     A            BJURPT        53   O    35
     A                                    91
     A                                      DFT('DATE')
     A            RDAT           7   O    96
     A                                   119
     A                                      DFT('PAGE')
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A*                                                   /Begin 057127/
     A                                      SPACEA(002)
     A*                                                   /End   057127/
      *
     A*                                                   /E38493/
     A**********                           2
     A**********                            DFT('Branch')
     A**********                            SPACEB(002)
     A**********  BRCA           3   O     9
     A  87                                 2DFT('Branch')
     A**87*******                           SPACEB(002)             /057127/
     A  87        BRCA           3   O     9
     A*                                                   /E38493/
     A                                    44
     A                                      DFT('OUTGOING MESSAGES RELEASE BY R-
     A                                      ANGE')
     A                                    91
     A                                      DFT('Released by ')
     A            @USRP         10   O   104
     A                                    44
     A                                      DFT('-------------------------------
     A                                      ----')
     A                                      SPACEB(001)
      *
     A          R R011
     A                                      SKIPB(010)
     A                                      SPACEA(002)
     A                                    28
     A                                      DFT('Transaction')
     A                                    45
     A                                      DFT('Transaction')
     A                                    61
     A                                      DFT('Message')
      *
     A                                     2
     A                                      DFT('T.R.N.')
     A                                      SPACEB(001)
     A                                    20
     A                                      DFT('Module')
     A                                    28
     A                                      DFT('number')
     A                                    45
     A                                      DFT('type')
     A                                    51
     A                                      DFT('sub-type')
     A                                    61
     A                                      DFT('type')
     A                                    71
     A                                      DFT('pty')
     A                                    78
     A                                      DFT('Network')
     A                                    89
     A                                      DFT('Time')
      *
     A          R R00
     A                                      SPACEA(002)
     A                                      SPACEB(001)
     A                                     2
     A                                      DFT('Options taken -')
     A*                                                   /E38493/
     A**********                          20
     A**********                            DFT('Branch')
     A**********  @@BCH          3   O    27
     A**********                          34
     A**********                            DFT('Level')
     A**********  @@LVL          1   O    40
     A  87                                20DFT('Branch')
     A  87        @@BCH          3   O    27
     A  87                                34DFT('Level')
     A  87        @@LVL          1   O    40
     A*                                                   /E38493/
     A                                    46
     A                                      DFT('Network')
     A            SNTWRK         6   O    54
     A                                    64
     A                                      DFT('First T.R.N.')
     A            SFTRN         16   O    77
     A                                    95
     A                                      DFT('Last T.R.N.')
     A            STTRN         16   O   107
     A*/COPY WNCPYSRC,ME0610P102
      *
     A          R R02
     A                                      SPACEA(001)
     A                                      SPACEB(001)
     A            TRNO          16   O     2
     A            MODI           2   O    20
     A N15        MTRN          15   O    28
     A N15        TNTP           2   O    45
     A N15        SBTP           2   O    51
     A  15                                28DFT('-------')
     A  15                                36DFT('Multiple message')
     A  15                                53DFT('-------')
     A            MTPY           3   O    61
     A            MPRY           1   O    71
     A            NWRK           6   O    78
     A N44N46     @RELT          8   O    89
     A  44
     AO 46                               100
     A                                      DFT('*** CANNOT RELEASE ***')
     A          R R03
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    50
     A                                      DFT('*** No Messages to report ***')
     A          R R04
     A                                      SPACEB(001)
     A*                                                   /E38493/
     A**********                          55
     A**********                            DFT('*** End Of Report ***')
     A N87                                55DFT('*** End Of Report ***')
     A  87                                55DFT('*** End Of Branch Report ***')
     A*                                                   /E38493/
     A*/COPY WNCPYSRC,ME0610P101
