      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Outgoing message details report')
     A*********************************************************************
     A*                                                                   *
     A*  Midas        MESSAGE MANAGEMENT
     A*                                                                   *
     A*   ME0810P1 - Outgoing message details report - general selection  *
     A*                                                                   *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                   *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    LAST AMEND NO. E32658             DATE 29MAY92                 *
     A*    PREV AMEND NO. E39473             DATE 01MAY92                 *
     A*                   E38623             DATE 15APR92                 *
     A*                   E38493             DATE 15APR92                 *
     A*                                                                   *
     A*-------------------------------------------------------------------*
     A*                                                                   *
     A*    E32658  -  'END OF REPORT FOR BRANCH' TEXT SHOULD SHOW THE     *
     A*               BRANCH CODE; 'END OF REPORT' TEXT SHOULD BE ON      *
     A*               THE SAME PAGE AS 'END OF REPORT FOR BRANCH' TEXT.   *
     A*    E39473  -  CREATE PARMS CHANGED FILESEP FROM 1 TO 0            *
     A*    E38623  -  REFERENCE SHOULD INCLUDE FULL NAME OF PRINTER FILE  *
     A*    E38493  -  SUPRESS ALL BRANCH INFO WHEN SINGLE BRANCHING       *
     A*                                                                   *
     A*********************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                          *
     A*                                                                   *
     A*********************************************************************
     A                                      REF(MFRFL)
     A**********R R01                       SKIPB(3)
     A          R R01                       TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                      SPACEA(2)
     A*                                                   /E38623/
     A**********                           2'Reference ME0810'
     A                                     2'Reference ME0810P1'
     A*                                                   /E38623/
     A            BJURPT        53        35
     A                                    93'DATE'
     A            RDAT           7        98
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A*                                                   /E38493/
     A*********   SBHWRK        10         2
     A  87        SBHWRK        10         2
     A*                                                   /E38493/
     A                                    40'OUTGOING MESSAGES DETAIL - GENERAL'
     A                                    75'SELECTION'
     A                                      SPACEA(1)
     A                                    40'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~+
     A                                       ~~~~~~~~~~'
     A                                      SPACEA(1)
     A*                                                   /E38493/
     A**********  OTHT20        20         2
     A**********  OTHB03         3        23
     A  87        OTHT20        20         2
     A  87        OTHB03         3        23
     A*                                                   /E38493/
     A                                      SPACEA(1)
     A*                                                   /E38493/
     A*N44******                           2'Settlement - receive'
     A*N44******  RCBR03         3        23
     A N44 87                              2'Settlement - receive'
     A N44 87     RCBR03         3        23
     A*                                                   /E38493/
      *
     A          R R00                       SPACEB(9)
     A                                    22'Option taken -'
     A                                      SPACEA(2)
     A*                                                   /E38493/
     A**********                          30'Branch'
     A**********  @@BCH          3        43
     A**********                          83'Level'
     A**********  @@LVL          1        99
     A  87                                30'Branch'
     A  87        @@BCH          3        43
     A  87                                83'Level'
     A  87        @@LVL          1        99
     A*                                                   /E38493/
     A                                      SPACEA(2)
      *
     A                                    30'Module id'
     A            @MODID         2        43
     A                                    83'External system'
     A            @EXSYI         6        99
     A                                      SPACEA(2)
      *
     A                                    30'Network'
     A            @NTWRK         6        43
     A                                    83'Message group'
     A            @MSGRP         3        99
     A                                      SPACEA(2)
      *
     A          R STATUS                    SPACEA(1)
     A            STS           68        30
      *
     A          R R00C
     A                                    30'Creation date range'
     A                                    83'from'
     A            FMDAT          6        99
     A                                      SPACEA(1)
     A                                    83'to'
     A            TODAT          6        99
     A                                      SPACEA(1)
     A                                    30'Transaction reference number'
     A                                    83'from'
     A            @FMTRN        16        99
     A                                      SPACEA(1)
     A                                    83'to'
     A            @TOTRN        16        99
     A                                      SPACEA(1)
      *
      *                                                   /E32658/
     A**********R*R03                       SPACEB(20)
     A**********                          50'** END OF REPORT FOR BRANCH **'
     A          R R03                       SPACEB(3)
     A                                    50'** END OF REPORT FOR BRANCH'
     A            BCHWK3         3        78
     A                                    82'**'
     A  98                                50'*** END OF REPORT ***'
     A                                      SPACEB(3)
      *
     A**********R*R04                       SPACEB(20)
     A          R R04                       SPACEB(3)
      *                                                   /E32658/
     A                                    50'*** END OF REPORT ***'
      *
     A          R R05                       SPACEB(20)
     A                                    50'For'
     A            MODI           2        55
     A            MTRN          15        60
     A                                      SPACEA(1)
     A                                    50'Please refer to multiple message'
     A            WK16          16        84
     A                                      SPACEA(1)
     A                                    50'which has already been printed'
     A                                    81'on page'
     A            @TABP          4  0     89EDTCDE(Z)
      *
     A          R DETL                      SPACEA(1)
     A            LINE         125         1
     A*/COPY WNCPYSRC,ME0810P101
