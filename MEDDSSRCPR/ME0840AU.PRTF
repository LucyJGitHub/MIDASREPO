      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Outgoing message status  report')
     A*****************************************************************
     A*                                                               *
     A*  Midas   Message Management                               *
     A*                                                               *
     A*  ME0840AU - Outgoing message status  report - selection       *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01518             Date 03Oct94               *
      *                 E38623             Date 15Apr92               *
     A*                 E38493             DATE 15APR92               *
     A*---------------------------------------------------------------*
      *  MD046248 - Finastra Rebranding                               *
     A*  S01518 - BLI Lending Swift Enhancements.                     *
     A*  E38623 - REFERENCE SHOULD INCLUDE FULL NAME OF PRINTER FILE  *
     A*  E38493 - SUPRESS ALL BRANCH INFO WHEN SINGLE BRANCHING       *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S
     A*
     A********************************************************************
     A                                      REF(MFRFL)
     A**********R AU1                       SKIPB(3)
     A          R AU1                       TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                      SPACEA(2)
     A*                                                   /E38623/
     A**********                           2'Reference ME0840'
     A                                     2'Reference ME0840AU'
     A*                                                   /E38623/
     A            BJURPT        53        35
     A                                    93'DATE'
     A            RDAT           7        98
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    50'OUTGOING MESSAGES STATUS'
     A                                      SPACEA(1)
     A                                    50'~~~~~~~~~~~~~~~~~~~~~~~~'
     A                                      SPACEA(1)
      *
     A          R RR0                       SPACEB(9)
     A                                    22'Option taken -'
     A                                      SPACEA(2)
     A*                                                   /E38493/
     A**********                          30'Branch'
     A**********  @@BCH          3        43
     A**********                          83'Level'
     A**********  @@LVL          1        99
     A  87                                30'Branch'
     A  87        @@BCH          3        43
     A  87                                83'Level'
     A  87        @@LVL          1        99
     A*                                                   /E38493/
     A                                      SPACEA(2)
      *
     A                                    30'Network'
     A            @NTWRK         6        38
     A                                      SPACEA(2)
      *
     A                                    30'Module id'
     A            @MODID         2        41
     A                                    83'External system'
     A            @EXSYI         6       100
     A                                      SPACEA(2)
      *
     A          R RR0C
     A                                    30'Creation date range'
     A                                    52'from'
     A            FMDAT          6        57
     A                                      SPACEA(1)
     A                                    52'to'
     A            TODAT          6        57
     A                                      SPACEA(1)
      *
     A          R SSTLN                     SPACEA(1)
     A            STS           70        30
      *
     A          R RR02                      SPACEB(20)
     A
     A                                    50'*** NO MESSAGES TO REPORT ***'
     A                                      SPACEA(1)
     A*                                              Begin /S01518/
     A          R R07                       SPACEB(3)
     A                                    45'Total Number of Messages +
     A                                       Generated'
     A                                      SPACEA(1)
     A                                    45'------------------------+
     A                                      ----------'
     A                                      SPACEA(1)
     A          R R08                       SPACEB(1)
     A                                    50'Number of'
     A            MSGTPX         5        60
     A                                    67'---'
     A            MSCNTX         3  0     72
     A*                                              End   /S01518/
      *
     A          R DBERROR
     A                                    39'OUTGOING MESSAGE STATUS DATABA+
     A                                      SE ERROR REPORT.'
     A                                      SPACEA(12)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'***************************** '
     A                                    50'************************'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'***************************** '
     A                                    50'************************'
     A*                                                   /E38623/
     A**********                            SKIPA(43)
     A**********                          55'*** END OF REPORT ***'
     A*                                                   /E38623/
