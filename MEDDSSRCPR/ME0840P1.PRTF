      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas ME Outgoing message status report')
     A*****************************************************************
     A*                                                               *
     A*  Midas   Message Management                               *
     A*                                                               *
     A*  ME0840P1 - Outgoing message status  report - selection       *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
     A*  Last Amend No. 184664             DATE 08Dec00               *
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. S01518             DATE 03OCT94               *
     A*                 E39480             DATE 12JUN92               *
     A*                 E32658             DATE 29MAY92               *
     A*                 E39473             DATE 01MAY92               *
     A*                 E38623             DATE 15APR92               *
     A*                 E38493             DATE 15APR92               *
     A*                 E29588             DATE 10DEC91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  184664 - Report parts of multiples, with status (and space   *
     A*           out user and workstation a bit better.)             *
     A*  S01518 - BLI Lending Swift Enhancements.                     *
     A*  E39480 - UNDERLINE COLUMN HEADINGS                           *
     A*         - 'REMOVE 'END OF BRANCH' TEXT FOR MULTINRANCH        *
     A*           REPORTS; PRINT 'END OF REPORT FOR BRANCH' ONLY.     *
     A*  E32658 - 'END OF REPORT FOR BRANCH' TEXT SHOULD SHOW THE     *
     A*            BRANCH CODE; 'END OF REPORT' TEXT SHOULD BE ON     *
     A*            THE SAME PAGE AS 'END OF REPORT FOR BRANCH' TEXT.  *
     A*  E39473 - CREATE PARMS CHANGED FILESEP FROM 1 TO 0            *
     A*  E38623 - REFERENCE SHOULD INCLUDE FULL NAME OF PRINTER FILE  *
     A*  E38493 - SUPRESS ALL BRANCH INFO WHEN SINGLE BRANCHING       *
     A*  E29588 - POST SWIFT RATIONALISATION AMENDMENTS               *
     A*           o If date left blank do not use rundate but select  *
     A*           all messages with TODY equal blank.                 *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**********R R01                       SKIPB(3)
     A          R R01                       TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                      SPACEA(2)
     A*                                                   /E38623/
     A**********                           2'Reference ME0840'
     A                                     2'Reference ME0840P1'
     A*                                                   /E38623/
     A            BJURPT        53        35
     A                                    93'DATE'
     A            RDAT           7        98
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A*                                                   /E38493/
     A**********                           2'Branch'
     A**********  BCHWK3         3         9
     A  87                                 2'Branch'
     A  87        BCHWK3         3         9
     A*                                                   /E38493/
     A                                    50'OUTGOING MESSAGES STATUS'
     A                                      SPACEA(1)
     A                                    50'~~~~~~~~~~~~~~~~~~~~~~~~'
      *
     A          R R00                       SPACEB(9)
     A                                    22'Option taken -'
     A                                      SPACEA(2)
     A*                                                   /E38493/
     A**********                          30'Branch'
     A**********  @@BCH          3        43
     A**********                          83'Level'
     A**********  @@LVL          1        99
     A  87                                30'Branch'
     A  87        @@BCH          3        43
     A  87                                83'Level'
     A  87        @@LVL          1        99
     A*                                                   /E38493/
     A                                      SPACEA(2)
      *
     A                                    30'Network'
     A            @NTWRK         6        38
     A                                      SPACEA(2)
      *
     A                                    30'Module id'
     A            @MODID         2        41
     A                                    84'External system'
     A            @EXSYI         6       101
     A                                      SPACEA(2)
      *
     A          R R00C
     A                                    30'Creation date range'
      ** BEGIN E29588 **
     A***********                         52'from'                        E29588
     A*********** FMDAT          6        57                              E29588
     A***********                           SPACEA(1)                     E29588
     A***********                         52'to'                          E29588
     A*********** TODAT          6        57                              E29588
     A***********                           SPACEA(2)                     E29588
     A N45                                52'from'
     A N45        FMDAT          6        57
     A N45                                  SPACEA(1)
     A N45                                52'to'
     A N45        TODAT          6        57
     A N45                                  SPACEA(2)
      *
     A  45                                52'Today''s Work'
     A  45                                  SPACEA(3)
      ** END E29588 **
      *                                                                   E29588
     A          R STATUS                    SPACEA(1)
     A            STS           70        30
      *
      *                                                   /E32658/
     A**********R R03                       SPACEB(20)
     A          R R03                       SPACEB(3)
     A**********                          50'** END OF REPORT FOR BRANCH * '
     A                                    50'** END OF REPORT FOR BRANCH'
     A            BCHWK3         3        78
     A                                    82'**'
      *                                                   /E39480/
     A**98******                            SPACEA(3)
     A**98******                          56'*** END OF REPORT ***'
      *                                                   /E39480/
      *
     A**********R R04                       SPACEB(20)
     A          R R04                       SPACEB(3)
     A                                    50'*** END OF REPORT ***'
      *                                                   /E32658/
     A*                                              Begin /S01518/
     A          R R05                       SPACEB(3)
     A                                     3'Total Number of Messages +
     A                                       Generated for Branch'
     A                                       SPACEA(1)
     A                                     3'------------------------+
     A                                      ---------------------'
     A                                      SPACEA(1)
     A          R R06                       SPACEB(1)
     A                                     3'Number of'
     A            MSGTP          5        13
     A                                    20'---'
     A            MSCNT          3  0     25
     A*                                              End   /S01518/
      *
      *                                                   /E39480/
     A**********R R011                      SKIPB(10)
     A          R R011                      SKIPB(9)
      *                                                   /E39480/
     A                                     3'Creation'
     A                                    30'Msg'
     A*/COPY WNCPYSRC,ME0840P101
     A                                    39'Del'
     A                                    43'Non'
     A*/COPY WNCPYSRC,ME0840P102
      *                  Start of Change                   /184664/
     A**********                          80'Release :-'
     A                                    80'Released/Amended by :-'
      *                  End of Change                     /184664/
     A                                      SPACEA(1)
      *
     A                                     3'Date'
     A                                    12'T.R.N'
     A                                    30'Typ'
     A*/COPY WNCPYSRC,ME0840P103
     A                                    35'Pr'
     A                                    39'Not'
     A                                    43'Del'
     A                                    48'Message'
     A                                    56'status'
     A                                    64'Network'
     A*/COPY WNCPYSRC,ME0840P104
     A                                    80'User'
      *                  Start of Change                   /184664/
     A**********                          90'Workstn'
     A                                    91'Workstn'
      *                  End of Change                     /184664/
     A                                   102'Date'
     A                                   110'Time'
      *                                                   /E39480/
     A                                      SPACEA(1)
      *
     A                                     3'--------'
     A                                    12'-----'
     A                                    30'---'
     A*/COPY WNCPYSRC,ME0840P105
     A                                    35'--'
     A                                    39'---'
     A                                    43'---'
     A                                    48'--------------'
     A                                    64'-------'
     A*/COPY WNCPYSRC,ME0840P106
     A                                    80'----'
      *                  Start of Change                   /184664/
     A**********                          90'-------'
     A                                    91'-------'
      *                  End of Change                     /184664/
     A                                   102'----'
     A                                   110'----'
      *                                                   /E39480/
     A                                      SPACEA(2)
      *
     A          R DETL                      SPACEA(1)
     A
     A            MGDEF          7         3
     A            TRNO          16        12
     A            MTPY           3        30
     A*/COPY WNCPYSRC,ME0840P107
     A            MPRY           1        35
     A            DLNOT          1        40
     A            NDELW          1        44
     A            MSTT          12        48
     A            NWRKF          6        64
     A*/COPY WNCPYSRC,ME0840P108
     A            RUSR          10        80
      *                  Start of Change                   /184664/
     A**********  RWSN          10        90
     A            RWSN          10        91
      *                  End of Change                     /184664/
     A            RELD           7       102
     A N20        @RELT          8       110
      *                  Start of Change                   /184664/
     A          R @PART                     SPACEA(1)
     A            @PTRN         16        14
     A            @PST          12        48
      *                  End of Change                     /184664/
