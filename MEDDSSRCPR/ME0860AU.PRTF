      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Outgoing mail re-print (ISO format)')
     A*********************************************************************
     A*                                                                   *
     A*  Midas        MESSAGE MANAGEMENT
     A*                                                                   *
     A*   ME0860AU - Outgoing Mail Re-print (ISO format)                  *
     A*                                                                   *
     A*      (c) Finastra International Limited 2001                      *
     A*                                                                   *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*    PREV AMEND NO. E38623             DATE 15APR92                 *
     A*                                                                   *
     A*-------------------------------------------------------------------*
     A*                                                                   *
     A*    MD046248 - Finastra Rebranding                                 *
     A*    E38623  -  REFERENCE SHOULD INCLUDE FULL NAME OF PRINTER FILE  *
     A*                                                                   *
     A*********************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                          *
     A*                                                                   *
     A*********************************************************************
     A                                      REF(MFRFL)
     A          R AU1                       TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(003)
     A                                      SPACEA(002)
     A                                     2
     A*                                      /E38623/
     A**********                            DFT('Reference ME0860')
     A                                      DFT('Reference ME0860AU')
     A*                                      /E38623/
     A            BJURPT        53   O    35
     A                                    93
     A                                      DFT('Date')
     A            BJMRDT         7   O    98
     A                                   119
     A                                      DFT('PAGE')
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(002)
      *
     A                                    40
     A                                      DFT('OUTGOING MAIL RE - PRINT (ISO -
     A                                      ')
     A                                    70
     A                                      DFT('FORMAT)')
     A                                      SPACEA(001)
     A                                    40
     A                                      DFT('~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-
     A                                      ')
     A                                    70
     A                                      DFT('~~~~~~~')
     A                                      SPACEA(001)
      *
      *
     A          R RR0
     A                                      SPACEB(009)
     A                                     7
     A                                      DFT('Option taken -')
     A                                      SPACEA(002)
      *
     A                                     7
     A                                      DFT('Module id')
     A            @MODID         2   O    18
     A                                      SPACEA(002)
      *
     A                                     7
     A                                      DFT('Creation date range')
     A                                    42
     A                                      DFT('from')
     A            FMDAT          6   O    49
     A                                      SPACEA(001)
     A                                    44
     A                                      DFT('to')
     A            TODAT          6   O    49
     A                                      SPACEA(003)
     A                                     7
     A                                      DFT('Transaction reference number')
     A                                    42
     A                                      DFT('from')
     A            @FMTRN        16   O    49
     A                                      SPACEA(001)
     A                                    42
     A                                      DFT('to')
     A            @TOTRN        16   O    49
     A                                      SPACEA(001)
      *
     A          R RR02
     A                                      SPACEB(020)
     A                                      SPACEA(001)
     A
     A                                    44
     A                                      DFT('*** NO MESSAGES TO REPORT ***')
      *
     A          R DBERROR
     A                                    36'OUTGOING MAIL RE-PRINT +
     A                                      DATABASE ERROR REPORT.'
     A                                      SPACEA(12)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A*                                      /E38623/
     A**********                            SKIPA(43)
     A**********                          55'*** END OF REPORT ***'
     A*                                      /E38623/
      *
