      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas ME Message gen. audit report - AU.')
     A*********************************************************************
     A*                                                                   *
     A*  Midas        MESSAGE MANAGEMENT
     A*                                                                   *
     A*   ME0890AU  -  MESSAGE GENERATION AUDIT REPORT AU                 *
     A*                                                                   *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                   *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*   LAST AMEND NO. E40242            DATE 19JUN92                   *
     A*   PREV AMEND NO.  00000            DATE 00XXX00                   *
     A*                                                                   *
     A*-------------------------------------------------------------------*
     A*                                                                   *
     A*   E40242  -  CREATED TO PRODUCE MESSAGE GENERATION AUDIT LIST     *
     A*                                                                   *
     A*********************************************************************
     A                                      REF(MFRFL)
     A          R AU1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                      SPACEA(2)
     A                                     2'Reference ME0890AU'
     A            BJURPT        53        35
     A                                    93'DATE'
     A            RDAT           7        98
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    50'MESSAGE GENERATION - AUDIT'
     A                                      SPACEA(1)
     A                                    50'--------------------------'
     A                                      SPACEA(1)
      *
      *
     A          R RR0                       SPACEB(9)
     A                                    30'The following messages were'
     A                                    58'created or deleted in job'
     A                                      SPACEA(2)
     A                                    30'Job'
     A            @JNAME        10        35
     A                                    47'User'
     A            @JUSER        10        53
     A                                    65'Number'
     A            @JNUM          6        73
     A                                      SPACEA(2)
     A                                    30'Module'
     A            EBMODD        20        40
     A                                      SPACEA(2)
     A                                    30'Type'
     A            JTYPE         15        40
     A                                      SPACEA(2)
      *
      *
     A          R RR02                      SPACEB(10)
     A
     A                                    50'*** NO MESSAGES TO REPORT ***'
     A                                      SPACEA(1)
      *
      *
     A          R DBERROR                   SPACEB(12)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(3)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SKIPA(43)
     A                                    55'*** END OF REPORT ***'
