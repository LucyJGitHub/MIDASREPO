      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas ME MT970 Accounts List')
      *****************************************************************
      *                                                               *
      *  Midas - ME Module                                            *
      *                                                               *
      *  PRTF/ME0901P1 - ME MT970 Accounts List Audit                 *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSW099  *CREATE    Date 28Feb05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW099 - Creation of MT970 Netting Statements                *
      *                                                               *
      *****************************************************************
      *
      **  FILE LEVEL KEYWORDS
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      **  REPORT HEADER FORMAT
      *
     A          R ME0901R0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
      *
     A                                      SKIPB(3)
     A                                     7'REFERENCE ME0901P1'
     A            RRTITL    R             36REFFLD(TITL)
     A                                   100'DATE'
     A            RRRUNA         7       105
     A                                   118'PAGE'
     A                                   123PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(1)
     A                                    60'MT970 Accounts List'
     A                                      SPACEA(1)
     A                                    60'-------------------'
     A                                      SPACEA(3)
      *
     A                                     1'Midas Account'
     A                                    34'Status'
     A                                      SPACEA(1)
      *
      **  FORMAT TO OUTPUT DETAIL LINES.
      *
     A          R ME0901R1                  TEXT('DETAIL OUTPUT FMT')
      *
     A            RBRCA          3         1SPACEB(1)
     A            RCNUM          6         5
     A            RCCY           3        12
     A            RACOD         10        16
     A            RACSQ          2        27
     A            RSTS          10        34
      *
     A                                      SPACEA(1)
      **  END OF REPORT FORMAT
     A          R ME0901R2                  TEXT('END OF REPORT FORMAT')
      *
     A                                      SPACEB(2)
     A                                    52'*** END OF REPORT ***'
