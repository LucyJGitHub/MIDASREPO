      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas ME Print I.M.M transactions')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Message Management Module
     A*                                                               *
     A*  PRTF/ME1140P1 - ME1140 Print File for IMM Transaction Report *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
     A*  Prev Amend No. CFT006             DATE 13DEC99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 068170             DATE 18SEP93               *
     A*                 S01435             DATE 10AUG93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CFT006 - Funds Transfer addition of MT101 and MT102 messages *
     A*  068170 - Remove V2R2 feature                                 *
     A*  S01435 - Incomimg Message Management                         *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R F0HDR1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                  3  6'REFERENCE'
     A            I#REFN        10      3 16
     A            BJURPT    R           3 37REFFLD(URPT)
     A                                  3100'DATE'
     A            BJMRDT    R           3105REFFLD(RUNA)
     A                                  3119'PAGE'
     A            PAGE1          4  0   3124EDTCDE(Z)
      *
      * Print Relevant Report Heading
      *
     A          R F0HDR2
     A            H2TITL        60      5 37
     A            H2TUL         60      6 37
      *
      * Print Branch Headings
      *
     A          R F0HDR3
     A                                  7  1'Branch :'
     A            H3BRCA    R           7 10REFFLD(BRCA)
      *
      * Print Control Information
      *
     A          R F1DTL1
     A                                    15'Sender           :'
     A                                      SPACEB(2)
     A            D1SNDR        20        34
     A                                    64'Message Type  :'
     A            D1MTPY         3        80
     A            D1MTPD        35        85
      *
     A                                    15'Input Reference  :'
     A                                      SPACEB(1)
     A            D1MIR         28        34
     A                                    64'Received Date :'
     A            D1MODE         6  0     80EDTWRD('  /  /  ')
     A                                    92'Time :'
     A            D1MOTM         4  0     99EDTWRD('  :  ')
     A
      *
     A                                    15'Output Reference :'
     A                                      SPACEB(1)
     A            D1MOR         28        34
     A                                    64'Sent Date     :'
     A            D1SNTD         6  0     80EDTWRD('  /  /  ')
     A                                    92'Time :'
     A            D1SNTT         4  0     99EDTWRD('  :  ')
     A*/COPY WNCPYSRC,ME1140P101
      *
     A*                                              Start /CFT006/
     A************                        15'Value Date       :'
     A************                          SPACEB(1)
     A************D1SVDT         6  0     34EDTWRD('  /  /  ')
     A************                        47'Currency :'
     A************D1CCY          3        58
     A************                        64'Amount :'
     A************D1AMTS        17        73
     A N20                                15'Value Date       :'
     A N20                                  SPACEB(1)
     A N20        D1SVDT         6  0     34EDTWRD('  /  /  ')
     A N20                                47'Currency :'
     A N20        D1CCY          3        58
     A N20                                64'Amount :'
     A N20        D1AMTS        17        73
     A*                                                End /CFT006/
     A N70                                92'Branch       :'
     A*                                              Start /CFT006/
     A  20N70                               SPACEB(1)
     A*                                                End /CFT006/
     A N70        D1BRCA         3       107
      *
     A                                    15'Duplication      :'
     A                                      SPACEB(1)
     A            D1DUPF         3        34
     A                                    47'Priority :'
     A            D1MPRY         1        58
     A                                    64'Network:'
     A            D1NWRK         6        73
     A                                    92'No. Of Parts :'
     A            D1NPRT         3  0    107EDTCDE(Z)
      *
     A                                    15'Job Name/User    :'
     A                                      SPACEB(1)
     A            D1AJOB        10        34
     A                                    45'/'
     A            D1AUSR        10        47
     A                                    64'Entry Time/Date :'
     A            D1ATIM         6  0     82EDTWRD('0 :  :  ')
     A            D1ARDT         7        92
      *
      *
      * Print Message Part Information
      *
     A          R F2DTL1
     A                                     1'Part :'
     A                                      SPACEB(2)
     A            D2KPRT         3  0      8EDTCDE(Z)
     A                                    15'Value Date       :'
     A            D2SVDT         6  0     34EDTWRD('  /  /  ')
     A                                    47'Currency :'
     A            D2CCY          3        58
     A                                    64'Amount :'
     A            D2AMTS        17        73
      *
     A                                    15'Transaction Ref. :'
     A                                      SPACEB(1)
     A            D2TRNO        16        34
     A*                                              Start /CFT006/
     A************                        71'Intermediary :'
     A************D2INBK        35        86
     A N21                                71'Intermediary :'
     A N21        D2INBK        35        86
     A*                                                End /CFT006/
      *
     A                                    15'A/C with Inst''n  :'
     A                                      SPACEB(1)
     A            D2ACIN        35        34
     A                                    71'Beneficiary  :'
     A            D2BENF        35        86
      *
      *
      * Print Transaction status information
      *
     A          R F3DTL1
     A                                     1'Status : FT'
     A                                      SPACEB(2)
     A*                                              Start /CFT006/
     A************                        15'Incoming Payment :'
     A N20N21                             15'Incoming Payment :'
     A  20                                15'RFT              :'
     A  21                                15'CCT              :'
     A*                                                End /CFT006/
     A            D3IPRF        15        34
     A                                    64'Status          :'
     A            D3INTX        35        82
      *
     A*                                              Start /CFT006/
     A************                        15'Outgoing Payment :'
     A************                          SPACEB(1)
     A************D3OPRF        15        34
     A************                        64'Status          :'
     A************D3OTTX        35        82
     A N20N21                             15'Outgoing Payment :'
     A N20N21                               SPACEB(1)
     A N20N21     D3OPRF        15        34
     A N20N21                             64'Status          :'
     A N20N21     D3OTTX        35        82
     A*                                                End /CFT006/
      *
     A N70                                15'Booking Branch   :'
     A                                      SPACEB(1)
     A N70        D3BRCA         3        34
      *
     A                                    15'Funds From       :'
     A                                      SPACEB(1)
     A**Start of change CGL029
     A**********  D3FRGL        22        34                                                  CGL029
     A            D3FRGL        28        34
     A**End of change CGL029
     A                                    64'Funds To        :'
     A**Start of change CGL029
     A**********  D3TOGL        22        82                                                  CGL029
     A            D3TOGL        28        82
     A**End of change CGL029
      *
     A                                    15'Job Name/User    :'
     A                                      SPACEB(1)
     A            D3AJOB        10        34
     A                                    45'/'
     A            D3AUSR        10        47
     A                                    64'Action Time/Date:'
     A            D3ATIM         6  0     82EDTWRD('0 :  :  ')
     A            D3ARDT         7        92
     A                                   101'Action Type:'
     A            D3ATYP         6       114
      *
      *
      * Print Tag Detail Line headings
      *
     A          R F4DTL1
     A            D4HEAD       119         1
     A                                      SPACEB(2)
      *
     A            D4UL         119         1
     A                                      SPACEB(1)
      *
      * Print Tag's Part No. (if part no. = 0)
      *
     A          R F4DTL2
     A* 068170 Start
     A                                     1' '
     A* 068170 End
     A                                      SPACEB(1)
      *
      * Print Tag's Part No. (if part no. > 0)
      *
     A          R F4DTL3
     A                                     1'Part :'
     A                                      SPACEB(2)
     A            D4KPRT         3  0      8EDTCDE(Z)
      *
      * Print tag Detail line
      *
     A          R F4DTL4
     A            D4LINE       119         1
     A                                      SPACEB(1)
      *
      *
      * Print Log file information
      *
     A          R F5DTL1
     A                                     1'Log :'
     A                                      SPACEB(2)
     A                                     9'Action :'
     A            D5ACTX       114        18
      *
     A  40                                 9'Result :'
     A                                      SPACEB(1)
     A  40        D5ARTX       114        18
      *
     A                                    18'Job Name/User :'
     A                                      SPACEB(1)
     A            D5AJOB        10        34
     A                                    45'/'
     A            D5AUSR        10        47
     A                                    64'Entry Time/Date :'
     A            D5ATIM         6  0     82EDTWRD('0 :  :  ')
     A            D5ARDT         7        92
      *
      *
      *  End of Report
      *
     A          R F0TRL1                    TEXT('End of Report')
     A                                      SPACEB(2)
     A                                    56'*** End of Report ***'
      *
      *  Database Error Detail
      *
     A          R F0DBERR                   TEXT('Database Error')
     A                                      SPACEB(1)
     A                                    30'Database Error'
     A                                      SPACEB(2)
     A                                    30'Error in Program..:'
     A                                      SPACEB(2)
     A            ERPGM     R             50REFFLD(ERPGM MEERDTA)
     A                                    30'File..............:'
     A                                      SPACEB(2)
     A            ERFILE    R             50REFFLD(ERFILE MEERDTA)
     A                                    30'Key...............:'
     A                                      SPACEB(2)
     A            ERKEY     R             50REFFLD(ERKEY MEERDTA)
     A                                    30'Subroutine........:'
     A                                      SPACEB(2)
     A            ERSTK     R             50REFFLD(ERSTK MEERDTA)
     A                                    30'Error number......:'
     A                                      SPACEB(2)
     A            ERERNB    R             50EDTCDE(J)
     A                                      REFFLD(ERERNB MEERDTA)
     A                                    30'Narrative.........:'
     A                                      SPACEB(2)
     A            ERNARR    R             50REFFLD(ERNARR MEERDTA)
     A                                    30'Primary reference.:'
     A                                      SPACEB(2)
     A            ERPREF    R             50REFFLD(ERPREF MEERDTA)
