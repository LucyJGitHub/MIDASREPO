      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas ME Print I.M.M Audit Log File')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Message Management Module
     A*                                                               *
     A*  PRTF/ME1170P1 - Print File For ME1170                        *
     A*                  Print Audit Log Report                       *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01435             DATE 10AUG93               *
     A*  PREV AMEND NO. Xnnnnn             DATE ddmmmyy               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  S01435 - Incomimg Message Management                         *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R F0HDR1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                  3  6'REFERENCE'
     A            I#REFN        10      3 16
     A            BJURPT    R           3 37REFFLD(URPT)
     A                                  3100'DATE'
     A            BJMRDT    R           3105REFFLD(RUNA)
     A                                  3119'PAGE'
     A            PAGE1          4  0   3124EDTCDE(Z)
      *
      * Print Relevant Report Heading
      *
     A          R F0HDR2
     A            H2TITL        60      5 37
     A            H2TUL         60      6 37
     A            H2RNG         80      8  1
      *
      * Print Branch Headings
      *
     A          R F0HDR3
     A                                 10  1'Branch :'
     A            H3BRCA    R          10 10REFFLD(BRCA)
      *
      * Print Control Information
      *
     A          R F1DTL1
     A                                    15'Sender           :'
     A                                      SPACEB(2)
     A            D1SNDR        20        34
     A                                    64'Message Type  :'
     A            D1MTPY         3        80
     A            D1MTPD        35        85
      *
     A                                    15'Input Reference  :'
     A                                      SPACEB(1)
     A            D1MIR         28        34
     A                                    64'Received Date :'
     A            D1MODE         6  0     80EDTWRD('  /  /  ')
     A                                    92'Time :'
     A            D1MOTM         4  0     99EDTWRD('  :  ')
     A
      *
     A                                    15'Output Reference :'
     A                                      SPACEB(1)
     A            D1MOR         28        34
     A                                    64'Sent Date     :'
     A            D1SNTD         6  0     80EDTWRD('  /  /  ')
     A                                    92'Time :'
     A            D1SNTT         4  0     99EDTWRD('  :  ')
     A*/COPY WNCPYSRC,ME1170P101
      *
     A                                    15'Value Date       :'
     A                                      SPACEB(1)
     A            D1SVDT         6  0     34EDTWRD('  /  /  ')
     A                                    47'Currency :'
     A            D1CCY          3        58
     A                                    64'Amount :'
     A            D1AMTS        17        73
     A N70                                92'Branch       :'
     A N70        D1BRCA         3       107
      *
     A                                    15'Duplication      :'
     A                                      SPACEB(1)
     A            D1DUPF         3        34
     A                                    47'Priority :'
     A            D1MPRY         1        58
     A                                    64'Network:'
     A            D1NWRK         6        73
     A                                    92'No. Of Parts :'
     A            D1NPRT         3  0    107EDTCDE(Z)
      *
     A                                    15'Job Name/User    :'
     A                                      SPACEB(1)
     A            D1AJOB        10        34
     A                                    45'/'
     A            D1AUSR        10        47
     A                                    64'Entry Time/Date :'
     A            D1ATIM         6  0     82EDTWRD('0 :  :  ')
     A            D1ARDT         7        92
      *
      *
      * Print Message Part Information
      *
     A          R F2DTL1
     A                                     1'Part :'
     A                                      SPACEB(2)
     A            D2KPRT         3  0      8EDTCDE(Z)
     A                                    15'Value Date       :'
     A            D2SVDT         6  0     34EDTWRD('  /  /  ')
     A                                    47'Currency :'
     A            D2CCY          3        58
     A                                    64'Amount :'
     A            D2AMTS        17        73
      *
     A                                    15'Transaction Ref. :'
     A                                      SPACEB(1)
     A            D2TRNO        16        34
     A                                    71'Intermediary :'
     A            D2INBK        35        86
      *
     A                                    15'A/C with Inst''n  :'
     A                                      SPACEB(1)
     A            D2ACIN        35        34
     A                                    71'Beneficiary  :'
     A            D2BENF        35        86
      *
      *
      * Print Log file information - Action/Result Details Line 1
      *
     A          R F3DTL1
     A                                      SPACEB(2)
     A  60                                 1'Log  :'
     A            D3SUBH         8         8
     A            D3TXT1       115        17
      *
      * Print Log file information - Extended Text Lines
      *
     A          R F3DTL2
     A            D3TXT2       115        17
     A                                      SPACEB(1)
      *
      * Print Log file information - Job details
      *
     A          R F3DTL3
     A                                    17'Job Name/User  :'
     A                                      SPACEB(1)
     A            D3AJOB        10        34
     A                                    45'/'
     A            D3AUSR        10        47
     A                                    64'Entry Time/Date :'
     A            D3ATIM         6  0     82EDTWRD('0 :  :  ')
     A            D3ARDT         7        92
      *
      * Print Log file information - No further Information
      *
     A          R F3DTL4
     A            D3NOIN        90        17
     A                                      SPACEB(1)
      *
      *
      *  End of Report
      *
     A          R F0TRL1                    TEXT('End of Report')
     A                                      SPACEB(2)
     A                                    56'*** End of Report ***'
      *
      *  No Details to report
      *
     A          R F0NODT                    TEXT('No Details to Report')
     A                                 25 53'*** No Details -
     A                                      To Report ***'
      *
      *  Database Error Detail
      *
     A          R F0DBERR                   TEXT('Database Error')
     A                                      SPACEB(1)
     A                                    30'Database Error'
     A                                      SPACEB(2)
     A                                    30'Error in Program..:'
     A                                      SPACEB(2)
     A            ERPGM     R             50REFFLD(ERPGM MEERDTA)
     A                                    30'File..............:'
     A                                      SPACEB(2)
     A            ERFILE    R             50REFFLD(ERFILE MEERDTA)
     A                                    30'Key...............:'
     A                                      SPACEB(2)
     A            ERKEY     R             50REFFLD(ERKEY MEERDTA)
     A                                    30'Subroutine........:'
     A                                      SPACEB(2)
     A            ERSTK     R             50REFFLD(ERSTK MEERDTA)
     A                                    30'Error number......:'
     A                                      SPACEB(2)
     A            ERERNB    R             50EDTCDE(J)
     A                                      REFFLD(ERERNB MEERDTA)
     A                                    30'Narrative.........:'
     A                                      SPACEB(2)
     A            ERNARR    R             50REFFLD(ERNARR MEERDTA)
     A                                    30'Primary reference.:'
     A                                      SPACEB(2)
     A            ERPREF    R             50REFFLD(ERPREF MEERDTA)
