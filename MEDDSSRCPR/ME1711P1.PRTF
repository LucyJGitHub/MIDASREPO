      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas ME Bilateral Agreements List Full Report')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PRTF/ME1711P1 - Bilateral Agreements List Report             *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CFT014  *CREATE    Date 25May00               *
      *                 CFT013  *CREATE    Date 25May00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFT014 - Straight-through Processing Phase 2 MT103 Messages  *
      *           Generation for FT.                                  *
      *  CFT013 - Straight-through Processing Phase 2 MT103 Messages  *
      *           Generation for non-FT.                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R ME1711F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2000')
      *
     A                                      SKIPB(3)
      *
     A                                     3'REFERENCE ME1711P1'
      *
     A            BJURPT    R             37REFFLD(URPT)
      *
     A                                   101'DATE'
      *
     A            BJMRDT    R            106REFFLD(MRDT)
      *
     A                                   119'PAGE'
      *
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A            RUSER         10A        3
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    48'Bilateral Agreements Report +
     A                                       - FULL List'
     A                                       SPACEA(1)
      *
     A                                    48'--------------------------+
     A                                       ------------------'
     A                                       SPACEA(3)
      *
      *-------------------------*
      ***  Detail Sub-Headings  *
      *-------------------------*
      *
     A                                     8'Bilateral Agreement Type'
     A                                    40'Branch'
     A                                    54'Other Party'
     A                                    74'Bilateral Agreement +
     A                                       Description'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R ME1711F2
      *
     A                                      SPACEB(1)
      *
     A            RM2BTY        10A        8TEXT('Bilateral Agreement Type')
      *
     A            RM2BRC         3A       40TEXT('Branch')
      *
     A            RM2OTH        11A       54TEXT('Other Party')
      *
     A            RM2BGD        35A       74TEXT('Bilateral Agreement +
     A                                            Description')
      *
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R ME1711F3
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
