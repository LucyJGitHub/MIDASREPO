      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas ME Swift ISO country codes audit list report')
      *****************************************************************
      *                                                               *
      *  Midas - Funds Transfer Module                                *
      *                                                               *
      *  PRTF/ME1714AU - SWIFT ISO Country Codes Audit Report         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CFT014  *CREATE    Date 25May00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFT014 - Straight-through Processing Phase 2 MT103 Messages  *
      *           Generation for FT.                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R ME1741F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2000')
      *
     A                                      SKIPB(3)
      *
     A                                     3'REFERENCE ME1741AU'
      *
     A            BJURPT    R             37REFFLD(URPT)
      *
     A                                   101'DATE'
      *
     A            BJMRDT    R            106REFFLD(MRDT)
      *
     A                                   119'PAGE'
      *
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A            RUSER         10A        3
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    48'SWIFT ISO Country Codes +
     A                                       Report - AUDIT'
     A                                       SPACEA(1)
      *
     A                                    48'--------------------------+
     A                                       ------------'
     A                                       SPACEA(3)
      *
      *---------------------*
      ***  File Control     *
      *---------------------*
      *
     A          R ME1741F2
      *
     A                                    46'No. of Records Processed'
     A            RPROC          5  0     82EDTCDE(3)
     A                                      SPACEA(1)
      *
      *---------------------*
      ***  No Details       *
      *---------------------*
      *
     A          R ME1741F3
      *
     A                                      SPACEB(3)
     A                                    51'*** NO DETAILS TO REPORT ***'
      *
      *---------------------*
      ***  No Details       *
      *---------------------*
      *
      *
     A          R ME1741F4
     A                                    55'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    40'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    40'*'
     A                                      SPACEB(1)
     A                                    93'*'
     A                                    40'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       59
     A                                    67''''
     A                                    93'*'
     A                                    40'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        59
     A                                    88 '''    *'
     A                                    40'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       59
     A                                    69''''
     A                                    93'*'
     A                                    40'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     59
     A                                    62''''
     A                                    93'*'
     A                                    40'*'
     A                                      SPACEB(1)
     A                                    93'*'
     A                                    40'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
