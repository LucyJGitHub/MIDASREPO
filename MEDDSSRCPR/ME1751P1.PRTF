      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas ME Outgoing Message Field Management Report')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  PRTF/ME1751P1 - ME Outgoing Message Management Report        *
      *                                                               *
      *  (c) Finastra International Limited 2006                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CSW207             Date 07Jul07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CFT032  *CREATE    Date 11Sep06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW207 - SWIFT 2007 Standard Changes                         *
      *  CFT032 - Account Line in Field 50X in MT103                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **------------------------------*
      ** Report Header Record Format  *
      **------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2006')
     A                                      SKIPB(001)
     A                                     1'REFERENCE ME1751P1'
     A                                      SKIPB(002)
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                    37'Outgoing Message Field Management -
     A                                      - FULL List'
     A                                      SPACEB(001)
     A                                   132' '
     A                                    37'-----------------------------------
     A                                      -----------'
     A                                      SPACEB(001)
     A                                    19'Network'
     A                                      SPACEB(002)
     A                                    30'Message'
     A                                    41'Message'
     A                                    52'Sender'
     A                                    62'Country'
     A                                    73'Currency'
     A                                    85'Nostro'
     A                                    95'Action'
     A                                   106'Flag'
      **Start of change CSW207
     A  50                               113'Swift 50F Format'
      **End of change CSW207
     A                                    31'Type'
     A                                      SPACEB(001)
     A                                    42'Field'
     A                                    52'Branch'
     A                                    62'Location'
     A                                    43'Tag'
     A                                      SPACEB(001)
      *
     A          R DTLP1
     A                                      SPACEB(001)
     A            RNWRK          6A  O    19
     A            RMTYP          3A  O    31
     A            RMTAG          5A  O    42
     A            RSBRC          3A  O    53
     A            RCTRY          2A  O    64
     A            RCURR          3A  O    75
     A            RNOST          2A  O    87
     A            RACTI         10A  O    94
     A            RFLAG          1A  O   107
      **Start of change CSW207
     A  50        R50F1         15A  O   113TEXT('Swift Format 1')
     A                                      SPACEA(001)
     A  50        R50F2         15A  O   113TEXT('Swift Format 2')
     A                                      SPACEA(001)
     A  50        R50F3         15A  O   113TEXT('Swift Format 3')
     A                                      SPACEA(001)
     A  50        R50F4         15A  O   113TEXT('Swift Format 4')
     A                                      SPACEA(001)
     A  50        R50F5         15A  O   113TEXT('Swift Format 5')
      **End of change CSW207
      *
      **-----------------*
      ** Report Trailer  *
      **-----------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(001)
