      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas ME Outgoing Message Field Management Excepti')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  PRTF/ME1752P1 - ME Outgoing Message Management -             *
      *                  Exception Report                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CFT032  *CREATE    Date 11Sep06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CFT032 - Account Line in Field 50X in MT103                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **------------------------------*
      ** Report Header Record Format  *
      **------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2006')
     A                                      SKIPB(1)
     A                                      SPACEA(2)
     A                                     1'REFERENCE ME1752P1'
     A                                      SKIPB(002)
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                   101'TIME'
     A                                      SPACEB(001)
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                    45'Outgoing Message Field Management -
     A                                      - Exception Report'
     A                                      SPACEB(001)
     A                                   132' '
     A                                    45'-----------------------------------
     A                                      ------------------'
     A                                      SPACEB(001)
     A                                     5'Booking Branch:'
     A                                      SPACEB(002)
     A            R1BRCA         3A  O    21
     A            R1BRCH        30A  O    28
     A                                    20'Payment Reference'
     A                                      SPACEB(002)
     A                                    42'Action Type'
     A                                    55'Value'
     A                                    63'Field Name'
     A                                    82'Field Data'
      *
     A          R DTLP1
     A                                      SPACEB(001)
     A            R1PREF        15A  O    20
     A            R1ACTP        11A  O    42
     A            R1VALU         1A  O    57
     A            R1FLDN        15A  O    63
     A            R1FLDD        31A  O    82
      *
      **-----------------*
      ** Report Trailer  *
      **-----------------*
      *
     A          R TRAILP1
     A                                    55'**  END OF REPORT  **'
     A                                      SPACEB(001)
