      *****************************************************************
/*STD *  PRTFBASE                                                     *
/**** *  TEXT('Midas ME Outgoing Msgs Settled in 'In' Ccy List')                            MD060753
/*EXI *  TEXT('Midas ME Outgoing Msgs Settled in  In  Ccy List')                            MD060753
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  PRTF/ME2001AU - Outgoing Messages Settled in 'In'            *
      *                  Currency List - AUDIT                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD060753           Date 10Nov22               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSW201  *CREATE    Date 02May01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD060753 - Error in source compilation                       *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW201 - SWIFT 2001 Standards Update                         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE ME2001AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    42'Outgoing Messages Settled +
     A                                      in ''In'' Currency List - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    42'--------------------------+
     A                                      -----------------------------'
     A                                      SPACEA(3)
      *
     A          R CONTROL
     A                                    58'PROCESSED'
     A                                      SPACEA(1)
     A                                    58'---------'
     A                                      SPACEA(3)
      *
     A                                     6'No. of Outgoing Messages +
     A                                      Settled in ''In'' Currency'
      *
     A            RCOUNT         5  0     60EDTCDE(3)
     A                                      SPACEA(1)
      *
     A                                     6'-------------------------+
     A                                      ------------------------'
     A                                      SPACEA(2)
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
