      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas ME Outgoing Msgs Settled in 'In' Ccy List')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  PRTF/ME2001P1 - Outgoing Messages Settled in 'In'            *
      *                  Currency List                                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Last Amend No. CSW201  *CREATE    Date 02May01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW201 - SWIFT 2001 Standards Update                         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE ME2001P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'Outgoing Messages Settled +
     A                                      in ''In'' Currency List'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'--------------------------+
     A                                      ---------------------'
     A                                      SPACEA(2)
      *
     A                                     3'Message Type'
     A                                    17'Transaction Reference No.'
     A                                    44'Message Status'
     A                                    60'Midas Transaction No.'
     A                                    83'Transaction Type'
     A                                   101'Message Currency'
     A                                   119'Value Date'
     A                                      SPACEA(2)
      *
     A          R DETAIL
      *
     A            MTPY           3         8
     A            TRNO          16        22
     A            MGST           4        49
     A            MTRN          15        63
     A            TNTP           2        90
     A            RCCY           3       108
     A            RVDAT          7       121
     A                                      SPACEA(1)
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
