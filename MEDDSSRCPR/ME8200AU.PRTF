      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MS Midas/MMM compression server')
      *****************************************************************
      *                                                               *
      *   Midas SWIFT Direct Link : Midas/MMM interface               *
      *                                                               *
      *   ME8200AU - Midas/MMM communications - Audit Report          *
      *                                                               *
      *  (c) Finastra International Limited 2006                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 238594             Date 08Mar06               *
      *                 NNNNNN *C *CREATE  Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *   238594 - Changed audit report of ME8200M to ME8200AU.       *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A          R ME8200F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2006')
     A                                  3  7'REFERENCE ME8200AU'
     A            BJURPT        53      3 45
     A                                  3101'DATE'
     A            BJMRDT         7      3106
     A                                  5 40'Midas/MMM Compression Server'
     A                                  6 40'----------------------------'
      *
     A          R ME8010F2
     A                                      SKIPB(10)
     A                                    62'ERROR REPORT'
     A                                      SPACEA(1)
     A                                    62'------------'
     A                                      SPACEA(12)
     A                                    20'In                    :'
     A            W0FILE        10        45
     A                                      SPACEA(2)
     A                                    20'Key/Operation         :'
     A            W0KEY         30        45
     A                                      SPACEA(2)
     A                                    20'Program               :'
     A            W0PGM         10        45
     A                                      SPACEA(2)
     A                                    20'At                    :'
     A            W0ERNB         5  0     45
     A                                      SPACEA(2)
      *
     A          R ME8200T1                  SPACEB(1)
     A            PR@TI          6  0     10EDTWRD('0 :  :  ')
     A            PR@TR         50        25
      *
     A          R ME8200T2                  SPACEB(2)
     A                                     2'Message with reference'
     A            TRNO      R             25
     A                                    43'was not transmitted because it is +
     A                                      greater than 10000 bytes limit.'
      *
     A          R ME8200F3                  SPACEB(5)
     A                                    55'*** END OF REPORT ***'
