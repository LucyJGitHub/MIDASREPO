      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MS Midas/MMM exception report')
      *****************************************************************
      *                                                               *
      *   Midas SWIFT Direct Link : Midas/MMM interface               *
      *                                                               *
      *   ME8200P1 - Midas/MMM communications - Exception Report      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *   Last amend no. 238594 *CREATE    Date 08Mar06               *
      *   Prev amend no. nnnnnn            Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   238594 - Print message that exceeds 10,000 limit by swift.  *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A          R ME8200D1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A                                  3  7'REFERENCE ME8200P1'
     A            BJURPT        53      3 42
     A                                  3101'DATE'
     A            BJMRDT         7      3106
     A                                  5 43'Midas/MMM Communication Excep+
     A                                      tion report.'
     A                                  6 43'-----------------------------+
     A                                      ------------'
     A                                  8 10'ERROR: List of outgoing message n+
     A                                      ot sent to MMM because the limit o+
     A                                      f 10,000 bytes have been exceeded.'
     A                                  9 10'Message status changed to FORMATT+
     A                                      ING ERROR (EFMT). Please investiga+
     A                                      te.'
      *
     A                                 11 12'T.R.N'
     A                                 11 30'Type'
     A                                 11 64'Network'
      *
     A          R ME8200D2                  SPACEB(1)
     A            TRNO      R             12
     A            MTPY      R             30
     A            NWRK      R             64
      *
     A          R ME8200D3                  SPACEB(5)
     A                                    55'*** END OF REPORT ***'
