     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ME Drop message history')                        *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  ME0250 - Drop Message History                                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CDL096             Date 22Sep14               *
      *  Prev Amend No. CDL094             Date 11Jun14               *
      *                 CSW213             Date 03Jun13               *
      *                 CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 07Jul06               *
      *                 CLE042             Date 06Sep05               *
      *                 CDL038             Date 10May05               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL028             Date 07Feb05               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSW201             Date 02May01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 190127  *CREATE    Date 02May01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CSW213 - SWIFT Changes 2013 (Recompile)                      *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference                                *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  CSW201 - SWIFT 2001 Standards Update (Recompiled)            *
      *  190127 - New Program for this fix.                           *
      *                                                               *
      *****************************************************************
     FMGHISTL0UF  E           K        DISK
     FDEALS   IF  E           K        DISK
     FCLOAN   IF  E           K        DISK
     F            CLOANHHF                          KIGNORE
     F            CLOANZ1F                          KIGNORE
      *
     E                    CPY@    1   1 80
      *
      * ID C  F  H  L    Function of indicators
      *    10            EOF MGHISTPD
      *    11            EOF DEALS
      *    12            EOF CLOAN
      *    U7U8LR        DB error
      *
     ICLOANCKF
     I              NLRA                            CNLRA
      *
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      **  Local data area data structure
      *
     I@HREF       DS                         16
     I                                        1   2 HSMODI
     I                                        3   8 HSTRAN
      **  Data structure for related reference
      *
      *****************************************************************
      *                   Index to subroutines                        *
      *   MAIN  - main process                                        *
      *   INIT  - initial process                                     *
      *   *PSSR - error handling                                      *
      *****************************************************************
     C/EJECT
      *****************************************************************
      *   MAIN - Main process                                         *
      *   Calls -  INIT initial process                               *
      *****************************************************************
      *
      **  Perform initial process
     C                     EXSR INIT
      *
      **  Read multiple headers & single messages
     C                     READ MGHISTL0                 10
      *
     C           *IN10     DOWEQ'0'
      *
      **  Remove history if deal dropped
     C           HSMODI    IFEQ 'DL'
     C           HSMODI    OREQ 'FI'
     C                     MOVE HSTRAN    @TRAN   60
     C           @TRAN     CHAINDEALS                11
      *
     C           *IN11     IFEQ '1'
     C                     DELETMGHISTD0
     C                     END
     C                     END
      *
      **  Remove history if loan dropped
     C           HSMODI    IFEQ 'LE'
     C**********           MOVE HSTRAN    @TRAN                                               CLE148
     C********** @TRAN     CHAINCLOAN                12                                       CLE148
     C                     MOVE HSTRAN    @TRANA  6                                           CLE148
     C           @TRANA    CHAINCLOAN                12                                       CLE148
      *
     C           *IN12     IFEQ '1'
     C                     DELETMGHISTD0
     C                     END
     C                     END
      *
     C                     READ MGHISTL0                 10
     C                     END
      *
     C                     SETON                     LR
     C/EJECT
      *****************************************************************
      *   INIT - Inital process                                       *
      *   Called by main process                                      *
      *****************************************************************
      *
     C           INIT      BEGSR
      *
     C                     MOVEACPY@      BIS@   80
      *
      **  Set up LDA
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'ME0250'  DBPGM
     C                     MOVE *BLANKS   DBASE
     C                     OUT  LDA
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   *PSSR - error handling                                      *
      *   Called by - INIT initial process                            *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
** CPY@   **      Object copyright
(c) Misys International Banking Systems Ltd. 2001
