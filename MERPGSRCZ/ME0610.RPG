     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ME Outgoing messages release by range')
/*OVRF*: OVRDBF FILE(MGOREFI) TOFILE(MGOREFPD)                      : *
/*OVRF*: OVRDBF FILE(MGOREFU) TOFILE(MGOREFPD)                      : *
      *****************************************************************
      *                                                               *
      *  Midas - MESSAGE MANAGEMENT                                   *
      *                                                               *
      *    ME0610 - Outgoing  messages release by range               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. 219242             Date 01Jul03               *
      *                 219241             Date 01Jul03               *
      *                 CSD015             Date 14Oct02               *
      * Midas Release 4.01.03 ----------------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *                 212173             Date 18Nov02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                    CDL008           Date 02May00              *
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                    142823           DATE 20Oct99              *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                    141572           DATE 21SEP98              *
      *                    113352           DATE 13FEB97              *
      *                    067576           DATE 22FEB94              *
      *                    E38493           DATE 16APR92              *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  219242 - Not possible to create MT103+ payment msg from      *
      *           non-FT transaction (Recompile)                      *
      *  219241 - Defaulting of "Details of Charges" assign a         *
      *           system value (Recompile)                            *
      *   CSD015 - Midas Compliance Watch - Watch List Checking       *
      *   212173 - Process validation fields for MT103s               *
      *   CDL008 - Continuous Linked Settlement (Recompiled MGOREFPD) *
      *   142823 - Apply 137492. Output branch code not 'ALL'.        *
      *   141572 - Recompile over changes in MGOREFPD                 *
      *   113352 - Recompile over changes in MGOREFPD                 *
      *   067576  - OUTPUT CORRECT BRANCH CODE TO ZSFILE, TO ENSURE   *
      *             THAT SPOOL FILE IS DISTRIBUTED CORRECTLY          *
      *   E38493  -  SUPRESS ALL BRANCH INFO WHEN SINGLE BRANCHING    *
      *                                                               *
      *****************************************************************
     FMGOREFU UF  E           K        DISK
     F/COPY WNCPYSRC,ME0610F003
     FMGOREFI IF  E           K        DISK
     F            MGOREFD0                          KRENAMEMGOREFDI
     F/COPY WNCPYSRC,ME0610F002
     FME0610AUO   E                    PRINTER      KINFDS SPOOLA     UC
     FME0610P1O   E             61     PRINTER      KINFDS SPOOLP     UC
      *                                                                                       CSD015
     FSDCWHTL1IF  E           K        DISK                                                   CSD015
      ** Compliance Watch Hit List by Function Type                                           CSD015
      *                                                                                       CSD015
      *
      * ID C  F  H  L    Function of indicaators
      * 99              Record locked
      * 98              Record not found/End of file
      * 97
      * 96
      * 46              Message not releasable - for printer file
      * 44              Record locked for update
      * 61              Over flow indicator
      * 15              'Multiple' on report
      * 87               Multibranch environment                          E38493
      * U7U8LR          DB error
      *
     F/COPY WNCPYSRC,ME0610F001
     E                    CPY@    1   1 80
      ** Copyright statement
      *
     E/COPY WNCPYSRC,ME0610E001
     E************        VERFY   1   5  3                                                    212173
     E                    VERFY   1   6  3                                                    212173
     E/COPY WNCPYSRC,ME0610E002
      **  Message types/fields requiring verification
      *
     I/COPY WNCPYSRC,ME0610I005
     ISPOOLA      DS
     I                                       83  92 AFILE
     I                                    B 123 1240AFNUM
     ISPOOLP      DS
     I                                       83  92 PFILE
     I                                    B 123 1240PFNUM
      **  File information data structure
      *
     I/COPY WNCPYSRC,ME0610I001
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      **  Local data area data structure
      *
     IDSRUN       DS
     I                                        1   7 RDAT
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
      **  RUNDAT data area Data structure (date format -ddmmmyy)
      *
     I@RPARM      DS
     I                                        1   6 SNTWRK
     I                                        7  22 SFTRN
     I                                       23  38 STTRN
     I                                       39  48 @USRP
     I                                       49  58 @PDEV
     I                                       59  61 @DBRN
      **  Parameters for &RPARM - 100 long string passed to RCF
      *
     I/COPY WNCPYSRC,ME0610I002
     I            DS
     I                                        1   60RELT
     I                                        1   2 @@HH
     I                                        3   4 @@MM
     I                                        5   6 @@SS
      **  Data structure for release TIME
      *
     I            DS
     I                                        1   8 @RELT
     I                                        1   2 @HH
     I I            ':'                       3   3 @
     I                                        4   5 @MM
     I I            ':'                       6   6 @@
     I                                        7   8 @SS
      **  Data structure for expanded time
      *
     I/COPY WNCPYSRC,ME0610I003
     ISDBANK    E DSSDBANKPD
      **  Bank details
      *
     I/COPY WNCPYSRC,ME0610I004
     ISDMGME    E DSSDMGMEPD
     I** Message Management details.
     I*
     IDSFDY     E DSDSFDY
      **  First data structure for access program; short data structure
      *
     ISDWLCC    E DSSDWLCCPD                                                                  CSD015
      **  Midas Functions for Watch List Checking Details File                                CSD015
      *                                                                                       CSD015
     ISCSARD    E DSSCSARDPD                                                                  CSD015
      ** External data structure for Switchable Features                                      CSD015
      *                                                                                       CSD015
     ISDSTAT    E DSSDSTAT                                                                    CSD015
      ** SD data area                                                                         CSD015
      *                                                                                       CSD015
     C/EJECT
      *
      ** Set up copyright statement
     C                     MOVEACPY@      BIS@   80
      *
      **  Parameters passed from RCF & ME0605
     C           *ENTRY    PLIST
     C                     PARM           @@RSEQ  5
     C                     PARM           @@LVL   1
     C                     PARM           @@BCH   3
     C                     PARM           @RPARM
      *
      **  Parameters passed to ZSFILE
     C           PLIST2    PLIST
     C                     PARM           @@RSEQ
     C                     PARM BRCA2     @PARM   3
     C                     PARM           SFILE  10
     C                     PARM           ZSNUM   60
     C                     PARM           ZSERR   1
      *
      **  Parameters passed to AOBANKR0
     C           PLIST3    PLIST
     C                     PARM '*MSG    '@RTCD   7
     C                     PARM '*FIRST  '@OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** LDA
     C           *NAMVAR   DEFN           LDA
     C/EJECT
      *****************************************************************
      *   MAIN   - Main process                                       *
      *   Calls  - INIT - Init process                                *
      *            DETLP- MAIN processing                             *
      *****************************************************************
      *
      **  Perform initial process
     C                     EXSR INIT
      *
      **  Perform main process
     C                     EXSR DETLP
     C/COPY WNCPYSRC,ME0610C011
      *
      **  Set on LR
     C                     SETON                     LR
     C/EJECT
      *****************************************************************
      *   INIT   - Initial process                                    *
      *   Called by main process                                      *
      *****************************************************************
      *
     C           INIT      BEGSR
      *
      **  Access sdbankpd (via aobankr0) for Bank Title
     C                     CALL 'AOBANKR0'PLIST3
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'SDBANKPD'DBFILE           ************
     C                     MOVE 'FIRST'   DBKEY            * DBERR 01 *
     C                     MOVE 'ME0610  'DBPGM            ************
     C                     MOVE '01'      DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      **  Access data area RUNDAT to find date
     C           *NAMVAR   DEFN RUNDAT    DSRUN
     C           *LOCK     IN   DSRUN
     C                     OUT  DSRUN
     C           MBIN      COMP 'Y'                      87               E38493
      *
      **  If data area not found then perform database error processing
     C           RDAT      IFEQ *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'ME0610  'DBPGM
     C                     MOVE '02'      DBASE            ************
     C                     MOVE 'RUNDAT'  DBFILE           * DBERR 02 *
     C                     OUT  LDA                        ************
     C                     EXSR *PSSR
     C                     END
      *
      ** Access SDMGMEPD for verification flag via the access program.
      *
     C                     CALL 'AOMGMER0'
     C                     PARM '*MSG    '@RTCD   7
     C                     PARM '*FIRST  '@OPTN   7
     C           SDMGME    PARM SDMGME    DSFDY
      *
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE            ************
     C                     MOVEL@OPTN     DBKEY            * DBERR 03 *
     C                     MOVEL'SDMGMEPD'DBFILE           ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END                                       E1
     C/COPY WNCPYSRC,ME0610C009
      *
     C           *NAMVAR   DEFN           SDSTAT                                              CSD015
     C                     IN   SDSTAT                                                        CSD015
      *                                                                                       CSD015
      ** Check if Midas Compliance Watch (CSD015) is installed                                CSD015
      *                                                                                       CSD015
     C                     MOVE 'N'       CSD015  1                                           CSD015
      *                                                                                       CSD015
     C                     CALL 'AOSARDR0'                                                    CSD015
     C                     PARM *BLANKS   PRTCD                                               CSD015
     C                     PARM '*VERIFY' POPTN                                               CSD015
     C                     PARM 'CSD015'  PSARD   6                                           CSD015
     C           SCSARD    PARM SCSARD    DSFDY                                               CSD015
      *                                                                                       CSD015
     C           PRTCD     IFNE *BLANK                                                        CSD015
     C           PRTCD     ANDNE'*NRF   '                                                     CSD015
     C           *LOCK     IN   LDA                                                           CSD015
     C                     MOVEL'SCSARDPD'DBFILE                                              CSD015
     C                     MOVE '05'      DBASE                                               CSD015
     C                     MOVELPSARD     DBKEY                                               CSD015
     C                     OUT  LDA                                                           CSD015
     C                     EXSR *PSSR                                                         CSD015
     C                     ENDIF                                                              CSD015
      *                                                                                       CSD015
     C           PRTCD     IFEQ *BLANK                                                        CSD015
     C                     MOVE 'Y'       CSD015                                              CSD015
     C                     ENDIF                                                              CSD015
      *                                                                                       CSD015
      ** If CSD015 is on, check that function code is being monitored by                      CSD015
      ** Compliance Watch by calling AOWLCCR0                                                 CSD015
      *                                                                                       CSD015
     C           CSD015    IFEQ 'Y'                                                           CSD015
      *                                                                                       CSD015
     C                     CALL 'AOWLCCR0'                                                    CSD015
     C                     PARM *BLANKS   PRTCD                                               CSD015
     C                     PARM '*KEY   ' POPTN                                               CSD015
     C                     PARM 'MESSGENT'PFUNC   8                                           CSD015
     C           SDWLCC    PARM SDWLCC    DSFDY                                               CSD015
      *                                                                                       CSD015
     C           PRTCD     IFNE *BLANK                                                        CSD015
     C           PRTCD     ANDNE'*NRF   '                                                     CSD015
     C           *LOCK     IN   LDA                                                           CSD015
     C                     MOVEL'SDWLCCPD'DBFILE                                              CSD015
     C                     MOVE '06'      DBASE                                               CSD015
     C                     MOVELPFUNC     DBKEY                                               CSD015
     C                     OUT  LDA                                                           CSD015
     C                     EXSR *PSSR                                                         CSD015
     C                     ENDIF                                                              CSD015
      *                                                                                       CSD015
     C                     ENDIF                                                              CSD015
      *                                                                                       CSD015
      ** Compliance Watch Hit List Key List                                                   CSD015
      *                                                                                       CSD015
     C           KWHTKY    KLIST                                                              CSD015
     C                     KFLD           WFUNT   8                                           CSD015
     C                     KFLD           WIDEN  40                                           CSD015
     C                     KFLD           BRCA                                                CSD015
      *
      **  Perform first read on Outgoing Message Reference file for
      **  update (which only contains records that meets user selection
      **  criteria due to OPNQRY command in calling CL MEC0610.
     C                     READ MGOREFU                9998
     C/COPY WNCPYSRC,ME0610C002
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   DETLP  - Detail processing                                  *
      *   Called by main process                                      *
      *****************************************************************
      *
     C           DETLP     BEGSR
      **  If no record found on the file PF/MGOREFPD
     C           *IN98     IFEQ '1'
      *
      ** Open Audit printer file & call ZSFILE via SPLF
     C                     OPEN ME0610AU
     C                     MOVE AFILE     SFILE
     C                     Z-ADDAFNUM     ZSNUM
      *
     C                     EXSR SPLF
      *
      *
      **  Write 'no records to report'
     C                     WRITEAU1
     C                     WRITERR02
      *
      **  Close Audit printer file PRTF/ME0610AU
     C                     CLOSEME0610AU
      *
      **  Else records present to process
     C                     ELSE
      *
      **  Open printer file PRTF/ME0610P1
     C                     OPEN ME0610P1
     C                     MOVE PFILE     SFILE
     C                     Z-ADDPFNUM     ZSNUM
      *
      **  Call ZSFILE via SPLF
     C                     EXSR SPLF
      *
      ** Write headings
      *
     C                     WRITER01
     C                     WRITER00
     C                     WRITER01
     C                     WRITER011
      ** Save current branch
     C                     MOVE BRCA      BCHWK3  3
      *
      ** While not end of MGOREFU  (update file)
     C           *IN98     DOWEQ'0'                        B2
     C/COPY WNCPYSRC,ME0610C016
      *
      ** If current message is locked access that message
      ** on the read only file.
     C           *IN99     IFEQ '1'                        B3
      *
     C                     READ MGOREFDI               9697
      *
      ** If first message on MGOREFU was locked the previous
      ** branch field needs to be initialised with the current
      ** branch
     C           BCHWK3    IFEQ *BLANK
     C                     MOVE BRCA      BCHWK3
     C                     END
      *
      ** Set on flag to show current record locked for update
     C                     SETON                       44
      *
     C                     ELSE                            X3
      *                                                                                       CSD015
      ** If Midas Compliance Watch is active and watch list                                   CSD015
      ** checking applied, check trade if pending investigation                               CSD015
      *                                                                                       CSD015
     C           W1EWLC    IFEQ 'Y'                                                           CSD015
      *                                                                                       CSD015
      ** Check if message type exists in the watch list                                       CSD015
      *                                                                                       CSD015
     C                     CALL 'AOWLMTR0'                                                    CSD015
     C                     PARM *BLANKS   PRTCD   7                                           CSD015
     C                     PARM '*MTYP'   POPTN   7                                           CSD015
     C                     PARM MTPY      PMTYP   3                                           CSD015
     C                     PARM *BLANKS   PMTAG  50                                           CSD015
      *                                                                                       CSD015
      ** If Message Type exists, Execute SRWTCH subroutine                                    CSD015
      *                                                                                       CSD015
     C           PRTCD     IFEQ *BLANKS                                                       CSD015
      *                                                                                       CSD015
     C                     MOVE 'MGOREF  'WFUNT                                               CSD015
     C                     MOVE *BLANK    WIDEN                                               CSD015
     C                     MOVELTRNO      WIDEN                                               CSD015
      *                                                                                       CSD015
     C           KWHTKY    CHAINSDCWHTL1             32                                       CSD015
      *                                                                                       CSD015
      ** If record found and under investigation, display error                               CSD015
      ** message 'Item under review by Compliance Officer'                                    CSD015
      *                                                                                       CSD015
     C           *IN32     IFEQ *OFF                                                          CSD015
     C           W3TREL    ANDNE'Y'                                                           CSD015
     C                     MOVE *ON       *IN46                                               CSD015
     C                     ENDIF                                                              CSD015
     C                     ENDIF                                                              CSD015
      *                                                                                       CSD015
     C                     ENDIF                                                              CSD015
      *
      ** If current message is verification type set on flag
     C           MTPY      LOKUPVERFY                    10
      *
      ** If verification is on AND type is verification type AND
      ** network is SWIFT set on flag to show non release
     C           ENVPM     IFEQ 'Y'                        B4
     C           *IN10     ANDEQ'1'
     C/COPY WNCPYSRC,ME0610C007
      *
     C                     SETON                         46
     C                     END                             E4
      *
      ** If network is TELEX and testkey status is Y and
      ** testkey field is blank set on non release flag
     C           NWRK      IFEQ 'TELEX'                    B4
     C           TSKS      ANDEQ'Y'
     C           TSKY      ANDEQ*BLANK
      *
     C                     SETON                         46
     C                     END                             E4
      *
     C/COPY WNCPYSRC,ME0610C012
      ** If current message is suitable for release
     C           *IN46     IFEQ '0'                        B4
     C/COPY WNCPYSRC,ME0610C008
      *
      ** Update Message status
     C                     MOVEL'RSND'    MGST
      *
      ** Update Message group
     C                     MOVEL'2'       MGSG
     C/COPY WNCPYSRC,ME0610C003
      *
      ** Update Last action date
     C                     MOVELRDAT      LADT
      *
      ** Update Released date
     C                     MOVELRDAT      RELD
      *
      ** Update Last action time
     C                     TIME           LATM
      *
      ** Update Released time
     C                     TIME           RELT
      *
      ** Update Released by user
     C                     MOVE @USRP     RUSR
      *
      ** Update Released by workstation
     C                     MOVE @PDEV     RWSN
     C/COPY WNCPYSRC,ME0610C010
      *
     C                     UPDATMGOREFD0
      *
     C/COPY WNCPYSRC,ME0610C013
      ** Format release time using data structures
     C                     MOVE @@HH      @HH
     C                     MOVE @@MM      @MM
     C                     MOVE @@SS      @SS
     C                     END                             E4
      *
     C                     END                             E3
      *
      ** Print message detail line
     C                     EXSR PRTMSG
     C/COPY WNCPYSRC,ME0610C017
      *
     C                     READ MGOREFU                9998
      *
     C                     END                             E2
     C/COPY WNCPYSRC,ME0610C004
      *
      **  Write end of report
     C                     WRITER04
      *
      **  Close printer file PRTF/ME0610P1
     C                     CLOSEME0610P1
      *
     C                     END                             E1
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   PRTMSG - Output single message                              *
      *   Called by DETLP process                                     *
      *****************************************************************
      *
     C           PRTMSG    BEGSR
      *
      ** If user has selected all branches and branch has changed
      ** set up new report
     C           @@BCH     IFEQ 'ALL'
     C           BRCA      ANDNEBCHWK3
     C/COPY WNCPYSRC,ME0610C005
      *
      ** Write End of report
     C                     WRITER04
     C                     CLOSEME0610P1
      *
      ** Save new branch code
     C                     MOVELBRCA      BCHWK3
      *
      ** Open Print file & call ZSFILE via SPLF
     C                     OPEN ME0610P1
     C                     MOVE PFILE     SFILE
     C                     Z-ADDPFNUM     ZSNUM
      *
     C                     EXSR SPLF
      *
      ** Write Report titles, selection criteria & Page headings
     C                     WRITER01
     C                     WRITER00
     C                     WRITER01
     C                     WRITER011
      *
      ** Set off overflow
     C                     SETOF                         61
     C                     END
      *
      ** If user has selected system level and branch has changed
      ** set up new page
     C           @@BCH     IFEQ ' '
     C           BRCA      ANDNEBCHWK3
      *
      ** Save new branch code
     C                     MOVELBRCA      BCHWK3
      *
      ** Write Report titles & Page headings
     C                     WRITER01
     C                     WRITER011
     C                     SETOF                         61
     C                     END
      *
      **  If overflow on write report title & page headings
     C           *IN61     IFEQ '1'
     C                     WRITER01
     C                     WRITER011
     C                     SETOF                         61
     C                     END
      *
      ** Set 'Multiple' indicator
     C                     SETOF                     15
     C           TRNM      IFNE *BLANKS
     C                     SETON                     15
     C                     END
      *
      ** Print detail line
     C                     WRITER02
      *
      ** Set off failed update indicators
     C                     SETOF                       4446
     C/COPY WNCPYSRC,ME0610C006
      *
     C                     ENDSR
      *
     C/EJECT
      *******************************************************************
      *   SPLF  - Ensure Spool File recorded by RCF                     * 067576
      ****SPLF**-*Printer*error*handling********************************* 067576
      *   Called by - DETLP processing                                  *
      *******************************************************************
      *
     C           SPLF      BEGSR
      *
      **  Set up key
      *
     C***********@@BCH     IFEQ 'ALL'                                     067576
     C***********          MOVE *BLANKS   BRCA2   3                       067576
     C***********          ELSE                                           067576
     C***********          MOVE @@BCH     BRCA2                           067576
     C***********          END                                            067576
      *
     C           @@BCH     IFEQ 'ALL'                                     142823
     C                     MOVELBRCA      BRCA2                           142823
     C                     ELSE                                           142823
     C                     MOVE @@BCH     BRCA2   3                       067576
     C                     END                                            142823
      *                                                                   142823
     C                     CALL 'ZSFILE'  PLIST2
      *
     C           ZSERR     IFEQ 'Y'
     C                     EXSR *PSSR
     C                     END
      *
     C                     ENDSR
      *
     C/COPY WNCPYSRC,ME0610C014
     C/EJECT
      ********************************************************************
      *   *PSSR - error handling                                         *
      *   Called by - INIT & SPLF processing                             *
      ********************************************************************
      *
      *
     C           *PSSR     BEGSR
      *
      **  Write database error to report
      *
     C                     OPEN ME0610AU
     C                     WRITEAU1
     C                     WRITEDBERROR
      *
     C/COPY WNCPYSRC,ME0610C015
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
     C/EJECT
     C/COPY WNCPYSRC,ME0610C001
** CPY@   **      Object copyright
(c) Misys International Banking Systems Ltd. 2001
** VERFY - Message types/fields requiring verification
100
200
201
202
203
103
     X/COPY WNCPYSRC,ME0610X001
