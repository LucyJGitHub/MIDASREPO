     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ME Outgoing messages block release')
      *****************************************************************
      *                                                               *
      *  Midas - MESSAGE MANAGEMENT                                   *
      *                                                               *
      *    ME0615 - Outgoing messages block release                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *****************************************************************
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. TDA035             Date 02Apr04               *
      *                 CAAA03             Date 29Mar04               *
      *                 CGL013             Date 29Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                  00000             Date 00XXX00               *
      *                    00000            DATE 00XXX00              *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CRE026 - Consumer Banking                                    *
      *  TDA035 - RTS Signon changes for MidasPlus. (Recompile)       *
      *  CAAA03 - Webfacing Changes                                   *
      *  CGL013 - MT94x Messages Generation (Recompile over new layout*
      *                                      of SDNWRKPD)             *
      *                                                               *
      *****************************************************************
     FMUSER   IF  E           K        DISK
     FME0615DFCF  E                    WORKSTN
      *
     E                    CPY@    1   1 80
     E                    ARR        56  1
      *
      * ID C  F  H  L Function of indicaators
      * 01            DSRUN (DATA AREA) not found indicator
      * 99            Read & Chain fail indicator
      * 98            End of file indicator
      * 30,31,32      Error indicator
      * 42 & 44       Used to control output in Display file ME0615df
      * U7U8LR        DB error
      *
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
      **  Local data area data structure
      *
     IDSRUN       DS
     I                                        1   7 RDAT
     I                                        1   2 @DD
     I                                        3   5 @MMM
     I                                        6   7 @YY
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
      *
      **  Data area for RUNDAT
      *
     I           SDS
     I                                     *PARMS   PGMPRM
     I                                      244 253 PGMDEV
     I                                      254 263 PGMUSR
      *
      **  Program status data structure
      *
     I            DS
     I                                        1 100 @@PRM
     I                                        1  10 @LGF
     I                                       11  64 @STTLE
     I                                       65  70 SNTWRK
     I                                       71  80 @USRP
     I                                       81  81 SABCH
     I I            'N'                      82  82 @IN42
     I                                       83  85 @DBRN
     I                                       86  95 @PDEV
      *
      **  Data structure for 100 long parameter
      *
     I            DS
     I                                        1  56 STTLE
     I                                        1   1 @HI
     I                                        2  55 @STT
     I I            'BLOCK RELEASE OF'        2  18 @T1
     I                                       19  55 @T2
      *
      **  Data structure for underlinning title
      *
     ISDNWRK    E DSSDNWRKPD
      **  Network details
      *
     IDSFDY     E DSDSFDY
     I/COPY WNCPYSRC,ME0615I001
      *
      ** Set up copyright statement
      *
     C                     MOVEACPY@      BIS@   80
      *
      ** Entry parameter list
      *
     C           *ENTRY    PLIST
     C           @LGF      PARM           @@LGF  10
     C           @T2       PARM           @PTTL  37
      *
      **  Parameters passed to FC0040X
      *
     C                     MOVELPGMDEV    @PDEV
      *
     C           PLIST1    PLIST
     C                     PARM *BLANKS   PRTCN   7
     C                     PARM 'ME0620'  PARM2  10
     C                     PARM 'MEC0620' PARM3  10
     C                     PARM '10001'   PARM4   5
     C                     PARM @@PRM     PARM5 100
     C                     PARM MBIN      PMBIN   1
     C                     PARM           @@LVL   1
     C                     PARM           @@ENT   3
      *
      **  Parameters passed to AONWRKR0 - look up
     C           PLIST2    PLIST
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY    '@OPTN   7
     C                     PARM SNTWRK    @NWRK   6
     C           SDNWRK    PARM SDNWRK    DSFDY
      *
      **  ZM1001 ERROR
      *
     C           PLIST4    PLIST
     C                     PARM           @MSGID 10
     C                     PARM           @MSDTA 78
      *
     C                     MOVEL'ME0615'  PGMQ   10                       S01227
     C                     MOVEL*BLANKS   MSGKEY  4
     C           *NAMVAR   DEFN           LDA
      *
      *****************************************************************
      *                   Index to subroutines                        *
      *   MAIN  - Main process                                        *
      *   INIT  - Initial process                                     *
      *   DETL  - Detail processing                                   *
      *   VSEL  - Validation processing                               *
      *   STPRM - Set up parameters                                   *
      *   *PSSR - Error handling                                      *
      *****************************************************************
     C/EJECT
      *****************************************************************
      *   MAIN   - Main process                                       *
      *   Calls  - INIT - Init process                                *
      *            DETL - Detail processing                           *
      *            TERM - Termination processing                      *
      *****************************************************************
      *
      **  Perform initial process
      *
     C                     EXSR INIT
      *
      **  Perform detail process
      *
     C                     EXSR DETL
      *
      ** Set on LR
      *
     C                     SETON                     LR
     C/EJECT
      *****************************************************************
      *   INIT   - Initial process                                    *
      *   Called by main process                                      *
      *****************************************************************
     C           INIT      BEGSR
      *
      **  Access data area DSRUN to find date
      *
     C           *NAMVAR   DEFN RUNDAT    DSRUN
     C           *LOCK     IN   DSRUN
     C                     OUT  DSRUN
      *
      **  If data area not found then perform database error processing
      *
     C           RDAT      IFEQ *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'ME0615  'DBPGM
     C                     MOVE '01'      DBASE            ***************
     C                     MOVE 'RUNDAT'  DBFILE           * DB ERROR 01 *
     C                     OUT  LDA                        ***************
     C                     EXSR *PSSR
     C                     END
      *
      **  Access ZMUSER for users authority
      *
     C           PGMUSR    CHAINMUSER                9899
      *
     C           *IN99     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '02'      DBASE            ***************
     C                     MOVE PGMUSR    DBKEY            * DB ERROR 02 *
     C                     MOVE 'MUSER'   DBFILE           ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
     C                     MOVE USRP      @USRP
     C                     MOVE DBRN      @DBRN
      *
     C           *IN98     IFEQ '0'
      *
     C           MBIN      IFEQ 'Y'
     C           ROUF      ANDEQ'Y'
     C           MBIN      OREQ 'Y'
     C           BANK      ANDEQ'Y'
     C                     SETON                     42
     C                     MOVE 'Y'       @IN42
     C                     ELSE
     C                     MOVE 'N'       SABCH
     C                     END
      *
     C                     END
      *
      **  Underline title
      *
     C**********           BITOF'01234567'@HI                                                 CAAA03
     C**********           BITON'256'     @HI                                                 CAAA03
     C**********           MOVEL@STT      @STTLE                                              CAAA03
     C**********           MOVEASTTLE     ARR                                                 CAAA03
      **********                                                                              CAAA03
     C**********           Z-ADD56        ZZ      20                                          CAAA03
     C********** ARR,ZZ    DOWEQ*BLANK                                                        CAAA03
     C**********           SUB  1         ZZ                                                  CAAA03
     C**********           END                                                                CAAA03
     C**********           ADD  1         ZZ                                                  CAAA03
     C**********           BITOF'01234567'@LO     1                                           CAAA03
     C**********           BITON'2'       @LO                                                 CAAA03
     C**********           MOVE @LO       ARR,ZZ                                              CAAA03
     C**********           MOVEAARR       STTLE                                               CAAA03
     C/COPY WNCPYSRC,ME0615C001
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   DETL   - Detail processing                                  *
      *   Called by main process                                      *
      *****************************************************************
     C           DETL      BEGSR
     C/COPY WNCPYSRC,ME0615C002
      *
      **  Display screen format
      *
     C                     EXFMTW01
      *
     C                     SETON                     30
      *
      **  Do while not F3 or errors outstanding
      *
     C           *INKC     DOWEQ'0'
     C           *IN30     ANDEQ'1'
      *
      **  Validate input - VSEL
      *
     C                     EXSR VSEL
      *
      **  If errors exist, display initial screen
      *
     C           *IN30     IFEQ '1'
     C                     WRITESFLMSGC
     C                     EXFMTW01
     C                     CALL 'ZM1002'
     C                     ELSE
      *
      **  Set up parameters
      *
     C                     EXSR STPRM
     C                     CALL 'FC0040X' PLIST1
     C                     END
      *
     C                     END
     C/COPY WNCPYSRC,ME0615C003
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   VSEL   - Validation processing                              *
      *   Called by DETL process                                      *
      *****************************************************************
     C           VSEL      BEGSR
      *
     C                     SETOF                     303132
     C/COPY WNCPYSRC,ME0615C004
      *
      **  Blank out error codes array
      *
     C                     Z-ADD0         W       20
      *
      **  Validate all branch
      *
     C           SABCH     IFNE 'N'
     C           SABCH     ANDNE'Y'
     C                     MOVEL'MEM1075' @MSGID
     C                     CALL 'ZM1001'  PLIST4
     C                     SETON                     3031
     C                     END
     C/COPY WNCPYSRC,ME0615C005
      *
      **  Access sdnwrkpd (via aonwrkr0)
      *
     C                     CALL 'AONWRKR0'PLIST2
      *
      **  Error handling for access object
      *
     C           @RTCD     IFNE *BLANKS
     C                     MOVEL'MEM1014' @MSGID
     C                     CALL 'ZM1001'  PLIST4
     C                     SETON                     3032
     C                     END
     C/COPY WNCPYSRC,ME0615C006
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   STPRM  - Set up parameter routine                           *
      *   Called by DETL process                                      *
      *****************************************************************
     C           STPRM     BEGSR
      *
      **  All branches = Y, set entity to blanks else to Default branch
      *
     C           SABCH     IFEQ 'Y'
     C                     MOVE *BLANKS   @@ENT
     C                     MOVE 'S'       @@LVL
     C                     ELSE
     C                     MOVE DBRN      @@ENT
     C                     MOVE 'B'       @@LVL
     C                     END
     C/COPY WNCPYSRC,ME0615C007
      *
     C                     ENDSR
     C/EJECT
      **************************************************************************
      *   *PSSR - error handling                                               *
      *   Called by - INIT initial process                                     *
      **************************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
** CPY@   **      Object copyright
(c) Misys International Banking Systems Ltd. 2001
