     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Outgoing Messages by transaction')
      *****************************************************************
      *                                                               *
      *  Midas - MESSAGE MANAGEMENT                                   *
      *                                                               *
      *    ME0825 - Outgoing messages by transaction report request   *
      *             selection                                         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *    PREV AMEND NO.  00000            DATE 00XXX00              *
      *                    00000            DATE 00XXX00              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *****************************************************************
      *                                                               *
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *                                                               *
      *****************************************************************
     FSDMEMOL0IF  E           K        DISK
     FME0825DFCF  E                    WORKSTN
      *
     E                    CPY@    1   1 80
      *
      * ID C  F  H  L Function of indicaators
      * 01            DSRUN (DATA AREA) not found indicator
      * 99            Read & Chain fail indicator
      * 98            End of file indicator
      * 91            Mandatory indicator for LOKUP operation(NOT USED)
      * 32            Error indicator
      * 33            Error indicator
      * U7U8LR        DB error
      *
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
      **  Local data area data structure
      *
     I           SDS
     I                                     *PARMS   PGMPRM
     I                                      244 253 PGMDEV
     I                                      254 263 PGMUSR
      *
      **  Program status data structure
      *
     IDSRUN       DS
     I                                        1   7 RDAT
     I                                        1   2 @DD
     I                                        3   5 @MMM
     I                                        6   7 @YY
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
      *
      **  RUNDAT data area data structure (date format -ddmmmyy)
      *
     I@RPARM      DS
     I                                        1 100 @@RPRM
     I                                        1   2 @MODID
     I                                        3  17 @FMTRN
     I                                       18  32 @TOTRN
      *
      **  Parameters for &RPARM - 100 long string passed to RCF
      *
      *
     ISDBANK    E DSSDBANKPD
      *
      **  Bank details
      *
     IDSFDY     E DSDSFDY
      *
      **  First data structure for access program, Short data structure
      *
      *
      ** Set up copyright statement
      *
     C                     MOVEACPY@      BIS@   80
      *
     C           *NAMVAR   DEFN           LDA
      *
      **  RCF Parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           @SEQ    5
     C                     PARM           @LVL    1
     C                     PARM           @ENT    1
     C                     PARM           @RPARM100
     C                     PARM           RACT    1
     C                     PARM           RCMD    1
      *
      **  ZM1001 ERROR
      *
     C           PLIST3    PLIST
     C                     PARM           @MSGID 10
     C                     PARM           @MSDTA 78
      *
     C                     MOVEL'ME0825'  PGMQ   10                       S01227
     C                     MOVEL*BLANKS   MSGKEY  4
      *
      *****************************************************************
      *                   Index to subroutines                        *
      *   MAIN  - Main process                                        *
      *   INIT  - Initial process                                     *
      *   DETL  - Detail processing                                   *
      *   TERM  - Termination processing                              *
      *   VSEL  - Validation processing                               *
      *   *PSSR - Error handling                                      *
      *****************************************************************
     C/EJECT
      *****************************************************************
      *   MAIN   - Main process                                       *
      *   Calls  - INIT - Init process                                *
      *            DETL - Detail processing                           *
      *            TERM - Termination processing                      *
      *****************************************************************
      *
      **  Perform initial process
      *
     C                     EXSR INIT
      *
      **  Perform detail process
      *
     C                     EXSR DETL
      *
      **  Perform termination process
      *
     C                     EXSR TERM
     C/EJECT
      *****************************************************************
      *   INIT   - Initial process                                    *
      *   Called by main process                                      *
      *****************************************************************
     C           INIT      BEGSR
      *
      **  Access data area DSRUN to find date
      *
     C           *NAMVAR   DEFN RUNDAT    DSRUN
     C           *LOCK     IN   DSRUN
     C                     OUT  DSRUN
      *
      **  If data area not found then perform database error processing
      *
     C           RDAT      IFEQ *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'ME0825  'DBPGM
     C                     MOVE '01'      DBASE            ***************
     C                     MOVE 'RUNDAT'  DBFILE           * DB ERROR 01 *
     C                     OUT  LDA                        ***************
     C                     EXSR *PSSR
     C                     END
      *
      **  Access sdbankpd (via aobankr0) & verify date format
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG  '  @RTCD   7
     C                     PARM '*FIRST  '@OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      **  Error handling for access object
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE 'ME0825  'DBPGM
     C                     MOVE '02'      DBASE            ***************
     C                     MOVE 'AOBANKR0'DBFILE           * DB ERROR 02 *
     C                     OUT  LDA                        ***************
     C                     EXSR *PSSR
     C                     END
      *
      **  Display initial screen
      *
     C                     EXFMTINITSCN
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   DETL   - Detail processing                                  *
      *   Called by main process                                      *
      *****************************************************************
     C           DETL      BEGSR
      *
     C                     SETON                     30
      *
      **  Do while not F3 / F12 or errors outstanding
      *
     C           *INKC     DOWEQ'0'
     C           *INKL     ANDEQ'0'
     C           *IN30     ANDEQ'1'
      *
      **  Validate input - VSEL
      *
     C                     EXSR VSEL
      *
      **  If errors exist, display initial screen
      *
     C           *IN30     IFEQ '1'
     C                     WRITESFLMSGC
     C                     EXFMTINITSCN
     C                     CALL 'ZM1002'
     C                     END
      *
     C                     END
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   VSEL   - Validation processing                              *
      *   Called by DETL process                                      *
      *****************************************************************
     C           VSEL      BEGSR
      *
     C                     SETOF                     30
     C                     SETOF                     32
     C                     SETOF                     33
      *
      **  Blank out error codes array
      *
      **  VAlidation of module id
      *
     C                     EXSR VMDSTS
      *
      **  Validation of FROM TRNO & TO TRNO
      *
     C                     EXSR VTRNSR
      *
      **  Move error codes to message line
      *
     C                     ENDSR
     C/EJECT
      **************************************************************************
      *   VMDSTS - Validation of module ID process                             *
      *   Called by DETL process                                               *
      **************************************************************************
     C           VMDSTS    BEGSR
      *
      **  If module id is not blank, access the module table file
      **  (PF/SDMEMOL0) using the module id entered as key
      *
      *
     C           SMODID    IFEQ *BLANKS
      *
      *
      **  If Module not specified, display 'Module id required'
      *
     C                     MOVEL'MEM1058' @MSGID
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     3032
     C                     SETOF                     98
      *
      *
     C                     ELSE
      *
     C           SMODID    CHAINSDMEMOL0             9899
      *
     C           *IN99     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE 'ME0825'  DBPGM
     C                     MOVE '03'      DBASE            ***************
     C                     MOVE 'FIRST'   DBKEY            * DB ERROR 03 *
     C                     MOVE 'SDMEMOL0'DBFILE           ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      **  If no record found, display 'Module id not found'
      *
     C           *IN98     IFEQ '1'
     C                     MOVEL'MEM1001' @MSGID
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     3032
     C                     SETOF                     98
      *
     C                     END
      *
     C                     END
      *
     C                     ENDSR
     C/EJECT
      **************************************************************************
      *   VTRNSR - Transaction referance number validation process             *
      *   Called by VSEL process                                               *
      *                                                                        *
      **************************************************************************
     C           VTRNSR    BEGSR
      *
     C           SFMTRN    IFNE *BLANKS
     C           STOTRN    ANDEQ*BLANKS
      *
      **  If From Midas Trn specified but not To Midas Trn, display
      **  'A Midas Trn range not specified'
      *
     C                     MOVEL'MEM1027' @MSGID
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     3033
     C                     SETOF                     98
      *
     C                     END
      *
     C           STOTRN    IFNE *BLANKS
     C           SFMTRN    ANDEQ*BLANKS
      *
      **  If To Midas Trn specified but not From Midas Trn, display
      **  'A Midas Trn range not specified'
      *
     C                     MOVEL'MEM1026' @MSGID
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     3033
     C                     SETOF                     98
      *
     C                     END
      *
     C           SFMTRN    IFGT STOTRN
      *
     C                     MOVE SFMTRN    TOTRN
     C                     MOVE STOTRN    FMTRN
      *
     C                     ELSE
      *
     C                     MOVE SFMTRN    FMTRN  15
     C                     MOVE STOTRN    TOTRN  15
      *
     C                     END
      *
     C                     ENDSR
     C/EJECT
      **************************************************************************
      *   TERM - Termination  Processing                                       *
      *   Called by MAIN process                                               *
      *                                                                        *
      **************************************************************************
     C           TERM      BEGSR
      *
      **  If F3 was pressed send termination code of 'E' back to RCF
      *
     C           *INKC     IFEQ '1'
     C                     MOVE 'E'       RCMD
     C                     END
      *
      **  If F12 was pressed send termination code of 'F' back to RCF
      *
     C           *INKL     IFEQ '1'
     C                     MOVE 'F'       RCMD
     C                     END
      *
     C           *INKC     IFEQ '0'
     C           *INKL     ANDEQ'0'
      *
      **  Set up &RPARM with selection criteria parameters
      *
     C                     MOVE SMODID    @MODID
     C                     MOVE FMTRN     @FMTRN
     C                     MOVE TOTRN     @TOTRN
      *
     C                     ELSE
      *
     C                     MOVE *BLANKS   @RPARM
     C                     END
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
     C/EJECT
     C/EJECT
      **************************************************************************
      *   *PSSR - error handling                                               *
      *   Called by - INIT initial process                                     *
      **************************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
** CPY@   **      Object copyright
(c) Finastra International Limited 2001
