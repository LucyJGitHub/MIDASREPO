     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ME Purge I.M.M transactions - phase 3/3')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Message Management Module
     F*                                                               *
     F*  ME1130 - Purge I.M.M. Transactions - Phase 3 of 3            *
     F*                                                               *
     F*  Function:  This program deletes all Log file records that    *
     F*  have an IMM reference of zero - i.e. all those records that  *
     F*  were generated because the message was not valid for         *
     F*  processing.                                                  *
     F*                                                               *
     F*  Called By: MEC1240 - I.M.M. Purge Phase III of III           *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSC022             Date 24Feb04               *
      *  Prev Amend No. 219242             Date 01Jul03               *
      *                 219241             Date 01Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 069250             Date 03May94               *
      *                 S01435             Date 23Jul93
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
      *  219242 - Not possible to create MT103+ payment msg from      *
      *           non-FT transaction (Recompile)                      *
      *  219241 - Defaulting of "Details of Charges" assign a         *
      *           system value (Recompile)                            *
     F*  069250 - Remove dummy OVRDBF command from creation parms.    *
     F*  S01435 - Incoming Message Management                         *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
/******:*OVRDBF*(File*in*program)*(file*on*system)******************:**   069250
     F*****************************************************************
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*                                                               *
     F*  50    First cycle                                            *
     F*  90    Chain fail                                             *
     F*  99    EOF on Read                                            *
     F*                                                               *
     F*  U7/U8 Error Ocurred                                          *
     F*  LR    End program                                            *
     F*                                                               *
     F*****************************************************************
     FMEINLFL0UF  E           K        DISK         KINFSR SRFILE     UC
      *
      *  Incoming Message Log File - Update
      *
     F/COPY WNCPYSRC,ME1130DFPG
      *
      *  /Copy point for File Definition
      *
     F/EJECT
     E                    CPY@    1   1 80
      *
      *  Copyright table
      *
     E/COPY WNCPYSRC,ME1130DEPG
      *
      *  /Copy point for Arrays
      *
     E/COPY MECPYSRC,SRERRE
      *
      *  end of Copysource for error processing
      *
     E/EJECT
      *
     I/COPY WNCPYSRC,ME1130DIPG
      *
      *  /Copy point for Input specifications
      *
      *
     ICPYR@#      DS
      *
      *  Data structure for compilation  - Copyright insertion
      *
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
     I/COPY MECPYSRC,SRERRI
      *
      *  End of Program Error Processing copysource member
      *
      *
     IRUNDTA    E DSRUNDAT
      *
      *  Define rundat data area
      *
     ISDMGME    E DSSDMGMEPD
      *
      * Message Management I.C.D data structure parameter
      *
     IDSFDY     E DSDSFDY
      *
      *   Access object Data Structure
      *
     I/EJECT
      *****************************************************************
      *                 M A I N L I N E
      *****************************************************************
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *  Define entry parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           W0RTN   7
      *
      * Initialise program
      *
     C           *IN50     IFEQ '0'
     C                     EXSR SRINIT
     C                     END
      *
      *  Determine if Log file record may be purged
      *
     C                     EXSR SRPURG
      *
      *  Close down program
      *
     C                     MOVEL'1'       *INLR
      *
      *  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRPURG   : Check If Log File record may be purged            *
      *                                                               *
      *  CALLED BY: Main processing section                           *
      *                                                               *
      *  CALLS    :         -                                         *
      *                                                               *
      *****************************************************************
     CSR         SRPURG    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'SRPURG'  @STK,Q
      *
      *  /Copy point for Purge Extension Files
      *
     C/COPY WNCPYSRC,ME1130DPRG
      *
      *  Set limits with a zero message reference in the partial key
      *
     C                     MOVEL*BLANKS   KFMSGR
      *
     C           KYINLF    SETLLMEINLFL0
     C           KYINLF    READEMEINLFL0                 90
      *
      *  Read while equal to the message reference
      *
     C           *IN90     DOWEQ'0'
      *
      *  Record may be purged if the Insertion date plus the message
      *  management no. of Retention days is less than the run date.
      *
     C           LFRDNB    ADD  ENDSMN    WKDATE  50        Purge Date
      *
     C           WKDATE    IFLE WURDNB
     C                     DELET@INLFL0
     C                     END
      *
      *  Read next record
      *
     C           KYINLF    READEMEINLFL0                 90
      *
     C                     ENDDO
      *
      *  Unwind subroutine stack name
      *
     C           EXPURG    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT   : Initialise and define fields                      *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : SRERR  - Error Processing                         *
      *                                                               *
      *****************************************************************
     CSR         SRINIT    BEGSR
      *
      *  Set up copyright statement
      *
     C                     MOVEACPY@      BIS@   80
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
      *
      *  Define fields
      *
     C           *LIKE     DEFN LFMSGR    KFMSGR
      *
      *  Key kist for Incoming Message Log File
      *
     C           KYINLF    KLIST
     C                     KFLD           KFMSGR
      *
      *  Open Files
      *
     C                     OPEN MEINLFL0
      *
      *  Get Rundate information
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDTA
     C                     IN   RUNDTA
     C                     MOVE AGMRDT    WUMRDT  7        Midas Run date
     C                     MOVE AGRDNB    WURDNB  50       Run day number
     C                     MOVE AGSUC     WUSUC   1        Set up complete
     C                     MOVE AGDFF     WUDFF   1        Date Format
     C                     MOVE AGMBIN    WUMBIN  1        Multi Branched
      *
      * Get Retention Days for Message Management Module
      *
     C                     CALL 'AOMGMER0'             90                 S01194
     C                     PARM '*DBERR ' @RTCD   7                       S01194
     C                     PARM '*FIRST ' @OPTN   7                       S01194
     C           SDMGME    PARM SDMGME    DSFDY                           S01194
      *
      *  If return with an error (LR seton in called program) then
      *  process for database error.
      *
     C           *IN90     IFEQ '1'
     C           @RTCD     OREQ '*ERROR*'
     C                     MOVEL'AOMGMER0'W0FILE
     C                     MOVEL'*CALL'   W0KEY            ***************
     C                     Z-ADD1         W0ERNB           * DB ERROR 04 *
     C                     MOVEL'MEM5003' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      *  If purge period is 0 set to 1.
      *
     C           ENDSMN    IFLE 0
     C                     Z-ADD1         ENDSMN
     C                     END
      *
      *  Indicate that set up is complete
      *
     C                     SETON                     50
      *
      *  Unwind subroutine stack name
      *
     C           EXINIT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
     C/COPY WNCPYSRC,ME1130DCPG
      *
      *  /Copy point for Calculations
      *
     C/EJECT
      *
      *  File and Program Error Processing
      *
     C/COPY MECPYSRC,SRERRC
     C/EJECT
     C/COPY WNCPYSRC,ME1130DOPG
      *
      *  /Copy point for Outputs
      *
**  CPY@ table
(c) Misys International Banking Systems Ltd. 2001
