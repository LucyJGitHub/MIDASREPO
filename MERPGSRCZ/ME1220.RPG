     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ME I.M.M. messages received today report')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Message Management Module
     F*                                                               *
     F*  ME1220 - I.M.M. Messages Received Today Report               *
     F*                                                               *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend NO. 145199             Date 16Oct98               *
      *                 069250             Date 03May94               *
     F*                 S01435             DATE 23JUL93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
     F*  145199 - Recompile for changes to MEINCRPD and MEINMPPD made *
     F*           in Year 2000 fix 124673.                            *
     F*  069250 - Remove dummy OVRDBF command from creation parms.    *
     F*  S01435 - Incoming Message Management                         *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
/******:*OVRDBF*(File*in*program)*(file*on*system)******************:**   069250
     F*****************************************************************
     F*                                                               *
     F*  Indicator usage                                              *
     F*  ~~~~~~~~~~~~~~~                                              *
     F*                                                               *
     F*  50    First cycle                                            *
     F*  90    General work indicator                                 *
     F*  91    General work indicator                                 *
     F*                                                               *
     F*  U7/U8 Error Ocurred                                          *
     F*  LR    End program                                            *
     F*                                                               *
     F*****************************************************************
     FMEINCRL4IF  E           K        DISK                           UC
     F                                              KINFSR SRFILE
      *
      * RTV : Incoming Message Control by Enter Date/Branch
     F/COPY WNCPYSRC,ME1220DFPG
      *
      *  /Copy point for File Definition
      *
     F/EJECT
     E                    CPY@    1   1 80
      *
      *  Copyright table
      *
     E/COPY WNCPYSRC,ME1220DEPG
      *
      *  /Copy point for Arrays
      *
     E/COPY MECPYSRC,SRERRE
      *
      *  end of Copysource for error processing
      *
     E/EJECT
     I/COPY WNCPYSRC,ME1220DIPG
      *
      *  /Copy point for Input specifications
      *
      *
     ICPYR@#      DS
      *
      *  Data structure for compilation  - Copyright insertion
      *
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
     I/COPY MECPYSRC,SRERRI
      *
      *  End of Program Error Processing copysource member
      *
     IRUNDTA    E DSRUNDAT
      *
      *  Define rundat data area
      *
     II#DTA       DS                            256
      *
      *  Report parameters data structure for ME1140
      *
      *           Spool file name (if blank, name unchanged).
      *                 N.B. To change name I#SPLR must be Y
      *
     I                                        1  10 I#SPLN
      *
      *           Repeat open and close of spool file
      *                 If Y create new spool file
      *
     I                                       11  11 I#SPLR
      *
      *           Report Reference if Access checking is off
      *
     I                                       12  21 I#REFN
      *
      *           Report Override Flag / Page No.
      *
     I                                       22  22 I#ROVR
     I                                       23  250I#PAGE
      *
      *           Report Title/Underline (uses msg ID on MEMSG)
      *
     I                                       26  32 I#TITL
     I                                       33  39 I#TUL
      *
      *           Report Formats to print
      *           Each character denotes a format option.
      *
     I                                       40  49 I#RFMT
      *
      *           Branch
      *
     I                                       50  52 I#BRCA
      *
      *           Incoming Message Ref./Part No.
      *
     I                                       53  600I#MSGR
     I                                       61  630I#PART
     I/EJECT
      *****************************************************************
      *                 M A I N L I N E
      *****************************************************************
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      *  Define entry parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           W0RTN   7
     C                     PARM           W0CMPN 10
     C                     PARM           W0CMPS  50
      *
      * Initialise program
      *
     C           *IN50     IFEQ '0'
     C                     EXSR SRINIT
     C                     END
      *
      *  Print todays messages
      *
     C                     EXSR SRPRT
      *
      *  Unwind subroutine stack name
      *
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      *  Return to calling program
      *
     C                     RETRN
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRPRT    : Print todays messages                             *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SRPRT     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRPRT '  @STK,Q
      *
      *  Position file
      *
     C           KYCRL4    KLIST
     C                     KFLD           CRRDNB           Enter date
     C                     Z-ADDWURDNB    CRRDNB           Enter date
     C           KYCRL4    SETLL@INCRL4
      *
      *  Read first record in sequenec
      *
     C           KYCRL4    READE@INCRL4                  90
      *
      *  If no records exit
      *
     C           *IN90     IFEQ '1'
     C                     GOTO EXPRT
     C                     END
      *
      *  Process until end of file
      *
     C           *IN90     DOWEQ'0'
      *
      *  Check for change of branch
      *
     C           SVBRCA    IFNE CRBRCA
     C                     EXSR SRBRCH
     C                     ELSE
      *
      *  Call Print process ME1140
      *
     C                     EXSR SRCALL
     C                     END
      *
      *  Read next record
      *
     C           KYCRL4    READE@INCRL4                  90
     C                     ENDDO
      *
      *  Call Print process ME1140 to close down
      *
     C                     EXSR SREND
      *
      *  Unwind subroutine stack name
      *
     C           EXPRT     TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRBRCH   : Change of branch processing                       *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SRBRCH    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRBRCH'  @STK,Q
      *
      *  /Copy point for Change of branch
      *
     C/COPY WNCPYSRC,ME1220DBRC
      *
      *  Define Branch
      *
     C           *LIKE     DEFN CRBRCA    SVBRCA
     C                     MOVELCRBRCA    SVBRCA
      *
      *  Clear data structure and set report printed
      *
     C                     CLEARI#DTA
      *
      *  Close spool file in ME1140
      *
     C                     MOVEL'ME1250P1'I#SPLN
     C                     MOVEL'Y'       I#SPLR
     C                     MOVEL*BLANKS   I#REFN
     C                     MOVEL'ME1250P1'I#REFN
      *
     C                     MOVEL'MIN0023' I#TITL
     C                     MOVEL'MIN0024' I#TUL
     C                     MOVELCRBRCA    I#BRCA
     C                     Z-ADDCRMSGR    I#MSGR
     C                     Z-ADD0         I#PART
     C                     MOVEL'HDL'     I#RFMT
      *
     C                     CALL 'ME1140'               9090
     C                     PARM *BLANKS   W0RTN   7
     C                     PARM '*PRINT  'W0ACT   8
     C                     PARM           I#DTA            Input Data Str.
     C                     PARM *BLANKS   I#RSQN  5
      *
     C           *IN90     IFEQ '1'
     C           W0RTN     ORNE *BLANKS
     C                     MOVEL'*CALL'   W0FILE
     C                     MOVEL'ME1140'  W0KEY
     C                     Z-ADD1         W0ERNB
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXBRCH    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRCALL   : Call Print Process                                *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SRCALL    BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRCALL'  @STK,Q
      *
      *  /Copy point for Detail only
      *
     C/COPY WNCPYSRC,ME1220DDET
      *
      *  Clear data structure and set report printed
      *
     C                     CLEARI#DTA
      *
      *  Close spool file in ME1140
      *
     C                     MOVEL'ME1250P1'I#SPLN
     C                     MOVEL*BLANKS   I#SPLR
     C                     MOVEL*BLANKS   I#REFN
     C                     MOVEL'ME1250P1'I#REFN
      *
     C                     MOVEL'MIN0023' I#TITL
     C                     MOVEL'MIN0024' I#TUL
     C                     MOVELCRBRCA    I#BRCA
     C                     Z-ADDCRMSGR    I#MSGR
     C                     Z-ADD0         I#PART
     C                     MOVEL'HDL'     I#RFMT
      *
     C                     CALL 'ME1140'               9090
     C                     PARM *BLANKS   W0RTN   7
     C                     PARM '*PRINT  'W0ACT   8
     C                     PARM           I#DTA            Input Data Str.
     C                     PARM *BLANKS   I#RSQN  5
      *
     C           *IN90     IFEQ '1'
     C           W0RTN     ORNE *BLANKS
     C                     MOVEL'*CALL'   W0FILE
     C                     MOVEL'ME1140'  W0KEY
     C                     Z-ADD2         W0ERNB
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXCALL    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SREND    : Call Print Process to close files                 *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SREND     BEGSR
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SREND '  @STK,Q
      *
      *  /Copy point for End of Report
      *
     C/COPY WNCPYSRC,ME1220DEND
      *
      *  Clear data structure and *CLOSE
      *
     C                     CLEARI#DTA
      *
     C                     CALL 'ME1140'               9090
     C                     PARM *BLANKS   W0RTN   7
     C                     PARM '*CLOSE  'W0ACT   8
     C                     PARM           I#DTA            Input Data Str.
     C                     PARM *BLANKS   I#RSQN  5
      *
     C           W0RTN     IFNE *BLANKS
     C                     MOVEL'*CALL'   W0FILE
     C                     MOVEL'ME1140'  W0KEY
     C                     Z-ADD3         W0ERNB
     C                     MOVEL'MEM5003' W0MSGD
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     END
      *
      *  Unwind subroutine stack name
      *
     C           EXEND     TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT   : Initialise and define fields                      *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *****************************************************************
     CSR         SRINIT    BEGSR
      *
      *  Set up copyright statement
      *
     C                     MOVEACPY@      BIS@   80
      *
      *  Set up subroutine stack name
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
      *
      *  Indicate that set up is complete
      *
     C                     SETON                     50
      *
      *  Open file MEINCRL4 if not already open
      *
     C           FIL001    IFEQ *BLANKS
     C                     OPEN MEINCRL4
     C                     MOVEL'Y'       FIL001  1
     C                     END
      *
      *  Get Rundate information
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDTA
     C                     IN   RUNDTA
     C                     MOVE AGMRDT    WUMRDT  7        Midas Run date
     C                     MOVE AGRDNB    WURDNB  50       Run day number
     C                     MOVE AGSUC     WUSUC   1        Set up complete
     C                     MOVE AGDFF     WUDFF   1        Date Format
     C                     MOVE AGMBIN    WUMBIN  1        Multi Branched
      *
      *  Unwind subroutine stack name
      *
     C           EXINIT    TAG
     C                     MOVEA*BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     CSR                   ENDSR
     C/EJECT
     C/COPY WNCPYSRC,ME1220DCPG
      *
      *  /Copy point for Calculations
      *
     C/EJECT
      *
      *  File and Program Error Processing
      *
     C/COPY MECPYSRC,SRERRC
     C/EJECT
     C/COPY WNCPYSRC,ME1220DOPG
      *
      *  /Copy point for Outputs
      *
**  CPY@ table
(c) Misys International Banking Systems Ltd. 2001
