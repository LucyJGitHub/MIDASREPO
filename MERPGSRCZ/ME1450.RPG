     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ME BIC plus installation routine')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Message Management Module                            *
     F*                                                               *
     F*  ME1450  - BIC Plus installation routine                      *
     F*                                                               *
     F*  Function - This program initialises the file trailers and    *
     F*             physical files when the switchable feature is     *
     F*             installed, reinstalled or uninstalled.            *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. CFT007  *CREATE    Date 22Nov99               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     F*  CFT007 - BIC Database Plus Integration                       *
     F*                                                               *
     F*****************************************************************
     FMEZIPPPAO   E           K        DISK                           UC
     F* ZIP placement and clearing code trailer
     FMEXPHRPAO   E           K        DISK                           UC
     F* Excluded phrases trailer
     F*
      /EJECT
      *
      *   INDICATOR USAGE
      *   ---------------
      *
      *   90 - 99 Used in validation/conversion routines (Z s/r's)
      *
      *   U7      Data base error
      *   U8      Data base error
      *
      /EJECT
      *
      *   SUB-ROUTINE DEFINITIONS
      *   -----------------------
      *
      *   INIT    Initialisation
      *   INSTAL  Install the switchable feature for the first time
      *   REINST  Reinstall the switchable feature
      *   UNINST  Uninstall the switchable feature
      *
      /EJECT
      *
     E                    CPY@    1   1 80
     E                    CLRP    1   7 21               CLRPFM Command
      /EJECT
      *
      * Data structure for compilation - Copyright insertion
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      /EJECT
      *****************************************************************
      *  MAIN PROCESSING
      *****************************************************************
      *
      * Initialise program
     C                     EXSR INIT
      *
     C                     SELEC
      *
      * The switchable feature is being installed for the first time
     C           @IMODE    WHEQ '1'
     C                     EXSR INSTAL
      *
      * The switchable feature is being reinstalled
     C           @IMODE    WHEQ '2'
     C                     EXSR REINST
      *
      * The switchable feature is being uninstalled
     C           @IMODE    WHEQ '3'
     C                     EXSR UNINST
      *
      * Invalid mode
     C                     OTHER
     C                     MOVE '*INVMOD' @RTCD
     C                     ENDSL
      *
      * Terminate program
     C                     SETON                     LR
     C                     RETRN
      /EJECT
      *****************************************************************
      *                                                               *
      *  INSTAL   : Install the switchable feature for the first time *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : *NONE                                             *
      *                                                               *
      *****************************************************************
     C           INSTAL    BEGSR
      *
     C                     OPEN MEZIPPPA
     C                     OPEN MEXPHRPA
      * Set the number of record of ZIP placement and clearing code to
      * zero
     C                     Z-ADD*ZERO     ZINORE
     C                     WRITE@ZIPPPA
      *
      *
      * Set the number of record of excluded phrases to zero
     C                     Z-ADD*ZERO     XPNORE
     C                     WRITE@XPHRPA
      *
     C                     CLOSEMEZIPPPA
     C                     CLOSEMEXPHRPA
      *
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  REINST   : Reinstall the switchable feature                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : UNINST                                            *
      *           : INSTAL                                            *
      *                                                               *
      *****************************************************************
     C           REINST    BEGSR
      *
      ** Uninstall feature
     C                     EXSR UNINST
      *
      ** Install feature
     C                     EXSR INSTAL
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      *  UNINST   : Uninstall the switchable feature                  *
      *                                                               *
      *  CALLED BY: Main processing                                   *
      *                                                               *
      *  CALLS    : *NONE                                             *
      *                                                               *
      *****************************************************************
     C           UNINST    BEGSR
      *
      ** Clear the ZIP placement and clearing code trailer table
     C                     CALL 'QCMDEXC'
     C                     PARM           CLRP,1
     C                     PARM           PRMLEN
      *
      ** Clear the Excluded phrases trailer table
     C                     CALL 'QCMDEXC'
     C                     PARM           CLRP,2
     C                     PARM           PRMLEN
      *
      ** Clear the BIC Database Plus table
     C                     CALL 'QCMDEXC'
     C                     PARM           CLRP,3
     C                     PARM           PRMLEN
      *
      ** Clear the BIC Country table
     C                     CALL 'QCMDEXC'
     C                     PARM           CLRP,4
     C                     PARM           PRMLEN
      *
      ** Clear the BIC search index table
     C                     CALL 'QCMDEXC'
     C                     PARM           CLRP,5
     C                     PARM           PRMLEN
      *
      ** Clear the ZIP placement and clearing prefix table
     C                     CALL 'QCMDEXC'
     C                     PARM           CLRP,6
     C                     PARM           PRMLEN
      *
      ** Clear the Excluded phrases table
     C                     CALL 'QCMDEXC'
     C                     PARM           CLRP,7
     C                     PARM           PRMLEN
      *
     C                     ENDSR
      *******************************************************************
      *  INIT  Program initialisation                                   *
      *******************************************************************
     CSR         INIT      BEGSR
      *
      *  Copyright
     C                     MOVEACPY@      BIS@   80
      *
      *  CLRPFM command length
     C                     Z-ADD21        PRMLEN 155
      *
      ** Entry Parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           @RTCD   7
     C                     PARM           @IMODE  1
      *
     CSR                   ENDSR                           INIT  ends
      *******************************************************************
      *
      /EJECT
** CPY@
(c) Finastra International Limited 2001
**  CLRP
CLRPFM FILE(MEZIPPPA)
CLRPFM FILE(MEXPHRPA)
CLRPFM FILE(MEBICPPD)
CLRPFM FILE(MEBICCPD)
CLRPFM FILE(MEBICIPD)
CLRPFM FILE(MEZIPPPD)
CLRPFM FILE(MEXPHRPD)
