     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ME BIC ZIP and clearing prefix list')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  ME1640 - Print ZIP Placement and Clearing Prefix List        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CFT007 *C *CREATE  Date 28Oct99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CFT007 - BIC Database Plus Integration                       *
      *                                                               *
      *****************************************************************
      **                FUNCTION OF INDICATORS
      **
      **  54      - Print '*** DIFFERENCE ***' legend
      **  70      - Page overflow
      **  82      - Access MEZIPPPD - data file
      **  83      - Access MEZIPPPA - trailer file
      **  98      - System date format
      **  U8      - File out of balance
      **  U7 U8   - Database error / database control error
      **  LR      - Last record on file
      **
      *==================================================================
     F/EJECT
      *
     FMEZIPPL0IF  E           K        DISK         KINFSR *PSSR
      * ZIP Placement and Clearing Code Prefix Logical
      *
     FMEZIPPPAIF  E                    DISK         KINFSR *PSSR
      * ZIP Placement and Clearing Code Prefix Trailer
      *
     FME1640P1O   E             70     PRINTER      KINFDS SPOOL      UC
      * ZIP Placement and Clearing Code Prefix List Printer File
      *
     FME1640AUO   E                    PRINTER      KINFDS SPOOLU
      * ZIP Placement and Clearing Code Prefix Audit Printer File
      *
      *
     E                    CPY@    1   1 80
      *
     ILDA         DS                            256
     I                                      134 183 DBLOT
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 183 DBASE
      * Local data area for database errors:
      *
     I           SDS
     I                                      244 246 WSID
     I                                      254 263 USID
      *
     ISDBANK    E DSSDBANKPD
      *  External DS for bank details
      *
     IDSFDY     E DSDSFDY
      * First DS for access programs, short data structure
      *
     ISPOOL       DS
     I                                       83  92 SFILE
     I                                    B 123 1240SFNUM
     ISPOOLU      DS
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *   Data structure for compilation  - copyright insertion:
      *
      * ================================================================
      * MAIN
      * ================================================================
      *
      * Define input parameters:
      *
     C           *ENTRY    PLIST
     C                     PARM           RSEQ    5
      *
      * Initialization:
      *
     C                     EXSR INIT
      *
      * Print data list
      *
     C                     EXSR PRINT
      *
      * Process audit:
      *
     C                     EXSR AUDIT
      *
      * ================================================================
      *            PROGRAM  END
      * ================================================================
      *
     C                     MOVE '1'       *INLR
      *
      * ================================================================
     C/EJECT
      * =================================================================
      *
      *  PRINT       Print file data
      *
      *     CALLED BY PROGRAM
      *     CALLS     FIELDS
      *
      * =================================================================
      *
     C           PRINT     BEGSR
      *
      * Print list header:
      *
      *                    MOVE '1'       *IN42
      *
      * Access data file
      *
     C                     MOVE *LOVAL    WKCNTY
     C           WKCNTY    SETLLMEZIPPL0
      *
     C                     READ @ZIPPPD                  82
     C           *IN82     IFEQ '0'
     C                     ELSE
     C                     MOVE '1'       *IN88
     C                     GOTO END02
     C                     END
      *
      * So long as end of file not reached
      *
     C           *IN82     DOWEQ'0'
      *
      * Execute page overflow
      *
     C           *IN70     IFEQ '1'
     C                     WRITEHEADLI
     C                     MOVE '0'       *IN70
     C                     END
      *
      * For each live record read
      *
      *
     C           WWFLAG    IFEQ *BLANK
      *
     C                     MOVE 'Y'       WWFLAG
     C                     OPEN ME1640P1
      *
      * Ensure report spool file recorded by RCF
      *
     C                     Z-ADDSFNUM     ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ     5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILE
     C                     PARM           ZSNUM
     C                     PARM           ZSERR   1
      *
     C           ZSERR     IFEQ 'Y'
      *
      * Error during ZSFILE processing, return to calling program
      *
     C           *LOCK     IN   LDA
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
     C                     WRITEHEADLI
     C                     END
      *
      * Increment record count
      *
     C                     ADD  1         WWNORE
      *
      * Transfer file and work fields to printer fields
      *
     C                     EXSR FIELDS
      *
      * Print list
      *
     C                     WRITELIST
      *
      * Access data file again
      *
     C                     READ MEZIPPL0                 82
     C                     END
      *
      * Print list end
      *
     C           WWFLAG    IFNE *BLANK
     C                     WRITEENDLI
     C                     CLOSEME1640P1
     C                     END
      *
     C           END02     ENDSR
      *
      * =================================================================
     C/EJECT
      * =================================================================
      *
      *  AUDIT       Chain on trailer file.
      *
      *     CALLED BY PROGRAM
      *     CALLS     REPORT, *PSSR
      *
      * =================================================================
      *
     C           AUDIT     BEGSR
      *
      *   Access trailer file
      *
     C                     READ MEZIPPPA                 83
      *
     C           *IN83     IFEQ '0'
      *
      *   Print audit report
      *
     C                     EXSR REPORT
     C                     ELSE
      *
      * Audit record not found, database error
      *
     C           *LOCK     IN   LDA
     C                     MOVEL'03'      DBASE
     C                     MOVEL'MEZIPPPA'DBFILE
     C                     MOVEL'AUDIT'   DBKEY
     C                     OUT  LDA
      *
     C                     EXSR *PSSR
     C                     END
      *
     C                     ENDSR
      *
      * =================================================================
     C/EJECT
      * =================================================================
      *
      *  FIELDS      Transfer file and work fields to printer fields
      *
      *     CALLED BY PRINT
      *     CALLS     -
      *
      * =================================================================
      *
     C           FIELDS    BEGSR
      *
     C                     MOVE ZICNTY    RRCNTY
     C                     MOVE ZIZLST    RRZLST
     C                     MOVE ZICPFX    RRCPFX
      *
     C                     ENDSR
      *
      * =================================================================
     C/EJECT
      * =================================================================
      *
      *  REPORT      Print audit report
      *
      *     CALLED BY AUDIT
      *     CALLS     -
      *
      * =================================================================
      *
     C           REPORT    BEGSR
      *
      * If live record count not equal to trailer record
      *
     C           WWNORE    IFNE ZINORE
      *
      * Print '*** DIFFERENCE ***'
      *
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *IN54
     C                     END
      *
     C                     Z-ADDWWNORE    R1NORE
     C                     Z-ADDZINORE    R2NORE
      *
      * Print audit report
      *
      *                    MOVE '1'       *IN42
     C                     WRITEHEADAU
     C   88                WRITENODETAIL
      *
     C                     ENDSR
      *
      * =================================================================
     C/EJECT
      * =================================================================
      *
      *   INIT       Initialize static fields and access bank record
      *
      *      CALLED BY PROGRAM
      *      CALLS     *PSSR
      *
      * =================================================================
      *
     C           INIT      BEGSR
      *
      * Copyright Statement.
      *
     C                     MOVEACPY@      ACT@   80
      *
      * Prepare LDA:
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'ME1640'  DBPGM
     C                     OUT  LDA
      *
      * Initialize country work field
      *
     C           *LIKE     DEFN ZICNTY    WKCNTY
      *
      * Initialize record count
      *
     C                     Z-ADD0         WWNORE  50
      *
      * Initialize indicators:
      *
     C                     MOVE '0'       *IN
      *
      * Initialize flag field
      *
     C                     MOVE *BLANK    WWFLAG  1
      *
      * Get installation control data record
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WWRTCD  7
     C                     PARM '*FIRST ' WWOPTN  7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      * If record found:
      *
     C           WWRTCD    IFEQ *BLANKS
      *
      * Check system data format (DDMMYY OR MMDDYY):
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     END
     C                     ELSE
      *
      * Otherwise DB error:
      *
     C           *LOCK     IN   LDA
     C                     MOVEL'01'      DBASE
     C                     MOVE 'SDBANKPD'DBFILE
     C                     MOVEL' NONE '  DBKEY
     C                     OUT  LDA
      *
     C                     EXSR *PSSR
     C                     END
     C                     MOVE RSEQ      SEQ
      *
      * Ensure audit spool file recorded by RCF
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
     C           ZSERR     IFEQ 'Y'
      *
      * Error during ZSFILE processing, return to calling program
      *
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
      *
     C                     ENDSR
      *
      * =================================================================
     C/EJECT
      * =================================================================
      *
      *   *PSSR       OUTPUT DATABASE ERROR.
      *
      *      CALLED BY PROGRAM, INIT, AUDIT
      *      CALLS     -
      *
      * =================================================================
      *
     C           *PSSR     BEGSR
      *
     C           WWERRO    IFEQ ' '
     C                     MOVE 'Y'       WWERRO  1
      *
     C           DBASE     IFNE '000'
      *
      *   Print error message
      *
     C                     MOVE '1'       *IN41
     C                     WRITEHEADAU
     C                     WRITEERRORAU
     C                     ELSE
      *
     C                     MOVE '999'     DBASE
     C                     END
      *
     C                     DUMP
     C                     END
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      * =================================================================
     C/EJECT
      * =================================================================
**  CPY@
(c) Finastra International Limited 2001
