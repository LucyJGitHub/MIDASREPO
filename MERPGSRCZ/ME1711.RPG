     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ME Bilateral Agreements List Report')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  ME1711 - Bilateral Agreements List Report                    *
      *                                                               *
      *  Function:  This program will list details of each of the     *
      *  (I/C)      Bilateral Agreements that have been defined       *
      *             to the system at the time of request.             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CFT014             Date 25May00               *
      *                 CFT013  *CREATE    Date 25May00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CFT014 - Straight-through Processing Phase 2 MT103 Messages  *
      *           Generation for FT.                                  *
      *  CFT013 - Straight-through Processing Phase 2 MT103 Messages  *
      *           Generation for non-FT.                              *
      *                                                               *
      *****************************************************************
     FMEBAGRL1IF  E           K        DISK
     FME1711AUO   E                    PRINTER
     F                                              KINFDS SPOOLU
     FME1711P1O   E                    PRINTER                        UC
     F                                              KINFDS SPOOL1
     F            ME1711F1                          KRENAMEME1711R1
     F            ME1711F2                          KRENAMEME1711R2
     F            ME1711F3                          KRENAMEME1711R3
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *   37  - Multibranching ON                                     *
      *   60  - Read Indicator for MEBAGRL1                           *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRINIT - Program Initialisation                              *
      *  SRPROC - Details Processing                                  *
      *  SRREPT - Process Report Lines                                *
      *                                                               *
      *  SP1REC - Format and Write a Detail Record to the Report      *
      *  SP1END - Write End of Report                                 *
      *                                                               *
      *  SRAUDT - Produce Audit Report and End Program                *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  SRCFP1 - RCF Processing for P1 Report                        *
      *  SRCFAU - RCF Processing for Audit Report                     *
      *                                                               *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      *
     IRUNDAT    E DSRUNDAT
      *
      ** Data Area giving Rundate
      *
     IPSDS       SDS
      *
      ** Program Status Data Structure
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
     ISPOOL1      DS
      *
      ** File Information Data Structure for M17019P1.
      *
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      *
      ** File Information Data Structure for ME1711AU.
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     IDSFDY     E DSDSFDY
      *
      **  First DS for access programs, short data structure
      *
     ISDBANK    E DSSDBANKPD
      *
      ** External data structures for Bank Details
      *
     C/TITLE Main Processing
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Perform Initialisation.
      *
     C                     EXSR SRINIT
      *
      ***  Perform Detail Processing.
      *
     C                     EXSR SRPROC
      *
      ***  Output Audit Report and End Program.
      *
     C                     EXSR SRAUDT
      *
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    :                                                   *
      *  SRCFAU - RCF Processing for Audit Report                     *
      *  AOBANKR0 Access Program                                      *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
     C                     MOVE USER      RUSER
      *
      ** Receive Parameter List.
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
      *
      ** Initialise LDA field.
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'ME1711'  DBPGM
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     Z-ADD0         DBASE
     C                     OUT  LDA
      *
      ***  Call Access Program for Bank Details - Title, Run Date and
      ***  Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ***  Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'*FIRST'  DBKEY
     C                     MOVE 'SDBANKPD'DBFILE
     C                     Z-ADD1         DBASE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Check System Date Format, DDMMYY (*IN98 off)
      **                           MMDDYY (*IN98 on).
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ** Access RUNDAT for multibranching indicator
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
      *
     C           AGMBIN    IFEQ 'M'
     C                     MOVE *ON       *IN37
     C                     END
      *
      ** RCF Processing for Audit File.
      *
     C                     EXSR SRCFAU
      *
      ** Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
      *
      ** Register GL3300P1 to RCF
      *
     C                     CLOSEME1711P1
     C                     OPEN ME1711P1
     C                     EXSR SRCFP1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRPROC
      *****************************************************************
      *                                                               *
      *  SRPROC - Subroutine to do the Detail Processing.             *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls    :                                                   *
      *  SRAUDT - Produce Audit Report and End Program                *
      *  SRREPT - Process Report Lines                                *
      *  SRBRCH - Process Branch Details                              *
      *  SRCFP1 - RCF Processing for P1 Report/s                      *
      *  SP1END - Write End of Report                                 *
      *                                                               *
      *****************************************************************
      *
     C           SRPROC    BEGSR
      *
     C           *LOVAL    SETLLMEBAGRL1
      *
      ** Read Bilateral Agreements Type Logical File
      *
     C                     READ MEBAGRL1                 60
      *
      ** If end of records, generate audit reporting to report
      ** no records found.
      *
     C           *IN60     IFEQ *ON
     C                     EXSR SRAUDT
     C                     ELSE
      *
     C           *IN60     DOWEQ*OFF
      *
      ** RCF Processing for Printer File.
      *
     C                     ADD  1         RCOUNT  50
      *
      ** Process Report Lines.
      *
     C                     EXSR SRREPT
      *
      ** Read next record.
      *
     C                     READ MEBAGRL1                 60
      *
      ** End of Do While not End of File.
      *
     C                     ENDDO
      *
     C                     ENDIF
      *
      ** Write final records and Totals to Report.
      *
     C                     EXSR SP1END
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRREPT
      *****************************************************************
      *                                                               *
      *  SRREPT - Process Report Lines.                               *
      *                                                               *
      *  Called By: SRPROC                                            *
      *  Calls    :                                                   *
      *  SP1REC - Format and Write a Detail Record to the Report      *
      *                                                               *
      *****************************************************************
      *
     C           SRREPT    BEGSR
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
      *
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEME1711R1
     C                     ENDIF
      *
     C                     EXSR SP1REC
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SP1REC
      *****************************************************************
      *                                                               *
      *  SP1REC - Subroutine to Write a Detail record to the Report.  *
      *                                                               *
      *  Called By: SRPROC                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SP1REC    BEGSR
      *
      ** Bilateral Agreeement Type
      *
     C                     MOVE M2BATY    RM2BTY
      *
      ** Branch
      *
     C                     MOVE M2BRCA    RM2BRC
      *
      ** Other Party
      *
     C                     MOVE M2OTHP    RM2OTH
      *
      ** Bilateral Agreeement Type Description
      *
     C                     MOVE M2BAGD    RM2BGD
      *
      ** Write out Detail Record.
      *
     C                     WRITEME1711R2
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SP1END
      *****************************************************************
      *                                                               *
      *  SP1END - Subroutine to Write End of Report.                  *
      *                                                               *
      *  Called By: SRPROC                                            *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SP1END    BEGSR
      *
      ** Check that sufficient lines remain for the Format. If not,
      ** write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEME1711R1
     C                     ENDIF
      *
      ** Write out Total Format.
      *
     C                     WRITEME1711R3
      *
     C                     CLOSEME1711P1
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRAUDT
      *****************************************************************
      *                                                               *
      *  SRAUDT - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, SRPROC, *PSSR                    *
      *  Calls    : None                                              *
      *                                                               *
      *****************************************************************
      *
     C           SRAUDT    BEGSR
      *
      ** Display user
      *
     C                     MOVELUSER      RUSER
      *
      **  Output Report Header and File Controls.
      *
     C                     WRITEME1711F1
      *
      ** If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEME1711F4
     C                     ELSE
      *
      ** If no records read, output no details to report.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITEME1711F3
     C                     ELSE
      *
      ** Output no. of records processed.
      *
     C                     MOVELRCOUNT    RPROC
     C                     WRITEME1711F2
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** End Program and Return.
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/*PSSR
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
      *
      ** Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     EXSR SRAUDT
     C                     DUMP
     C                     ENDIF
      *
      ** If *PSSR already run, then just end the program here.
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRCFP1
      *****************************************************************
      *                                                               *
      *  SRCFP1 - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           SRCFP1    BEGSR
      *
      ** Ensure Report Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ     5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM *BLANKS   ZSERR   1
      *
      ** If Error occurs during ZSFILE processing, then return to the
      ** Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /TITLE SR/SRCFAU
      *****************************************************************
      *                                                               *
      *  SRCFAU - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
      *
     C           SRCFAU    BEGSR
      *
      ** Ensure Audit Spool File recorded by RCF.
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM PSEQ      SEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
