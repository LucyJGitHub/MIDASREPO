     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ME Codeword check')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  ME9910 - Codeword Check                                      *
      *                                                               *
      *  Function:  This program checks the syntax of SWIFT field     *
      *             :72: codewords.                                   *
      *                                                               *
      *  Called By: SD2501R - SWIFT codewords maintenance             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. CSW201             Date 02May01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW096 *CREATE     Date 04Jun96               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *           (Recompile)                                         *
      *  CSW201 - SWIFT 2001 Standards Update                         *
      *  CSW096 - SWIFT Changes November 1996                         *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *   Indicator Usage                                             *
      *   ---------------                                             *
      *                                                               *
      *   50        Work                                              *
      *                                                               *
      *****************************************************************
      *                                                               *
      *   Notes                                                       *
      *   -----                                                       *
      *                                                               *
      *   1. This program checks the syntactic validity of SWIFT      *
      *      field :72: codewords passed by the standard codewords    *
      *      maintenance function.                                    *
      *                                                               *
      *   2. If the codeword is valid this program acknoledges the    *
      *      fact by sending a message to itself which may be seen in *
      *      the joblog.                                              *
      *                                                               *
      *   3. If the codeword is invalid this program will send an     *
      *      error message to the calling program's message queue,    *
      *      and set up the error status in a return code.            *
      *                                                               *
      *****************************************************************
      /EJECT
      ** Standard F-spec /COPY
     F/COPY WNCPYSRC,ME9910DFPG
      /EJECT
      ** Standard copyright statement
     E                    CPY@    1   1 80
      ** Standard error processing E-spec /COPY
     E/COPY MECPYSRC,SRERRE
      ** Standard E-Spec /COPY
     E/COPY WNCPYSRC,ME9910DEPG
      /EJECT
      ** Standard I-spec /COPY
     I/COPY WNCPYSRC,ME9910DIPG
      ** Standard error processing I-spec /COPY
     I/COPY MECPYSRC,SRERRI
      *
     ICODEDS    E DSSDCODEPD
      ** Record structure for SWIFT Code words
      *
     I              'ABCDEFGHIJKLMNOPQRST-C         W@ALPH
     I              'UVWXYZ'
      ** Set of upper-case alphabetic characters
      *
     I              'ABCDEFGHIJKLMNOPQRST-C         WALPHN                                    CSW201
     I              'UVWXYZ0123456789'                                                        CSW201
      ** Set of upper-case alphabetic characters and numeric digits                           CSW201
      *                                                                                       CSW201
      /EJECT
     C           *ENTRY    PLIST
     C                     PARM           P0RTN   7
     C                     PARM           W0CPGM 10        I Calling Program
     C                     PARM           CODEDS115        RCD: I Codewords
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      CPY2@  80
      *
      ** If codeword is not '*ALL', perform validation
     C           FYSWCD    IFNE '*ALL'
      *
      ** First character must be '/'
     C                     MOVELFYSWCD    W@SLSH  1
     C           W@SLSH    IFNE '/'
     C                     MOVEL'MEM9901' P0RTN
     C                     EXSR SRINVL
     C                     RETRN
     C                     ENDIF
      *
      ** check for presence and position of terminating '/'; W@ is
      ** position of second '/'
     C           '/'       SCAN FYSWCD:2  W@      50     50
      *
      ** If second '/' not found - error: codeword must end '/'
     C           *IN50     IFEQ '0'
     C                     MOVEL'MEM9902' P0RTN
     C                     EXSR SRINVL
     C                     RETRN
     C                     ENDIF
      *
      ** If '/' is in posn. 2 - error: '//' invalid for codeword
     C           W@        IFEQ 2
     C                     MOVEL'MEM9903' P0RTN
     C                     EXSR SRINVL
     C                     RETRN
     C                     ENDIF
      *
      ** If '/' is in posn. 11 or greater - error: codeword should be
      ** 8 characters or fewer.
     C           W@        IFGT 10
      *                                                                                       CSW201
     C           CSW201    IFEQ 'Y'                                                           CSW201
     C                     MOVEL'MEM9913' P0RTN                                               CSW201
     C                     EXSR SRINVL                                                        CSW201
     C                     RETRN                                                              CSW201
     C                     ELSE                                                               CSW201
     C                     MOVEL'MEM9904' P0RTN
     C                     EXSR SRINVL
     C                     RETRN
     C                     ENDIF                                                              CSW201
      *                                                                                       CSW201
     C                     ENDIF
      *
      ** If any characters after second '/' - error: should be blank
     C           20        SUB  W@        W@L     50
     C           W@        ADD  1         W@P     50
     C           W@L       SUBSTFYSWCD:W@PW@SBBL 12 P
     C           W@SBBL    IFNE *BLANKS
     C                     MOVEL'MEM9905' P0RTN
     C                     EXSR SRINVL
     C                     RETRN
     C                     ENDIF
      *                                                                                       CSW201
      ** If codeword contains non-upper case or non-numeric characters                        CSW201
      *                                                                                       CSW201
     C           CSW201    IFEQ 'Y'                                                           CSW201
      *                                                                                       CSW201
     C           WALPHN    CHECKFYSWCD:2  W@L                                                 CSW201
     C           W@L       IFLT W@                                                            CSW201
     C                     MOVEL'MEM9914' P0RTN                                               CSW201
     C                     EXSR SRINVL                                                        CSW201
     C                     RETRN                                                              CSW201
     C                     ENDIF                                                              CSW201
      *                                                                                       CSW201
     C                     ELSE                                                               CSW201
      *
      ** If codeword contains non-upper case alpha characters - error:
      ** codeword should be SWIFT 8a.
     C           W@ALPH    CHECKFYSWCD:2  W@L
     C           W@L       IFLT W@
     C                     MOVEL'MEM9906' P0RTN
     C                     EXSR SRINVL
     C                     RETRN
     C                     ENDIF
      *                                                                                       CSW201
     C                     ENDIF                                                              CSW201
      *
      ** (codeword is '*ALL')
     C                     ENDIF
      *
      ** Valid, so send message to ourselves indicating all OK.
     C                     MOVEL'MEM9900' MEMSID
     C                     CLEARP0RTN
     C                     CLEARMEPGMQ
     C                     EXSR MESNMS
      *
     C                     RETRN
      /EJECT
      *****************************************************************
      * Subroutine  :  SRINVL                                         *
      * Purpose     :  Process invalid code                           *
      *                                                               *
      * Called by   :  Main process                                   *
      * Calls       :  MESNMS                                         *
      *****************************************************************
      *
     C           SRINVL    BEGSR
      *
      ** Send message indicated in return code to calling program
     C                     MOVELP0RTN     MEMSID
     C                     MOVELW0CPGM    MEPGMQ
     C                     EXSR MESNMS
      *
     C                     ENDSR                           SRINVL
      *
      /EJECT                                                                                  CSW201
      *****************************************************************                       CSW201
      * Subroutine  :  *INZSR                                         *                       CSW201
      * Purpose     :  Subroutine to do Initialisation                *                       CSW201
      *                                                               *                       CSW201
      * Called by   :  Automatically                                  *                       CSW201
      * Calls       :  SWIF2001                                       *                       CSW201
      *****************************************************************                       CSW201
      *                                                                                       CSW201
     C           *INZSR    BEGSR                                                              CSW201
      *                                                                                       CSW201
      ** Check if CSW201 is switched on                                                       CSW201
      *                                                                                       CSW201
     C                     CALL 'SWIF2001'                                                    CSW201
     C                     PARM           P@RTCD  7                                           CSW201
      *                                                                                       CSW201
     C           P@RTCD    IFEQ 'CSW2001'                                                     CSW201
     C                     MOVE 'Y'       CSW201  1                                           CSW201
     C                     ELSE                                                               CSW201
     C                     MOVE 'N'       CSW201                                              CSW201
     C                     ENDIF                                                              CSW201
      *                                                                                       CSW201
     C                     ENDSR                                                              CSW201
      *                                                                                       CSW201
      ********************************************************************
      /EJECT
      * Standard send-program-message routine /COPY
     C/COPY MECPYSRC,SMESNMSC
      * Standard error processing I-spec /COPY
     C/COPY MECPYSRC,SRERRC
      * Standard C-spec /COPY
     C/COPY WNCPYSRC,ME9910DOPG
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
