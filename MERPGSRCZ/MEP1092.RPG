     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*OVRF*: OVRDBF (FILE IN PROGRAM) (FILE ON SYSTEM)                  : *
/*EXI *  TEXT('Midas ME  Check if the message is from Ebanking')      *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  MEP1092 - Check if the message is from Ebanking              *
      *            and if it can be auto authorised.                  *
      *                                                               *
      *  Function:  This check function accepts field 59              *
      *             (Beneficiary) and does folowing checks to see     *
      *             EBanking payments can be auto authorised.         *
      *             * Charge structure is setup for the message type  *
      *             * The IBAN is not on a blocked account list       *
      *             * The Payment amount is within limit              *
      *                                                               *
      *             In-house program does all the checks and decides  *
      *             if payment can be auto authorised                 *
      *                                                               *
      *  Last Amend No. LUC111   *CREATE   Date 21Nov2017             *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC111 - STP changes for in-house eBanking payment           *
      *         - Enhance to process message from other Ebanking      *
      *           source (e.g. EGate) and different payment type.     *
      *                                                               *
      *****************************************************************
      *
     E                    CPY@    1   1 80
      *
      ** Array containing Copyright statement
      *
     ICPYR@#      DS
      *
      *  Data structure for compilation  - Copyright insertion
      *
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      *
      /COPY MECPYSRC,SME1100P
      *
      *  Data Structures used by Access Objects
      *
     IPCRIT     E DSMEGNCRPD
      *
      *  Return Criteria Details to calling program
      *
     I            DS
      *
      *  Split the detail fields into field tag name and contents
      *
     I                                        1 256 WUFDDT
     I                                        1   5 WUMTAG
     I                                        6   6 WUFDIN
     I                                        6 256 WUTGCK
     I                                       31  36 W#DATE
     I                                       37  39 W#CCY
     I                                       40  57 W#AMT
     I                                       58 256 REST
      *
     IW#P23       DS
     I                                        1   5 WTAG1
     I                                       31  80 W#P23E
      *
      *
      *
      *  Set up copyright statement
      *
     C                     MOVEACPY@      BIS@   80
      *
     C           *LIKE     DEFN WUTGCK    P@IN
      *
     C           *ENTRY    PLIST
     C                     PARM           P#RETN  7
     C           WUFDDT    PARM           P@IN
     C                     PARM           P#PTTN 32
     C                     PARM           GDOPER  2
     C                     PARM           PHEAD            General Details
     C                     PARM           PDATA            Message Details
     C                     PARM           PDAT2            Message Details
     C                     PARM           PCRIT
      * If true put T
     C                     MOVEL'F'       P#RETN
      *
      *** Reset working fields
     C                     MOVE *BLANKS   W#P23
      *
     C           PMTPY     IFEQ 'IN3'
     C           PMTPY     OREQ '103'
      *** Reset working fields
      *
     C                     MOVELP23E      W#P23
     C                     MOVEL'IN'      W#OPIN  2        Payment Type
      *
      ** Call In-house program
      ** Field 23E is used to determin if charges structure for the message type is in place
      ** Field 32A is used to check if the pay amount is within set limit
     C                     CALL 'CM1221'               9090
     C                     PARM *BLANKS   W#RTN   7        Return code
     C                     PARM           W#CCY            Pay Currency
     C                     PARM           W#AMT            Pay Amount
     C                     PARM           W#P23E           Instruction Code
     C                     PARM           W#OPIN           Pay Type
     C                     PARM *BLANKS   W#AUT   1        Auto Authorise
      *
      *
     C           W#AUT     IFEQ 'Y'
     C                     MOVEL'T'       P#RETN
     C                     ENDIF
      *
      *
     C                     ENDIF
      *
      *  Exit program
     C                     RETRN
**  CPY@ table
(C) Copyright Misys International Banking Systems Ltd. 2016
