     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas ME Check if SWIFT 2005 is active')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  SWIF2005 - SWIFT 2005 Active?                                *
      *                                                               *
      *  Function:  This program will ascertain whether SWIFT 2005    *
      *             enhancement is on.  It is active if switchable    *
      *             feature CSW205 is installed or the run date       *
      *             is greater than or equal to 14th May 2005.        *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSW205  *CREATE    Date 30Nov04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW205 - SWIFT 2005 Standards                                *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      *****************************************************************
      /SPACE 3
      *****************************************************************
     IRUNDTA    E DSRUNDAT
      ** Data Area giving Installation Control Details
      *
     ISCSARD    E DSSCSARDPD
      ** SAR Descriptions
      *
     IDSFDY     E DSDSFDY
      ** Data Structures used by Access Programs
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
     C           *ENTRY    PLIST
     C                     PARM           PRTCD   7
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C           WFIRST    IFNE 'N'
     C                     EXSR SRINIT
     C                     ENDIF
      *
     C                     MOVE WRTCD     PRTCD
      *
     C                     RETRN
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SRINIT - Initialisation Subroutine                            *
      *                                                               *
      * Called by: Main Processing                                    *
      *                                                               *
      * Calls: None                                                   *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
     C                     MOVE 'N'       WFIRST  1
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDTA
     C                     IN   RUNDTA
     C                     MOVE AGRDNB    WURDNB  50
      *
      ** Access AOSARDR0 to check if CSW205 is switched on
      *
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   PRTCD
     C                     PARM '*VERIFY' POPTN   7
     C                     PARM 'CSW205'  PSARD   6
     C           SCSARD    PARM SCSARD    DSFDY
      *
     C           PRTCD     IFEQ *BLANKS
     C           WURDNB    ORGE 12188
     C                     MOVE 'CSW2005' WRTCD   7
     C                     ELSE
     C                     MOVE *BLANKS   WRTCD
     C                     ENDIF
      *
     C                     ENDSR
      *
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2004
