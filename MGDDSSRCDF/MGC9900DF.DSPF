      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MG Routing Information test')
      *****************************************************************
      *                                                               *
      *  Midas - Message Generation                                   *
      *                                                               *
      *  DSPF/MGC9900DF - Test Routing Information                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CSW096             Date 05JUN96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAAA02 - Remove use of INVITE keyword to enable WebFacing    *
      *           to process screens.                                 *
      *           INVITE is in this DSPF but is not used.             *
      *  CSW096 - SWIFT 96 Changes                                    *
      *                                                               *
      *****************************************************************
      *================================================================
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A          R #RCDKEY                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      KEEP
      ** Start of CAAA02
     A******************                    INVITE
      ** End of CAAA02
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(31 'Invalid: #1BRCA')
     A                                      SETOF(32 'Invalid: #1DSCN')
     A                                      SETOF(33 'Invalid: #1MTPY')
     A                                      SETOF(34 'Invalid: #1SETT')
     A                                      SETOF(35 'Invalid: #1CONC')
     A                                      SETOF(36 'Invalid: #1FTCS')
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'MGC9900F1'
     A                                      DSPATR(HI)
     A                                  1 26'            Midas            '
     A            ##MRDT         7      1 72TEXT('Midas run date')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##ONAM        40      2 21TEXT('Option Name')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 72TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A* Fields .................................................................
     A                                  6  2'Sender''s Branch .  . :'
     A            #1BRCA         3A  B  6 27TEXT('Sender''s branch')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A N31                                  DSPATR(UL)
     A N31                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  6 41'Midas Module .  .  . :'
     A            #1MMOD         2A  B  6 65TEXT('Midas Module')
     A N25                                  OVRDTA
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A N37                                  DSPATR(UL)
     A N37                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  7  2'Destination Customer :'
     A            #1DSCN         6A  B  7 27TEXT('estination Customer')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N33                                  DSPATR(UL)
     A N33                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  8  2'Message type . . . . :'
     A            #1MTPY         3A  B  8 27TEXT('Message type')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(UL)
     A N32                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                  9  2'Settlement Method. . :'
     A            #1SETT         2A  B  9 27TEXT('Settlement method')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A N34                                  DSPATR(UL)
     A N34                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 10  2'ConCord (Y/N). . . . :'
     A            #1CONC         1A  B 10 27TEXT('ConCord')
     A N25                                  OVRDTA
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A N35                                  DSPATR(UL)
     A N35                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 11  2'Default Status (C/R) :'
     A            #1FTCS         1A  B 11 27TEXT('Default Status')
     A N25                                  OVRDTA
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A N36                                  DSPATR(UL)
     A N36                                  DSPATR(HI)
     A N25                                  OVRATR
     A                                 13  2'BICN of Sender . . . :'
     A            #1CNSN         6A    13 27TEXT('BICN of Sender')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 13 41'Parent indicator . . :'
     A            #1PAIN         1A    13 65TEXT('Parent indicator')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14  2'Sender''s TID . . . . :'
     A            #1SWSN        12A    14 27TEXT('Sender''s TID')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 14 41'Parent customer  . . :'
     A            #1PCNB         6A    14 65TEXT('Parent customer')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 15  2'Sender''s Network Addr:'
     A            #1NWSN        20A    15 27TEXT('Sender''s Network Addr')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 16  2'Destination BIC . . .:'
     A            #1SWDS        11A    16 27TEXT('Destination BIC')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 17  2'Destination Network A:'
     A            #1NWDS        20A    17 27TEXT('Destination Network Addr')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 18  2'Network . . . . . . .:'
     A            #1NWRK         6A    18 27TEXT('Network')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 19  2'Msg Status Group. . .:'
     A            #1MGSG         1A    19 27TEXT('Message Status Group')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 19 41'Suppress msg ? . . . :'
     A            #1SPRS         3A    19 65TEXT('Suppress Msg ?')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 20  2'Message Status p. . .:'
     A            #1MGST         4A    20 27TEXT('Message Status')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #CMDTXT                   TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A            ##CMD1        78     22  2TEXT('Command Line 1')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            ##CMD2        78     23  2TEXT('Command Line 2')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
