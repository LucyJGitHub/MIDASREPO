      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas MG MT515 and MT518 Message Extract')             *
      *****************************************************************
      *                                                               *
      *  Midas - Message Generation Module                            *
      *                                                               *
      *  PF/MGF51NPD - MT515 and MT518 Message Extract                *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CSW210             Date 04May10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      *                 234140             Date 26Sep06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSW025             Date 26Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSE023             Date 24Jul00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE006             Date 20Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW098  *CREATE    Date 08May98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW210 - SWIFT 2010 Changes                                  *
      *  234140 - EU witholding tax                                   *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW025 - Midas Message Manager                               *
      *           - Pass original change type                         *
      *  CSE023 - Treaty Withholding Tax.                             *
      *  CSE006 - Repurchase Agreements (REPOS) enhancement:          *
      *           New fields-Maturity date of the Repo                *
      *                     -Repo Rate                                *
      *                     -Associated Deal Number                   *
      *  CSw098 - SWIFT 1998 changes.                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R MGF51ND0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            MGMTYP    R               REFFLD(MTPY)
     A                                      TEXT('Message Type')
     A                                      COLHDG('Message Type')
      * Begin /CSD027/
     A**********  MGDEST    R               REFFLD(M5DEST)                                    CSD027
     A            MGDEST         6A
      * End /CSD027/
     A                                      TEXT('Destination customer')
     A                                      COLHDG('Destination customer')
     A            MGTNUM    R               TEXT('Transaction Reference No.')
     A                                      COLHDG('Transaction'+
     A                                             'Ref No.')
     A            MGMODI    R               REFFLD(MODI)
     A                                      TEXT('Module ID')
     A                                      COLHDG('Module ID')
     A            MGTTYP    R               REFFLD(TNTP)
     A                                      TEXT('Transaction Type')
     A                                      COLHDG('Transaction Type')
     A            MGTSTP    R               REFFLD(SBTP)
     A                                      TEXT('Transaction Sub-type')
     A                                      COLHDG('Transaction Sub-type')
     A            MGSNBR    R               REFFLD(SNBR)
     A                                      TEXT('Branch Code of Sender')
     A                                      COLHDG('Branch Code'+
     A                                             'Sender')
     A            MGSETP    R               REFFLD(STLT)
     A                                      TEXT('Settlement Type')
     A                                      COLHDG('Settlement Type')
     A            MGVDAT    R               REFFLD(VUDT)
     A                                      TEXT('Value Date')
     A                                      COLHDG('Value Date')
     A            MGTDAT    R               REFFLD(TDDT)
     A                                      TEXT('Trade Date')
     A                                      COLHDG('Trade Date')
     A            MGTRTT         6          TEXT('Trade Time')
     A                                      COLHDG('Trade Time')
     A            MGPRIC    R               REFFLD(BBPT)
     A                                      TEXT('Price')
     A                                      COLHDG('Price')
     A            MGSTBS    R               REFFLD(STBS)
     A                                      TEXT('Trade Basis')
     A                                      COLHDG('Trade Basis')
     A            MGSPBS    R               REFFLD(BQPB)
     A                                      TEXT('Price Basis')
     A                                      COLHDG('Price Basis')
     A            MGEXIN    R               REFFLD(INDA)
     A                                      TEXT('Cum/Ex Indicator')
     A                                      COLHDG('Cum/Ex Indicator')
     A            MGACND    R               REFFLD(BINA)
     A                                      TEXT('No. of days accrued')
     A                                      COLHDG('No. of days accrued')
     A            MGITRA    R               REFFLD(CITM)
     A                                      TEXT('Interest Amount')
     A                                      COLHDG('Interest Amount')
     A            MGSTCY    R               REFFLD(M5STCY)
     A                                      TEXT('Settlement Currency')
     A                                      COLHDG('Settlement Currency')
     A            MGSTAM    R               REFFLD(SETAM)
     A                                      TEXT('Settlement Amount')
     A                                      COLHDG('Settlement Amount')
     A            MGRESU    R               REFFLD(SETAM)
     A                                      TEXT('Settlement Amt in +
     A                                      Alternate Currency')
     A                                      COLHDG('Setl Amt in Alt Ccy')
     A            MGPCOD    R               REFFLD(PCOD)
     A                                      TEXT('Pay Code')
     A                                      COLHDG('Pay Code')
      * Begin /CSD027/
     A**********  MGINVE    R               REFFLD(ASOC)                                      CSD027
     A            MGINVE         6A
      * End /CSD027/
     A                                      TEXT('Investor')
     A                                      COLHDG('Investor')
     A            MGEUCL    R               REFFLD(M5EUCL)
     A            MGSA1L    R               REFFLD(ACB1)
     A                                      TEXT('Safekeeping Account')
     A                                      COLHDG('Safekeeping Account')
     A            MGAP1L    R               REFFLD(ACB1)
     A                                      TEXT('Account for Payment')
     A                                      COLHDG('Account for Payment')
     A            MGPROT    R               REFFLD(BBTV)
     A                                      TEXT('Processing Type')
     A                                      COLHDG('Processing Type')
     A            MGQANT    R               REFFLD(QANT)
     A                                      TEXT('Quantity of Securities')
     A                                      COLHDG('Quantity'+
     A                                             'Securities')
     A            MGISIN    R               REFFLD(M5ISIN)
     A                                      TEXT('ISIN Code of Securities')
     A                                      COLHDG('ISIN Code'+
     A                                             'Securities')
     A            MGSFN1    R               REFFLD(SFN1)
     A                                      TEXT('Security Full Name - 1')
     A                                      COLHDG('Security Full'+
     A                                             'Name1')
     A            MGSFN2    R               REFFLD(SFN2)
     A                                      TEXT('Security Full Name - 2')
     A                                      COLHDG('Security Full'+
     A                                             'Name2')
     A            MGDYBS    R               REFFLD(DYBS)
     A                                      TEXT('Day Basis')
     A                                      COLHDG('Day Basis')
     A            MGDVBS    R               REFFLD(DVBS)
     A                                      TEXT('Divisor Basis')
     A                                      COLHDG('Divisor Basis')
     A            MGPFRE    R               REFFLD(BKMY)
     A                                      TEXT('Payment Frequency')
     A                                      COLHDG('Payment Frequency')
     A            MGPRTP    R               REFFLD(BBTV)
     A                                      TEXT('Partly-Paid')
     A                                      COLHDG('Partly-Paid')
     A            MGNMCY    R               REFFLD(CRCY)
     A                                      TEXT('Nominal Currency')
     A                                      COLHDG('Nominal Currency')
     A            MGNCDT    R               REFFLD(M5NCDT)
     A                                      TEXT('Next Coupon Date')
     A                                      COLHDG('Next Coupon Date')
     A            MGNRDT    R               REFFLD(ERTC)
     A                                      TEXT('Next Rate Change Date')
     A                                      COLHDG('Next Rate'+
     A                                             'ChangeDate')
     A            MGMDAT    R               REFFLD(MATY)
     A                                      TEXT('Maturity Date')
     A                                      COLHDG('Maturity Date')
     A            MGCUFC        10P 9       TEXT('Current Factor')
     A                                      COLHDG('Current Factor')
     A            MGCPNR    R               REFFLD(CPNR)
     A                                      TEXT('Coupon Rate')
     A                                      COLHDG('Coupon Rate')
     A            MGCRTN    R               REFFLD(M5CRTN)
     A                                      TEXT('Certificate Numbers Required')
     A                                      COLHDG('Certificate'+
     A                                             'Nbrs Req')
     A            MGINSS    R               REFFLD(INSS)
     A                                      TEXT('Instruction Sub-type')
     A                                      COLHDG('Instruction Sub-type')
     A            MGSTAN    R               REFFLD(BBTV)
     A                                      TEXT('Standing Instruction +
     A                                            Override')
     A                                      COLHDG('Standing Ins'+
     A                                             'Ovride')
     A            MGDSSN    R               REFFLD(DSSN)
     A                                      TEXT('Deliverer of Securities')
     A                                      COLHDG('Deliverer'+
     A                                             'Securities')
     A            MGDSS1    R               REFFLD(DSS1)
     A                                      TEXT('Deliverer of Securities 1')
     A                                      COLHDG('Deliverer'+
     A                                             'Sec 1')
     A            MGRSSN    R               REFFLD(M5RSSN)
     A                                      TEXT('Receiver of Securities')
     A                                      COLHDG('Receiver'+
     A                                             'Securities')
     A            MGRSS1    R               REFFLD(RSS1)
     A                                      TEXT('Receiver of Securities 1')
     A                                      COLHDG('Receiver'+
     A                                             'Sec 1')
     A            MGAWIN    R               REFFLD(AWIN)
     A                                      TEXT('Account With Institution')
     A                                      COLHDG('A/C With Institution')
     A            MGAWIL    R               REFFLD(AWIL)
     A                                      TEXT('Account With Institution +
     A                                            A/C Line')
     A                                      COLHDG('A/C With Ins'+
     A                                            'A/CLine')
     A            MGAWIC    R               REFFLD(BERGA)
     A                                      TEXT('Account With Institution +
     A                                            Issuer Code')
     A                                      COLHDG('A/CWthIns'+
     A                                            'IssuerCode')
     A            MGBOFN    R               REFFLD(M5BOFN)
     A                                      TEXT('Beneficiary of Money')
     A                                      COLHDG('Beneficiary of Money')
     A            MGBOF1    R               REFFLD(M5BOF1)
     A                                      TEXT('Beneficiary of Money +
     A                                            A/C Line')
     A                                      COLHDG('Bene Money'+
     A                                            'A/C Line')
     A            MGBOFC    R               REFFLD(BERGA)
     A                                      TEXT('Beneficiary of Money Issuer +
     A                                            Code')
     A                                      COLHDG('Bene Money'+
     A                                            'Iss Code')
     A            MGDLAM    R               REFFLD(M5DLAM)
     A                                      TEXT('Deal Amount')
     A                                      COLHDG('Deal Amount')
     A            MGISDI    R               REFFLD(ACBL)
     A                                      TEXT('Reallowance')
     A                                      COLHDG('Reallowance')
     A            MGTBCI    R               REFFLD(BERGA)
     A                                      TEXT('Broker Commission ID')
     A                                      COLHDG('Broker Commission ID')
     A            MGTBCA    R               REFFLD(BCMA)
     A                                      TEXT('Broker Commission Amount')
     A                                      COLHDG('Broker Comm'+
     A                                             'Amount')
     A            MGTCCI    R               REFFLD(BERGA)
     A                                      TEXT('Customer Commission ID')
     A                                      COLHDG('Customer Comm'+
     A                                             'ID')
     A            MGTCCA    R               REFFLD(CCMA)
     A                                      TEXT('Customer Commission Amount')
     A                                      COLHDG('Customer Comm'+
     A                                             'Amount')
     A            MGTCI1    R               REFFLD(BERGA)
     A                                      TEXT('Charge 1 ID')
     A                                      COLHDG('Charge 1 ID')
     A            MGTCA1    R               REFFLD(PCHA)
     A                                      TEXT('Charge Amount 1')
     A                                      COLHDG('Charge Amount 1')
     A            MGTCI2    R               REFFLD(BERGA)
     A                                      TEXT('Charge 2 ID')
     A                                      COLHDG('Charge 2 ID')
     A            MGTCA2    R               REFFLD(PCHA)
     A                                      TEXT('Charge Amount 2')
     A                                      COLHDG('Charge Amount 2')
     A            MGTCI3    R               REFFLD(BERGA)
     A                                      TEXT('Charge 3 ID')
     A                                      COLHDG('Charge 3 ID')
     A            MGTCA3    R               REFFLD(PCHA)
     A                                      TEXT('Charge Amount 3')
     A                                      COLHDG('Charge Amount 3')
     A            MGTCI4    R               REFFLD(BERGA)
     A                                      TEXT('Charge 4 ID')
     A                                      COLHDG('Charge 4 ID')
     A            MGTCA4    R               REFFLD(PCHA)
     A                                      TEXT('Charge Amount 4')
     A                                      COLHDG('Charge Amount 4')
     A            MGTCI5    R               REFFLD(BERGA)
     A                                      TEXT('Charge 5 ID')
     A                                      COLHDG('Charge 5 ID')
     A            MGTCA5    R               REFFLD(PCHA)
     A                                      TEXT('Charge Amount 5')
     A                                      COLHDG('Charge Amount 5')
     A            MGTCI6    R               REFFLD(BERGA)
     A                                      TEXT('Charge 6 ID')
     A                                      COLHDG('Charge 6 ID')
     A            MGTCA6    R               REFFLD(PCHA)
     A                                      TEXT('Charge Amount 6')
     A                                      COLHDG('Charge Amount 6')
     A            MGTCI7    R               REFFLD(BERGA)
     A                                      TEXT('Charge 7 ID')
     A                                      COLHDG('Charge 7 ID')
     A            MGTCA7    R               REFFLD(PCHA)
     A                                      TEXT('Charge Amount 7')
     A                                      COLHDG('Charge Amount 7')
     A            MGTAX1    R               REFFLD(BERGA)
     A                                      TEXT('Tax ID')
     A                                      COLHDG('Tax ID')
     A            MGTAXA    R               REFFLD(TLTA)
     A                                      TEXT('Tax Amount')
     A                                      COLHDG('Tax Amount')
     A            MGOTRC    R               REFFLD(CRCY)
     A                                      TEXT('Original Currency')
     A                                      COLHDG('Original Currency')
     A            MGOTRA    R               REFFLD(AFPY)
     A                                      TEXT('Original Amount')
     A                                      COLHDG('Original Amount')
     A            MGDAXR    R               REFFLD(DAXR)
     A                                      TEXT('Exchange Rate')
     A                                      COLHDG('Exchange Rate')
     A            MGNMDP    R               REFFLD(NODP)
     A                                      TEXT('Nominal Decimal Places')
     A                                      COLHDG('Nominal Dec'+
     A                                             'Places')
      *
      *                                              Begin /CSE006/
     A            MGMD           5  0       TEXT('Maturity Date of          +
     A                                           the Repo')
     A                                      COLHDG('Maturity date'        +
     A                                             'of the Repo')
     A            MGRR          13P 8       TEXT('Repo Rate')
     A                                      COLHDG('Repo Rate')
     A            MGARF          6          TEXT('Associated Deal Number')
     A                                      COLHDG('Associated'    +
     A                                             'Deal Number')
      *                                              End   /CSE006/
     A**Start of change CSE023
     A            MGWHTX        13P 0       TEXT('Withholding tax')
     A                                      COLHDG('Withholding tax')
     A**End of change CSE023
     A**Start of change CSW025
     A            MGCHG     R               REFFLD(CHTP)
     A**End of change CSW025
     A**Start of change 234140
     A            EUTS          13P 0       TEXT('EU Tax Amount in +
     A                                      Settlement Currency')
     A                                      COLHDG('EU Tax Amount in ' +
     A                                      'Settlement Currency')
     A**End of change 234140
     A**Start of change CSW210
     A            MGDIDN        10A         TEXT('Debtor - Identification')
     A                                      COLHDG('Debtor ' +
     A                                             '- Identification')
     A            MGDAD1        35A         TEXT('Debtor - Address 1')
     A                                      COLHDG('Debtor - Address 1')
     A            MGDAD2        35A         TEXT('Debtor - Address 2')
     A                                      COLHDG('Debtor - Address 2')
     A            MGDAD3        35A         TEXT('Debtor - Address 3')
     A                                      COLHDG('Debtor - Address 3')
     A            MGDAD4        35A         TEXT('Debtor - Address 4')
     A                                      COLHDG('Debtor - Address 4')
     A            MGDLIN        35A         TEXT('Debtor - Account Line')
     A                                      COLHDG('Debtor ' +
     A                                            '- Account Line')
     A            MGFAP1         1A         TEXT('Account for Payment Line 1 +
     A                                            A/C IBAN Flag')
     A                                      COLHDG('A/C for Payment Line' +
     A                                             ' 1 A/C IBAN Flag')
     A            MGFAP2         1A         TEXT('Account for Payment Line 2 +
     A                                            A/C IBAN Flag')
     A                                      COLHDG('A/C for Payment Line' +
     A                                             ' 2 A/C IBAN Flag')
     A            MGFDBT         1A         TEXT('Debtor Account Line +
     A                                            IBAN Flag')
     A                                      COLHDG('Debtor Account Line' +
     A                                             'IBAN Flag')
     A            MGFAI1         1A         TEXT('Account with Institution +
     A                                            Line IBAN Flag')
     A                                      COLHDG('A/C with Institution' +
     A                                             'Line IBAN Flag')
     A            MGFBF1         1A         TEXT('Beneficiary of Money Line +
     A                                            IBAN Flag')
     A                                      COLHDG('Beneficiary of Money' +
     A                                             'Line IBAN Flag')
     A**End of change CSW210
