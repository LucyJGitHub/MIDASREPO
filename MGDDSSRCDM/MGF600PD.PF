      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas MG MT600 Generation Driver file')                *
      *****************************************************************
      *                                                               *
      *  Midas - Message Generation                                   *
      *                                                               *
      *  PF/MGF600PD - Midas MG MT600 Generation Driver File          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2012            *
      *                                                               *
      *  Last Amend No. CSW214             Date 25Nov14               *
      *  Prev Amend No. CSW213             Date 03Jun13               *
      *                 AR1094963          Date 06Mar12               *
      *                 CMG003  *CREATE    Date 04Oct12               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSW214 - SWIFT Changes 2014                                  *
      *  CSW213 - SWIFT Changes 2013                                  *
      *  AR1094963 - MT600 clashes to BF2.1                           *
      *  CMG003 - SWIFT MT600 Message Generation                      *
      *                                                               *
      *****************************************************************
 
     A          R MGF600D0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
 
     A            MGBRCA         3A         TEXT('Branch Code')
     A                                      COLHDG('Branch Code')
 
      **Start of Change AR1094963
     A**********  MGCNUM         6S 0       TEXT('Customer number')                        AR1094963
     A            MGCNUM         6A         TEXT('Customer number')
     A                                      COLHDG('Customer NUmber')
     A**********                            EDTCDE(X)                                      AR1094963
      **End of Change AR1094963
 
     A            MGMODI         2A         TEXT('Module id')
     A                                      COLHDG('Module id')
 
     A            MGTNUM        15A         TEXT('Transaction Number')
     A                                      COLHDG('Transaction Number')
 
     A            MGTDAT         5P 0       TEXT('Deal date')
     A                                      COLHDG('Deal date')
     A                                      EDTCDE(Y)
 
     A            MGEXRT        13P 8       TEXT('Exchange rate')
     A                                      COLHDG('Exchange rate')
     A                                      EDTCDE(L)
 
     A            MGBRKC         4A         TEXT('Broker code')
     A                                      COLHDG('Broker code')
 
     A            MGMDAT         5P 0       TEXT('Maturity date')
     A                                      COLHDG('Maturity date')
     A                                      EDTCDE(Y)
 
     A            MGVDAT         5P 0       TEXT('Value Date')
     A                                      COLHDG('Value Date')
     A                                      EDTCDE(Y)
 
     A            MGOTDT         5P 0       TEXT('Option to date')
     A                                      COLHDG('Option to date')
     A                                      EDTCDE(Y)
 
     A            MGPUCY         3A         TEXT('Purchase currency')
     A                                      COLHDG('Purchase currency')
 
     A            MGPAMT        13P 0       TEXT('Purchase Amount')
     A                                      COLHDG('Purchase Amount')
     A                                      EDTCDE(J)
 
     A            MGRIBN         8A         TEXT('Receipt - Intermediary bank n+
     A                                      umber')
     A                                      COLHDG('Rec - Int Bank')
 
     A            MGRIBA        35A         TEXT('Receipt - Intermediary Bank A+
     A                                      /c line')
     A                                      COLHDG('Rcpt - Int A/c')
 
     A            MGRONS        18A         TEXT('Receipt - Our Nostro')
     A                                      COLHDG('Receipt - Our Nostro')
 
     A            MGSLCY         3A         TEXT('Sell currency')
     A                                      COLHDG('Sell currency')
 
     A            MGSAMT        13P 0       TEXT('Original sale amount')
     A                                      COLHDG('Original sale amount')
     A                                      EDTCDE(J)
 
     A            MGPONS        18A         TEXT('Pay - Our Nostro')
     A                                      COLHDG('Pay - Our Nostro')
 
     A            MGPIBN         8A         TEXT('Pay - Intermediary Bank No')
     A                                      COLHDG('Pay - Int Bank')
 
     A            MGPIBA        35A         TEXT('pay - intermediary bank a/c l+
     A                                      ine')
     A                                      COLHDG('Pay - Int Bank A/c')
 
     A            MGAWBN         8A         TEXT('a/c with bank no')
     A                                      COLHDG('A/c Bank No')
 
     A            MGAWBA        35A         TEXT('a/c with bank a/c line')
     A                                      COLHDG('A/c Bank Acc')
 
     A            MGRVNO         8A         TEXT('Receiver number')
     A                                      COLHDG('Receiver number')
 
     A            MGBENN         8A         TEXT('Beneficiary No')
     A                                      COLHDG('Beneficiary No')
 
     A            MGBENA        35A         TEXT('Beneficiary A/c line')
     A                                      COLHDG('Beneficiary A/c line')
 
     A            MGTTYP         2A         TEXT('Transaction Type')
     A                                      COLHDG('Transaction Type')
 
     A            MGTSTP         2A         TEXT('Trans. Sub-Type')
     A                                      COLHDG('Trans. Sub-Type')
 
     A            MGPOBR         3A         TEXT('Our Pay Branch')
     A                                      COLHDG('Our Pay Branch')
 
     A            MGCHTP         1A         TEXT('Type of last change')
     A                                      COLHDG('Type of last change')
 
     A            MGROBR         3A         TEXT('Our Receive Branch')
     A                                      COLHDG('Our Receive Branch')
 
     A            MGPSTM         2S 0       TEXT('Pay - Settlement Method')
     A                                      COLHDG('Pay - Sett Method')
 
     A            MGRSTM         2S 0       TEXT('Receipt - Settlement Method')
     A                                      COLHDG('Rcpt Sett Method')
 
      **Start of Change AR1094963
     A**********  MGPOBN         6S 0       TEXT('Pay - Ordering bank No')                 AR1094963
     A            MGPOBN         6A         TEXT('Pay - Ordering bank No')
     A                                      COLHDG('Pay - Ord bank No')
 
     A**********  MGPOCN         6S 0       TEXT('Pay - Ordering Cust No')                 AR1094963
     A            MGPOCN         6A         TEXT('Pay - Ordering Cust No')
     A                                      COLHDG('Pay - Ord Cust No')
      **End of Change AR1094963
 
     A            MGCTC1        35A         TEXT('Contact  name & tel. no. 1')
     A                                      COLHDG('Contact  name &' +
     A                                             'telephone number 1')
 
     A            MGCTC2        35A         TEXT('Contact  name & tel. no. 2')
     A                                      COLHDG('Contact  name &' +
     A                                             'telephone number 2')
 
     A            MGCTC3        35A         TEXT('Contact  name & tel. no. 3')
     A                                      COLHDG('Contact  name &' +
     A                                             'telephone number 3')
 
     A            MGCTC4        35A         TEXT('Contact  name & tel. no. 4')
     A                                      COLHDG('Contact  name &' +
     A                                             'telephone number 4')
 
     A            MGPYCY         3A         TEXT('Payment Settlement Currency')
     A                                      COLHDG('Payment Settlement' +
     A                                             'Currency')
 
     A            MGRCCY         3A         TEXT('Receipt Settlement Currency')
     A                                      COLHDG('Receipt Settlement' +
     A                                             'Currency')
 
     A            MGCLSS         1A         TEXT('CLS Settlement')
     A                                      COLHDG('CLS Settlement')
 
     A            MGBTB1        35A         TEXT('Bank to Bank Info 1')
     A                                      COLHDG('Bank to Bank Info 1')
 
     A            MGBTB2        35A         TEXT('Bank to Bank Info 2')
     A                                      COLHDG('Bank to Bank Info 2')
 
     A            MGBTB3        35A         TEXT('Bank to Bank Info 3')
     A                                      COLHDG('Bank to Bank Info 3')
 
     A            MGBTB4        35A         TEXT('Bank to Bank Info 4')
     A                                      COLHDG('Bank to Bank Info 4')
 
     A            MGBTB5        35A         TEXT('Bank to Bank Info 5')
     A                                      COLHDG('Bank to Bank Info 5')
 
     A            MGBTB6        35A         TEXT('Bank to Bank Info 6')
     A                                      COLHDG('Bank to Bank Info 6')
 
     A            MGCON1        35A         TEXT('Condition 1')
     A                                      COLHDG('Condition 1')
 
     A            MGCON2        35A         TEXT('Condition 2')
     A                                      COLHDG('Condition 2')
 
     A            MGCON3        35A         TEXT('Condition 3')
     A                                      COLHDG('Condition 3')
 
     A            MGCON4        35A         TEXT('Condition 4')
     A                                      COLHDG('Condition 4')
 
     A            MGCON5        35A         TEXT('Condition 5')
     A                                      COLHDG('Condition 5')
 
     A            MGCON6        35A         TEXT('Condition 6')
     A                                      COLHDG('Condition 6')
 
     A            MGISDA         4S 0       TEXT('Version of ISDA')
     A                                      COLHDG('Version of' +
     A                                             'ISDA')
 
     A            MGAGTY         6A         TEXT('Type of ISDA')
     A                                      COLHDG('Type of' +
     A                                             'ISDA')
 
     A            MGAGDT         8S 0       TEXT('Date of ISDA')
     A                                      COLHDG('Date of' +
     A                                             'ISDA')
 
     A            MGAGVV         4S 0       TEXT('Ver of ISDA Agreement')
     A                                      COLHDG('Ver of ISDA' +
     A                                             'Agreement')
 
     A            MGPMBC         1A         TEXT('Precious Metal - Buy')
     A                                      COLHDG('Precious' 'Metal' +
     A                                             'Buy Ccy')
 
     A            MGPMSC         1A         TEXT('Precious Metal - Sell')
     A                                      COLHDG('Precious' 'Metal' +
     A                                             'Sell Ccy')
      **Start of change CSW213
     A            MGAPID         4A         TEXT('API ID')
     A                                      COLHDG('API ID')
     A            MGTRRF        16A         TEXT('Transaction Reference')
     A                                      COLHDG('Transaction Ref.')
     A            MGLNRF        16A         TEXT('Link Deal Reference')
     A                                      COLHDG('Link Deal Ref.')
     A            MGCPID         6A         TEXT('Counterparty')
     A                                      COLHDG('Counter' 'party')
     A            MGDTYP         2A         TEXT('Deal Type')
     A                                      COLHDG('Deal' 'Type')
     A            MGDLST         2A         TEXT('Deal Sub-Type')
     A                                      COLHDG('Deal' 'S-Typ')
     A            MGTRID         6A         TEXT('Trade Repository ID')
     A                                      COLHDG('Trade' 'Repository' 'ID')
     A            MGCCPI         6A         TEXT('Cnt. Cntrparty Clearing(CCP)')
     A                                      COLHDG('Central' 'Cntrprty +
     A                                      Clear(CCP)')
     A            MGCCPA        34A         TEXT('CCP Account')
     A                                      COLHDG('CCP Account')
     A            MGCEPI         6A         TEXT('Clearing Exception Party')
     A                                      COLHDG('Clearing' 'Exception' +
     A                                             'Party')
     A            MGCEPA        34A         TEXT('Clearing Except. Party Accnt')
     A                                      COLHDG('Clearing' 'Exception' +
     A                                             'Party Accnt')
     A            MGCBCI        35A         TEXT('Clearing Brokr ID Client Leg')
     A                                      COLHDG('Clearing Broker ID' +
     A                                             'Client Leg')
     A            MGCBPI        35A         TEXT('Clearing Broker ID CCP Leg')
     A                                      COLHDG('Clearing Broker ID' +
     A                                             'Client CCP Leg')
     A            MGPRID        35A         TEXT('Cleared Product ID')
     A                                      COLHDG('Cleared Product ID')
     A            MGEXVE        35A         TEXT('Execution Venue')
     A                                      COLHDG('Execution Venue')
     A            MGUPID         6A         TEXT('Underlying Product ID')
     A                                      COLHDG('Underlying' 'Product ID')
      *
     A            MGRJ01        35A         TEXT('Reporting Jurisdiction 1')
     A                                      COLHDG('Reporting' 'Jurisdiction 1')
     A            MGRJ02        35A         TEXT('Reporting Jurisdiction 2')
     A                                      COLHDG('Reporting' 'Jurisdiction 2')
     A            MGRJ03        35A         TEXT('Reporting Jurisdiction 3')
     A                                      COLHDG('Reporting' 'Jurisdiction 3')
     A            MGRJ04        35A         TEXT('Reporting Jurisdiction 4')
     A                                      COLHDG('Reporting' 'Jurisdiction 4')
      *
     A            MGRP01         6A         TEXT('Reporting Party 1')
     A                                      COLHDG('Report.' 'Party 1')
     A            MGRP02         6A         TEXT('Reporting Party 2')
     A                                      COLHDG('Report.' 'Party 2')
     A            MGRP03         6A         TEXT('Reporting Party 3')
     A                                      COLHDG('Report.' 'Party 3')
     A            MGRP04         6A         TEXT('Reporting Party 4')
     A                                      COLHDG('Report.' 'Party 4')
      *
     A            MGRA01        34A         TEXT('Reporting Party Account 1')
     A                                      COLHDG('Reporting Party' +
     A                                             'Account 1')
     A            MGRA02        34A         TEXT('Reporting Party Account 2')
     A                                      COLHDG('Reporting Party' +
     A                                             'Account 2')
     A            MGRA03        34A         TEXT('Reporting Party Account 3')
     A                                      COLHDG('Reporting Party' +
     A                                             'Account 3')
     A            MGRA04        34A         TEXT('Reporting Party Account 4')
     A                                      COLHDG('Reporting Party' +
     A                                             'Account 4')
      *
     A            MGRN01        20A         TEXT('Reporting Party Name 1')
     A                                      COLHDG('Reporting' 'Party Name 1')
     A            MGRN02        20A         TEXT('Reporting Party Name 2')
     A                                      COLHDG('Reporting' 'Party Name 2')
     A            MGRN03        20A         TEXT('Reporting Party Name 3')
     A                                      COLHDG('Reporting' 'Party Name 3')
     A            MGRN04        20A         TEXT('Reporting Party Name 4')
     A                                      COLHDG('Reporting' 'Party Name 4')
      *
     A            MGRC01        10A         TEXT('Reporting Party City 1')
     A                                      COLHDG('Reporting' 'Party City 1')
     A            MGRC02        10A         TEXT('Reporting Party City 2')
     A                                      COLHDG('Reporting' 'Party City 2')
     A            MGRC03        10A         TEXT('Reporting Party City 3')
     A                                      COLHDG('Reporting' 'Party City 3')
     A            MGRC04        10A         TEXT('Reporting Party City 4')
     A                                      COLHDG('Reporting' 'Party City 4')
      *
     A            MGRS01        11A         TEXT('Reporting Party SWIFT ID1')
     A                                      COLHDG('Reporting Party' +
     A                                             'SWIFT ID-1')
     A            MGRS02        11A         TEXT('Reporting Party SWIFT ID2')
     A                                      COLHDG('Reporting Party' +
     A                                             'SWIFT ID-2')
     A            MGRS03        11A         TEXT('Reporting Party SWIFT ID3')
     A                                      COLHDG('Reporting Party' +
     A                                             'SWIFT ID-3')
     A            MGRS04        11A         TEXT('Reporting Party SWIFT ID4')
     A                                      COLHDG('Reporting Party' +
     A                                             'SWIFT ID-4')
      *
     A            MGRL01        20A         TEXT('Reporting Party LEI 1')
     A                                      COLHDG('Reporting' 'Party LEI 1')
     A            MGRL02        20A         TEXT('Reporting Party LEI 2')
     A                                      COLHDG('Reporting' 'Party LEI 2')
     A            MGRL03        20A         TEXT('Reporting Party LEI 3')
     A                                      COLHDG('Reporting' 'Party LEI 3')
     A            MGRL04        20A         TEXT('Reporting Party LEI 4')
     A                                      COLHDG('Reporting' 'Party LEI 4')
      *
      **Start of change CSW214
     A**********  MGUT01        10A         TEXT('UTI Name Space/Issuer Code 1')              CSW214
     A            MGUT01        20A         TEXT('UTI Name Space/Issuer Code 1')
      **End of change CSW214
     A                                      COLHDG('UTI Name' 'Space/Issuer' +
     A                                             'Code 1')
      **Start of change CSW214
     A**********  MGUT02        10A         TEXT('UTI Name Space/Issuer Code 2')              CSW214
     A            MGUT02        20A         TEXT('UTI Name Space/Issuer Code 2')
      **End of change CSW214
     A                                      COLHDG('UTI Name' 'Space/Issuer' +
     A                                             'Code 2')
      **Start of change CSW214
     A**********  MGUT03        10A         TEXT('UTI Name Space/Issuer Code 3')              CSW214
     A            MGUT03        20A         TEXT('UTI Name Space/Issuer Code 3')
      **End of change CSW214
     A                                      COLHDG('UTI Name' 'Space/Issuer' +
     A                                             'Code 3')
      **Start of change CSW214
     A**********  MGUT04        10A         TEXT('UTI Name Space/Issuer Code 4')
     A            MGUT04        20A         TEXT('UTI Name Space/Issuer Code 4')              CSW214
      **End of change CSW214
     A                                      COLHDG('UTI Name' 'Space/Issuer' +
     A                                             'Code 4')
      *
     A            MGTN01        32A         TEXT('Transaction ID 1')
     A                                      COLHDG('Transaction ID 1')
     A            MGTN02        32A         TEXT('Transaction ID 2')
     A                                      COLHDG('Transaction ID 2')
     A            MGTN03        32A         TEXT('Transaction ID 3')
     A                                      COLHDG('Transaction ID 3')
     A            MGTN04        32A         TEXT('Transaction ID 4')
     A                                      COLHDG('Transaction ID 4')
      *
     A            MGTI01         1A         TEXT('Transaction ID Type 1')
     A                                      COLHDG('Trans.' 'ID' 'Typ 1')
     A            MGTI02         1A         TEXT('Transaction ID Type 2')
     A                                      COLHDG('Trans.' 'ID' 'Typ 2')
     A            MGTI03         1A         TEXT('Transaction ID Type 3')
     A                                      COLHDG('Trans.' 'ID' 'Typ 3')
     A            MGTI04         1A         TEXT('Transaction ID Type 4')
     A                                      COLHDG('Trans.' 'ID' 'Typ 4')
      *
     A            MGET01         1A         TEXT('Event Type 1')
     A                                      COLHDG('EVNT' 'TYP 1')
     A            MGET02         1A         TEXT('Event Type 2')
     A                                      COLHDG('EVNT' 'Typ 2')
     A            MGET03         1A         TEXT('Event Type 3')
     A                                      COLHDG('EVNT' 'Typ 3')
     A            MGET04         1A         TEXT('Event Type 4')
     A                                      COLHDG('EVNT' 'Typ 4')
      *
     A            MGCGTI         1A         TEXT('Clearing Threshold Indicator')
     A            MGALLI         1A         TEXT('Allocation Indicator')
     A            MGCLLI         1A         TEXT('Collateralisation Indicator')
     A            MGNORS         1A         TEXT('Non Standard Flag')
     A            MGGEDT         8S 0       TEXT('Gen. Exec. TMST(Date)')
     A                                      COLHDG('Gen. Exec.' 'TMST(Date)')
     A            MGGETM         6S 0       TEXT('Gen. Exec. TMST(Time)')
     A                                      COLHDG('Gen. Exec.' 'TMST(Time)')
     A            MGUSGN         1A         TEXT('UTC Sign')
     A                                      COLHDG('UTC' 'Sign')
     A            MGUTCT         4S 0       TEXT('Execution Timestamp (UTC)')
     A                                      COLHDG('Execution' 'Timestamp' +
                                                   '(UTC)')
     A            MGLSID        42A         TEXT('Link SWAP ID')
     A                                      COLHDG('Link SWAP ID')
      *
     A            MGAR01        35A         TEXT('Add. Reporting Information 1')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 1')
     A            MGAR02        35A         TEXT('Add. Reporting Information 2')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 2')
     A            MGAR03        35A         TEXT('Add. Reporting Information 3')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 3')
     A            MGAR04        35A         TEXT('Add. Reporting Information 4')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 4')
     A            MGAR05        35A         TEXT('Add. Reporting Information 5')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 5')
     A            MGAR06        35A         TEXT('Add. Reporting Information 6')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 6')
     A            MGAR07        35A         TEXT('Add. Reporting Information 7')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 7')
     A            MGAR08        35A         TEXT('Add. Reporting Information 8')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 8')
     A            MGAR09        35A         TEXT('Add. Reporting Information 9')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 9')
     A            MGAR10        35A         TEXT('Add. Reporting Information10')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 10')
     A            MGAR11        35A         TEXT('Add. Reporting Information11')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 11')
     A            MGAR12        35A         TEXT('Add. Reporting Information12')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 12')
     A            MGAR13        35A         TEXT('Add. Reporting Information13')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 13')
     A            MGAR14        35A         TEXT('Add. Reporting Information14')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 14')
     A            MGAR15        35A         TEXT('Add. Reporting Information15')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 15')
     A            MGAR16        35A         TEXT('Add. Reporting Information16')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 16')
     A            MGAR17        35A         TEXT('Add. Reporting Information17')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 17')
     A            MGAR18        35A         TEXT('Add. Reporting Information18')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 18')
     A            MGAR19        35A         TEXT('Add. Reporting Information19')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 19')
     A            MGAR20        35A         TEXT('Add. Reporting Information20')
     A                                      COLHDG('Add. Reporting' +
     A                                             'Information 20')
      *
     A            MGFOID        20A         TEXT('Front Office  Trans. ID')
     A                                      COLHDG('Front Office' 'Trans. ID')
     A            MGAFOI        20A         TEXT('Assoc. Front Office Tran. ID')
     A                                      COLHDG('Assoc. Front Office' +
     A                                             'Tran. ID')
     A            MGCFRQ         1A         TEXT('Confirmation Require')
     A            MGCFGN         1A         TEXT('Confirmation Generated')
     A            MGCFNR         3S 0       TEXT('Confirmation Sequence Number')
     A                                      COLHDG('Confirm.' 'Seq.' 'No.')
     A            MGPRED         6A         TEXT('Previous Repository Id')
     A                                      COLHDG('Previous' 'Repository' 'Id')
     A            MGBCCY         3A         TEXT('Buy ccy')
     A            MGBPMI         1A         TEXT('Buy ccy Prec. metal Indicatr')
     A            MGSCCY         3A         TEXT('Sell ccy')
     A                                      COLHDG('Sell' 'ccy')
     A            MGSPMI         1A         TEXT('Sell ccy Prec. metal Indctr')
     A            MGLCD          5S 0       TEXT('Last Change Date')
     A                                      COLHDG('Last' 'Change' 'Date')
     A            MGTMST          Z         TEXT('Timestamp')
     A                                      COLHDG('Time stamp')
     A            MGACMI         1A         TEXT('Add. Confirm. Message Ind.')
     A            MGPTYA         6A         TEXT('Party A')
     A                                      COLHDG('Party A')
      **End of change CSW213
      **Start of change CSW214
     A            MGFNCI         1A         TEXT('Financial Nature of the +
     A                                        Counterparty Indicator')
     A                                      COLHDG('Financial Nature' +
     A                                       'CtrParty' 'Ind')
     A            MGCOPI         1A         TEXT('Collateral Portfolio Ind')
     A                                      COLHDG('Collateral' 'Portfolio' +
     A                                       'Ind')
     A            MGCOPC        10A         TEXT('Collateral Portfolio Code')
     A                                      COLHDG('Collateral' 'Portfolio' +
     A                                       'Code')
     A            MGPOCI         1A         TEXT('Portfolio Compression Ind')
     A                                      COLHDG('Portfolio' 'Compress' +
     A                                       'Ind')
     A            MGCOSI         1A         TEXT('Corporate Sector Indicator')
     A                                      COLHDG('Corporate' 'Sector' +
     A                                       'Ind')
     A            MGTNEI         1A         TEXT('Trade with Non-EEA +
     A                                       Counterparty Indicator')
     A                                      COLHDG('Trade with' 'Non-EEA' +
     A                                       'CtrParty Ind')
     A            MGIGTI         1A         TEXT('Intragroup Trade Indicator')
     A                                      COLHDG('Intragroup' 'Trade' +
     A                                       'Ind')
     A            MGCTFI         1A         TEXT('Commercial or Treasury +
     A                                       Financial Indicator')
     A                                      COLHDG('Commercial or' 'Treas Fin' +
     A                                       'Ind')
      **End of change CSW214
