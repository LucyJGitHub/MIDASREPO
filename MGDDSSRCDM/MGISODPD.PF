      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas MG ISO15022 Generation Driver - Depot Mvmnt.')   *
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  PF/MGISODPD - ISO15022 Generation Driver File                *
      *                - Depot Movement                               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CSW210             Date 04May10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. CSW208             Date 15Apr08               *
      *                 CSW207             Date 07Jul07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      * Midas Release 4.01 -------------------------------------------*
      *                 200292             Date 17Oct01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSE029 *CREATE     Date 17Jul01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSW210 - SWIFT 2010 Changes                                  *
      *  CSW208 - SWIFT 2008 Changes                                  *
      *  CSW207 - SWIFT 2007 Changes                                  *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  200292 - Additional fields needed for ISO15022 message       *
      *  CSE029 - ISO15022 Message Generation                         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R MGISODD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            MGTYPE         7A         TEXT('Message Type')
     A                                      COLHDG('Message' +
     A                                             'Type' )
      *
      * Begin /CSD027/
     A**********  MGDEST    R               REFFLD(M5DEST)                                    CSD027
     A            MGDEST         6A
     A                                      TEXT('Destination Customer Number.')
     A                                      COLHDG('Dest                ' +
     A                                      'Cust                ' +
     A                                      'No.')
      * End /CSD027/
     A            MGTTYP    R               REFFLD(TTYP)
     A            MGTSTP    R               REFFLD(TSTP)
     A            MGINST    R               REFFLD(M5INST)
     A            MGINSS    R               REFFLD(INSS)
     A*
     A            MGPFIX         3A         TEXT('Reference Prefix')
     A                                      COLHDG('Reference' +
     A                                             'Prefix')
     A            MGTREF         6A         TEXT('Transaction Reference Number')
     A                                      COLHDG('Transaction' +
     A                                             'Ref. No.')
     A            MGTSEQ         3  0       TEXT('Transaction Sequence Number')
     A                                      COLHDG('Transaction' +
     A                                             'Seq. No.')
     A*
     A            MGSETP    R               REFFLD(SETP)
     A            MGSDAT    R               REFFLD(SDAT)
     A            MGTDAT    R               REFFLD(TDAT)
      *
     A            MGPROT    R               REFFLD(PROT)
     A                                      TEXT('Security Processing Type')
     A                                      COLHDG('Security' +
     A                                             'Processing'+
     A                                              'TYPE')
      *
     A            MGCCLS         1A         TEXT('Customer Classification')
     A                                      COLHDG('Customer' +
     A                                             'Classification')
     A            MGFCLS         1A         TEXT('From Depot Classification')
     A                                      COLHDG('From Depot' +
     A                                             'Classification')
     A            MGTCLS         1A         TEXT('To Depot Classification')
     A                                      COLHDG('To Depot' +
     A                                             'Classification')
     A*
     A            MGSTCY    R               REFFLD(M5STCY)
     A            MGPRIC    R               REFFLD(PRIC)
     A            MGSTAM    R               REFFLD(M5STAM)
     A            MGSTDP         1S 0       TEXT('Settlement ccy Dec. Places')
     A                                      COLHDG('Settlement Ccy' +
     A                                             'Dec. Places')
     A*
     A            MGOTRA    R               REFFLD(EAMT)
     A                                      TEXT('Original Transaction Amount')
     A            MGOTRC    R               REFFLD(CCY)
     A                                      TEXT('Original Transaction Ccy')
     A            MGOTDP         1S 0       TEXT('Original CCY Dec. Places')
     A                                      COLHDG('Original Ccy' +
     A                                             'Dec. Places')
     A*
     A            MGITRA    R               REFFLD(ITRA)
     A            MGITCY    R               REFFLD(ITCY)
     A            MGITDP         1S 0       TEXT('Interest CCY Dec. Places')
     A                                      COLHDG('Interest Ccy' +
     A                                             'Dec. Places')
      *
     A            MGACND    R               REFFLD(ACND)
     A            MGNCDT    R               REFFLD(M5NCDT)
     A            MGSWTP    R               REFFLD(SWTP)
      *
     A            MGIMD1         6A         TEXT('Settlement Parties +
     A                                           Intermediary 1')
     A                                      COLHDG('Settlement Parties' +
     A                                             'Intermediary 1')
     A            MGIMD2         6A         TEXT('Settlement Parties +
     A                                           Intermediary 2')
     A                                      COLHDG('Settlement Parties' +
     A                                             'Intermediary 2')
     A            MGIMD3         6A         TEXT('Settlement Parties +
     A                                           Intermediary 3')
     A                                      COLHDG('Settlement Parties' +
     A                                             'Intermediary 3')
     A            MGIMD4         6A         TEXT('Settlement Parties +
     A                                           Intermediary 4')
     A                                      COLHDG('Settlement Parties' +
     A                                             'Intermediary 4')
     A            MGIMD5         6A         TEXT('Settlement Parties +
     A                                           Intermediary 5')
     A                                      COLHDG('Settlement Parties' +
     A                                             'Intermediary 5')
     A            MGIMD6         6A         TEXT('Settlement Parties +
     A                                           Intermediary 6')
     A                                      COLHDG('Settlement Parties' +
     A                                             'Intermediary 6')
     A            MGIMD7         6A         TEXT('Settlement Parties +
     A                                           Intermediary 7')
     A                                      COLHDG('Settlement Parties' +
     A                                             'Intermediary 7')
     A            MGIMD8         6A         TEXT('Settlement Parties +
     A                                           Intermediary 8')
     A                                      COLHDG('Settlement Parties' +
     A                                             'Intermediary 8')
     A            MGIMD9         6A         TEXT('Settlement Parties +
     A                                           Intermediary 9')
     A                                      COLHDG('Settlement Parties' +
     A                                             'Intermediary 9')
     A            MGPSET         6A         TEXT('Place of Settlement')
     A                                      COLHDG('Place of Settlement')
      *
     A            MGTSTS         1A         TEXT('Transaction Status')
     A                                      COLHDG('Transaction' +
     A                                             'Status')
      *
     A            MGTMCD    R               REFFLD(NOTD)
     A                                      TEXT('Telekurs Market Centre')
     A                                      COLHDG('Telekurs' +
     A                                             'Market'   +
     A                                             'Centre')
     A            MGMCTY         2A         TEXT('Country Code')
     A                                      COLHDG('Country' 'Code')
     A
     A            MGSMKC         4A         TEXT('SWIFT Market Centre')
     A                                      COLHDG('SWIFT'    +
     A                                             'Market'   +
     A                                             'Centre')
      *
     A            MGBCCD    R               REFFLD(PDCD)
     A                                      TEXT('Broker Commission')
     A                                      COLHDG('Broker' 'Commission')
     A            MGCCCD    R               REFFLD(PDCD)
     A                                      TEXT('Customer Commission')
     A                                      COLHDG('Customer' 'Commission')
     A            MGCHG1    R               REFFLD(PDCD)
     A                                      TEXT('Charge Code 1')
     A                                      COLHDG('Charge' 'Code 1')
     A            MGCHG2    R               REFFLD(PDCD)
     A                                      TEXT('Charge Code 2')
     A                                      COLHDG('Charge' 'Code 2')
     A            MGCHG3    R               REFFLD(PDCD)
     A                                      TEXT('Charge Code 3')
     A                                      COLHDG('Charge' 'Code 3')
     A            MGCHG4    R               REFFLD(PDCD)
     A                                      TEXT('Charge Code 4')
     A                                      COLHDG('Charge' 'Code 4')
     A            MGCHG5    R               REFFLD(PDCD)
     A                                      TEXT('Charge Code 5')
     A                                      COLHDG('Charge' 'Code 5')
     A            MGCHG6    R               REFFLD(PDCD)
     A                                      TEXT('Charge Code 6')
     A                                      COLHDG('Charge' 'Code 6')
     A            MGCHG7    R               REFFLD(PDCD)
     A                                      TEXT('Charge Code 7')
     A                                      COLHDG('Charge' 'Code 7')
      *                                              Begin /200292/
     A            MGSNDI        12A         TEXT('Sender ID')
     A                                      COLHDG('Sender' 'ID')
     A            MGAWBI        12A         TEXT('Institution BIC')
     A                                      COLHDG('Institution' +
     A                                             'BIC Code')
     A            MGAWL1        35A         TEXT('Institution description +
     A                                              Line 1')
     A                                      COLHDG('Institution' +
     A                                             'Name' 'Address 1')
     A            MGAWL2        35A         TEXT('Institution description +
     A                                              Line 2')
     A                                      COLHDG('Institution' +
     A                                             'Name' 'Address 2')
     A            MGAWL3        35A         TEXT('Institution description +
     A                                              Line 3')
     A                                      COLHDG('Institution' +
     A                                             'Name' 'Address 3')
     A            MGAWL4        35A         TEXT('Institution description +
     A                                              Line 4')
     A                                      COLHDG('Institution' +
     A                                             'Name' 'Address 4')
     A            MGCDBI        12A         TEXT('Custodian BIC')
     A                                      COLHDG('Custodian' +
     A                                             'BIC Code')
     A            MGCDL1        35A         TEXT('Custodian description +
     A                                              Line 1')
     A                                      COLHDG('Custodian' +
     A                                             'Name' 'Address 1')
     A            MGCDL2        35A         TEXT('Custodian description +
     A                                              Line 2')
     A                                      COLHDG('Custodian' +
     A                                             'Name' 'Address 2')
     A            MGCDL3        35A         TEXT('Custodian description +
     A                                              Line 3')
     A                                      COLHDG('Institution' +
     A                                             'Name' 'Address 3')
     A            MGCDL4        35A         TEXT('Custodian description +
     A                                              Line 4')
     A                                      COLHDG('Institution' +
     A                                             'Name' 'Address 4')
     A            MGDLBI        12A         TEXT('Deliverer BIC')
     A                                      COLHDG('Deliverer' +
     A                                             'BIC Code')
     A            MGDLL1        35A         TEXT('Deliverer description +
     A                                              Line 1')
     A                                      COLHDG('Deliverer' +
     A                                             'Name' 'Address 1')
     A            MGDLL2        35A         TEXT('Deliverer   description +
     A                                              Line 2')
     A                                      COLHDG('Deliverer' +
     A                                             'Name' 'Address 2')
     A            MGDLL3        35A         TEXT('Deliverer description +
     A                                              Line 3')
     A                                      COLHDG('Deliverer' +
     A                                             'Name' 'Address 3')
     A            MGDLL4        35A         TEXT('Deliverer description +
     A                                              Line 4')
     A                                      COLHDG('Deliverer' +
     A                                             'Name' 'Address 4')
     A            MGRRBI        12A         TEXT('Receiver    BIC')
     A                                      COLHDG('Receiver   ' +
     A                                             'BIC Code')
     A            MGRRL1        35A         TEXT('Receiver description +
     A                                              Line 1')
     A                                      COLHDG('Receiver   ' +
     A                                             'Name' 'Address 1')
     A            MGRRL2        35A         TEXT('Receiver description +
     A                                              Line 2')
     A                                      COLHDG('Receiver   ' +
     A                                             'Name' 'Address 2')
     A            MGRRL3        35A         TEXT('Receiver description +
     A                                              Line 3')
     A                                      COLHDG('Receiver' +
     A                                             'Name' 'Address 3')
     A            MGRRL4        35A         TEXT('Receiver description +
     A                                              Line 4')
     A                                      COLHDG('Institution' +
     A                                             'Name' 'Address 4')
     A            MGBSBI        12A         TEXT('Beneficiary of Security BIC')
     A                                      COLHDG('Beneficiary' 'of Security' +
     A                                             'BIC Code')
     A            MGBSL1        35A         TEXT('Ben. of Sec description +
     A                                              Line 1')
     A                                      COLHDG('Beneficiary' +
     A                                             'Name' 'Address 1')
     A            MGBSL2        35A         TEXT('Ben. of Sec description +
     A                                              Line 2')
     A                                      COLHDG('Beneficiary' +
     A                                             'Name' 'Address 2')
     A            MGBSL3        35A         TEXT('Ben. of Sec description +
     A                                              Line 3')
     A                                      COLHDG('Beneficiary' +
     A                                             'Name' 'Address 3')
     A            MGBSL4        35A         TEXT('Ben. of Sec description +
     A                                              Line 4')
     A                                      COLHDG('Beneficiary' +
     A                                             'Name' 'Address 4')
     A            MGBMBI        12A         TEXT('Beneficiary of Money BIC')
     A                                      COLHDG('Beneficiary' 'of Money' +
     A                                             'BIC Code')
     A            MGBML1        35A         TEXT('Beneficiary description +
     A                                              Line 1')
     A                                      COLHDG('Beneficiary' +
     A                                             'Name' 'Address 1')
     A            MGBML2        35A         TEXT('Beneficiary description +
     A                                              Line 2')
     A                                      COLHDG('Beneficiary' +
     A                                             'Name' 'Address 2')
     A            MGBML3        35A         TEXT('Beneficiary description +
     A                                              Line 3')
     A                                      COLHDG('Beneficiary' +
     A                                             'Name' 'Address 3')
     A            MGBML4        35A         TEXT('Beneficiary description +
     A                                              Line 4')
     A                                      COLHDG('Institution' +
     A                                             'Name' 'Address 4')
     A            MGIPBI        12A         TEXT('Instructing Party BIC')
     A                                      COLHDG('Instructing' +
     A                                             'Party' 'BIC Code')
     A            MGIPL1        35A         TEXT('Instructing Party +
     A                                              Line 1')
     A                                      COLHDG('Instructing' +
     A                                             'Name/' 'Address 1')
     A            MGIPL2        35A         TEXT('Instructing Party +
     A                                              Line 2')
     A                                      COLHDG('Instructing ' +
     A                                             'Name/' 'Address 2')
     A            MGIPL3        35A         TEXT('Instructing Party +
     A                                              Line 3')
     A                                      COLHDG('Instructing' +
     A                                             'Name/' 'Address 3')
     A            MGIPL4        35A         TEXT('Instructing Party +
     A                                              Line 4')
     A                                      COLHDG('Instructing' +
     A                                             'Name' 'Address 4')
     A            MGPSBI        12A         TEXT('Place of Settlement BIC')
     A                                      COLHDG('Settlement' +
     A                                             'BIC Code')
     A            MGPSL1        35A         TEXT('Place of Settlement +
     A                                              Line 1')
     A                                      COLHDG('Settlement' +
     A                                             'Name' 'Address 1')
     A            MGPSL2        35A         TEXT('Place of Settlement +
     A                                              Line 2')
     A                                      COLHDG('Settlement' +
     A                                             'Name' 'Address 2')
     A            MGPSL3        35A         TEXT('Place of Settlement +
     A                                              Line 3')
     A                                      COLHDG('Settlement' +
     A                                             'Name' 'Address 3')
     A            MGPSL4        35A         TEXT('Place of Settlement +
     A                                              Line 4')
     A                                      COLHDG('Settlement' +
     A                                             'Name' 'Address 4')
     A            MGWHEN         1A         TEXT('When produced Indicator')
     A                                      COLHDG('When' +
     A                                             'Produced' 'Indicator')
     A            MGDPSS        10A         TEXT('Security shortname')
     A                                      COLHDG('Security' +
     A                                             'Shortname')
     A            MGDIPI        12A         TEXT('Deliverer Instructing +
     A                                            Party BIC')
     A                                      COLHDG('Instructing' +
     A                                             'Party' 'BIC Code')
     A            MGDIP1        35A         TEXT('Deliverer Instructing +
     A                                              Party L1')
     A                                      COLHDG('Instructing' +
     A                                             'Name/' 'Address 1')
     A            MGDIP2        35A         TEXT('Deliverer Instructing +
     A                                              Party L2')
     A                                      COLHDG('Instructing ' +
     A                                             'Name/' 'Address 2')
     A            MGDIP3        35A         TEXT('Deliverer Instructing +
     A                                              Party L3')
     A                                      COLHDG('Instructing' +
     A                                             'Name/' 'Address 3')
     A            MGDIP4        35A         TEXT('Deliverer Instructing +
     A                                              Party L4')
     A                                      COLHDG('Instructing' +
     A                                             'Name' 'Address 4')
      *                                              End   /200292/
      **Start of change CSW207
     A            MGMFID         5A         TEXT('UTC Indication')
     A                                      COLHDG('UTC' +
     A                                             'Indicator')
     A            MGINCA         4A         TEXT('Investor Capacity Indicator')
     A                                      COLHDG('Investor Capacity' +
     A                                             'Indicator')
     A            MGTRCA         4A         TEXT('Party Capacity Indicator')
     A                                      COLHDG('Party Capacity' +
     A                                             'Indicator')
      **End of change CSW207
      *
      **Start of change CSW208
      *
     A            MGMKDT         5P         TEXT('Date at Market')
     A                                      COLHDG('Date at Market')
     A            MGMKTM         6S         TEXT('Time at Market')
     A                                      COLHDG('Time at Market')
     A            MGMKTO         5A         TEXT('Offset at Market')
     A                                      COLHDG('Offset at Market')
     A            MGLCDT         5P         TEXT('Date at Local')
     A                                      COLHDG('Date at Local')
     A            MGLCTM         6S         TEXT('Time at Local')
     A                                      COLHDG('Time at Local')
     A            MGLCTO         5A         TEXT('Offset at Local')
     A                                      COLHDG('Offset at Local')
      *
      **End of change CSW208
      *
     A**Start of Change CSW210
     A            MGDIBI        12A         TEXT('Debtor BIC')
     A                                      COLHDG('Debtor BIC')
     A            MGDAL1        35A         TEXT('Debtor - Address 1')
     A                                      COLHDG('Debtor - Address 1')
     A            MGDAL2        35A         TEXT('Debtor - Address 2')
     A                                      COLHDG('Debtor - Address 2')
     A            MGDAL3        35A         TEXT('Debtor - Address 3')
     A                                      COLHDG('Debtor - Address 3')
     A            MGDAL4        35A         TEXT('Debtor - Address 4')
     A                                      COLHDG('Debtor - Address 4')
     A            MGQFBI        12A         TEXT('Qualified Foreign +
     A                                            Intermediary BIC')
     A                                      COLHDG('Qualified Foreign In' +
     A                                             'termediary  BIC')
     A            MGQAL1        35A         TEXT('Qualified Foreign +
     A                                            Intermediary Address Line 1')
     A                                      COLHDG('Qualified Foreign In' +
     A                                             'termediary Add Line1')
     A            MGQAL2        35A         TEXT('Qualified Foreign +
     A                                            Intermediary Address Line 2')
     A                                      COLHDG('Qualified Foreign In' +
     A                                             'termediary Add Line2')
     A            MGQAL3        35A         TEXT('Qualified Foreign +
     A                                            Intermediary Address Line 3')
     A                                      COLHDG('Qualified Foreign In' +
     A                                             'termediary Add Line3')
     A            MGQAL4        35A         TEXT('Qualified Foreign +
     A                                            Intermediary Address Line 4')
     A                                      COLHDG('Qualified Foreign In' +
     A                                             'termediary Add Line4')
     A            MGPCBI        10A         TEXT('Place of Clearing  +
     A                                            BIC')
     A                                      COLHDG('Place of Clearing ' +
     A                                             'BIC')
     A**End of Change CSW210
