      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_NM                                                        *
/*EXI *  TEXT('Midas MG MT565 Message Extract')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *    PF/MGF565PD - MT565 Message Extract                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 25Nov05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSW025             Date 26Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSE023  *CREATE    Date 12Jul00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CSW025 - Midas Message Manager                               *
      *           - Pass original change type                         *
      *  CSE023 - Treaty Withholding Tax.                             *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R MGF565D0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            RECI      R
     A            PBCA      R
     A            PSSH      R
      * Begin /CSD027/
     A**********  PCPY      R                                                                 CSD027
     A            PCPY           6A
     A                                      TEXT('Counterparty')
     A                                      COLHDG('Cust')
      * End /CSD027/
     A            PMAR      R
     A            PDUD      R
     A            PEVT      R
     A            PAUI      R
     A            PNMP      R
     A            PNCY      R
     A            PBCD      R
     A            PAMD      R
     A            PPRE      R
     A            PSCU      R
     A            PSXR      R
     A            PMDI      R
     A            PBRX      R
     A            PSMT      R
     A**Start of change CGL029
     A**********  PSEA      R                                                                 CGL029
     A            PSEAQQ        12A
     A**End of change CGL029
     A            PSEB      R
     A            PCPN      R
     A            PFAC      R
     A            PSPI      R
     A            PCHC      R
     A            PCHA      R
     A            PCCD      R
     A            PCAM      R
     A            PSAT      R
     A            PSMI      R
     A            PDTG      R
     A            LCD       R
     A            CHTP      R
     A            TNLU      R
     A            PPFC      R
     A            TASO      R               REFFLD(TAXA)
     A                                      TEXT('Tax ded. at source')
     A            TASR      R               REFFLD(TAXA)
     A                                      TEXT('Tax ded.at srce ref.')
     A            TAUS      R               REFFLD(TAXA)
     A                                      TEXT('Tax ded. by user')
     A            PSVL      R               REFFLD(PDUD)
     A                                      TEXT('Value Date')
     A                                      COLHDG('Value +
     A                                      Date')
     A            PTFR      R
     A            SRPG      R               REFFLD(IND)
     A                                      TEXT('Settle Reversal Postings Gen')
     A                                      COLHDG('Reversal' 'Pos Sttls' 'Gen')
     A            SABC      R               REFFLD(NOMN)
     A                                      TEXT('Sett Amount in Base Ccy')
     A                                      COLHDG('Settl Amount' 'in Base Ccy')
     A            BAUS      R               REFFLD(BCEQ)
     A                                      TEXT('Base currency equivalent of +
     A                                            TAUS')
     A                                      COLHDG('B/C equiv'+
     A                                             'Of'+
     A                                             'TAUS')
     A            PSCF      R               TEXT('Posn Sett.Conf. Produced')
     A                                      REFFLD(RECI)
     A            FXMP      R               REFFLD(FWPP)
     A                                      TEXT('FX Margin Points')
     A                                      COLHDG('FX' +
     A                                             'Margin' +
     A                                             'Points')
     A            FXMR      R               REFFLD(TDER)
     A                                      TEXT('FX Margin Rate')
     A                                      COLHDG('FX' +
     A                                             'Margin' +
     A                                             'Rate')
     A            MAMT      R               REFFLD(AABC)
     A                                      TEXT('Margin Amount')
     A                                      COLHDG('Margin' +
     A                                             'Amount')
     A            TXBS      R               REFFLD(ZZ002)
     A                                      TEXT('Tax basket')
     A                                      COLHDG('Tax' 'basket')
     A            TXBN      R               REFFLD(ZZ015)
     A                                      TEXT('Tax basket narrative')
     A                                      COLHDG('Tax basket nar-' +
     A                                             'rative')
     A            EXCD      R               REFFLD(ZZ002)
     A                                      TEXT('Exemption code')
     A                                      COLHDG('Exempt' 'code')
     A            CRTX      R               REFFLD(CCOD)
     A                                      TEXT('Country of tax')
     A                                      COLHDG('Country' +
     A                                             'of tax')
     A            RPCD      R               REFFLD(ZZ002)
     A                                      TEXT('Recipient code')
     A                                      COLHDG('Recipient' 'Code')
     A            ICTP      R               REFFLD(ZZ002)
     A                                      TEXT('Income type')
     A                                      COLHDG('Income' +
     A                                             'type')
     A            BNFO      R               REFFLD(ZZ026)
     A                                      TEXT('Beneficial owner number')
     A                                      COLHDG('Beneficial owner' +
     A                                             'number')
     A            MT5R      R               REFFLD(ZZ001)
     A                                      TEXT('MT565 message required Y/N')
     A                                      COLHDG('MT565' 'Req' 'Y/N')
     A            MT5G      R               REFFLD(ZZ001)
     A                                      TEXT('MT565 generated Y/N/R')
     A                                      COLHDG('MT565' 'Gen' 'Y/N/R')
     A            CTYP      R               REFFLD(ZZ001)
     A                                      TEXT('Customer type')
     A                                      COLHDG('Cust' 'type')
     A            PREF      R               REFFLD(ZZ006)
     A                                      TEXT('Position settlement ref no.')
     A                                      COLHDG('Position' 'Sett.' 'ref no.')
     A            PSEQ      R               REFFLD(DTSEQN)
     A                                      TEXT('Sequence number')
     A                                      COLHDG('Seq' ' No.')
     A            CTIN      R               REFFLD(ZZ020)
     A                                      TEXT('Customer Tax Id number')
     A                                      COLHDG('Customer tax' 'Id number')
     A            DAMT      R               REFFLD(AMNT)
     A                                      TEXT('Total discounted amount')
     A                                      COLHDG('Total discount' ' amount')
     A            PCTG      R               REFFLD(NDRT)
     A                                      TEXT('Tax Rate')
     A                                      COLHDG('Tax Rate')
     A**Start of change CSW025
     A            MGCHG     R               REFFLD(CHTP)
     A**End of change CSW025
     A**Start of change CGL029
     A            PSEA          18A
     A**End of change CGL029
