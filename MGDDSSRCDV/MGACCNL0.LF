      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas MG Account details - BA3101')                    *
      *****************************************************************
      *                                                               *
      *  Midas - Message Generation Module                            *
      *                                                               *
      *  LF/MGACCNL0 - Midas MG Account details                       *
      *                                                               *
      *  (c) Finastra International Limited 2022                      *
      *                                                               *
      *  Last Amend No. BA3101 *CREATE      Date 12Sep22              *
      *  Prev Amend No. nnnnnn              Date ddmmmyy              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BA3101 - Account line on field 50K for MT103.                *
      *                                                               *
      *****************************************************************
      *
     A          R ACCNTABF                  PFILE(ACCNTAB)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2022')
      *
     A            MARECI                    TEXT('Record Id')
     A                                      RENAME(RECI)
     A            MABRCA                    TEXT('Branch Code')
     A                                      RENAME(BRCA)
     A            MACNUM                    TEXT('Customer Number')
     A                                      RENAME(CNUM)
     A            MACCY                     TEXT('Currency code')
     A                                      RENAME(CCY)
     A            MAACSQ                    TEXT('Account sequence number')
     A                                      RENAME(ACSQ)
     A            MAIBAN                    TEXT('IBAN number')
     A                                      RENAME(IBAN)
     A            MAACNO                    TEXT('Retail number')
     A                                      RENAME(ACNO)
      ** Key fields
     A          K MABRCA
     A          K MACNUM
     A          K MACCY
     A          K MAACSQ
      *
     A          S MARECI                    COMP(EQ 'D')
     A            MACCY                     COMP(EQ 'EUR')
