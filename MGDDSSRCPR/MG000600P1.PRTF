      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MG FX Confirmation Formatting Error report')     *
      *****************************************************************
      *                                                               *
      *  Midas - Message Generation                                   *
      *                                                               *
      *   MG000600P1 - Midas MG FX Confirmation Formatting            *
      *                Error Report                                   *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR1094963          Date 07Mar13               *
      *                 CMG003  *CREATE    Date 04Oct12               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *   AR1094963 - MT600 clashes to BF2.1                          *
      *   CMG003 - SWIFT MT600 Message Generation                     *
      *                                                               *
      *****************************************************************
 
     A          R HEADER                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2012')
     A                                  3  7'REFERENCE MG000600'
 
      */COPY WNCPYSRC,MG0600P101
 
     A            BJURPT        53      3 45
     A                                  3101'DATE'
     A            BJMRDT         7      3106
 
      */COPY WNCPYSRC,MG0600P102
 
     A                                  3119'PAGE'
     A                                  3124PAGNBR EDTCDE(Z)
     A          R TEXT                      SKIPB(5)
     A N30                                 7'THE FOLLOWING MT600 - CONFI+
     A                                      RMATION MESSAGE COULD NOT BE +
     A                                      CORRECTLY GENERATED'
     A                                      SPACEA(2)
     A  30                                 7'THE FOLLOWING MT600 - CONFI+
     A                                      RMATION MESSAGE DID NOT GENERATE +
     A                                      A FEEDBACK RECORD'
     A                                      SPACEA(2)
     A                                     7'SENDER'
     A            MGBRCA         3        20
      **Start of Change AR1094963
     A**********  SECN           6S 0     25                                               AR1094963
     A            SECN           6A       25
      **End of Change AR1094963
     A            NWSN          20A       33
     A                                      SPACEA(2)
     A                                     7'DESTINATION'
      **Start of Change AR1094963
     A**********  DECN           6S 0     25                                               AR1094963
     A            DECN           6A       25
      **End of Change AR1094963
     A            NWDS          20A       33
     A            DSERR         50        60
     A                                      SPACEA(2)
     A                                     7'NETWORK'
     A            NWRK           6A       25
     A                                      SPACEA(2)
     A                                     7'MODULE ID'
     A            MGMODI         2        25
     A                                      SPACEA(2)
     A                                     7'TRANSACTION NO.'
     A            TNUM           6  0     25
     A                                      SPACEA(2)
 
     A          R LINE
     A            MTAG           5         2
     A            MFLD          50        10
     A            MERR          50        70
     A                                      SPACEA(1)
 
     A          R FEED
     A            TRNO          16A       10
     A                                    27'FEEDBACK RECORD FOR THE +
     A                                      CONFIRMATION MESSAGE NOT +
     A                                      GENERATED'
 
     A          R EOREPT                    SKIPB(43)
     A                                    55'*** END OF REPORT ***'
