      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MG Financial Instit. Transfer Error Rpt')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MESSAGE GENERATION
     A*                                                               *
     A*   MG0100P1 - CUSTOMER TRANSFER MESSAGE                        *
     A*              GENERATION ERROR REPORT                          *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CFT013             Date 25May00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CFT013 - Straight-through Processing Phase 2 MT103 Messages  *
      *           Generation for non-FT.                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R HEADER
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                  3  7'REFERENCE MG0100'
     A            BJURPT        53      3 45
     A                                  3101'DATE'
     A            BJMRDT         7      3106
     A                                  3119'PAGE'
     A                                  3124PAGNBR EDTCDE(Z)
     A          R TEXT                      SKIPB(5)
      *                                              Begin /CFT013/
     A*N30*******                          7'THE FOLLOWING MT100 - CUSTOMER +
     A***********                           TRANSFER MESSAGE COULD NOT BE +
     A***********                           CORRECTLY GENERATED'
     A N30N09                              7'THE FOLLOWING MT100 - CUSTOMER +
     A                                      TRANSFER MESSAGE COULD NOT BE +
     A                                      CORRECTLY GENERATED'
     A N30 09                              7'THE FOLLOWING MT103 - CUSTOMER +
     A                                      TRANSFER MESSAGE COULD NOT BE +
     A                                      CORRECTLY GENERATED'
      *                                              End   /CFT013/
     A                                      SPACEA(2)
      *                                              Begin /CFT013/
     A**30*******                          7'THE FOLLOWING MT100 - CUSTOMER +
     A***********                           TRANSFER MESSAGE DID NOT +
     A***********                           GENERATE A FEEDBACK RECORD'
     A  30N09                              7'THE FOLLOWING MT100 - CUSTOMER +
     A                                      TRANSFER MESSAGE DID NOT +
     A                                      GENERATE A FEEDBACK RECORD'
     A  30 09                              7'THE FOLLOWING MT103 - CUSTOMER +
     A                                      TRANSFER MESSAGE DID NOT +
     A                                      GENERATE A FEEDBACK RECORD'
      *                                              End   /CFT013/
     A                                      SPACEA(2)
     A                                     7'SENDER'
     A            MGBRCA         3        20
      * Begin /CSD027/
     A**********  SECN      R             25EDTCDE(Z)                                         CSD027
     A            SECN           6A       25TEXT('Customer no of sender')
      * End /CSD027/
     A            NWSN      R             33
     A                                      SPACEA(2)
     A                                     7'DESTINATION'
      * Begin /CSD027/
     A**********  DECN      R             25EDTCDE(Z)                                         CSD027
     A            DECN           6A       25TEXT('Customer no of destination')
      * End /CSD027/
     A            NWDS      R             33
     A            DSERR         50        60
     A                                      SPACEA(2)
     A                                     7'NETWORK'
     A            NWRK      R             25
     A                                      SPACEA(2)
     A                                     7'MODULE ID'
     A            MGMODI         2        25
     A                                      SPACEA(2)
     A                                     7'TRANSACTION NO.'
     A            MGTNUM        15        25
     A                                      SPACEA(2)
     A*
     A          R LINE
     A            MTAG           5         2
     A            MFLD          50        10
     A            MERR          50        70
     A                                      SPACEA(1)
      *
     A          R FEED
     A            TRNO      R             10
     A                                    27'FEEDBACK RECORD FOR THE +
     A                                      CONFIRMATION MESSAGE NOT +
     A                                      GENERATED'
      *
     A          R EOREPT                    SKIPB(43)
     A                                    55'*** END OF REPORT ***'
