      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MG MT321 formatting error report')               *
     A*****************************************************************
     A*                                                               *
     A*  Midas - MESSAGE GENERATION
     A*                                                               *
     A*   MG0321P1 - MT321 GENERATION ERROR REPORT                    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4.01.01 ----------------------------------------*
      *                 CSW202             Date 20May02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                                                               *
     A*****************************************************************
      *                                                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW202 - SWIFT 2002 Standards Update                         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R HEADER
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                  3  7'REFERENCE MG0321'
     A*/COPY WNCPYSRC,MG0321P101
     A            BJURPT        53      3 45
     A                                  3101'DATE'
     A            BJMRDT         7      3106
     A*/COPY WNCPYSRC,MG0321P102
     A                                  3119'PAGE'
     A                                  3124PAGNBR EDTCDE(Z)
     A          R TEXT                      SKIPB(5)
     A**Start of change CSW202
     A*N30******                           7'THE FOLLOWING MT321 - SECONDARY +
     A**********                             CERTIFICATE OF DEPOSIT MESSAGE +
     A**********                            WAS INCORRECTLY GENERATED'
     A**********                            SPACEA(2)
     A**30******                           7'THE FOLLOWING MT321 - SECONDARY +
     A**********                             CERTIFICATE OF DEPOSIT MESSAGE +
     A**********                            DID NOT GENERATE A FEEDBACK +
     A**********                            RECORD'
     A**********                            SPACEA(2)
     A N30N25                              7'THE FOLLOWING MT321 - SECONDARY +
     A                                       CERTIFICATE OF DEPOSIT MESSAGE +
     A                                      WAS INCORRECTLY GENERATED'
     A                                      SPACEA(2)
     A  30N25                              7'THE FOLLOWING MT321 - SECONDARY +
     A                                       CERTIFICATE OF DEPOSIT MESSAGE +
     A                                      DID NOT GENERATE A FEEDBACK +
     A                                      RECORD'
     A                                      SPACEA(2)
     A N30 25                              7'THE FOLLOWING MT322 - SECONDARY +
     A                                       CERTIFICATE OF DEPOSIT MESSAGE +
     A                                      WAS INCORRECTLY GENERATED'
     A                                      SPACEA(2)
     A  30 25                              7'THE FOLLOWING MT322 - SECONDARY +
     A                                       CERTIFICATE OF DEPOSIT MESSAGE +
     A                                      DID NOT GENERATE A FEEDBACK +
     A                                      RECORD'
     A                                      SPACEA(2)
     A**End of change CSW202
     A                                     7'SENDER'
     A            MGBRCA         3        20
      * Begin /CSD027/
     A**********  SECN      R             25EDTCDE(Z)                                         CSD027
     A            SECN           6A       25TEXT('Customer no of sender')
      * End /CSD027/
     A            NWSN      R             33
     A                                      SPACEA(2)
     A                                     7'DESTINATION'
      * Begin /CSD027/
     A**********  DECN      R             25EDTCDE(Z)                                         CSD027
     A            DECN           6A       25TEXT('Customer no of destination')
      * End /CSD027/
     A            NWDS      R             33
     A            DSERR         50        60
     A                                      SPACEA(2)
     A                                     7'NETWORK'
     A            NWRK      R             25
     A                                      SPACEA(2)
     A                                     7'MODULE ID'
     A            MGMODI         2        25
     A                                      SPACEA(2)
     A                                     7'TRANSACTION NO.'
      **Start of change CLE148
     A**********  TNUM           6  0     25                                                  CLE148
     A            TNUM           6A       25
      **End of change CLE148
     A                                      SPACEA(2)
     A*
     A          R LINE
     A            MTAG           5         2
     A            MFLD          50        10
     A            MERR          50        70
     A                                      SPACEA(1)
      *
     A          R FEED
     A            TRNO      R             10
     A                                    27'FEEDBACK RECORD FOR THE +
     A                                      CONFIRMATION MESSAGE NOT +
     A                                      GENERATED'
      *
     A          R EOREPT                    SKIPB(43)
     A                                    55'*** END OF REPORT ***'
