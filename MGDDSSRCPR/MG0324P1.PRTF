      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MG Fixed loan/deposit liquidation error')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MESSAGE GENERATION
     A*                                                               *
     A*   MG0324P1 - MT324 LIQUIDATION ERROR REPORT                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. 00000              Date 00XXX00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R HEADER
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                  3  7'REFERENCE MG0324'
     A*/COPY WNCPYSRC,MG0324P101
     A            BJURPT        53      3 45
     A                                  3101'DATE'
     A            BJMRDT         7      3106
     A*/COPY WNCPYSRC,MG0324P102
     A                                  3119'PAGE'
     A                                  3124PAGNBR EDTCDE(Z)
     A          R TEXT                      SKIPB(5)
     A N30                                 7'THE FOLLOWING MT324 - FIXED +
     A                                      LOAN/DEPOSIT LIQUIDATION +
     A                                      MESSAGE COULD NOT BE +
     A                                      CORRECTLY GENERATED'
     A                                      SPACEA(2)
     A  30                                 7'THE FOLLOWING MT324 - FIXED +
     A                                      LOAN/DEPOSIT LIQUIDATION +
     A                                      MESSAGE DID NOT GENERATE A +
     A                                      FEEDBACK RECORD'
     A                                      SPACEA(2)
     A                                     7'SENDER'
     A            MGBRCA         3        20
      * Begin /CSD027/
     A**********  SECN      R             25                                                  CSD027
     A            SECN           6A       25TEXT('Customer no of sender')
      * End /CSD027/
     A            NWSN      R             33
     A                                      SPACEA(2)
     A                                     7'DESTINATION'
      * Begin /CSD027/
     A**********  DECN      R             25EDTCDE(Z)                                         CSD027
     A            DECN           6A       25TEXT('Customer no of destination')
      * End /CSD027/
     A            NWDS      R             33
     A            DSERR         50        60
     A                                      SPACEA(2)
     A                                     7'NETWORK'
     A            NWRK      R             25
     A                                      SPACEA(2)
     A                                     7'MODULE ID'
     A            MGMODI         2        25
     A                                      SPACEA(2)
     A                                     7'TRANSACTION NO.'
     A            MGTNUM        15        25
     A                                      SPACEA(2)
     A*
     A          R LINE
     A            MTAG           5         2
     A            MFLD          50        10
     A            MERR          50        70
     A                                      SPACEA(1)
      *
     A          R FEED
     A            TRNO      R             10
     A                                    27'FEEDBACK RECORD FOR THE +
     A                                      CONFIRMATION MESSAGE NOT +
     A                                      GENERATED'
      *
     A          R EOREPT                    SKIPB(43)
     A                                    55'*** END OF REPORT ***'
      *
