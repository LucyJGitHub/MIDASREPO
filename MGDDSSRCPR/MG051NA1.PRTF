/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MG MT51N outgoing messages generated')
      *****************************************************************
      *                                                               *
      *  Midas - MESSAGE GENERATION                                   *
      *                                                               *
      *  PRTF/MG051NA1 - Outgoing S.W.I.F.T. Securities               *
      *                  Messages Generated                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW098  *CREATE    Date 17Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW098 - SWIFT 1998 changes.                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER1                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                  3  7'REFERENCE MG051NA1'
     A            BJURPT    R           3 40REFFLD(URPT)
     A                                  3101'DATE'
     A            BJMRDT    R           3106REFFLD(MRDT)
     A                                  3119'PAGE'
     A                                  3124PAGNBR EDTCDE(Z)
      *
     A                                  5 42'OUTGOING S.W.I.F.T. SECURITIES +
     A                                       MESSAGES GENERATED'
      *
     A                                  6 42'-------------------------------+
     A                                       ------------------'
      *
      *----------------------*
      *** Main Text Heading  *
      *----------------------*
     A          R TEXT1
      *
     A                                      SPACEB(2)
     A                                     7'THE FOLLOWING MT'
     A            R1MTYP         3        23
     A                                    26' - CONFIRMATION OF PURCHASE OR +
     A                                      SALE MESSAGE CORRECTLY GENERATED +
     A                                      FOR'
     A                                      SPACEA(2)
      *
     A                                    10'TRADE REFERENCE'
     A            R1REFE         6  0     29
     A                                    44'TRADE TYPE'
     A            R1TYP1         2        58
     A            R1TYP2        20        62
     A                                      SPACEA(2)
      *
     A                                    32'SENDER'
     A            R1SND1         3        42
     A            R1SND2         6        47
     A            R1SND3        20        55
     A            R1SRNM        20        73
     A            R1SRTN        10        95
     A                                      SPACEA(1)
      *
     A                                    32'PRIORITY'
     A            R1PRTY         2        47
     A                                      SPACEA(1)
      *
     A                                    32'DESTINATION'
      * Begin /CSD027/
     A**********  R1DES1         6  0     47                                                  CSD027
     A            R1DES1         6A       47
      * End /CSD027/
     A            R1DES2        20        55
     A            R1DRNM        20        73
     A            R1DRTN        10        95
     A                                      SPACEA(2)
      *
     A                                    36'NETWORK'
     A            R1NETW         6        47
     A                                      SPACEA(2)
      *
      *----------------*
      *** Detail Line  *
      *----------------*
     A          R LINE1
     A                                      SPACEB(1)
     A            MDES          36         7
     A            MTAG           5        47
     A            RDAT          50        55
     A                                      SPACEA(1)
      *
      *------------------------*
      *** No Details to Report *
      *------------------------*
     A          R NODETS                    SKIPB(20)
     A                                    50'*** NO DETAILS TO REPORT ***'
      *
      *-----------------*
      *** Trailer Line  *
      *-----------------*
     A          R EOREPT1
     A                                      SPACEB(3)
     A                                    55'*** END OF REPORT ***'
