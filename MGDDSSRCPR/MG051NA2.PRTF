/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MG MT51N outgoing messages not generated')
      *****************************************************************
      *                                                               *
      *  Midas - MESSAGE GENERATION                                   *
      *                                                               *
      *  PRTF/MG051NA2 - Outgoing S.W.I.F.T. Securities               *
      *                  Messages not Generated                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW098  *CREATE    Date 17Apr98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW098 - SWIFT 1998 changes.                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER2                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                  3  7'REFERENCE MG051NA2'
     A            BJURPT    R           3 40REFFLD(URPT)
     A                                  3101'DATE'
     A            BJMRDT    R           3106REFFLD(MRDT)
     A                                  3119'PAGE'
     A                                  3124PAGNBR EDTCDE(Z)
      *
     A                                  5 42'OUTGOING S.W.I.F.T. SECURITIES +
     A                                       MESSAGES NOT GENERATED'
      *
     A                                  6 42'-------------------------------+
     A                                       ------------------'
      *
      *----------------------*
      *** Main Test Heading  *
      *----------------------*
     A          R TEXT2
      *
     A                                      SPACEB(2)
     A                                     7'THE FOLLOWING MT'
     A            R2MTYP         3        23
     A                                    26' - CONFIRMATION OF PURCHASE OR +
     A                                      SALE MESSAGE NOT CORRECTLY +
     A                                      GENERATED FOR'
     A                                      SPACEA(2)
      *
     A                                     7'TRADE REFERENCE'
     A            R2REFE         6  0     29
     A                                    41'TRADE TYPE'
     A            R2TYP1         2        58
     A            R2TYP2        20        62
     A                                      SPACEA(2)
      *
     A                                     7'SENDER'
     A            R2SND1         3        20
     A            R2SND2         6        25
     A            R2SND3        20        33
     A                                      SPACEA(1)
      *
     A                                     7'PRIORITY'
     A            R2PRTY         2        25
     A                                      SPACEA(1)
      *
     A                                     7'DESTINATION'
      * Begin /CSD027/
     A**********  R2DES1         6  0     25                                                  CSD027
     A            R2DES1         6A       25
      * End /CSD027/
     A            R2DES2        20        33
     A            R2EMSG        50        60
     A                                      SPACEA(2)
      *
     A                                     7'NETWORK'
     A            R2NETW         6        25
     A            R2NWER        50        60
     A                                      SPACEA(2)
      *
     A                                     7'MODULE ID'
     A            R2MODI         2        25
     A                                      SPACEA(2)
      *
     A                                     7'TRANSACTION NO.'
     A            R2TNUM        15        25
     A                                      SPACEA(2)
      *
      *-----------------*
      *** Detail Line   *
      *-----------------*
     A          R LINE2
     A            MTAG           5         2
     A            RDAT          50        10
     A            MERR          50        70
     A                                      SPACEA(1)
      *
      *-----------------*
      *** Trailer Line  *
      *-----------------*
     A          R EOREPT2
     A                                      SPACEB(3)
     A                                    55'*** END OF REPORT ***'
