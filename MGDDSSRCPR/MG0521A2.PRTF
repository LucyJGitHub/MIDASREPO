/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MG MT521 outgoing messages not generated')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MESSAGE GENERATION
     A*                                                               *
     A****MG0521P2*-*OUTGOING*S.W.I.F.T.*SECURITIES********************   091250
     A*   MG0521A2 - OUTGOING S.W.I.F.T. SECURITIES                   *   091250
     A*              MESSAGES NOT GENERATED                           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. 091250             Date 24Feb95               *
     A*                 S01401             Date 18Jun93               *
     A*                                                               *
     A*****************************************************************
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  091250  - MG0521P2 changed to MG0521A2 as this is an audit   *
      *            report, and AU reports should go to MPRINTAU so    *
      *            change to standard creation parameters.            *
      *  S01401  - WRITTEN FOR MT5XX MESSAGES GENERATION              *
      *****************************************************************
     A*----------------------------------------------------------------
     A* HEADING
     A*----------------------------------------------------------------
     A          R HEADER2
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                              Begin 091250
     A************                      3  7'REFERENCE MG0521P2'
     A                                  3  7'REFERENCE MG0521A2'
     A*                                              End   091250
     A            BJURPT        53      3 40
     A                                  3101'DATE'
     A            BJMRDT         7      3106
     A                                  3119'PAGE'
     A                                  3124PAGNBR EDTCDE(Z)
     A*
     A                                  5 42'OUTGOING S.W.I.F.T. SECURITIES +
     A                                       MESSAGES NOT GENERATED'
     A*
     A                                  6 42'-------------------------------+
     A                                       ------------------'
     A*
     A*----------------------------------------------------------------
     A* MAIN TEXT HEADING
     A*----------------------------------------------------------------
     A          R TEXT2
     A*
     A                                      SPACEB(2)
     A                                     7'THE FOLLOWING MT521 - RECEIVE +
     A                                      AGAINST PAYMENT MESSAGE NOT +
     A                                      CORRECTLY GENERATED FOR'
     A                                      SPACEA(2)
     A*
     A  82                                 7'TRADE REFERENCE'
     A  83                                 7'MOVEMENT REFERENCE'
     A            R2REFE         6  0     29
     A  82                                41'TRADE TYPE'
     A  83                                41'MOVEMENT TYPE'
     A            R2TYP1         2        58
     A            R2TYP2        20        62
     A                                      SPACEA(2)
     A*
     A                                     7'SENDER'
     A            R2SND1         3        20
     A            R2SND2         6        25
     A            R2SND3        20        33
     A                                      SPACEA(2)
     A*
     A                                     7'DESTINATION'
      * Begin /CSD027/
     A**********  R2DES1         6  0     25                                                  CSD027
     A            R2DES1         6A       25
      * End /CSD027/
     A            R2DES2        20        33
     A            R2EMSG        50        60
     A                                      SPACEA(2)
     A*
     A                                     7'NETWORK'
     A            R2NETW         6        25
     A            R2NWER        50        60
     A                                      SPACEA(2)
     A*
     A                                     7'MODULE ID'
     A            R2MODI         2        25
     A                                      SPACEA(2)
     A*
     A                                     7'TRANSACTION NO.'
     A            R2TNUM        15        25
     A                                      SPACEA(2)
     A*
     A*----------------------------------------------------------------
     A* DETAIL LINE
     A*----------------------------------------------------------------
     A          R LINE2
     A            MTAG           5         2
     A            RDAT          50        10
     A            MERR          50        70
     A                                      SPACEA(1)
     A*
     A*----------------------------------------------------------------
     A* TRAILER LINE
     A*----------------------------------------------------------------
     A          R EOREPT2
     A                                      SPACEB(3)
     A                                    55'*** END OF REPORT ***'
