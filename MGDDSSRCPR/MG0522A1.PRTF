/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MG MT522 outgoing msgs generated')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MESSAGE GENERATION
     A*                                                               *
     A****MG0522P1*-*OUTGOING*S.W.I.F.T.*SECURITIES********************   091250
     A*   MG0522A1 - OUTGOING S.W.I.F.T. SECURITIES                   *   091250
     A*              MESSAGES GENERATED                               *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 091250             Date 24Feb95               *
     A*                 S01401             Date 18Jun93               *
     A*                                                               *
     A*****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  091250  - MG0522P1 changed to MG0522A1 as this is an audit   *
      *            report, and AU reports should go to MPRINTAU so    *
      *            change to standard creation parameters.            *
      *  S01401  - WRITTEN FOR MT5XX MESSAGES GENERATION              *
      *****************************************************************
     A*----------------------------------------------------------------
     A* HEADING
     A*----------------------------------------------------------------
     A          R HEADER1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*                                              Begin 091250
     A************                      3  7'REFERENCE MG0522P1'
     A                                  3  7'REFERENCE MG0522A1'
     A*                                              End   091250
     A            BJURPT        53      3 40
     A                                  3101'DATE'
     A            BJMRDT         7      3106
     A                                  3119'PAGE'
     A                                  3124PAGNBR EDTCDE(Z)
     A*
     A                                  5 42'OUTGOING S.W.I.F.T. SECURITIES +
     A                                       MESSAGES GENERATED'
     A*
     A                                  6 42'-------------------------------+
     A                                       ------------------'
     A*
     A*----------------------------------------------------------------
     A* MAIN TEXT HEADING
     A*----------------------------------------------------------------
     A          R TEXT1
     A*
     A                                      SPACEB(2)
     A                                     7'THE FOLLOWING MT522 - DELIVER +
     A                                      FREE MESSAGE +
     A                                      CORRECTLY GENERATED FOR'
     A                                      SPACEA(2)
     A*
     A  80                                10'TRADE REFERENCE'
     A  81                                 7'MOVEMENT REFERENCE'
     A            R1REFE         6  0     29
     A  80                                44'TRADE TYPE'
     A  81                                41'MOVEMENT TYPE'
     A            R1TYP1         2        58
     A            R1TYP2        20        62
     A                                      SPACEA(2)
     A*
     A                                    32'SENDER'
     A            R1SND1         3        42
     A            R1SND2         6        47
     A            R1SND3        20        55
     A*M5T054/START
     A            R1SRNM        20        73
     A            R1SRTN        10        95
     A*M5T054/END
     A                                      SPACEA(1)
     A*
     A                                    32'PRIORITY'
     A            R1PRTY         2        47
     A                                      SPACEA(1)
     A*
     A                                    32'DESTINATION'
      * Begin /CSD027/
     A**********  R1DES1         6  0     47                                                  CSD027
     A            R1DES1         6A       47
      * End /CSD027/
     A            R1DES2        20        55
     A*M5T054/START
     A            R1DRNM        20        73
     A            R1DRTN        10        95
     A*M5T054/END
     A                                      SPACEA(2)
     A*
     A                                    36'NETWORK'
     A            R1NETW         6        47
     A                                      SPACEA(3)
     A*
     A*----------------------------------------------------------------
     A* DETAIL LINE
     A*----------------------------------------------------------------
     A          R LINE1
     A            MDES          36         7
     A            MTAG           5        47
     A            RDAT          50        55
     A                                      SPACEA(1)
     A*
     A*----------------------------------------------------------------
     A* NO DETAILS TO REPORT
     A*----------------------------------------------------------------
     A          R NODETS                    SKIPB(20)
     A                                    50'*** NO DETAILS TO REPORT ***'
     A*
     A*----------------------------------------------------------------
     A* TRAILER LINE
     A*----------------------------------------------------------------
     A          R EOREPT1
     A                                      SPACEB(3)
     A                                    55'*** END OF REPORT ***'
