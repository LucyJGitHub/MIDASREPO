      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MG MT565 Outgoing Message Generated Report')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/MG0565A1 - MT565 Outgoing SWIFT Message Generated       *
      *                  Report                                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Last Amend No. CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEAD1P1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
      *
     A                                  3  6'Reference MG0565A1'
     A            BJURPT    R           3 37REFFLD(URPT)
     A                                  3101'Date'
     A            BJMRDT    R           3106REFFLD(MRDT)
     A                                  3119'Page'
     A                                  3124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  4101'Time'
     A                                  4106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  5 42'Outgoing S.W.I.F.T. Securities +
     A                                       Messages Generated'
      *
     A                                  6 42'-------------------------------+
     A                                       ------------------'
      *
     A                                  8  7'The following MT'
     A            R1MTYP         3A     8 23
     A                                  8 26' - Treaty Withholding Tax +
     A                                      message correctly generated +
     A                                      for'
      *
      *----------------------*
      ***  Main Text Heading *
      *----------------------*
      *
     A          R HEAD1P2
      *
     A                                      SPACEB(2)
     A                                     6'Security'
     A            R1RSECT       10A       23
     A                                    50'Date of Payment'
     A            R1RDATE        6S       67EDTWRD('  /  /  ')
     A                                    85'Event Type'
     A            R1REVNT        2A       98
     A                                      SPACEA(1)
      *
     A                                     6'Counterparty'
     A            R1CTPY         6A       23
     A                                      SPACEA(2)
      *
     A                                     6'Destination'
     A            R1DES1         6        23
     A            R1DES2        20        31
     A            R1DRNM        20        53
     A            R1DRTN        10        75
     A                                      SPACEA(1)
      *
     A                                     6'Network'
     A            R1NETW         6        23
     A                                      SPACEA(2)
      *
      *----------------*
      *** Detail Line  *
      *----------------*
      *
     A          R DETL1
     A                                      SPACEB(1)
     A            R1MDES        36         7
     A            R1MTAG         5        47
     A            R1RDAT        50        55
     A                                      SPACEA(1)
      *
      *------------------------*
      *** No Details to Report *
      *------------------------*
     A          R NODETS1                   SKIPB(20)
     A                                    50'*** No Details To Report ***'
      *
      *-----------------*
      *** Trailer Line  *
      *-----------------*
      *
     A          R EOREPT1
     A                                      SPACEB(3)
     A                                    55'*** End Of Report ***'
