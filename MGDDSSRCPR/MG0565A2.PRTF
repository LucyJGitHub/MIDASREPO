      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MG MT565 Outgoing Message not Generated Rpt')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  PRTF/MG0565A2 - MT565 Outgoing SWIFT Message not             *
      *                  Generated Report                             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 12Jan06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CSE023  *CREATE    Date 12Jul00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE023 - Treaty Withholding Tax                              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEAD2P1                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
      *
     A                                  3  6'Reference MG0565A2'
     A            BJURPT    R           3 37REFFLD(URPT)
     A                                  3101'Date'
     A            BJMRDT    R           3106REFFLD(MRDT)
     A                                  3119'Page'
     A                                  3124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  4101'Time'
     A                                  4106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  5 42'Outgoing S.W.I.F.T. Securities +
     A                                       Messages not Generated'
      *
     A                                  6 42'-------------------------------+
     A                                       ----------------------'
      *
     A                                  8  7'The following MT'
     A            R2MTYP         3      8 23
     A                                  8 26' - Treaty Withholding Tax +
     A                                      message not correctly +
     A                                      generated for'
      *
      *----------------------*
      ***  Main Text Heading *
      *----------------------*
      *
     A          R HEAD2P2
      *
     A                                      SPACEB(2)
     A                                     6'Security'
     A            R2RSECT       10A       23
     A                                    50'Date of Payment'
     A            R2RDATE        6S       67EDTWRD('  /  /  ')
     A                                    85'Event Type'
     A            R2REVNT        2A       98
     A                                      SPACEA(1)
      *
     A                                     6'Counterparty'
     A            R2CTPY         6A       23
     A                                      SPACEA(2)
      *
     A                                     6'Destination'
      * Begin /CSD027/
     A**********  R2DES1         6  0     23                                                  CSD027
     A            R2DES1         6A       23
      * End /CSD027/
     A            R2DES2        20        31
     A            R2EMSG        50        58
     A                                      SPACEA(1)
      *
     A                                     6'Network'
     A            R2NETW         6        23
     A            R2NWER        50        58
     A                                      SPACEA(2)
      *
      *----------------*
      *** Detail Line  *
      *----------------*
      *
     A          R DETL2
     A                                      SPACEB(1)
     A            R2MTAG         5         2
     A            R2RDAT        50        10
     A            R2MERR        50        70
     A                                      SPACEA(1)
      *
      *------------------------*
      *** No Details to Report *
      *------------------------*
     A          R NODETS2                   SKIPB(20)
     A                                    50'*** No Details To Report ***'
      *
      *-----------------*
      *** Trailer Line  *
      *-----------------*
      *
     A          R EOREPT2
     A                                      SPACEB(3)
     A                                    55'*** End Of Report ***'
