/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MG MT579 outgoing messages generated')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MESSAGE GENERATION
     A*                                                               *
     A****MG0579P1*-*OUTGOING*S.W.I.F.T.*SECURITIES********************   091250
     A*   MG0579A1 - OUTGOING S.W.I.F.T. SECURITIES                   *   091250
     A*              MESSAGES GENERATED                               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*   LAST AMEND NO. 091250            DATE 24FEB95               *
     A*   PREV AMEND NO. S01401            DATE 18JUN93               *
     A*                                                               *
     A*****************************************************************
      *                                                               *
      *  091250  - MG0579P1 changed to MG0579A1 as this is an audit   *
      *            report, and AU reports should go to MPRINTAU so    *
      *            change to standard creation parameters.            *
      *   S01401 - WRITTEN FOR MT5XX MESSAGES GENERATION              *
      *****************************************************************
     A*----------------------------------------------------------------
     A* HEADING
     A*----------------------------------------------------------------
     A          R HEADER1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                              Begin 091250
     A************                      3  7'REFERENCE MG0579P1'
     A                                  3  7'REFERENCE MG0579A1'
     A*                                              End   091250
     A            BJURPT        53      3 40
     A                                  3101'DATE'
     A            BJMRDT         7      3106
     A                                  3119'PAGE'
     A                                  3124PAGNBR EDTCDE(Z)
     A*
     A                                  5 42'OUTGOING S.W.I.F.T. SECURITIES +
     A                                       MESSAGES GENERATED'
     A*
     A                                  6 42'-------------------------------+
     A                                       ------------------'
     A                                  7  1' '
     A                                  8  1' '
     A*
     A*----------------------------------------------------------------
     A* MAIN TEXT HEADING
     A*----------------------------------------------------------------
     A          R TEXT1
     A*
     A                                     7'THE FOLLOWING MT579 - CERTIFCATE +
     A                                      NUMBERS MESSAGE CORRECTLY +
     A                                      GENERATED FOR'
     A                                      SPACEA(2)
     A*
     A                                    10'TRADE REFERENCE'
     A            R1REFE         6        29
     A                                    44'TRADE TYPE'
     A            R1TYP1         2        58
     A            R1TYP2        20        62
     A                                      SPACEA(2)
     A*
     A                                    32'SENDER'
     A            R1SND1         3        42
     A            R1SND2         6        47
     A            R1SND3        20        55
     A*M5T054/START
     A            R1SRNM        20        73
     A            R1SRTN        10        95
     A*M5T054/END
     A                                      SPACEA(1)
     A*
     A                                    32'PRIORITY'
     A            R1PRTY         2        47
     A                                      SPACEA(1)
     A*
     A                                    32'DESTINATION'
     A            R1DES1         6        47
     A            R1DES2        20        55
     A*M5T054/START
     A            R1DRNM        20        73
     A            R1DRTN        10        95
     A*M5T054/END
     A                                      SPACEA(2)
     A*
     A                                    36'NETWORK'
     A            R1NETW         6        47
     A                                      SPACEA(3)
     A*
     A*----------------------------------------------------------------
     A* DETAIL - :28:, :20:, :21: - LINE
     A*----------------------------------------------------------------
     A          R DETL1
     A            R1MDES        36         7
     A            R1MTAG         5        47
     A            R1MDAT        50        55
     A                                      SPACEA(1)
     A*
     A*----------------------------------------------------------------
     A* DETAIL LINE
     A*----------------------------------------------------------------
     A          R LINE1
     A            MDES          36         7
     A            WWMTAG         5        47
     A            MDAT          50        55
     A                                      SPACEA(1)
     A*
     A*----------------------------------------------------------------
     A* NO DETAILS TO REPORT
     A*----------------------------------------------------------------
     A          R NODETS                    SKIPB(20)
     A                                    50'*** NO DETAILS TO REPORT ***'
     A*
     A*----------------------------------------------------------------
     A* TRAILER LINE
     A*----------------------------------------------------------------
     A          R EOREPT1
     A                                      SPACEB(3)
     A                                    55'*** END OF REPORT ***'
