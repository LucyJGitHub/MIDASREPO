/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MG MT579 outgoing messages not generated')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MESSAGE GENERATION
     A*                                                               *
     A****MG0579P2*-*OUTGOING*S.W.I.F.T.*SECURITIES********************   091250
     A*   MG0579A2 - OUTGOING S.W.I.F.T. SECURITIES                   *   091250
     A*              MESSAGES NOT GENERATED                           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*   LAST AMEND NO. 091250            DATE 24FEB95               *
     A*   PREV AMEND NO. S01401            DATE 18JUN93               *
     A*                                                               *
     A*****************************************************************
      *                                                               *
      *  091250  - MG0579P1 changed to MG0579A1 as this is an audit   *
      *            report, and AU reports should go to MPRINTAU so    *
      *            change to standard creation parameters.            *
      *  S01401  - WRITTEN FOR MT5XX MESSAGES GENERATION              *
      *****************************************************************
     A*----------------------------------------------------------------
     A* HEADING
     A*----------------------------------------------------------------
     A          R HEADER2
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                              Begin 091250
     A************                      3  7'REFERENCE MG0579P2'
     A                                  3  7'REFERENCE MG0579A2'
     A*                                              End   091250
     A            BJURPT        53      3 40
     A                                  3101'DATE'
     A            BJMRDT         7      3106
     A                                  3119'PAGE'
     A                                  3124PAGNBR EDTCDE(Z)
     A*
     A                                  5 42'OUTGOING S.W.I.F.T. SECURITIES +
     A                                       MESSAGES NOT GENERATED'
     A*
     A                                  6 42'-------------------------------+
     A                                       ------------------'
     A                                  7  1' '
     A                                  8  1' '
     A*
     A*----------------------------------------------------------------
     A* MAIN TEXT HEADING
     A*----------------------------------------------------------------
     A          R TEXT2
     A*
     A                                     7'THE FOLLOWING MT579 - CERTIFCATE +
     A                                      NUMBERS MESSAGE NOT CORRECTLY +
     A                                      GENERATED FOR'
     A                                      SPACEA(2)
     A*
     A                                    10'TRADE REFERENCE'
     A            R2REFE         6        29
     A                                    44'TRADE TYPE'
     A            R2TYP1         2        58
     A            R2TYP2        20        62
     A                                      SPACEA(2)
     A*
     A                                     7'SENDER'
     A            R2SND1         3        20
     A            R2SND2         6        25
     A            R2SND3        20        33
     A                                      SPACEA(2)
     A*
     A                                     7'DESTINATION'
     A            R2DES1         6        25
     A            R2DES2        20        33
     A            R2EMSG        50        60
     A                                      SPACEA(2)
     A*
     A                                     7'NETWORK'
     A            R2NETW         6        25
     A            R2NWER        50        60
     A                                      SPACEA(2)
     A*
     A                                     7'MODULE ID'
     A            R2MODI         2        25
     A                                      SPACEA(2)
     A*
     A                                     7'TRANSACTION NO.'
     A            R2TNUM        15        25
     A                                      SPACEA(2)
     A*
     A*----------------------------------------------------------------
     A* DETAIL LINE - :28:, :20:, :21:
     A*----------------------------------------------------------------
     A          R DETL2
     A            R2MTAG         5         2
     A            R2MDAT        50        10
     A            R2MERR        50        70
     A                                      SPACEA(1)
     A*
     A*----------------------------------------------------------------
     A* DETAIL LINE
     A*----------------------------------------------------------------
     A          R LINE2
     A            WWMTAG         5         2
     A            MDAT          50        10
     A            MERR          50        70
     A                                      SPACEA(1)
     A*
     A*----------------------------------------------------------------
     A* TRAILER LINE
     A*----------------------------------------------------------------
     A          R EOREPT2
     A                                      SPACEB(3)
     A                                    55'*** END OF REPORT ***'
