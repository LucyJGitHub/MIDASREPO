     H
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MG MT5xx - to switch on feature S01401')         *
/*OVRF*: OVRDBF FILE(DPTMVX) TOFILE(DPTMVDL1) SHARE(*NO)            : *
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *    RPG/DPTMVDU - CHECK FOR DUPLICATE DEPOT REFERENCE          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CDL099             Date 06Oct17               *
      *                 CGL165             Date 17Feb15               *
      *                 CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      *                 CRE073             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER054             Date 07Apr09               *
      *                 CER048             Date 19May08               *
      *                 CER020             Date 19May08               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 07Jul06               *
      *                 CDL038             Date 10May05               *
      *                 CAS009             Date 16May05               *
      *                 CDL028             Date 07Feb05               *
      *                 CAS008             Date 16Jun04               *
      *                 CGL014             Date 20Oct03               *
      *                 222727             Date 05Nov03               *
      *                 217684             Date 12May03               *
      *                 CAS006             Date 21Jan03               *
      *                 CAS005             Date 16Dec02               *
      *                 CAS004             Date 26Jun02               *
      * Midas Release 4.01.01 ----------------------------------------*
      *                 207006             Date 18Jun02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP056             Date 13Mar02               *
      *                 CIR008             Date 13Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CIR005             Date 21Jan00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE015             Date 06Dec99               *
      *                 CSE017             Date 20Oct99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP003             Date 17May98               *
      *                 S01401             Date 06May93               *
      *                                                               *
      *    THIS PROGRAM MUST BE RUN IF CLIENT HAS BROUGHT THE SWITCH -*
      *    ABLE FEATURE. THE PROGRAM RUNS OFF DPTMVD CHECKING  FOR    *
      *    DUPLICATE DEPOT MOVEMENT REF. WHEN FOUND, A NEW DEPOT      *
      *    MOVEMENT REF IS GENERATED . THE FOLLOWING FILES FILL ALSO  *
      *    HAVE ANY REFERENCE TO THE DUPLICATE, UPDATED WITH THE NEW  *
      *    DEPOT MOVEMENT REF.                                        *
      *    1/ DEALSDC.                                                *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSD103 - LIBOR DEREGULATION - COMMON LAYER/STANDING DATA     *
      *           (Recompile)                                         *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL099 - Split Value Date (Recompile)                        *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER054 - German Features - Church Tax (Recompile)            *
      *  CER048 - German Features - Taxes (Recompile)                 *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus (Recompile)                              *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CAS009 - EIR/AC Accounting (Recompile)                       *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  CAS008 - IAS 39 Enhancements (Recompile)                     *
      *  CGL014 - Collaterals Processing (Recompile)                  *
      *  222727 - Release 5.0 errors  (Recompile)                     *
      *  217684 - Authorisation User Malfunction (Recompile)          *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *           (Recompile)                                         *
      *  CAS004 - Hedge Accounting Phase A (Recompile)                *
      *  207006 - Add Counterparty & Market Centre to SSI (Recompile) *
      *  CAP056 - Automatic Authorisation of Interface deals          *
      *           (Recompile)                                         *
      *  CIR008 - FRA/IRS Deals Authorisation (Recompile)             *
      *  CDL008 - Continuous Linked Settlement (Recompiled)           *
      *  CIR005 - FRA/IRS Business Day Conventions to DEALSDG         *
      *           (recompile)                                         *
      *  CSE015 - Forward Valued Depot Movements (Recompiled)         *
      *  CSE017 - Cum/Ex Indicator on Depot Movements (Recompile)     *
      *  CAP003 - Conversion of SE inputs into modular structure to   *
      *           use as APIs. Recompiled to adopt changes in DPTMVD. *
      *    S01401 - MT5XX MESSAGES GENERATION                         *
      *                                                               *
      *****************************************************************
      *
     FDPTMVDL1UF  E           K        DISK
     FDPTMVX  IF  E           K        DISK
     F            DPTMVDF                           KRENAMEDPTMVDX
     FDEALS   UF  E           K        DISK
     F            DEALSDAF                          KIGNORE
     F            DEALSDBF                          KIGNORE
     F            DEALSDDF                          KIGNORE
     F            DEALSDEF                          KIGNORE
     F            DEALSDGF                          KIGNORE
     F            DEALSDFF                          KIGNORE
      *
     FDPTMVDP1O   E             06     PRINTER
      *
      *****************************************************************
     F/EJECT
      *****************************************************************
      *
      ** ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
      *
      *****************************************************************
     E/EJECT
      *****************************************************************
      *
      *
     I** RENAME DUPLICATE FIELDS
     IDPTMVDX
     I              RECI                            DXRECI
     I              DPSS                            DXDPSS
     I              DPRN                            DXDPRN
     I              DPMV                            DXDPMV
     I              DPID                            DXDPID
     I              DPMD                            DXDPMD
     I              DPOD                            DXDPOD
     I              DPDO                            DXDPDO
     I              DPBA                            DXDPBA
     I              ORBR                            DXORBR
     I              DPBK                            DXDPBK
     I              DPMK                            DXDPMK
     I              DPBN                            DXDPBN
     I              DPNA                            DXDPNA
     I              DPNR                            DXDPNR
     I              MKTP                            DXMKTP
     I              DPAD                            DXDPAD
     I              SWRF                            DXSWRF
     I              SIOI                            DXSIOI
     I              DPMS                            DXDPMS
     I              DCFR                            DXDCFR
     I              TACI                            DXTACI
     I              DPR1                            DXDPR1
     I              DPR2                            DXDPR2
     I              ORED                            DXORED
     I              LCD                             DXLCD
     I              CHTP                            DXCHTP
     I              TNLU                            DXTNLU
     I              RPIN                            DXRPIN
     I              DPPC                            DXDPPC
      *
      ** LOCAL DATA AREA FOR DATA BASE ERRORS
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     ISDBANK    E DSSDBANKPD
      **  Securities Customer Details
      *
     IDSFDY     E DSDSFDY
      *
      **  FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
      *****************************************************************
     C/EJECT
      *****************************************************************
      *
      **  Initial processing
     C                     EXSR INIT
      *
      **  Main processing
     C                     EXSR MAIN
      *
      **  Termaination processing
     C                     MOVEL*ON       *INLR
     C                     RETRN
      *
     C/EJECT
      *****************************************************************
      *                                                               *
      **  INIT PROCESS                                                *
      *                                                               *
      *****************************************************************
     C           INIT      BEGSR
      *
      ***  Copyright Statement.
     C                     MOVEACPY@      BIS@   80
      *
      **  Set up LDA
     C                     Z-ADD0         DBASE
     C                     MOVEL*BLANKS   DBPGM
     C                     MOVEL*BLANKS   DBFILE
     C                     MOVEL*BLANKS   DBKEY
      *
      **  Access Bank details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      **  Initial Read of DPTMVR
     C                     MOVEL'*NONE '  PRVS    6
     C                     READ DPTMVDL1               0201
     C                     MOVELDPRN      CURR    6
      *
      **   If error
     C           *IN02     IFEQ *ON
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL'DPTMVDU 'DBPGM
     C                     Z-ADD01        DBASE            ************
     C                     MOVEL'DPTMVDL1'DBFILE           * DBER 001 *
     C                     MOVEL'        'DBKEY            ************
     C                     OUT  LDA
     C                     SETON                     LRU7U8
     C                     RETRN
     C                     ENDIF
      *
     C                     MOVEL'Y'       OCCUR   1
      *
      **  Setup Key KDPCU
     C           KDPCU     KLIST
     C                     KFLD           DPSS
     C                     KFLD           DPRN
      *
     C                     ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      **  MAIN PROCESS                                                *
      *                                                               *
      *****************************************************************
     C           MAIN      BEGSR
      *
     C           *IN01     DOWEQ*OFF
      *
     C           CURR      IFEQ PRVS
      *
      **  Create a new DEPOT reference value
     C                     MOVEL*OFF      *IN02
     C                     MOVELCURR      CALC    60
      *
     C           *IN02     DOWEQ*OFF
     C                     ADD  1         CALC
     C           CALC      MULT 7         NEW     60
     C                     MOVELNEW       CHECK   6
     C           CHECK     CHAINDPTMVX               02
     C                     ENDDO
      *
      **  Update DPRN with new value generated
     C                     MOVELNEW       DPRN
     C                     UPDATDPTMVDF
      *
      ** Update printer file with details
      ** Check over flow indicator & whether it's the first time round.
     C           *IN06     IFEQ *ON
     C           OCCUR     OREQ 'Y'
     C                     MOVEL'N'       OCCUR
     C                     WRITEHEADNG
     C                     ENDIF
      *
     C                     MOVELDPSS      SSDPSS
     C                     MOVELCURR      OLDREF
     C                     MOVELNEW       NEWREF
     C                     WRITEDETAIL
      *
      ** Access DEALSDC using associated deals number - If not blank.
     C                     EXSR DEAL
     C                     ENDIF
      *
     C                     MOVELCURR      PRVS
     C                     READ DPTMVDL1                 01
     C                     MOVELDPRN      CURR
     C                     ENDDO
      *
     C                     ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      **  DEAL PROCESS                                                *
      *                                                               *
      *****************************************************************
     C           DEAL      BEGSR
      *
     C           DPAD      IFNE *ZEROS
     C           DPAD      CHAINDEALS                05
     C           *IN05     IFEQ *OFF
     C                     MOVELNEW       DDMR
     C                     UPDATDEALSDCF
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2001
