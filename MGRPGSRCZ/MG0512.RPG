     H        1                           F                               S01421
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MG Trade confirmation messages')                 *
     H******  1                                                           S01421
      *****************************************************************
      *                                                               *
      *  Midas - Message Generation Module                            *
      *                                                               *
      *   MG0512 - FORMAT MT512 REPORT MESSAGES                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 222373             Date 27Oct03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSW025             Date 26Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 141572             Date 21Sep98               *
      *                 113352             Date 13Feb97               *
      *                 122283             DATE 16FEB98               *
      *                 104606             DATE 19JUN96               *
      *                 094335             DATE 06JUN96               *
      *                 103449             DATE 21MAY96               *
      *                 103474             DATE 21MAY96               *
      *                 067617             DATE 10MAY96               *
      *                 069154             DATE 10MAY96               *
      *                 056532             DATE 09MAY96               *
      *                 057263             DATE 09MAY96               *
      *                 102916             DATE 08MAY96               *
      *                 078256             DATE 07MAY96               *
      *                 082282             DATE 02MAY96               *
      *                 CSW003             DATE 09JAN96               *
      *                 091250             DATE 24FEB95               *
      *                 080096             DATE 12JAN95               *
      *                 067522             DATE 28FEB94               *
      *                 S01421             DATE 18AUG93               *
      *                   S01401           DATE 17JUN93               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  222373 - Parameter Mismatch                                  *
      *  CSW025 - Midas Message Manager                               *
      *           - Pass original change type                         *
      *  CDL008 - Continuous Linked Settlement (Recompiled)           *
      *  141572 - Setup CINDV (century flag) SVDT ( value date).      *
      *  113352 - Set-up CIND (Century flag)  to correct year2000     *
      *           display problem.                                    *
      *  122283 - Blank line in MGOMSGPD if account number is blank   *
      *           and not account line.                               *
      *  104606 - Bought should be Sold and vice-versa in some cases  *
      *  094335 - MT5xx cancellation process fix                      *
      *  103449 - Allow confirmation message for 'PD' and 'PL'        *
      *  103474 - Pgm alwasy falls over with OPEN to opened file      *
      *  067617 - Invalid Price formatting for Depots Movements       *
      *  069154 - To print customer shortname and town on line :87a:  *
      *  056532 - Process incorrect car. '-' ' ' ':' in first position*
      *  057263 - All postiion in field "MTAG" need to be full.       *
      *  102916 - Field :33T: 'YLD' instead 'REN' if trade basis is   *
      *           'Y' = yield basis. Price field will already conatin *
      *           the 'Price/Discount/Yield' value.                   *
      *  078256 - Use nominal decimal places from file                *
      *  082282 - Set message status according to customer details    *
      *  CSW003 - MT5XX Message Generation - Phase 2.                 *
      *  091250 - Print Files MG0512P1/P2 renamed to MG0512A1/A2 as   *
      *           these are audit reports.                            *
      *  080096 - RECOMPILE BECAUSE COPY MEMBER MGCPYSRC,SWIFTTRANS   *
      *           CHANGED                                             *
      *   067522 - Msg 23 should be at sender point of view.          *
      *            Therefore 'TS' is SOLD and 'TP' is BOUGHT.         *
      *   S01421 - REPLACE THE SWIFT TRANSLATION TABLE WITH A         *
      *              /COPY STATEMENT (SWIFTTRANS).                    *
      *   S01401 - MT500 messages for Release 10; new program.        *
      *                                                               *
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *****************************************************************
     F*
     FMGF512PDUF  E                    DISK         KINFSR *PSSR
     F                                              KCOMIT
     FSDBANKPDIF  E                    DISK         KINFSR *PSSR
     FSDBRCHL1IF  E           K        DISK         KINFSR *PSSR
     FSDCURRL1IF  E           K        DISK         KINFSR *PSSR
     F/COPY WNCPYSRC,MG0512F001
     FSDMGMEPDIF  E                    DISK         KINFSR *PSSR
     F*
     FMGOREFPDO   E                    DISK         KCOMIT
     F                                              KINFSR *PSSR
     FMGOMSGPDO   E                    DISK         KCOMIT
     F                                              KINFSR *PSSR
     F*G0512P1O***E********************PRINTER******KINFDS*OVRFL1         091250
     FMG0512A1O   E                    PRINTER      KINFDS OVRFL1         091250
     F                                              KINFSR *PSSR
     F*G0512P2O***E********************PRINTER******KINFDS*OVRFL2     UC  091250
     F*G0512A2O***E******              PRINTER      KINFDS OVRFL2  091250 103474
     FMG0512A2O   E                    PRINTER      KINFDS OVRFL2     UC  103474
     F                                              KINFSR *PSSR
     FMG0512AUO   E                    PRINTER                        UC
     F*
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     E*
     E** ARRAY USED FOR COPYRIGHT
     E                    CPY@    1   1 80
     E*
     E** ARRAY USED TO FORMAT ERROR MESSAGE FIELD ON MULTIPLE
     E** OCCURENCE DATA STRUCTURE
     E                    ERRA    1  15 50
     E*
     E** ARRAY USED TO FORMAT DESCRIPTION FIELD ON MULTIPLE
     E** OCCURENCE DATA STRUCTURE
     E                    DESA    1  23 36
     E*
     E** ARRAY USED TO REMOVE FORBIDDEN CHARACTERS (E.G. %)
     E                    FORB       50  1
     E*
     E** ARRAY USED TO FORMAT TRADE TYPE
     E** DESCRIPTION ON REPORTS
     E                    TYPA    1   4 20
     E*
      **  Array used to define fields to be suppressed
     E                    SUPP    1  50  5
      *                                                                   082282
      **  Array used to set message status                                082282
     E                    @ST        15  3                                082282
     E/COPY WNCPYSRC,MG0512E001
     E*
     E*****************************************************************
     E/EJECT
     E*****************************************************************
     I*
     I** Multiple occurrence data structure for message
     IMULT        DS                         80
     I                                        1  36 MDES
     I                                       37  41 MTAG
     I                                       42  91 MDAT
     I                                       92 141 MERR
     I                                      160 179 CRNM
     I                                      182 191 CRTN
     I                                      142 191 RDAT
     I*
     I** Local data area
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I** DATA STRUCTURE FOR CONSTRUCTING DATA FIELD
     IWWMDAT      DS                         50
     I                                        1   3 WD0103
     I                                        1   6 WD0106
     I                                        1  21 WD0121
     I                                        4   6 WD0406
     I                                        4  19 WD0419
     I                                        6  17 WD0617
     I                                        7   9 WD0709
     I                                        7  21 WD0721
     I                                        7  26 WD0726
     I                                       10  22 WD1026
     I                                       10  24 WD1024
     I                                        1   5 WD0105
     I                                        4   9 WD0409
     I                                       10  16 WD1016
     I                                        1  13 WD0113
     I                                        1   4 WD0104
     I                                        5  16 WD0516
     I*
     I** DATA STRUCTURE FOR CONSTRUCTING DATA FIELD FOR REPORT
     IWWRDAT      DS                         50
     I                                        1   6 WR0106
     I                                        7   9 WR0709
     I                                       10  30 WR1030
     I                                        1   8 WR0108
     I                                        9  11 WR0911
     I                                        9  28 WR0928
     I                                        1   3 WR0103
     I                                        4  24 WR0424
     I                                       12  32 WR1232
     I*
     I** DATA STRUCTURE FOR DBERROR
     IDBERR       DS                             20
     I                                        1   2 @MODI
     I                                        3  17 WW0317
     I*
     I** DATA STRUCTURE FOR DBERROR
     IDBERR1      DS                             26
     I                                        1   6 @SNDR
     I                                        7  18 @SCSD
     I                                       19  24 @DEST
     I                                       25  26 @SETP
     I*
     I** DATA STRUCTURE FOR DBERROR
     IDBERR2      DS                             16
     I                                        1  130WWAMT
     I                                       14  16 ZCCY
     I*
     I** DATA STRUCTURE TO SPLIT DATE
     I            DS
     I                                        1   6 ZSDATE
     I                                        1   2 WWYEAR
     I                                        3   4 WWMNTH
     I                                        5   6 WWDAY
     I*
     I** DATA STRUCTURE TO BUILD DATE
     I            DS
     I                                        1   8 WRDATE
     I                                        1   2 WRYEAR
     I I            '/'                       3   3 WRSL1
     I                                        4   5 WRMNTH
     I I            '/'                       6   6 WRSL2
     I                                        7   8 WRDAY
     I*
     I***DATA*STRUCTURE*FOR*OVERFLOW*ON*PRINTER*MG0512P1                  091250
     I** FILE INFORMATION DATA STRUCTURE FOR MG0512A1                     091250
     IOVRFL1      DS
     I                                    B 188 1890WWOFL1
     I                                    B 367 3680WWPTL1
     I*
     I***DATA*STRUCTURE*FOR*OVERFLOW*ON*PRINTER*MG0512P2                  091250
     I** FILE INFORMATION DATA STRUCTURE FOR MG0512A2                     091250
     IOVRFL2      DS
     I                                    B 188 1890WWOFL2
     I                                    B 367 3680WWPTL2
     I*
     I** DATA STRUCTURE FOR FORMATTING FIELD :33T:
     IWWTEST      DS                              2
     I                                        1   1 MGSTBS
     I                                        2   2 MGSPBS
     I*
     I** DATA STRUCTURE FOR CRLF CONTROL CHARACTER
     ICRLFDS      DS                              2
     I                                        1   2 CTRC
     I                                        1   1 WWCR
     I                                        2   2 WWLF
     I*
     I* DATA STRUCTURE FOR EXTENDED SETTLEMENT FIELDS
     I            DS
     I                                        1  16 WXSFLD
     I                                        1   1 WXS53
     I                                        2   2 WXS57
     I                                        3   3 WXS58
     I                                        4   4 WXS71
     I                                        5   5 WXS72
     I                                        6   6 WXS77F
     I                                        7   7 WXS77R
     I                                        8   8 WXS79
     I                                        9   9 WXS82I
     I                                       10  10 WXS82C
     I                                       11  11 WXS83
     I                                       12  12 WXS84
     I                                       13  13 WXS85
     I                                       14  14 WXS87R
     I                                       15  15 WXS87D
     I                                       16  16 WXS88
     I*
     ISCSARD    E DSSCSARDPD                                              CSW003
      ** External data structure for Switchable Features                  CSW003
      *                                                                   CSW003
     IDSFDY     E DSDSFDY                                                 CSW003
      ** First Data Structure for Access Objects                          CSW003
      *                                                                   CSW003
      * Data structures for access objects                                082282
     IDSSDY     E DSDSSDY                                                 082282
     ISDCUST    E DSSDCUSTPD                                              082282
      *                                                                   082282
     I*****************************************************************
     I/EJECT
     I*****************************************************************
      *                                                               *
      * 1. Multiple Data Structure MULT                               *
      *      The multiple occurrence data structure is used to        *
      *      store up the records that make up the SWIFT message      *
      *      before writing the whole message to file.                *
      *      To store the records of the message, you must            *
      *      first access an OCUR record, obtain the Message Tag      *
      *      & Message field data values for this OCUR record,        *
      *      then go on to the next OCUR record and continue          *
      *      this process until the SWIFT message is complete.        *
      *                                                               *
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*   MAIN PROCESSING - FORMAT MT512                              *
     C*                                                               *
     C*   CALLS THE FOLLOWING SUBROUTINES                             *
     C*                                                               *
     C*   INIT     - INITIALISATION                                   *
     C*   INTF     - INITIALISE NON-CONSTANT FIELDS                   *
     C*   ROUT     - DETERMINE MESSAGE ROUTING                        *
     C*   FLD20    - FORMAT MANDATORY FIELD :20:                      *
     C*   FLD21    - FORMAT MANDATORY FIELD :21:                      *
     C*   FLD23    - FORMAT MANDATORY FIELD :23:                      *
     C*   FLD31P   - FORMAT MANDATORY FIELD :31P:                     *
     C*   FLD31T   - FORMAT FIELD :31T:                               *
     C*   FLD30    - FORMAT MANDATORY FIELD :30:                      *
     C*   FLD351   - FORMAT MANDATORY FIELD :35A:                     *
     C*   FLD35B   - FORMAT MANDATORY FIELD :35B:                     *
     C*   FLD352   - FORMAT FIELD :35a:                               *
     C*   FLD822   - FORMAT FIELD :82a:                               *
     C*   FLD872   - FORMAT MANDATORY FIELD :87a:                     *
     C*   FLD873   - FORMAT FIELD :87a:                               *
     C*   FLD33T   - FORMAT MANDATORY FIELD :33T:                     *
     C*   FLD32M   - FORMAT FIELD :32M:                               *
     C*   FLD33S   - FORMAT FIELD :33S:                               *
     C*   FLD342   - FORMAT FIELD :34a:                               *
     C*   FLD32B   - FORMAT MANDATORY FIELD :32B:                     *
     C*   FLD71C   - FORMAT FIELD :71C:                               *
     C*   FLD71B   - FORMAT FIELD :71B:                               *
     C*   FLD36    - FORMAT FIELD :36:                                *
     C*   FLD343   - FORMAT FIELD :34a:                               *
     C*   FLD572   - FORMAT FIELD :57a:                               *
     C*   FLD72    - FORMAT FIELD :72:                                *
     C*   #W01     - PRINT MESSAGE GENERATED DETAILS                  *
     C*   #W02     - PRINT MESSAGE NOT GENERATED DETAILS              *
     C*   OFLDS    - FORMAT DETAILS FOR MIDAS SWIFT DATABASE FILES    *
     C*   MG5900   - UPDATE SEQUENCE NUMBERS ON TRANSACTION RECORD    *
     C*   MG5910   - UPDATE GENERATION GENERATED INDICATOR            *
     C*   *PSSR    - ERROR HANDLING                                   *
     C*                                                               *
     C*****************************************************************
     C*
     C** INITIALISATION
     C                     EXSR INIT
     C*
     C** READ FIRST RECORD FROM PF/MGF512PD
     C                     READ MGF512PD                 10
     C*
     C** IF RECORDS PRESENT ON THE FILE
     C           *IN10     IFEQ '0'
     C*
     C** DO WHILE RECORDS ON THIS FILE
     C           *IN10     DOWEQ'0'
     C*
     C** INITIALISE NON-CONSTANT FIELDS
     C                     EXSR INTF
     C*
     C** DETERMINE MESSAGE ROUTING
     C                     EXSR ROUT
     C*
     C** FORMAT MANDATORY FIELD :20:
     C                     MOVE ':20: '   WWGENF  5
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD20
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :21:
     C                     MOVE ':21: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD21
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :23:
     C                     MOVE ':23: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD23
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :31P:
     C                     MOVE ':31P:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD31P
     C                     END
     C*
     C** FORMAT FIELD :31T:
     C                     MOVE ':31T:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD31T
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :30:
     C                     MOVE ':30: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD30
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :35A:
     C                     MOVE ':35A:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD351
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :35B:
     C                     MOVE ':35B:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD35B
     C                     END
     C*
     C** FORMAT FIELD :35a:
     C                     MOVE ':35D:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD352
     C                     END
     C*
     C** FORMAT FIELD :82a:
     C                     MOVE ':82A:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           WXS82C    OREQ 'Y'
     C                     EXSR FLD822
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :87a:
     C                     MOVE ':87E:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           WXS87R    OREQ 'Y'
     C                     EXSR FLD872
     C                     END
     C*
     C** FORMAT FIELD :87a:
     C** Note: two :87a: are required in MT0512 with the same options (E o
     C** one is mandatory and one is optional, so it was necessary to create
     C** a dummy :87O: for the optional field.
     C                     MOVE ':87O:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           WXS87D    OREQ 'Y'
     C                     EXSR FLD873
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :33T:
     C                     MOVE ':33T:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD33T
     C                     END
     C*
     C** FORMAT FIELD :32M:
     C                     MOVE ':32M:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD32M
     C                     END
     C*
     C** FORMAT FIELD :33S:
     C                     MOVE ':33S:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD33S
     C                     END
     C*
     C** FORMAT FIELD :34a:
     C                     MOVE ':34G:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD342
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :32B:
     C                     MOVE ':32B:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD32B
     C                     END
     C*
     C** FORMAT FIELD :71C:
     C                     MOVE ':71C:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD71C
     C                     END
     C*
     C** FORMAT FIELD :71B:
     C                     MOVE ':71B:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD71B
     C                     END
     C*
     C** FORMAT FIELD :36:
     C                     MOVE ':36: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD36
     C                     END
     C*
     C** FORMAT FIELD :34a:
     C                     MOVE ':34A:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD343
     C                     END
     C*
     C** FORMAT FIELD :57a:
     C                     MOVE ':57A:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           WXS57     OREQ 'Y'
     C                     EXSR FLD572
     C                     END
     C*
     C** FORMAT FIELD :72:
     C                     MOVE ':72: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           WXS72     OREQ 'Y'
     C                     EXSR FLD72
     C                     END
     C*
     C** IF ERROR FOUND WHILST FORMATTING THIS MESSAGE
     C           @ERR      IFEQ 'Y'
     C*
     C** PRINT MESSAGE NOT GENERATED DETAILS
     C                     EXSR #W02
     C*
     C                     ELSE
     C*
     C** WRITE TO MIDAS SWIFT DATABASE FILES
     C                     EXSR OFLDS
     C*
     C** PRINT MESSAGE GENERATED DETAILS
     C                     EXSR #W01
     C                     MOVEL'Y'       WWPTR1  1
     C                     END
     C*
     C** DELETE THE RECORD ON PF/MGF512PD PROCESSED
     C                     DELETMGF512PD               11
     C*
     C** IF ERROR ON DELETE OF RECORD
     C           *IN11     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '005'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 005  *
     C                     MOVEL'MGF512PD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0512'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF NO ERRORS FOUND WHILST FORMATTING THIS MESSAGE
     C           @ERR      IFNE 'Y'
     C*
     C** UPDATE SEQUENCE NUMBERS ON TRANSACTION RECORD
     C*
     C** FORMAT @TRAN AS FOLLOWS
     C** CHARS 1-6 TO LEFTMOST 6 CHARS FROM MGTNUM
     C** CHARS 7-13 TO BLANKS
     C** CHARS 14-15 TO MGTTYP
     C                     MOVELMGTNUM    WWFL06  6
     C                     MOVELWWFL06    WWTRAN 15
     C                     MOVE MGTTYP    WWTRAN
     C*
     C                     CALL 'MG5900'               20
     C                     PARM MGMODI    @MODI   2
     C                     PARM WWTRAN    @TRAN  15
     C                     PARM 'CE'      @PROG   2
     C                     PARM 'U'       @MODE   1
     C                     PARM *ZEROS    @LSWS   70
     C                     PARM *BLANKS   @ERCD   1
     C                     PARM CSW003    @MT5XX  1                       CSW003
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN20     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '001'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 001  *
     C                     MOVEL'MG5900'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM
     C           @ERCD     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     MOVEL@TRAN     WW0317
     C                     MOVE '002'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 002  *
     C                     MOVELDBERR     DBKEY            * * * * * * * *
     C                     MOVEL'MG5900'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** UPDATE GENERATION GENERATED INDICATOR (INSERTION MODE)
     C           MGCHTP    IFEQ 'I'
     C*
     C** FORMAT @TREF AS FOLLOWS
     C** CHARS 1-6 TO LEFTMOST 6 CHARS FROM MGTNUM
     C** CHARS 7-13 TO BLANKS
     C** CHARS 14-15 TO MGTTYP
     C                     MOVELMGTNUM    WWFL06
     C                     MOVELWWFL06    WWTREF 15
     C                     MOVE MGTTYP    WWTREF
     C*
     C                     CALL 'MG5910'               28
     C                     PARM MGMODI    @MODI
     C                     PARM WWTREF    @TREF  15
     C                     PARM 'CE'      @PROG
     C                     PARM 'U'       @MODE
     C                     PARM *BLANKS   @CONG   1
     C                     PARM *BLANKS   @RCDG   1
     C                     PARM *BLANKS   @ERCD   1
     C                     PARM CSW003    @MT5XX                          CSW003
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN28     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '003'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 003  *
     C                     MOVEL'MG5910'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM
     C           @ERCD     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     MOVEL@TREF     WW0317
     C                     MOVE '004'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 004  *
     C                     MOVELDBERR     DBKEY            * * * * * * * *
     C                     MOVEL'MG5910'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C** COMIT ALL UPDATES TO FILES
     C                     COMIT
     C*
     C** READ NEXT RECORD FROM PF/MGF512PD
     C                     READ MGF512PD                 10
     C                     END
     C*
     C** WRITE TRAILER ON SWIFT MESSAGES GENERATED REPORT
     C** ONLY IF MESSAGES GENERATED
     C           WWPTR1    IFNE 'Y'
     C                     WRITEHEADER1
     C                     WRITENODETS
     C*
     C                     ELSE
     C*
     C** CHECK FOR OVERFLOW
     C           WWOFL1    SUB  WWPTL1    WWLINE  20
     C*
     C           WWLINE    IFLT 4
     C                     WRITEHEADER1
     C                     END
     C                     WRITEEOREPT1
     C                     END
     C*
     C** WRITE TRAILER ON SWIFT MESSAGES NOT GENERATED REPORT
     C** IF THE PRINTER FILE HAS BEEN OPENED
     C           *IN50     IFEQ '1'
     C*
     C** CHECK FOR OVERFLOW
     C           WWOFL2    SUB  WWPTL2    WWLINE
     C*
     C           WWLINE    IFLT 4
     C                     WRITEHEADER2
     C                     END
     C*
     C                     WRITEEOREPT2
     C                     END
     C*
     C                     ELSE
     C*
     C** WRITE NO DETAILS TO REPORT
     C                     WRITEHEADER1
     C                     WRITENODETS
     C                     END
     C/COPY WNCPYSRC,MG0512C003
     C*
     C                     SETON                     LR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  INIT      - INITIALISATION                                  *
     C**                                                              *
     C**  CALLS     - *PSSR                                           *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESS                                    *
     C**                                                              *
     C*****************************************************************
     C*
     C           INIT      BEGSR
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C** SET UP LDA
     C           *NAMVAR   DEFN           LDA
     C*
     C** ACCESS SDBANKPD FOR BANK DETAILS
     C           1         SETLLSDBANKPD
     C                     READ SDBANKPD                 12
     C*
     C** IF RECORD NOT FOUND OR LAST CHANGE TYPE = 'D'
     C           *IN12     IFEQ '1'
     C           BJTYLC    OREQ 'D'
     C           *LOCK     IN   LDA
     C                     MOVE '006'     DBASE            * * * * * * * *
     C                     MOVEL*BLANKS   DBKEY            *  DBERR 006  *
     C                     MOVEL'SDBANKPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0512'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** ACCESS SDMGMEPD FOR MESSAGE GENERATION & MESSAGE MANAGEMENT
     C           1         SETLLSDMGMEPD
     C                     READ SDMGMEPD                 15
     C*
     C** IF RECORD NOT FOUND OR LAST CHANGE TYPE = 'D'
     C           *IN15     IFEQ '1'
     C           ENTYLC    OREQ 'D'
     C           *LOCK     IN   LDA
     C                     MOVE '037'     DBASE            * * * * * * * *
     C                     MOVEL*BLANKS   DBKEY            *  DBERR 037  *
     C                     MOVEL'SDMGMEPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0512'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** SET CRLF CONTROL CHARACTER
     C                     MOVE *LOVAL    CTRC
     C                     BITON'457'     WWCR
     C                     BITON'257'     WWLF
     C*
      ** Check if (CSW003) MT5XX - Phase 2 Message Generation is on.      CSW003
      *                                                                   CSW003
     C                     CALL 'AOSARDR0'                                CSW003
     C                     PARM '*BLANKS' WRTCD   7                       CSW003
     C                     PARM '*VERIFY' WOPTN   7                       CSW003
     C                     PARM 'CSW003'  @SARD   6                       CSW003
     C           SCSARD    PARM SCSARD    DSFDY                           CSW003
      *                                                                   CSW003
     C           WRTCD     IFEQ *BLANKS                                   CSW003
     C                     MOVE 'Y'       CSW003  1                       CSW003
     C                     ENDIF                                          CSW003
      *                                                                   CSW003
     C/COPY WNCPYSRC,MG0512C001
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  GENF      - GENERATE FIELDS CONDITION                       *
     C**                                                              *
     C**  CALLS     -                                                 *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESS                                    *
     C**                                                              *
     C*****************************************************************
     C*
     C           GENF      BEGSR
     C*
      **  If message field tag appears on array SUPP, supress its
      **  production by setting off indicator 01.
     C           WWGENF    LOKUPSUPP                     55
     C           *IN55     IFEQ '0'
     C                     SETON                     01
     C/COPY WNCPYSRC,MG0512C002
     C                     ELSE
     C                     SETOF                     01
     C                     END
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  INTF      - INITIALISE NON-CONSTANT FIELDS                  *
     C**                                                              *
     C**  CALLS     - MG5900, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESS                                    *
     C**                                                              *
     C*****************************************************************
     C*
     C           INTF      BEGSR
     C*
     C** OBTAIN SEQUENCE NUMBERS FROM TRANSACTION RECORD
     C** FORMAT @TRAN AS FOLLOWS
     C** CHARS 1-6 EQUAL TO LEFTMOST 6 CHARS FROM MGTNUM
     C** CHARS 7-13 EQUAL TO BLANKS
     C** CHARS 14-15 EQUAL TO MGTTYP
     C                     MOVE *BLANKS   WWTRAN
     C                     MOVE *BLANKS   WWFL06
     C                     MOVELMGTNUM    WWFL06
     C                     MOVELWWFL06    WWTRAN
     C                     MOVE MGTTYP    WWTRAN
     C*
     C                     CALL 'MG5900'               20
     C                     PARM MGMODI    @MODI
     C                     PARM WWTRAN    @TRAN
     C                     PARM 'CE'      @PROG
     C                     PARM *BLANKS   @MODE
     C                     PARM *ZEROS    @LSWS
     C                     PARM *BLANKS   @ERCD
     C                     PARM CSW003    @MT5XX                          CSW003
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN20     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '007'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 007  *
     C                     MOVEL'MG5900'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM
     C           @ERCD     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     MOVEL@TRAN     WW0317
     C                     MOVE '008'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 008  *
     C                     MOVELDBERR     DBKEY            * * * * * * * *
     C                     MOVEL'MG5900'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** OBTAIN GENERATION GENERATED INDICATOR
     C** FORMAT @TREF AS FOLLOWS
     C** CHARS 1-6 EQUAL TO LEFTMOST 6 CHARS FROM MGTNUM
     C** CHARS 7-13 EQUAL TO BLANKS
     C** CHARS 14-15 EQUAL TO MGTTYP
     C                     MOVE *BLANKS   WWTREF
     C                     MOVE *BLANKS   WWFL06
     C                     MOVELMGTNUM    WWFL06
     C                     MOVELWWFL06    WWTREF
     C                     MOVE MGTTYP    WWTREF
     C*
     C                     CALL 'MG5910'               28
     C                     PARM MGMODI    @MODI
     C                     PARM WWTREF    @TREF
     C                     PARM 'CE'      @PROG
     C                     PARM *BLANKS   @MODE
     C                     PARM *BLANKS   @CONG
     C                     PARM *BLANKS   @RCDG
     C                     PARM *BLANKS   @ERCD
     C                     PARM CSW003    @MT5XX                          CSW003
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN28     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '009'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 009  *
     C                     MOVEL'MG5910'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM
     C           @ERCD     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     MOVEL@TREF     WW0317
     C                     MOVE '010'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 010  *
     C                     MOVELDBERR     DBKEY            * * * * * * * *
     C                     MOVEL'MG5910'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** CLEAR MULTIPLE OCCURENCE DATA STRUCTURE
     C                     Z-ADD1         X       20
     C           X         OCUR MULT
     C*
     C           X         DOWLE80
     C           MULT      ANDNE*BLANKS
     C                     MOVE *BLANKS   MULT
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C** SET INDEX TO FIRST OCCURENCE
     C                     Z-ADD1         X
     C           X         OCUR MULT
     C*
     C** SET FLAG FOR MESSAGE CORRECTLY GENERATED
     C                     MOVE *BLANKS   @ERR    1
     C                     MOVE *BLANKS   DSERR
     C                     MOVE *BLANKS   NWERR  50
     C*
     C** SET EXTENDED SETTLEMENT FIELDS PRESENCE
     C                     MOVE MGXSFL    WXSFLD
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  ROUT      - DETERMINE MESSAGE ROUTING                       *
     C**                                                              *
     C**  CALLS     - *PSSR, ZM1100                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESS                                    *
     C**                                                              *
     C*****************************************************************
     C*
     C           ROUT      BEGSR
     C*
     C** ACCESS SDBRCHL1 USING KEY OF MGSNBR FROM PF/MGF512PD
     C           MGSNBR    CHAINSDBRCHL1             13
     C*
     C** IF RECORD NOT FOUND OR LAST CHANGE TYPE = 'D'
     C           *IN13     IFEQ '1'
     C           A8BRCD    OREQ 'D'
     C           *LOCK     IN   LDA
     C                     MOVE '011'     DBASE            * * * * * * * *
     C                     MOVELMGSNBR    DBKEY            *  DBERR 011  *
     C                     MOVEL'SDBRCHL1'DBFILE           * * * * * * * *
     C                     MOVEL'MG0512'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** DETERMINE NETWORK ROUTING AND ADDRESSES
     C                     MOVE A8BICN    @SNDR   6
     C                     MOVE A8BTID    @SCSD  12
     C                     MOVE MGDEST    @DEST   6
     C                     MOVELMGSETP    @SETP   2
     C                     MOVE *BLANKS   @NWSN  20
     C                     MOVE *BLANKS   @NWDS  22
     C                     MOVE *BLANKS   @NWRK   6
     C                     MOVE *BLANKS   @ERRC   1
     C                     MOVE *BLANKS   @SRNM  20
     C                     MOVE *BLANKS   @SRTN  10
     C                     MOVE *BLANKS   @DRNM  20
     C                     MOVE *BLANKS   @DRTN  10
     C*
     C*********************CALL 'ZM1100'               21                 CSW036
     C                     CALL 'ZM1101'               21                 CSW036
     C                     PARM           @SNDR
     C                     PARM           @SCSD
     C                     PARM           @DEST
     C                     PARM           @SETP
     C                     PARM           @NWSN
     C                     PARM           @NWDS
     C                     PARM           @NWRK
     C                     PARM           @ERRC
     C                     PARM           @SRNM
     C                     PARM           @SRTN
     C                     PARM           @DRNM
     C                     PARM           @DRTN
     C*
     C***IF*PROGRAM*ZM1100*NOT*FOUND                                      CSW036
     C** IF PROGRAM ZM1101 NOT FOUND                                      CSW036
     C           *IN21     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '012'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 012  *
     C*********************MOVEL'ZM1100'  DBPGM            * * * * * * * *CSW036
     C                     MOVEL'ZM1101'  DBPGM            * * * * * * * *CSW036
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING CALLED PROGRAM
     C           @ERRC     IFEQ 'S'
     C           *LOCK     IN   LDA
     C                     MOVE '013'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 013  *
     C                     MOVELDBERR1    DBKEY            * * * * * * * *
     C*********************MOVEL'ZM1100'  DBPGM                           CSW036
     C                     MOVEL'ZM1101'  DBPGM                           CSW036
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DESTINATION ERROR MESSAGE
     C           @ERRC     IFEQ 'D'
     C                     MOVE *BLANKS   DSERR  50
     C                     MOVELERRA,1    DSERR
     C                     MOVE 'Y'       @ERR
     C                     END
     C*
     C** FORMAT NETWORK ERROR MESSAGE
     C           @NWRK     IFEQ 'UNROUT'
     C                     MOVELERRA,15   NWERR
     C                     MOVE 'Y'       @ERR
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD20     - FORMAT MANDATORY FIELD :20:                     *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD20     BEGSR
     C*
     C** IF REQUIRED FIELDS ON MGF512PD NOT PRESENT THEN ERROR
     C                     MOVELMGTNUM    WWFL06
     C           MGMODI    IFNE 'SE'
     C           MGTTYP    ORNE 'TR'
     C           MGTTYP    ANDNE'DM'
     C           MGTTYP    ANDNE'DN'                                      CSW003
     C           WWFL06    OREQ *BLANKS
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,2    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,1    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C***********          MOVEL':20:'    MTAG                            057263
     C                     MOVEL':20: '   MTAG                            057263
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C           MGMODI    IFEQ 'SE'
     C           MGTTYP    ANDEQ'TR'
     C                     MOVEL'SET'     WD0103
     C*
     C                     ELSE
     C*
     C           MGMODI    IFEQ 'SE'
     C           MGTTYP    ANDEQ'DM'
     C                     MOVEL'SED'     WD0103
     C                     ELSE                                           CSW003
     C           MGMODI    IFEQ 'SE'                                      CSW003
     C           MGTTYP    ANDEQ'DN'                                      CSW003
     C                     MOVEL'SEE'     WD0103                          CSW003
     C                     ENDIF                                          CSW003
     C                     END
     C*
     C                     END
     C*
     C                     MOVELMGTNUM    WD0409
     C*
     C           @LSWS     IFEQ 9999999
     C                     Z-ADD1         WWLSWS  70
     C*
     C                     ELSE
     C*
     C           @LSWS     ADD  1         WWLSWS
     C                     END
     C                     MOVELWWLSWS    WD1016
     C                     MOVELWWMDAT    MDAT
     C                     MOVELWWMDAT    WWDATA 16
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD21     - FORMAT MANDATORY FIELD :21:                     *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD21     BEGSR
     C*
     C** IF REQUIRED FIELDS ON MGF512PD NOT PRESENT THEN ERROR
     C                     MOVELMGTNUM    WWFL06
     C           MGMODI    IFNE 'SE'
     C           MGTTYP    ORNE 'TR'
     C           MGTTYP    ANDNE'DM'
     C           MGTTYP    ANDNE'DN'                                      CSW003
     C           WWFL06    OREQ *BLANKS
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,3    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,2    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C***********          MOVEL':21:'    MTAG                            057263
     C                     MOVEL':21: '   MTAG                            057263
     C                     MOVE *BLANKS   WWMDAT
     C*
     C** IF @LSWS EQUALS ZERO
     C           @LSWS     IFEQ 0
     C                     MOVEL'NEW'     WWMDAT
     C*
     C                     ELSE
     C*
     C           MGMODI    IFEQ 'SE'
     C           MGTTYP    ANDEQ'TR'
     C                     MOVEL'SET'     WD0103
     C*
     C                     ELSE
     C*
     C           MGMODI    IFEQ 'SE'
     C           MGTTYP    ANDEQ'DM'
     C                     MOVEL'SED'     WD0103
     C                     ELSE                                           CSW003
     C           MGMODI    IFEQ 'SE'                                      CSW003
     C           MGTTYP    ANDEQ'DN'                                      CSW003
     C                     MOVEL'SEE'     WD0103                          CSW003
     C                     ENDIF                                          CSW003
     C                     END
     C*
     C                     END
     C                     MOVELWWFL06    WD0409
     C                     MOVEL@LSWS     WD1016
     C                     END
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD23     - FORMAT MANDATORY FIELD :23:                     *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD23     BEGSR
     C*
     C** IF MGTSTP IS NOT 'TP', 'TS', 'RR' OR 'RP' THEN ERROR
     C           MGTSTP    IFNE 'TP'
     C           MGTSTP    ANDNE'TS'
     C           MGTSTP    ANDNE'RR'
     C           MGTSTP    ANDNE'RP'
     C           MGTSTP    ANDNE'PD'                                      103449
     C           MGTSTP    ANDNE'PL'                                      103449
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,4    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,3    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C***********          MOVEL':23:'    MTAG                            057263
     C                     MOVEL':23: '   MTAG                            057263
     C*
     C** SET UP NEW CONFIRMATION WORKFIELD
     C           MGCHTP    IFEQ 'I'
     C                     MOVEL'NEW   '  WWNEWC  6
     C                     END
     C*
     C           MGCHTP    IFEQ 'A'
     C                     MOVEL'AMEND '  WWNEWC  6
     C                     END
     C*
     C           MGCHTP    IFEQ 'D'
     C                     MOVEL'CANCEL'  WWNEWC  6
     C                     END
     C*
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVEL'/BOUGHT' WWBGHT  7
     C                     MOVEL'/SOLD'   WWSOLD  5
     C*
     C** IF MGTTYP EQUALS 'TR' AND MGTSTP EQUALS 'TP'
     C           MGTTYP    IFEQ 'TR'
     C           MGTSTP    ANDEQ'TP'
     C*
     C***********WWNEWC    CAT  WWSOLD:0  WD0113                          067522
     C           WWNEWC    CAT  WWBGHT:0  WD0113                          067522
     C                     END
     C*
     C** IF MGTTYP EQUALS 'TR' AND MGTSTP EQUALS 'TS'
     C           MGTTYP    IFEQ 'TR'
     C           MGTSTP    ANDEQ'TS'
     C*
     C***********WWNEWC    CAT  WWBGHT:0  WD0113                          067522
     C           WWNEWC    CAT  WWSOLD:0  WD0113                          067522
     C                     END
     C*
     C** IF MGTTYP EQUALS 'DM'
     C           MGTTYP    IFEQ 'DM'
     C           MGTTYP    OREQ 'DN'                                      CSW003
     C*
     C** IF RP START DATE (DATE OUT) OR RR START DATE (DATE IN)
     C           MGTSTP    IFEQ 'RR'
     C           MGVDAT    ANDEQMGDMDI
     C           MGTSTP    OREQ 'RP'
     C***********MGVDAT    ANDEQMGDMDO                                    104606
     C           MGVDAT    ANDEQMGDMDI                                    104606
     C           MGTSTP    OREQ 'PL'                                      103449
     C           MGVDAT    ANDEQMGDMDI                                    103449
     C           MGTSTP    OREQ 'PD'                                      103449
     C***********MGVDAT    ANDEQMGDMDO                             103449 104606
     C           MGVDAT    ANDEQMGDMDI                                    104606
     C*
     C           WWNEWC    CAT  WWBGHT:0  WD0113
     C*
     C                     ELSE
     C*
     C           WWNEWC    CAT  WWSOLD:0  WD0113
     C*
     C                     END
     C*
     C                     END
     C*
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD31P    - FORMAT MANDATORY FIELD :31P:                    *
     C**                                                              *
     C**  CALLS     - ZM0060, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD31P    BEGSR
     C*
     C** IF MGTDAT IS ZERO
     C           MGTDAT    IFEQ 0
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,5    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,4    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':31P:'   MTAG
     C*
     C** CONVERT TRADE DATE TO SWIFT FORMAT
     C                     MOVE MGTDAT    ZMDAY   50
     C                     MOVE *BLANKS   ZSDATE  6
     C                     CALL 'ZM0060'               22
     C                     PARM           ZMDAY
     C                     PARM           ZSDATE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN22     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '014'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 014  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVELZSDATE    WD0106
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVELWWYEAR    WRYEAR
     C                     MOVELWWMNTH    WRMNTH
     C                     MOVELWWDAY     WRDAY
     C                     MOVELWRDATE    WR0108
     C*
     C** IF MGTTYP EQUALS 'TR'
     C           MGTTYP    IFEQ 'TR'
     C                     MOVELMGMKTN    WD0726
     C                     MOVELMGMKTN    WR0928
     C*
     C                     ELSE
     C*
     C** IF MGTTYP EQUALS 'DM'
     C           MGTTYP    IFEQ 'DM'
     C           MGTTYP    OREQ 'DN'                                      CSW003
     C                     MOVE *BLANKS   WD0726
     C                     MOVE *BLANKS   WR0928
     C                     END
     C*
     C                     END
     C                     MOVELWWMDAT    MDAT
     C                     MOVELWWRDAT    RDAT
     C*
     C** SAVE ZSDATE FOR FORMATTING MANDATORY FIELD :34a: LATER (FILE)
     C                     MOVELZSDATE    WWDATE
     C*
     C** SAVE WRDATE FOR FORMATTING MANDATORY FIELD :34a: LATER (REPORT)
     C                     MOVELWRDATE    W8DATE
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD31T   - FORMAT FIELD :31T:                               *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD31T    BEGSR
     C*
     C** IF MGTRTT FROM PF/MGF512PD IS NOT BLANK
     C           MGTRTT    IFNE *BLANKS
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,5    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':31T:'   MTAG
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGTRTT    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD30    - FORMAT FIELD :30:                                *
     C**                                                              *
     C**  CALLS     - ZM0060, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD30     BEGSR
     C*
     C** IF MGVDAT FROM PF/MGF512PD IS NOT ZERO
     C           MGVDAT    IFNE 0
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,6    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C***********          MOVEL':30:'    MTAG                            057263
     C                     MOVEL':30: '   MTAG                            057263
     C*
     C** CONVERT VALUE DATE TO SWIFT FORMAT
     C                     MOVE *BLANKS   ZSDATE  6
     C                     Z-ADDMGVDAT    ZMDAY   50
     C*
     C                     CALL 'ZM0060'               22
     C                     PARM           ZMDAY
     C                     PARM           ZSDATE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN22     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '015'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 015  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVELZSDATE    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVELWWYEAR    WRYEAR
     C                     MOVELWWMNTH    WRMNTH
     C                     MOVELWWDAY     WRDAY
     C                     MOVELWRDATE    RDAT
     C*
     C** SAVE ZSDATE FOR FORMATTING MANDATORY FIELD :34a: LATER
     C                     MOVELZSDATE    WWDATE  6
     C*
     C** SAVE WRDATE FOR FORMATTING MANDATORY FIELD :34a: LATER
     C                     MOVELWRDATE    W8DATE  8
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD351    - FORMAT MANDATORY FIELD :35A:                    *
     C**                                                              *
     C**  CALLS     - ZM1140, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD351    BEGSR
     C*
     C** IF MGQANT EQUALS ZERO OR MGSWTP IS BLANK THEN ERROR
     C           MGQANT    IFEQ 0
     C           MGSWTP    OREQ *BLANKS
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,6    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,7    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':35A:'   MTAG
     C*
     C** CONVERT MIDAS AMOUNT TO SWIFT FORMAT
     C                     Z-ADDMGQANT    ZAMNT  150
     C***********          Z-ADD0         ZCDP    10                      078256
     C                     Z-ADDMGNMDP    ZCDP    10                      078256
     C                     MOVE *BLANKS   ZSAMNT 17
     C                     MOVE *BLANKS   ZOAMNT 21
     C*
     C                     CALL 'ZM1140'               23
     C                     PARM           ZAMNT
     C                     PARM           ZCDP
     C                     PARM           ZSAMNT
     C                     PARM           ZOAMNT
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN23     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '016'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 016  *
     C                     MOVEL'ZM1140'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVELMGSWTP    WD0103
     C                     MOVELZSAMNT    WD0419
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVELMGSWTP    WR0103
     C                     MOVELZOAMNT    WR0424
     C                     MOVELWWRDAT    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD35B    - FORMAT MANDATORY FIELD :35B:                    *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD35B    BEGSR
     C*
     C** IF MGSFN1 IS BLANK THEN ERROR
     C           MGSFN1    IFEQ *BLANKS
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,7    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,8    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':35B:'   MTAG
     C*
     C** IF MGISIN IS NOT BLANK
     C           MGISIN    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C*
     C** CHECK ISIN OR COMMON CODE
     C                     MOVE MGISIN    WWISIN  1
     C           WWISIN    IFNE *BLANK
     C                     MOVEL'ISIN '   WD0105           ISIN code
     C                     MOVELMGISIN    WD0617
     C                     ELSE
     C                     MOVEL'/XS/'    WD0104           COMMON code
     C                     MOVELMGISIN    WD0516
     C                     END
     C*
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C                     END
     C*
     C** REMOVE FORBIDDEN CHARACTERS (E.G. %)
     C                     MOVEAMGSFN1    FORB
     C                     Z-ADD1         J       20
     C           *IN85     DOUEQ'0'                                       056532
     C           '%'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN1
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '&'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN1
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '/'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN1
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '$'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN1
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J                               056532
     C           FORB,J    DOWEQ'-'                                       056532
     C           FORB,J    OREQ ' '                                       056532
     C           FORB,J    OREQ ':'                                       056532
     C                     ADD  1         J                               056532
     C                     MOVE *BLANKS   MGSFN1                          056532
     C                     MOVEAFORB,J    MGSFN1                          056532
     C           J         IFEQ 50                                        056532
     C           FORB,50   IFEQ '-'                                       056532
     C           FORB,50   OREQ ':'                                       056532
     C                     MOVE *BLANKS   MGSFN1                          056532
     C                     ENDIF                                          056532
     C                     LEAVE                                          056532
     C                     ENDIF                                          056532
     C                     ENDDO                                          056532
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGSFN1    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** IF MGSFN2 IS NOT BLANK
     C           MGSFN2    IFNE *BLANKS
     C*
     C** REMOVE FORBIDDEN CHARACTERS (E.G. %)
     C                     MOVEAMGSFN2    FORB
     C                     Z-ADD1         J       20
     C           *IN85     DOUEQ'0'                                       056532
     C           '%'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN2
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '&'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN2
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '/'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN2
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '$'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN2
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J                               056532
     C           FORB,J    DOWEQ'-'                                       056532
     C           FORB,J    OREQ ' '                                       056532
     C           FORB,J    OREQ ':'                                       056532
     C                     ADD  1         J                               056532
     C                     MOVE *BLANKS   MGSFN2                          056532
     C                     MOVEAFORB,J    MGSFN2                          056532
     C           J         IFEQ 50                                        056532
     C           FORB,50   IFEQ '-'                                       056532
     C           FORB,50   OREQ ':'                                       056532
     C                     MOVE *BLANKS   MGSFN2                          056532
     C                     ENDIF                                          056532
     C                     LEAVE                                          056532
     C                     ENDIF                                          056532
     C                     ENDDO                                          056532
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGSFN2    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD352    - FORMAT FIELD :35a:                              *
     C**                                                              *
     C**  CALLS     - ZM0060, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD352    BEGSR
     C*
     C** IF MGNCDT IS NOT ZERO
     C           MGNCDT    IFNE 0
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,9    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':35D:'   MTAG
     C*
     C** CONVERT NEXT COUPON DATE TO SWIFT FORMAT
     C                     MOVE MGNCDT    ZMDAY
     C                     MOVE *BLANKS   ZSDATE
     C*
     C                     CALL 'ZM0060'               22
     C                     PARM           ZMDAY
     C                     PARM           ZSDATE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN22     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '017'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 017  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVELZSDATE    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVELWWYEAR    WRYEAR
     C                     MOVELWWMNTH    WRMNTH
     C                     MOVELWWDAY     WRDAY
     C                     MOVELWRDATE    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD822    - FORMAT FIELD :82a:                              *
     C**                                                              *
     C**  CALLS     - ZM1150, *PSSR, #ZA                              *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD822    BEGSR
     C*
     C** IF MGCTYN IS NOT BLANK
     C           MGCTYN    IFNE *BLANKS
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,10   MDES
     C*
     C** FORMAT ADDRESS FIELDS
     C                     MOVELMGCTYN    ZDEST  12
     C                     MOVE *BLANKS   ZSADD1 35
     C                     MOVE *BLANKS   ZSADD2 35
     C                     MOVE *BLANKS   ZSADD3 35
     C                     MOVE *BLANKS   ZSADD4 35
     C                     MOVE *BLANKS   ZSTAG   1
     C                     MOVE *BLANKS   ZSTYPE  1
     C                     MOVE *BLANKS   ZERR    1
     C                     MOVE *BLANKS   ZCRNM  20
     C                     MOVE *BLANKS   ZCRTN  10
     C*
     C*********************CALL 'ZM1150'               24                 CSW036
     C                     CALL 'ZM1151'               24                 CSW036
     C                     PARM           ZDEST
     C                     PARM           ZSADD1
     C                     PARM           ZSADD2
     C                     PARM           ZSADD3
     C                     PARM           ZSADD4
     C                     PARM           ZSTAG
     C                     PARM           ZSTYPE
     C                     PARM           ZERR
     C                     PARM           ZCRNM
     C                     PARM           ZCRTN
     C                     PARM           ZDBEI   1                                           222373
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN24     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '018'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 018  *
     C*********************MOVEL'ZM1150'  DBPGM            * * * * * *    CSW036
     C                     MOVEL'ZM1151'  DBPGM            * * * * * *    CSW036
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING CALLED PROGRAM
     C           ZERR      IFEQ '1'
     C                     MOVELERRA,8    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** IF ZSTAG IS 'A'
     C           ZSTAG     IFEQ 'A'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':82A:'   MTAG
     C                     END
     C*
     C** IF ZSTAG IS 'D'
     C           ZSTAG     IFEQ 'D'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':82D:'   MTAG
     C                     END
     C*
     C** IF MGCTYL IS NOT BLANK
     C           MGCTYL    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGCTYL    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     EXSR #ZA
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD872    - FORMAT MANDATORY FIELD :87a:                    *
     C**                                                              *
     C**  CALLS     - ZM1150, *PSSR, #ZA                              *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD872    BEGSR
     C*
     C** IF REQUIRED FIELDS ON MGF512PD NOT PRESENT THEN ERROR
     C           MGRSSN    IFEQ *BLANKS
     C           MGRSS1    ANDEQ*BLANKS
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,9    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,11   MDES
     C*
     C** FORMAT ADDRESS FIELDS
     C                     MOVELMGRSSN    ZDEST
     C                     MOVE *BLANKS   ZSADD1
     C                     MOVE *BLANKS   ZSADD2
     C                     MOVE *BLANKS   ZSADD3
     C                     MOVE *BLANKS   ZSADD4
     C                     MOVE *BLANKS   ZSTAG
     C                     MOVE *BLANKS   ZSTYPE
     C                     MOVE *BLANKS   ZERR
     C                     MOVE *BLANKS   ZCRNM
     C                     MOVE *BLANKS   ZCRTN
     C*
     C*********************CALL 'ZM1150'               24                 CSW036
     C                     CALL 'ZM1151'               24                 CSW036
     C                     PARM           ZDEST
     C                     PARM           ZSADD1
     C                     PARM           ZSADD2
     C                     PARM           ZSADD3
     C                     PARM           ZSADD4
     C                     PARM           ZSTAG
     C                     PARM           ZSTYPE
     C                     PARM           ZERR
     C                     PARM           ZCRNM
     C                     PARM           ZCRTN
     C                     PARM           ZDBEI                                               222373
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN24     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '019'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 019  *
     C*********************MOVEL'ZM1150'  DBPGM            * * * * * * * *CSW036
     C                     MOVEL'ZM1151'  DBPGM            * * * * * * * *CSW036
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING CALLED PROGRAM
     C           ZERR      IFEQ '1'
     C                     MOVELERRA,10   MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** IF ZSTAG IS 'A'
     C           ZSTAG     IFEQ 'A'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':87E:'   MTAG
     C                     END
     C*
     C** IF ZSTAG IS 'D'
     C           ZSTAG     IFEQ 'D'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':87F:'   MTAG
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C*
     C** IF MGPCOD EQUALS '1' OR '5'
     C           MGPCOD    IFEQ '1'
     C           MGPCOD    OREQ '5'
     C                     MOVEL'APMT'    MDAT
     C*
     C                     ELSE
     C*
     C                     MOVEL'FREE'    MDAT
     C                     END
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C                     MOVELMTAG      XMTAG                           069154
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C*
     C** IF MGRSS1 IS NOT BLANK
     C           MGRSS1    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGRSS1    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     EXSR #ZA
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD873    - FORMAT FIELD :87a:                              *
     C**                                                              *
     C**  CALLS     - ZM1150, *PSSR, #ZA                              *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD873    BEGSR
     C*
     C** IF MGDSSN OR MGDSS1 ARE NOT BLANK
     C           MGDSSN    IFNE *BLANKS
     C           MGDSS1    ORNE *BLANKS
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,12   MDES
     C*
     C** FORMAT ADDRESS FIELDS
     C                     MOVELMGDSSN    ZDEST
     C                     MOVE *BLANKS   ZSADD1
     C                     MOVE *BLANKS   ZSADD2
     C                     MOVE *BLANKS   ZSADD3
     C                     MOVE *BLANKS   ZSADD4
     C                     MOVE *BLANKS   ZSTAG
     C                     MOVE *BLANKS   ZSTYPE
     C                     MOVE *BLANKS   ZERR
     C                     MOVE *BLANKS   ZCRNM
     C                     MOVE *BLANKS   ZCRTN
     C*
     C*********************CALL 'ZM1150'               24                 CSW036
     C                     CALL 'ZM1151'               24                 CSW036
     C                     PARM           ZDEST
     C                     PARM           ZSADD1
     C                     PARM           ZSADD2
     C                     PARM           ZSADD3
     C                     PARM           ZSADD4
     C                     PARM           ZSTAG
     C                     PARM           ZSTYPE
     C                     PARM           ZERR
     C                     PARM           ZCRNM
     C                     PARM           ZCRTN
     C                     PARM           ZDBEI                                               222373
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN24     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '020'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 020  *
     C*********************MOVEL'ZM1150'  DBPGM            * * * * * *    CSW036
     C                     MOVEL'ZM1151'  DBPGM            * * * * * *    CSW036
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING CALLED PROGRAM
     C           ZERR      IFEQ '1'
     C                     MOVELERRA,10   MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** IF ZSTAG IS 'A'
     C           ZSTAG     IFEQ 'A'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':87E:'   MTAG
     C                     END
     C*
     C** IF ZSTAG IS 'D'
     C           ZSTAG     IFEQ 'D'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':87F:'   MTAG
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C** IF MGPCOD EQUALS '1' OR '5'
     C                     MOVE *BLANKS   WWMDAT
     C           MGPCOD    IFEQ '1'
     C           MGPCOD    OREQ '5'
     C                     MOVEL'APMT'    MDAT
     C*
     C                     ELSE
     C*
     C                     MOVEL'FREE'    MDAT
     C                     END
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C                     MOVELMTAG      XMTAG   5                       069154
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C*
     C** IF MGDSS1 IS NOT BLANK
     C           MGDSS1    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGDSS1    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     EXSR #ZA
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD33T    - FORMAT MANDATORY FIELD :33T:                    *
     C**                                                              *
     C**  CALLS     - ZM1190, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD33T    BEGSR
     C*
     C** INITIALISE FLAG FOR FIELD FORMATTED SUCCESSFULLY
     C                     MOVE *BLANKS   WWFLAG  1
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,13   MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':33T:'   MTAG
     C*
      *** If trade basis is 'Y' = yield, set workfield to 'YLD'.          102916
     C           MGSTBS    IFEQ 'Y'                                       102916
     C                     MOVEL'YLD'     WWBASE                          102916
     C                     MOVE 'Y'       WWFLAG                          102916
     C                     ENDIF                                          102916
     C***IF*MGSTBS*EQUALS*'Y'*OR*'D'***                                   102916
     C***********MGSTBS    IFEQ 'Y'                                       102916
     C***********MGSTBS    OREQ 'D'                                       102916
     C           MGSTBS    IFEQ 'D'                                       102916
     C*
     C** SET BASIS WORKFIELD TO 'REN'
     C                     MOVEL'REN'     WWBASE  3
     C                     MOVE 'Y'       WWFLAG
     C                     END
     C*
     C** IF MGSTBS EQUALS 'P' OR MGTTYP EQUALS 'DM'
     C           MGSTBS    IFEQ 'P'
     C***********MGTTYP    OREQ 'DM'                                      067617
     C*
     C** IF MGSPBS EQUALS 'P' AND MGTTYP EQUALS 'TR'
     C           MGSPBS    IFEQ 'P'
     C           MGTTYP    ANDEQ'TR'
     C           MGTTYP    OREQ 'DM'                                      067617
     C           MGTTYP    OREQ 'DN'                                      067617
     C*
     C** SET BASIS WORKFIELD TO 'PCT'
     C                     MOVEL'PCT'     WWBASE
     C                     MOVE 'Y'       WWFLAG
     C                     END
     C*
     C** IF MGSPBS EQUALS 'U'
     C           MGSPBS    IFEQ 'U'
     C*
     C** ACCESS SDCURRL1 USING KEY OF MGNMCY FROM PF/MGF512PD
     C           MGNMCY    CHAINSDCURRL1             14
     C*
     C** IF RECORD NOT FOUND OR LAST CHANGE TYPE = 'D'
     C           *IN14     IFEQ '1'
     C           A6TYLC    OREQ 'D'
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,11   MERR
     C                     MOVE 'Y'       @ERR
     C                     MOVE MGNMCY    A6SWCY
     C                     END
     C*
     C** SET BASIS WORKFIELD TO A6SWCY FROM SDCURRL1
     C                     MOVELA6SWCY    WWBASE
     C                     MOVE 'Y'       WWFLAG
     C                     END
     C*
     C                     END
     C*
     C** CONVERT SECURITY PRICE TO SWIFT FORMAT
     C                     Z-ADDMGPRIC    ZPRIC  158
     C                     MOVE *BLANKS   ZSPRIC 16
     C                     MOVE *BLANKS   ZOPRIC 21
     C*
     C                     CALL 'ZM1190'               25
     C                     PARM           ZPRIC
     C                     PARM           ZSPRIC
     C                     PARM           ZOPRIC
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN25     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '021'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 021  *
     C                     MOVEL'ZM1190'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVELWWBASE    WD0103
     C                     MOVELZSPRIC    WD0419
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVELWWBASE    WR0103
     C                     MOVELZOPRIC    WR0424
     C                     MOVELWWRDAT    RDAT
     C*
     C** IF FIELDS ON MGF0510PD IN ERROR THEN UNABLE TO FORMAT FIELD
     C           MGPRIC    IFEQ 0
     C           WWFLAG    ORNE 'Y'
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C                     MOVE *BLANKS   MDAT
     C                     MOVE *BLANKS   RDAT
     C                     MOVELERRA,12   MERR
     C                     MOVE 'Y'       @ERR
     C                     END
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD32M    - FORMAT FIELD :32M:                              *
     C**                                                              *
     C**  CALLS     - ZM1240, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD32M    BEGSR
     C*
     C** IF MGSTAM IS NOT ZERO
     C           MGSTAM    IFNE 0
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,14   MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':32M:'   MTAG
     C*
     C** CONVERT MIDAS AMOUNT TO SWIFT FORMAT
     C                     Z-ADDMGDLAM    ZAMT   130
     C                     MOVELMGNMCY    ZCCY    3
     C                     MOVE *BLANKS   ZSAMT  17
     C                     MOVE *BLANKS   ZOAMT  21
     C                     MOVE *BLANKS   ZSCCY   3
     C                     MOVE *BLANKS   ZERR
     C*
     C                     CALL 'ZM1240'               26
     C                     PARM           ZAMT
     C                     PARM           ZCCY
     C                     PARM           ZSAMT
     C                     PARM           ZOAMT
     C                     PARM           ZSCCY
     C                     PARM           ZERR
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN26     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '022'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 022  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM ZM1240
     C           ZERR      IFEQ '1'
     C                     Z-ADDZAMT      WWAMT
     C           *LOCK     IN   LDA
     C                     MOVE '023'     DBASE            * * * * * * * *
     C                     MOVELDBERR2    DBKEY            *  DBERR 023  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVELZSCCY     WD0103
     C                     MOVELZSAMT     WD0419
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVELZSCCY     WR0103
     C                     MOVELZOAMT     WR0424
     C                     MOVELWWRDAT    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD33S    - FORMAT FIELD :33S:                              *
     C**                                                              *
     C**  CALLS     - ZM1190, *PSSR,                                  *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD33S    BEGSR
     C*
     C** IF MGRALL IS NOT ZERO
     C           MGRALL    IFNE 0
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,15   MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':33S:'   MTAG
     C*
     C** CONVERT SECURITY PRICE TO SWIFT FORMAT
     C                     Z-ADDMGRALL    ZPRIC  158
     C                     MOVE *BLANKS   ZSPRIC 16
     C                     MOVE *BLANKS   ZOPRIC 21
     C*
     C                     CALL 'ZM1190'               25
     C                     PARM           ZPRIC
     C                     PARM           ZSPRIC
     C                     PARM           ZOPRIC
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN25     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '024'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 024  *
     C                     MOVEL'ZM1190'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVELWWBASE    WD0103
     C                     MOVELZSPRIC    WD0419
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVELWWBASE    WR0103
     C                     MOVELZOPRIC    WR0424
     C                     MOVELWWRDAT    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD342    - FORMAT FIELD :34a:                              *
     C**                                                              *
     C**  CALLS     - ZM1240, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD342    BEGSR
     C*
     C** IF MGTTYP EQUALS 'TR'
     C           MGTTYP    IFEQ 'TR'
     C           MGITRA    IFNE 0
     C           MGACND    ORNE 0
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,16   MDES
     C*
     C** IF MGACIN EQUALS 'C' OR BLANK
     C           MGACIN    IFEQ 'C'
     C           MGACIN    OREQ *BLANKS
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':34G:'   MTAG
     C                     END
     C*
     C** IF MGACIN EQUALS 'X'
     C           MGACIN    IFEQ 'X'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':34H:'   MTAG
     C                     END
     C*
     C** CONVERT MIDAS AMOUNT TO SWIFT FORMAT
     C                     Z-ADDMGITRA    ZAMT
     C                     MOVELMGNMCY    ZCCY
     C                     MOVE *BLANKS   ZSAMT
     C                     MOVE *BLANKS   ZOAMT
     C                     MOVE *BLANKS   ZSCCY
     C                     MOVE *BLANKS   ZERR
     C*
     C                     CALL 'ZM1240'               26
     C                     PARM           ZAMT
     C                     PARM           ZCCY
     C                     PARM           ZSAMT
     C                     PARM           ZOAMT
     C                     PARM           ZSCCY
     C                     PARM           ZERR
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN26     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '025'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 025  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM ZM1240
     C           ZERR      IFEQ '1'
     C                     Z-ADDZAMT      WWAMT
     C           *LOCK     IN   LDA
     C                     MOVE '026'     DBASE            * * * * * * * *
     C                     MOVELDBERR2    DBKEY            *  DBERR 026  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVE *BLANKS   WWRDAT
     C*
     C           MGACND    IFNE 0
     C                     MOVELMGACND    MDAT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVE *BLANKS   WWRDAT
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVELZSCCY     WD0103
     C                     MOVELZSAMT     WD0419
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVELZSCCY     WR0103
     C                     MOVELZOAMT     WR0424
     C                     MOVELWWRDAT    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD32B    - FORMAT MANDATORY FIELD :32B:                    *
     C**                                                              *
     C**  CALLS     - ZM1240, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD32B    BEGSR
     C*
     C** IF REQUIRED FIELDS ON MGF512PD NOT PRESENT THEN ERROR
     C           MGDLAM    IFEQ 0
     C           MGSTCY    OREQ *BLANKS
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,13   MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,17   MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':32B:'   MTAG
     C*
     C** CONVERT MIDAS AMOUNT TO SWIFT FORMAT
     C                     Z-ADDMGSTAM    ZAMT
     C                     MOVELMGSTCY    ZCCY
     C                     MOVE *BLANKS   ZSAMT
     C                     MOVE *BLANKS   ZOAMT
     C                     MOVE *BLANKS   ZSCCY
     C                     MOVE *BLANKS   ZERR
     C*
     C                     CALL 'ZM1240'               26
     C                     PARM           ZAMT
     C                     PARM           ZCCY
     C                     PARM           ZSAMT
     C                     PARM           ZOAMT
     C                     PARM           ZSCCY
     C                     PARM           ZERR
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN26     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '027'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 027  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM ZM1240
     C           ZERR      IFEQ '1'
     C                     Z-ADDZAMT      WWAMT
     C           *LOCK     IN   LDA
     C                     MOVE '028'     DBASE            * * * * * * * *
     C                     MOVELDBERR2    DBKEY            *  DBERR 028  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVELZSCCY     WD0103
     C                     MOVELZSAMT     WD0419
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVELZSCCY     WR0103
     C                     MOVELZOAMT     WR0424
     C                     MOVELWWRDAT    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD71C    - FORMAT FIELD :71C:                              *
     C**                                                              *
     C**  CALLS     - ZM1240, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD71C    BEGSR
     C*
     C** PROCESS ONLY IF MGOTHC IS NOT ZERO
     C           MGOTHC    IFNE 0
     C*
     C** IF MGTTYP IS 'TR'
     C           MGTTYP    IFEQ 'TR'
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,18   MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':71C:'   MTAG
     C*
     C** CONVERT MIDAS AMOUNT TO SWIFT FORMAT
     C                     Z-ADDMGOTHC    ZAMT
     C                     MOVELMGNMCY    ZCCY
     C                     MOVE *BLANKS   ZSAMT
     C                     MOVE *BLANKS   ZOAMT
     C                     MOVE *BLANKS   ZSCCY
     C                     MOVE *BLANKS   ZERR
     C*
     C                     CALL 'ZM1240'               26
     C                     PARM           ZAMT
     C                     PARM           ZCCY
     C                     PARM           ZSAMT
     C                     PARM           ZOAMT
     C                     PARM           ZSCCY
     C                     PARM           ZERR
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN26     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '029'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 029  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM ZM1240
     C           ZERR      IFEQ '1'
     C                     Z-ADDZAMT      WWAMT
     C           *LOCK     IN   LDA
     C                     MOVE '030'     DBASE            * * * * * * * *
     C                     MOVELDBERR2    DBKEY            *  DBERR 030  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVEL'/CHGS/'  WD0106
     C                     MOVELZSCCY     WD0709
     C                     MOVELZSAMT     WD1026
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVEL'/CHGS/'  WR0106
     C                     MOVELZSCCY     WR0709
     C                     MOVELZOAMT     WR1030
     C                     MOVELWWRDAT    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD71B    - FORMAT FIELD :71B:                              *
     C**                                                              *
     C**  CALLS     - ZM1240, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD71B    BEGSR
     C*
     C** PROCESS ONLY IF MGOWNC IS NOT ZERO
     C           MGOWNC    IFNE 0
     C*
     C** IF MGTTYP IS 'TR'
     C           MGTTYP    IFEQ 'TR'
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,19   MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':71B:'   MTAG
     C*
     C** CONVERT MIDAS AMOUNT TO SWIFT FORMAT
     C                     Z-ADDMGOWNC    ZAMT
     C                     MOVELMGNMCY    ZCCY
     C                     MOVE *BLANKS   ZSAMT
     C                     MOVE *BLANKS   ZOAMT
     C                     MOVE *BLANKS   ZSCCY
     C                     MOVE *BLANKS   ZERR
     C*
     C                     CALL 'ZM1240'               26
     C                     PARM           ZAMT
     C                     PARM           ZCCY
     C                     PARM           ZSAMT
     C                     PARM           ZOAMT
     C                     PARM           ZSCCY
     C                     PARM           ZERR
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN26     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '031'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 031  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM ZM1240
     C           ZERR      IFEQ '1'
     C                     Z-ADDZAMT      WWAMT
     C           *LOCK     IN   LDA
     C                     MOVE '032'     DBASE            * * * * * * * *
     C                     MOVELDBERR2    DBKEY            *  DBERR 032  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVEL'/COMM/'  WD0106
     C                     MOVELZSCCY     WD0709
     C                     MOVELZSAMT     WD1026
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVEL'/COMM/'  WR0106
     C                     MOVELZSCCY     WR0709
     C                     MOVELZOAMT     WR1030
     C                     MOVELWWRDAT    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD36     - FORMAT FIELD :36:                               *
     C**                                                              *
     C**  CALLS     - ZM1190, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD36     BEGSR
     C*
     C** IF MGTTYP IS 'TR'
     C           MGTTYP    IFEQ 'TR'
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,20   MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C***********          MOVEL':36:'    MTAG                            057263
     C                     MOVEL':36: '   MTAG                            057263
     C*
     C** CONVERT EXCHANGE RATE TO SWIFT FORMAT
     C                     Z-ADDMGTDER    ZPRIC
     C                     MOVE *BLANKS   ZSPRIC
     C                     MOVE *BLANKS   ZOPRIC
     C*
     C                     CALL 'ZM1190'               25
     C                     PARM           ZPRIC
     C                     PARM           ZSPRIC
     C                     PARM           ZOPRIC
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN25     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '033'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 033  *
     C                     MOVEL'ZM1190'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVELZSPRIC    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVELZOPRIC    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD343    - FORMAT FIELD :34a:                              *
     C**                                                              *
     C**  CALLS     - ZM1240, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD343    BEGSR
     C*
     C** IF MGNPRO IS NOT ZERO
     C           MGNPRO    IFNE 0
     C*
     C** SET FLAG :34A: FIELD SUCCESSFULLY FORMATTED
     C                     MOVE 'Y'       WWFL34  1
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,21   MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':34A:'   MTAG
     C*
     C** CONVERT MIDAS AMOUNT TO SWIFT FORMAT
     C                     Z-ADDMGNPRO    ZAMT
     C                     MOVELMGSTCY    ZCCY
     C                     MOVE *BLANKS   ZSAMT
     C                     MOVE *BLANKS   ZOAMT
     C                     MOVE *BLANKS   ZSCCY
     C                     MOVE *BLANKS   ZERR
     C*
     C                     CALL 'ZM1240'               26
     C                     PARM           ZAMT
     C                     PARM           ZCCY
     C                     PARM           ZSAMT
     C                     PARM           ZOAMT
     C                     PARM           ZSCCY
     C                     PARM           ZERR
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN26     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '034'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 034  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM ZM1240
     C           ZERR      IFEQ '1'
     C                     Z-ADDZAMT      WWAMT
     C           *LOCK     IN   LDA
     C                     MOVE '035'     DBASE            * * * * * * * *
     C                     MOVELDBERR2    DBKEY            *  DBERR 035  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVELWWDATE    WD0106
     C                     MOVELZSCCY     WD0709
     C                     MOVELZSAMT     WD1026
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVELW8DATE    WR0108
     C                     MOVELZSCCY     WR0911
     C                     MOVELZOAMT     WR1232
     C                     MOVELWWRDAT    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD572    - FORMAT FIELD :57a:                              *
     C**                                                              *
     C**  CALLS     - ZM1080, *PSSR, #ZA                              *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD572    BEGSR
     C*
     C** IF MGAWIN IS NOT BLANK
     C           MGAWIN    IFNE *BLANKS
     C*
     C** SET FLAG FIELD :57a: SUCCESSFULLY FORMATED
     C                     MOVE 'Y'       WWFL57  1
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,22   MDES
     C*
     C** FORMAT ADDRESS FIELDS
     C                     MOVELMGAWIN    ZDEST
     C                     MOVELMGDEST    ZSRCN   6
     C                     MOVE *BLANKS   ZSADD1
     C                     MOVE *BLANKS   ZSADD2
     C                     MOVE *BLANKS   ZSADD3
     C                     MOVE *BLANKS   ZSADD4
     C                     MOVE *BLANKS   ZSTAG
     C                     MOVE *BLANKS   ZSTYPE
     C                     MOVE *BLANKS   ZERR
     C                     MOVE *BLANKS   ZCRNM
     C                     MOVE *BLANKS   ZCRTN
     C*
     C*********************CALL 'ZM1080'               27                 CSW036
     C                     CALL 'ZM1081'               27                 CSW036
     C                     PARM           ZDEST
     C                     PARM           ZSRCN
     C                     PARM           ZSADD1
     C                     PARM           ZSADD2
     C                     PARM           ZSADD3
     C                     PARM           ZSADD4
     C                     PARM           ZSTAG
     C                     PARM           ZSTYPE
     C                     PARM           ZERR
     C                     PARM           ZCRNM
     C                     PARM           ZCRTN
     C                     PARM           ZDBEI                                               222373
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN27     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '036'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 036  *
     C*********************MOVEL'ZM1080'  DBPGM            * * * * * * * *CSW036
     C                     MOVEL'ZM1081'  DBPGM            * * * * * * * *CSW036
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING CALLED PROGRAM
     C           ZERR      IFEQ '1'
     C                     MOVELERRA,14   MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** IF ZSTAG IS 'A'
     C           ZSTAG     IFEQ 'A'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':57A:'   MTAG
     C                     END
     C*
     C** IF ZSTAG IS 'B'
     C           ZSTAG     IFEQ 'B'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':57B:'   MTAG
     C                     END
     C*
     C** IF ZSTAG IS 'D'
     C           ZSTAG     IFEQ 'D'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':57D:'   MTAG
     C                     END
     C*
     C** IF MGAWIL IS NOT BLANK
     C           MGAWIL    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGAWIL    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     EXSR #ZA
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD72     - FORMAT FIELD :72:                               *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD72     BEGSR
     C*
     C** IF MGSRL1 IS NOT BLANK
     C           MGSRL1    IFNE *BLANKS
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,23   MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C***********          MOVEL':72:'    MTAG                            057263
     C                     MOVEL':72: '   MTAG                            057263
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGSRL1    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** IF MGSRL2 IS NOT BLANK
     C           MGSRL2    IFNE *BLANKS
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGSRL2    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C** IF MGSRL3 IS NOT BLANK
     C           MGSRL3    IFNE *BLANKS
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGSRL3    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  OFLDS     - FORMAT FIELDS FOR MIDAS SWIFT DATABASE FILES    *
     C**                                                              *
     C**  CALLS     - ZM0060, *PSSR, ZM1240, #W04                     *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           OFLDS     BEGSR
     C*
     C** INITIALISE FIELDS ON PF/MGOREFPD
     C                     CLEARMGOREFD0
     C*
     C** HISTORY RETENTION DATE
     C           MGVDAT    ADD  ENDSMN    WWHDAT  50
     C                     MOVE WWHDAT    ZMDAY   50
     C                     MOVE *BLANKS   ZSDATE  6
     C*
     C                     CALL 'ZM0060'               22
     C                     PARM           ZMDAY
     C                     PARM           ZSDATE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN22     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '038'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 038  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** SET HISTORY RETENTION DATE TO OUTPUT FIELD
     C                     MOVELZSDATE    HRDT
     C*
     C** MESSAGE GENERATION DATE
     C                     MOVE BJRDNB    ZMDAY
     C                     MOVE *BLANKS   ZSDATE
     C*
     C                     CALL 'ZM0060'               22
     C                     PARM           ZMDAY
     C                     PARM           ZSDATE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN22     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '039'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 039  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** SET MESSAGE GEN. DATE TO OUTPUT FIELD
     C                     MOVELZSDATE    MGDE
      *                                                                   113352
      ** Set-up Message Generation  Century flag                          113352
      *                                                                   113352
     C                     MOVELMGDE      YY      20                      113352
     C           YY        IFLT 72                                        113352
     C                     MOVE '2'       CIND                            113352
     C                     ELSE                                           113352
     C                     MOVE '1'       CIND                            113352
     C                     ENDIF                                          113352
     C*
     C** CONVERT MIDAS AMOUNT TO SWIFT FORMAT
     C                     Z-ADDMGSTAM    ZAMT
     C                     MOVELMGSTCY    ZCCY
     C                     MOVE *BLANKS   ZSAMT
     C                     MOVE *BLANKS   ZOAMT
     C                     MOVE *BLANKS   ZSCCY
     C                     MOVE *BLANKS   ZERR
     C*
     C                     CALL 'ZM1240'               26
     C                     PARM           ZAMT
     C                     PARM           ZCCY
     C                     PARM           ZSAMT
     C                     PARM           ZOAMT
     C                     PARM           ZSCCY
     C                     PARM           ZERR
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN26     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '040'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 040  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM ZM1240
     C           ZERR      IFEQ '1'
     C                     Z-ADDZAMT      WWAMT
     C           *LOCK     IN   LDA
     C                     MOVE '041'     DBASE            * * * * * * * *
     C                     MOVELDBERR2    DBKEY            *  DBERR 041  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** SET SWIFT AMOUNT TO OUTPUT FIELD
     C                     MOVELZSAMT     AMTS
     C*
     C** FORMAT FIELDS ON PF/MGOREFPD
     C                     EXSR #W04
     C*
     C/COPY WNCPYSRC,MG0512C004
     C** WRITE RECORD TO PF/MGOREFPD
     C                     MOVELMGCHG     AORR                                                CSW025
     C                     WRITEMGOREFD0               16
     C*
     C** IF ERROR ON WRITE OF RECORD
     C           *IN16     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '042'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 042  *
     C                     MOVEL'MGOREFPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0512'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** PROCESS MULTIPLE OCCURRENCE DATA STRUCTURE
     C                     Z-ADD1         Y       20
     C           Y         DOWLTX
     C           Y         OCUR MULT
     C*
     C** FORMAT FIELDS ON PF/MGOMSGPD
     C                     MOVELMDAT      MFLD
     C                     MOVELWWDATA    TRNO
     C                     MOVE *BLANKS   PTDL
     C*
     C** WRITE RECORD TO PF/MGOMSGPD
     C                     WRITEMGOMSGD0               17
     C*
     C** IF ERROR ON WRITE OF RECORD
     C           *IN17     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '043'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 043  *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0512'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** INCREMENT INDEX
     C                     ADD  1         Y
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  #ZA       - FORMAT ADDRESSES                                *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - FLD822, FLD872, FLD873, FLD572                  *
     C**                                                              *
     C*****************************************************************
     C*
     C           #ZA       BEGSR
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C           ZSADD1    IFNE *BLANKS                                   122283
     C                     MOVELZSADD1    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** PRINT CUSTOMER REPORT NAME AND REPORT TOWN
     C           MTAG      IFEQ ':82A:'
     C***********MTAG      OREQ ':87E:'                                   069154
     C           XMTAG     OREQ ':87E:'                                   069154
     C           MTAG      OREQ ':57A:'
     C                     MOVELZCRNM     CRNM
     C                     MOVELZCRTN     CRTN
     C                     MOVE *BLANKS   XMTAG                           069154
     C                     END
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*                                                                   122283
     C                     END                                            122283
     C*
     C** IF ZSADD2 IS NOT BLANK
     C           ZSADD2    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELZSADD2    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C** IF ZSADD3 IS NOT BLANK
     C           ZSADD3    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELZSADD3    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C** IF ZSADD4 IS NOT BLANK
     C           ZSADD4    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELZSADD4    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  #W01      - PRINT MESSAGE GENERATED DETAILS                 *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           #W01      BEGSR
     C*
     C** INITIALISE FLAGS AND FIELDS USED IN REPORT
     C                     MOVE '0'       *IN80
     C                     MOVE '0'       *IN81
     C                     CLEARTEXT1
     C*
     C** IF MGMODI EQUALS 'SE' AND MGTTYP EQUALS 'TR'
     C           MGMODI    IFEQ 'SE'
     C           MGTTYP    ANDEQ'TR'
     C                     MOVE '1'       *IN80
     C*
     C** FORMAT TRADE REFERENCE
     C                     MOVELMGTNUM    R1REFE
     C*
     C** FORMAT TRADE TYPE
     C                     MOVELMGTSTP    R1TYP1
     C*
     C** FORMAT TRADE TYPE DESCRIPTION
     C           MGTSTP    IFEQ 'TS'
     C                     MOVELTYPA,1    R1TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'TP'
     C                     MOVELTYPA,2    R1TYP2
     C                     END
     C*
     C                     END
     C*
     C** IF MGMODI EQUALS 'SE' AND MGTTYP EQUALS 'DM'
     C           MGMODI    IFEQ 'SE'
     C           MGTTYP    ANDEQ'DM'
     C           MGMODI    OREQ 'SE'                                      CSW003
     C           MGTTYP    ANDEQ'DN'                                      CSW003
     C                     MOVE '1'       *IN81
     C*
     C** FORMAT MOVEMENT REFERENCE
     C                     MOVELMGTNUM    R1REFE
     C*
     C** FORMAT MOVEMENT TYPE
     C                     MOVELMGTSTP    R1TYP1
     C*
     C** FORMAT MOVEMENT TYPE DESCRIPTION
     C           MGTSTP    IFEQ 'RR'
     C           MGTSTP    OREQ 'PL'                                      103449
     C                     MOVELTYPA,3    R1TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'RP'
     C           MGTSTP    OREQ 'PD'                                      103449
     C                     MOVELTYPA,4    R1TYP2
     C                     END
     C*
     C                     END
     C*
     C** FORMAT SENDER BRANCH
     C                     MOVELMGSNBR    R1SND1
     C*
     C** FORMAT SENDER CUSTOMER NUMBER
     C                     MOVEL@SNDR     R1SND2
     C*
     C** FORMAT SENDER NETWORK ADDRESS
     C                     MOVEL@NWSN     R1SND3
     C*
     C** FORMAT SENDER REPORT NAME
     C                     MOVEL@SRNM     R1SRNM
     C*
     C** FORMAT SENDER REPORT TOWN
     C                     MOVEL@SRTN     R1SRTN
     C*
     C** FORMAT DESTINATION REPORT NAME
     C                     MOVEL@DRNM     R1DRNM
     C*
     C** FORMAT DESTINATION REPORT TOWN
     C                     MOVEL@DRTN     R1DRTN
     C*
     C** FORMAT MESSAGE PRIORITY
     C                     MOVEL'N'       R1PRTY
     C*
     C** FORMAT DESTINATION CUSTOMER NUMBER
     C**********           Z-ADDMGDEST    R1DES1                                              CSD027
     C                     MOVE MGDEST    R1DES1                                              CSD027
     C*
     C** FORMAT DESTINATION NETWORK ADDRESS
     C                     MOVEL@NWDS     R1DES2
     C*
     C** FORMAT NETWORK
     C                     MOVEL@NWRK     R1NETW
     C*
     C** WRITE THE HEADER AND THE MAIN TEXT FOR THE REPORT
     C                     WRITEHEADER1
     C                     WRITETEXT1
     C*
     C** PROCESS MULTIPLE OCCURRENCE DATA STRUCTURE
     C                     Z-ADD0         I       20
     C           I         DOWLEX
     C                     ADD  1         I
     C           I         OCUR MULT
     C*
     C** WRITE A DETAIL LINE TO THE REPORT CHECKING FOR OVERFLOW
     C           WWOFL1    SUB  WWPTL1    WWLINE
     C*
     C           WWLINE    IFLT 3
     C                     WRITEHEADER1
     C                     END
     C                     WRITELINE1
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  #W02      - PRINT MESSAGE NOT GENERATED DETAILS             *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           #W02      BEGSR
     C*
     C** INITIALISE FLAGS AND FIELDS USED IN REPORT
     C                     MOVE '0'       *IN82
     C                     MOVE '0'       *IN83
     C                     CLEARTEXT2
     C*
     C** OPEN PRINTER FILE
     C           *IN50     IFEQ '0'
     C************         OPEN MG0512P2                                  091250
     C                     OPEN MG0512A2                                  091250
     C                     MOVE '1'       *IN50
     C                     END
     C*
     C** IF MGMODI EQUALS 'SE' AND MGTTYP EQUALS 'TR'
     C           MGMODI    IFEQ 'SE'
     C           MGTTYP    ANDEQ'TR'
     C                     MOVE '1'       *IN82
     C*
     C** FORMAT TRADE REFERENCE
     C                     MOVELMGTNUM    R2REFE
     C*
     C** FORMAT TRADE TYPE
     C                     MOVELMGTSTP    R2TYP1
     C*
     C** FORMAT TRADE TYPE DESCRIPTION
     C           MGTSTP    IFEQ 'TS'
     C                     MOVELTYPA,1    R2TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'TP'
     C                     MOVELTYPA,2    R2TYP2
     C                     END
     C*
     C                     END
     C*
     C** IF MGMODI EQUALS 'SE' AND MGTTYP EQUALS 'DM'
     C           MGMODI    IFEQ 'SE'
     C           MGTTYP    ANDEQ'DM'
     C           MGMODI    OREQ 'SE'                                      CSW003
     C           MGTTYP    ANDEQ'DN'                                      CSW003
     C                     MOVE '1'       *IN83
     C*
     C** FORMAT MOVEMENT REFERENCE
     C                     MOVELMGTNUM    R2REFE
     C*
     C** FORMAT MOVEMENT TYPE
     C                     MOVELMGTSTP    R2TYP1
     C*
     C** FORMAT MOVEMENT TYPE DESCRIPTION
     C           MGTSTP    IFEQ 'RR'
     C           MGTSTP    OREQ 'PL'                                      103449
     C                     MOVELTYPA,3    R2TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'RP'
     C           MGTSTP    OREQ 'PD'                                      103449
     C                     MOVELTYPA,4    R2TYP2
     C                     END
     C*
     C                     END
     C*
     C** FORMAT SENDER BRANCH
     C                     MOVELMGSNBR    R2SND1
     C*
     C** FORMAT SENDER CUSTOMER NUMBER
     C                     MOVEL@SNDR     R2SND2
     C*
     C** FORMAT SENDER NETWORK ADDRESS
     C                     MOVEL@NWSN     R2SND3
     C*
     C** FORMAT MESSAGE PRIORITY
     C                     MOVEL'N'       R2PRTY
     C*
     C** FORMAT DESTINATION CUSTOMER NUMBER
     C**********           Z-ADDMGDEST    R2DES1                                              CSD027
     C                     MOVE MGDEST    R2DES1                                              CSD027
     C*
     C** FORMAT DESTINATION NETWORK ADDRESS
     C                     MOVEL@NWDS     R2DES2
     C*
     C** FORMAT DESTINATION ERROR MESSAGE
     C                     MOVELDSERR     R2EMSG
     C*
     C** FORMAT NETWORK
     C                     MOVEL@NWRK     R2NETW
     C*
     C** FORMAT DESTINATION ERROR MESSAGE
     C                     MOVELNWERR     R2NWER
     C*
     C** FORMAT MODULE ID
     C                     MOVELMGMODI    R2MODI
     C*
     C** FORMAT TRANSACTION NUMBER
     C                     MOVELMGTNUM    R2TNUM
     C*
     C** WRITE THE HEADER AND THE MAIN TEXT FOR THE REPORT
     C                     WRITEHEADER2
     C                     WRITETEXT2
     C*
     C** PROCESS MULTIPLE OCCURRENCE DATA STRUCTURE
     C                     Z-ADD0         I       20
     C           I         DOWLEX
     C                     ADD  1         I
     C           I         OCUR MULT
     C*
     C** WRITE A DETAIL LINE TO THE REPORT CHECKING FOR OVERFLOW
     C           WWOFL2    SUB  WWPTL2    WWLINE
     C*
     C           WWLINE    IFLT 3
     C                     WRITEHEADER2
     C                     END
     C                     WRITELINE2
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  #W04      - FORMAT FIELDS FOR MGOREFPD                      *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - OFLDS
     C**                                                              *
     C*****************************************************************
     C*
     C           #W04      BEGSR
     C*
     C** FORMAT FIELDS ON PF/MGOREFPD
     C                     MOVELMGMODI    MODI
     C                     MOVELWWDATA    TRNO
     C                     MOVELMGTNUM    MTRN
     C                     MOVELMGTTYP    TNTP
     C                     MOVELMGTSTP    SBTP
     C                     MOVEL@NWRK     NWRK
     C                     MOVELA8BICN    SECN
     C                     MOVEL@NWSN     NWSN
     C**********           Z-ADDMGDEST    DECN                                                CSD027
     C                     MOVE MGDEST    DECN                                                CSD027
     C                     MOVEL@NWDS     NWDS
     C                     MOVE *BLANKS   NWBN
     C                     MOVEL'512'     MTPY
     C                     MOVEL'N'       MPRY
     C***********          MOVEL'1'       MGSG                            082282
     C***********          MOVEL'CRTD'    MGST                            082282
     C                     EXSR MSTAT                                     082282
     C                     TIME           MGTM
     C                     MOVELBJMRDT    LADT
     C                     TIME           LATM
     C                     MOVELWWDATE    SVDT
      *                                                                   141572
      ** Setup century indicator CINDV for value date SVDT                141572
     C           SVDT      IFEQ *BLANK                                    141572
     C                     MOVE *BLANK    CINDV                           141572
     C                     ELSE                                           141572
     C                     MOVELSVDT      YY      20                      141572
     C           YY        IFLT 72                                        141572
     C                     MOVE '2'       CINDV                           141572
     C                     ELSE                                           141572
     C                     MOVE '1'       CINDV                           141572
     C                     ENDIF                                          141572
     C                     ENDIF                                          141572
      *                                                                   141572
     C                     MOVELMGSTCY    CCY
     C                     MOVELMGSNBR    BRCA
     C                     MOVEL'Y'       TSKS
     C*                                                                   094335
     C           MGCHTP    IFEQ 'I'                                       094334
     C                     MOVEL'NEW   '  LSCC                            094335
     C                     END                                            094335
     C*                                                                   094335
     C           MGCHTP    IFEQ 'A'                                       094335
     C                     MOVEL'AMEND '  LSCC                            094335
     C                     END                                            094335
     C*                                                                   094335
     C           MGCHTP    IFEQ 'D'                                       094335
     C                     MOVEL'CANCEL'  LSCC                            094335
     C                     END                                            094335
     C*                                                                   094335
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT                                                              082282
     C*****************************************************************   082282
     C**                                                              *   082282
     C**  MSTAT     - Determine message status                        *   082282
     C**                                                              *   082282
     C*****************************************************************   082282
      *                                                                   082282
     C           MSTAT     BEGSR                                          082282
      *                                                                   082282
      * Reset status array                                                082282
     C                     CLEAR@ST                                       082282
      *                                                                   082282
      * Set key to destination customer number                            082282
     C                     MOVELDECN      @KEY      P                     082282
      *                                                                   082282
      * Get customer details via access object                            082282
     C                     CALL 'AOCUSTR0'                                082282
     C                     PARM *BLANKS   @RTCD   7                       082282
     C                     PARM '*KEY   ' @OPTN   7                       082282
     C                     PARM           @KEY   10                       082282
     C                     PARM *BLANKS   @KYST   7                       082282
     C           SDCUST    PARM SDCUST    DSSDY                           082282
      *                                                                   082282
      * Fill status array                                                 082282
     C           @RTCD     IFEQ *BLANKS                                   082282
     C                     MOVELBBDS01    @ST,1                           082282
     C                     MOVELBBDS02    @ST,2                           082282
     C                     MOVELBBDS03    @ST,3                           082282
     C                     MOVELBBDS04    @ST,4                           082282
     C                     MOVELBBDS05    @ST,5                           082282
     C                     MOVELBBDS06    @ST,6                           082282
     C                     MOVELBBDS07    @ST,7                           082282
     C                     MOVELBBDS08    @ST,8                           082282
     C                     MOVELBBDS09    @ST,9                           082282
     C                     MOVELBBDS10    @ST,10                          082282
     C                     MOVELBBDS11    @ST,11                          082282
     C                     MOVELBBDS12    @ST,12                          082282
     C                     MOVELBBDS13    @ST,13                          082282
     C                     MOVELBBDS14    @ST,14                          082282
     C                     MOVELBBDS15    @ST,15                          082282
     C                     ENDIF                                          082282
      *                                                                   082282
      * Is the message type in the array? Set on *IN60 if it is.          082282
     C           '512'     LOKUP@ST                      60               082282
     C  N60      '51*'     LOKUP@ST                      60               082282
     C  N60      '5**'     LOKUP@ST                      60               082282
      *                                                                   082282
      * If found, set status to 'ready to send', else set to 'created'    082282
     C           *IN60     IFEQ *ON                                       082282
     C                     MOVEL'RSND'    MGST                            082282
     C                     MOVEL'2'       MGSG                            082282
     C                     ELSE                                           082282
     C                     MOVEL'CRTD'    MGST                            082282
     C                     MOVEL'1'       MGSG                            082282
     C                     ENDIF                                          082282
      *                                                                   082282
     C                     ENDSR                                          082282
      *                                                                   082282
     C*****************************************************************   082282
     C/COPY WNCPYSRC,MG0512C005
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  PSSR      - ERROR HANDLING                                  *
     C**                                                              *
     C**  CALLS     -                                                 *
     C**                                                              *
     C**  CALLED BY -                                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C** ROLLBACK ALL FILE CHANGES
     C                     ROLBK
     C*
     C** WRITE DATABASE ERROR TO REPORT
     C                     OPEN MG0512AU
     C                     WRITEHEAD
     C                     WRITEDBERROR
     C*
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C/COPY MGCPYSRC,SWIFTTRANS                                           BLI605
** CPY@   Object Copyright
(c) Finastra International Limited 2001
** ERRA   ERROR MESSAGES
Customer number not found
Unable to format mandatory field :20:
Unable to format mandatory field :21:
Unable to format mandatory field :23:
Unable to format mandatory field :31P:
Unable to format mandatory field :35A:
Unable to format mandatory field :35B:
Unable to format addresses for field :82a:
Unable to format mandatory field :87a:
Unable to format addresses for field :87a:
Nominal currency not found on currency file
Unable to format mandatory field :33T:
Unable to format mandatory field :32B:
Unable to format addresses for field :57a:
*** Message is Unroutable ***
** DESA   DESCRIPTIONS
                              T.R.N.
                   RELATED REFERENCE
              FURTHER IDENTIFICATION
             DATE AND PLACE OF TRADE
                       TIME OF TRADE
         DATE OF INTENDED SETTLEMENT
              QUANTITY OF SECURITIES
        IDENTIFICATION OF SECURITIES
                         NEXT COUPON
                        COUNTERPARTY
              RECEIVER OF SECURITIES
             DELIVERER OF SECURITIES
                          DEAL PRICE
                         DEAL AMOUNT
                 SPECIAL CONCESSIONS
                    ACCRUED INTEREST
                   SETTLEMENT AMOUNT
                       OTHER CHARGES
                         OWN CHARGES
                       EXCHANGE RATE
                        NET PROCEEDS
            ACCOUNT WITH INSTITUTION
      SENDER TO RECEIVER INFORMATION
** TYPA   TRADE/MOVEMENT TYPE DESCRIPTIONS
TRADE SALE
TRADE PURCHASE
REVERSE REPURCHASE
REPURCHASE AGREEMENT
** SUPP - add any fields tags which should be suppressed
