     H        1                           F                               S01421
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MG Deliver free messages')
     H******  1                                                           S01421
      *****************************************************************
      *                                                               *
      *  Midas - MESSAGE GENERATION                              *
      *                                                               *
      *   MG0522 - FORMAT MT522 REPORT MESSAGES                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSW025             Date 26Mar02               *
      *                 192221             Date 26Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSW201             Date 02May01               *
      *                 183583             Date 25Jun01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 185241             Date 29Dec00               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 141572             Date 21Sep98               *
      *                 113352             Date 13Feb97               *
      *                 122283             DATE 16FEB98               *
      *                 056532             DATE 09MAY96               *
      *                 057263             DATE 09MAY96               *
      *                 078256             DATE 07MAY96               *
      *                 082282             DATE 02MAY96               *
      *                 CSW003             DATE 09JAN96               *  *
      *                 091250             DATE 24FEB95               *
      *                 085245             DATE 11APR95               *
      *                 080096             DATE 12JAN95               *
      *                 S01421             DATE 18AUG93               *
      *                 S01401             DATE 17JUN93               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CSW025 - Midas Message Manager                               *
      *           - Pass original change type                         *
      *  192221 - Format message tag 87 (Receiver Of Security) to     *
      *           include detail lines 2 to 4.                        *
      *  CSW201 - SWIFT 2001 Standards Update                         *
      *  183583 - Add extension file MGOEXTPD to store customer no.   *
      *           of field with SWIFT address.                        *
      *  185241 - Database error in SEC0603. Applied fixes 141579,    *
      *           138847 and 130960.                                  *
      *  CDL008 - Continuous Linked Settlement (Recompiled)           *
      *  141572 - Setup CINDV (century flag) SVDT ( value date).      *
      *  113352 - Set-up CIND (Century flag)  to correct year2000     *
      *           display problem.                                    *
      *  122283 - Blank line in MGOMSGPD if account number is blank   *
      *           and not account line.                               *
      *  056532 - Process incorrect car. '-' ' ' ':' in first position*
      *  057263 - All postiion in field "MTAG" need to be full.       *
      *  078256 - Use nominal decimal places from file                *
      *  082282 - Set message status according to customer details    *
      *  CSW003 - MT5XX Message Generation - Phase 2.                 *
      *  091250 - Print Files MG0522P1/P2 renamed to MG0522A1/A2 as   *
      *           these are audit reports.                            *
      *  085245 - MT52x messages to Federal Reserve Bank of New York  *
      *           It is required to use the CUSIP number not the ISIN/*
      *           Common code in field :35B. It also required to print*
      *           the word 'CUSIP' for US Treasury Bill but not for   *
      *           US Treasury Notes.                                  *
      *  080096 - RECOMPILE BECAUSE COPY MEMBER MGCPYSRC,SWIFTTRANS   *
      *           CHANGED                                             *
      *   S01421 - REPLACE THE SWIFT TRANSLATION TABLE WITH A         *
      *              /COPY STATEMENT (SWIFTTRANS).                    *
      *                                                               *
      *  S01401 - MT500 messages for Release 10.                      *
      *           (This also includes SWIFT SEPT 93 message format    *
      *           changes)                                            *
      *                                                               *
      *****************************************************************
     F*
     FMGF522PDUF  E                    DISK         KINFSR *PSSR
     F                                              KCOMIT
     F/COPY WNCPYSRC,MG0522F002
     FSDBANKPDIF  E                    DISK         KINFSR *PSSR
     FSDBRCHL1IF  E           K        DISK         KINFSR *PSSR
     FSECRTNL1IF  E           K        DISK         KINFSR *PSSR      UC
     F/COPY WNCPYSRC,MG0522F001
     FSDMGMEPDIF  E                    DISK         KINFSR *PSSR
     F*
     FMGOREFPDO   E                    DISK         KINFSR *PSSR
     F                                              KCOMIT
     FMGOMSGPDO   E                    DISK         KINFSR *PSSR
     F                                              KCOMIT
     FMGOEXTPDO   E                    DISK         KCOMIT                183583
     F                                              KINFSR *PSSR          183583
     FMGF579PDO   E                    DISK         KINFSR *PSSR      UC
     F*G0522P1O***E********************PRINTER******KINFDS*OVRFL1         091250
     FMG0522A1O   E                    PRINTER      KINFDS OVRFL1         091250
     F                                              KINFSR *PSSR
     F*G0522P2O***E********************PRINTER******KINFDS*OVRFL2     UC  091250
     FMG0522A2O   E                    PRINTER      KINFDS OVRFL2     UC  091250
     F                                              KINFSR *PSSR
     FMG0522AUO   E                    PRINTER                        UC
     F*
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     E*
     E** ARRAY USED FOR COPYRIGHT
     E                    CPY@    1   1 80
     E*
     E** ARRAY USED TO FORMAT ERROR MESSAGE FIELD ON MULTIPLE
     E** OCCURENCE DATA STRUCTURE
     E                    ERRA    1  11 50
     E*
     E** ARRAY USED TO FORMAT DESCRIPTION FIELD ON MULTIPLE
     E** OCCURENCE DATA STRUCTURE
     E                    DESA    1  13 36
     E*
     E** ARRAY USED TO REMOVE FORBIDDEN CHARACTERS (E.G. %)
     E                    FORB       50  1
     E*
     E** ARRAY USED TO FORMAT TRADE/MOVEMENT TYPE
     E** DESCRIPTION ON REPORTS
     E                    TYPA    1  11 20
     E*
      *
      **  Array used to define fields to be suppressed
     E                    SUPP    1  50  5
      *                                                                   082282
      **  Array used to set message status                                082282
     E                    @ST        15  3                                082282
     E/COPY WNCPYSRC,MG0522E001
     E*****************************************************************
     E/EJECT
     E*****************************************************************
     I*
     I** Multiple occurrence data structure for message
     IMULT       IDS                         80
     I                                        1  36 MDES
     I                                       37  41 MTAG
     I                                       42  91 MDAT
     I                                       92 141 MERR
     I                                      160 179 CRNM
     I                                      182 191 CRTN
     I                                      142 191 RDAT
      *                                                                   183583
      ** Multiple occurrence data structure for extension file            183583
      *                                                                   183583
     IEXTN        DS                         30                           183583
     I                                        1   20MPSN                  183583
     I                                        3   7 MTG                   183583
     I                                        8  15 MCSI                  183583
      *                                                                   183583
      ** Field tag data structure                                         183583
      *                                                                   183583
     I            DS                                                      183583
     I                                        1   5 @MTG                  183583
     I                                        2   2 FLDTYP                183583
     I                                        4   4 FLDTAG                183583
     I*
     I** Local data area
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I** DATA STRUCTURE FOR CONSTRUCTING DATA FIELD FOR FILE
     IWWMDAT      DS                         50
     I                                        1   3 WD0103
     I                                        4   9 WD0409
     I                                       10  16 WD1016
     I                                        1   6 WD0106
     I                                        7  26 WD0726
     I                                        4  19 WD0419
     I                                        1   5 WD0105
     I                                        6  17 WD0617
     I                                        1   4 WD0104
     I                                        5  16 WD0516
     I*
     I** DATA STRUCTURE FOR CONSTRUCTING DATA FIELD FOR REPORT
     IWWRDAT      DS                         50
     I                                        1   8 WR0108
     I                                        9  24 WR0924
     I                                        1   3 WR0103
     I                                        4  24 WR0424
     I                                        9  28 WR0928
     I*
     I** DATA STRUCTURE FOR DBERROR
     IDBERR       DS                             20
     I                                        1   2 @MODI
     I                                        3  17 @TRAN
     I*
     I** DATA STRUCTURE FOR DBERROR
     IDBERR1      DS                             26
     I                                        1   6 @SNDR
     I                                        7  18 @SCSD
     I                                       19  24 @DEST
     I                                       25  26 @SETP
     I*
     I** DATA STRUCTURE FOR DBERROR
     IDBERR2      DS                             20
     I                                        1   2 WWMODE
     I                                        3  17 @TREF
     I*
     I** DATA STRUCTURE TO SPLIT DATE
     I            DS
     I                                        1   6 ZSDATE
     I                                        1   2 WWYEAR
     I                                        3   4 WWMNTH
     I                                        5   6 WWDAY
     I*
     I** DATA STRUCTURE TO BUILD DATE
     I            DS
     I                                        1   8 WRDATE
     I                                        1   2 WRYEAR
     I I            '/'                       3   3 WRSL1
     I                                        4   5 WRMNTH
     I I            '/'                       6   6 WRSL2
     I                                        7   8 WRDAY
     I*
     I** DATA STRUCTURE TO BUILD PARAMETER FOR MG5900
     I            DS
     I                                        1  15 WWTREF
     I                                       13  13 WT1313
     I*
     I***DATA*STRUCTURE*FOR*OVERFLOW*ON*PRINTER*MG0522P1***               091250
     I** FILE INFORMATION DATA STRUCTURE FOR MG0522A1                     091250
     IOVRFL1      DS
     I                                    B 188 1890WWOFL1
     I                                    B 367 3680WWPTL1
     I*
     I***DATA*STRUCTURE*FOR*OVERFLOW*ON*PRINTER*MG0522P2***               091250
     I** FILE INFORMATION DATA STRUCTURE FOR MG0522A2                     091250
     IOVRFL2      DS
     I                                    B 188 1890WWOFL2
     I                                    B 367 3680WWPTL2
     I*
     I** DATA STRUCTURE FOR CRLF CONTROL CHARACTER
     ICRLFDS      DS                              2
     I                                        1   2 CTRC
     I                                        1   1 WWCR
     I                                        2   2 WWLF
     I*
     I* DATA STRUCTURE FOR EXTENDED SETTLEMENT FIELDS
     I            DS
     I                                        1  16 WXSFLD
     I                                        1   1 WXS53
     I                                        2   2 WXS57
     I                                        3   3 WXS58
     I                                        4   4 WXS71
     I                                        5   5 WXS72
     I                                        6   6 WXS77F
     I                                        7   7 WXS77R
     I                                        8   8 WXS79
     I                                        9   9 WXS82I
     I                                       10  10 WXS82C
     I                                       11  11 WXS83
     I                                       12  12 WXS84
     I                                       13  13 WXS85
     I                                       14  14 WXS87R
     I                                       15  15 WXS87D
     I                                       16  16 WXS88
     I*
     ISCSARD    E DSSCSARDPD                                              CSW003
      ** External data structure for Switchable Features                  CSW003
      *                                                                   CSW003
     IDSFDY     E DSDSFDY                                                 CSW003
      ** First Data Structure for Access Objects                          CSW003
      *                                                                   CSW003
      * Data structures for access objects                                082282
     IDSSDY     E DSDSSDY                                                 082282
     ISDCUST    E DSSDCUSTPD                                              082282
      *                                                                   082282
     I*****************************************************************
     I/EJECT
     I*****************************************************************
      *                                                               *
      * 1. Multiple Data Structure MULT                               *
      *      The multiple occurrence data structure is used to        *
      *      store up the records that make up the SWIFT message      *
      *      before writing the whole message to file.                *
      *      To store the records of the message, you must            *
      *      first access an OCUR record, obtain the Message Tag      *
      *      & Message field data values for this OCUR record,        *
      *      then go on to the next OCUR record and continue          *
      *      this process until the SWIFT message is complete.        *
      *                                                               *
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*   MAIN PROCESSING - FORMAT MT522                              *
     C*                                                               *
     C*   CALLS THE FOLLOWING SUBROUTINES                             *
     C*                                                               *
     C*   INIT     - INITIALISATION                                   *
     C*   INTF     - INITIALISE NON-CONSTANT FIELDS                   *
     C*   ROUT     - DETERMINE MESSAGE ROUTING                        *
     C*   FLD30    - FORMAT FIELD :30:                                *
     C*   FLD20    - FORMAT MANDATORY FIELD :20:                      *
     C*   FLD21    - FORMAT FIELD :21:                                *
     C*   FLD31P   - FORMAT FIELD :31P:                               *
     C*   FLD35B   - FORMAT MANDATORY FIELD :35B:                     *
     C*   FLD352   - FORMAT FIELD :35a:                               *
     C*   FLD33V   - FORMAT FIELD :33V:                               *
     C*   FLD822   - FORMAT FIELD :82a:                               *
     C*   FLD351   - FORMAT MANDATORY FIELD :35A:                     *
     C*   FLD832   - FORMAT FIELD :83a:                               *
     C*   FLD35E   - FORMAT FIELD :35E:                               *
     C*   FLD872   - FORMAT MANDATORY FIELD :87a:                     *
     C*   FLD882   - FORMAT FIELD :88a:                               *
     C*   FLD72    - FORMAT FIELD :72:                                *
     C*   #W01     - PRINT MESSAGE GENERATED DETAILS                  *
     C*   #W02     - PRINT MESSAGE NOT GENERATED DETAILS              *
     C*   OFLDS    - FORMAT DETAILS FOR MIDAS SWIFT DATABASE FILES    *
     C*   MG5900   - UPDATE SEQUENCE NUMBERS                          *
     C*   MG5910   - UPDATE GENERATION GENERATED INDICATOR            *
     C*   *PSSR    - ERROR HANDLING                                   *
     C*                                                               *
     C*****************************************************************
     C*
     C** INITIALISATION
     C                     EXSR INIT
     C/COPY WNCPYSRC,MG0522C012
     C*
     C** READ FIRST RECORD FROM PF/MGF522PD
     C                     READ MGF522PD                 10
     C/COPY WNCPYSRC,MG0522C013
     C*
     C** IF RECORDS PRESENT ON THE FILE
     C           *IN10     IFEQ '0'
     C*
     C** DO WHILE RECORDS ON THIS FILE
     C           *IN10     DOWEQ'0'
     C*                                                                   CSW003
     C           MGTTYP    IFEQ 'TS'                                      CSW003
     C           MGTSTP    IFEQ 'TP'                                      CSW003
     C                     MOVE 'TS'      MGTSTP                          CSW003
     C                     ELSE                                           CSW003
     C                     MOVE 'TP'      MGTSTP                          CSW003
     C                     END                                            CSW003
     C                     END                                            CSW003
     C*
     C** INITIALISE NON-CONSTANT FIELDS
     C                     EXSR INTF
     C*
     C** DETERMINE MESSAGE ROUTING
     C                     EXSR ROUT
     C*
     C** FORMAT FIELD :30:
     C                     MOVE ':30: '   WWGENF  5
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD30
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :20:
     C                     MOVE ':20: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD20
     C                     END
     C*
     C** FORMAT FIELD :21:
     C                     MOVE ':21: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD21
     C                     END
     C*
     C** FORMAT FIELD :31P:
     C                     MOVE ':31P:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD31P
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :35B:
     C                     MOVE ':35B:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD35B
     C                     END
     C*
     C** FORMAT FIELD :35a:
     C                     MOVE ':35D:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD352
     C                     END
     C*
     C** FORMAT FIELD :33V:
     C                     MOVE ':33V:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD33V
     C                     END
     C*
     C** FORMAT FIELD :82a:
     C                     MOVE ':82A:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           WXS82I    OREQ 'Y'
     C                     EXSR FLD822
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :35A:
     C                     MOVE ':35A:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD351
     C                     END
     C*
     C** FORMAT FIELD :83a:
     C                     MOVE ':83A:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           WXS83     OREQ 'Y'
     C                     EXSR FLD832
     C                     END
     C*
     C** FORMAT FIELD :35E:
     C                     MOVE ':35E:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           MGCRTN    OREQ 'Y'
     C                     EXSR FLD35E
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :87a:
     C                     MOVE ':87A:'   WWGENF
     C                     EXSR GENF
     C/COPY WNCPYSRC,MG0522C018
     C           *IN01     IFEQ '1'
     C           WXS87R    OREQ 'Y'
     C           WXS87D    OREQ 'Y'
     C                     EXSR FLD872
     C                     END
     C/COPY WNCPYSRC,MG0522C019
     C*
     C** FORMAT FIELD :88a:
     C                     MOVE ':88A:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           WXS88     OREQ 'Y'
     C                     EXSR FLD882
     C                     END
     C/COPY WNCPYSRC,MG0522C014
     C*
     C** FORMAT FIELD :72:
     C                     MOVE ':72: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           WXS72     OREQ 'Y'
     C           MGCRTN    OREQ 'Y'
     C                     EXSR FLD72
     C                     END
     C*
     C** IF ERROR FOUND WHILST FORMATTING THIS MESSAGE
     C           @ERR      IFEQ 'Y'
     C*
     C** PRINT MESSAGE NOT GENERATED DETAILS
     C                     EXSR #W02
     C                     ELSE
     C*
     C** FORMAT DETAILS FOR MIDAS SWIFT DATABASE FILES
     C                     EXSR OFLDS
     C*
     C** PRINT MESSAGE GENERATED DETAILS
     C                     EXSR #W01
     C                     MOVEL'Y'       WWPTR1  1
     C                     END
     C*
     C** DELETE THE RECORD ON PF/MGF522PD PROCESSED
     C                     DELETMGF522PD               11
     C/COPY WNCPYSRC,MG0522C015
     C           *IN11     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '001'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 001  *
     C                     MOVEL'MGF522PD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0522'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF NO ERRORS FOUND WHILST FORMATTING THIS MESSAGE
     C           @ERR      IFNE 'Y'
     C*
     C** UPDATE SEQUENCE NUMBERS ON TRANSACTION RECORD
     C** FORMAT @TRAN AS FOLLOWS
     C** CHARS 1-6 TO LEFTMOST 6 CHARS FROM MGTNUM
     C** CHARS 7-13 TO BLANKS
     C** CHARS 14-15 TO MGTTYP
     C                     MOVELMGTNUM    WWFL06  6
     C                     MOVELWWFL06    WWTRAN 15
     C                     MOVE MGTTYP    WWTRAN
     C                     CALL 'MG5900'               20
     C                     PARM MGMODI    @MODI   2
     C                     PARM WWTRAN    @TRAN  15
     C                     PARM 'PR'      @PROG   2
     C                     PARM 'U'       @MODE   1
     C                     PARM *ZEROS    @LSWS   70
     C                     PARM *BLANKS   @ERCD   1
     C                     PARM CSW003    @MT5XX  1                       CSW003
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN20     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '002'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 002  *
     C                     MOVEL'MG5900'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM
     C           @ERCD     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     MOVE '003'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 003  *
     C                     MOVELDBERR     DBKEY            * * * * * * * *
     C                     MOVEL'MG5900'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** UPDATE GENERATION GENERATED INDICATOR
     C*
     C** FORMAT @TREF AS FOLLOWS
     C** CHARS 7-13 TO BLANKS
     C** CHARS 14-15 TO MGTTYP
     C                     MOVE *BLANKS   WWTREF
     C                     MOVELMGTNUM    WWFL06  6
     C                     MOVELWWFL06    WWTREF
     C                     MOVE MGTTYP    WWTREF
     C*
     C** IF MGTTYP EQUALS 'DM' AND MGTSTP IS ONE OF THE FOLLOWING
     C           MGTTYP    IFEQ 'DM'
     C           CSW003    ANDNE'Y'                                       CSW003
     C*
     C           MGTSTP    IFEQ 'RR'
     C           MGTSTP    OREQ 'RP'
     C           MGTSTP    OREQ 'BB'
     C           MGTSTP    OREQ 'BL'
     C           MGTSTP    OREQ 'PL'
     C           MGTSTP    OREQ 'PD'
     C           MGTSTP    OREQ 'DT'                                      185241
     C*
     C** MOVE 'A' TO POSITION 13 OF PARAMETER
     C                     MOVEL'A'       WT1313
     C                     END
     C*
     C                     END
     C*
     C                     CALL 'MG5910'               26
     C                     PARM MGMODI    @MODI   2
     C                     PARM WWTREF    @TREF  15
     C                     PARM 'PR'      @PROG   2
     C                     PARM 'U'       @MODE   1
     C                     PARM *BLANKS   @CONG   1
     C                     PARM *BLANKS   @RCDG   1
     C                     PARM *BLANKS   @ERCD   1
     C                     PARM CSW003    @MT5XX                          CSW003
     C*                                                                   CSW003
     C                     MOVEL@MODI     WWMODE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN26     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '023'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 023  *
     C                     MOVEL'MG5910'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM
     C           @ERCD     IFEQ 'Y'
     C           MGTSTP    ANDNE'DT'                                      185241
     C           MGTSTP    ANDNE'RP'                                      185241
     C           MGTSTP    ANDNE'RR'                                      185241
     C           MGTSTP    ANDNE'BL'                                      185241
     C           MGTSTP    ANDNE'BB'                                      185241
     C           MGTSTP    ANDNE'PL'                                      185241
     C           MGTSTP    ANDNE'PD'                                      185241
     C           *LOCK     IN   LDA
     C                     MOVE '024'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 024  *
     C                     MOVELDBERR2    DBKEY            * * * * * * * *
     C                     MOVEL'MG5910'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C***IF @CONG EQUALS 'Y'
     C***        @CONG     IFEQ 'Y'
     C** IF @RCDG EQUALS 'Y'
     C           @RCDG     IFEQ 'Y'
     C           MGTSTP    ANDNE'DT'                                      185241
     C           MGTSTP    ANDNE'RP'                                      185241
     C           MGTSTP    ANDNE'RR'                                      185241
     C           MGTSTP    ANDNE'BL'                                      185241
     C           MGTSTP    ANDNE'BB'                                      185241
     C           MGTSTP    ANDNE'PL'                                      185241
     C           MGTSTP    ANDNE'PD'                                      185241
     C           *LOCK     IN   LDA
     C                     MOVE '025'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 025  *
     C                     MOVELDBERR2    DBKEY            * * * * * * * *
     C                     MOVEL'MG5910'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     END
     C*
     C** COMIT ALL CHANGES TO FILES
     C                     COMIT
     C*
     C** READ NEXT RECORD FROM PF/MGF522PD
     C                     READ MGF522PD                 10
     C/COPY WNCPYSRC,MG0522C016
     C*
     C                     END
     C*
     C** WRITE TRAILER ON SWIFT MESSAGES GENERATED REPORT
     C** ONLY IF MESSAGES GENERATED
     C           WWPTR1    IFNE 'Y'
     C                     WRITEHEADER1
     C                     WRITENODETS
     C*
     C                     ELSE
     C*
     C** CHECK FOR OVERFLOW
     C           WWOFL1    SUB  WWPTL1    WWLINE  20
     C*
     C           WWLINE    IFLT 4
     C                     WRITEHEADER1
     C                     END
     C                     WRITEEOREPT1
     C                     END
     C*
     C** WRITE TRAILER ON SWIFT MESSAGES NOT GENERATED REPORT
     C** IF THE PRINTER FILE HAS BEEN OPENED
     C           *IN50     IFEQ '1'
     C*
     C** CHECK FOR OVERFLOW
     C           WWOFL2    SUB  WWPTL2    WWLINE
     C*
     C           WWLINE    IFLT 4
     C                     WRITEHEADER2
     C                     END
     C                     WRITEEOREPT2
     C                     END
     C*
     C                     ELSE
     C*
     C** WRITE NO DETAILS TO REPORT
     C                     WRITEHEADER1
     C                     WRITENODETS
     C                     END
     C/COPY WNCPYSRC,MG0522C010
     C*
     C                     SETON                     LR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  INIT      - INITIALISATION                                  *
     C**                                                              *
     C**  CALLS     - *PSSR                                           *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESS                                    *
     C**                                                              *
     C*****************************************************************
     C*
     C           INIT      BEGSR
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C** SET UP LDA
     C           *NAMVAR   DEFN           LDA
     C*
     C** ACCESS SDBANKPD FOR BANK DETAILS
     C           1         SETLLSDBANKPD
     C                     READ SDBANKPD                 12
     C           *IN12     IFEQ '1'
     C           BJTYLC    OREQ 'D'
     C           *LOCK     IN   LDA
     C                     MOVE '004'     DBASE            * * * * * * * *
     C                     MOVEL*BLANKS   DBKEY            *  DBERR 004  *
     C                     MOVEL'SDBANKPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0522'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** ACCESS SDMGMEPD FOR MESSAGE GENERATION & MESSAGE MANAGEMENT
     C           1         SETLLSDMGMEPD
     C                     READ SDMGMEPD                 15
     C           *IN15     IFEQ '1'
     C           ENTYLC    OREQ 'D'
     C           *LOCK     IN   LDA
     C                     MOVE '018'     DBASE            * * * * * * * *
     C                     MOVEL*BLANKS   DBKEY            *  DBERR 018  *
     C                     MOVEL'SDMGMEPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0522'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** SET CRLF CONTROL CHARACTER
     C                     MOVE *LOVAL    CTRC
     C                     BITON'457'     WWCR
     C                     BITON'257'     WWLF
     C*
      ** Check if (CSW003) MT5XX - Phase 2 Message Generation is on.      CSW003
      *                                                                   CSW003
     C                     CALL 'AOSARDR0'                                CSW003
     C                     PARM '*BLANKS' WRTCD   7                       CSW003
     C                     PARM '*VERIFY' WOPTN   7                       CSW003
     C                     PARM 'CSW003'  @SARD   6                       CSW003
     C           SCSARD    PARM SCSARD    DSFDY                           CSW003
      *                                                                   CSW003
     C           WRTCD     IFEQ *BLANKS                                   CSW003
     C                     MOVE 'Y'       CSW003  1                       CSW003
     C                     ENDIF                                          CSW003
      *                                                                   CSW003
     C/COPY WNCPYSRC,MG0522C001
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  GENF      - GENERATE FIELDS CONDITION                       *
     C**                                                              *
     C**  CALLS     -                                                 *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESS                                    *
     C**                                                              *
     C*****************************************************************
     C*
     C           GENF      BEGSR
      *
      **  If message field tag appears on array SUPP, supress its
      **  production by setting off indicator 01.
     C           WWGENF    LOKUPSUPP                     55
     C           *IN55     IFEQ '0'
     C                     SETON                     01
     C/COPY WNCPYSRC,MG0522C002
     C                     ELSE
     C                     SETOF                     01
     C                     END
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  INTF      - INITIALISE NON-CONSTANT FIELDS                  *
     C**                                                              *
     C**  CALLS     - MG5900, MG5910, *PSSR                           *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESS                                    *
     C**                                                              *
     C*****************************************************************
     C*
     C           INTF      BEGSR
     C*
     C** OBTAIN SEQUENCE NUMBERS FROM TRANSACTION RECORD
     C** FORMAT @TRAN AS FOLLOWS
     C** CHARS 1-6 EQUAL TO LEFTMOST 6 CHARS FROM MGTNUM
     C** CHARS 7-13 EQUAL TO BLANKS
     C** CHARS 14-15 EQUAL TO MGTTYP
     C                     MOVE *BLANKS   WWTRAN
     C                     MOVE *BLANKS   WWFL06
     C                     MOVELMGTNUM    WWFL06
     C                     MOVELWWFL06    WWTRAN
     C                     MOVE MGTTYP    WWTRAN
     C*
     C                     CALL 'MG5900'               20
     C                     PARM MGMODI    @MODI
     C                     PARM WWTRAN    @TRAN
     C                     PARM 'PR'      @PROG
     C                     PARM *BLANKS   @MODE
     C                     PARM *ZEROS    @LSWS
     C                     PARM *BLANKS   @ERCD
     C                     PARM CSW003    @MT5XX                          CSW003
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN20     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '005'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 005  *
     C                     MOVEL'MG5900'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM
     C           @ERCD     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     MOVE '006'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 006  *
     C                     MOVELDBERR     DBKEY            * * * * * * * *
     C                     MOVEL'MG5900'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** UPDATE GENERATION GENERATED INDICATOR
     C*
     C** FORMAT @TREF AS FOLLOWS
     C** CHARS 7-13 TO BLANKS
     C** CHARS 14-15 TO MGTTYP
     C                     MOVE *BLANKS   WWTREF
     C                     MOVELMGTNUM    WWFL06  6
     C                     MOVELWWFL06    WWTREF
     C                     MOVE MGTTYP    WWTREF
     C*
     C** IF MGTTYP EQUALS 'DM' AND MGTSTP IS ONE OF THE FOLLOWING
     C           MGTTYP    IFEQ 'DM'
     C           CSW003    ANDNE'Y'                                       CSW003
     C*
     C           MGTSTP    IFEQ 'RR'
     C           MGTSTP    OREQ 'RP'
     C           MGTSTP    OREQ 'BB'
     C           MGTSTP    OREQ 'BL'
     C           MGTSTP    OREQ 'PL'
     C           MGTSTP    OREQ 'PD'
     C           MGTSTP    OREQ 'DT'                                      185241
     C*
     C** MOVE 'A' TO POSITION 13 OF PARAMETER
     C                     MOVEL'A'       WT1313
     C                     END
     C*
     C                     END
     C*
     C                     CALL 'MG5910'               26
     C                     PARM MGMODI    @MODI
     C                     PARM WWTREF    @TREF
     C                     PARM 'PR'      @PROG
     C                     PARM *BLANKS   @MODE
     C                     PARM *BLANKS   @CONG
     C                     PARM *BLANKS   @RCDG
     C                     PARM *BLANKS   @ERCD
     C                     PARM CSW003    @MT5XX                          CSW003
     C*                                                                   CSW003
     C                     MOVEL@MODI     WWMODE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN26     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '026'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 026  *
     C                     MOVEL'MG5910'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM
     C           @ERCD     IFEQ 'Y'
     C           MGTSTP    ANDNE'DT'                                      185241
     C           MGTSTP    ANDNE'RP'                                      185241
     C           MGTSTP    ANDNE'RR'                                      185241
     C           MGTSTP    ANDNE'BL'                                      185241
     C           MGTSTP    ANDNE'BB'                                      185241
     C           MGTSTP    ANDNE'PL'                                      185241
     C           MGTSTP    ANDNE'PD'                                      185241
     C           *LOCK     IN   LDA
     C                     MOVE '027'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 027  *
     C                     MOVELDBERR2    DBKEY            * * * * * * * *
     C                     MOVEL'MG5910'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C***IF @CONG EQUALS 'Y'
     C***        @CONG     IFEQ 'Y'
     C** IF @RCDG EQUALS 'Y'
     C           @RCDG     IFEQ 'Y'
     C           MGTSTP    ANDNE'DT'                                      185241
     C           MGTSTP    ANDNE'RP'                                      185241
     C           MGTSTP    ANDNE'RR'                                      185241
     C           MGTSTP    ANDNE'BL'                                      185241
     C           MGTSTP    ANDNE'BB'                                      185241
     C           MGTSTP    ANDNE'PL'                                      185241
     C           MGTSTP    ANDNE'PD'                                      185241
     C           *LOCK     IN   LDA
     C                     MOVE '028'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 028  *
     C                     MOVELDBERR2    DBKEY            * * * * * * * *
     C                     MOVEL'MG5910'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** CLEAR MULTIPLE OCCURENCE DATA STRUCTURE
     C                     Z-ADD1         X       20
     C           X         OCUR MULT
     C*
     C           X         DOWLE80
     C           MULT      ANDNE*BLANKS
     C                     MOVE *BLANKS   MULT
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
      *                                                                   183583
      ** Clear multiple occurrence DS for the extension file.             183583
      *                                                                   183583
     C                     Z-ADD1         V       20                      183583
     C           V         DOWLE30                                        183583
     C           MCSI      ANDNE*BLANKS                                   183583
     C           V         OCUR EXTN                                      183583
     C                     Z-ADD*ZEROS    MPSN                            183583
     C                     MOVE *BLANKS   MTG                             183583
     C                     MOVE *BLANKS   MCSI                            183583
     C                     ADD  1         V                               183583
     C                     ENDDO                                          183583
     C                     Z-ADD0         V                               183583
     C*
     C** SET INDEX TO FIRST OCCURENCE
     C                     Z-ADD1         X
     C           X         OCUR MULT
     C*
     C** SET FLAG FOR MESSAGE CORRECTLY GENERATED
     C                     MOVE *BLANKS   @ERR    1
     C                     MOVE *BLANKS   NWERR  50
     C                     MOVE *BLANKS   DSERR
     C*
     C** SET EXTENDED SETTLEMENT FIELDS PRESENCE
     C                     MOVE MGXSFL    WXSFLD
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  ROUT      - DETERMINE MESSAGE ROUTING                       *
     C**                                                              *
     C**  CALLS     - *PSSR, ZM1100                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESS                                    *
     C**                                                              *
     C*****************************************************************
     C*
     C           ROUT      BEGSR
     C*
     C** ACCESS SDBRCHL1 USING KEY OF MGSNBR FROM PF/MGF522PD
     C           MGSNBR    CHAINSDBRCHL1             13
     C           *IN13     IFEQ '1'
     C           A8BRCD    OREQ 'D'
     C           *LOCK     IN   LDA
     C                     MOVE '007'     DBASE            * * * * * * * *
     C                     MOVELMGSNBR    DBKEY            *  DBERR 007  *
     C                     MOVEL'SDBRCHL1'DBFILE           * * * * * * * *
     C                     MOVEL'MG0522'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** DETERMINE NETWORK ROUTING AND ADDRESSES
     C                     MOVE A8BICN    @SNDR   6
     C                     MOVE A8BTID    @SCSD  12
     C                     MOVE MGDEST    @DEST   6
     C                     MOVELMGSETP    @SETP   2
     C                     MOVE *BLANKS   @NWSN  20
     C                     MOVE *BLANKS   @NWDS  22
     C                     MOVE *BLANKS   @NWRK   6
     C                     MOVE *BLANKS   @ERRC   1
     C                     MOVE *BLANKS   @SRNM  20
     C                     MOVE *BLANKS   @SRTN  10
     C                     MOVE *BLANKS   @DRNM  20
     C                     MOVE *BLANKS   @DRTN  10
     C*
     C                     CALL 'ZM1100'               21
     C                     PARM           @SNDR
     C                     PARM           @SCSD
     C                     PARM           @DEST
     C                     PARM           @SETP
     C                     PARM           @NWSN
     C                     PARM           @NWDS
     C                     PARM           @NWRK
     C                     PARM           @ERRC
     C                     PARM           @SRNM
     C                     PARM           @SRTN
     C                     PARM           @DRNM
     C                     PARM           @DRTN
     C*
     C** IF PROGRAM ZM1100 NOT FOUND
     C           *IN21     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '008'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 008  *
     C                     MOVEL'ZM1100'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING CALLED PROGRAM
     C           @ERRC     IFEQ 'S'
     C           *LOCK     IN   LDA
     C                     MOVE '009'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 009  *
     C                     MOVELDBERR1    DBKEY            * * * * * * * *
     C                     MOVEL'ZM1100'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DESTINATION ERROR MESSAGE
     C           @ERRC     IFEQ 'D'
     C                     MOVE *BLANKS   DSERR  50
     C                     MOVELERRA,1    DSERR
     C                     MOVE 'Y'       @ERR
     C                     END
     C*
     C** FORMAT NETWORK ERROR MESSAGE
     C           @NWRK     IFEQ 'UNROUT'
     C                     MOVELERRA,11   NWERR
     C                     MOVE 'Y'       @ERR
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD30     - FORMAT FIELD :30:                               *
     C**                                                              *
     C**  CALLS     - ZM0060, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD30     BEGSR
     C*
     C** IF MGSDAT FROM PF/MGF522PD IS NOT ZERO
     C           MGSDAT    IFNE 0
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,1    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C***********          MOVEL':30:'    MTAG                            057263
     C                     MOVEL':30: '   MTAG                            057263
     C*
     C** CONVERT SETTLEMENT DATE TO SWIFT FORMAT
     C                     MOVE MGSDAT    ZMDAY   50
     C                     MOVE *BLANKS   ZSDATE  6
     C*
     C                     CALL 'ZM0060'               22
     C                     PARM           ZMDAY
     C                     PARM           ZSDATE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN22     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '010'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 010  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVELZSDATE    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVELWWYEAR    WRYEAR
     C                     MOVELWWMNTH    WRMNTH
     C                     MOVELWWDAY     WRDAY
     C                     MOVELWRDATE    RDAT
     C*
     C** STORE ZSDATE FOR SWIFT MESSAGE FILES
     C                     MOVELZSDATE    WWSDAT  6
     C                     MOVELWRDATE    W8DATE  8
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD20     - FORMAT MANDATORY FIELD :20:                     *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD20     BEGSR
     C*
     C** IF REQUIRED FIELDS ON MGF522PD NOT PRESENT THEN ERROR
     C                     MOVELMGTNUM    WWFL06
     C           MGMODI    IFNE 'SE'
     C           MGTTYP    ORNE 'TR'
     C           MGTTYP    ANDNE'TS'                                      CSW003
     C           MGTTYP    ANDNE'DM'
     C           MGTTYP    ANDNE'DN'                                      CSW003
     C           WWFL06    OREQ *BLANKS
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,2    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,2    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C***********          MOVEL':20:'    MTAG                            057263
     C                     MOVEL':20: '   MTAG                            057263
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C           MGMODI    IFEQ 'SE'
     C           MGTTYP    ANDEQ'TR'
     C                     MOVEL'SET'     WD0103
     C*
     C                     ELSE
     C*
     C           MGMODI    IFEQ 'SE'
     C           MGTTYP    ANDEQ'DM'
     C                     MOVEL'SED'     WD0103
     C                     ELSE                                           CSW003
     C           MGMODI    IFEQ 'SE'                                      CSW003
     C           MGTTYP    ANDEQ'DN'                                      CSW003
     C                     MOVEL'SEE'     WD0103                          CSW003
     C                     ELSE                                           CSW003
     C           MGMODI    IFEQ 'SE'                                      CSW003
     C           MGTTYP    ANDEQ'TS'                                      CSW003
     C                     MOVEL'SEU'     WD0103                          CSW003
     C                     ENDIF                                          CSW003
     C                     ENDIF                                          CSW003
     C                     END
     C*
     C                     END
     C*
     C/COPY WNCPYSRC,MG0522C003
     C                     MOVELMGTNUM    WD0409
     C*
     C           @LSWS     IFEQ 9999999
     C                     Z-ADD1         WWLSWS  70
     C*
     C                     ELSE
     C*
     C           @LSWS     ADD  1         WWLSWS
     C                     END
     C                     MOVELWWLSWS    WD1016
     C                     MOVELWWMDAT    WWDATA 16
     C/COPY WNCPYSRC,MG0522C005
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD21     - FORMAT FIELD :21:                               *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD21     BEGSR
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,3    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C***********          MOVEL':21:'    MTAG                            057263
     C                     MOVEL':21: '   MTAG                            057263
     C                     MOVE *BLANKS   WWMDAT
     C*
     C** IF @LSWS EQUALS ZERO
     C           @LSWS     IFEQ 0
     C                     MOVEL'NEW'     WD0103
     C*
     C                     ELSE
     C*
     C           MGMODI    IFEQ 'SE'
     C           MGTTYP    ANDEQ'TR'
     C                     MOVEL'SET'     WD0103
     C*
     C                     ELSE
     C*
     C           MGMODI    IFEQ 'SE'
     C           MGTTYP    ANDEQ'DM'
     C                     MOVEL'SED'     WD0103
     C                     ELSE                                           CSW003
     C           MGMODI    IFEQ 'SE'                                      CSW003
     C           MGTTYP    ANDEQ'DN'                                      CSW003
     C                     MOVEL'SEE'     WD0103                          CSW003
     C                     ELSE                                           CSW003
     C           MGMODI    IFEQ 'SE'                                      CSW003
     C           MGTTYP    ANDEQ'TS'                                      CSW003
     C                     MOVEL'SEU'     WD0103                          CSW003
     C                     ENDIF                                          CSW003
     C                     ENDIF                                          CSW003
     C                     END
     C*
     C                     END
     C*
     C                     MOVELMGTNUM    WD0409
     C                     MOVEL@LSWS     WD1016
     C*
     C                     END
     C*
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD31P    - FORMAT FIELD :31P:                              *
     C**                                                              *
     C**  CALLS     - ZM0060, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD31P    BEGSR
     C*
     C** IF MGTDAT IS NOT ZERO
     C           MGTDAT    IFNE 0
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,4    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':31P:'   MTAG
     C*
     C** CONVERT TRADE DATE TO SWIFT FORMAT
     C                     MOVE MGTDAT    ZMDAY
     C                     MOVE *BLANKS   ZSDATE
     C*
     C                     CALL 'ZM0060'               22
     C                     PARM           ZMDAY
     C                     PARM           ZSDATE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN22     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '011'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 011  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVE ZSDATE    WD0106
     C                     MOVE MGMKTN    WD0726
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVELWWYEAR    WRYEAR
     C                     MOVELWWMNTH    WRMNTH
     C                     MOVELWWDAY     WRDAY
     C                     MOVELWRDATE    WR0108
     C                     MOVELMGMKTN    WR0928
     C                     MOVELWWRDAT    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD351    - FORMAT MANDATORY FIELD :35A:                    *
     C**                                                              *
     C**  CALLS     - ZM1140, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD351    BEGSR
     C*
     C** IF REQUIRED FIELDS ON MGF522PD NOT PRESENT THEN ERROR
     C           MGQANT    IFEQ 0
     C           MGSWTP    OREQ *BLANKS
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,3    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,5    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':35A:'   MTAG
     C*
     C** CONVERT MIDAS AMOUNT TO SWIFT FORMAT
     C                     Z-ADDMGQANT    ZAMNT  150
     C***********          Z-ADD0         ZCDP    10                      078256
     C                     Z-ADDMGNMDP    ZCDP    10                      078256
     C                     MOVE *BLANKS   ZSAMNT 17
     C                     MOVE *BLANKS   ZOAMNT 21
     C*
     C                     CALL 'ZM1140'               23
     C                     PARM           ZAMNT
     C                     PARM           ZCDP
     C                     PARM           ZSAMNT
     C                     PARM           ZOAMNT
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN23     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '012'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 012  *
     C                     MOVEL'ZM1140'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVE MGSWTP    WD0103
     C/COPY WNCPYSRC,MG0522C004
     C                     MOVELZSAMNT    WD0419
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVELMGSWTP    WR0103
     C/COPY WNCPYSRC,MG0522C006
     C                     MOVELZOAMNT    WR0424
     C                     MOVELWWRDAT    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD35B    - FORMAT MANDATORY FIELD :35B:                    *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD35B    BEGSR
     C*
     C** IF REQUIRED FIELD ON MGF522PD NOT PRESENT THEN ERROR
     C           MGSFN1    IFEQ *BLANKS
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,4    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,6    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':35B:'   MTAG
     C*
     C** IF MGISIN IS NOT BLANK
     C           MGISIN    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C*
     C** CHECK ISIN OR COMMON CODE
     C                     MOVE MGISIN    WWISIN  1
     C           WWISIN    IFNE *BLANK
     C*                                                                   085245
     C           WWISIN    IFNE '%'                                       085245
     C           WWISIN    ANDNE'\'                                       085245
     C                     MOVEL'ISIN '   WD0105           ISIN code
     C                     MOVELMGISIN    WD0617
     C                     ELSE                                           085245
     C*                                                                   085245
     C*** If it is a CUSIP number for Bill then output 'CUSIP '           085245
     C*** Else if it is a CUSIP number for note then donot output the     085245
     C*** word.                                                           085245
     C                     MOVE ' '       MGISIN                          085245
     C           WWISIN    IFEQ '%'                                       085245
     C                     MOVEL'CUSIP'   WD0106                          085245
     C                     MOVELMGISIN    WD0726                          085245
     C                     ELSE                                           085245
     C                     MOVELMGISIN    WWMDAT                          085245
     C                     END                                            085245
     C                     END                                            085245
     C*                                                                   085245
     C                     ELSE
     C                     MOVEL'/XS/'    WD0104           COMMON code
     C                     MOVELMGISIN    WD0516
     C                     END
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C                     END
     C*
     C** REMOVE FORBIDDEN CHARACTERS (E.G. %)
     C                     MOVEAMGSFN1    FORB
     C                     Z-ADD1         J       20
     C           *IN85     DOUEQ'0'                                       056532
     C           '%'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN1
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '&'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN1
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '/'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN1
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '$'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN1
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J                               056532
     C           FORB,J    DOWEQ'-'                                       056532
     C           FORB,J    OREQ ' '                                       056532
     C           FORB,J    OREQ ':'                                       056532
     C                     ADD  1         J                               056532
     C                     MOVE *BLANKS   MGSFN1                          056532
     C                     MOVEAFORB,J    MGSFN1                          056532
     C           J         IFEQ 50                                        056532
     C           FORB,50   IFEQ '-'                                       056532
     C           FORB,50   OREQ ':'                                       056532
     C                     MOVE *BLANKS   MGSFN1                          056532
     C                     ENDIF                                          056532
     C                     LEAVE                                          056532
     C                     ENDIF                                          056532
     C                     ENDDO                                          056532
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGSFN1    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** IF MGSFN2 IS NOT BLANK
     C           MGSFN2    IFNE *BLANKS
     C*
     C** REMOVE FORBIDDEN CHARACTERS (E.G. %)
     C                     MOVEAMGSFN2    FORB
     C                     Z-ADD1         J       20
     C           *IN85     DOUEQ'0'                                       056532
     C           '%'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN2
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '&'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN2
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '/'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN2
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '$'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN2
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J                               056532
     C           FORB,J    DOWEQ'-'                                       056532
     C           FORB,J    OREQ ' '                                       056532
     C           FORB,J    OREQ ':'                                       056532
     C                     ADD  1         J                               056532
     C                     MOVE *BLANKS   MGSFN2                          056532
     C                     MOVEAFORB,J    MGSFN2                          056532
     C           J         IFEQ 50                                        056532
     C           FORB,50   IFEQ '-'                                       056532
     C           FORB,50   OREQ ':'                                       056532
     C                     MOVE *BLANKS   MGSFN2                          056532
     C                     ENDIF                                          056532
     C                     LEAVE                                          056532
     C                     ENDIF                                          056532
     C                     ENDDO                                          056532
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGSFN2    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD352    - FORMAT FIELD :35a:                              *
     C**                                                              *
     C**  CALLS     - ZM0060, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD352    BEGSR
     C*
     C** IF MGNCDT IS NOT ZERO
     C           MGNCDT    IFNE 0
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,7    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':35D:'   MTAG
     C*
     C** CONVERT NEXT COUPON DATE TO SWIFT FORMAT
     C                     MOVE MGNCDT    ZMDAY
     C                     MOVE *BLANKS   ZSDATE
     C*
     C                     CALL 'ZM0060'               22
     C                     PARM           ZMDAY
     C                     PARM           ZSDATE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN22     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '013'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 013  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVELZSDATE    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVELWWYEAR    WRYEAR
     C                     MOVELWWMNTH    WRMNTH
     C                     MOVELWWDAY     WRDAY
     C                     MOVELWRDATE    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD35E    - FORMAT FIELD :35E:                              *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD35E    BEGSR
     C*
     C** IF MGCRTN EQUALS 'Y'
     C           MGCRTN    IFEQ 'Y'
     C*
     C** ACCESS SECRTNPD USING KEY OF LEFTMOST 6 CHARS FROM MGF510PD
     C                     MOVELMGTNUM    WWKEY1  6
     C*
     C           *IN51     IFEQ '0'
     C                     OPEN SECRTNL1
     C                     MOVE '1'       *IN51
     C                     END
     C*
     C           WWKEY1    CHAINSECRTNL1             14
     C           *IN14     IFEQ '0'
     C*
     C** WRITE A RECORD TO MGF579PD
     C                     MOVELWWDATA    MGRREF
     C*
     C           *IN52     IFEQ '0'
     C                     OPEN MGF579PD
     C                     MOVE '1'       *IN52
     C                     END
     C*
     C                     WRITEMGF579D0
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,8    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR FILE
     C                     MOVE *BLANKS   MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   RDAT
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,5    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD33V    - FORMAT FIELD :33V:                              *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD33V    BEGSR
     C*
     C** FOR POSSIBLE FUTURE USE ONLY
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD822    - FORMAT FIELD :82a:                              *
     C**                                                              *
     C**  CALLS     - ZM0050, *PSSR, #ZA                              *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD822    BEGSR
     C*
     C** IF MGIPYN IS NOT BLANK
     C           MGIPYN    IFNE *BLANKS
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,9    MDES
     C*
     C** FORMAT ADDRESS FIELDS
     C                     MOVELMGIPYN    ZDEST  12
     C                     MOVE *BLANKS   ZSADD1 35
     C                     MOVE *BLANKS   ZSADD2 35
     C                     MOVE *BLANKS   ZSADD3 35
     C                     MOVE *BLANKS   ZSADD4 35
     C                     MOVE *BLANKS   ZSTAG   1
     C                     MOVE *BLANKS   ZSTYPE  1
     C                     MOVE *BLANKS   ZERR    1
     C                     MOVE *BLANKS   ZCRNM  20
     C                     MOVE *BLANKS   ZCRTN  10
     C*
     C***                  CALL 'ZM0050'               24
     C                     CALL 'ZM1150'               24
     C                     PARM           ZDEST
     C                     PARM           ZSADD1
     C                     PARM           ZSADD2
     C                     PARM           ZSADD3
     C                     PARM           ZSADD4
     C                     PARM           ZSTAG
     C                     PARM           ZSTYPE
     C                     PARM           ZERR
     C                     PARM           ZCRNM
     C                     PARM           ZCRTN
     C                     PARM           ZDBEI   1                                           CSW201
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN24     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '014'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 014  *
     C***                  MOVEL'ZM0050'  DBPGM            * * * * * *
     C                     MOVEL'ZM1150'  DBPGM            * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING CALLED PROGRAM
     C           ZERR      IFEQ '1'
     C                     MOVELERRA,6    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** IF ZSTAG IS 'A'
     C                     MOVE *BLANKS   SAVMTG  5                       183583
     C           ZSTAG     IFEQ 'A'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':82A:'   MTAG
     C                     MOVE MTAG      SAVMTG                          183583
     C                     END
     C*
     C** IF ZSTAG IS 'D'
     C           ZSTAG     IFEQ 'D'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':82D:'   MTAG
     C                     END
     C*
     C** IF MGIPYL IS NOT BLANK
     C           MGIPYL    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGIPYL    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     EXSR #ZA
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD832    - FORMAT FIELD :83a:                              *
     C**                                                              *
     C**  CALLS     - ZM1070, *PSSR, #ZA                              *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD832    BEGSR
     C*
     C** IF MGSKAN OR MGSKAL ARE NOT BLANK
     C           MGSKAN    IFNE *BLANKS
     C           MGSKAL    ORNE *BLANKS
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,10   MDES
     C*
     C** FORMAT ADDRESS FIELDS
     C                     MOVELMGSKAN    ZDEST
     C                     MOVELMGSKAL    ZACLN  35
     C                     MOVE *BLANKS   ZSADD1
     C                     MOVE *BLANKS   ZSADD2
     C                     MOVE *BLANKS   ZSADD3
     C                     MOVE *BLANKS   ZSADD4
     C                     MOVE *BLANKS   ZSTAG
     C                     MOVE *BLANKS   ZSTYPE
     C                     MOVE *BLANKS   ZERR
     C                     MOVE *BLANKS   ZCRNM
     C                     MOVE *BLANKS   ZCRTN
     C*
     C                     CALL 'ZM1070'               25
     C                     PARM           ZDEST
     C                     PARM           ZACLN
     C                     PARM           ZSADD1
     C                     PARM           ZSADD2
     C                     PARM           ZSADD3
     C                     PARM           ZSADD4
     C                     PARM           ZSTAG
     C                     PARM           ZSTYPE
     C                     PARM           ZERR
     C                     PARM           ZCRNM
     C                     PARM           ZCRTN
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN25     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '015'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 015  *
     C                     MOVEL'ZM1070'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING CALLED PROGRAM
     C           ZERR      IFEQ '1'
     C                     MOVELERRA,7    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** IF ZSTAG IS 'A'
     C                     MOVE *BLANKS   SAVMTG                          183583
     C           ZSTAG     IFEQ 'A'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':83A:'   MTAG
     C                     MOVE MTAG      SAVMTG                          183583
     C                     END
     C*
     C** IF ZSTAG IS 'C'
     C           ZSTAG     IFEQ 'C'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':83C:'   MTAG
     C                     END
     C*
     C** IF ZSTAG IS 'D'
     C           ZSTAG     IFEQ 'D'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':83D:'   MTAG
     C                     END
     C*
     C** IF MGSKAL IS NOT BLANK
     C           MGSKAL    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGSKAL    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     EXSR #ZA
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD872    - FORMAT MANDATORY FIELD :87a:                    *
     C**                                                              *
     C**  CALLS     - ZM0050, *PSSR, #ZA                              *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD872    BEGSR
     C*
     C** IF REQUIRED FIELDS ON MGF522PD NOT PRESENT THEN ERROR
     C           MGRSSN    IFEQ *BLANKS
     C           MGRSS1    ANDEQ*BLANKS
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,8    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,11   MDES
     C*
     C** FORMAT ADDRESS FIELDS
     C                     MOVELMGRSSN    ZDEST
     C                     MOVE *BLANKS   ZSADD1
     C                     MOVE *BLANKS   ZSADD2
     C                     MOVE *BLANKS   ZSADD3
     C                     MOVE *BLANKS   ZSADD4
     C                     MOVE *BLANKS   ZSTAG
     C                     MOVE *BLANKS   ZSTYPE
     C                     MOVE *BLANKS   ZERR
     C                     MOVE *BLANKS   ZCRNM
     C                     MOVE *BLANKS   ZCRTN
     C*
     C***                  CALL 'ZM0050'               24
     C                     CALL 'ZM1150'               24
     C                     PARM           ZDEST
     C                     PARM           ZSADD1
     C                     PARM           ZSADD2
     C                     PARM           ZSADD3
     C                     PARM           ZSADD4
     C                     PARM           ZSTAG
     C                     PARM           ZSTYPE
     C                     PARM           ZERR
     C                     PARM           ZCRNM
     C                     PARM           ZCRTN
     C                     PARM           ZDBEI                                               CSW201
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN24     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '016'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 016  *
     C***                  MOVEL'ZM0050'  DBPGM            * * * * * *
     C                     MOVEL'ZM1150'  DBPGM            * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING CALLED PROGRAM
     C           ZERR      IFEQ '1'
     C                     MOVELERRA,9    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C/COPY WNCPYSRC,MG0522C007
     C*
     C** IF ZSTAG IS 'A'
     C                     MOVE *BLANKS   SAVMTG                          183583
     C           ZSTAG     IFEQ 'A'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':87A:'   MTAG
     C                     MOVE MTAG      SAVMTG                          183583
     C                     END
     C*
     C** IF ZSTAG IS 'D'
     C           ZSTAG     IFEQ 'D'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':87D:'   MTAG
     C                     END
     C*
     C** IF MGRSS1 IS NOT BLANKS
     C           MGRSS1    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGRSS1    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C                     END
      *                                                                                       192221
      ** Format Data Field of multiple occurence data structure                               192221
      ** (Receiver of Security lines 2 to 4)                                                  192221
      *                                                                                       192221
     C           MGRSS2    IFNE *BLANKS                                                       192221
     C                     MOVELMGRSS2    MDAT                                                192221
     C                     MOVELMDAT      RDAT                                                192221
     C                     ADD  1         X                                                   192221
     C           X         OCUR MULT                                                          192221
     C                     ENDIF                                                              192221
      *                                                                                       192221
     C           MGRSS3    IFNE *BLANKS                                                       192221
     C                     MOVELMGRSS3    MDAT                                                192221
     C                     MOVELMDAT      RDAT                                                192221
     C                     ADD  1         X                                                   192221
     C           X         OCUR MULT                                                          192221
     C                     ENDIF                                                              192221
      *                                                                                       192221
     C           MGRSS4    IFNE *BLANKS                                                       192221
     C                     MOVELMGRSS4    MDAT                                                192221
     C                     MOVELMDAT      RDAT                                                192221
     C                     ADD  1         X                                                   192221
     C           X         OCUR MULT                                                          192221
     C                     ENDIF                                                              192221
      *                                                                                       192221
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     EXSR #ZA
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD882    - FORMAT FIELD :88a:                              *
     C**                                                              *
     C**  CALLS     - ZM1070, *PSSR, #ZA                              *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD882    BEGSR
     C*
     C** IF MGBSSN OR MGBSS1 ARE NOT BLANK
     C           MGBSSN    IFNE *BLANKS
     C           MGBSS1    ORNE *BLANKS
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,12   MDES
     C*
     C** FORMAT ADDRESS FIELDS
     C                     MOVELMGBSSN    ZDEST
     C                     MOVELMGBSS1    ZACLN
     C                     MOVE *BLANKS   ZSADD1
     C                     MOVE *BLANKS   ZSADD2
     C                     MOVE *BLANKS   ZSADD3
     C                     MOVE *BLANKS   ZSADD4
     C                     MOVE *BLANKS   ZSTAG
     C                     MOVE *BLANKS   ZSTYPE
     C                     MOVE *BLANKS   ZERR
     C*
     C                     CALL 'ZM1070'               25
     C                     PARM           ZDEST
     C                     PARM           ZACLN
     C                     PARM           ZSADD1
     C                     PARM           ZSADD2
     C                     PARM           ZSADD3
     C                     PARM           ZSADD4
     C                     PARM           ZSTAG
     C                     PARM           ZSTYPE
     C                     PARM           ZERR
     C                     PARM           ZCRNM
     C                     PARM           ZCRTN
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN25     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '017'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 017  *
     C                     MOVEL'ZM1070'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING CALLED PROGRAM
     C           ZERR      IFEQ '1'
     C                     MOVELERRA,10   MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C/COPY WNCPYSRC,MG0522C008
     C*
     C** IF ZSTAG IS 'A'
     C                     MOVE *BLANKS   SAVMTG                          183583
     C           ZSTAG     IFEQ 'A'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':88A:'   MTAG
     C                     MOVE MTAG      SAVMTG                          183583
     C                     END
     C*
     C** IF ZSTAG IS 'C'
     C           ZSTAG     IFEQ 'C'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':88C:'   MTAG
     C                     END
     C*
     C** IF ZSTAG IS 'D'
     C           ZSTAG     IFEQ 'D'
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':88D:'   MTAG
     C                     END
     C*
     C** IF MGBSS1 IS NOT BLANK
     C           MGBSS1    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGBSS1    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     EXSR #ZA
     C                     END
     C*
     C                     END
     C                     ENDSR
     C/COPY WNCPYSRC,MG0522C017
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD72     - FORMAT FIELD :72:                               *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD72     BEGSR
     C*
     C           MGCRTN    IFEQ 'Y'
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,13   MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C***********          MOVEL':72:'    MTAG                            057263
     C                     MOVEL':72: '   MTAG                            057263
     C*
     C** FORMAT DATA FIELD
     C                     MOVEL'/MSG579/'MDAT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C** IF MGSRL1 IS NOT BLANK
     C           MGSRL1    IFNE *BLANKS
     C*
     C* IF MSG579 NOT OUTPUT IN FIELD 72
     C           MGCRTN    IFNE 'Y'
     C*
     C**    FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,13   MDES
     C*
     C**    FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C***********          MOVEL':72:'    MTAG                            057263
     C                     MOVEL':72: '   MTAG                            057263
     C*
     C* IF MSG579 OUTPUT IN FIELD 72
     C                     ELSE
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGSRL1    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** IF MGSRL2 IS NOT BLANK
     C           MGSRL2    IFNE *BLANKS
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGSRL2    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C** IF MGSRL3 IS NOT BLANK
     C           MGSRL3    IFNE *BLANKS
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGSRL3    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  OFLDS     - FORMAT FIELDS FOR MIDAS SWIFT DATABASE FILES    *
     C**                                                              *
     C**  CALLS     - ZM0060, *PSSR, #W05                             *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           OFLDS     BEGSR
     C*
     C** INITIALISE FIELDS ON PF/MGOREFPD
     C                     CLEARMGOREFD0
     C*
     C** HISTORY RETENTION DATE
     C           MGSDAT    ADD  ENDSMN    WWDATE  50
     C                     MOVE WWDATE    ZMDAY   50
     C                     MOVE *BLANKS   ZSDATE  6
     C*
     C                     CALL 'ZM0060'               22
     C                     PARM           ZMDAY
     C                     PARM           ZSDATE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN22     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '019'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 019  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** SET HISTORY RETENTION DATE TO OUTPUT FIELD
     C                     MOVELZSDATE    HRDT
     C*
     C** MESSAGE GENERATION DATE
     C                     MOVE BJRDNB    ZMDAY   50
     C                     MOVE *BLANKS   ZSDATE  6
     C*
     C                     CALL 'ZM0060'               22
     C                     PARM           ZMDAY
     C                     PARM           ZSDATE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN22     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '020'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 020  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** SET MESSAGE GEN. DATE TO OUTPUT FIELD
     C                     MOVELZSDATE    MGDE
      *                                                                   113352
      ** Set-up Message Generation  Century flag                          113352
      *                                                                   113352
     C                     MOVELMGDE      YY      20                      113352
     C           YY        IFLT 72                                        113352
     C                     MOVE '2'       CIND                            113352
     C                     ELSE                                           113352
     C                     MOVE '1'       CIND                            113352
     C                     ENDIF                                          113352
     C*
     C** FORMAT FIELDS ON PF/MGOREFPD
     C                     EXSR #W05
     C/COPY WNCPYSRC,MG0522C011
     C*
     C** WRITE RECORD TO PF/MGOREFPD
     C                     MOVELMGCHG     AORR                                                CSW025
     C                     WRITEMGOREFD0               16
     C*
     C** IF ERROR ON WRITE OF RECORD
     C           *IN16     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '021'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 021  *
     C                     MOVEL'MGOREFPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0522'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** PROCESS MULTIPLE OCCURRENCE DATA STRUCTURE
     C                     Z-ADD1         Y       20
     C           Y         DOWLTX
     C           Y         OCUR MULT
     C*
     C** FORMAT FIELDS ON PF/MGOMSGPD
     C                     MOVELMDAT      MFLD
     C                     MOVELWWDATA    TRNO
     C                     MOVE *BLANKS   PTDL
     C*
     C** WRITE RECORD TO PF/MGOMSGPD
     C                     WRITEMGOMSGD0               17
     C*
     C** IF ERROR ON WRITE OF RECORD
     C           *IN17     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '022'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 022  *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0522'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** INCREMENT INDEX
     C                     ADD  1         Y
     C                     END
      *                                                                   183583
      ** Write multiple occurrence data structure to MGOEXTPD             183583
      *                                                                   183583
     C                     Z-ADD1         VV      20                      183583
     C           VV        DOWLEV                                         183583
     C           MCSI      ANDNE*BLANKS                                   183583
     C           VV        OCUR EXTN                                      183583
     C                     WRITEMGOEXTD0               06                 183583
      *                                                                   183583
      ** Error on write to PF/MGOEXTPD (message data extension file)      183583
      *                                                                   183583
     C           *IN06     IFEQ '1'                                       183583
     C           *LOCK     IN   LDA                                       183583
     C                     MOVE '049'     DBASE            ************** 183583
     C                     MOVEL'        'DBKEY            * DBERROR 49 * 183583
     C                     MOVEL'MGOEXTPD'DBFILE           ************** 183583
     C                     OUT  LDA                                       183583
     C                     EXSR *PSSR                                     183583
     C                     ENDIF                                          183583
     C                     ADD  1         VV                              183583
     C                     ENDDO                                          183583
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  #ZA       - FORMAT ADDRESSES                                *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - FLD822, FLD832, FLD872, FLD882, FLD532, FLD572, *
     C**              FLD582                                          *
     C**                                                              *
     C*****************************************************************
     C*
     C           #ZA       BEGSR
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C           ZSADD1    IFNE *BLANKS                                   122283
     C                     MOVELZSADD1    MDAT
     C                     EXSR SEXTN                                     183583
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** PRINT CUSTOMER REPORT NAME AND REPORT TOWN
     C           MTAG      IFEQ ':82A:'
     C           MTAG      OREQ ':83A:'
     C           MTAG      OREQ ':87A:'
     C           MTAG      OREQ ':88A:'
     C                     MOVELZCRNM     CRNM
     C                     MOVELZCRTN     CRTN
     C                     END
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*                                                                   122283
     C                     END                                            122283
     C*
     C** IF ZSADD2 IS NOT BLANK
     C           ZSADD2    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELZSADD2    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C** IF ZSADD3 IS NOT BLANK
     C           ZSADD3    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELZSADD3    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C** IF ZSADD4 IS NOT BLANK
     C           ZSADD4    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELZSADD4    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
      /EJECT                                                              183583
      *****************************************************************   183583
      *                                                               *   183583
      *   SEXTN - Store customer with SWIFT address to data structure *   183583
      *                                                               *   183583
      *   Called by: #ZA                                              *   183583
      *   Calls    : None                                             *   183583
      *                                                               *   183583
      *****************************************************************   183583
      *                                                                   183583
     C           SEXTN     BEGSR                                          183583
      *                                                                   183583
     C           SAVMTG    IFNE *BLANKS                                   183583
     C                     MOVE SAVMTG    @MTG                            183583
     C                     ELSE                                           183583
     C                     MOVELMTAG      @MTG                            183583
     C                     ENDIF                                          183583
      *                                                                   183583
     C           FLDTYP    IFEQ '8'                                       183583
     C           FLDTAG    ANDEQ'A'                                       183583
     C                     ADD  1         V                               183583
     C           V         OCUR EXTN                                      183583
     C                     Z-ADDX         MPSN                            183583
     C                     MOVEL@MTG      MTG                             183583
     C                     MOVELZDEST     MCSI                            183583
     C                     ENDIF                                          183583
      *                                                                   183583
     C           SEXT9     ENDSR                                          183583
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  #W01      - PRINT MESSAGE GENERATED DETAILS                 *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           #W01      BEGSR
     C*
     C** INITIALISE FLAGS AND FIELDS USED IN REPORT
     C                     MOVE '0'       *IN80
     C                     MOVE '0'       *IN81
     C                     CLEARTEXT1
     C*
     C** IF MGMODI EQUALS 'SE' AND MGTTYP EQUALS 'TR'
     C           MGMODI    IFEQ 'SE'
     C           MGTTYP    ANDEQ'TR'
     C           MGMODI    OREQ 'SE'                                      CSW003
     C           MGTTYP    ANDEQ'TS'                                      CSW003
     C                     MOVE '1'       *IN80
     C*
     C** FORMAT TRADE REFERENCE
     C                     MOVELMGTNUM    R1REFE
     C*
     C** FORMAT TRADE TYPE
     C                     MOVELMGTSTP    R1TYP1
     C*
     C** FORMAT TRADE TYPE DESCRIPTION
     C           MGTSTP    IFEQ 'TS'
     C                     MOVELTYPA,1    R1TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'TP'
     C                     MOVELTYPA,2    R1TYP2
     C                     END
     C*
     C                     END
     C*
     C** IF MGMODI EQUALS 'SE' AND MGTTYP EQUALS 'DM'
     C           MGMODI    IFEQ 'SE'
     C           MGTTYP    ANDEQ'DM'
     C           MGMODI    OREQ 'SE'                                      CSW003
     C           MGTTYP    ANDEQ'DN'                                      CSW003
     C                     MOVE '1'       *IN81
     C*
     C** FORMAT MOVEMENT REFERENCE
     C                     MOVELMGTNUM    R1REFE
     C*
     C** FORMAT MOVEMENT TYPE
     C                     MOVELMGTSTP    R1TYP1
     C*
     C** FORMAT MOVEMENT TYPE DESCRIPTION
     C           MGTSTP    IFEQ 'WI'
     C                     MOVELTYPA,3    R1TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'WO'
     C                     MOVELTYPA,4    R1TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'BB'
     C                     MOVELTYPA,5    R1TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'BL'
     C                     MOVELTYPA,6    R1TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'PL'
     C                     MOVELTYPA,7    R1TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'PD'
     C                     MOVELTYPA,8    R1TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'RR'
     C                     MOVELTYPA,9    R1TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'RP'
     C                     MOVELTYPA,10   R1TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'DT'
     C                     MOVELTYPA,11   R1TYP2
     C                     END
     C*
     C                     END
     C*
     C** FORMAT SENDER BRANCH
     C                     MOVELMGSNBR    R1SND1
     C*
     C** FORMAT SENDER CUSTOMER NUMBER
     C                     MOVEL@SNDR     R1SND2
     C*
     C** FORMAT SENDER NETWORK ADDRESS
     C                     MOVEL@NWSN     R1SND3
     C*
     C** FORMAT SENDER REPORT NAME
     C                     MOVEL@SRNM     R1SRNM
     C*
     C** FORMAT SENDER REPORT TOWN
     C                     MOVEL@SRTN     R1SRTN
     C*
     C** FORMAT DESTINATION REPORT NAME
     C                     MOVEL@DRNM     R1DRNM
     C*
     C** FORMAT DESTINATION REPORT TOWN
     C                     MOVEL@DRTN     R1DRTN
     C*
     C** FORMAT MESSAGE PRIORITY
     C                     MOVEL'N'       R1PRTY
     C*
     C** FORMAT DESTINATION CUSTOMER NUMBER
     C**********           Z-ADDMGDEST    R1DES1                                              CSD027
     C                     MOVE MGDEST    R1DES1                                              CSD027
     C*
     C** FORMAT DESTINATION NETWORK ADDRESS
     C                     MOVEL@NWDS     R1DES2
     C*
     C** FORMAT NETWORK
     C                     MOVEL@NWRK     R1NETW
     C*
     C** WRITE THE HEADER AND THE MAIN TEXT FOR THE REPORT
     C                     WRITEHEADER1
     C                     WRITETEXT1
     C*
     C** PROCESS MULTIPLE OCCURRENCE DATA STRUCTURE
     C                     Z-ADD0         I       20
     C           I         DOWLEX
     C                     ADD  1         I
     C           I         OCUR MULT
     C*
     C** WRITE A DETAIL LINE TO THE REPORT CHECKING FOR OVERFLOW
     C           WWOFL1    SUB  WWPTL1    WWLINE
     C*
     C           WWLINE    IFLT 3
     C                     WRITEHEADER1
     C                     END
     C                     WRITELINE1
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  #W02      - PRINT MESSAGE NOT GENERATED DETAILS             *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           #W02      BEGSR
     C*
     C** INITIALISE FLAGS AND FIELDS USED IN REPORT
     C                     MOVE '0'       *IN82
     C                     MOVE '0'       *IN83
     C                     CLEARTEXT2
     C*
     C** OPEN PRINTER FILE
     C           *IN50     IFEQ '0'
     C************         OPEN MG0522P2                                  091250
     C                     OPEN MG0522A2                                  091250
     C                     MOVE '1'       *IN50
     C                     END
     C*
     C** IF MGMODI EQUALS 'SE' AND MGTTYP EQUALS 'TR'
     C           MGMODI    IFEQ 'SE'
     C           MGTTYP    ANDEQ'TR'
     C           MGMODI    OREQ 'SE'                                      CSW003
     C           MGTTYP    ANDEQ'TS'                                      CSW003
     C                     MOVE '1'       *IN82
     C*
     C** FORMAT TRADE REFERENCE
     C                     MOVELMGTNUM    R2REFE
     C*
     C** FORMAT TRADE TYPE
     C                     MOVELMGTTYP    R2TYP1
     C*
     C** FORMAT TRADE TYPE DESCRIPTION
     C           MGTSTP    IFEQ 'TS'
     C                     MOVELTYPA,1    R2TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'TP'
     C                     MOVELTYPA,2    R2TYP2
     C                     END
     C*
     C                     END
     C*
     C** IF MGMODI EQUALS 'SE' AND MGTTYP EQUALS 'DM'
     C           MGMODI    IFEQ 'SE'
     C           MGTTYP    ANDEQ'DM'
     C           MGMODI    OREQ 'SE'                                      CSW003
     C           MGTTYP    ANDEQ'DN'                                      CSW003
     C                     MOVE '1'       *IN83
     C*
     C** FORMAT MOVEMENT REFERENCE
     C                     MOVELMGTNUM    R2REFE
     C*
     C** FORMAT MOVEMENT TYPE
     C                     MOVELMGTSTP    R2TYP1
     C*
     C** FORMAT MOVEMENT TYPE DESCRIPTION
     C           MGTSTP    IFEQ 'WI'
     C                     MOVELTYPA,3    R2TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'WO'
     C                     MOVELTYPA,4    R2TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'BB'
     C                     MOVELTYPA,5    R2TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'BL'
     C                     MOVELTYPA,6    R2TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'PL'
     C                     MOVELTYPA,7    R2TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'PD'
     C                     MOVELTYPA,8    R2TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'RR'
     C                     MOVELTYPA,9    R2TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'RP'
     C                     MOVELTYPA,10   R2TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'DT'
     C                     MOVELTYPA,11   R2TYP2
     C                     END
     C*
     C                     END
     C*
     C** FORMAT SENDER BRANCH
     C                     MOVELMGSNBR    R2SND1
     C*
     C** FORMAT SENDER CUSTOMER NUMBER
     C                     MOVEL@SNDR     R2SND2
     C*
     C** FORMAT SENDER NETWORK ADDRESS
     C                     MOVEL@NWSN     R2SND3
     C*
     C** FORMAT DESTINATION CUSTOMER NUMBER
     C**********           Z-ADDMGDEST    R2DES1                                              CSD027
     C                     MOVE MGDEST    R2DES1                                              CSD027
     C*
     C** FORMAT DESTINATION NETWORK ADDRESS
     C                     MOVEL@NWDS     R2DES2
     C*
     C** FORMAT DESTINATION ERROR MESSAGE
     C                     MOVELDSERR     R2EMSG
     C*
     C** FORMAT NETWORK
     C                     MOVEL@NWRK     R2NETW
     C*
     C** FORMAT NETWORK ERROR MESSAGE
     C                     MOVELNWERR     R2NWER
     C*
     C** FORMAT MODULE ID
     C                     MOVELMGMODI    R2MODI
     C*
     C** FORMAT TRANSACTION NUMBER
     C                     MOVELMGTNUM    R2TNUM
     C*
     C** WRITE THE HEADER AND THE MAIN TEXT FOR THE REPORT
     C                     WRITEHEADER2
     C                     WRITETEXT2
     C*
     C** PROCESS MULTIPLE OCCURRENCE DATA STRUCTURE
     C                     Z-ADD0         I       20
     C           I         DOWLEX
     C                     ADD  1         I
     C           I         OCUR MULT
     C*
     C** WRITE A DETAIL LINE TO THE REPORT CHECKING FOR OVERFLOW
     C           WWOFL2    SUB  WWPTL2    WWLINE
     C*
     C           WWLINE    IFLT 3
     C                     WRITEHEADER2
     C                     END
     C                     WRITELINE2
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  #W05      - FORMAT FIELDS FOR MGOREFPD                      *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - OFLDS
     C**                                                              *
     C*****************************************************************
     C*
     C           #W05      BEGSR
     C*
     C** FORMAT FIELDS ON PF/MGOREFPD
     C                     MOVELMGMODI    MODI
     C                     MOVELWWDATA    TRNO
     C                     MOVELMGTNUM    MTRN
     C                     MOVELMGTTYP    TNTP
     C                     MOVELMGTSTP    SBTP
     C                     MOVEL@NWRK     NWRK
     C                     MOVELA8BICN    SECN
     C                     MOVEL@NWSN     NWSN
     C**********           Z-ADDMGDEST    DECN                                                CSD027
     C                     MOVE MGDEST    DECN                                                CSD027
     C                     MOVEL@NWDS     NWDS
     C                     MOVELMGBSSN    NWBN
     C                     MOVEL'522'     MTPY
     C                     MOVEL'N'       MPRY
     C***********          MOVEL'1'       MGSG                            082282
     C***********          MOVEL'CRTD'    MGST                            082282
     C                     EXSR MSTAT                                     082282
     C                     TIME           MGTM
     C                     MOVELBJMRDT    LADT
     C                     TIME           LATM
     C                     MOVELWWSDAT    SVDT
      *                                                                   141572
      ** Setup century indicator CINDV for value date SVDT                141572
     C           SVDT      IFEQ *BLANK                                    141572
     C                     MOVE *BLANK    CINDV                           141572
     C                     ELSE                                           141572
     C                     MOVELSVDT      YY      20                      141572
     C           YY        IFLT 72                                        141572
     C                     MOVE '2'       CINDV                           141572
     C                     ELSE                                           141572
     C                     MOVE '1'       CINDV                           141572
     C                     ENDIF                                          141572
     C                     ENDIF                                          141572
      *                                                                   141572
     C                     MOVELMGBRCA    BRCA
     C                     MOVEL'Y'       TSKS
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT                                                              082282
     C*****************************************************************   082282
     C**                                                              *   082282
     C**  MSTAT     - Determine message status                        *   082282
     C**                                                              *   082282
     C*****************************************************************   082282
      *                                                                   082282
     C           MSTAT     BEGSR                                          082282
      *                                                                   082282
      * Reset status array                                                082282
     C                     CLEAR@ST                                       082282
      *                                                                   082282
      * Set key to destination customer number                            082282
     C                     MOVELDECN      @KEY      P                     082282
      *                                                                   082282
      * Get customer details via access object                            082282
     C                     CALL 'AOCUSTR0'                                082282
     C                     PARM *BLANKS   @RTCD   7                       082282
     C                     PARM '*KEY   ' @OPTN   7                       082282
     C                     PARM           @KEY   10                       082282
     C                     PARM *BLANKS   @KYST   7                       082282
     C           SDCUST    PARM SDCUST    DSSDY                           082282
      *                                                                   082282
      * Fill status array                                                 082282
     C           @RTCD     IFEQ *BLANKS                                   082282
     C                     MOVELBBDS01    @ST,1                           082282
     C                     MOVELBBDS02    @ST,2                           082282
     C                     MOVELBBDS03    @ST,3                           082282
     C                     MOVELBBDS04    @ST,4                           082282
     C                     MOVELBBDS05    @ST,5                           082282
     C                     MOVELBBDS06    @ST,6                           082282
     C                     MOVELBBDS07    @ST,7                           082282
     C                     MOVELBBDS08    @ST,8                           082282
     C                     MOVELBBDS09    @ST,9                           082282
     C                     MOVELBBDS10    @ST,10                          082282
     C                     MOVELBBDS11    @ST,11                          082282
     C                     MOVELBBDS12    @ST,12                          082282
     C                     MOVELBBDS13    @ST,13                          082282
     C                     MOVELBBDS14    @ST,14                          082282
     C                     MOVELBBDS15    @ST,15                          082282
     C                     ENDIF                                          082282
      *                                                                   082282
      * Is the message type in the array? Set on *IN60 if it is.          082282
     C           '522'     LOKUP@ST                      60               082282
     C  N60      '52*'     LOKUP@ST                      60               082282
     C  N60      '5**'     LOKUP@ST                      60               082282
      *                                                                   082282
      * If found, set status to 'ready to send', else set to 'created'    082282
     C           *IN60     IFEQ *ON                                       082282
     C                     MOVEL'RSND'    MGST                            082282
     C                     MOVEL'2'       MGSG                            082282
     C                     ELSE                                           082282
     C                     MOVEL'CRTD'    MGST                            082282
     C                     MOVEL'1'       MGSG                            082282
     C                     ENDIF                                          082282
      *                                                                   082282
     C                     ENDSR                                          082282
      *                                                                   082282
     C*****************************************************************   082282
     C/EJECT
     C/COPY WNCPYSRC,MG0522C009
     C*****************************************************************
     C**                                                              *
     C**  PSSR      - ERROR HANDLING                                  *
     C**                                                              *
     C**  CALLS     -                                                 *
     C**                                                              *
     C**  CALLED BY -                                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C** ROLLBACK ALL FILE CHANGES
     C                     ROLBK
     C*
     C** WRITE DATABASE ERROR TO REPORT
     C                     OPEN MG0522AU
     C                     WRITEHEAD
     C                     WRITEDBERROR
     C*
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C/COPY MGCPYSRC,SWIFTTRANS                                           BLI605
** CPY@   Object Copyright
(c) Misys International Banking Systems Ltd. 2001
** ERRA   ERROR MESSAGES
Customer number not found
Unable to format mandatory field :20:
Unable to format mandatory field :35A:
Unable to format mandatory field :35B:
Certificate numbers not found on file
Unable to format addresses for field :82a:
Unable to format addresses for field :83a:
Unable to format mandatory field :87a:
Unable to format addresses for field :87a:
Unable to format addresses for field :88a:
*** Message is Unroutable ***
** DESA   DESCRIPTIONS
                       DELIVERY DATE
                              T.R.N.
                   RELATED REFERENCE
             DATE AND PLACE OF TRADE
              QUANTITY OF SECURITIES
        IDENTIFICATION OF SECURITIES
                         NEXT COUPON
                 CERTIFICATE NUMBERS
                   INSTRUCTING PARTY
                 SAFEKEEPING ACCOUNT
              RECEIVER OF SECURITIES
           BENEFICIARY OF SECURITIES
      SENDER TO RECEIVER INFORMATION
** TYPA   TRADE/MOVEMENT TYPE DESCRIPTIONS
TRADE SALE
TRADE PURCHASE
WALK IN
WALK OUT
BOND BORROWING
BOND LENDING
PLEDGED LOAN
PLEDGED DEPOSIT
REVERSE REPURCHASE
REPURCHASE AGREEMENT
DEPOT TRANSFER
** SUPP - add any fields tags which should be suppressed
