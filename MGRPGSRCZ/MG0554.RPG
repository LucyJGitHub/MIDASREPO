     H        1                           F                               S01421
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MG Advice of money income messages')             *
     H******  1                                                           S01421
      *****************************************************************
      *                                                               *
      *  Midas - Message Generation Module                            *
      *                                                               *
      *   MG0554- FORMAT MT554 REPORT MESSAGES                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSW025             Date 26Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSW201             Date 02May01               *
      *                 183583             Date 25Jun01               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSW020             Date 04Jan00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 141572             Date 21Sep98               *
      *                 113352            DATE 13FEB97                *
      *                 122283             DATE 16FEB98               *
      *                 CFF004             DATE 02OCT96               *
      *                 056532             DATE 09MAY96               *
      *                 057263             DATE 09MAY96               *
      *                 078256             DATE 07MAY96               *
      *                 CSW003             DATE 09JAN96               *
      *                 091250             DATE 24FEB95               *
      *                 080096             DATE 12JAN95               *
      *                 S01421             DATE 18AUG93               *
      *                 S01401             DATE 28JUN93               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW025 - Midas Message Manager                               *
      *           - Pass original change type                         *
      *  CSW201 - SWIFT 2001 Standards Update                         *
      *  183583 - Add extension file MGOEXTPD to store customer no.   *
      *           of field with SWIFT address.                        *
      *  CDL008 - Continuous Linked Settlement (Recompiled)           *
      *  CSW020 - SWIFT Decimal Places                                *
      *           Take in account the swift decimal places to         *
      *           calculate the Sum of Nets Proceeds.                 *
      *  141572 - Recompiled over MGOREFPD                            *
      *  113352 - Set-up CIND (Century flag)  to correct year2000     *
      *           display problem.                                    *
      *  122283 - Blank line in MGOMSGPD if account number is blank   *
      *           and not account line.                               *
      *  CFF004 - Increase in size of Price Fields,(Recompiled Only)  *
      *  056532 - Process incorrect car. '-' ' ' ':' in first position*
      *  057263 - All postiion in field "MTAG" need to be full.       *
      *  078256 - Use nominal decimal places from file                *
      *  CSW003 - MT5xx Message Generation - Phase 2                  *
      *  091250 - Print Files MG0554P1/P2 renamed to MG0554A1/A2 as   *
      *           these are audit reports.                            *
      *  080096 - RECOMPILE BECAUSE COPY MEMBER MGCPYSRC,SWIFTTRANS   *
      *           CHANGED                                             *
      *   S01421 - REPLACE THE SWIFT TRANSLATION TABLE WITH A         *
      *              /COPY STATEMENT (SWIFTTRANS).                    *
      *                                                               *
      *  S01401 - MT500 messages for Release 10; new program.         *
      *                                                               *
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *****************************************************************
     F*
     FMGF554L0UF  E           K        DISK         KINFSR *PSSR
     F                                              KCOMIT
     FSDBANKPDIF  E                    DISK         KINFSR *PSSR
     FSDBRCHL1IF  E           K        DISK         KINFSR *PSSR
     FSDCURRL1IF  E           K        DISK         KINFSR *PSSR
     F/COPY WNCPYSRC,MG0554F001
     FSDMGMEPDIF  E                    DISK         KINFSR *PSSR
     F*
     FMGOREFPDO   E                    DISK         KCOMIT
     F                                              KINFSR *PSSR
     FMGOMSGPDO   E                    DISK         KCOMIT
     F                                              KINFSR *PSSR
     FMGOEXTPDO   E                    DISK         KCOMIT                183583
     F                                              KINFSR *PSSR          183583
     FPOSETDY1UF  E           K        DISK         KINFSR *PSSR          MR0109
     F*G0554P1O***E********************PRINTER******KINFDS*OVRFL1         091250
     FMG0554A1O   E                    PRINTER      KINFDS OVRFL1         091250
     F                                              KINFSR *PSSR
     F*G0554P2O***E********************PRINTER******KINFDS*OVRFL2     UC  091250
     FMG0554A2O   E                    PRINTER      KINFDS OVRFL2     UC  091250
     F                                              KINFSR *PSSR
     FMG0554AUO   E                    PRINTER                        UC
     F*
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     E*
     E** ARRAY USED FOR COPYRIGHT
     E                    CPY@    1   1 80
     E*
     E** ARRAY USED TO FORMAT ERROR MESSAGE FIELD ON MULTIPLE
     E** OCCURENCE DATA STRUCTURE
     E                    ERRA    1  16 52
     E*
     E** ARRAY USED TO FORMAT DESCRIPTION FIELD ON MULTIPLE
     E** OCCURENCE DATA STRUCTURE
     E/COPY WNCPYSRC,MG0554E002
     E                    DESA    1  18 36
     E/COPY WNCPYSRC,MG0554E003
     E*
     E** ARRAY USED TO REMOVE FORBIDDEN CHARACTERS (E.G. %)
     E                    FORB       50  1
     E*
      **  Array used to define fields to be suppressed
     E                    SUPP    1  50  5
      *
     E/COPY WNCPYSRC,MG0554E001
     E*****************************************************************
     E/EJECT
     E*****************************************************************
     I*
     IRUNDAT      DS                                                      CSW020
     I                                        1   7 MRDT                  CSW020
     I                                    P   8  100RDNB                  CSW020
     I                                       11  11 SUC                   CSW020
     I                                       12  12 DFF                   CSW020
     I                                       13  13 MBIN                  CSW020
      *                                                                   CSW020
     I** Multiple occurrence data structure for message
     IMULT        DS                        500
     I                                        1  36 MDES
     I                                       37  41 MTAG
     I                                       42  91 MDAT
     I                                       92 143 MERR
     I                                      162 181 CRNM
     I                                      184 193 CRTN
     I                                      144 193 RDAT
      *                                                                   183583
      ** Multiple occurrence data structure for extension file            183583
      *                                                                   183583
     IEXTN        DS                         95                           183583
     I                                        1   20MPSN                  183583
     I                                        3   7 MTG                   183583
     I                                        8  15 MCSI                  183583
      *                                                                   183583
      ** Field tag data structure                                         183583
      *                                                                   183583
     I            DS                                                      183583
     I                                        1   5 @MTG                  183583
     I                                        2   2 FLDTYP                183583
     I                                        4   4 FLDTAG                183583
     I*
     I** Multiple occurrence data structure for incorrectly
     I** formatted messages
     IWMULT       DS                        500
     I                                        1   5 WMTAG
     I                                        6  55 WMDAT
     I                                       56 107 WMERR
     I*
     I** Multiple occurrence data structure for each MGOREFPD record
     IREFDS       DS                         10
     I                                        1   2 WWMOD
     I                                        3  18 WWTRNO
     I                                       19  33 WWMTRN
     I                                       34  39 WWDECN
     I                                       40  42 WWBRCA
     I                                       43  59 WWAMTS
     I*
     I** Multiple occurrence data structure for each Transaction field
     ITRANS       DS                         11
     I                                        1  16 WWTRNM
     I*
     I** Local data area
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I** DATA STRUCTURE FOR CONSTRUCTING DATA FIELD
     IWWMDAT      DS                         50
     I                                        1   3 WD0103
     I                                        1   6 WD0106
     I                                        4  19 WD0419
     I                                        6  17 WD0617
     I                                        7   9 WD0709
     I                                       10  26 WD1026
     I                                       10  25 WD1025
     I                                        1   5 WD0105
     I                                        1  17 WD0117
     I                                        4  13 WD0413
     I                                       14  16 WD1416
     I                                        1  12 WD0112
     I                                        4   9 WD0409
     I                                       10  16 WD1016
     I                                        1   4 WD0104
     I                                        5  16 WD0516
     I*
     I** DATA STRUCTURE FOR CONSTRUCTING DATA FIELD
     IWWRDAT      DS                         50
     I                                        1   3 WR0103
     I                                        4  24 WR0424
     I                                        1   6 WR0106
     I                                        7   9 WR0709
     I                                       10  30 WR1030
     I                                        1   8 WR0108
     I                                        9  11 WR0911
     I                                       12  32 WR1232
     I*
     I** DATA STRUCTURE FOR READING MATCHING RECORDS ON MGF554L0
     IWWDS1       DS
     I                                        1 110 WWKDS1
     I                                        1   2 WWMODI
     I**********                              3   80WWDEST                                    CSD027
     I                                        3   8 WWDEST                                    CSD027
     I                                        9  11 WWSNBR
     I                                       12  21 WWAFPN
     I                                       22  56 WWAFPL
     I                                       57  59 WWCYNP
     I                                       60  69 WWSKAN
     I                                       70 104 WWSKAL
     I                                      105 110 WWFID3
     I*
     I** DATA STRUCTURE FOR SETTING UP @TRAN PARAMETER
     IWWTPRM      DS
     I                                        1  15 WWTRAN
     I                                        1   6 WT0106
     I                                        7  13 WT0713
     I                                       14  15 WT1415
     I*
     I** DATA STRUCTURE FOR READING MATCHING RECORDS ON MGF554L0
     IWWDS2       DS
     I                                        1 110 WWKDS2
     I                                        1   2 MGMODI
     I**********                              3   80MGDEST                                    CSD027
     I                                        3   8 MGDEST                                    CSD027
     I                                        9  11 MGSNBR
     I                                       12  21 MGAFPN
     I                                       22  56 MGAFPL
     I                                       57  59 MGCYNP
     I                                       60  69 MGSKAN
     I                                       70 104 MGSKAL
     I                                      105 110 MGFID3
     I*
     I** DATA STRUCTURE FOR DBERROR
     IDBERR       DS                             20
     I                                        1   2 @MODI
     I                                        3  17 @TRAN
     I*
     I** DATA STRUCTURE FOR DBERROR
     IDBERR1      DS                             26
     I                                        1   6 @SNDR
     I                                        7  18 @SCSD
     I                                       19  24 @DEST
     I                                       25  26 @SETP
     I*
     I** DATA STRUCTURE TO SPLIT DATE
     I            DS
     I                                        1   6 ZSDATE
     I                                        1   2 WWYEAR
     I                                        3   4 WWMNTH
     I                                        5   6 WWDAY
     I*
     I** DATA STRUCTURE TO BUILD DATE
     I            DS
     I                                        1   8 WRDATE
     I                                        1   2 WRYEAR
     I I            '/'                       3   3 WRSL1
     I                                        4   5 WRMNTH
     I I            '/'                       6   6 WRSL2
     I                                        7   8 WRDAY
     I*
     I***DATA*STRUCTURE*FOR*OVERFLOW*ON*PRINTER*MG0554P1***               091250
     I** FILE INFORMATION DATA STRUCTURE FOR MG0554A1                     091250
     IOVRFL1      DS
     I                                    B 188 1890WWOFL1
     I                                    B 367 3680WWPTL1
     I*
     I***DATA*STRUCTURE*FOR*OVERFLOW*ON*PRINTER*MG0554P2***               091250
     I** FILE INFORMATION DATA STRUCTURE FOR MG0554A2                     091250
     IOVRFL2      DS
     I                                    B 188 1890WWOFL2
     I                                    B 367 3680WWPTL2
     I*
     I** DATA STRUCTURE FOR CRLF CONTROL CHARACTER
     ICRLFDS      DS                              2
     I                                        1   2 CTRC
     I                                        1   1 WWCR
     I                                        2   2 WWLF
     I*
     I** DATA STRUCTURE FOR FORMATTING POSETDY1 KEY WHEN DATABASE
     I** ERROR IS GENERATED
     IPOSEDS      DS
     I                                        1  31 WWPOSE
     I                                        1   3 WWSEQN
     I                                        4  13 MGPSSH
     I**********                             14  190WWTNUM                                    CSD027
     I                                       14  19 WWTNUM                                    CSD027
     I                                       20  240MGPDUD
     I                                       25  26 MGPEVT
     I*
     I* DATA STRUCTURE FOR EXTENDED SETTLEMENT FIELDS
     I            DS
     I                                        1  16 WXSFLD
     I                                        1   1 WXS53
     I                                        2   2 WXS57
     I                                        3   3 WXS58
     I                                        4   4 WXS71
     I                                        5   5 WXS72
     I                                        6   6 WXS77F
     I                                        7   7 WXS77R
     I                                        8   8 WXS79
     I                                        9   9 WXS82I
     I                                       10  10 WXS82C
     I                                       11  11 WXS83
     I                                       12  12 WXS84
     I                                       13  13 WXS85
     I                                       14  14 WXS87R
     I                                       15  15 WXS87D
     I                                       16  16 WXS88
     I*                                                                   CSW020
     ISCSARD    E DSSCSARDPD                                              CSW020
     I**  Data structure for SAR data.                                    CSW020
      *                                                                   CSW020
     IDSFDY     E DSDSFDY                                                 CSW020
      **  Short data structure for access programs                        CSW020
     I*
     I*****************************************************************
     I/EJECT
     I*****************************************************************
      *                                                               *
      * 1. Multiple Data Structure MULT                               *
      *      The multiple occurrence data structure is used to        *
      *      store up the records that make up the SWIFT message      *
      *      before writing the whole message to file.                *
      *      To store the records of the message, you must            *
      *      first access an OCUR record, obtain the Message Tag      *
      *      & Message field data values for this OCUR record,        *
      *      then go on to the next OCUR record and continue          *
      *      this process until the SWIFT message is complete.        *
      *                                                               *
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*   MAIN PROCESSING - FORMAT MT554                              *
     C*                                                               *
     C*   CALLS THE FOLLOWING SUBROUTINES                             *
     C*                                                               *
     C*   INIT     - INITIALISATION                                   *
     C*   INTF     - INITIALISE NON-CONSTANT FIELDS                   *
     C*   ROUT     - DETERMINE MESSAGE ROUTING                        *
     C*   NRSEQ    - FORMAT FIELDS FOR NON-REPETITIVE SEQUENCE        *
     C*   RSEQ     - FORMAT FIELDS FOR REPETITIVE SEQUENCE            *
     C*   FLD19    - FORMAT MANDATORY FIELD :19:                      *
     C*   FLD23    - FORMAT MANDATORY FIELD :23:                      *
     C*   FLD53A   - FORMAT FIELD :53a:                               *
     C*   FLD83A   - FORMAT FIELD :83a:                               *
     C*   FLD72N   - FORMAT FIELD :72: (NON-REPETITIVE SEQ)           *
     C*   FLD20    - FORMAT MANDATORY FIELD :20:                      *
     C*   FLD21    - FORMAT FIELD :21:                                *
     C*   FLD87A   - FORMAT FIELD :87a:                               *
     C*   FLD35A   - FORMAT MANDATORY FIELD :35A:                     *
     C*   FLD35B   - FORMAT MANDATORY FIELD :35B:                     *
     C*   FLD35    - FORMAT FIELD :35a:                               *
     C*   FLD35U   - FORMAT MANDATORY FIELD :35U:                     *
     C*   FLD31X   - FORMAT FIELD :31X:                               *
     C*   FLD31E   - FORMAT FIELD :31E:                               *
     C*   FLD31L   - FORMAT FIELD :31L:                               *
     C*   FLD31C   - FORMAT FIELD :31C:                               *
     C*   FLD80C   - FORMAT FIELD :80C:                               *
     C*   FLD32G   - FORMAT FIELD :32G:                               *
     C*   FLD71C   - FORMAT FIELD :71C:                               *
     C*   FLD71B   - FORMAT FIELD :71B:                               *
     C*   FLD36    - FORMAT FIELD :36:                                *
     C*   FLD34A   - FORMAT MANDATORY FIELD :34A:                     *
     C*   FLD72R   - FORMAT FIELD :72: (REPETITIVE SEQ)               *
     C*   #ZA      - FORMAT ADDRESSES                                 *
     C*   #W01     - PRINT MESSAGE GENERATED DETAILS                  *
     C*   #W02     - PRINT MESSAGE NOT GENERATED DETAILS              *
     C*   #W04     - FORMAT FIELDS FOR PF/MGOREFPD                    *
     C*   WREF     - FORMAT FIELDS FOR PF/MGOREFPD DATA STRUCTURE     *
     C*   OFLDS    - FORMAT DETAILS FOR MIDAS SWIFT DATABASE FILES    *
     C*   POSU     - POSITION SETTLEMENTS EXTENSION FILE UPDATE       *
     C*   #W03     - PRINT DATABASE ERROR REPORT                      *
     C*   *PSSR    - ERROR HANDLING                                   *
     C*                                                               *
     C*****************************************************************
     C*
     C** INITIALISATION
     C                     EXSR INIT
     C*
     C** READ FIRST RECORD FROM LF/MGF554PD
     C                     READ MGF554L0                 10
     C*
     C** IF RECORDS PRESENT ON THE FILE, STORE KEY FIELDS
     C           *IN10     IFEQ '0'
     C                     MOVE MGMODI    WWMODI
     C                     MOVE MGDEST    WWDEST
     C                     MOVE MGSNBR    WWSNBR
     C                     MOVE MGFID3    WWFID3
     C                     MOVE MGAFPN    WWAFPN
     C                     MOVE MGAFPL    WWAFPL
     C                     MOVE MGCYNP    WWCYNP
     C                     MOVE MGSKAN    WWSKAN
     C                     MOVE MGSKAL    WWSKAL
     C*
     C** BLANK OUT FIELDS NOT REQUIRED IN DATA STRUCTURE FOR MULTIPLE
     C           F53       IFNE 'Y'
     C                     MOVE *BLANKS   WWAFPN
     C                     MOVE *BLANKS   WWAFPL
     C                     MOVE *BLANKS   MGAFPN
     C                     MOVE *BLANKS   MGAFPL
     C                     END
     C           F83       IFNE 'Y'
     C                     MOVE *BLANKS   WWSKAN
     C                     MOVE *BLANKS   WWSKAL
     C                     MOVE *BLANKS   MGSKAN
     C                     MOVE *BLANKS   MGSKAL
     C                     END
     C*
     C** DO WHILE RECORDS ON MGF554L0
     C           *IN10     DOWEQ'0'
     C*
     C** INITIALISE NON-CONSTANT FIELDS
     C                     EXSR INTF
     C*
     C** DETERMINE MESSAGE ROUTING
     C                     EXSR ROUT
     C*
     C** STORE FIELDS FFROM MGF554L0 FOR REPORT HEADERS
     C                     MOVE MGSNBR    WWHDR1
     C                     MOVE MGDEST    WWHDR2
     C                     MOVE MGMODI    WWHDR3
     C*
     C** FORMAT FIELDS FOR NON REPETITIVE SEQUENCE
     C                     EXSR NRSEQ
     C*
     C** FORMAT FIELDS FOR REPETITIVE SEQUENCE
     C                     EXSR RSEQ
     C*
     C** IF ERROR FOUND ON MESSAGE THEN SET UP DATA STRUCTURE TO WRITE
     C** ONLY THIS PART OF MESSAGE TO MESSAGES NOT CORRECTLY GENERATED
     C** REPORT
     C           WWNGEN    IFEQ 'Y'
     C                     EXSR NGEN
     C                     ELSE
     C*
     C** IF NO ERRORS, PERFORM UPDATES
     C                     MOVE 'Y'       WWVALP  1
     C*
     C** UPDATE SEQUENCE NUMBERS ON TRANSACTION RECORD
     C*
     C** FORMAT @TRAN AS FOLLOWS
     C** CHARS 1-6 TO LEFTMOST 6 CHARS FROM MGTNUM
     C** CHARS 7-13 TO BLANKS
     C** CHARS 14-15 TO 'PS'
     C                     MOVE *BLANKS   WWTRAN
     C                     MOVELMGTNUM    WT0106
     C                     MOVE 'PS'      WT1415
     C                     CALL 'MG5900'               20
     C                     PARM MGMODI    @MODI   2
     C                     PARM WWTRAN    @TRAN  15
     C                     PARM 'PR'      @PROG   2
     C                     PARM 'U'       @MODE   1
     C                     PARM *ZEROS    MGLSWS  70
     C                     PARM *BLANKS   @ERCD   1
     C                     PARM *BLANKS   CSW003  1                       CSW003
     C*
     C** IF PROGRAM MG5900 NOT FOUND
     C           *IN20     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '001'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 001  *
     C                     MOVEL'MG5900'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM MG5900
     C           @ERCD     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     MOVE '002'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 002  *
     C                     MOVELDBERR     DBKEY            * * * * * * * *
     C                     MOVEL'MG5900'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     END
     C*
     C** DELETE THE RECORD ON LF/MGF554L0 PROCESSED
     C                     DELETMGF554D0               11
     C*
     C** IF ERROR ON DELETE OF RECORD
     C           *IN11     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '003'     DBASE              * * * * * * *
     C                     MOVELMGTNUM    DBKEY              * DBERR 003 *
     C                     MOVEL'MGF554L0'DBFILE             * * * * * * *
     C                     MOVEL'MG0554'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** UPDATE INDICATOR ON POS. SETTL. EXT. FILE
     C           WWVALP    IFEQ 'Y'
     C                     EXSR POSU
     C                     END
     C*
     C** READ NEXT RECORD FROM LF/MGF554L0
     C                     READ MGF554L0                 10
     C*
     C** BLANK OUT FIELDS NOT REQUIRED IN DATA STRUCTURE FOR MULTIPLE
     C           F53       IFNE 'Y'
     C                     MOVE *BLANKS   WWAFPN
     C                     MOVE *BLANKS   WWAFPL
     C                     MOVE *BLANKS   MGAFPN
     C                     MOVE *BLANKS   MGAFPL
     C                     END
     C*
     C           F83       IFNE 'Y'
     C                     MOVE *BLANKS   WWSKAN
     C                     MOVE *BLANKS   WWSKAL
     C                     MOVE *BLANKS   MGSKAN
     C                     MOVE *BLANKS   MGSKAL
     C                     END
     C*
     C** DO WHILE THE KEY FIELDS ON THE RECORD READ MATCH THE
     C** STORED KEY FIELDS, BUT EXECUTE THE LOOP NO MORE THAN
     C** 10 TIMES
     C           WWKDS1    DOWEQWWKDS2
     C           WWTOT1    ANDLT10
     C           *IN10     ANDEQ'0'
     C*
     C                     ADD  1         WWTOT1
     C*
     C** UPDATE SEQUENCE NUMBERS ON TRANSACTION RECORD
     C*
     C** FORMAT @TRAN AS FOLLOWS
     C** CHARS 1-6 TO LEFTMOST 6 CHARS FROM MGTNUM
     C** CHARS 7-13 TO BLANKS
     C** CHARS 14-15 TO 'PS'
     C                     MOVE *BLANKS   WWTRAN
     C                     MOVELMGTNUM    WT0106
     C                     MOVE 'PS'      WT1415
     C                     CALL 'MG5900'               20
     C                     PARM MGMODI    @MODI
     C                     PARM WWTRAN    @TRAN
     C                     PARM 'PR'      @PROG
     C                     PARM *BLANK    @MODE
     C                     PARM *ZEROS    MGLSWS
     C                     PARM *BLANKS   @ERCD
     C                     PARM *BLANKS   CSW003                          CSW003
     C*
     C** IF PROGRAM MG5900 NOT FOUND
     C           *IN20     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '011'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 011  *
     C                     MOVEL'MG5900'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM MG5900
     C           @ERCD     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     MOVE '014'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 014  *
     C                     MOVELDBERR     DBKEY            * * * * * * * *
     C                     MOVEL'MG5900'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT FIELDS FOR REPETITIVE SEQUENCE
     C                     EXSR RSEQ
     C*
     C** IF ERROR FOUND ON MESSAGE THEN SET UP DATA STRUCTURE TO WRITE
     C** ONLY THIS PART OF MESSAGE TO MESSAGES NOT CORRECTLY GENERATED
     C** REPORT
     C           WWNGEN    IFEQ 'Y'
     C                     EXSR NGEN
     C                     ELSE
     C*
     C** IF NO ERRORS, PERFORM UPDATES
     C                     MOVE 'Y'       WWVALP
     C*
     C** UPDATE SEQUENCE NUMBERS ON TRANSACTION RECORD
     C*
     C** FORMAT @TRAN AS FOLLOWS
     C** CHARS 1-6 TO LEFTMOST 6 CHARS FROM MGTNUM
     C** CHARS 7-13 TO BLANKS
     C** CHARS 14-15 TO 'PS'
     C                     MOVE *BLANKS   WWTRAN
     C                     MOVELMGTNUM    WT0106
     C                     MOVE 'PS'      WT1415
     C                     CALL 'MG5900'               20
     C                     PARM MGMODI    @MODI
     C                     PARM WWTRAN    @TRAN
     C                     PARM 'PR'      @PROG
     C                     PARM 'U'       @MODE
     C                     PARM *ZEROS    MGLSWS
     C                     PARM *BLANKS   @ERCD
     C                     PARM *BLANKS   CSW003                          CSW003
     C*
     C** IF PROGRAM MG5900 NOT FOUND
     C           *IN20     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '015'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 015  *
     C                     MOVEL'MG5900'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM MG5900
     C           @ERCD     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     MOVE '018'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 018  *
     C                     MOVELDBERR     DBKEY            * * * * * * * *
     C                     MOVEL'MG5900'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     END
     C*
     C** DELETE THE RECORD PROCESSED
     C                     DELETMGF554D0               11
     C*
     C** IF ERROR ON DELETE OF RECORD
     C           *IN11     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '020'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 020  *
     C                     MOVEL'MGF554L0'DBFILE           * * * * * * * *
     C                     MOVEL'MG0554'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** UPDATE INDICATOR ON POS. SETTL. EXT. FILE
     C           WWVALP    IFEQ 'Y'
     C                     EXSR POSU
     C                     END
     C*
     C** READ NEXT RECORD FROM LF/MGF554L0
     C                     READ MGF554L0                 10
     C*
     C** BLANK OUT FIELDS NOT REQUIRED IN DATA STRUCTURE FOR MULTIPLE
     C           F53       IFNE 'Y'
     C                     MOVE *BLANKS   WWAFPN
     C                     MOVE *BLANKS   WWAFPL
     C                     MOVE *BLANKS   MGAFPN
     C                     MOVE *BLANKS   MGAFPL
     C                     END
     C*
     C           F83       IFNE 'Y'
     C                     MOVE *BLANKS   WWSKAN
     C                     MOVE *BLANKS   WWSKAL
     C                     MOVE *BLANKS   MGSKAN
     C                     MOVE *BLANKS   MGSKAL
     C                     END
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :19:
     C                     MOVE ':19: '   WWGENF  5
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD19
     C                     END
     C*
     C** IF ERROR FOUND WHILST FORMATTING THIS MESSAGE
     C           @ERR      IFEQ 'Y'
     C*
     C** PRINT MESSAGE NOT GENERATED DETAILS
     C                     EXSR #W02
     C                     MOVE *BLANKS   @ERR
     C                     END
     C*
     C** WRITE TO MIDAS SWIFT DATABASE FILES
     C           WWVALP    IFEQ 'Y'
     C                     EXSR OFLDS
     C*
     C** PRINT MESSAGE GENERATED DETAILS
     C                     EXSR #W01
     C                     MOVE 'Y'       WWPTR1  1
     C                     MOVE *BLANKS   WWVALP
     C                     END
     C*
     C** COMIT ALL UPDATES TO FILES
     C                     COMIT
     C*
     C** RE-READ LAST RECORD ON LF/MGF554L0 TO LOCK THE RECORD
     C** FOR THE NEXT DELETE
     C           *LOVAL    SETLLMGF554L0
     C                     READ MGF554L0                 10
     C*
     C** STORE KEY FIELDS OF LAST RECORD READ
     C                     MOVE MGMODI    WWMODI
     C                     MOVE MGDEST    WWDEST
     C                     MOVE MGSNBR    WWSNBR
     C                     MOVE MGFID3    WWFID3
     C                     MOVE MGAFPN    WWAFPN
     C                     MOVE MGAFPL    WWAFPL
     C                     MOVE MGCYNP    WWCYNP
     C                     MOVE MGSKAN    WWSKAN
     C                     MOVE MGSKAL    WWSKAL
     C*
     C** BLANK OUT FIELDS NOT REQUIRED IN DATA STRUCTURE FOR MULTIPLE
     C           F53       IFNE 'Y'
     C                     MOVE *BLANKS   WWAFPN
     C                     MOVE *BLANKS   WWAFPL
     C                     MOVE *BLANKS   MGAFPN
     C                     MOVE *BLANKS   MGAFPL
     C                     END
     C*
     C           F83       IFNE 'Y'
     C                     MOVE *BLANKS   WWSKAN
     C                     MOVE *BLANKS   WWSKAL
     C                     MOVE *BLANKS   MGSKAN
     C                     MOVE *BLANKS   MGSKAL
     C                     END
     C*
     C                     END
     C*
     C** WRITE TRAILER ON SWIFT MESSAGES GENERATED REPORT
     C** ONLY IF MESSAGES GENERATED
     C           WWPTR1    IFNE 'Y'
     C                     WRITEHEADER1
     C                     WRITENODETS
     C                     ELSE
     C*
     C** CHECK FOR OVERFLOW
     C           WWOFL1    SUB  WWPTL1    WWLINE  20
     C*
     C           WWLINE    IFLT 4
     C                     WRITEHEADER1
     C                     END
     C                     WRITEEOREPT1
     C                     END
     C*
     C** WRITE TRAILER ON SWIFT MESSAGES NOT GENERATED REPORT
     C** IF THE PRINTER FILE HAS BEEN OPENED
     C           *IN50     IFEQ '1'
     C*
     C** CHECK FOR OVERFLOW
     C           WWOFL2    SUB  WWPTL2    WWLINE
     C*
     C           WWLINE    IFLT 4
     C                     WRITEHEADER2
     C                     END
     C*
     C                     WRITEEOREPT2
     C                     END
     C*
     C                     ELSE
     C*
     C** WRITE NO DETAILS TO REPORT
     C                     WRITEHEADER1
     C                     WRITENODETS
     C                     END
     C/COPY WNCPYSRC,MG0554C003
     C*
     C                     SETON                     LR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  INIT      - INITIALISATION                                  *
     C**                                                              *
     C**  CALLS     - *PSSR                                           *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESS                                    *
     C**                                                              *
     C*****************************************************************
     C*
     C           INIT      BEGSR
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C** DEFINE WORK FIELDS
     C           *LIKE     DEFN A6NBDP    WWCDPN
     C           *LIKE     DEFN MGDEST    WWHDR2
     C           *LIKE     DEFN MGSNBR    WWHDR1
     C           *LIKE     DEFN MGMODI    WWHDR3
     C*
     C                     MOVE *BLANKS   WWFL06  6
     C*
     C** SET UP LDA
     C           *NAMVAR   DEFN           LDA
     C*
     C** ACCESS SDBANKPD FOR BANK DETAILS
     C           1         SETLLSDBANKPD
     C                     READ SDBANKPD                 12
     C           *IN12     IFEQ '1'
     C           BJTYLC    OREQ 'D'
     C           *LOCK     IN   LDA
     C                     MOVE '004'     DBASE            * * * * * * * *
     C                     MOVEL*BLANKS   DBKEY            *  DBERR 004  *
     C                     MOVEL'SDBANKPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0554'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** ACCESS SDMGMEPD FOR MESSAGE GENERATION & MESSAGE MANAGEMENT
     C           1         SETLLSDMGMEPD
     C                     READ SDMGMEPD                 15
     C           *IN15     IFEQ '1'
     C           ENTYLC    OREQ 'D'
     C           *LOCK     IN   LDA
     C                     MOVE '030'     DBASE            * * * * * * * *
     C                     MOVEL*BLANKS   DBKEY            *  DBERR 030  *
     C                     MOVEL'SDMGMEPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0554'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C/COPY WNCPYSRC,MG0554C004
     C*
     C** SET CRLF CONTROL CHARACTER
     C                     MOVE *LOVAL    CTRC
     C                     BITON'457'     WWCR
     C                     BITON'257'     WWLF
     C*
     C** SET UP KEY FOR POSITION SETTLEMENTS EXTENSION FILE - POSETDY1
     C           WWKPOS    KLIST
     C                     KFLD           WWSEQN
     C                     KFLD           MGPSSH
     C                     KFLD           WWTNUM
     C                     KFLD           MGPDUD
     C                     KFLD           MGPEVT
     C*
     C/COPY WNCPYSRC,MG0554C001
     C*
     C** CHECK IF FIELD 53 AND 83 TO BE PRODUCED
     C                     MOVE ':53: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           WXS53     OREQ 'Y'
     C                     MOVE 'Y'       F53     1
     C                     END
     C*
     C                     MOVE ':83A:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           WXS83     OREQ 'Y'
     C                     MOVE 'Y'       F83     1
     C                     END
      *
      * Get Rundate - Rundate  *                                          CSW020
     C           *NAMVAR   DEFN           RUNDAT                          CSW020
     C                     IN   RUNDAT                                    CSW020
      ** Determine if Swift decimal places feature is installed           CSW020
      *                                                                   CSW020
     C                     CALL 'AOSARDR0'                                CSW020
     C                     PARM *BLANKS   P@RTCD  7                       CSW020
     C                     PARM '*KEY   ' P@OPTN  7                       CSW020
     C                     PARM 'CSW020'  P@SARD  6                       CSW020
     C           SCSARD    PARM SCSARD    DSFDY                           CSW020
      *                                                                   CSW020
     C           P@RTCD    IFEQ *BLANKS                                   CSW020
     C                     MOVE 'Y'       CSW020  1                       CSW020
     C                     ELSE                                           CSW020
     C                     MOVE 'N'       CSW020                          CSW020
      *                                                                   CSW020
     C           P@RTCD    IFNE '*NRF    '                                CSW020
     C           *LOCK     IN   LDA                                       CSW020
     C                     MOVEL'MG0554  'DBPGM                           CSW020
     C                     MOVEL'SCSARDPD'DBFILE           ************   CSW020
     C                     Z-ADD39        DBASE            * DBERR 39 *   CSW020
     C                     MOVEL'CSW020'  DBKEY     P      ************   CSW020
     C                     OUT  LDA                                       CSW020
     C                     EXSR *PSSR                                     CSW020
     C                     ENDIF                                          CSW020
      *                                                                   CSW020
     C                     ENDIF                                          CSW020
     C/COPY WNCPYSRC,MG0554C007
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  GENF      - GENERATE FIELDS CONDITION                       *
     C**                                                              *
     C**  CALLS     -                                                 *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESS                                    *
     C**                                                              *
     C*****************************************************************
     C*
     C           GENF      BEGSR
      *
      **  If message field tag appears on array SUPP, supress its
      **  production by setting off indicator 01.
     C           WWGENF    LOKUPSUPP                     55
     C           *IN55     IFEQ '0'
     C                     SETON                     01
     C/COPY WNCPYSRC,MG0554C002
     C                     ELSE
     C                     SETOF                     01
     C                     END
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  INTF      - INITIALISE NON-CONSTANT FIELDS                  *
     C**                                                              *
     C**  CALLS     - MG5900, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESS                                    *
     C**                                                              *
     C*****************************************************************
     C*
     C           INTF      BEGSR
     C*
     C** OBTAIN SEQUENCE NUMBERS FROM TRANSACTION RECORD
     C** FORMAT @TRAN AS FOLLOWS
     C** CHARS 1-6 TO LEFTMOST 6 CHARS FROM MGTNUM
     C** CHARS 7-13 TO BLANKS
     C** CHARS 14-15 TO 'PS'
     C                     MOVE *BLANKS   WWTRAN
     C                     MOVELMGTNUM    WT0106
     C                     MOVE 'PS'      WT1415
     C                     CALL 'MG5900'               20
     C                     PARM MGMODI    @MODI
     C                     PARM WWTRAN    @TRAN
     C                     PARM 'PR'      @PROG
     C                     PARM *BLANKS   @MODE
     C                     PARM *ZEROS    MGLSWS
     C                     PARM *BLANKS   @ERCD
     C                     PARM *BLANKS   CSW003                          CSW003
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN20     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '005'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 005  *
     C                     MOVEL'MG5900'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM MG5900
     C           @ERCD     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     MOVE '006'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 006  *
     C                     MOVELDBERR     DBKEY            * * * * * * * *
     C                     MOVEL'MG5900'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** CLEAR MULTIPLE OCCURENCE DATA STRUCTURE OF MESSAGES
     C                     Z-ADD1         X       30
     C           X         OCUR MULT
     C*
     C           X         DOWLT500
     C                     MOVE *BLANKS   MULT
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C** SET INDEX TO SECOND OCCURENCE
     C                     Z-ADD2         X
     C           X         OCUR MULT
     C*
     C** CLEAR MULTIPLE OCCURENCE DATA STRUCTURE, WMULT
     C                     Z-ADD1         WWZ     30
     C           WWZ       OCUR WMULT
     C*
     C           WWZ       DOWLE500
     C           WMULT     ANDNE*BLANKS
     C                     MOVE *BLANKS   WMULT
     C                     ADD  1         WWZ
     C           WWZ       OCUR WMULT
     C                     END
     C*
     C** SET INDEX TO FIRST OCCURENCE
     C                     Z-ADD1         WWZ
     C           WWZ       OCUR WMULT
     C*
     C** CLEAR MULTIPLE OCCURENCE DS OF RECORDS TO PF/MGOREFPD
     C                     Z-ADD1         WWTOT1  20
     C           WWTOT1    OCUR REFDS
     C*
     C********** WWTOT1    DOWLE10
     C           WWTOT1    DOWLT10
     C           REFDS     ANDNE*BLANKS
     C                     MOVE *BLANKS   REFDS
     C                     ADD  1         WWTOT1
     C           WWTOT1    OCUR REFDS
     C                     END
     C*
     C** SET INDEX TO FIRST OCCURENCE
     C                     Z-ADD1         WWTOT1
     C           WWTOT1    OCUR REFDS
     C*
     C** ZEROIZE TOTAL NET PROFITS
     C                     Z-ADD*ZEROS    WWTNET 150
      *                                                                   183583
      ** Clear multiple occurrence DS for the extension file.             183583
      *                                                                   183583
     C                     Z-ADD1         V       20                      183583
     C           V         DOWLE95                                        183583
     C           MCSI      ANDNE*BLANKS                                   183583
     C           V         OCUR EXTN                                      183583
     C                     Z-ADD*ZEROS    MPSN                            183583
     C                     MOVE *BLANKS   MTG                             183583
     C                     MOVE *BLANKS   MCSI                            183583
     C                     ADD  1         V                               183583
     C                     ENDDO                                          183583
     C                     Z-ADD0         V                               183583
     C*
     C** SET FLAG FOR MESSAGE CORRECTLY GENERATED
     C                     MOVE *BLANKS   @ERR    1
     C                     MOVE *BLANKS   WWNGEN  1
     C                     MOVE *BLANKS   DSERR
     C                     MOVE *BLANKS   NWERR  50
     C*
     C** SET EXTENDED SETTLEMENT FIELDS PRESENCE
     C                     MOVE MGXSFL    WXSFLD
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  ROUT      - DETERMINE MESSAGE ROUTING                       *
     C**                                                              *
     C**  CALLS     - *PSSR, ZM1100                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESS                                    *
     C**                                                              *
     C*****************************************************************
     C*
     C           ROUT      BEGSR
     C*
     C** ACCESS SDBRCHL1 USING KEY OF MGSNBR FROM PF/MGF554L0
     C           MGSNBR    CHAINSDBRCHL1             13
     C           *IN13     IFEQ '1'
     C           A8BRCD    OREQ 'D'
     C           *LOCK     IN   LDA
     C                     MOVE '007'     DBASE            * * * * * * * *
     C                     MOVELMGSNBR    DBKEY            *  DBERR 007  *
     C                     MOVEL'SDBRCHL1'DBFILE           * * * * * * * *
     C                     MOVEL'MG0554'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** DETERMINE NETWORK ROUTING AND ADDRESSES
     C                     MOVE A8BICN    @SNDR   6
     C                     MOVE A8BTID    @SCSD  12
     C                     MOVE MGDEST    @DEST   6
     C                     MOVELMGSETP    @SETP   2
     C                     MOVE *BLANKS   @NWSN  20
     C                     MOVE *BLANKS   @NWDS  22
     C                     MOVE *BLANKS   @NWRK   6
     C                     MOVE *BLANKS   @ERRC   1
     C                     MOVE *BLANKS   @DRNM  20
     C                     MOVE *BLANKS   @DRTN  10
     C                     MOVE *BLANKS   @SRNM  20
     C                     MOVE *BLANKS   @SRTN  10
     C*
     C                     CALL 'ZM1100'               21
     C                     PARM           @SNDR
     C                     PARM           @SCSD
     C                     PARM           @DEST
     C                     PARM           @SETP
     C                     PARM           @NWSN
     C                     PARM           @NWDS
     C                     PARM           @NWRK
     C                     PARM           @ERRC
     C                     PARM           @SRNM
     C                     PARM           @SRTN
     C                     PARM           @DRNM
     C                     PARM           @DRTN
     C*
     C** IF PROGRAM ZM1100 NOT FOUND
     C           *IN21     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '008'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 008  *
     C                     MOVEL'ZM1100'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING CALLED PROGRAM
     C           @ERRC     IFEQ 'S'
     C           *LOCK     IN   LDA
     C                     MOVE '009'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 009  *
     C                     MOVELDBERR1    DBKEY            * * * * * * * *
     C                     MOVEL'ZM1100'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DESTINATION ERROR MESSAGE
     C           @ERRC     IFEQ 'D'
     C                     MOVE *BLANKS   DSERR  50
     C                     MOVELERRA,1    DSERR
     C                     MOVE 'Y'       @ERR
     C                     END
     C*
     C** FORMAT NETWORK ERROR MESSAGE
     C           @NWRK     IFEQ 'UNROUT'
     C                     MOVELERRA,16   NWERR
     C                     MOVE 'Y'       @ERR
     C                     END
     C*
     C** IF ERROR DETERMINING MESSAGE ROUTING, SET FLAG TO
     C** OUTPUT THIS PART OF MESSAGE TO MG0580P2
     C           @ERR      IFEQ 'Y'
     C                     MOVE 'Y'       WWNGEN
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  NRSEQ     - FORMAT FIELDS FOR NON-REPETITIVE SEQUENCE       *
     C**                                                              *
     C**  CALLS     - FLD23, FLD53A, FLD83A, FLD72N                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           NRSEQ     BEGSR
     C*
     C** INITIALIZE FLAG FOR ERROR ON NON-REPETITIVE SEQUENCE
     C                     MOVE *BLANKS   WWNRSQ
     C*
     C** FORMAT MANDATORY FIELD :23:
     C                     MOVE ':23: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD23
     C                     END
     C*
     C** FORMAT FIELD :53a:
     C                     MOVE ':53A:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           WXS53     OREQ 'Y'
     C                     EXSR FLD53A
     C                     END
     C*
     C** FORMAT FIELD :83a:
     C                     MOVE ':83A:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           WXS83     OREQ 'Y'
     C                     EXSR FLD83A
     C                     END
     C*
     C** FORMAT FIELD :72:
     C                     MOVE ':72: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           WXS72     OREQ 'Y'
     C                     EXSR FLD72N
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  RSEQ      - FORMAT FIELDS FOR REPETITIVE SEQUENCE           *
     C**                                                              *
     C**  CALLS     - FLD20, FLD21, FLD87A, FLD35A, FLD35B, FLD35,    *
     C**            - FLD35U, FLD31X, FLD31E, FLD31L, FLD31C, FLD80C, *
     C**            - FLD32G, FLD71C, FLD71B, FLD36, FLD34A, FLD72R   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           RSEQ      BEGSR
     C*
     C** FORMAT MANDATORY FIELD :20:
     C                     MOVE ':20: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD20
     C                     END
     C*
     C** FORMAT FIELD :21:
     C                     MOVE ':21: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD21
     C                     END
     C*
     C** FORMAT FIELD :87a:
     C                     MOVE ':87A:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           WXS87R    OREQ 'Y'
     C           WXS87D    OREQ 'Y'
     C                     EXSR FLD87A
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :35A:
     C                     MOVE ':35A:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD35A
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :35B:
     C                     MOVE ':35B:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD35B
     C                     END
     C*
     C** FORMAT FIELD :35a:
     C                     MOVE ':35D:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD35
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :35U:
     C                     MOVE ':35U:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD35U
     C                     END
     C*
     C** FORMAT FIELD :31X:
     C                     MOVE ':31X:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD31X
     C                     END
     C*
     C** FORMAT FIELD :31E:
     C                     MOVE ':31E:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD31E
     C                     END
     C*
     C** FORMAT FIELD :31L:
     C                     MOVE ':31L:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD31L
     C                     END
     C*
     C** FORMAT FIELD :31C:
     C                     MOVE ':31C:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD31C
     C                     END
     C*
     C** FORMAT FIELD :80C:
     C                     MOVE ':80C:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD80C
     C                     END
     C*
     C** FORMAT FIELD :32G:
     C                     MOVE ':32G:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD32G
     C                     END
     C*
     C** FORMAT FIELD :71C:
     C                     MOVE ':71C:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD71C
     C                     END
     C*
     C** FORMAT FIELD :71B:
     C                     MOVE ':71B:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD71B
     C                     END
     C*
     C** FORMAT FIELD :36:
     C                     MOVE ':36: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD36
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :34A:
     C                     MOVE ':34A:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD34A
     C                     END
     C*
     C** FORMAT FIELD :72: (REPETITIVE SEQUENCE)
     C                     MOVE ':72: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C           WXS72     OREQ 'Y'
     C                     EXSR FLD72R
     C                     END
     C*
     C** SET UP REFERENCE FILE INFORMATION FOR EACH PART OF MT554
     C                     EXSR WREF
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD19     - FORMAT MANDATORY FIELD :19:                     *
     C**                                                              *
     C**  CALLS     - ZM1140                                          *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD19     BEGSR
     C*
     C** SET INDEX TO MULTIPLE OCCURENCE DATA STRUCTURE
     C** TO FIRST OCCURENCE
     C                     Z-ADD1         X
     C           X         OCUR MULT
     C*
     C** FORMAT DESCRIPTION FIELD
     C                     MOVELDESA,1    MDES
     C*
     C** FORMAT TAG FIELD
     C***********          MOVEL':19:'    MTAG                            057263
     C                     MOVEL':19: '   MTAG                            057263
     C*
     C** CONVERT MIDAS AMOUNT TO SWIFT FORMAT
     C           CSW020    IFEQ 'Y'                                       CSW020
     C                     CALL 'ZM1240'               23                 CSW020
     C                     PARM WWTNET    ZAMT   130                      CSW020
     C                     PARM MGCYNP    ZCCY                            CSW020
     C                     PARM *BLANKS   ZSAMNT                          CSW020
     C                     PARM *BLANKS   ZOAMNT 21                       CSW020
     C                     PARM *BLANKS   ZSCCY   3                       CSW020
     C                     PARM *BLANKS   ZERR    1                       CSW020
     C*                                                                   CSW020
     C** IF PROGRAM NOT FOUND                                             CSW020
     C           *IN23     IFEQ '1'                                       CSW020
     C           *LOCK     IN   LDA                                       CSW020
     C                     MOVE '038'     DBASE            * * * * * * * *CSW020
     C                     MOVE *BLANKS   DBKEY            *  DBERR 038  *CSW020
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *CSW020
     C                     OUT  LDA                                       CSW020
     C                     EXSR *PSSR                                     CSW020
     C                     END                                            CSW020
     C                     ELSE                                           CSW020
     C                     CALL 'ZM1140'               23
     C                     PARM WWTNET    ZAMNT  150
     C                     PARM WWCDPN    ZCDP
     C                     PARM *BLANKS   ZSAMNT
     C                     PARM *BLANKS   ZOAMNT 21
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN23     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '012'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 012  *
     C                     MOVEL'ZM1140'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C                     ENDIF                                          CSW020
     C*
     C** FORMAT DATA FIELD
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVELZSAMNT    WD0117
     C                     MOVELZSAMNT    WWAMTX 17
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD FOR REPORT
     C                     MOVELZOAMNT    RDAT
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD23     - FORMAT MANDATORY FIELD :23:                     *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - NRSEQ                                           *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD23     BEGSR
     C*
     C** ERROR IF MGFID3 IS BLANK
     C           MGFID3    IFEQ *BLANKS
     C*
     C** FORMAT ERROR MESSAGE FIELD
     C                     MOVELERRA,3    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** UPDATE MULT OCCUR DATA STRUCTURE - WMULT
     C** FOR MESSAGES INCORRECTLY GENERATED REPORT
     C                     MOVELERRA,3    WMERR
     C                     MOVE 'Y'       WWNRSQ  1
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE - WMULT
     C                     ADD  1         WWZ
     C           WWZ       OCUR WMULT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD
     C                     MOVELDESA,2    MDES
     C*
     C** FORMAT TAG FIELD
     C***********          MOVEL':23:'    MTAG                            057263
     C                     MOVEL':23: '   MTAG                            057263
     C*
     C** UPDATE MULT OCCUR DATA STRUCTURE - WMULT
     C** FOR MESSAGES INCORRECTLY GENERATED REPORT
     C                     MOVELMTAG      WMTAG
     C*
     C** FORMAT DATA FIELD
     C                     MOVELMGFID3    MDAT
     C*
     C** UPDATE MULT OCCUR DATA STRUCTURE - WMULT
     C** FOR MESSAGES INCORRECTLY GENERATED REPORT
     C                     MOVELMDAT      WMDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE - WMULT
     C                     ADD  1         WWZ
     C           WWZ       OCUR WMULT
     C*
     C** FORMAT DATA FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD53A    - FORMAT FIELD :53a:                              *
     C**                                                              *
     C**  CALLS     - ZM1070, *PSSR, #ZA                              *
     C**                                                              *
     C**  CALLED BY - NRSEQ                                           *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD53A    BEGSR
     C*
     C** IF MGAFPN OR MGAFPL ARE NOT BLANK
     C           MGAFPN    IFNE *BLANKS
     C           MGAFPL    ORNE *BLANKS
     C*
     C** SET FLAG :53a: SUCCESSFULLY FORMATTED
     C                     MOVE 'Y'       WWFL53  1
     C*
     C** FORMAT DESCRIPTION FIELD
     C                     MOVELDESA,3    MDES
     C*
     C** FORMAT ADDRESS FIELDS
     C*
     C                     CALL 'ZM1070'               26
     C                     PARM MGAFPN    ZDEST  12
     C                     PARM MGAFPL    ZACLN
     C                     PARM *BLANKS   ZSADD1 35
     C                     PARM *BLANKS   ZSADD2 35
     C                     PARM *BLANKS   ZSADD3 35
     C                     PARM *BLANKS   ZSADD4 35
     C                     PARM *BLANKS   ZSTAG   1
     C                     PARM *BLANKS   ZSTYPE  1
     C                     PARM *BLANKS   ZERR    1
     C                     PARM *BLANKS   ZCRNM  20
     C                     PARM *BLANKS   ZCRTN  10
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN26     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '019'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 019  *
     C                     MOVEL'ZM1070'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING CALLED PROGRAM
     C           ZERR      IFEQ '1'
     C                     MOVELERRA,15   MERR
     C*
     C** UPDATE MULT OCCUR DATA STRUCTURE - WMULT
     C** FOR MESSAGES INCORRECTLY GENERATED REPORT
     C                     MOVELMERR      WMERR
     C                     MOVE 'Y'       WWNRSQ
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE - WMULT
     C                     ADD  1         WWZ
     C           WWZ       OCUR WMULT
     C*
     C                     MOVE 'Y'       @ERR
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     ELSE
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C*
     C** IF ZSTAG IS 'A'
     C                     MOVE *BLANKS   SAVMTG  5                       183583
     C           ZSTAG     IFEQ 'A'
     C                     MOVEL':53A:'   MTAG
     C                     MOVE MTAG      SAVMTG                          183583
     C                     END
     C*
     C** IF ZSTAG IS 'C'
     C           ZSTAG     IFEQ 'C'
     C                     MOVEL':53C:'   MTAG
     C                     END
     C*
     C** IF ZSTAG IS 'D'
     C           ZSTAG     IFEQ 'D'
     C                     MOVEL':53D:'   MTAG
     C                     END
     C*
     C** IF MGAFPL IS NOT BLANK
     C           MGAFPL    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MDAT
     C                     MOVELMGAFPL    MDAT
     C                     MOVELMDAT      RDAT
     C* FORMAT REPORT NAME AND TOWN IF SWIFT ID
     C           ZSTAG     IFEQ 'A'
     C                     MOVELZCRNM     CRNM
     C                     MOVELZCRTN     CRTN
     C                     END
     C*
     C** UPDATE MULT OCCUR DATA STRUCTURE - WMULT
     C** FOR MESSAGES INCORRECTLY GENERATED REPORT
     C                     MOVELMTAG      WMTAG
     C                     MOVELMDAT      WMDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE - WMULT
     C                     ADD  1         WWZ
     C           WWZ       OCUR WMULT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     EXSR #ZA
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD83A    - FORMAT FIELD :83a:                              *
     C**                                                              *
     C**  CALLS     - ZM1070, *PSSR, #ZA                              *
     C**                                                              *
     C**  CALLED BY - NRSEQ                                           *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD83A    BEGSR
     C*
     C** IF MGSKAN OR MGSKAL ARE NOT BLANK
     C           MGSKAN    IFNE *BLANKS
     C           MGSKAL    ORNE *BLANKS
     C*
     C** SET FLAG FIELD :83a: SUCCESSFULLY FORMATTED
     C                     MOVE 'Y'       WWFL83  1
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,4    MDES
     C*
     C** FORMAT ADDRESS FIELDS
     C*
     C                     CALL 'ZM1070'               26
     C                     PARM MGSKAN    ZDEST
     C                     PARM MGSKAL    ZACLN  35
     C                     PARM *BLANKS   ZSADD1
     C                     PARM *BLANKS   ZSADD2
     C                     PARM *BLANKS   ZSADD3
     C                     PARM *BLANKS   ZSADD4
     C                     PARM *BLANKS   ZSTAG
     C                     PARM *BLANKS   ZSTYPE
     C                     PARM *BLANKS   ZERR
     C                     PARM *BLANKS   ZCRNM  20
     C                     PARM *BLANKS   ZCRTN  10
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN26     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '016'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 016  *
     C                     MOVEL'ZM1070'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING CALLED PROGRAM
     C           ZERR      IFEQ '1'
     C                     MOVELERRA,8    MERR
     C                     MOVE 'Y'       @ERR
     C*
     C** UPDATE MULT OCCUR DATA STRUCTURE - WMULT
     C** FOR MESSAGES INCORRECTLY GENERATED REPORT
     C                     MOVELMERR      WMERR
     C                     MOVE 'Y'       WWNRSQ
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE - WMULT
     C                     ADD  1         WWZ
     C           WWZ       OCUR WMULT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C*
     C** IF ZSTAG IS 'A'
     C                     MOVE *BLANKS   SAVMTG                          183583
     C           ZSTAG     IFEQ 'A'
     C                     MOVEL':83A:'   MTAG
     C                     MOVE MTAG      SAVMTG                          183583
     C                     END
     C*
     C** IF ZSTAG IS 'C'
     C           ZSTAG     IFEQ 'C'
     C                     MOVEL':83C:'   MTAG
     C                     END
     C*
     C** IF ZSTAG IS 'D'
     C           ZSTAG     IFEQ 'D'
     C                     MOVEL':83D:'   MTAG
     C                     END
     C*
     C** IF MGSKAL IS NOT BLANK
     C           MGSKAL    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGSKAL    MDAT
     C                     MOVELMDAT      RDAT
     C* FORMAT REPORT NAME AND TOWN IF SWIFT ID
     C           ZSTAG     IFEQ 'A'
     C                     MOVELZCRNM     CRNM
     C                     MOVELZCRTN     CRTN
     C                     END
     C*
     C** UPDATE MULT OCCUR DATA STRUCTURE - WMULT
     C** FOR MESSAGES INCORRECTLY GENERATED REPORT
     C                     MOVELMTAG      WMTAG
     C                     MOVELMDAT      WMDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE - WMULT
     C                     ADD  1         WWZ
     C           WWZ       OCUR WMULT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     EXSR #ZA
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD72N    - FORMAT FIELD :72: (NON-REPETITIVE SEQUENCE)     *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - NRSEQ                                           *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD72N    BEGSR
     C*
     C** NOT REQUIRED
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD20     - FORMAT MANDATORY FIELD :20:                     *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD20     BEGSR
     C*
     C** STORE POSITION ON MULT OCCURENCE DATA STRUCTURE INCASE
     C** THERE IS AN ERROR IN FORMATTING THIS MESSAGE - OUTPUT THIS
     C** PART OF MESSAGE TO MG0554P2
     C                     Z-ADDX         WWX1    30
     C*
     C** IF LEFTMOST SIX CHARS OF MGTNUM ARE BLANK THEN ERROR
     C                     MOVELMGTNUM    WWFL06
     C           WWFL06    IFEQ *BLANKS
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,2    MERR
     C                     MOVE 'Y'       @ERR
     C                     MOVE 'Y'       WWNGEN
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,5    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C***********          MOVEL':20:'    MTAG                            057263
     C                     MOVEL':20: '   MTAG                            057263
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVEL'SEC'     WD0103
     C*
     C                     MOVELMGTNUM    WD0409
     C*
     C           MGLSWS    IFEQ 9999999
     C                     Z-ADD1         WWLSWS  70
     C                     ELSE
     C*
     C           MGLSWS    ADD  1         WWLSWS
     C                     END
     C                     MOVELWWLSWS    WD1016
     C                     MOVELWWMDAT    MDAT
     C                     MOVELMDAT      RDAT
     C                     MOVELWWMDAT    WWDATA 16
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD21     - FORMAT FIELD :21:                               *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD21     BEGSR
     C*
     C** IF LEFTMOST TEN CHARS OF MGTNUM ARE BLANK THEN ERROR
     C                     MOVELMGTNUM    WWFL06
     C           WWFL06    IFEQ *BLANKS
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,13   MERR
     C                     MOVE 'Y'       @ERR
     C                     MOVE 'Y'       WWNGEN
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,6    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C***********          MOVEL':21:'    MTAG                            057263
     C                     MOVEL':21: '   MTAG                            057263
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C*
     C           MGLSWS    IFEQ 0
     C                     MOVEL'NEW'     WD0103
     C                     ELSE
     C                     MOVE 'SEC'     WD0103
     C                     MOVELMGTNUM    WD0409
     C                     MOVELMGLSWS    WD1016
     C                     END
     C*
     C                     MOVELWWMDAT    MDAT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD87A    - FORMAT FIELD :87a:                              *
     C**                                                              *
     C**  CALLS     - ZM1150, *PSSR, #ZA                              *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD87A    BEGSR
     C*
     C** FORMAT FIELD IF NOT BLANK
     C*
     C           MGRSSN    IFNE *BLANKS
     C           MGRSS1    ORNE *BLANKS
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,7    MDES
     C*
     C** FORMAT ADDRESS FIELDS
     C                     CALL 'ZM1150'               25
     C                     PARM MGRSSN    ZDEST
     C                     PARM *BLANKS   ZSADD1
     C                     PARM *BLANKS   ZSADD2
     C                     PARM *BLANKS   ZSADD3
     C                     PARM *BLANKS   ZSADD4
     C                     PARM *BLANKS   ZSTAG
     C                     PARM *BLANKS   ZSTYPE
     C                     PARM *BLANKS   ZERR
     C                     PARM *BLANKS   ZCRNM  20
     C                     PARM *BLANKS   ZCRTN  10
     C                     PARM           ZDBEI   1                                           CSW201
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN25     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '017'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 017  *
     C                     MOVEL'ZM1150'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING CALLED PROGRAM
     C           ZERR      IFEQ '1'
     C                     MOVELERRA,9    MERR
     C                     MOVE 'Y'       @ERR
     C                     MOVE 'Y'       WWNGEN
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C*
     C** IF ZSTAG IS 'A'
     C                     MOVE *BLANKS   SAVMTG                          183583
     C           ZSTAG     IFEQ 'A'
     C                     MOVEL':87A:'   MTAG
     C                     MOVE MTAG      SAVMTG                          183583
     C                     ELSE
     C** IF ZSTAG IS 'D'
     C                     MOVEL':87D:'   MTAG
     C                     END
     C*
     C** IF MGRSS1 IS NOT BLANK
     C           MGRSS1    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELMGRSS1    MDAT
     C                     MOVELMDAT      RDAT
     C*
     C* FORMAT REPORT NAME AND TOWN IF SWIFT ID
     C           ZSTAG     IFEQ 'A'
     C                     MOVELZCRNM     CRNM
     C                     MOVELZCRTN     CRTN
     C                     END
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** MOVE BLANKS TO TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MTAG
     C*
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     EXSR #ZA
     C*
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD35A    - FORMAT MANDATORY FIELD :35A:                    *
     C**                                                              *
     C**  CALLS     - ZM1140, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD35A    BEGSR
     C*
     C** IF MGQANT EQUALS ZERO OR MGSWTP IS BLANK THEN ERROR
     C           MGQANT    IFEQ 0
     C           MGSWTP    OREQ *BLANKS
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,5    MERR
     C                     MOVE 'Y'       @ERR
     C                     MOVE 'Y'       WWNGEN
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,8    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':35A:'   MTAG
     C*
     C** CONVERT MIDAS AMOUNT TO SWIFT FORMAT
     C                     CALL 'ZM1140'               23
     C                     PARM MGQANT    ZAMNT
     C***********          PARM 0         ZCDP                            078256
     C                     PARM MGNMDP    ZCDP                            078256
     C                     PARM *BLANKS   ZSAMNT
     C                     PARM *BLANKS   ZOAMNT
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN23     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '021'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 021  *
     C                     MOVEL'ZM1140'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVELMGSWTP    WD0103
     C                     MOVELZSAMNT    WD0419
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVELMGSWTP    WR0103
     C                     MOVELZOAMNT    WR0424
     C                     MOVELWWRDAT    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD35B    - FORMAT MANDATORY FIELD :35B:                    *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD35B    BEGSR
     C*
     C** IF MGSFN1 IS BLANK THEN ERROR
     C           MGSFN1    IFEQ *BLANKS
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,6    MERR
     C                     MOVE 'Y'       @ERR
     C                     MOVE 'Y'       WWNGEN
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ELSE
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,9    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':35B:'   MTAG
     C*
     C** IF MGISIN IS NOT BLANK
     C           MGISIN    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C*
     C** CHECK ISIN OR COMMON CODE
     C                     MOVE MGISIN    WWISIN  1
     C           WWISIN    IFNE *BLANK
     C                     MOVEL'ISIN '   WD0105           ISIN code
     C                     MOVELMGISIN    WD0617
     C                     ELSE
     C                     MOVEL'/XS/'    WD0104           COMMON code
     C                     MOVELMGISIN    WD0516
     C                     END
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     END
     C*
     C** REMOVE FORBIDDEN CHARACTERS (E.G. %)
     C                     MOVEAMGSFN1    FORB
     C                     Z-ADD1         J       20
     C           *IN85     DOUEQ'0'                                       056532
     C           '%'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN1
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '&'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN1
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '/'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN1
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '$'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN1
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J                               056532
     C           FORB,J    DOWEQ'-'                                       056532
     C           FORB,J    OREQ ' '                                       056532
     C           FORB,J    OREQ ':'                                       056532
     C                     ADD  1         J                               056532
     C                     MOVE *BLANKS   MGSFN1                          056532
     C                     MOVEAFORB,J    MGSFN1                          056532
     C           J         IFEQ 50                                        056532
     C           FORB,50   IFEQ '-'                                       056532
     C           FORB,50   OREQ ':'                                       056532
     C                     MOVE *BLANKS   MGSFN1                          056532
     C                     ENDIF                                          056532
     C                     LEAVE                                          056532
     C                     ENDIF                                          056532
     C                     ENDDO                                          056532
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MDAT
     C                     MOVELMGSFN1    MDAT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** IF MGSFN2 IS NOT BLANK
     C           MGSFN2    IFNE *BLANKS
     C*
     C** REMOVE FORBIDDEN CHARACTERS (E.G. %)
     C                     MOVEAMGSFN2    FORB
     C                     Z-ADD1         J       20
     C           *IN85     DOUEQ'0'                                       056532
     C           '%'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN2
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '&'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN2
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '/'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN2
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J
     C           *IN85     DOUEQ'0'                                       056532
     C           '$'       LOKUPFORB,J                   85
     C           *IN85     IFEQ '1'
     C                     MOVE ' '       FORB,J
     C                     MOVEAFORB      MGSFN2
     C                     END
     C                     ENDDO                                          056532
     C                     Z-ADD1         J                               056532
     C           FORB,J    DOWEQ'-'                                       056532
     C           FORB,J    OREQ ' '                                       056532
     C           FORB,J    OREQ ':'                                       056532
     C                     ADD  1         J                               056532
     C                     MOVE *BLANKS   MGSFN2                          056532
     C                     MOVEAFORB,J    MGSFN2                          056532
     C           J         IFEQ 50                                        056532
     C           FORB,50   IFEQ '-'                                       056532
     C           FORB,50   OREQ ':'                                       056532
     C                     MOVE *BLANKS   MGSFN2                          056532
     C                     ENDIF                                          056532
     C                     LEAVE                                          056532
     C                     ENDIF                                          056532
     C                     ENDDO                                          056532
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MDAT
     C                     MOVELMGSFN2    MDAT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD35     - FORMAT MANDATORY FIELD :35a:                    *
     C**                                                              *
     C**  CALLS     - ZM0060, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD35     BEGSR
     C*
     C** PROCESS ONLY IF NEXT COUPON DATE IS NON-ZERO
     C           MGNCDT    IFNE 0
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,12   MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':35D:'   MTAG
     C*
     C** CONVERT TRADE DATE TO SWIFT FORMAT
     C                     CALL 'ZM0060'               22
     C                     PARM MGNCDT    ZMDAY
     C                     PARM *BLANKS   ZSDATE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN22     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '010'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 010  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVELZSDATE    WD0106
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVELWWYEAR    WRYEAR
     C                     MOVELWWMNTH    WRMNTH
     C                     MOVELWWDAY     WRDAY
     C                     MOVELWRDATE    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD35U    - FORMAT MANDATORY FIELD :35U:                    *
     C**                                                              *
     C**  CALLS     - ZM1190, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD35U    BEGSR
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,10   MDES
     C*
     C           MGDVIR    IFEQ 'I'
     C*
     C** CONVERT INTEREST RATE TO SWIFT FORMAT
     C                     Z-ADDMGIRAT    WWIRAT 116H
     C                     CALL 'ZM1190'               28
     C                     PARM WWIRAT    ZPRIC
     C                     PARM *BLANKS   ZSPRIC
     C                     PARM *BLANKS   ZOPRIC 21
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN28     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '022'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 022  *
     C                     MOVEL'ZM1190'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVEL'PCT'     WD0103
     C                     MOVELZSPRIC    WD0419
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVEL'PCT'     WR0103
     C                     MOVELZOPRIC    WR0424
     C                     MOVELWWRDAT    RDAT
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':35U:'   MTAG
     C*
     C                     ELSE
     C*
     C           MGDVIR    IFEQ 'D'
     C*
     C** ACCESS SDCURRL1 FOR NO. OF DECIMAL PLACES FOR NOMINAL CURRENCY
     C           MGNMCY    CHAINSDCURRL1             14
     C           *IN14     IFEQ '1'
     C           A6TYLC    OREQ 'D'
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,11   MERR
     C                     MOVE 'Y'       @ERR
     C                     MOVE 'Y'       WWNGEN
     C                     ELSE
     C*
     C** CONVERT DIVIDEND AMOUNT TO SWIFT FORMAT WITH MGDIVD ROUNDED
     C** TO A6NBDP, LF/SDCURRL1 DECIMAL PLACES
     C*
     C           A6NBDP    IFEQ 0
     C                     Z-ADDMGDIVD    WWDIV0 130H
     C                     Z-ADDWWDIV0    ZPRIC
     C                     END
     C*
     C           A6NBDP    IFEQ 1
     C                     Z-ADDMGDIVD    WWDIV1 131H
     C                     Z-ADDWWDIV1    ZPRIC
     C                     END
     C*
     C           A6NBDP    IFEQ 2
     C                     Z-ADDMGDIVD    WWDIV2 132H
     C                     Z-ADDWWDIV2    ZPRIC
     C                     ELSE
     C                     Z-ADDMGDIVD    WWDIV3 133H
     C                     Z-ADDWWDIV3    ZPRIC
     C                     END
     C*
     C                     CALL 'ZM1190'               28
     C                     PARM           ZPRIC
     C                     PARM *BLANKS   ZSPRIC
     C                     PARM *BLANKS   ZOPRIC
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN28     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '024'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 024  *
     C                     MOVEL'ZM1190'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVELA6SWCY    WD0103
     C                     MOVELZSPRIC    WD0419
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVELA6SWCY    WR0103
     C                     MOVELZOPRIC    WR0424
     C                     MOVELWWRDAT    RDAT
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':35U:'   MTAG
     C                     END
     C*
     C** ELSE MGDVIR IS NOT 'I' OR 'D', FORMAT ERROR FIELD
     C                     ELSE
     C                     MOVELERRA,14   MERR
     C                     MOVE 'Y'       @ERR
     C                     MOVE 'Y'       WWNGEN
     C                     END
     C                     END
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD31X    - FORMAT FIELD :31X:                              *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD31X    BEGSR
     C*
     C** NOT REQUIRED
     C*
     C/COPY WNCPYSRC,MG0554C008
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD31E    - FORMAT FIELD :31E:                              *
     C**                                                              *
     C**  CALLS     - ZM0060, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD31E    BEGSR
     C*
     C** PROCESS ONLY IF COUPON/DIVIDEND VALUE DATE IS NON-ZERO
     C           MGCDDT    IFNE 0
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':31E:'   MTAG
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,13   MDES
     C*
     C** CONVERT COUPON/DIVIDEND DATE  TO SWIFT FORMAT
     C                     CALL 'ZM0060'               22
     C                     PARM MGCDDT    ZMDAY   50
     C                     PARM *BLANKS   ZSDATE  6
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN22     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '025'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 025  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELZSDATE    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVELWWYEAR    WRYEAR
     C                     MOVELWWMNTH    WRMNTH
     C                     MOVELWWDAY     WRDAY
     C                     MOVELWRDATE    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD31L    - FORMAT FIELD :31L:                              *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD31L    BEGSR
     C*
     C** COUPON CLIPPING DATE FIELD NOT REQUIRED
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD31C    - FORMAT FIELD :31C:                              *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD31C    BEGSR
     C*
     C** PAYABLE DATE FIELD NOT REQUIRED
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD80C    - FORMAT FIELD :80C:                              *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD80C    BEGSR
     C*
     C** COMPONENTS OF INCOME FIELD NOT REQUIRED
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD32G    - FORMAT FIELD :32G:                              *
     C**                                                              *
     C**  CALLS     - ZM1240, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD32G    BEGSR
     C*
     C** PROCESS ONLY IF GROSS AMOUNT IS NOT ZERO
     C           MGGAMT    IFNE 0
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,14   MDES
     C*
     C** CONVERT MIDAS AMOUNT TO SWIFT FORMAT
     C                     CALL 'ZM1240'               29
     C                     PARM MGGAMT    ZAMT
     C                     PARM MGNMCY    ZCCY    3
     C                     PARM *BLANKS   ZSAMNT 17
     C                     PARM *BLANKS   ZOAMNT 21
     C                     PARM *BLANKS   ZSCCY   3
     C                     PARM *BLANKS   ZERR
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN29     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '023'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 023  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM ZM1240
     C           ZERR      IFEQ '1'
     C                     MOVELERRA,4    MERR
     C                     MOVE 'Y'       @ERR
     C                     MOVE 'Y'       WWNGEN
     C                     ELSE
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVELZSCCY     WD0103
     C                     MOVELZSAMNT    WD0419
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVELZSCCY     WR0103
     C                     MOVELZOAMNT    WR0424
     C                     MOVELWWRDAT    RDAT
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':32G:'   MTAG
     C*
     C                     END
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD71C   - FORMAT FIELD :71C:                               *
     C**                                                              *
     C**  CALLS     - ZM1240, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD71C    BEGSR
     C*
     C** FORMAT ONLY IF NOT ZERO
     C           MGCOMM    IFNE *ZEROS
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,15   MDES
     C*
     C** CONVERT MIDAS AMOUNT TO SWIFT FORMAT
     C                     CALL 'ZM1240'               29
     C                     PARM MGCOMM    ZAMT
     C                     PARM MGNMCY    ZCCY
     C                     PARM *BLANKS   ZSAMNT
     C                     PARM *BLANKS   ZOAMNT
     C                     PARM *BLANKS   ZSCCY
     C                     PARM *BLANKS   ZERR
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN29     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '026'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 026  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM ZM1240
     C           ZERR      IFEQ '1'
     C                     MOVELERRA,7    MERR
     C                     MOVE 'Y'       @ERR
     C                     MOVE 'Y'       WWNGEN
     C                     ELSE
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVEL'/COMM/'  WD0106
     C                     MOVELZSCCY     WD0709
     C                     MOVELZSAMNT    WD1026
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVEL'/COMM/'  WR0106
     C                     MOVELZSCCY     WR0709
     C                     MOVELZOAMNT    WR1030
     C                     MOVELWWRDAT    RDAT
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':71C:'   MTAG
     C*
     C                     END
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD71B    - FORMAT FIELD :71B:                              *
     C**                                                              *
     C**  CALLS     - ZM1240, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD71B    BEGSR
     C*
     C** FORMAT ONLY IF NOT ZERO
     C           MGCHGS    IFNE *ZEROS
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,16   MDES
     C*
     C** CONVERT MIDAS AMOUNT TO SWIFT FORMAT
     C                     CALL 'ZM1240'               29
     C                     PARM MGCHGS    ZAMT   130
     C                     PARM MGNMCY    ZCCY
     C                     PARM *BLANKS   ZSAMNT
     C                     PARM *BLANKS   ZOAMNT
     C                     PARM *BLANKS   ZSCCY
     C                     PARM *BLANKS   ZERR
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN29     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '027'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 027  *
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM ZM1240
     C           ZERR      IFEQ '1'
     C                     MOVELERRA,10   MERR
     C                     MOVE 'Y'       @ERR
     C                     MOVE 'Y'       WWNGEN
     C                     ELSE
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVEL'/CHGS/'  WD0106
     C                     MOVELZSCCY     WD0709
     C                     MOVELZSAMNT    WD1026
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVEL'/CHGS/'  WR0106
     C                     MOVELZSCCY     WR0709
     C                     MOVELZOAMNT    WR1030
     C                     MOVELWWRDAT    RDAT
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':71B:'   MTAG
     C                     END
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD36     - FORMAT MANDATORY FIELD :36:                     *
     C**                                                              *
     C**  CALLS     - ZM1190, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD36     BEGSR
     C*
     C** PROCESS ONLY IF EXCHANGE RATE IS NOT ZERO
     C           MGPSXR    IFNE 0
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,17   MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C***********          MOVEL':36:'    MTAG                            057263
     C                     MOVEL':36: '   MTAG                            057263
     C*
     C** CONVERT EXCHANGE RATE TO SWIFT FORMAT
     C                     CALL 'ZM1190'               28
     C                     PARM MGPSXR    ZPRIC  158
     C                     PARM *BLANKS   ZSPRIC 16
     C                     PARM *BLANKS   ZOPRIC 21
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN28     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '028'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 028  *
     C                     MOVEL'ZM1190'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELZSPRIC    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVELZOPRIC    RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD34A    - FORMAT MANDATORY FIELD :34A:                    *
     C**                                                              *
     C**  CALLS     - ZM0060, ZM1140, *PSSR                           *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD34A    BEGSR
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,18   MDES
     C*
     C** CONVERT COUPON/DIVIDEND DATE TO SWIFT FORMAT
     C                     CALL 'ZM0060'               22
     C                     PARM MGCDDT    ZMDAY
     C                     PARM *BLANKS   ZSDATE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN22     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '013'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 013  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** ACCESS SDCURRL1 FOR NO. OF DECIMAL PLACES FOR NET PROCEEDS
     C           MGCYNP    CHAINSDCURRL1             14
     C           *IN14     IFEQ '1'
     C           A6TYLC    OREQ 'D'
     C*
     C** FORMAT ERROR MESSAGE FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELERRA,12   MERR
     C                     MOVE 'Y'       @ERR
     C                     MOVE 'Y'       WWNGEN
     C                     ELSE
     C*
     C*                                                                   CSW020
     C* Check if change required for swift decimal places                 CSW020
     C           CSW020    IFEQ 'Y'                                       CSW020
     C                     MOVE ' '       WCHGRQ  1                       CSW020
     C*                                                                   CSW020
     C           A6SWDP    IFNE A6NBDP                                    CSW020
     C           A6SWDT    ANDLERDNB                                      CSW020
     C*                                                                   CSW020
     C           A6SWDP    IFLT A6NBDP                                    CSW020
     C           A6NBDP    SUB  A6SWDP    WDIFF   10                      CSW020
     C* Set Change required field to Yes                                  CSW020
     C                     MOVE 'Y'       WCHGRQ                          CSW020
     C                     ELSE                                           CSW020
     C           A6SWDP    SUB  A6NBDP    WDIFF                           CSW020
     C                     ENDIF                                          CSW020
     C*                                                                   CSW020
     C           WDIFF     IFEQ 1                                         CSW020
     C                     Z-ADD10        WPOWER  40                      CSW020
     C                     ENDIF                                          CSW020
     C           WDIFF     IFEQ 2                                         CSW020
     C                     Z-ADD100       WPOWER                          CSW020
     C                     ENDIF                                          CSW020
     C           WDIFF     IFEQ 3                                         CSW020
     C                     Z-ADD1000      WPOWER                          CSW020
     C                     ENDIF                                          CSW020
     C*                                                                   CSW020
     C                     ENDIF                                          CSW020
     C                     ENDIF                                          CSW020
      *                                                                   CSW020
     C** ADD SETTLEMENT AMOUNT TO TOTAL NET PROCEEDS FIELD                CSW020
     C           WCHGRQ    IFEQ 'Y'                                       CSW020
     C           MGSTAM    DIV  WPOWER    NWSTAM 150                      CSW020
     C                     MULT WPOWER    NWSTAM                          CSW020
     C                     ADD  NWSTAM    WWTNET                          CSW020
     C                     ELSE                                           CSW020
     C                     ADD  MGSTAM    WWTNET                          CSW020
     C                     ENDIF                                          CSW020
     C*                                                                   CSW020
     C*
     C***STORE*NO.*OF*DECIMAL PLACES FOR CURRENCY FIELD                   CSW020
     C*********************Z-ADDA6NBDP    WWCDPN                          CSW020
     C*********************                                               CSW020
     C***ADD*SETTLEMENT*AMOUNT TO TOTAL NET PROCEEDS FIELD                CSW020
     C*********************ADD  MGSTAM    WWTNET                          CSW020
     C*
     C** CONVERT MIDAS AMOUNT TO SWIFT FORMAT
     C           CSW020    IFEQ 'Y'                                       CSW020
     C                     CALL 'ZM1240'               23                 CSW020
     C                     PARM MGSTAM    ZAMT                            CSW020
     C                     PARM MGCYNP    ZCCY                            CSW020
     C                     PARM *BLANKS   ZSAMNT                          CSW020
     C                     PARM *BLANKS   ZOAMNT 21                       CSW020
     C                     PARM *BLANKS   ZSCCY   3                       CSW020
     C                     PARM *BLANKS   ZERR    1                       CSW020
     C*                                                                   CSW020
     C** IF PROGRAM NOT FOUND                                             CSW020
     C           *IN23     IFEQ '1'                                       CSW020
     C           *LOCK     IN   LDA                                       CSW020
     C                     MOVE '037'     DBASE            * * * * * * * *CSW020
     C                     MOVE *BLANKS   DBKEY            *  DBERR 037  *CSW020
     C                     MOVEL'ZM1240'  DBPGM            * * * * * * * *CSW020
     C                     OUT  LDA                                       CSW020
     C                     EXSR *PSSR                                     CSW020
     C                     END                                            CSW020
     C*                                                                   CSW020
     C                     ELSE                                           CSW020
     C                     CALL 'ZM1140'               23
     C                     PARM MGSTAM    ZAMNT
     C                     PARM A6NBDP    ZCDP    10
     C                     PARM *BLANKS   ZSAMNT
     C                     PARM *BLANKS   ZOAMNT 21
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN23     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '029'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 029  *
     C                     MOVEL'ZM1140'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** STORE NO. OF DECIMAL PLACES FOR CURRENCY FIELD                   CSW020
     C                     Z-ADDA6NBDP    WWCDPN                          CSW020
     C                     ENDIF                                          CSW020
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   WWMDAT
     C                     MOVELZSDATE    WD0106
     C                     MOVELA6SWCY    WD0709
     C                     MOVELZSAMNT    WD1025
     C                     MOVELZSAMNT    WWSAMT 17
     C                     MOVELWWMDAT    MDAT
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE FOR REPORT
     C                     MOVE *BLANKS   WWRDAT
     C                     MOVELWWYEAR    WRYEAR
     C                     MOVELWWMNTH    WRMNTH
     C                     MOVELWWDAY     WRDAY
     C                     MOVELWRDATE    WR0108
     C                     MOVELA6SWCY    WR0911
     C                     MOVELZOAMNT    WR1232
     C                     MOVELWWRDAT    RDAT
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':34A:'   MTAG
     C                     END
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     Z-ADDX         Y       30
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD72R    - FORMAT FIELD :72: (REPETITIVE SEQUENCE)         *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD72R    BEGSR
     C*
     C** PROCESS ONLY IF SENDER TO RECEIVER INFORMATION IS NOT BLANK
     C           MGSRL1    IFNE *BLANKS
     C           MGSRL2    ORNE *BLANKS
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,11   MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C***********          MOVEL':72:'    MTAG                            057263
     C                     MOVEL':72: '   MTAG                            057263
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MDAT
     C                     MOVELMGSRL1    MDAT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     Z-ADDX         Y
     C*
     C** IF MGSRL2 IS NOT BLANK
     C           MGSRL2    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVE *BLANKS   MDAT
     C                     MOVELMGSRL2    MDAT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     Z-ADDX         Y
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  NGEN      - FORMAT MESSAGE FOR MG0554P2 REPORT              *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           NGEN      BEGSR
     C*
     C** STORE LATEST OCCURENCE OF MULT DATA STRUCTURE AND RETURN TO
     C** THE START OF THIS PART OF MESSAGE
     C                     Z-ADDX         WWX2    30
     C                     Z-ADDWWX1      X
     C           X         OCUR MULT
     C*
     C** SET UP DATA STRUCTURE FOR INCORRECTLY GENERATED MESSAGES
     C** AND CLEAR THESE DETAILS FROM DATA STRUCTURE FOR CORRECTLY
     C** GENERATED MESSAGES
     C           X         DOUEQWWX2
     C                     MOVELMDAT      WMDAT
     C                     MOVELMTAG      WMTAG
     C                     MOVELMERR      WMERR
     C                     MOVE *BLANKS   MULT
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     ADD  1         WWZ
     C           WWZ       OCUR WMULT
     C*
     C                     END
     C*
     C                     Z-ADDWWZ       WWY     30
     C*
     C** RETURN TO OCCURENCE BEFORE THE ERROR
     C                     Z-ADDWWX1      X
     C           X         OCUR MULT
     C*
     C** REMOVE REFERENCE FROM REFDS AND TRANS TO AVOID OUTPUT OF INCORRECT
     C** MESSAGE TO THE FILES
     C           WWTOT1    OCUR TRANS
     C           WWTOT1    OCUR REFDS
     C                     MOVEL*BLANKS   TRANS
     C                     MOVEL*BLANKS   REFDS
     C                     SUB  1         WWTOT1
      * CSW020: If the swift number decimal places is less than the       CSW020
      *         normal number of decimal place, zeroise the extra decimalsCSW020
      *         when reducing the sum of nets proceds(WWTNET).            CSW020
     C           WCHGRQ    IFEQ 'Y'                                       CSW020
     C           MGSTAM    DIV  WPOWER    NWSTAM 150                      CSW020
     C                     MULT WPOWER    NWSTAM                          CSW020
     C                     SUB  NWSTAM    WWTNET                          CSW020
     C                     ELSE                                           CSW020
     C                     SUB  MGSTAM    WWTNET
     C                     ENDIF                                          CSW020
     C*
     C** RESET MESSAGE NOT GENERATED CORRECTLY FLAG ONLY IF
     C** THE ERROR WAS FOUND IN THE REPETITIVE SEQUENCE
     C           WWNRSQ    IFNE 'Y'
     C                     MOVE *BLANKS   WWNGEN
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  WREF      - FORMAT FIELDS FOR PF/MGOREFPD DATA STRUCTURE    *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - RSEQ                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           WREF      BEGSR
     C*
     C           WWTOT1    OCUR REFDS
     C           WWTOT1    OCUR TRANS
     C                     MOVELMGMODI    WWMOD
     C                     MOVELWWDATA    WWTRNO
     C                     MOVELMGTNUM    WWMTRN
     C                     MOVELMGDEST    WWDECN
     C                     MOVELMGSNBR    WWBRCA
     C                     MOVELWWSAMT    WWAMTS
     C                     MOVELWWDATA    WWTRNM
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  OFLDS     - FORMAT FIELDS FOR MIDAS SWIFT DATABASE FILES    *
     C**                                                              *
     C**  CALLS     - ZM0060, *PSSR, #W04                             *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           OFLDS     BEGSR
     C*
     C** INITIALISE FIELDS ON PF/MGOREFPD
     C                     CLEARMGOREFD0
     C*
     C** HISTORY RETENTION DATE
     C           BJRDNB    ADD  ENDSMN    WWHDAT  50
     C                     MOVE WWHDAT    ZMDAY   50
     C                     MOVE *BLANKS   ZSDATE  6
     C*
     C                     CALL 'ZM0060'               22
     C                     PARM           ZMDAY
     C                     PARM           ZSDATE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN22     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '031'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 031  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** SET HISTORY RETENTION DATE TO OUTPUT FIELD
     C                     MOVELZSDATE    HRDT
     C*
     C** MESSAGE GENERATION DATE
     C                     MOVE BJRDNB    ZMDAY
     C                     MOVE *BLANKS   ZSDATE
     C*
     C                     CALL 'ZM0060'               22
     C                     PARM           ZMDAY
     C                     PARM           ZSDATE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN22     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '032'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 032  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** SET MESSAGE GEN. DATE TO OUTPUT FIELD
     C                     MOVELZSDATE    MGDE
     C*
      *                                                                   113352
      ** Set-up Message Generation  Century flag                          113352
      *                                                                   113352
     C                     MOVELMGDE      YY      20                      113352
     C           YY        IFLT 72                                        113352
     C                     MOVE '2'       CIND                            113352
     C                     ELSE                                           113352
     C                     MOVE '1'       CIND                            113352
     C                     ENDIF                                          113352
     C** SET TOTAL SWIFT AMOUNT TO TOTAL NET PROCEEDS
     C                     MOVELWWAMTX    AMTX
     C*
     C** SET INDEX TO PROCESS MULTIPLE OCCURRENCE DS FOR PF/MGOREFPD
     C                     Z-ADD1         W       20
     C*
     C** SAVE FIRST PART OF THE MULTIPLE MESSAGE FOR TRANS NUMBER
     C           W         OCUR TRANS
     C                     MOVE WWTRNM    WWTRNF 16
     C*
     C** PROCESS MULTIPLE OCCURRENCE DATA STRUCTURE FOR PF/MGOREFPD
     C           W         DOWLEWWTOT1
     C           W         OCUR REFDS
     C*
     C** FORMAT FIELDS ON PF/MGOREFPD
     C                     EXSR #W04
     C/COPY WNCPYSRC,MG0554C005
     C*
     C** WRITE RECORD TO PF/MGOREFPD
     C                     MOVELMGCHG     AORR                                                CSW025
     C                     WRITEMGOREFD0               16
     C*
     C** IF ERROR ON WRITE OF RECORD
     C           *IN16     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '033'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 033  *
     C                     MOVEL'MGOREFPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0554'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** SET TOTAL AMOUNT FIELD TO BLANKS FOR REMAINING RECORDS
     C                     MOVE *BLANKS   AMTX
     C                     END
     C*
     C** PROCESS MULTIPLE OCCURRENCE DATA STRUCTURE
     C                     Z-ADD1         Z       30
     C           Z         DOWLTY
     C           Z         OCUR MULT
     C*
     C** FORMAT FIELDS ON PF/MGOMSGPD
     C                     MOVELMDAT      MFLD
     C                     MOVELWWTRNF    TRNO
     C                     MOVE *BLANKS   PTDL
     C*
     C** WRITE RECORD TO PF/MGOMSGPD
     C                     WRITEMGOMSGD0               17
     C*
     C** IF ERROR ON WRITE OF RECORD
     C           *IN17     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '034'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 034  *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0554'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** INCREMENT INDEX
     C                     ADD  1         Z
     C                     END
      *                                                                   183583
      ** Write multiple occurrence data structure to MGOEXTPD             183583
      *                                                                   183583
     C                     Z-ADD1         VV      20                      183583
     C           VV        DOWLEV                                         183583
     C           MCSI      ANDNE*BLANKS                                   183583
     C           VV        OCUR EXTN                                      183583
     C                     WRITEMGOEXTD0               06                 183583
      *                                                                   183583
      ** Error on write to PF/MGOEXTPD (message data extension file)      183583
      *                                                                   183583
     C           *IN06     IFEQ '1'                                       183583
     C           *LOCK     IN   LDA                                       183583
     C                     MOVE '049'     DBASE            ************** 183583
     C                     MOVEL'        'DBKEY            * DBERROR 49 * 183583
     C                     MOVEL'MGOEXTPD'DBFILE           ************** 183583
     C                     OUT  LDA                                       183583
     C                     EXSR *PSSR                                     183583
     C                     ENDIF                                          183583
     C                     ADD  1         VV                              183583
     C                     ENDDO                                          183583
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  POSU      - FORMAT ADDRESSES                                *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN                                            *
     C**                                                              *
     C*****************************************************************
     C*
     C           POSU      BEGSR
     C*
     C** UPDATE THE MESSAGE GENERATED INDICATOR ON POSITION SETTLEMENTS
     C** EXTENSIONS FILE
     C                     MOVELMGTNUM    WWTNUM
     C                     MOVE MGSNBR    WWSEQN
     C*
     C           WWKPOS    CHAINPOSETDY1             18
     C           *IN18     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '035'     DBASE            * * * * * * * *
     C                     MOVELWWPOSE    DBKEY            *  DBERR 035  *
     C                     MOVEL'POSETDY1'DBFILE           * * * * * * * *
     C                     MOVEL'MG0554'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C           PORCDG    IFEQ 'N'
     C                     MOVE 'Y'       PORCDG
     C                     MOVE WWLSWS    POSEQN
     C                     UPDATPOSETDD1
     C                     ELSE
     C*
     C           *LOCK     IN   LDA
     C                     MOVE '036'     DBASE            * * * * * * * *
     C                     MOVELWWPOSE    DBKEY            *  DBERR 036  *
     C                     MOVEL'POSETDY1'DBFILE           * * * * * * * *
     C                     MOVEL'MG0554'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  #ZA       - FORMAT ADDRESSES                                *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - FLD53A, FLD83A, FLD87A                          *
     C**                                                              *
     C*****************************************************************
     C*
     C           #ZA       BEGSR
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C           ZSADD1    IFNE *BLANKS                                   122283
     C                     MOVELZSADD1    MDAT
     C                     EXSR SEXTN                                     183583
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C*
     C** PRINT CUSTOMER REPORT NAME AND REPORT TOWN
     C           MTAG      IFEQ ':53A:'
     C           MTAG      OREQ ':83A:'
     C           MTAG      OREQ ':87A:'
     C                     MOVELZCRNM     CRNM
     C                     MOVELZCRTN     CRTN
     C                     END
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C*                                                                   122283
     C                     END                                            122283
     C*
     C** IF ZSADD2 IS NOT BLANK
     C           ZSADD2    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELZSADD2    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C** IF ZSADD3 IS NOT BLANK
     C           ZSADD3    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELZSADD3    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C** IF ZSADD4 IS NOT BLANK
     C           ZSADD4    IFNE *BLANKS
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELZSADD4    MDAT
     C*
     C** FORMAT FIELD FOR REPORT
     C                     MOVELMDAT      RDAT
     C*
     C** INCREASE INDEX FOR MULT OCCUR. DATA STRUCTURE
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
      /EJECT                                                              183583
      *****************************************************************   183583
      *                                                               *   183583
      *   SEXTN - Store customer with SWIFT address to data structure *   183583
      *                                                               *   183583
      *   Called by: #ZA                                              *   183583
      *   Calls    : None                                             *   183583
      *                                                               *   183583
      *****************************************************************   183583
      *                                                                   183583
     C           SEXTN     BEGSR                                          183583
      *                                                                   183583
     C           SAVMTG    IFNE *BLANKS                                   183583
     C                     MOVE SAVMTG    @MTG                            183583
     C                     ELSE                                           183583
     C                     MOVELMTAG      @MTG                            183583
     C                     ENDIF                                          183583
      *                                                                   183583
     C           FLDTYP    IFEQ '5'                                       183583
     C           FLDTAG    ANDEQ'A'                                       183583
     C           FLDTYP    OREQ '8'                                       183583
     C           FLDTAG    ANDEQ'A'                                       183583
     C                     ADD  1         V                               183583
     C           V         OCUR EXTN                                      183583
     C                     Z-ADDX         MPSN                            183583
     C                     MOVEL@MTG      MTG                             183583
     C                     MOVELZDEST     MCSI                            183583
     C                     ENDIF                                          183583
      *                                                                   183583
     C           SEXT9     ENDSR                                          183583
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  #W01      - PRINT MESSAGE GENERATED DETAILS                 *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           #W01      BEGSR
     C*
     C** INITIALISE FIELDS USED IN REPORT
     C                     MOVE *BLANKS   R1SNBR
     C                     MOVE *BLANKS   R1SNDR
     C                     MOVE *BLANKS   R1NWSN
     C                     MOVE *BLANKS   R1DEST
     C                     MOVE *BLANKS   R1NWDS
     C                     MOVE *BLANKS   R1SRNM
     C                     MOVE *BLANKS   R1SRTN
     C                     MOVE *BLANKS   R1DRNM
     C                     MOVE *BLANKS   R1DRTN
     C*
     C** FORMAT REPORT FIELDS
     C                     MOVELWWHDR1    R1SNBR
     C                     MOVEL@SNDR     R1SNDR
     C                     MOVEL@NWSN     R1NWSN
     C                     MOVE 'N'       R1MESS
     C                     MOVELWWHDR2    R1DEST
     C                     MOVEL@NWRK     R1NWRK
     C                     MOVEL@NWDS     R1NWDS
     C                     MOVEL@SRNM     R1SRNM
     C                     MOVEL@SRTN     R1SRTN
     C                     MOVEL@DRNM     R1DRNM
     C                     MOVEL@DRTN     R1DRTN
     C*
     C** WRITE THE HEADER AND THE MAIN TEXT FOR THE REPORT
     C                     WRITEHEADER1
     C                     WRITETEXT1
     C                     MOVEL'N'       WWC1    1
     C*
     C** PROCESS MULTIPLE OCCURRENCE DATA STRUCTURE
     C                     Z-ADD0         I       30
     C           I         DOWLEY
     C                     ADD  1         I
     C           I         OCUR MULT
     C*
     C** WRITE A DETAIL LINE TO THE REPORT CHECKING FOR OVERFLOW
     C           WWOFL1    SUB  WWPTL1    WWLINE
     C*
     C           WWLINE    IFLT 3
     C                     WRITEHEADER1
B03  C           MTAG      IFEQ ':20: '
     C                     WRITECONTI1
E03  C                     END
X02  C                     ELSE
     C*
     C** NEW PAGE FOR EACH NEW PART
B03  C           MTAG      IFEQ ':20: '
     C           WWC1      ANDEQ'Y'
     C                     WRITEHEADER1
     C                     WRITECONTI1
E03  C                     END
     C*
E02  C                     END
     C*
     C** WRITE A BLANK LINE AFTER 83A AND BEFORE 20
B03  C           MTAG      IFEQ ':20: '
     C                     WRITESPAC1
E03  C                     END
     C*
     C** WRITE A DETAIL LINE TO THE REPORT
     C                     WRITELINE1
     C*
B02  C           MTAG      IFEQ ':35A:'
     C                     MOVEL'Y'       WWC1
E02  C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  #W02      - PRINT MESSAGE NOT GENERATED DETAILS             *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           #W02      BEGSR
     C*
     C** INITIALISE  FIELDS USED IN REPORT
     C                     MOVE *BLANKS   R2SNBR
     C                     MOVE *BLANKS   R2SNDR
     C                     MOVE *BLANKS   R2NWSN
     C***********          Z-ADD*ZEROS    R2DEST                          CSD027
     C                     MOVE *BLANKS   R2DEST                          CSD027
     C                     MOVE *BLANKS   R2NWDS
     C                     MOVE *BLANKS   R2DERR
     C                     MOVE *BLANKS   R2NWRK
     C                     MOVE *BLANKS   R2MODI
     C*
     C** OPEN PRINTER FILE
     C           *IN50     IFEQ '0'
     C************         OPEN MG0554P2                                  091250
     C                     OPEN MG0554A2                                  091250
     C                     MOVE '1'       *IN50
     C                     END
     C*
     C** FORMAT REPORT FIELDS
     C                     MOVELWWHDR1    R2SNBR
     C                     MOVEL@SNDR     R2SNDR
     C                     MOVEL@NWSN     R2NWSN
     C                     MOVELWWHDR2    R2DEST
     C                     MOVEL@NWDS     R2NWDS
     C                     MOVELDSERR     R2DERR
     C                     MOVEL@NWRK     R2NWRK
     C                     MOVELNWERR     R2NWER
     C                     MOVELWWHDR3    R2MODI
     C*
     C** WRITE THE HEADER AND THE MAIN TEXT FOR THE REPORT
     C                     WRITEHEADER2
     C                     WRITETEXT2
     C                     MOVEL'N'       WWC2    1
     C*
     C** PROCESS MULTIPLE OCCURRENCE DATA STRUCTURE
     C                     Z-ADD0         I
     C***********I         DOWLTY
     C           I         DOWLTWWY
     C                     ADD  1         I
     C***********I         OCUR MULT
     C           I         OCUR WMULT
     C*
     C** WRITE A DETAIL LINE TO THE REPORT CHECKING FOR OVERFLOW
     C           WWOFL2    SUB  WWPTL2    WWLINE
     C*
     C           WWLINE    IFLT 3
     C***********I         ANDLTY
     C           I         ANDLTWWY
     C                     WRITEHEADER2
B03  C           MTAG      IFEQ ':20: '
     C                     WRITECONTI2
E03  C                     END
X02  C                     ELSE
     C*
     C** NEW PAGE FOR EACH NEW PART
B03  C           MTAG      IFEQ ':20: '
     C           WWC2      ANDEQ'Y'
     C                     WRITEHEADER2
     C                     WRITECONTI2
E03  C                     END
     C*
     C                     END
     C*
     C*
     C** WRITE A BLANK LINE AFTER 83A AND BEFORE 20
B03  C           MTAG      IFEQ ':20: '
     C                     WRITESPAC2
E03  C                     END
     C** WRITE A DETAIL LINE TO THE REPORT
     C                     WRITELINE2
     C*
B02  C           MTAG      IFEQ ':35A:'
     C                     MOVEL'Y'       WWC2
E02  C                     END
     C*
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  #W04      - FORMAT FIELDS FOR MGOREFPD                      *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - OFLDS                                           *
     C**                                                              *
     C*****************************************************************
     C*
     C           #W04      BEGSR
     C*
     C** FORMAT FIELDS ON PF/MGOREFPD
     C                     MOVELWWMODI    MODI
     C                     MOVELWWTRNO    TRNO
     C                     MOVELWWMTRN    MTRN
     C                     MOVEL@NWRK     NWRK
     C                     MOVELA8BICN    SECN
     C                     MOVEL@NWSN     NWSN
     C                     MOVELWWDECN    DECN
     C                     MOVEL@NWDS     NWDS
     C                     MOVEL'554'     MTPY
     C                     MOVEL'N'       MPRY
     C                     MOVEL'1'       MGSG
     C                     MOVEL'CRTD'    MGST
     C                     TIME           MGTM
     C                     MOVELBJMRDT    LADT
     C                     TIME           LATM
     C                     MOVELMGSNBR    BRCA
     C                     MOVEL'Y'       TSKS
     C                     MOVELWWAMTS    AMTS
     C*
     C** INCREMENT INDEX
     C                     ADD  1         W
     C           WWTOT1    IFGT 1
     C           W         OCUR TRANS
     C                     MOVELWWTRNF    TRNF
     C                     MOVELWWTRNM    TRNM
     C                     END
     C*
     C                     ENDSR
     C/COPY WNCPYSRC,MG0554C006
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  PSSR      - ERROR HANDLING                                  *
     C**                                                              *
     C**  CALLS     -                                                 *
     C**                                                              *
     C**  CALLED BY -                                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C** ROLLBACK ALL FILE CHANGES
     C                     ROLBK
     C*
     C** WRITE DATABASE ERROR TO REPORT
     C                     OPEN MG0554AU
     C                     WRITEHEAD
     C                     WRITEDBERROR
     C*
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C/COPY MGCPYSRC,SWIFTTRANS                                           BLI605
** CPY@   Object Copyright
(c) Finastra International Limited 2001
** ERRA   ERROR MESSAGES
Customer number not found on customer file
Unable to format mandatory field :20:
Unable to format mandatory field :23:
Unable to format field :32G:
Unable to format mandatory field :35A:
Unable to format mandatory field :35B:
Unable to format addresses for field :71C:
Unable to format addresses for field :83a:
Unable to format addresses for field :87a:
Unable to format addresses for field :71B:
Nominal currency not found on currency file :35U:
Settlement currency not found on currency file :34A:
Unable to format field :21:
Invalid Dividend/Interest Rate data :35U:
Unable to format addresses for field :53a:
*** Message is Unroutable ***
** DESA   DESCRIPTIONS
                 SUM OF NET PROCEEDS
              FURTHER IDENTIFICATION
                 ACCOUNT FOR PAYMENT
                 SAFEKEEPING ACCOUNT
                              T.R.N.
                   RELATED REFERENCE
    RECEIVER/DELIVERER OF SECURITIES
              QUANTITY OF SECURITIES
        IDENTIFICATION OF SECURITIES
              DIVIDEND/INTEREST RATE
      SENDER TO RECEIVER INFORMATION
                         NEXT COUPON
                         RECORD DATE
                        GROSS AMOUNT
                       OTHER CHARGES
                         OWN CHARGES
                       EXCHANGE RATE
                        NET PROCEEDS
** SUPP - add any fields tags which should be suppressed
