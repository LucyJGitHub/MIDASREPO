     H******  1                                                           S01421
     H        1                           F                               S01421
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MG Certificate number messages')
      *****************************************************************
      *                                                               *
      *   Midas - MESSAGE GENERATION                                  *
      *                                                               *
      *   MG0579 - FORMAT MT579 REPORT MESSAGES                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 BUG3122            Date 17Jul04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSW025             Date 26Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 141572             Date 21Sep98               *
      *                 113352             Date 13Feb97               *
      *                 082282             DATE 02MAY96               *
      *                 CSW003             DATE 09JAN96               *
      *                 091250             DATE 24FEB95               *
      *                 080096             DATE 12JAN95               *
      *                 S01485             DATE 12APR94               *   S01485
      *                 S01421             DATE 18AUG93               *
      *                 S01401             DATE 17JUN93               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  BUG3122 - Recompiled due to changes in SDSECSPD.             *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CSW025 - Midas Message Manager                               *
      *           - Pass original change type                         *
      *  CDL008 - Continuous Linked Settlement (Recompiled)           *
      *  141572 - Recompiled over MGOREFPD                            *
      *  113352 - Set-up CIND (Century flag)  to correct year2000     *
      *           display problem.                                    *
      *  082282 - Set message status according to customer details    *
      *  CSW003 - MT5xx Message Generation - Phase 2                  *
      *  091250 - Print Files MG0579P1/P2 renamed to MG0579A1/A2 as   *
      *           these are audit reports.                            *
      *  080096 - RECOMPILE BECAUSE COPY MEMBER MGCPYSRC,SWIFTTRANS   *
      *           CHANGED                                             *
      *   S01485 - S.W.I.F.T. September 1994 Changes.                 *
      *            Field 28 goes from 1n/1n to 5n/2n.                 *
      *   S01421 - REPLACE THE SWIFT TRANSLATION TABLE WITH A         *
      *              /COPY STATEMENT (SWIFTTRANS).                    *
      *                                                               *
      *  S01401 - MT500 messages for Release 10; new program.         *
      *                                                               *
      *****************************************************************
     F*
     FMGF579PDUF  E                    DISK         KINFSR *PSSR
     F                                              KCOMIT
     FSDBANKPDIF  E                    DISK         KINFSR *PSSR
     FSDSTRDPDIF  E                    DISK         KINFSR *PSSR
     FSDBRCHL1IF  E           K        DISK         KINFSR *PSSR
     F/COPY WNCPYSRC,MG0579F001
     FSDMGMEPDIF  E                    DISK         KINFSR *PSSR
     F*
     FMGOREFPDO   E                    DISK         KCOMIT
     F                                              KINFSR *PSSR
     FMGOMSGPDO   E                    DISK         KCOMIT
     F                                              KINFSR *PSSR
     F*G0579P1O***E********************PRINTER******KINFDS*OVRFL1         091250
     FMG0579A1O   E                    PRINTER      KINFDS OVRFL1         091250
     F                                              KINFSR *PSSR
     F*G0579P2O***E********************PRINTER******KINFDS*OVRFL2     UC  091250
     FMG0579A2O   E                    PRINTER      KINFDS OVRFL2     UC  091250
     F                                              KINFSR *PSSR
     FMG0579AUO   E                    PRINTER                        UC
     F*
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     E*
     E** ARRAY USED FOR COPYRIGHT
     E                    CPY@    1   1 80
     E*
     E** ARRAY USED TO FORMAT ERROR MESSAGE FIELD ON MULTIPLE
     E** OCCURENCE DATA STRUCTURE
     E                    ERRA    1   6 50
     E*
     E** ARRAY USED TO FORMAT DESCRIPTION FIELD ON MULTIPLE
     E** OCCURENCE DATA STRUCTURE
     E                    DESA    1   4 36
     E*
     E** ARRAY USED TO FORMAT TRADE/MOVEMENT TYPE
     E** DESCRIPTION ON REPORTS
     E                    TYPA    1   2 20
      *
      **  Array used to define fields to be suppressed
     E                    SUPP    1  50  5
      *                                                                   S01485
      ** Build up Field 28                                                S01485
     E                    WORK28      8  1                                S01485
      *                                                                   082282
      **  Array used to set message status                                082282
     E                    @ST        15  3                                082282
     E/COPY WNCPYSRC,MG0579E001
     E*****************************************************************
     E/EJECT
     E*****************************************************************
     I*
     I** Multiple occurrence data structure for message
     I** NEEDS TO BE NO. OF MESSAGES, MULTIPLIED BY 35, PLUS 1
     I** IF THIS CHANGES THEN THE CHECK ON THE INDEX IN SR FLD35
     I** MUST ALSO CHANGE TO BE 12 LESS THAN THE SIZE OF THE DATA
     I** STRUCTURE. E.G. IF MULT IS 316, CHECK MUST BE LESS THAN
     I** OR EQUAL TO 304
     IMULT        DS                        316
     I                                        1  36 MDES
     I                                       37  41 WWMTAG
     I                                       42  91 MDAT
     I                                       92 141 MERR
     I*
     I** Local data area
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I** DATA STRUCTURE FOR :20: FIELD
     IDS20        DS                        141
     I                                        1  36 WWDS20
     I                                       37  41 WWTG20
     I                                       42  91 WWDT20
     I                                       92 141 WWMR20
     I                                       42  44 WA0103
     I                                       45  50 WA0409
     I                                       42  48 WA0107
     I                                       49  57 WA0816
     I                                       51  57 WA1016
     I                                       42  57 WWDATA
     I*
     I** DATA STRUCTURE FOR :21: FIELD
     IDS21        DS                        141
     I                                        1  36 WWDS21
     I                                       37  41 WWTG21
     I                                       42  91 WWDT21
     I                                       92 141 WWMR21
     I                                       42  57 WB0116
     I*
     I** DATA STRUCTURE FOR :28: FIELD
     IDS28        DS                        141
     I                                        1  36 WWDS28
     I                                       37  41 WWTG28
     I                                       42  91 WWDT28
     I                                       92 141 WWMR28
     I**********                             42  42 WC0101                S01485
     I**********                             43  43 WC0202                S01485
     I**********                             44  440WC0303                S01485
      *                                                                   S01485
      ** Work field for building up the Field 28 details.                 S01485
     I            DS                                                      S01485
     I                                        1   8 WORK28                S01485
     I                                        1   50F28S1N                S01485
     I                                        6   6 SLASH                 S01485
     I                                        7   8 F28S2A                S01485
     I*
     I** DATA STRUCTURE FOR DBERROR ON MG5900
     IDBERR       DS                             20
     I                                        1   2 @MODI
     I                                        3  17 @TRAN
     I*
     I** DATA STRUCTURE FOR DBERROR ON ZM1100
     IDBERR1      DS                             26
     I                                        1   6 @SNDR
     I                                        7  18 @SCSD
     I                                       19  24 @DEST
     I                                       25  26 @SETP
     I*
     I***DATA*STRUCTURE*FOR*OVERFLOW*ON*PRINTER*MG0579P1***               091250
     I** FILE INFORMATION DATA STRUCTURE FOR MG0579A1                     091250
     IOVRFL1      DS
     I                                    B 188 1890WWOFL1
     I                                    B 367 3680WWPTL1
     I*
     I***DATA*STRUCTURE*FOR*OVERFLOW*ON*PRINTER*MG0579P2***               091250
     I** FILE INFORMATION DATA STRUCTURE FOR MG0579A2                     091250
     IOVRFL2      DS
     I                                    B 188 1890WWOFL2
     I                                    B 367 3680WWPTL2
     I*
     I** DATA STRUCTURE FOR CRLF CONTROL CHARACTER
     ICRLFDS      DS                              2
     I                                        1   2 CTRC
     I                                        1   1 WWCR
     I                                        2   2 WWLF
     I*
     I** DATA STRUCTURE TO OBTAIN THE LATEST MT580 TRN
     IM5STAT    E DS
     I*
      * Data structures for access objects                                082282
     IDSSDY     E DSDSSDY                                                 082282
     ISDCUST    E DSSDCUSTPD                                              082282
      *                                                                   082282
     ISDSECS    E DSSDSECSPD                                              CSW003
      ** Securities Trading Clients                                       CSW003
     I*****************************************************************
     I/EJECT
     C*****************************************************************
     C*                                                               *
     C*   MAIN PROCESSING - FORMAT MT579                              *
     C*                                                               *
     C*   CALLS THE FOLLOWING SUBROUTINES                             *
     C*                                                               *
     C*   INIT     - INITIALISATION                                   *
     C*   INTF     - INITIALISE NON-CONSTANT FIELDS                   *
     C*   ROUT     - DETERMINE MESSAGE ROUTING                        *
     C*   FLD20    - FORMAT MANDATORY FIELD :20:                      *
     C*   FLD21    - FORMAT MANDATORY FIELD :21:                      *
     C*   FLD35    - FORMAT MANDATORY FIELD :35F:                     *
     C*   FLD28    - FORMAT MANDATORY FIELD :28:                      *
     C*   #W01     - PRINT MESSAGE GENERATED DETAILS                  *
     C*   #W02     - PRINT MESSAGE NOT GENERATED DETAILS              *
     C*   *PSSR    - ERROR HANDLING                                   *
     C*                                                               *
     C*****************************************************************
     C*
     C** INITIALISATION
     C                     EXSR INIT
     C*
     C** READ FIRST RECORD FROM PF/MGF579PD
     C                     READ MGF579PD                 10
     C*
     C** IF RECORDS PRESENT ON THE FILE
     C           *IN10     IFEQ '0'
     C*
     C** DO WHILE RECORDS ON THIS FILE
     C           *IN10     DOWEQ'0'
     C*
     C** INITIALISE NON-CONSTANT FIELDS
     C                     EXSR INTF
     C*
     C** DETERMINE MESSAGE ROUTING
     C                     EXSR ROUT
     C*
     C** FORMAT MANDATORY FIELD :20:
     C                     MOVE ':20: '   WWGENF  5
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD20
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :21:
     C                     MOVE ':21: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD21
     C                     END
     C*
     C** FORMAT MANDATOTY FIELD :35F:
     C                     MOVE ':35F:'   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD35
     C                     END
     C*
     C** FORMAT MANDATORY FIELD :28:
     C                     MOVE ':28: '   WWGENF
     C                     EXSR GENF
     C           *IN01     IFEQ '1'
     C                     EXSR FLD28
     C                     END
     C*
     C** IF ERROR FOUND WHILST FORMATTING THIS MESSAGE
     C           @ERR      IFEQ 'Y'
     C*
     C** PRINT MESSAGE NOT GENERATED DETAILS
     C                     EXSR #W02
     C*
     C                     ELSE
     C*
     C** PRINT MESSAGE GENERATED DETAILS
     C                     EXSR #W01
     C                     MOVEL'Y'       WWPTR1  1
     C                     END
     C*
     C** DELETE THE RECORD ON PF/MGF579PD PROCESSED
     C                     DELETMGF579PD               11
     C*
     C** IF ERROR ON DELETE OF RECORD
     C           *IN11     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '001'     DBASE              ***************
     C                     MOVELMGTNUM    DBKEY              * DBERROR 001 *
     C                     MOVEL'MGF579PD'DBFILE             ***************
     C                     MOVEL'MG0579'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** COMIT ALL UPDATES TO FILES
     C                     COMIT
     C*
     C** READ NEXT RECORD FROM PF/MGF579PD
     C                     READ MGF579PD                 10
     C*
     C                     END
     C*
     C** WRITE TRAILER ON SWIFT MESSAGES GENERATED REPORT
     C** ONLY IF MESSAGES GENERATED
     C           WWPTR1    IFNE 'Y'
     C                     WRITEHEADER1
     C                     WRITENODETS
     C*
     C                     ELSE
     C*
     C** CHECK FOR OVERFLOW
     C           WWOFL1    SUB  WWPTL1    WWLINE  20
     C*
     C           WWLINE    IFLT 4
     C                     WRITEHEADER1
     C                     END
     C                     WRITEEOREPT1
     C                     END
     C*
     C** WRITE TRAILER ON SWIFT MESSAGES NOT GENERATED REPORT
     C** IF THE PRINTER FILE HAS BEEN OPENED
     C           *IN50     IFEQ '1'
     C*
     C** CHECK FOR OVERFLOW
     C           WWOFL2    SUB  WWPTL2    WWLINE
     C*
     C           WWLINE    IFLT 4
     C                     WRITEHEADER2
     C                     END
     C                     WRITEEOREPT2
     C                     END
     C*
     C                     ELSE
     C*
     C** WRITE NO DETAILS TO REPORT
     C                     WRITEHEADER1
     C                     WRITENODETS
     C                     END
     C/COPY WNCPYSRC,MG0579C003
     C*
     C                     SETON                     LR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  INIT      - INITIALISATION                                  *
     C**                                                              *
     C**  CALLS     - *PSSR                                           *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESS                                    *
     C**                                                              *
     C*****************************************************************
     C*
     C           INIT      BEGSR
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C** SET UP LDA
     C           *NAMVAR   DEFN           LDA
     C*
     C** ACCESS SDBANKPD FOR BANK DETAILS
     C           1         SETLLSDBANKPD
     C                     READ SDBANKPD                 12
     C*
     C** IF RECORD NOT FOUND OR LAST CHANGE TYPE = 'D'
     C           *IN12     IFEQ '1'
     C           BJTYLC    OREQ 'D'
     C           *LOCK     IN   LDA
     C                     MOVE '002'     DBASE            ***************
     C                     MOVEL*BLANKS   DBKEY            * DBERROR 002 *
     C                     MOVEL'SDBANKPD'DBFILE           ***************
     C                     MOVEL'MG0579'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C/COPY WNCPYSRC,MG0579C004
     C*
     C** ACCESS SDMGMEPD FOR MESSAGE GENERATION & MESSAGE MANAGEMENT
     C           1         SETLLSDMGMEPD
     C                     READ SDMGMEPD                 14
     C*
     C** IF RECORD NOT FOUND OR LAST CHANGE TYPE = 'D'
     C           *IN14     IFEQ '1'
     C           ENTYLC    OREQ 'D'
     C           *LOCK     IN   LDA
     C                     MOVE '014'     DBASE            * * * * * * * *
     C                     MOVEL*BLANKS   DBKEY            *  DBERR 014  *
     C                     MOVEL'SDMGMEPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0579'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** SET CRLF CONTROL CHARACTER
     C                     MOVE *LOVAL    CTRC
     C                     BITON'457'     WWCR
     C                     BITON'257'     WWLF
     C*
     C** ACCESS SDSTRDPD FOR SE ICD DETAILS
     C           1         SETLLSDSTRDPD
     C                     READ SDSTRDPD                 17
     C*
     C** IF RECORD NOT FOUND OR LAST CHANGE TYPE = 'D'
     C           *IN15     IFEQ '1'
     C           BVTYLC    OREQ 'D'
     C           *LOCK     IN   LDA
     C                     MOVE '030'     DBASE            * * * * * * * *
     C                     MOVEL*BLANKS   DBKEY            *  DBERR 030  *
     C                     MOVEL'SDSTRDPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0579'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C/COPY WNCPYSRC,MG0579C001
     C**********           MOVELBVDR2     WWCEDN  60                                          CSD027
     C                     MOVELBVDR2     WWCEDN  6                                           CSD027
     C*
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  GENF      - GENERATE FIELDS CONDITION                       *
     C**                                                              *
     C**  CALLS     -                                                 *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESS                                    *
     C**                                                              *
     C*****************************************************************
     C*
     C           GENF      BEGSR
      *
      **  If message field tag appears on array SUPP, supress its
      **  production by setting off indicator 01.
     C           WWGENF    LOKUPSUPP                     55
     C           *IN55     IFEQ '0'
     C                     SETON                     01
     C/COPY WNCPYSRC,MG0579C002
     C                     ELSE
     C                     SETOF                     01
     C                     END
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  INTF      - INITIALISE NON-CONSTANT FIELDS                  *
     C**                                                              *
     C**  CALLS     - MG5900, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESS                                    *
     C**                                                              *
     C*****************************************************************
     C*
     C           INTF      BEGSR
     C*
     C                     MOVE MGDEST    WWCUST                          CSW003
     C                     CALL 'AOSECSR0'                                CSW003
     C                     PARM *BLANKS   @RTCD                           CSW003
     C                     PARM '*KEY   ' @OPTN                           CSW003
     C                     PARM           WWCUST  6                       CSW003
     C           SDSECS    PARM SDSECS    DSSDY                           CSW003
     C*                                                                   CSW003
     C**     IF SENT TO CEDEL, USE  THE CEDEL SEQUENCE NO
     C           MGDEST    IFEQ WWCEDN
     C           BFCLAS    OREQ 'C'                                       CSW003
     C                     Z-ADDWW80CE    @LSWS
     C                     ELSE
     C*
     C** OBTAIN SEQUENCE NUMBERS FROM TRANSACTION RECORD
     C** FORMAT @TRAN AS FOLLOWS
     C** CHARS 1-6 EQUAL TO LEFTMOST 6 CHARS FROM MGTNUM
     C** CHARS 7-13 EQUAL TO BLANKS
     C** CHARS 14-15 EQUAL TO 'TR'
     C                     MOVE *BLANKS   WWTRAN 15
     C                     MOVE *BLANKS   WWFL06  6
     C                     MOVELMGTNUM    WWFL06
     C                     MOVELWWFL06    WWTRAN
     C                     MOVE 'TR'      WWTRAN
     C*
     C                     CALL 'MG5900'               20
     C                     PARM MGMODI    @MODI   2
     C                     PARM WWTRAN    @TRAN  15
     C                     PARM 'PR'      @PROG   2
     C                     PARM *BLANKS   @MODE   1
     C                     PARM *ZEROS    @LSWS   70
     C                     PARM *BLANKS   @ERCD   1
     C                     PARM *BLANKS   CSW003  1                       CSW003
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN20     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '003'     DBASE            ***************
     C                     MOVE *BLANKS   DBKEY            * DBERROR 003 *
     C                     MOVEL'MG5900'  DBPGM            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM
     C           @ERCD     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     MOVE '004'     DBASE            ***************
     C                     MOVE *BLANKS   DBKEY            * DBERROR 004 *
     C                     MOVELDBERR     DBKEY            ***************
     C                     MOVEL'MG5900'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     END
     C*
     C** CLEAR MULTIPLE OCCURENCE DATA STRUCTURE
     C***********          Z-ADD1         X       20                      S01485
     C                     Z-ADD1         X       30                      S01485
     C           X         OCUR MULT
     C*
     C           X         DOWLE316
     C           MULT      ANDNE*BLANKS
     C                     MOVE *BLANKS   MULT
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C** SET INDEX TO FIRST OCCURENCE
     C                     Z-ADD1         X
     C           X         OCUR MULT
     C*
     C** SET FLAG FOR MESSAGE CORRECTLY GENERATED
     C                     MOVE *BLANKS   @ERR    1
     C                     MOVE *BLANKS   DSERR
     C                     MOVE *BLANKS   NWERR  50
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  ROUT      - DETERMINE MESSAGE ROUTING                       *
     C**                                                              *
     C**  CALLS     - *PSSR, ZM1100                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESS                                    *
     C**                                                              *
     C*****************************************************************
     C*
     C           ROUT      BEGSR
     C*
     C** ACCESS SDBRCHL1 USING KEY OF MGSNBR FROM PF/MGF579PD
     C           MGSNBR    CHAINSDBRCHL1             13
     C*
     C** IF RECORD NOT FOUND OR LAST CHANGE TYPE = 'D'
     C           *IN13     IFEQ '1'
     C           A8BRCD    OREQ 'D'
     C           *LOCK     IN   LDA
     C                     MOVE '005'     DBASE            ***************
     C                     MOVELMGSNBR    DBKEY            * DBERROR 005 *
     C                     MOVEL'SDBRCHL1'DBFILE           ***************
     C                     MOVEL'MG0579'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** DETERMINE NETWORK ROUTING AND ADDRESSES
     C                     MOVE A8BICN    @SNDR   6
     C                     MOVE A8BTID    @SCSD  12
     C                     MOVE MGDEST    @DEST   6
     C                     MOVELMGSETP    @SETP   2
     C                     MOVE *BLANKS   @NWSN  20
     C                     MOVE *BLANKS   @NWDS  22
     C                     MOVE *BLANKS   @NWRK   6
     C                     MOVE *BLANKS   @ERRC   1
     C                     MOVE *BLANKS   @SRNM  20
     C                     MOVE *BLANKS   @SRTN  10
     C                     MOVE *BLANKS   @DRNM  20
     C                     MOVE *BLANKS   @DRTN  10
     C*
     C                     CALL 'ZM1100'               21
     C                     PARM           @SNDR
     C                     PARM           @SCSD
     C                     PARM           @DEST
     C                     PARM           @SETP
     C                     PARM           @NWSN
     C                     PARM           @NWDS
     C                     PARM           @NWRK
     C                     PARM           @ERRC
     C                     PARM           @SRNM
     C                     PARM           @SRTN
     C                     PARM           @DRNM
     C                     PARM           @DRTN
     C*
     C** IF PROGRAM ZM1100 NOT FOUND
     C           *IN21     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '006'     DBASE            ***************
     C                     MOVE *BLANKS   DBKEY            * DBERROR 006 *
     C                     MOVEL'ZM1100'  DBPGM            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING CALLED PROGRAM
     C           @ERRC     IFEQ 'S'
     C           *LOCK     IN   LDA
     C                     MOVE '007'     DBASE            ***************
     C                     MOVE *BLANKS   DBKEY            * DBERROR 007 *
     C                     MOVELDBERR1    DBKEY            ***************
     C                     MOVEL'ZM1100'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT DESTINATION ERROR MESSAGE
     C           @ERRC     IFEQ 'D'
     C                     MOVE *BLANKS   DSERR  50
     C                     MOVELERRA,1    DSERR
     C                     MOVE 'Y'       @ERR
     C                     END
     C*
     C** FORMAT NETWORK ERROR MESSAGE
     C           @NWRK     IFEQ 'UNROUT'
     C                     MOVELERRA,6    NWERR
     C                     MOVE 'Y'       @ERR
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD20     - FORMAT MANDATORY FIELD :20:                     *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD20     BEGSR
     C*
     C** IF LEFTMOST SIX CHARS OF MGTNUM ARE BLANK THEN ERROR
     C                     MOVELMGTNUM    WWFL06
     C           WWFL06    IFEQ *BLANKS
     C*
     C** FORMAT ERROR MESSAGE WORK FIELD
     C                     MOVELERRA,4    WWMR20
     C                     MOVE 'Y'       @ERR
     C*
     C                     ELSE
     C*
     C                     MOVE *BLANKS   DS20
     C*
     C** FORMAT DESCRIPTION WORK FIELD OF :20:
     C                     MOVELDESA,1    WWDS20
     C*
     C** FORMAT TAG WORK FIELD OF :20:
     C                     MOVEL':20:'    WWTG20
     C*
     C** IF DESTINATION IS NOT CEDEL
     C*
     C           MGDEST    IFNE WWCEDN
     C           BFCLAS    OREQ 'C'                                       CSW003
     C*
     C** FORMAT DATA WORK FIELD OF :20:
     C                     MOVEL'SET'     WA0103
     C                     MOVELMGTNUM    WA0409
     C*
     C           @LSWS     IFEQ 9999999
     C                     Z-ADD1         WWLSWS  70
     C                     ELSE
     C           @LSWS     ADD  1         WWLSWS
     C                     END
     C*
     C                     MOVELWWLSWS    WA1016
     C*
     C                     ELSE
     C** DESTINATION IS CEDEL
     C*
     C** OBTAIN THE LATEST MT580 TRN FOR CEDEL
     C           *NAMVAR   DEFN           M5STAT
     C           *LOCK     IN   M5STAT
     C*
     C** CONVERT MT80CE ALPHANUMERIC FIELD TO NUMERIC
     C                     MOVE MT80CE    WW80CE  70
     C           WW80CE    IFEQ 9999999
     C                     Z-ADD1         WW80CE
     C                     ELSE
     C                     ADD  1         WW80CE
     C                     END
     C                     MOVELWW80CE    WA1016
     C                     MOVEL'000'     WA0103
     C                     MOVELMGTNUM    WA0409
     C*
     C** UPDATE AND RELEASE DATA AREA
     C                     MOVE WW80CE    MT80CE
     C                     OUT  M5STAT
     C*
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD21     - FORMAT MANDATORY FIELD :21:                     *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD21     BEGSR
     C*
     C** IF MGRREF IS BLANK THEN ERROR
     C           MGRREF    IFEQ *BLANKS
     C*
     C** FORMAT ERROR MESSAGE WORK FIELD
     C                     MOVELERRA,5    WWMR21
     C                     MOVE 'Y'       @ERR
     C*
     C                     ELSE
     C*
     C                     MOVE *BLANKS   DS21
     C*
     C** FORMAT DESCRIPTION WORK FIELD OF :21:
     C                     MOVELDESA,2    WWDS21
     C*
     C** FORMAT TAG WORK FIELD OF :21:
     C                     MOVEL':21:'    WWTG21
     C*
     C** FORMAT DATA WORK FIELD OF :21:
     C                     MOVELMGRREF    WB0116
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD35     - FORMAT MANDATORY FIELD :35F:                    *
     C**                                                              *
     C**  CALLS     - ZM1050, *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD35     BEGSR
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELDESA,3    MDES
     C*
     C** FORMAT TAG FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVEL':35F:'   WWMTAG
     C*
     C** FORMAT CERTIFICATE NUMBERS FIELDS
     C                     MOVELMGTNUM    WWTDRF  6
     C                     Z-ADD0         WWSEQN  40
     C                     MOVE *BLANKS   WWCRT1 50
     C                     MOVE *BLANKS   WWCRT2 50
     C                     MOVE *BLANKS   WWCRT3 50
     C                     MOVE *BLANKS   WWCRT4 50
     C                     MOVE *BLANKS   WWCRT5 50
     C                     MOVE *BLANKS   WWCRT6 50
     C                     MOVE *BLANKS   WWFIN   1
     C                     MOVE *BLANKS   WWERR   1
     C*
     C                     CALL 'ZM1050'               23
     C                     PARM           WWTDRF
     C                     PARM           WWSEQN
     C                     PARM           WWCRT1
     C                     PARM           WWCRT2
     C                     PARM           WWCRT3
     C                     PARM           WWCRT4
     C                     PARM           WWCRT5
     C                     PARM           WWCRT6
     C                     PARM           WWFIN
     C                     PARM           WWERR
     C*
     C** IF PROGRAM ZM1050 NOT FOUND
     C           *IN23     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '008'     DBASE            ***************
     C                     MOVE *BLANKS   DBKEY            * DBERROR 008 *
     C                     MOVEL'ZM1050'  DBPGM            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT ERROR FIELD OF MULT OCCUR. DATA STRUCTURE
     C           WWERR     IFEQ '1'
     C                     MOVELERRA,2    MERR
     C                     MOVE 'Y'       @ERR
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C**   SEE COMMENTS IN I SPECIFICATIONS WHERE MULT IS DEFINED
     C           WWFIN     DOWEQ'N'
     C           X         ANDLE304
     C                     MOVELWWCRT1    MDAT
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     MOVELWWCRT2    MDAT
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     MOVELWWCRT3    MDAT
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     MOVELWWCRT4    MDAT
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     MOVELWWCRT5    MDAT
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C                     MOVELWWCRT6    MDAT
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C** FORMAT CERTIFICATE NUMBERS FIELDS
     C                     MOVELMGTNUM    WWTDRF
     C                     MOVE *BLANKS   WWCRT1
     C                     MOVE *BLANKS   WWCRT2
     C                     MOVE *BLANKS   WWCRT3
     C                     MOVE *BLANKS   WWCRT4
     C                     MOVE *BLANKS   WWCRT5
     C                     MOVE *BLANKS   WWCRT6
     C                     MOVE *BLANKS   WWFIN
     C                     MOVE *BLANKS   WWERR
     C*
     C                     CALL 'ZM1050'               23
     C                     PARM           WWTDRF
     C                     PARM           WWSEQN
     C                     PARM           WWCRT1
     C                     PARM           WWCRT2
     C                     PARM           WWCRT3
     C                     PARM           WWCRT4
     C                     PARM           WWCRT5
     C                     PARM           WWCRT6
     C                     PARM           WWFIN
     C                     PARM           WWERR
     C*
     C** IF PROGRAM ZM1050 NOT FOUND
     C           *IN23     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '009'     DBASE            ***************
     C                     MOVE *BLANKS   DBKEY            * DBERROR 009 *
     C                     MOVEL'ZM1050'  DBPGM            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT ERROR FIELD OF MULT OCCUR. DATA STRUCTURE
     C           WWERR     IFEQ '1'
     C                     MOVELERRA,2    MERR
     C                     MOVE 'Y'       @ERR
     C                     END
     C*
     C                     END
     C*
     C** FORMAT DATA FIELD OF MULT OCCURENCE DATA STRUCTURE
     C                     MOVELWWCRT1    MDAT
     C                     ADD  1         X
     C           X         OCUR MULT
     C*
     C           WWCRT2    IFNE *BLANKS
     C                     MOVELWWCRT2    MDAT
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C           WWCRT3    IFNE *BLANKS
     C                     MOVELWWCRT3    MDAT
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C           WWCRT4    IFNE *BLANKS
     C                     MOVELWWCRT4    MDAT
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C           WWCRT5    IFNE *BLANKS
     C                     MOVELWWCRT5    MDAT
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C*
     C           WWCRT6    IFNE *BLANKS
     C                     MOVELWWCRT6    MDAT
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  FLD28     - FORMAT MANDATORY FIELD :28:                     *
     C**                                                              *
     C**  CALLS     -         *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           FLD28     BEGSR
     C*
     C                     MOVE *BLANKS   DS28
     C*
     C** FORMAT DESCRIPTION WORK FIELD OF :28:
     C                     MOVELDESA,4    WWDS28
     C*
     C** FORMAT TAG WORK FIELD OF :28:
     C                     MOVEL':28:'    WWTG28
     C*
     C** SET TOTAL NUMBER OF :35: FIELDS FORMATTED BY ZM1050
     C           X         SUB  1         WWTTFF  30
     C*
     C** IF TOTAL FIELDS FORMATTED IS LE 35
     C           WWTTFF    IFLE 35
     C                     MOVEL'1'       WWDT28
     C                     Z-ADD1         WWTNMS                          S01485
     C*
     C                     ELSE
     C*
     C** SET TOTAL NUMBER OF MT579 MESSAGES REQUIRED
     C***********WWTTFF    DIV  35        WWTNMS  30                      S01485
     C           WWTTFF    DIV  35        WWTNMS  50                      S01485
     C                     MVR            WWRSUT  30     14
     C           *IN14     IFNE '1'
     C                     ADD  1         WWTNMS
     C                     END
     C*
     C** SET DATA WORK FIELD FOR :28:
     C***********          MOVE WWTNMS    WC0101                          S01485
     C***********          MOVEL'/'       WC0202                          S01485
     C***********          Z-ADD1         WC0303                          S01485
     C                     END
      *                                                                   S01485
      ** Set up the data field for Field 28                               S01485
      *                                                                   S01485
     C                     Z-ADD0         SUB1    50                      S01485
     C                     EXSR MDTA28                                    S01485
      *                                                                   S01485
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  MDTA28    - Format data for Field :28:                      *
     C**                                                              *
     C**  CALLS     -         *PSSR                                   *
     C**                                                              *
     C**  CALLED BY - FLD28; #W01; #W02                               *
     C**                                                              *
     C*****************************************************************
     C*
     C           MDTA28    BEGSR
      *                                                                   S01485
      ** Initialise fields                                                S01485
     C                     MOVE *BLANKS   WORK28                          S01485
     C                     MOVE *BLANKS   WWDT28                          S01485
     C                     ADD  1         SUB1                            S01485
     C                     Z-ADDSUB1      F28S1N                          S01485
     C                     MOVE '/'       SLASH                           S01485
      *                                                                   S01485
      ** Subfield 2 = 99;  No. of pages = Current page                    S01485
      ** Subfield 2 = 0;   No. of pages > Current page                    S01485
      *                                                                   S01485
     C           WWTNMS    IFEQ F28S1N                                    S01485
     C                     MOVE '99'      F28S2A                          S01485
     C                     ELSE                                           S01485
     C                     MOVEL'0'       F28S2A                          S01485
     C                     END                                            S01485
      *                                                                   S01485
      ** Clear the field of leading 0's.                                  S01485
      *                                                                   S01485
     C                     Z-ADD1         A       50
     C           WORK28,A  DOWLT'1'
     C                     ADD  1         A
     C                     END
      *                                                                   S01485
     C                     MOVEAWORK28,A  WWDT28
      *                                                                   S01485
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  #W01      - PRINT MESSAGE GENERATED DETAILS                 *
     C**                                                              *
     C**  CALLS     - MG5900, *PSSR, #W04                             *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           #W01      BEGSR
     C*
     C** INITIALISE FIELDS USED IN REPORT
     C                     MOVE *BLANKS   R1REFE
     C                     MOVE *BLANKS   R1TYP1
     C                     MOVE *BLANKS   R1TYP2
     C                     MOVE *BLANKS   R1SND1
     C                     MOVE *BLANKS   R1SND2
     C                     MOVE *BLANKS   R1SND3
     C                     MOVE *BLANKS   R1PRTY
     C                     MOVE *BLANKS   R1DES1
     C                     MOVE *BLANKS   R1DES2
     C                     MOVE *BLANKS   R1NETW
     C                     MOVE *BLANKS   R1MDES
     C                     MOVE *BLANKS   R1MTAG
     C                     MOVE *BLANKS   R1MDAT
     C*
     C** FORMAT TRADE REFERENCE
     C                     MOVELMGTNUM    R1REFE
     C*
     C** FORMAT TRADE TYPE
     C                     MOVELMGTSTP    R1TYP1
     C*
     C** FORMAT TRADE TYPE DESCRIPTION
     C           MGTSTP    IFEQ 'TS'
     C                     MOVELTYPA,1    R1TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'TP'
     C                     MOVELTYPA,2    R1TYP2
     C                     END
     C*
     C** FORMAT SENDER BRANCH
     C                     MOVELMGSNBR    R1SND1
     C*
     C** FORMAT SENDER CUSTOMER NUMBER
     C                     MOVEL@SNDR     R1SND2
     C*
     C** FORMAT SENDER NETWORK ADDRESS
     C                     MOVEL@NWSN     R1SND3
     C*
     C** FORMAT SENDER REPORT NAME
     C                     MOVEL@SRNM     R1SRNM
     C*
     C** FORMAT SENDER REPORT TOWN
     C                     MOVEL@SRTN     R1SRTN
     C*
     C** FORMAT DESTINATION REPORT NAME
     C                     MOVEL@DRNM     R1DRNM
     C*
     C** FORMAT DESTINATION REPORT TOWN
     C                     MOVEL@DRTN     R1DRTN
     C*
     C** FORMAT MESSAGE PRIORITY
     C                     MOVEL'N'       R1PRTY
     C*
     C** FORMAT DESTINATION CUSTOMER NUMBER
     C                     MOVELMGDEST    R1DES1
     C*
     C** FORMAT DESTINATION NETWORK ADDRESS
     C                     MOVEL@NWDS     R1DES2
     C*
     C** FORMAT NETWORK
     C                     MOVEL@NWRK     R1NETW
     C*
     C** INITIALISE FIELDS ON PF/MGOREFPD
     C                     CLEARMGOREFD0
     C*
     C** HISTORY RETENTION DATE
     C           BJRDNB    ADD  ENDSMN    WWHDAT  50
     C                     MOVE WWHDAT    ZMDAY   50
     C                     MOVE *BLANKS   ZSDATE  6
     C*
     C                     CALL 'ZM0060'               24
     C                     PARM           ZMDAY
     C                     PARM           ZSDATE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN24     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '015'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 015  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** SET HISTORY RETENTION DATE TO OUTPUT FIELD
     C                     MOVELZSDATE    WWHRDT  6
     C*
     C** MESSAGE GENERATION DATE
     C                     MOVE BJRDNB    ZMDAY
     C                     MOVE *BLANKS   ZSDATE
     C*
     C                     CALL 'ZM0060'               24
     C                     PARM           ZMDAY
     C                     PARM           ZSDATE
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN24     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '016'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 016  *
     C                     MOVEL'ZM0060'  DBPGM            * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** SET MESSAGE GEN. DATE TO OUTPUT FIELD
     C                     MOVELZSDATE    WWMGDE  6
     C*
     C** INITIALISE FIELDS ON PF/MGOREFPD
     C                     CLEARMGOREFD0
     C*
     C** FORMAT FIELDS ON PF/MGOREFPD
     C                     EXSR #W04
     C/COPY WNCPYSRC,MG0579C005
     C*
     C** WRITE RECORD TO PF/MGOREFPD
     C                     MOVELMGCHG     AORR                                                CSW025
     C                     WRITEMGOREFD0               15
     C*
     C** IF ERROR ON WRITE OF RECORD
     C           *IN15     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '017'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 017  *
     C                     MOVEL'MGOREFPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0579'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT :28: LINE
     C                     MOVELWWDS28    R1MDES
     C                     MOVELWWTG28    R1MTAG
     C                     MOVELWWDT28    R1MDAT
     C*
     C** WRITE THE HEADER, MAIN TEXT AND :28: LINE FOR THE REPORT
     C                     WRITEHEADER1
     C                     WRITETEXT1
     C                     WRITEDETL1
     C*
     C** FORMAT FIELDS ON PF/MGOMSGPD
     C                     MOVELWWTG28    MTAG
     C                     MOVELWWDT28    MFLD
     C                     MOVELWWDATA    TRNO
     C                     MOVE *BLANKS   PTDL
     C*
     C** WRITE RECORD TO PF/MGOMSGPD
     C                     WRITEMGOMSGD0               16
     C*
     C** IF ERROR ON WRITE OF RECORD
     C           *IN16     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '018'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 018  *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0579'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT :20: LINE
     C                     MOVELWWDS20    R1MDES
     C                     MOVELWWTG20    R1MTAG
     C                     MOVELWWDT20    R1MDAT
     C*
     C** WRITE THE :20: LINE FOR THE REPORT
     C                     WRITEDETL1
     C*
     C** FORMAT FIELDS ON PF/MGOMSGPD
     C                     MOVELWWTG20    MTAG
     C                     MOVELWWDT20    MFLD
     C                     MOVELWWDATA    TRNO
     C                     MOVE *BLANKS   PTDL
     C*
     C** WRITE RECORD TO PF/MGOMSGPD
     C                     WRITEMGOMSGD0               16
     C*
     C** IF ERROR ON WRITE OF RECORD
     C           *IN16     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '019'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 019  *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0579'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT :21: LINE
     C                     MOVELWWDS21    R1MDES
     C                     MOVELWWTG21    R1MTAG
     C                     MOVELWWDT21    R1MDAT
     C*
     C** WRITE THE :21: LINE FOR THE REPORT
     C                     WRITEDETL1
     C*
     C** FORMAT FIELDS ON PF/MGOMSGPD
     C                     MOVELWWTG21    MTAG
     C                     MOVELWWDT21    MFLD
     C                     MOVELWWDATA    TRNO
     C                     MOVE *BLANKS   PTDL
     C*
     C** WRITE RECORD TO PF/MGOMSGPD
     C                     WRITEMGOMSGD0               16
     C*
     C** IF ERROR ON WRITE OF RECORD
     C           *IN16     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '020'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 020  *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0579'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** SET INDEX OF MULTIPLE OCCURRENCE DATA STRUCTURE TO 1
     C***********          Z-ADD1         I       20                      S01485
     C                     Z-ADD1         I       30                      S01485
     C           I         OCUR MULT
     C*
     C** SUBTRACT 35 FROM TOTAL NUMBER OF MESSAGES SENT AND STORE
     C           WWTTFF    SUB  35        WWTOTF  30
     C*
     C** DO WHILE WORK TOTAL FIELD > 0
     C           WWTOTF    DOWGT0
     C*
     C** WRITE DETAIL LINES TO THE REPORT CHECKING FOR OVERFLOW
     C                     DO   35        COUNT   20
     C           WWOFL1    SUB  WWPTL1    WWLINE
     C*
     C           WWLINE    IFLT 3
     C                     WRITEHEADER1
     C                     END
     C                     WRITELINE1
     C*
     C** FORMAT FIELDS ON PF/MGOMSGPD
     C                     MOVELWWMTAG    MTAG
     C                     MOVELMDAT      MFLD
     C                     MOVELWWDATA    TRNO
     C                     MOVE *BLANKS   PTDL
     C*
     C** WRITE RECORD TO PF/MGOMSGPD
     C                     WRITEMGOMSGD0               16
     C*
     C** IF ERROR ON WRITE OF RECORD
     C           *IN16     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '021'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 021  *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0579'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** INCREASE INDEX FOR MULT. OCCUR. DATA STRUCTURE
     C                     ADD  1         I
     C           I         OCUR MULT
     C                     END
     C*
     C**     IF SENT TO CEDEL, DO NOT UPDATE SEQUENCE NO ON TRANSACTION
     C**     USE CEDEL SEQUENCE NUMBER
     C           MGDEST    IFEQ WWCEDN
     C           BFCLAS    OREQ 'C'                                       CSW003
     C                     Z-ADDWW80CE    @LSWS
     C                     ELSE
     C*
     C** OBTAIN SEQUENCE NUMBERS FROM TRANSACTION RECORD
     C** FORMAT @TRAN AS FOLLOWS
     C** CHARS 1-6 EQUAL TO LEFTMOST 6 CHARS FROM MGTNUM
     C** CHARS 7-13 EQUAL TO BLANKS
     C** CHARS 14-15 EQUAL TO 'TR'
     C                     MOVE *BLANKS   WWTRAN
     C                     MOVE *BLANKS   WWFL06
     C                     MOVELMGTNUM    WWFL06
     C                     MOVELWWFL06    WWTRAN
     C                     MOVE 'TR'      WWTRAN
     C*
     C                     CALL 'MG5900'               20
     C                     PARM MGMODI    @MODI
     C                     PARM WWTRAN    @TRAN
     C                     PARM 'PR'      @PROG
     C                     PARM 'U'       @MODE
     C                     PARM *ZEROS    @LSWS
     C                     PARM *BLANKS   @ERCD
     C                     PARM *BLANKS   CSW003                          CSW003
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN20     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '010'     DBASE            ***************
     C                     MOVE *BLANKS   DBKEY            * DBERROR 010 *
     C                     MOVEL'MG5900'  DBPGM            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM
     C           @ERCD     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     MOVE '011'     DBASE            ***************
     C                     MOVE *BLANKS   DBKEY            * DBERROR 011 *
     C                     MOVELDBERR     DBKEY            ***************
     C                     MOVEL'MG5900'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     END
     C*
     C** FORMAT TAG FIELD OF MULT. OCCUR. DATA STRUCTURE
     C                     MOVEL':35F:'   WWMTAG
     C*
     C** FORMAT DESCRIPTION FIELD OF MULT. OCCUR. DATA STRUCTURE
     C                     MOVELDESA,3    MDES
     C*
     C** FORMAT DATA WORK FIELD FOR :28: FIELD
     C***********          ADD  1         WC0303
     C                     EXSR MDTA28
     C*
     C** FORMAT DATA WORK FIELD FOR :20: FIELD
     C                     ADD  2         @LSWS
     C                     MOVEL@LSWS     WA1016
     C*
     C** FORMAT FIELDS ON PF/MGOREFPD
     C                     EXSR #W04
     C/COPY WNCPYSRC,MG0579C006
     C*
     C** WRITE RECORD TO PF/MGOREFPD
     C                     WRITEMGOREFD0               15
     C*
     C** IF ERROR ON WRITE OF RECORD
     C           *IN15     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '022'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 022  *
     C                     MOVEL'MGOREFPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0579'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT :28: LINE
     C                     MOVELWWDS28    R1MDES
     C                     MOVELWWTG28    R1MTAG
     C                     MOVELWWDT28    R1MDAT
     C*
     C** WRITE THE HEADER, MAIN TEXT AND :28: LINE FOR THE REPORT
     C                     WRITEHEADER1
     C                     WRITETEXT1
     C                     WRITEDETL1
     C*
     C** FORMAT FIELDS ON PF/MGOMSGPD
     C                     MOVELWWTG28    MTAG
     C                     MOVELWWDT28    MFLD
     C                     MOVELWWDATA    TRNO
     C                     MOVE *BLANKS   PTDL
     C*
     C** WRITE RECORD TO PF/MGOMSGPD
     C                     WRITEMGOMSGD0               16
     C*
     C** IF ERROR ON WRITE OF RECORD
     C           *IN16     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '023'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 023  *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0579'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT :20: LINE
     C                     MOVELWWDS20    R1MDES
     C                     MOVELWWTG20    R1MTAG
     C                     MOVELWWDT20    R1MDAT
     C*
     C** WRITE THE :20: LINE FOR THE REPORT
     C                     WRITEDETL1
     C*
     C** FORMAT FIELDS ON PF/MGOMSGPD
     C                     MOVELWWTG20    MTAG
     C                     MOVELWWDT20    MFLD
     C                     MOVELWWDATA    TRNO
     C                     MOVE *BLANKS   PTDL
     C*
     C** WRITE RECORD TO PF/MGOMSGPD
     C                     WRITEMGOMSGD0               16
     C*
     C** IF ERROR ON WRITE OF RECORD
     C           *IN16     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '024'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 024  *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0579'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** FORMAT :21: LINE
     C                     MOVELWWDS21    R1MDES
     C                     MOVELWWTG21    R1MTAG
     C                     MOVELWWDT21    R1MDAT
     C*
     C** WRITE THE :21: LINE FOR THE REPORT
     C                     WRITEDETL1
     C*
     C** FORMAT FIELDS ON PF/MGOMSGPD
     C                     MOVELWWTG21    MTAG
     C                     MOVELWWDT21    MFLD
     C                     MOVELWWDATA    TRNO
     C                     MOVE *BLANKS   PTDL
     C*
     C** WRITE RECORD TO PF/MGOMSGPD
     C                     WRITEMGOMSGD0               16
     C*
     C** IF ERROR ON WRITE OF RECORD
     C           *IN16     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '025'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 025  *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0579'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** SUBTRACT 35 FROM TOTAL NUMBER OF MESSAGES SENT AND STORE
     C           WWTOTF    SUB  35        WWTOTF
     C*
     C                     END
     C*
     C** ADD 35 TO WORK TOTAL FIELD
     C                     ADD  35        WWTOTF
     C*
     C** WRITE DETAIL LINES TO THE REPORT CHECKING FOR OVERFLOW
     C                     DO   WWTOTF    COUNT2  20
     C           WWOFL1    SUB  WWPTL1    WWLINE
     C*
     C           WWLINE    IFLT 3
     C                     WRITEHEADER1
     C                     END
     C                     WRITELINE1
     C*
     C** FORMAT FIELDS ON PF/MGOMSGPD
     C                     MOVELWWMTAG    MTAG
     C                     MOVELMDAT      MFLD
     C                     MOVELWWDATA    TRNO
     C                     MOVE *BLANKS   PTDL
     C*
     C** WRITE RECORD TO PF/MGOMSGPD
     C                     WRITEMGOMSGD0               16
     C*
     C** IF ERROR ON WRITE OF RECORD
     C           *IN16     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '026'     DBASE            * * * * * * * *
     C                     MOVELMGTNUM    DBKEY            *  DBERR 026  *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     MOVEL'MG0579'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** INCREASE INDEX FOR MULT. OCCUR. DATA STRUCTURE
     C                     ADD  1         I
     C           I         OCUR MULT
     C                     END
     C*
     C**     IF SENT TO CEDEL, DO NOT UPDATE SEQUENCE NO ON TRANSACTION
     C**     USE CEDEL SEQUENCE NUMBER
     C*
     C           MGDEST    IFEQ WWCEDN
     C           BFCLAS    OREQ 'C'                                       CSW003
     C                     Z-ADDWW80CE    @LSWS
     C                     ELSE
     C** OBTAIN SEQUENCE NUMBERS FROM TRANSACTION RECORD
     C** FORMAT @TRAN AS FOLLOWS
     C** CHARS 1-6 EQUAL TO LEFTMOST 6 CHARS FROM MGTNUM
     C** CHARS 7-13 EQUAL TO BLANKS
     C** CHARS 14-15 EQUAL TO 'TR'
     C                     MOVE *BLANKS   WWTRAN
     C                     MOVE *BLANKS   WWFL06
     C                     MOVELMGTNUM    WWFL06
     C                     MOVELWWFL06    WWTRAN
     C                     MOVE 'TR'      WWTRAN
     C*
     C                     CALL 'MG5900'               20
     C                     PARM MGMODI    @MODI
     C                     PARM WWTRAN    @TRAN
     C                     PARM 'PR'      @PROG
     C                     PARM 'U'       @MODE
     C                     PARM *ZEROS    @LSWS
     C                     PARM *BLANKS   @ERCD
     C                     PARM *BLANKS   CSW003                          CSW003
     C*
     C** IF PROGRAM NOT FOUND
     C           *IN20     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '012'     DBASE            ***************
     C                     MOVE *BLANKS   DBKEY            * DBERROR 012 *
     C                     MOVEL'MG5900'  DBPGM            ***************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF ERROR DURING PROGRAM
     C           @ERCD     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     MOVE '013'     DBASE            ***************
     C                     MOVE *BLANKS   DBKEY            * DBERROR 013 *
     C                     MOVELDBERR     DBKEY            ***************
     C                     MOVEL'MG5900'  DBPGM
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  #W02      - PRINT MESSAGE NOT GENERATED DETAILS             *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - MAIN PROCESSING                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           #W02      BEGSR
     C*
     C** INITIALISE FLAGS AND FIELDS USED IN REPORT
     C                     MOVE *BLANKS   R2REFE
     C                     MOVE *BLANKS   R2TYP1
     C                     MOVE *BLANKS   R2TYP2
     C                     MOVE *BLANKS   R2SND1
     C                     MOVE *BLANKS   R2SND2
     C                     MOVE *BLANKS   R2SND3
     C                     MOVE *BLANKS   R2DES1
     C                     MOVE *BLANKS   R2DES2
     C                     MOVE *BLANKS   R2EMSG
     C                     MOVE *BLANKS   R2NETW
     C                     MOVE *BLANKS   R2MODI
     C                     MOVE *BLANKS   R2TNUM
     C                     MOVE *BLANKS   R2MTAG
     C                     MOVE *BLANKS   R2MDAT
     C                     MOVE *BLANKS   R2MERR
     C*
     C** OPEN PRINTER FILE
     C           *IN50     IFEQ '0'
     C************         OPEN MG0579P2                                  091250
     C                     OPEN MG0579A2                                  091250
     C                     MOVE '1'       *IN50
     C                     END
     C*
     C** FORMAT TRADE REFERENCE
     C                     MOVELMGTNUM    R2REFE
     C*
     C** FORMAT TRADE TYPE
     C                     MOVELMGTSTP    R2TYP1
     C*
     C** FORMAT TRADE TYPE DESCRIPTION
     C           MGTSTP    IFEQ 'TS'
     C                     MOVELTYPA,1    R2TYP2
     C                     END
     C*
     C           MGTSTP    IFEQ 'TP'
     C                     MOVELTYPA,2    R2TYP2
     C                     END
     C*
     C** FORMAT SENDER BRANCH
     C                     MOVELMGSNBR    R2SND1
     C*
     C** FORMAT SENDER CUSTOMER NUMBER
     C                     MOVEL@SNDR     R2SND2
     C*
     C** FORMAT SENDER NETWORK ADDRESS
     C                     MOVEL@NWSN     R2SND3
     C*
     C** FORMAT DESTINATION CUSTOMER NUMBER
     C                     MOVELMGDEST    R2DES1
     C*
     C** FORMAT DESTINATION NETWORK ADDRESS
     C                     MOVEL@NWDS     R2DES2
     C*
     C** FORMAT DESTINATION ERROR MESSAGE
     C                     MOVELDSERR     R2EMSG
     C*
     C** FORMAT NETWORK
     C                     MOVEL@NWRK     R2NETW
     C*
     C** FORMAT NETWORK ERROR MESSAGE
     C                     MOVELNWERR     R2NWER
     C*
     C** FORMAT MODULE ID
     C                     MOVELMGMODI    R2MODI
     C*
     C** FORMAT TRANSACTION NUMBER
     C                     MOVELMGTNUM    R2TNUM
     C*
     C** FORMAT :28: LINE
     C                     MOVELWWTG28    R2MTAG
     C                     MOVELWWDT28    R2MDAT
     C                     MOVELWWMR28    R2MERR
     C*
     C** WRITE THE HEADER, MAIN TEXT AND :28: LINE FOR THE REPORT
     C                     WRITEHEADER2
     C                     WRITETEXT2
     C                     WRITEDETL2
     C*
     C** FORMAT :20: LINE
     C                     MOVELWWTG20    R2MTAG
     C                     MOVELWWDT20    R2MDAT
     C                     MOVELWWMR20    R2MERR
     C*
     C** WRITE THE :20: LINE FOR THE REPORT
     C                     WRITEDETL2
     C*
     C** FORMAT :21: LINE
     C                     MOVELWWTG21    R2MTAG
     C                     MOVELWWDT21    R2MDAT
     C                     MOVELWWMR21    R2MERR
     C*
     C** WRITE THE :21: LINE FOR THE REPORT
     C                     WRITEDETL2
     C*
     C** SET INDEX OF MULTIPLE OCCURRENCE DATA STRUCTURE TO 1
     C                     Z-ADD1         I
     C           I         OCUR MULT
     C*
     C** SUBTRACT 35 FROM TOTAL NUMBER OF MESSAGES SENT AND STORE
     C           WWTTFF    SUB  35        WWTOTF
     C*
     C** DO WHILE WORK TOTAL FIELD > 0
     C           WWTOTF    DOWGT0
     C*
     C** FORMAT ERROR FIELD OF MULT OCCUR. DATA STRUCTURE
     C           WWFIN     IFEQ 'N'
     C                     MOVELERRA,3    MERR
     C                     MOVE 'Y'       @ERR
     C                     END
     C*
     C** WRITE DETAIL LINES TO THE REPORT CHECKING FOR OVERFLOW
     C                     DO   35        COUNT3  20
     C           WWOFL2    SUB  WWPTL2    WWLINE
     C*
     C           WWLINE    IFLT 3
     C                     WRITEHEADER2
     C                     END
     C                     WRITELINE2
     C*
     C** INCREASE INDEX FOR MULT. OCCUR. DATA STRUCTURE
     C                     ADD  1         I
     C           I         OCUR MULT
     C                     END
     C*
     C** FORMAT TAG FIELD OF MULT. OCCUR. DATA STRUCTURE
     C                     MOVEL':35F:'   WWMTAG
     C*
     C** FORMAT DATA WORK FIELD FOR :28: FIELD
     C***********          ADD  1         WC0303
     C                     EXSR MDTA28
     C*
     C** FORMAT DATA WORK FIELD FOR :20: FIELD
     C                     ADD  1         @LSWS
     C                     MOVEL@LSWS     WA1016
     C*
     C** FORMAT :28: LINE
     C                     MOVELWWTG28    R2MTAG
     C                     MOVELWWDT28    R2MDAT
     C                     MOVELWWMR28    R2MERR
     C*
     C** WRITE THE HEADER, MAIN TEXT AND :28: LINE FOR THE REPORT
     C                     WRITEHEADER2
     C                     WRITETEXT2
     C                     WRITEDETL2
     C*
     C** FORMAT :20: LINE
     C                     MOVELWWTG20    R2MTAG
     C                     MOVELWWDT20    R2MDAT
     C                     MOVELWWMR20    R2MERR
     C*
     C** WRITE THE :20: LINE FOR THE REPORT
     C                     WRITEDETL2
     C*
     C** FORMAT :21: LINE
     C                     MOVELWWTG21    R2MTAG
     C                     MOVELWWDT21    R2MDAT
     C                     MOVELWWMR21    R2MERR
     C*
     C** WRITE THE :21: LINE FOR THE REPORT
     C                     WRITEDETL2
     C*
     C** SUBTRACT 35 FROM TOTAL NUMBER OF MESSAGES SENT AND STORE
     C           WWTOTF    SUB  35        WWTOTF
     C*
     C                     END
     C*
     C** ADD 35 TO WORK TOTAL FIELD
     C                     ADD  35        WWTOTF
     C*
     C** FORMAT ERROR FIELD OF MULT OCCUR. DATA STRUCTURE
     C           WWFIN     IFEQ 'N'
     C                     MOVELERRA,3    MERR
     C                     MOVE 'Y'       @ERR
     C                     END
     C*
     C** WRITE DETAIL LINES TO THE REPORT CHECKING FOR OVERFLOW
     C                     DO   WWTOTF    COUNT4  20
     C           WWOFL2    SUB  WWPTL2    WWLINE
     C*
     C           WWLINE    IFLT 3
     C                     WRITEHEADER2
     C                     END
     C                     WRITELINE2
     C*
     C** INCREASE INDEX FOR MULT. OCCUR. DATA STRUCTURE
     C                     ADD  1         I
     C           I         OCUR MULT
     C                     END
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  #W04      - FORMAT FIELDS FOR MGOREFPD                      *
     C**                                                              *
     C**  CALLS     - NONE                                            *
     C**                                                              *
     C**  CALLED BY - #W01
     C**                                                              *
     C*****************************************************************
     C*
     C           #W04      BEGSR
     C*
     C** FORMAT FIELDS ON PF/MGOREFPD
     C                     MOVELMGMODI    MODI
     C                     MOVELWWDATA    TRNO
     C                     MOVELMGTNUM    MTRN
     C                     MOVELMGTSTP    SBTP
     C                     MOVEL@NWRK     NWRK
     C                     MOVELA8BICN    SECN
     C                     MOVEL@NWSN     NWSN
     C**********           Z-ADDMGDEST    DECN                                                CSD027
     C                     MOVE MGDEST    DECN                                                CSD027
     C                     MOVEL@NWDS     NWDS
     C                     MOVEL'579'     MTPY
     C                     MOVEL'N'       MPRY
     C***********          MOVEL'1'       MGSG                            082282
     C***********          MOVEL'CRTD'    MGST                            082282
     C                     EXSR MSTAT                                     082282
     C                     MOVELWWHRDT    HRDT
     C                     MOVELWWMGDE    MGDE
      *                                                                   113352
      ** Set-up Message Generation  Century flag                          113352
      *                                                                   113352
     C                     MOVELMGDE      YY      20                      113352
     C           YY        IFLT 72                                        113352
     C                     MOVE '2'       CIND                            113352
     C                     ELSE                                           113352
     C                     MOVE '1'       CIND                            113352
     C                     ENDIF                                          113352
     C                     TIME           MGTM
     C                     MOVELBJMRDT    LADT
     C                     TIME           LATM
     C                     MOVELMGSNBR    BRCA
     C                     MOVEL'Y'       TSKS
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT                                                              082282
     C*****************************************************************   082282
     C**                                                              *   082282
     C**  MSTAT     - Determine message status                        *   082282
     C**                                                              *   082282
     C*****************************************************************   082282
      *                                                                   082282
     C           MSTAT     BEGSR                                          082282
      *                                                                   082282
      * Reset status array                                                082282
     C                     CLEAR@ST                                       082282
      *                                                                   082282
      * Set key to destination customer number                            082282
     C                     MOVELDECN      @KEY      P                     082282
      *                                                                   082282
      * Get customer details via access object                            082282
     C                     CALL 'AOCUSTR0'                                082282
     C                     PARM *BLANKS   @RTCD   7                       082282
     C                     PARM '*KEY   ' @OPTN   7                       082282
     C                     PARM           @KEY   10                       082282
     C                     PARM *BLANKS   @KYST   7                       082282
     C           SDCUST    PARM SDCUST    DSSDY                           082282
      *                                                                   082282
      * Fill status array                                                 082282
     C           @RTCD     IFEQ *BLANKS                                   082282
     C                     MOVELBBDS01    @ST,1                           082282
     C                     MOVELBBDS02    @ST,2                           082282
     C                     MOVELBBDS03    @ST,3                           082282
     C                     MOVELBBDS04    @ST,4                           082282
     C                     MOVELBBDS05    @ST,5                           082282
     C                     MOVELBBDS06    @ST,6                           082282
     C                     MOVELBBDS07    @ST,7                           082282
     C                     MOVELBBDS08    @ST,8                           082282
     C                     MOVELBBDS09    @ST,9                           082282
     C                     MOVELBBDS10    @ST,10                          082282
     C                     MOVELBBDS11    @ST,11                          082282
     C                     MOVELBBDS12    @ST,12                          082282
     C                     MOVELBBDS13    @ST,13                          082282
     C                     MOVELBBDS14    @ST,14                          082282
     C                     MOVELBBDS15    @ST,15                          082282
     C                     ENDIF                                          082282
      *                                                                   082282
      * Is the message type in the array? Set on *IN60 if it is.          082282
     C           '579'     LOKUP@ST                      60               082282
     C  N60      '57*'     LOKUP@ST                      60               082282
     C  N60      '5**'     LOKUP@ST                      60               082282
      *                                                                   082282
      * If found, set status to 'ready to send', else set to 'created'    082282
     C           *IN60     IFEQ *ON                                       082282
     C                     MOVEL'RSND'    MGST                            082282
     C                     MOVEL'2'       MGSG                            082282
     C                     ELSE                                           082282
     C                     MOVEL'CRTD'    MGST                            082282
     C                     MOVEL'1'       MGSG                            082282
     C                     ENDIF                                          082282
      *                                                                   082282
     C                     ENDSR                                          082282
     C/COPY WNCPYSRC,MG0579C007
      *                                                                   082282
     C*****************************************************************   082282
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**  PSSR      - ERROR HANDLING                                  *
     C**                                                              *
     C**  CALLS     -                                                 *
     C**                                                              *
     C**  CALLED BY -                                                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C** ROLLBACK ALL FILE CHANGES
     C                     ROLBK
     C*
     C** WRITE DATABASE ERROR TO REPORT
     C                     OPEN MG0579AU
     C                     WRITEHEAD
     C                     WRITEDBERROR
     C*
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C/COPY MGCPYSRC,SWIFTTRANS                                           BLI605
** CPY@   Object Copyright
(c) Misys International Banking Systems Ltd. 2001
** ERRA   ERROR MESSAGES
Customer number not found on SDCUSTPD
Unable to format mandatory field :35F:
Unable to format :35F: - too many certificate Nos.
Unable to format mandatory field :20:
Unable to format mandatory field :21:
*** Message is Unroutable ***
** DESA   DESCRIPTIONS
                              T.R.N.
                   RELATED REFERENCE
                 CERTIFICATE NUMBERS
                   SEQUENCE OF TOTAL
** TYPA   TRADE/MOVEMENT TYPE DESCRIPTIONS
TRADE SALE
TRADE PURCHASE
** SUPP - add any fields tags which should be suppressed
