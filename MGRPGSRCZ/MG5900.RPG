     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MG MT5xx Trans file SWIFT seq update')
     F*****************************************************************
     F*                                                               *
     F*  Midas - MESSAGE GENERATION
     F*                                                               *
     F*   MG5900 - UPDATE S.W.I.F.T. SEQUENCE NUMBERS ON              *
     F*            TRANSACTION, DEPOT MOVEMENTS AND CUSTOMER FILES    *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 222727             Date 05Nov03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSE028             Date 06Jun01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW003             Date 08Jan96               *
      *                 089863             Date 31Jul95               *
     F*                   S01401           DATE 17JUN93               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  222727 - Release 5.0 errors  (Recompile)                     *
      *  CSE028 - Standing Settlement Instructions Enhancement.       *
      *           (Recompiled).                                       *
     F*   CSW003 - MT5xx Message Generation - Phase 2                 *
     F*   089863 - SE Pay/Rec cannot be restarted and often result    *
     F*            in DB error in MG0580 at 044 in pgm MG5910         *
     F*   S01401 - WRITTEN FOR MT5XX MESSAGES GENERATION              *
     F*                                                               *
     F*****************************************************************
     F*
     F** TRADES FILE                    PREFIX - T1
     F*RADSDY1UF**E*          KK       DISKK        KINFSR *PSSR          089863
     FTRADSDY1UF  E           K        DISK         KCOMIT                089863
     F                                              KINFSR *PSSR          089863
     F*
     F** DEPOT MOVEMENTS FILE           PREFIX - DP
     F*PTMVDY4UF**E*          KK       DISKK        KINFSR *PSSR          089863
     FDPTMVDY4UF  E           K        DISK         KCOMIT                089863
     F                                              KINFSR *PSSR          089863
     F*
     F** CUSTOMER DETAILS FILE          PREFIX - NONE
     FSDCUSTXAUF  E           K        DISK         KINFSR *PSSR A
     F*
     F********************************************************************
     F/EJECT
     F********************************************************************
     F*
     F* ID C  F  H  L    FUNCTION OF INDICATORS
     F*
     F*    08            UPDAT/CHAIN to LF/TRADSDY1
     F*    09            UPDAT/CHAIN to LF/DPTMVDY4
     F*    10            UPDAT/CHAIN to LF/SDCUSTXA
     F*
     F********************************************************************
     F/EJECT
     F********************************************************************
     E*
     E** COPYRIGHT
     E                    CPY@    1   1 80
     E*
     E********************************************************************
     E*
     E********************************************************************
     I*
     I** RENAME FIELD T1LSWS ON FILE TRADSDY1 TO LSWS
     ITRADSDD1
     I              T1LSWS                          LSWS
     I              T1LSSC                          LSSC
     I*
     I** RENAME FIELD DPLSWS ON FILE DPTMVDY4 TO LSWS
     IDPTMVDD1
     I              DPLSWS                          LSWS
     I              DPLSSC                          LSSC
     I*
     I** RENAME FIELDS CULSWS ON FILE SDCUSTXA TO LSWS
     ISDCUSTDA
     I              CULSWS                          LSWS
     I*
     I** LOCAL DATA AREA
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*                                                                   CSW003
     IDPTK        DS                                                      CSW003
     I                                        1   6 WWKEY1                CSW003
     I                                        7   7 PWHEN                 CSW003
     I*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* Main Process                                                  *
     C* Calls       INIT   Initial processing                         *
     C*                                                               *
     C*  @MODE = ' '   OBTAIN LAST SWIFT SEQUENCE NUMBER              *
     C*  @MODE = 'C'   OBTAIN LAST SWIFT SEQUENCE NUMBER FOR CEDEL    *
     C*  @MODE = 'U'   UPDATE LAST SWIFT SEQUENCE NUMBER              *
     C*  @MODE = 'L'   UPDATE LAST SWIFT SEQUENCE NUMBER FOR CEDEL    *
     C*                SEQUENCE IS PASSED IN THE PARAMETER            *
     C*****************************************************************
     C*
     C** RECEIVE THE MODULE ID, TRANSACTION NUMBER AND THE CALLING
     C** PROGRAM ID (CONFIRMATION OR PAY/RECEIVE PROGRAM)
     C           *ENTRY    PLIST
     C                     PARM           @MODI   2
     C                     PARM           @TRAN  15
     C                     PARM           @PROG   2
     C                     PARM           @MODE   1
     C                     PARM           @LSWS   70
     C                     PARM           @ERCD   1
     C                     PARM           CSW003  1                       CSW003
     C*                                                                   CSW003
      ** KLIST FOR DPTMVDY4                                               CSW003
     C           KDPT      KLIST                                          CSW003
     C                     KFLD           WWKEY1                          CSW003
     C                     KFLD           PWHEN   1                       CSW003
     C*
     C** INITIAL PROCESSING
     C                     EXSR INIT
     C*
     C** IF MODULE ID EQUALS 'SE'
     C           @MODI     IFEQ 'SE'
     C*
     C** MOVE LAST TWO CHARS OF PARAMETER @TRAN TO WWTRAN
     C                     MOVE @TRAN     WWTRAN  2
     C*
     C** IF WWTRAN EQUALS 'TR'
     C           WWTRAN    IFEQ 'TR'
     C           WWTRAN    OREQ 'TS'                                      CSW003
     C*
     C** KEY FOR CHAINING MUST BE 6A
     C                     MOVEL@TRAN     WWKEY1  6
     C*                                                                   CSW003
     C           CSW003    IFEQ 'Y'                                       CSW003
     C           WWTRAN    IFEQ 'TR'                                      CSW003
     C                     MOVE 'A'       PWHEN                           CSW003
     C                     END                                            CSW003
     C*                                                                   CSW003
     C           WWTRAN    IFEQ 'TS'                                      CSW003
     C                     MOVE 'B'       PWHEN                           CSW003
     C                     END                                            CSW003
     C                     ELSE                                           CSW003
     C                     MOVE *BLANKS   PWHEN                           CSW003
     C                     END                                            CSW003
     C*                                                                   CSW003
     C***********WWKEY1    CHAINTRADSDY1             08                   CSW003
     C           KDPT      CHAINTRADSDY1             08                   CSW003
     C*
     C** IF TRADING TRANSACTION NOT FOUND
     C           *IN08     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '001'     DBASE            * * * * * * * *
     C***********          MOVELWWKEY1    DBKEY            *  DBERR 001  *CSW003
     C                     MOVELDPTK      DBKEY            *  DBERR 001  *CSW003
     C                     MOVEL'TRADSDY1'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     ELSE
     C*
     C** IF WWTRAN EQUALS 'DM'
     C           WWTRAN    IFEQ 'DM'
     C           WWTRAN    OREQ 'DN'                                      CSW003
     C*
     C** KEY FOR CHAINING MUST BE 6A
     C                     MOVEL@TRAN     WWKEY1
     C*                                                                   CSW003
     C           CSW003    IFEQ 'Y'                                       CSW003
     C           WWTRAN    IFEQ 'DM'                                      CSW003
     C                     MOVE 'S'       PWHEN                           CSW003
     C                     END                                            CSW003
     C*                                                                   CSW003
     C           WWTRAN    IFEQ 'DN'                                      CSW003
     C                     MOVE 'M'       PWHEN                           CSW003
     C                     END                                            CSW003
     C                     ELSE                                           CSW003
     C                     MOVE *BLANKS   PWHEN                           CSW003
     C                     END                                            CSW003
     C*                                                                   CSW003
     C***********WWKEY1    CHAINDPTMVDY4             09                   CSW003
     C           KDPT      CHAINDPTMVDY4             09                   CSW003
     C*
     C** IF DEPOT MOVEMENTS TRANSACTION NOT FOUND
     C           *IN09     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '002'     DBASE            * * * * * * * *
     C***********          MOVELWWKEY1    DBKEY            *  DBERR 002  *CSW003
     C                     MOVELDPTK      DBKEY            *  DBERR 002  *CSW003
     C                     MOVEL'DPTMVDY4'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     ELSE
     C*
     C** IF WWTRAN EQUALS 'PS'
     C           WWTRAN    IFEQ 'PS'
     C*
     C** KEY FOR CHAINING MUST BE 6A
     C** IF CUSTOMER NOT FOUND THEN NO DATABASE ERROR - INSTEAD NEW
     C** RECORD WILL BE WRITTEN FOR CUSTOMER IN SR/UPMODE
     C                     MOVEL@TRAN     WWKEY1
     C           WWKEY1    CHAINSDCUSTXA             10
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C** IF MODE IS 'C', OBTAIN CURRENT CEDEL SEQUENCE NUMBER
     C           @MODE     IFEQ 'C'
     C                     Z-ADDLSSC      @LSWS
     C                     END
     C** SAVE CURRENT S.W.I.F.T. SEQUENCE NUMBER IF NOT MODE L
     C           @MODE     IFEQ ' '
     C           @MODE     OREQ 'U'
     C                     Z-ADDLSWS      @LSWS
     C                     END
     C*
     C** UPDATE THE S.W.I.F.T. SEQUENCE NUMBERS
     C           @MODE     IFEQ 'U'
     C           @MODE     OREQ 'L'
     C                     EXSR UPMODE
     C                     END
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*   UPMODE  Update the S.W.I.F.T. sequence numbers              *
     C*   Called by - main process                                    *
     C*   Calls     -                                                 *
     C*                                                               *
     C*****************************************************************
     C*
     C           UPMODE    BEGSR
     C*
     C*  IF THIS IS NOT A CEDEL SWIFT SEQUENCE
     C           @MODE     IFEQ 'U'
     C** INCREMENT S.W.I.F.T. SEQ. NUMBER ON THE TRANSACTION FILE
     C           LSWS      IFEQ 9999999
     C                     Z-ADD1         LSWS
     C*
     C                     ELSE
     C*
     C                     ADD  1         LSWS
     C                     END
     C*
     C                     ELSE
     C                     Z-ADD@LSWS     LSSC
     C                     END
     C*
     C** IF MODULE ID EQUALS 'SE'
     C           @MODI     IFEQ 'SE'
     C*
     C** TRADES TRANSACTION
     C           WWTRAN    IFEQ 'TR'
     C           WWTRAN    OREQ 'TS'                                      CSW003
     C                     UPDATTRADSDD1               08
     C*
     C** IF UPDATE OF TRADES RECORD FAILS
     C           *IN08     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '004'     DBASE            * * * * * * * *
     C***********          MOVELWWKEY1    DBKEY            *  DBERR 004  *CSW003
     C                     MOVELDPTK      DBKEY            *  DBERR 004  *CSW003
     C                     MOVEL'TRADSDY1'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     ELSE
     C*
     C** DEPOT MOVEMENTS TRANSACTION
     C           WWTRAN    IFEQ 'DM'
     C           WWTRAN    OREQ 'DN'                                      CSW003
     C                     UPDATDPTMVDD1               09
     C*
     C** IF UPDATE OF DEPOT MOVEMENTS RECORD FAILS
     C           *IN09     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '005'     DBASE            * * * * * * * *
     C***********          MOVELWWKEY1    DBKEY            *  DBERR 005  *CSW003
     C                     MOVELDPTK      DBKEY            *  DBERR 005  *CSW003
     C                     MOVEL'DPTMVDY4'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     ELSE
     C*
     C** CUSTOMER TRANSACTION (IF PREVIOUS CHAIN FAILED WRITE RECORD)
     C           WWTRAN    IFEQ 'PS'
     C*
     C           *IN10     IFEQ '1'
     C                     MOVELWWKEY1    IBCNUM
     C                     Z-ADD*ZEROS    LSWS
     C                     Z-ADD*ZEROS    CULSWC
     C                     WRITESDCUSTDA               10
     C                     ELSE
     C                     UPDATSDCUSTDA               10
     C                     END
     C*
     C** IF WRITE OR UPDATE OF CUSTOMER RECORD FAILS
     C           *IN10     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '006'     DBASE            * * * * * * * *
     C                     MOVELWWKEY1    DBKEY            *  DBERR 006  *
     C                     MOVEL'SDCUSTXA'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*   INIT - inital process                                       *
     C*   Called by - main process                                    *
     C*   Calls     -                                                 *
     C*                                                               *
     C*****************************************************************
     C*
     C           INIT      BEGSR
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C**  Set up LDA for database error processing
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'MG5900'  DBPGM
     C                     MOVE *BLANKS   DBASE
     C                     OUT  LDA
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*   *PSSR - error handling                                      *
     C*                                                               *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C                     DUMP
     C                     MOVE 'Y'       @ERCD
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*
** CPY@   Object Copyright
(c) Finastra International Limited 2001
