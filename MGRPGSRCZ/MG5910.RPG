     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MG MT5xx trans file msg gen ind upd')
     F*****************************************************************
     F*                                                               *
     F*  Midas - MESSAGE GENERATION                                   *
     F*                                                               *
     F*   MG5910 - UPDATE TRANSACTION AND DEPOT MOVEMENTS FILES       *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 222727             Date 05Nov03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 200292             Date 19Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSE028             Date 06Jun01               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 156853             Date 06Oct99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW098             Date 16Apr98               *
     F*                   116583           DATE 19FEB98               *
     F*                   CSW003           DATE 08JAN96               *
     F*                   089863           DATE 28JUL95               *
     F*                   S01401           DATE 10SEP92               *
     F*                                                               *
     F*****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  222727 - Release 5.0 errors  (Recompile)                     *
     F*  200292 - Need to set RCDG to 'N' for ISO15022 messages, as   *
     F*           there are no cancelation generation in Midas.       *
      *  CSE028 - Standing Settlement Instructions Enhancement.       *
      *           (Recompiled).                                       *
     F*  156853 - If T1CONG or T1RCDG = ' ', move 'N' to field.       *
     F*           Applied fix 122220.                                 *
     F*   CSW098 - SWIFT 1998 changes                                 *
     F*   116583 - PART OF PHASE 2 MISSING                            *
     F*   CSW003 - MT5xx Message Generation - Phase 2                 *
     F*   089863 - SE Pay/Rec cannot be restarted and often result    *
     F*            in DB error in MG0580 at 044 in pgm MG5910         *
     F*   S01401 - WRITTEN FOR MT5XX MESSAGES GENERATION              *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*****************************************************************
     F*
     F** TRADES FILE                    PREFIX - T1
     F*RADSDY1UF**E***        K        DISK                               089863
     FTRADSDY1UF  E           K        DISK         KCOMIT                089863
     F*
     F** DEPOT MOVEMENTS FILE           PREFIX - DP
     F*PTMVDY4UF**E***        K        DISK                               089863
     FDPTMVDY4UF  E           K        DISK         KCOMIT                089863
     F********************************************************************
     F/EJECT
     F********************************************************************
     F*
     F* ID C  F  H  L    FUNCTION OF INDICATORS
     F*
     F*    08            UPDAT/CHAIN to LF/TRADSDY1
     F*    09            UPDAT/CHAIN to LF/DPTMVDY4
     F*
     F********************************************************************
     F/EJECT
     F********************************************************************
     E*
     E** COPYRIGHT
     E                    CPY@    1   1 80
     E********************************************************************
     E*
     E********************************************************************
     I*
      ***  Rename flag fields from TRADSDY1 and DPTMVDY4 to have the
      ***  same names, for ease of use within the program.
      *
     ITRADSDD1
     I              T1CONG                          CONG
     I              T1RCDG                          RCDG
     I              T1SCMG                          SCMG
     I              T1NEW                           MNEW                  200292
     I*
     IDPTMVDD1
     I              DPCONG                          CONG
     I              DPRCDG                          RCDG
     I              DPSCMG                          SCMG
     I              DPNEW                           MNEW                  200292
     I*
     I** LOCAL DATA AREA
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*                                                                   CSW003
     IDPTK        DS                                                      CSW003
     I                                        1   6 WWKEY1                CSW003
     I                                        7   7 PWHEN                 CSW003
     I*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C* Main Process                                                  *
     C* Calls       INIT   Initial processing                         *
     C*                                                               *
     C*****************************************************************
     C*
     C** RECEIVE THE MODULE ID, TRANSACTION NUMBER AND THE CALLING
     C** PROGRAM ID (CONFIRMATION OR PAY/RECEIVE PROGRAM)
     C           *ENTRY    PLIST
     C                     PARM           @MODI   2
     C                     PARM           @TREF  15
     C                     PARM           @PROG   2
     C                     PARM           @MODE   1
     C                     PARM           @CONG   1
     C                     PARM           @RCDG   1
     C                     PARM           @ERCD   1
     C                     PARM           CSW003  1                       CSW003
     C*                                                                   CSW003
      ** KLIST FOR DPTMVDY4                                               CSW003
     C           KDPT      KLIST                                          CSW003
     C                     KFLD           WWKEY1                          CSW003
     C                     KFLD           PWHEN   1                       CSW003
     C*
     C** INITIAL PROCESSING
     C                     EXSR INIT
     C*
     C** IF MODULE ID EQUALS 'SE'
     C           @MODI     IFEQ 'SE'
     C*
     C** MOVE LAST TWO CHARS OF PARAMETER @TRAN TO WWTRAN
     C                     MOVE @TREF     WWTRAN  2
     C*
     C** IF WWTRAN EQUALS 'TR'
     C           WWTRAN    IFEQ 'TR'
     C           WWTRAN    OREQ 'TS'                                      CSW003
     C*
     C** KEY FOR CHAINING MUST BE 6A
     C                     MOVEL@TREF     WWKEY1  6
     C*                                                                   CSW003
     C           CSW003    IFEQ 'Y'                                       CSW003
     C           WWTRAN    IFEQ 'TR'                                      CSW003
     C                     MOVE 'A'       PWHEN                           CSW003
     C                     END                                            CSW003
     C*                                                                   CSW003
     C           WWTRAN    IFEQ 'TS'                                      CSW003
     C                     MOVE 'B'       PWHEN                           CSW003
     C                     END                                            CSW003
     C                     ELSE                                           CSW003
     C                     MOVE *BLANKS   PWHEN                           CSW003
     C                     END                                            CSW003
     C*                                                                   CSW003
     C***********WWKEY1    CHAINTRADSDY1             08                   CSW003
     C           KDPT      CHAINTRADSDY1             08                   CSW003
     C*
     C** IF TRADING TRANSACTION NOT FOUND
     C           *IN08     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '001'     DBASE            * * * * * * * *
     C***********          MOVELWWKEY1    DBKEY            *  DBERR 001  *CSW003
     C                     MOVELDPTK      DBKEY            *  DBERR 001  *CSW003
     C                     MOVEL'TRADSDY1'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     END
     C*
     C** IF WWTRAN EQUALS 'DM'
     C           WWTRAN    IFEQ 'DM'
     C           WWTRAN    OREQ 'DN'                                      CSW003
     C*
     C** KEY FOR CHAINING MUST BE 6A
     C***********          MOVEL@TREF     WWKEY2  6                       CSW003
     C                     MOVEL@TREF     WWKEY1                          CSW003
     C*                                                                   CSW003
     C           CSW003    IFEQ 'Y'                                       CSW003
     C           WWTRAN    IFEQ 'DM'                                      CSW003
     C                     MOVE 'S'       PWHEN                           CSW003
     C                     END                                            CSW003
     C*                                                                   CSW003
     C           WWTRAN    IFEQ 'DN'                                      CSW003
     C                     MOVE 'M'       PWHEN                           CSW003
     C                     END                                            CSW003
     C                     ELSE                                           CSW003
     C                     MOVE *BLANKS   PWHEN                           CSW003
     C                     END                                            CSW003
     C*                                                                   CSW003
     C***********WWKEY2    CHAINDPTMVDY4             09                   CSW003
     C           KDPT      CHAINDPTMVDY4             09                   CSW003
     C*
     C** IF DEPOT MOVEMENTS TRANSACTION NOT FOUND
     C           *IN09     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '002'     DBASE            * * * * * * * *
     C***********          MOVELWWKEY2    DBKEY            *  DBERR 002  *CSW003
     C                     MOVELDPTK      DBKEY            *  DBERR 002  *CSW003
     C                     MOVEL'DPTMVDY4'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     END
      *                                                                   156853
      ** If Confirmation or Receive/Deliver Generated Indicator           156853
      ** is equal to ' ', initialise field to 'N'.                        156853
      *                                                                   156853
     C           CONG      IFEQ ' '                                       156853
     C                     MOVE 'N'       CONG                            156853
     C                     ENDIF                                          156853
      *                                                                   156853
     C           RCDG      IFEQ ' '                                       156853
     C                     MOVE 'N'       RCDG                            156853
     C                     ENDIF                                          156853
     C*
     C                     END
      *                                                                   200292
      ** Set RCDG to 'N' for ISO15022 messages as there are no            200292
      ** cancelation process in Midas.                                    200292
      *                                                                   200292
     C           MNEW      IFEQ 'Y'                                       200292
     C                     MOVE 'N'       RCDG                            200292
     C                     ENDIF                                          200292
     C*
     C** SAVE CURRENT S.W.I.F.T. VALUES
     C                     MOVELCONG      @CONG
     C                     MOVELRCDG      @RCDG
     C*
     C** UPDATE THE S.W.I.F.T. VALUES
     C           @MODE     IFEQ 'U'
     C           @MODE     OREQ 'C'
     C                     EXSR UPMODE
     C                     END
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*   UPMODE - UPDATE THE S.W.I.F.T. CONFIRMATION GENERTION IND.  *
     C*            AND RECEIVE/DELIVER GENERTION IND.                 *
     C*   Called by - main process                                    *
     C*   Calls     -                                                 *
     C*                                                               *
     C*****************************************************************
     C*
     C           UPMODE    BEGSR
     C*
     C           @MODE     IFEQ 'U'
     C*
     C** UPDATE CONFIRMATION GENERATED IND. WHEN CONF. CALLING PROGRAM
     C** UPDATE RECEIVE/DELIVER GENERATED IND. WHEN PAY RECEIVE PROGRAM
     C           @PROG     IFEQ 'CE'
     C*
     C** UPDATE CONFIRMATION GENERATED IND.
     C           WWTRAN    IFEQ 'DM'
     C           WWTRAN    OREQ 'DN'                                      CSW003
     C*
     C           CONG      IFEQ 'N'
     C           CSW003    IFNE 'Y'                                       CSW003
      *                                                                   CSW098
     C           CSW098    IFNE 'Y'                                       CSW098
     C                     MOVEL'A'       CONG
     C                     ELSE                                           CSW098
     C                     MOVEL'Y'       CONG                            CSW098
     C                     ENDIF                                          CSW098
      *                                                                   CSW098
     C                     ELSE                                           CSW003
     C                     MOVEL'Y'       CONG                            CSW003
     C                     END                                            CSW003
     C                     ELSE
      *                                                                   CSW098
     C           CSW098    IFNE 'Y'                                       CSW098
      *                                                                   CSW098
     C           CONG      IFEQ 'A'
     C                     MOVEL'Y'       CONG
     C                     ELSE
     C                     MOVEL'Y'       @ERCD
     C                     END
      *                                                                   CSW098
     C                     ELSE                                           CSW098
     C           CONG      IFNE 'Y'                                       CSW098
     C                     MOVEL'Y'       @ERCD                           CSW098
     C                     ENDIF                                          CSW098
     C                     ENDIF                                          CSW098
      *                                                                   CSW098
     C                     END
     C*
     C                     ELSE
     C*
     C           CONG      IFEQ 'N'
     C                     MOVEL'Y'       CONG
     C                     ELSE
     C                     MOVEL'Y'       @ERCD
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C           @PROG     IFEQ 'PR'
     C*
     C** UPDATE RECEIVE/DELIVER GENERATED IND.
     C                     MOVE @TREF     WWTMP3  3
     C* PART OF PHASE 2 MISSING (CSW003 IN SE1873 WHICH SUPPRESS A)       116583
     C***********WWTMP3    IFEQ 'ADM'                                     116583
     C           WWTMP3    IFEQ ' DM'                                     116583
     C           WWTMP3    OREQ ' DN'                                     116583
     C*
     C           RCDG      IFEQ 'N'
     C           CSW003    IFNE 'Y'                                       CSW003
     C                     MOVEL'A'       RCDG
     C                     ELSE                                           CSW003
     C                     MOVEL'Y'       RCDG                            CSW003
     C                     END                                            CSW003
     C                     ELSE
     C           RCDG      IFEQ 'A'
     C                     MOVEL'Y'       RCDG
     C                     ELSE
     C                     MOVEL'Y'       @ERCD
     C                     END
     C                     END
      *
      ***  At the same time, ensure that "S.W.I.F.T. Settlement Message
      ***  Required Flag" is set to No.
      *
     C                     MOVEL'N'       SCMG
     C*
     C                     ELSE
     C*
     C           RCDG      IFEQ 'N'
     C                     MOVEL'Y'       RCDG
      *
      ***  At the same time, ensure that "S.W.I.F.T. Settlement Message
      ***  Required Flag" is set to No.
      *
     C                     MOVE 'N'       SCMG
     C                     ELSE
     C                     MOVEL'Y'       @ERCD
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C*
     C** IF MODE EQUALS CANCELLATION RESET CONG OR RCDG
     C           @MODE     IFEQ 'C'
     C*
     C           @PROG     IFEQ 'CE'
     C*
     C           WWTRAN    IFEQ 'DM'
     C           WWTRAN    OREQ 'DN'                                      CSW003
     C*
     C           CONG      IFEQ 'Y'
     C           CSW003    IFNE 'Y'                                       CSW003
      *                                                                   CSW098
     C           CSW098    IFNE 'Y'                                       CSW098
     C                     MOVEL'A'       CONG
     C                     ELSE                                           CSW098
     C                     MOVEL'N'       CONG                            CSW098
     C                     ENDIF                                          CSW098
      *                                                                   CSW098
     C                     ELSE                                           CSW003
     C                     MOVEL'N'       CONG                            CSW003
     C                     END                                            CSW003
     C                     ELSE
     C                     MOVEL'N'       CONG
     C                     END
     C*
     C                     ELSE
     C*
     C                     MOVEL'N'       CONG
     C*
     C                     END
     C*
     C                     ELSE
     C*
     C           @PROG     IFEQ 'PR'
     C*
     C** UPDATE RECEIVE/DELIVER GENERATED IND.
     C                     MOVE @TREF     WWTMP3  3
     C           WWTMP3    IFEQ 'ADM'
     C*
     C           RCDG      IFEQ 'Y'
     C           CSW003    IFNE 'Y'                                       CSW003
     C                     MOVEL'A'       RCDG
     C                     ELSE                                           CSW003
     C                     MOVEL'N'       RCDG                            CSW003
     C                     END                                            CSW003
     C                     ELSE
     C                     MOVEL'N'       RCDG
     C                     END
     C*
     C                     ELSE
     C                     MOVEL'N'       RCDG
     C                     END
      *
      ***  At the same time, ensure that "S.W.I.F.T. Settlement Message
      ***  Required Flag" is set to No.
      *
     C                     MOVE 'N'       SCMG
     C                     END
     C*
     C                     END
     C                     END
     C*
     C** IF NO ERRORS
     C           @ERCD     IFNE 'Y'
     C*
     C** IF MODULE ID EQUALS 'SE'
     C           @MODI     IFEQ 'SE'
     C*
     C** TRADES TRANSACTION
     C           WWTRAN    IFEQ 'TR'
     C           WWTRAN    OREQ 'TS'                                      CSW003
     C                     UPDATTRADSDD1               08
     C*
     C** IF UPDATE OF TRADES RECORD FAILS
     C           *IN08     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '003'     DBASE            * * * * * * * *
     C***********          MOVELWWKEY1    DBKEY            *  DBERR 003  *CSW003
     C                     MOVELDPTK      DBKEY            *  DBERR 003  *CSW003
     C                     MOVEL'TRADSDY1'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     END
     C*
     C** DEPOT MOVEMENTS TRANSACTION
     C           WWTRAN    IFEQ 'DM'
     C           WWTRAN    OREQ 'DN'                                      CSW003
     C                     UPDATDPTMVDD1               09
     C*
     C** IF UPDATE OF DEPOT MOVEMENTS RECORD FAILS
     C           *IN09     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '004'     DBASE            * * * * * * * *
     C***********          MOVELWWKEY2    DBKEY            *  DBERR 004  *CSW003
     C                     MOVELDPTK      DBKEY            *  DBERR 004  *CSW003
     C                     MOVEL'DPTMVDY4'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*   INIT - inital process                                       *
     C*   Called by - main process                                    *
     C*   Calls     -                                                 *
     C*                                                               *
     C*****************************************************************
     C*
     C           INIT      BEGSR
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C**  Set up LDA for database error processing
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'MG5910'  DBPGM
     C                     MOVE *BLANKS   DBASE
     C                     OUT  LDA
      *                                                                   CSW098
      ** Verify if CSW098 feature is present                              CSW098
     C                     CALL 'SWIFT98'                                 CSW098
     C                     PARM           P@RTCD  7                       CSW098
      *                                                                   CSW098
     C           P@RTCD    IFEQ 'CSW098'                                  CSW098
     C                     MOVE 'Y'       CSW098  1                       CSW098
     C                     ENDIF                                          CSW098
      *                                                                   CSW098
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*   *PSSR - error handling                                      *
     C*                                                               *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C                     MOVE 'Y'       @ERCD
     C                     DUMP                                           CSW003
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*
** CPY@   Object Copyright
(c) Finastra International Limited 2001
