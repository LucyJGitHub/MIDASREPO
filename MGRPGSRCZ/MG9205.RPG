     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MG Message deletion processing')                 *
      *****************************************************************
      *                                                               *
      *  Midas - Message Generation Module                            *
      *                                                               *
      *   MG9205 - MESSAGE DELETION PROCESSING                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CGL165             Date 17Feb15               *
      *  Prev Amend No. CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      *                 CSW213             Date 03Jun13               *
      *                 CLE134             Date 01Aug12               *
      *                 CLE148             Date 23Jul12               *
      *                 CSW212             Date 03May12               *
      *                 CRE073             Date 06Dec10               *
      *                 CLE143             Date 06Dec10               *
      *                 CLE139             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER054             Date 07Apr09               *
      *                 CER048             Date 19May08               *
      *                 CER020             Date 19May08               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 CAS016             Date 28Feb06               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 151123             Date 02Nov06               *
      *                 CDL049             Date 07Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 CLE042             Date 06Sep05               *
      *                 CDL038             Date 10May05               *
      *                 CAS009             Date 16May05               *
      *                 CDL028             Date 07Feb05               *
      *                 CAS008             Date 16Jun04               *
      *                 CGL014             Date 20Oct03               *
      *                 CAP074             Date 08Aug02               *
      *                 222385             Date 23Oct03               *
      *                 221145             Date 08Sep03               *
      *                 217684             Date 12May03               *
      *                 CAS006             Date 21Jan03               *
      *                 CAS005             Date 16Dec02               *
      *                 CSD015             Date 14Oct02               *
      *                 CLE028             Date 27Jun02               *
      *                 CAS004             Date 26Jun02               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 210005             Date 07Nov02               *
      *                 204452             Date 03May02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP056             Date 13Mar02               *
      *                 CIR008             Date 13Mar02               *
      *                 186066             Date 27Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CSW201             Date 02May01               *
      *                 183583             Date 11Jun01               *
      * Midas DBA 3.05 -----------------------------------------------*
      *                 190127             Date 28Feb01               *
      *                 CLE011             Date 07Nov00               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSE023             Date 12Jul00               *
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CLE009             Date 26Apr99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 141572             DATE 21SEP98               *
      *                 142484             DATE 10Sep98               *
      *                 113352             DATE 13FEB98               *
      *                 S01408             Date 07JAN98               *
      *                 CLE007             Date 22MAY97               *
      *                 CDL002             DATE 09APR97               *
      *                 103007             Date 31Jul96               *
      *                 090912             Date 20Oct95               *
      *                   090214           DATE 27JUL95               *
      *                   052254           DATE 10JAN94               *
      *                   S01453           DATE 14DEC93               *
      *                   E42824           DATE 22JUL92               *
      *                   E37455           DATE 03JUL92               *
      *                   E37311           DATE 03JUL92               *
      *                   E40242           DATE 25JUN92               *
      *                   S01386           DATE 18MAY92               *
      *                   00000            DATE 00XXX00               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CSW213 - SWIFT Changes 2013 (Recompile)                      *
      *  CLE134 - Past Due Call Loan Processing (Recompile)           *
      *  CLE148 - Alpha Loan Reference                                *
      *  CSW212 - SWIFT 2012 Changes (Recompile)                      *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CLE143 - Original Loan Contract Date & Effective Interest    *
      *           Rate (Recompile)                                    *
      *  CLE139 - Lending Fee Capitalisation (Recompile)              *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER054 - German Features - Church Tax (Recompile)            *
      *  CER048 - German Features - Taxes (Recompile)                 *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus (Recompile)                              *
      *  CER043 - cerman Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  CAS016 - IAS18 EIR Recalculation (Fee Amortisation Over      *
      *           Whole Period) -Recompile.                           *
      *  151123 - When deleting a part of an MT580, start  from field *
      *            :23: and continue until the next field :23:        *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE042 - Extended Loan Sub Type (Recompile)                  *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CAS009 - EIR/AC Accounting (Recompile)                       *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  CAS008 - IAS 39 Enhancements (Recompile)                     *
      *  CGL014 - Collaterals Processing (Recompile)                  *
      *  CAP074 - Lending API enhancements - Loans input.             *
      *  222385 - SWIFT msgs are Watch List checked regardless of     *
      *           configuration file setting                          *
      *  221145 - Compliance Watch WLC: Move compliance check to      *
      *           after COMIT to MGOMSGPD                             *
      *  217684 - Authorisation User Malfunction (Recompile)          *
      *  CAS006 - Hedge Accounting Phase B                            *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *           (Recompile)                                         *
      *  CSD015 - Midas Compliance Watch - Watch List Checking.       *
      *  CLE028 - Flat Rate Personal Loans (Rule of 78s)              *
      *           (Recompile)                                         *
      *  CAS004 - Hedge Accounting Phase A (Recompile)                *
      *  210005 - MT202 created with incorrect details in field :32A:.*
      *           Applied fix 205530.                                 *
      *  204452 - Recompiled due to changed PF/LEFEED.                *
      *  CAP056 - Automatic Authorisation of Interface deals          *
      *           (Recompile)                                         *
      *  CIR008 - FRA/IRS Deals Authorisation (Recompile)             *
      *  186066 - Allow cancellation or deletion processing for fees  *
      *           which have been reversed or whose settlement method *
      *           has been amended from 08 to 01. Amend program to    *
      *           correcly generate TRNO of new MT202 used to convert *
      *           existing MT203. Also, amend program to use DLNRF as *
      *           key to access CLOAN record of fees based on loans.  *
      *  CSW201 - SWIFT 2001 Standards Update                         *
      *  183583 - Output details of field tags :5nA: and :8nA: to     *
      *           PF/MGOEXTPD to solve incorrect display of customer  *
      *           report name & town for a Swift Address used by more *
      *           than one customer.                                  *
      *  190127 - Related reference should contain tansaction         *
      *           reference of previous message of same type          *
      *  CLE011 - Rate Fixing Days for Lending Rollover Processing    *
      *           Recompile                                           *
      *  CSE023 - Treaty Withholding Tax (Recompiled)                 *
      *  CDL008 - Continuous Linked Settlement (Recompiled)           *
      *  CLE009 - LE I/C Swift Message Generation for Fees            *
      *           Modify Transaction Number Specification             *
      *   141572 - Recompile over MGOREFPD                            *
      *   142484 - Stops MG9205 dumping with array index error.       *
      *   113352 - Set-up CIND (Century flag)  to correct year2000    *
      *            display problem.                                   *
      *   S01408 - FX netting placed new I specs above existing E spec*
      *            hook. This fix just manually moves the hook back to*
      *            the E specs.                                       *
      *   CLE007 - Customer Lending Enhancements - Others (T3)        *
      *   CDL002 - FX Netting                                         *
      *   103007 - DO NOT CONVERT ONE PART OF MT203 TO MT202 IF THE   *
      *            DEAL IS ALREADY HISTORIC                           *
      *   090912 - Remove the coding line whitch reset to blank       *
      *            the field DELC (SWIFT II delivery notification)    *
      *            to retrieve the reference information about this   *
      *            field for the new MT202 (created after deletion    *
      *            of an MT203 first part).                           *
      *   090214 - Enable program to handle MT580 deletion            *
      *   052254 - RECOMPILED as CURRENT FACTOR amended from 9,8      *
      *            to 10,9                                            *
      *   S01453 - MARGIN AND FX POSITIONS/POINTS AND BASE CURRENCY   *
      *            POSITIONS/POINTS ADDED TO FX INPUT (RECOMPILED)    *
      *   E42824 - db err on attempting to delete multiple            *
      *            MT210s                                             *
      *   E37455 - Deletion of an MT210 by deleting the transaction   *
      *            reference no. results in db error because of error *
      *            in array for Swift amount conversion.  Fix applied *
      *            checks if TRN to be deleted is the header TRN ref. *
      *            so that Swift amount need not be updated and MGST  *
      *            will just be updated to 'DLTD'.  Also add a DUMP   *
      *            in *PSSR subr. so that error in program will be    *
      *            traceable.                                         *
      *   E37311 - In the Outgoing Message Status report, the operat- *
      *            or, time, date and workstation id are not indicated*
      *            when there are message deletions.  Before updating *
      *            MGOREFPD, move the user id, workstation id, date & *
      *            time to the respective fields in the file.         *
      *   E40242 - RE-COMPILED DUE TO CHANGE OF ASEQ                  *
      *   S01386 - MG/SE AMENDMENTS                                   *
      *    o Add trades file (LF/TRADS)                               *
      *                                                               *
      *****************************************************************
     FMGOREFL0UF  E           K        DISK
     F            MGOREFD0                          KRENAMEMGREFL0
     F                                              KCOMIT
     FMGOMSGPDUF  E           K        DISK                      A
     F                                              KCOMIT
     FMGOEXTPDO   E                    DISK                               183583
     F                                              KCOMIT                183583
     FMGOEXTLFIF  E           K        DISK                               183583
     F            MGOEXTD0                          KRENAMEMGOEXL0        183583
     FDEALS   UF  E           K        DISK                           UC
     F                                              KCOMIT
     FCLOAN   UF  E           K        DISK                           UC
     F                                              KCOMIT
     F***TRADS   UF  E           K        DISK
     F***                                           KCOMIT
     FTRADS   UF  E           K        DISK                           UC  S01386
     F                                              KCOMIT                S01386
     FMGOREFPDO   E                    DISK
     F                                              KCOMIT
     FFXNETML4UF  E           K        DISK                           UC  CDL002
     F                                              KCOMIT                CDL002
     F/COPY WNCPYSRC,MG9205F001
      *
     FLEFEEDL1UF  E           K        DISK                           UC  CLE007
     F                                              KCOMIT                CLE007
      ** MG Transaction history file                                                          190127
     FMGHISTL0UF  E           K        DISK                                                   190127
     F                                              KCOMIT                                    190127
      *
      ********************************************************************
      *
      * ID C  F  H  L    FUNCTION OF INDICATORS
      *
      *    05            READ/WRITE/UPDAT/CHAIN to PF/MGOREFPD
      *    06            READ/WRITE/UPDAT/CHAIN to PF/MGOMSGPD
      *    07            UPDAT/CHAIN to LF/DEALS
      *    08            UPDAT/CHAIN to LF/CLOAN
      *****09*****       UPDAT/CHAIN to LF/TRADS
      *    09            UPDAT/CHAIN to LF/TRADS                          S01386
      *    11            CHAIN to MGOREFL0 (TRN unique ?)
      *   12  - Chain to LEFEEDL1                                     *   CLE007
      *    18            Error in files LF/MGOEXTLF or PF/MGOEXTPD        183583
      *
     E                    CPY@    1   1 80               copyright
     E                    @WRK       50  1               concatenation field
      *
      **  Arrays used in SWAMT subroutine
     E                    NUMB       15  1
     E                    NUMY       15  1
     E                    NUMX        4  1
     E                    NUMO       17  1
      *
      **  Arrays used in ANTCOM sunroutine
     E                    SAMT       17  1
     E                    NUM        15  1
     E                    DEC         4  1
      *                                                                                       CAS006
      ** Rename Credit Risk Spread Field                                                      CAS006
      *                                                                                       CAS006
     ITRADSDF                                                                                 CAS006
     I              CRSK                            ASCRSK                                    CAS006
      *                                                                   183583
      ** Rename extension file fields.                                    183583
      *                                                                   183583
     IMGOEXL0                                                             183583
     I              TRNO                            VTRNO                 183583
     I              MTPY                            VMTPY                 183583
     I              MPSN                            VMPSN                 183583
     I              MTG                             VMTG                  183583
     I              MCSI                            VMCSI                 183583
      *                                                                   CLE007
     E/COPY WNCPYSRC,MG9205E001                                           S01408
      *                                                                   CLE007
      ** Rename fields to avoid override.                                 CLE007
      *                                                                   CLE007
     ILEFEEDF                                                             CLE007
     I              LSWS                            FLSWS                 CLE007
     ICLOANCKF                                                            CLE007
     I              NLRA                            KNLRA                 CLE007
      *                                                                   CDL002
     IFXNETMD0                                                            CDL002
     I              AMLSWC                          LSWC                  CDL002
     I              AMLSWS                          LSWS                  CDL002
      *                                                                   CLE007
    *E*COPY*WNCPYSRC,MG9205E001****************************************** S01408
      ** Parse the transaction number                                     CLE007
      *                                                                   CLE007
     I            DS                                                      CLE007
     I                                        1  16 WTNUM                 CLE007
     I*********************                   1   60DCNUM           CLE007CLE009
     I*********************                   7   7 DFIND           CLE007CLE009
     I*********************                   8  12 DFACL           CLE007CLE009
     I*********************                   7  12 DLNRF           CLE007CLE009
     I*********************                  13  140DFSEQ           CLE007CLE009
     I                                        1   1 DFIND                 CLE009
     I**********                              2   70DCNUM                              CLE009 CSD027
     I                                        2   7 DCNUM                                     CSD027
     I                                        8  12 DFACL                 CLE009
     I                                       13  140DFFSQ                 CLE009
     I                                        2   7 DLNRF                 CLE009
     I                                        8   90DLFSQ                 CLE009
     I**********                             10  14 DBLNK                              CLE009 186066
     I                                       12  16 DBLNK                                     186066
      *                                                                   CLE007
      ** LEFEEDL1 Key                                                     CLE007
      *                                                                   CLE007
     I            DS                                                      CLE007
     I**********                              1   60WCNUM                              CLE007 CSD027
     I                                        1   6 WCNUM                                     CSD027
     I                                        7  110WFACL                 CLE007
     I**********                             12  170WLNRF                              CLE007 CLE148
     I                                       12  17 WLNRF                                     CLE148
     I                                       18  190WFSEQ                 CLE007
     I                                        1  19 DSFEE                 CLE007
      *
     IMULT        DS                         40
     I                                        1   5 @MTAG
     I                                        6  55 @MFLD
      **  Multiple occurrence data structure for message part
      *
     IMMSG        DS                         10
     I                                        1   5 MMTAG
     I                                        6  55 MMFLD
      **  Multiple occurrence data structure for fields 52/53
      *
     IEXTN        DS                         80                           183583
     I                                        1   20MPSN                  183583
     I                                        3   7 MTG                   183583
     I                                        8  15 MCSI                  183583
      *                                                                   183583
      ** Multiple occurrence data structure for extension file            183583
      *                                                                   183583
     I            DS                                                      183583
     I                                        1   5 @MTG                  183583
     I                                        2   2 FLDTYP                183583
     I                                        4   4 FLDTAG                183583
      *                                                                   183583
      ** Field tag data structure                                         183583
      *                                                                   183583
     IW5FLD       DS                          4                           183583
     I                                        1   20WVPOS                 183583
     I                                        3   7 WMTAG                 183583
     I                                        8  57 WMFLD                 183583
      *                                                                   183583
      ** Multiple occurrence data structure for fields 52, 53, 54 and 72  183583
      *                                                                   183583
     IFLD32A      DS                         26
     I                                        1   6 @DATE
     I                                        7  26 T32B
      **  Data structure containing codeword for Fields 32A
      *
     ITTRN        DS                         16
     I                                        3   80MTRAN
     I                                        3   8 MTRANA                                    CLE148
      **  Data structure to retrieve midas transaction number
      *
     I@HREF       DS                         16                                               190127
     I                                        1   2 HSMODI                                    190127
     I                                        3   8 HSTRAN                                    190127
     I                                        9  11 HSSEQ                                     190127
      **  Data structure for related reference                                                190127
      *
     ISDBANK    E DSSDBANKPD
      **  Data structure for bank file
      *
     ISCSARD    E DSSCSARDPD                                              CLE007
      ** Switchable Features File                                         CLE007
      *                                                                   CLE007
     IDSFDY     E DSDSFDY
      **  Short data structure for access programs
      *
     IDSSDY     E DSDSSDY
      **  Long data structure for access programs
      *
     IWLTDS     E DSSDWLTOPD                                                                  CSD015
      ** DS For Watch List Transaction Details File                                           CSD015
      *                                                                                       CSD015
     IPSTRNG      DS                           9999                                           CSD015
      *                                                                                       CSD015
     ISDWLCC    E DSSDWLCCPD                                                                  222385
      ** Watch list checking details                                                          222385
      *                                                                                       222385
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      **  Local data area
      *
     I           SDS                                                      E37311
     I                                      244 253 WSID                  E37311
     I                                      254 263 USRID                 E37311
      **  Workstation ID                                                  E37311
      *                                                                   E37311
     I/COPY WNCPYSRC,MG9205I001
     C/EJECT
      *****************************************************************
      *                          NOTES                                *
      *                                                               *
      * Single Messages                                               *
      * 1.  A message is 'deleted' when the message status field on   *
      *     the reference field (MGST, PF/MGOREFPD), is equal to      *
      *     'DLTD'.                                                   *
      *                                                               *
      * Multiple Messages (MT203, MT210)                              *
      * 2.  The message status field of the header record on the      *
      *     message reference file (PF/MGOREFPD), contains the        *
      *     status for the multiple message. This status is then      *
      *     the status for all parts within the multiple message.     *
      *                                                               *
      *     If the message status field (MGST, PF/MGOREFPD), of the   *
      *     header record contains the value 'DLTD' the whole         *
      *     message is deleted, (i.e. all parts are deleted).         *
      *                                                               *
      *     To delete a message part from the multiple message, the   *
      *     message is descibed as 'part deleted'. The reference      *
      *     record for the message part being deleted has the         *
      *     'part deleted' field (PTST, PF/MGOREFPD), set to 'D'.     *
      *     The header record message status (MGST, PF/MGOREFPD),     *
      *     is then set to 'AMND', to indicate the multiple message   *
      *     has been 'amended'.                                       *
      *                                                               *
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                   Index to subroutines                        *
      *  main process                                                 *
      *  INIT    Initial processing                                   *
      *  TYPE    Determine type of message being deleted              *
      *  DLT210  Perform Multiple MT210 deletion                      *
      *  DLT203  Perform MT203 deletion                               *
      *  DLT580  Perform MT580 deletion                               *   090214
      *  LIVE    Determine the number of live parts                   *
      *  UPDREF  Update the status of the message being deleted       *
      *  AMTCOM  Convert SWIFT amount to numeric amount               *
      *  SWFAMT  Convert numeric amount to SWIFT amount               *
      *  MSGDLT  Part delete the part from the multiple message       *
      *  CONVRT  Convert one part MT203 to an MT202                   *
      *  PART    Store the last part of the one part MT203            *
      *  OUT202  Output the one part MT203 as an MT202                *
      *  TRAN    Retrieve the msg. sequence from the transaction file *
      *  WREF    Output a reference record for the MT202              *
      *                                                               *
      *****************************************************************
     C/EJECT
      *****************************************************************
      * Main Process                                                  *
      * Calls    INIT    Initial processing                           *
      *          TYPE    Determine type of message being deleted      *
      *          DLT210  Perform Multiple MT210 deletion              *
      *          DLT203  Perform MT203 deletion                       *
      *          DLT580  Perform MT580 deletion                       *   090214
      *****************************************************************
      *
      *
      **  Receive the Transaction Reference Number and the module id
      **  of the message being deleted
      *
     C           *ENTRY    PLIST
     C                     PARM           @TNUM  16
     C                     PARM           @MODI   2
     C                     PARM           @ERCD   1
      *
      **  Transaction history key                                                             190127
     C           HSKEY     KLIST                                                              190127
     C                     KFLD           HSMODI                                              190127
     C                     KFLD           HSTRAN                                              190127
     C                     KFLD           HSMTPY                                              190127
     C                     KFLD           HSSEQ                                               190127
      *
      **  Initial processing
      *
     C                     EXSR INIT
      *
      **  Use the parameter TRN to access the message we are deleting
      *
     C           @TNUM     CHAINMGREFL0              05
      *
      **  Error message not found
      *
     C           *IN05     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '001'     DBASE            * * * * * * * *
     C                     MOVEL@TNUM     DBKEY            *  DBERR 001  *
     C                     MOVEL'MGOREFL0'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C/COPY WNCPYSRC,MG9205C002
      *
      **  Process message if not already deleted or part deleted
      *
     C           MGST      IFNE 'DLTD'
     C           PTST      ANDNE'D'
     C/COPY WNCPYSRC,MG9205C003
      *
      **  Determine what type of message being deleted
      **  Flag single message as DeLeTeD
      *
     C                     EXSR TYPE
      *
      **  Do deletion process for multiple message
      *
     C           MMEG      IFEQ 'Y'
      *
      **  Perform multiple MT210 message deletion
      *
     C           MTPY      IFEQ '210'
     C                     EXSR DLT210
      *                                                                   090214
      **  Perform multiple MT580 message deletion                         090214
      *                                                                   090214
     C                     ELSE                                           090214
     C           MTPY      IFEQ '580'                                     090214
     C                     EXSR DLT580                                    090214
      *
      **  Perform MT203 message deletion
      *
     C                     ELSE
     C                     EXSR DLT203
     C                     END                                            090214
     C                     END
     C                     END
      *
      **  COMIT all file changes
      *
     C                     COMIT
      *                                                                   221145
      **  Call Compliance Watch:Watch List Checking                       221145
      *                                                                   221145
     C           CSD015    IFEQ 'Y'                                       221145
     C           W1EWLC    ANDEQ'Y'                                                           222385
     C           CSW025    ANDEQ'N'                                       221145
     C                     EXSR WLCDQ                                     221145
     C                     ENDIF                                          221145
      *                                                                   221145
     C                     END
     C/COPY WNCPYSRC,MG9205C006
      *
     C                     SETON                     LR
     C                     RETRN
     C/EJECT
      *****************************************************************
      *   TYPE - determine the type of message being deleted          *
      *   Called by   Main process                                    *
      *   Calls       -                                               *
      *****************************************************************
      *
     C           TYPE      BEGSR
      *
      **  Initialise multiple message flag
      *
     C                     MOVE 'N'       MMEG    1
     C                     MOVE PT210     @PT210  20
     C                     MOVE MTPY      @MTPY   3
      *
      **  The message being deleted is part of a multiple and
      **  is part-deleted from the multiple message (PF/MGOREFPD)
      **  BHFFIX: Treat hdr of mult 210 as single, ie. delete entire msg  E42824
      *
     C           TRNF      IFNE *BLANKS
     C           TRNM      ORNE *BLANKS
     C           MTPY      ANDNE'210'                                     E42824
     C                     MOVE 'Y'       MMEG
     C                     MOVE 'D'       PTST
      *
      **  The header TRN is the TRN of the message being deleted
      *
     C           TRNF      IFEQ *BLANKS
     C                     MOVEL@TNUM     @HEAD  16
      *
      **  The header TRN is the 'TRN of the first part' (TRNF)
      *
     C                     ELSE
     C                     MOVELTRNF      @HEAD
     C                     END
      *
      **  This is a single message, hence flag as deleted
      *
     C                     ELSE
     C/COPY WNCPYSRC,MG9205C010
     C                     MOVE 'DLTD'    MGST
     C                     MOVE '4'       MGSG
     C/COPY WNCPYSRC,MG9205C001
     C                     END
      *
      **  Update the message being deleted
      *
     C                     MOVELBJMRDT    LADT
     C                     TIME           LATM
     C*                                                                   E37311
     C                     MOVE LADT      RELD                            E37311
     C                     MOVE LATM      RELT                            E37311
     C                     MOVE USRID     RUSR                            E37311
     C                     MOVE WSID      RWSN                            E37311
     C*                                                                   E37311
     C                     UPDATMGREFL0
      *
      **  Delete transaction history                                                          190127
     C                     MOVEL@TNUM     @HREF                                               190127
     C                     MOVE MTPY      HSMTPY                                              190127
      *                                                                                       190127
      ** If Swift 2001 is on, delete all history records for the                              CSW201
      ** for the transaction (many records for the same transaction)                          CSW201
      *                                                                                       CSW201
     C           CSW201    IFEQ 'Y'                                                           CSW201
     C           MTPY      ANDEQ'362'                                                         CSW201
     C           HSKEY     SETLLMGHISTL0                                                      CSW201
     C           HSKEY     READEMGHISTL0                 56                                   CSW201
     C           *IN56     DOWEQ*OFF                                                          CSW201
     C                     DELETMGHISTD0                                                      CSW201
     C           HSKEY     READEMGHISTL0                 56                                   CSW201
     C                     ENDDO                                                              CSW201
      *                                                                                       CSW201
      ** Otherwise delete single record (one history per transaction)                         CSW201
      *                                                                                       CSW201
     C                     ELSE                                                               CSW201
     C           HSKEY     CHAINMGHISTL0             56                                       190127
      *                                                                                       190127
     C           *IN56     IFEQ '0'                                                           190127
     C                     DELETMGHISTD0                                                      190127
     C                     END                                                                190127
      *                                                                                       190127
     C                     ENDIF                                                              CSW201
      *                                                                                       190127
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   DLT210  - Perform multiple MT210 deletion processing        *
      *   Called by   Main process                                    *
      *   Calls       LIVE    Determine the number of live parts      *
      *               UPDREF  Update status of message on ref. file   *
      *               MSGDLT  Part delete message from multiple       *
      *****************************************************************
      *
     C           DLT210    BEGSR
      *
      **  Save amount for the part being deleted and access header
      *
     C                     MOVELAMTS      @AMTS  17
     C           @HEAD     CHAINMGREFL0              05
      *
      **  Header reference record not found
     C           *IN05     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '002'     DBASE            * * * * * * * *
     C                     MOVEL@HEAD     DBKEY            *  DBERR 002  *
     C                     MOVEL'MGOREFL0'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      **  Count number of live parts (PARTS), within the multiple
      **  Header reference record is not a part of the multiple, it
      **  is a means to access the multiple, hence not checked
      *
     C                     Z-ADD0         PARTS   20
     C                     EXSR LIVE
      *
      **  Update the message status on the message reference file
      *
     C                     EXSR UPDREF
      *
      **  If parts remain, flag the deleting message as part deleted
      *
     C           PARTS     IFGT 0
     C                     EXSR MSGDLT
     C                     END
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   DLT203  - Perform MT203 deletion processing                 *
      *   Called by   Main process                                    *
      *   Calls       LIVE    Determine the number of live parts      *
      *               UPDREF  Update status of message on ref. file   *
      *               MSGDLT  Part delete message from multiple       *
      *               CONVRT  Convert one part MT203 to an MT202      *
      *****************************************************************
      *
     C           DLT203    BEGSR
      *
      **  Save amount for the part being deleted and access header
      *
     C                     MOVELAMTS      @AMTS  17
     C           @HEAD     CHAINMGREFL0              05
      *
      **  Header reference record not found
     C           *IN05     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '003'     DBASE            * * * * * * * *
     C                     MOVEL@HEAD     DBKEY            *  DBERR 003  *
     C                     MOVEL'MGOREFL0'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      **  Count number of live parts (PARTS), save the first live
      **  part found, this will be the MT202 if a one part MT203 exists
      *
      **  Header record checked since part of the multiple message
      *
     C                     Z-ADD0         PARTS   20
     C                     MOVE *BLANKS   FIRST
     C           PTST      IFEQ *BLANK
     C                     ADD  1         PARTS
     C                     MOVEL@HEAD     FIRST
     C                     END
      *
     C                     EXSR LIVE
      *
      **  If a valid MT203 (more than 2 live parts), update status of
      **  message to 'AMND' and flag message as part-deleted
      *
     C           PARTS     IFGT 1
     C                     EXSR UPDREF
     C                     EXSR MSGDLT
     C                     END
      *
      **  No live parts, status of message is 'DLTD'
      *
     C           PARTS     IFEQ 0
     C                     EXSR UPDREF
     C                     END
      *
      **  One part MT203 exists, hence convert to an MT202
      *
     C           PARTS     IFEQ 1
     C                     EXSR CONVRT
     C                     END
      *
     C                     ENDSR
     C/EJECT                                                              090214
      *****************************************************************   090214
      *   DLT580  - Perform MT580 deletion processing                 *   090214
      *   Called by   Main process                                    *   090214
      *   Calls       LIVE    Determine the number of live parts      *   090214
      *               UPDREF  Update status of message on ref. file   *   090214
      *               MSGDLT  Part delete message from multiple       *   090214
      *****************************************************************   090214
      *                                                                   090214
     C           DLT580    BEGSR                                          090214
      *                                                                   090214
      **  Access header                                                   090214
      *                                                                   090214
     C           @HEAD     CHAINMGREFL0              05                   090214
      *                                                                   090214
      **  Header reference record not found                               090214
     C           *IN05     IFEQ '1'                                       090214
     C           *LOCK     IN   LDA                                       090214
     C                     MOVE '018'     DBASE            * * * * * * * *090214
     C                     MOVEL@HEAD     DBKEY            *  DBERR 018  *090214
     C                     MOVEL'MGOREFL0'DBFILE           * * * * * * * *090214
     C                     OUT  LDA                                       090214
     C                     EXSR *PSSR                                     090214
     C                     END                                            090214
      *                                                                   090214
      **  Count number of live parts (PARTS)                              090214
      *                                                                   090214
     C                     Z-ADD0         PARTS   20                      090214
     C                     MOVE *BLANKS   FIRST                           090214
     C           PTST      IFEQ *BLANK                                    090214
     C                     ADD  1         PARTS                           090214
     C                     MOVEL@HEAD     FIRST                           090214
     C                     END                                            090214
      *                                                                   090214
     C                     EXSR LIVE                                      090214
      *                                                                   090214
      **  If more than 2 live parts, update status of message to          090214
      **  'AMND' and flag message as part-deleted                         090214
      *                                                                   090214
     C           PARTS     IFGE 1                                         090214
     C                     EXSR UPDREF                                    090214
     C                     EXSR MSGDLT                                    090214
     C                     END                                            090214
      *                                                                   090214
      **  No live parts, status of message is 'DLTD'                      090214
      *                                                                   090214
     C           PARTS     IFEQ 0                                         090214
     C                     EXSR UPDREF                                    090214
     C                     END                                            090214
      *                                                                   090214
     C                     ENDSR                                          090214
     C/EJECT
      *****************************************************************
      *   LIVE  - determine type of deletion processing               *
      *   Called by   DLT210  perform multiple MT210 deletion         *
      *               DLT203  perform MT203 deletion                  *
      *               DLT580  perform MT580 deletion                  *   090214
      *   Calls       -                                               *
      *****************************************************************
      *
     C           LIVE      BEGSR
      *
      **  Check until no more parts or 2 live parts have been found
      *
     C           TRNM      DOWNE*BLANKS
     C           PARTS     ANDLT2
      *
      **  Access the next part of the multiple message
      *
     C           TRNM      CHAINMGREFL0              05
      *
      **  Next part not found
     C           *IN05     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '004'     DBASE            * * * * * * * *
     C                     MOVELTRNM      DBKEY            *  DBERR 004  *
     C                     MOVEL'MGOREFL0'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      **  Message is a live part if the message is not part-deleted
      *
     C           PTST      IFEQ *BLANK
     C                     ADD  1         PARTS
      *
      **  First live part, as the MT203 may contain only one part
      *
     C           FIRST     IFEQ *BLANKS
     C                     MOVELTRNO      FIRST  16
     C                     END
     C                     END
      *
     C                     END
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   UPDREF - Update the status of the message being deleted     *
      *   Called by   DLT210  perform multiple MT210 deletion         *
      *               DLT203  perform MT203 deletion                  *
      *               DLT580  perform MT580 deletion                  *   090214
      *               CONVRT  convert one part MT203 to an MT202      *
      *   Calls       AMTCOM  convert SWIFT amount to numeric amount  *
      *               SWFAMT  convert numeric amoutn to SWIFT amount  *
      *****************************************************************
      *
     C           UPDREF    BEGSR
      *
      **  The header of a multiple message holds the status of the
      **  whole message (the status for all parts of the multiple)
      *
     C           @HEAD     CHAINMGREFL0              05
      *
      **  Header record not found
     C           *IN05     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '005'     DBASE            * * * * * * * *
     C                     MOVEL@HEAD     DBKEY            *  DBERR 005  *
     C                     MOVEL'MGOREFL0'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      **  Message deleted when no live parts (status equals 'DLTD')
      *
     C           PARTS     IFEQ 0
     C*                                                                   E37455
     C**  If deleting through the header TRN ref., then whole message     E37455
     C**  is 'DLTD'.                                                      E37455
     C*                                                                   E37455
     C           @HEAD     OREQ @TNUM                                     E37455
     C           MTPY      ANDEQ'210'                                     E37455
     C*                                                                   E37455
     C                     MOVE 'DLTD'    MGST
     C                     MOVE '4'       MGSG
      *
      **  Message contains live parts, hence message amended ('AMND')
     C                     ELSE
     C                     MOVE 'AMND'    MGST
     C                     MOVE '1'       MGSG
      *                                                                   090214
      **  No need to update total amount field for MT580 as they          090214
      **  don't have an total amount field                                090214
      *                                                                   090214
     C           MTPY      IFNE '580'                                     090214
      *
      **  Part of a multiple message so update total amount field
      **  Convert message part S.W.I.F.T. amount to numeric
      *
     C                     MOVE *BLANKS   AMTT   17
     C                     MOVEL@AMTS     AMTT
     C                     EXSR AMTCON
     C                     Z-ADDNAMT      SUBAM  150
     C                     Z-ADDNDEC      SUBDEC  43
      *
      **  Convert S.W.I.F.T. total amount of the multiple to numeric
      *
     C                     MOVE *BLANKS   AMTT
     C                     MOVE AMTX      AMTT
     C                     EXSR AMTCON
     C                     Z-ADDNAMT      HSHAM  150
     C                     Z-ADDNDEC      HSHDEC  43
      *
      **  Subtract message amount from total multiple message amount
      *
     C           HSHAM     SUB  SUBAM     HSHAM
     C           HSHDEC    SUB  SUBDEC    HSHDEC
      *
     C           HSHDEC    IFLT 0
     C           HSHAM     SUB  1         HSHAM
     C                     ADD  10        HSHDEC
     C                     END
      *
      **  Convert total back to S.W.I.F.T. format
      *
     C                     MOVE HSHAM     NUMCH  15
     C                     MOVE HSHDEC    DECCH   4
     C                     EXSR SWFAMT
     C                     MOVELSWNUM     AMTX
     C                     END                                            090214
     C                     END
      *
      **  Update the reference record on PF/MGOREFPD
      *
     C                     MOVELBJMRDT    LADT
     C                     TIME           LATM
     C*                                                                   E37311
     C                     MOVE LADT      RELD                            E37311
     C                     MOVE LATM      RELT                            E37311
     C                     MOVE USRID     RUSR                            E37311
     C                     MOVE WSID      RWSN                            E37311
     C*                                                                   E37311
     C                     UPDATMGREFL0
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *    AMTCON - Converts S.W.I.F.T. amount to numeric equivalent  *
      *    Called by  UPDREF update status of message being deleted   *
      *                                                               *
      *    Input  : AMTT 17 long SWIFT amount                         *
      *    Outout : NAMT 15 long digit of amount                      *
      *             NDEC  4 long 1 digit & 3 decimal of amount        *
      *****************************************************************
      *
     C           AMTCON    BEGSR
      *
     C                     MOVEAAMTT      SAMT
     C                     Z-ADD1         R       20
     C                     Z-ADD2         Q       20
     C           SAMT,Q    DOWNE','
     C                     MOVE SAMT,R    NUM,R
     C                     ADD  1         Q
     C                     ADD  1         R
     C                     END
      *
     C                     Z-ADDQ         COM     20
     C                     ADD  1         Q
     C                     Z-ADD1         W       20
     C                     Z-ADD0         NODP    10
     C                     MOVE SAMT,R    DEC,W
     C                     ADD  1         W
      *
     C           W         DOWLE4
     C           Q         ANDLE17
     C           SAMT,Q    IFNE *BLANK
     C                     MOVE SAMT,Q    DEC,W
     C                     ADD  1         NODP
     C                     ELSE
     C                     MOVE '0'       DEC,W
     C                     END
     C                     ADD  1         W
     C                     ADD  1         Q
     C                     END
      *
     C           18        SUB  COM       Z       20
      *
     C                     MOVE *ZEROS    NUMB
      *
     C           COM       IFGT 2
     C                     MOVEANUM       NUMB,Z
     C                     MOVEANUMB      NUMA   15
     C                     MOVE NUMA      NAMT   150
     C                     ELSE
     C                     Z-ADD0         NAMT
     C                     END
      *
     C                     MOVEADEC       DECA    4
     C                     MOVE DECA      NDEC    43
      *
     C                     ENDSR
      /EJECT
      ****************************************************************
      *    SWFAMT - Converts numeric amount into S.W.I.F.T. format   *
      *    Called by : UPDREF update status of message being deleted *
      *                                                              *
      *    Input  : DECCH  4 long 1 unit & 3 decimal part of amount  *
      *             NUMCH 15 long numeric part of amount             *
      *    Output : SWNUM 17 long SWIFT amount                       *
      ****************************************************************
      *
     C           SWFAMT    BEGSR
     C*
     C                     MOVE *BLANKS   NUMO
     C                     MOVEADECCH     NUMX
     C                     MOVEANUMCH     NUMY
     C                     Z-ADD1         T       20
     C                     Z-ADD1         P       20
     C           NUMCH     IFNE *ZEROS
     C           NUMY,T    DOWEQ'0'
     C                     ADD  1         T
     C                     END
     C           T         DOWLE15
     C                     MOVE NUMY,T    NUMO,P
     C                     ADD  1         T
     C                     ADD  1         P
     C                     END
     C                     END
     C                     MOVE NUMX,1    NUMO,P
     C                     ADD  1         P
     C                     MOVE ','       NUMO,P
     C                     ADD  1         P
     C                     Z-ADD2         T
     C                     DO   NODP
     C                     MOVE NUMX,T    NUMO,P
     C                     ADD  1         T
     C                     ADD  1         P
     C                     END
     C                     MOVEANUMO      SWNUM  17
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   MSGDLT - Flag part of message on PF/MGOMSGPD as part deleted*
      *   Called by   DLT210  perform multiple MT210 deletion         *
      *               DLT203  perform MT203 deletion                  *
      *               DLT580  perform MT580 deletion                  *   090214
      *   Calls       -                                               *
      *****************************************************************
      *
     C           MSGDLT    BEGSR
      *
      **  Access multiple message on the message data file
      *
     C           @HEAD     CHAINMGOMSGD0             06
     C           *IN06     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '006'     DBASE            * * * * * * * *
     C                     MOVEL@HEAD     DBKEY            *  DBERR 006  *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      **  Update field 19 (total amount) for MT203 messages
      *
     C           @MTPY     IFEQ '203'
     C                     MOVE *BLANKS   MFLD
     C                     MOVELAMTX      MFLD
     C                     UPDATMGOMSGD0
     C                     END
      *
     C                     MOVE 'N'       FOUND
     C                     Z-ADD0         P210    20
      *
      **  Read through the multiple to find the deleting message
      *
     C           @HEAD     DOWEQTRNO
     C           *IN06     ANDEQ'0'
     C           FOUND     ANDEQ'N'
      *                                                                   090214
      **  For MT580, part found when Field :20: contains the              090214
      **  TRN of the message we are deleting.                             090214
      *                                                                   090214
     C           @MTPY     IFEQ '580'                                     090214
     C           MTAG      ANDEQ':20: '                                   090214
     C                     MOVELMFLD      @FLD   16                       090214
     C           @FLD      IFEQ @TNUM                                     090214
      *                                                                                       151123
      ** Read previous record until :23: is found.  This is the                               151123
      ** beginning of the part of the multiple message to be deleted.                         151123
      *                                                                                       151123
     C           MTAG      DOUEQ':23: '                                                       151123
     C           *IN06     OREQ '1'                                                           151123
     C           @HEAD     ORNE TRNO                                                          151123
     C                     READPMGOMSGD0                 06                                   151123
     C                     END                                                                151123
      *                                                                   090214
      **  For this part of the multiple message, flag as part deleted     090214
      *                                                                   090214
     C********** MTAG      DOUEQ':20: '                                                090214 151123
     C           MTAG      DOUEQ':23: '                                                       151123
     C           *IN06     OREQ '1'                                       090214
     C           @HEAD     ORNE TRNO                                      090214
     C                     MOVE 'D'       PTDL                            090214
     C                     UPDATMGOMSGD0                                  090214
     C                     READ MGOMSGD0                 06               090214
     C                     END                                            090214
      *                                                                   090214
      **  Message-part flagged as part deleted                            090214
     C                     MOVE 'Y'       FOUND   1                       090214
     C                     END                                            090214
     C                     END                                            090214
      *
      **  For MT203, part found when Field :20: contains the
      **  TRN of the message we are deleting.
      *
     C           @MTPY     IFEQ '203'
     C           MTAG      ANDEQ':20: '
     C                     MOVELMFLD      @FLD   16
     C/COPY WNCPYSRC,MG9205C011
     C           @FLD      IFEQ @TNUM
      *
      **  For this part of the multiple message, flag as part deleted
      *
     C           MTAG      DOUEQ':20: '
     C           *IN06     OREQ '1'
     C           @HEAD     ORNE TRNO
     C                     MOVE 'D'       PTDL
     C                     UPDATMGOMSGD0
     C                     READ MGOMSGD0                 06
     C                     END
      *
      **  Message-part flagged as part deleted
     C                     MOVE 'Y'       FOUND   1
     C                     END
     C                     END
      *
      **  For MT210, part found when part position within multiple on
      **  the reference file is equal to the number of field :21:'s
      **  we have read within the message on the data file
      *
     C           @MTPY     IFEQ '210'
     C           MTAG      ANDEQ':21: '
     C                     ADD  1         P210
     C           P210      IFEQ @PT210
      *
      **  For this part of the multiple message, flag as part deleted
      *
     C           MTAG      DOUEQ':21: '
     C           *IN06     OREQ '1'
     C           @HEAD     ORNE TRNO
     C                     MOVE 'D'       PTDL
     C                     UPDATMGOMSGD0
     C                     READ MGOMSGD0                 06
     C                     END
      *
      **  Message-part flagged as part deleted
     C                     MOVE 'Y'       FOUND   1
     C                     END
     C                     END
      *
     C                     READ MGOMSGD0                 06
     C                     END
      *
     C                     ENDSR
      *
     C/EJECT
      *****************************************************************
      *   CONVRT - determine type of deletion processing              *
      *   Called by   DLT203 Perform MT203 deletion                   *
      *   Calls       UPDREF  update message status on the ref. file  *
      *               PART    Store last part of the one part MT203   *
      *               OUT202  Output one part MT203 as an MT202       *
      *****************************************************************
      *
     C           CONVRT    BEGSR
      *
      **  Flag status of multiple message as deleted (DLTD)
      *
     C                     Z-ADD0         PARTS
     C                     EXSR UPDREF
      *
      **  Access multiple message on the message data file
      *
     C           @HEAD     CHAINMGOMSGD0             06
      *
      **  Multiple message not found
     C           *IN06     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '007'     DBASE            * * * * * * * *
     C                     MOVEL@HEAD     DBKEY            *  DBERR 007  *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *                                                                   183583
      ** Position counter to reference MGOEXTPD later.                    183583
      *                                                                   183583
     C                     Z-ADD1         @VPOS                           183583
      *
      **  Access field :30: to retrieve Value Date (2nd record)
      *
     C                     READ MGOMSGD0                 06
     C                     ADD  1         @VPOS                           183583
     C                     MOVELMFLD      @DATE   6
      *
      **  Store fields 52/53 (if present)
      *
     C                     Z-ADD0         J       20
     C                     READ MGOMSGD0                 06
     C                     ADD  1         @VPOS                           183583
      *
     C           MTAG      DOWNE':20: '
     C           *IN06     ANDNE'1'
      *
     C                     ADD  1         J
     C           J         OCUR MMSG
     C                     MOVELMTAG      MMTAG
     C                     MOVELMFLD      MMFLD
     C           J         OCUR W5FLD                                     183583
     C                     Z-ADD@VPOS     WVPOS                           183583
     C                     MOVELMTAG      WMTAG                           183583
     C                     MOVELMFLD      WMFLD                           183583
     C                     EXSR SEXTN                                     183583
     C                     READ MGOMSGD0                 06
     C                     ADD  1         @VPOS                           183583
     C                     END
      *
      **  Store the message part (fields 20/21/32B/56n/57n/58n/72)
      *
     C                     EXSR PART
      *
      **  Output the one part MT203 as an MT202
      *
     C                     EXSR OUT202
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   PART - Store the last part of the one part MT203            *
      *   Called by   CONVRT convert one part MT203 to an MT202       *
      *   Calls       -                                               *
      *****************************************************************
      *
     C           PART      BEGSR
      *
      **  Search through multiple message for part being deleted
      *
     C                     MOVE 'N'       FOUND   1
     C           @HEAD     DOWEQTRNO
     C           *IN06     ANDEQ'0'
     C           FOUND     ANDEQ'N'
      *
      **  Last live part found when field :20: contains the TRN of
      **  first live part found.
      *
     C                     MOVELMFLD      @FLD   16
     C/COPY WNCPYSRC,MG9205C012
     C           MTAG      IFEQ ':20: '
     C           @FLD      ANDEQFIRST
      *
      **  Save all the message data for this part
      *
     C                     Z-ADD0         X       20
     C                     Z-ADD0         Z       20                      183583
     C                     MOVE *BLANKS   WSMTAG  5                                           210005
     C                     MOVE *BLANKS   WSMFLD 50                                           210005
     C           MTAG      DOUEQ':20: '
     C           *IN06     OREQ '1'
     C           @HEAD     ORNE TRNO
     C                     ADD  1         X
     C                     ADD  1         Z                               183583
      *                                                                   183583
     C           X         IFEQ 4                                         183583
     C           J         IFNE 0                                         183583
     C                     Z-ADD@VPOS     WSVPS   20                      183583
     C                     MOVEL@MTAG     WSMTAG                                              210005
     C                     MOVEL@MFLD     WSMFLD                                              210005
     C                     Z-ADD0         WCNTR   20                      183583
      *                                                                   183583
     C           WCNTR     DOWNEJ                                         183583
     C                     ADD  1         WCNTR                           183583
     C           WCNTR     OCUR W5FLD                                     183583
     C                     MOVELWMTAG     @MTAG                           183583
     C                     MOVELWMFLD     @MFLD                           183583
     C                     Z-ADDWVPOS     @VPOS                           183583
     C                     EXSR SEXTN                                     183583
     C                     ADD  1         Z                               183583
     C                     ENDDO                                          183583
      *                                                                   183583
     C                     Z-ADDWSVPS     @VPOS   20                      183583
     C                     MOVE *BLANKS   @MTAG                                               210005
     C                     MOVE *BLANKS   @MFLD                                               210005
     C                     MOVELWSMTAG    @MTAG                                               210005
     C                     MOVELWSMFLD    @MFLD                                               210005
     C                     ENDIF                                          183583
     C                     ENDIF                                          183583
      *                                                                   183583
     C           X         OCUR MULT
     C                     MOVELMTAG      @MTAG
     C                     MOVELMFLD      @MFLD
     C                     EXSR SEXTN                                     183583
     C                     READ MGOMSGD0                 06
     C           *IN06     IFEQ *OFF                                      183583
     C                     ADD  1         @VPOS                           183583
     C                     ENDIF                                          183583
     C                     END
      *
      **  Indicate the part has been found and save the end point.
     C                     MOVE 'Y'       FOUND   1
     C                     Z-ADDX         STOP    20
     C                     END
      *
     C                     READ MGOMSGD0                 06
     C           *IN06     IFEQ *OFF                                      183583
     C                     ADD  1         @VPOS                           183583
     C                     ENDIF                                          183583
     C                     END
      *
     C                     ENDSR
      /EJECT                                                              183583
      *****************************************************************   183583
      *                                                               *   183583
      *   SEXTN - Store customer with SWIFT address to data structure *   183583
      *                                                               *   183583
      *   Called by: PART                                             *   183583
      *   Calls    : Nothing                                          *   183583
      *                                                               *   183583
      *****************************************************************   183583
      *                                                                   183583
     C           SEXTN     BEGSR                                          183583
      *                                                                   183583
      ** Previous line was an a/c line, so move saved msg tag.            183583
      *                                                                   183583
     C           @MTAG     IFEQ *BLANKS                                   183583
     C           SAVMTG    ANDNE*BLANKS                                   183583
     C                     MOVELSAVMTG    @MTG                            183583
     C                     MOVE *BLANKS   SAVMTG                          183583
     C                     ELSE                                           183583
     C                     MOVEL@MTAG     @MTG                            183583
     C                     MOVEL@MTAG     SAVMTG                          183583
     C                     ENDIF                                          183583
      *                                                                   183583
     C           FLDTYP    IFEQ '5'                                       183583
     C           FLDTAG    ANDEQ'A'                                       183583
     C           FLDTYP    OREQ '8'                                       183583
     C           FLDTAG    ANDEQ'A'                                       183583
      *                                                                   183583
      ** Access the Swift Address's customer from the cancelled           183583
      ** message.                                                         183583
      *                                                                   183583
     C           KEXTN     CHAINMGOEXTLF             1818                 183583
     C           *IN18     IFEQ *OFF                                      183583
     C                     ADD  1         V                               183583
     C           V         OCUR EXTN                                      183583
     C                     Z-ADDZ         MPSN                            183583
     C                     MOVELVMTG      MTG                             183583
     C                     MOVELVMCSI     MCSI                            183583
     C                     ENDIF                                          183583
     C                     ENDIF                                          183583
      *                                                                   183583
     C           SEXT9     ENDSR                                          183583
     C/EJECT
      *****************************************************************
      *   OUT202 - Output the converted one part MT203 as an MT202    *
      *   Called by   CONVRT convert one part MT203 to an MT202       *
      *   Calls       TRAN  Retrieve msg. seq. from transaction file  *
      *               WREF  Output a reference record for the MT202   *
      *****************************************************************
      *
     C           OUT202    BEGSR
      *
      **  Access transaction to update S.W.I.F.T. sequence number
      *
     C                     EXSR TRAN
      *                                                                   103007
     C           *IN07     IFEQ '0'                                       103007
      *
      **  Update field :20:, use updated S.W.I.F.T. seq no.
      *
     C           1         OCUR MULT
     C/COPY WNCPYSRC,MG9205C013
     C           CLE007    IFEQ 'Y'                                                           186066
     C           @MODI     ANDEQ'LF'                                                          186066
     C           CLE007    OREQ 'Y'                                                           186066
     C           @MODI     ANDEQ'LS'                                                          186066
      *                                                                                       186066
     C           DBLNK     IFEQ *BLANKS                                                       186066
     C           @MODI     ANDEQ'LF'                                                          186066
     C                     MOVEL@MFLD     WTM11  11                                           186066
     C                     MOVE FLSWS     WTM2    2                                           186066
     C                     MOVE WTM2      WTM11                                               186066
     C                     MOVELWTM11     @MFLD                                               186066
     C                     MOVELWTM11     @TRNO                                               186066
     C                     ELSE                                                               186066
     C                     MOVEL@MFLD     WTM16  16                                           186066
     C                     MOVE FLSWS     WTM2                                                186066
     C                     MOVE WTM2      WTM16                                               186066
     C                     MOVELWTM16     @MFLD                                               186066
     C                     MOVELWTM16     @TRNO                                               186066
     C                     ENDIF                                                              186066
     C                     ELSE                                                               186066
     C                     MOVEA@MFLD     @WRK
     C                     MOVE LSWS      @LSWS   3
     C                     MOVEA@LSWS     @WRK,9
     C                     MOVEA@WRK      @MFLD
     C                     MOVEA@WRK      @TRNO  16
     C                     ENDIF                                                              186066
     C/COPY WNCPYSRC,MG9205C014
      *
      **  The new transaction reference number should be unique
      *
     C           @TRNO     CHAINMGREFL0              11
      *
     C           *IN11     IFEQ '0'
     C           *LOCK     IN   LDA
     C                     MOVE '008'     DBASE            * * * * * * * *
     C                     MOVEL@TRNO     DBKEY            *  DBERR 008  *
     C                     MOVEL'MGOREFL0'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      **  Write a reference record for this new MT202 (PF/MGOREFL0)
      *
     C                     EXSR WREF
      *
      **  Field :32A:  concatenate value date and field 32B
      *
     C           3         OCUR MULT
     C                     MOVE ':32A:'   @MTAG
     C                     MOVEL@MFLD     T32B
     C                     MOVELFLD32A    @MFLD
      *
      **  Write fields :20: :21: and :32A:
      *
     C                     MOVE *BLANK    PTDL
     C                     Z-ADD0         X
     C                     DO   3
     C                     ADD  1         X
     C           X         OCUR MULT
      *
     C                     MOVEL@MTAG     MTAG
     C                     MOVEL@MFLD     MFLD
      *
     C                     WRITEMGOMSGD0               06
      *
      **  Error on write to PF/MGOMSGPD (message data file)
     C           *IN06     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '009'     DBASE            * * * * * * * *
     C                     MOVELMTAG      DBKEY            *  DBERR 009  *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C                     END
      *
      **  Write the fields :52n: & :53n: (if used...)
      *
     C           J         IFNE 0
      *
     C                     Z-ADD0         I       20
     C           I         DOWNEJ
     C                     ADD  1         I
     C           I         OCUR MMSG
     C                     MOVELMMTAG     MTAG
     C                     MOVELMMFLD     MFLD
     C                     WRITEMGOMSGD0               06
      *
      **  Error on write to PF/MGOMSGPD (message data file)
     C           *IN06     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '010'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 010  *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C                     END
     C                     END
      *
      **  Write the remaining fields to PF/MGOMSGPD
      *
     C           X         DOWNESTOP
     C           STOP      ANDNE0                                         142484
     C                     ADD  1         X
     C           X         OCUR MULT
     C                     MOVEL@MTAG     MTAG
     C                     MOVEL@MFLD     MFLD
     C                     WRITEMGOMSGD0               06
      *
      **  Error on write to PF/MGOMSGPD (message data file)
     C           *IN06     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '011'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 011  *
     C                     MOVEL'MGOMSGPD'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
     C                     END
      *                                                                   183583
      ** Write multiple occurrence data structure to MGOEXTPD             183583
      *                                                                   183583
     C                     Z-ADD1         X       20                      183583
     C           X         OCUR EXTN                                      183583
      *                                                                   183583
     C           X         DOWLEV                                         183583
     C           MCSI      ANDNE*BLANKS                                   183583
     C                     WRITEMGOEXTD0               18                 183583
      *                                                                   183583
      ** Error on write to PF/MGOEXTPD (message data extension file)      183583
      *                                                                   183583
     C           *IN18     IFEQ *ON                                       183583
     C                     MOVE MCSI      @VTAG  13                       183583
     C                     MOVELMTG       @VTAG                           183583
     C           *LOCK     IN   LDA                                       183583
     C                     MOVE '069'     DBASE            ************** 183583
     C                     MOVEL@TRNO     DBKEY            * DBERROR 69 * 183583
     C                     MOVE @VTAG     DBKEY            ************** 183583
     C                     MOVEL'MGOEXTPD'DBFILE                          183583
     C                     OUT  LDA                                       183583
     C                     EXSR *PSSR                                     183583
     C                     ENDIF                                          183583
     C                     ADD  1         X                               183583
     C           X         OCUR EXTN                                      183583
     C                     ENDDO                                          183583
     C                     END                                            103007
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   TRAN  - Access the appropriate transaction file             *
      *   Called by   OUT202  output one part MT203 as an MT202       *
      *   Calls       -                                               *
      *****************************************************************
      *
     C           TRAN      BEGSR
      *
      **  Retrieve Midas Transaction Number of last live part
      *
     C                     MOVELFIRST     TTRN
      *
      **  DEALING transaction
      *
     C           @MODI     IFEQ 'DL'
     C                     OPEN DEALS
     C           MTRAN     CHAINDEALS                07
      *
      **  Dealing transaction not found
      *
     C           *IN07     IFEQ '1'
     C************LOCK     IN   LDA                                       103007
     C***********          MOVE '012'     DBASE            * * * * * * * *103007
     C***********          MOVELMTRAN     DBKEY            *  DBERR 012  *103007
     C***********          MOVEL'LF/DEALS'DBFILE           * * * * * * * *103007
     C***********          OUT  LDA                                       103007
     C***********          EXSR *PSSR                                     103007
     C***********          END                                            103007
     C                     ELSE                                           103007
      *
      **  Increment S.W.I.F.T. seq. number & update appropriate file
     C                     ADD  1         LSWS
      *
      **  Foreign Exchange deal (swaps/outrights)
     C           RCDT      IFEQ 'B'
     C                     UPDATDEALSDBF
     C                     END
      *
      **  Takings/Placings/Time Deposits/Loans
     C           RCDT      IFEQ 'C'
     C                     UPDATDEALSDCF
     C                     END
      *
      **  Negotiable Assets Purchased
     C           RCDT      IFEQ 'D'
     C                     UPDATDEALSDDF
     C                     END
      *
      **  Negotiable Assets Sold
     C           RCDT      IFEQ 'E'
     C                     UPDATDEALSDEF
     C                     END
     C                     END                                            103007
     C                     END
      *
      **  FRA/IRS transaction
      *
     C           @MODI     IFEQ 'FI'
     C                     OPEN DEALS
     C           MTRAN     CHAINDEALS                07
      *
      **  FRA/IRS transaction not found
      *
     C           *IN07     IFEQ '1'
     C************LOCK     IN   LDA                                       103007
     C***********          MOVE '012'     DBASE            * * * * * * * *103007
     C***********          MOVELMTRAN     DBKEY            *  DBERR 012  *103007
     C***********          MOVEL'LF/DEALS'DBFILE           * * * * * * * *103007
     C***********          OUT  LDA                                       103007
     C***********          EXSR *PSSR                                     103007
     C***********          END                                            103007
     C                     ELSE                                           103007
      *
      **  Increment S.W.I.F.T. seq. number & update appropriate file
     C                     ADD  1         LSWS
      **  FRA/IRS
      *
     C           RCDT      IFEQ 'G'
     C                     UPDATDEALSDGF
     C                     END
     C                     END                                            103007
      *
     C                     END
      *
      **  LENDING transaction
      *
     C           @MODI     IFEQ 'LE'
     C                     OPEN CLOAN
     C********** MTRAN     CHAINCLOAN                08                                       CLE148
     C           MTRANA    CHAINCLOAN                08                                       CLE148
      *
      **  Lending transaction not found
      *
     C           *IN08     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '013'     DBASE            * * * * * * * *
     C                     MOVELMTRAN     DBKEY            *  DBERR 013  *
     C                     MOVEL'LF/CLOAN'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      **  Increment S.W.I.F.T. seq. number & update lending file
      *
     C                     ADD  1         LSWS
     C                     UPDATCLOANCLF
     C                     END
      *
      ****SECURITIES TRADING transaction
      *
     C***********@MODI     IFEQ 'SE'
     C***********          MOVE MTRAN     MTRAD   6
     C***********MTRAD     CHAINTRADS                09
      *
      **  Securities transaction not found
      *
     C************IN09     IFEQ '1'
     C************LOCK     IN   LDA
     C***********          MOVE '014'     DBASE            * * * * * * * *
     C***********          MOVELMTRAD     DBKEY            *  DBERR 014  *
     C***********          MOVEL'LF/TRADS'DBFILE           * * * * * * * *
     C***********          OUT  LDA
     C***********          EXSR *PSSR
     C***********          END
      *
      ****Increment S.W.I.F.T. seq. number & update trading file
      *
     C***********          ADD  1         TSSQ
     C***********          Z-ADDTSSQ      LSWS
     C***********          UPDATTRADSDF
     C***********          END
      *                                                                   S01386
      **  SECURITIES TRADING transaction                                  S01386
     C           @MODI     IFEQ 'SE'                                      S01386
     C                     MOVE MTRAN     MTRAD   6                       S01386
     C                     OPEN TRADS                                     S01386
     C           MTRAD     CHAINTRADS                09                   S01386
      *                                                                   S01386
      **  Securities transaction not found                                S01386
     C           *IN09     IFEQ '1'                                       S01386
     C           *LOCK     IN   LDA                                       S01386
     C                     MOVE '014'     DBASE            * * * * * * * *S01386
     C                     MOVELMTRAD     DBKEY            *  DBERR 014  *S01386
     C                     MOVEL'LF/TRADS'DBFILE           * * * * * * * *S01386
     C                     OUT  LDA                                       S01386
     C                     EXSR *PSSR                                     S01386
     C                     END                                            S01386
      *                                                                   S01386
      **  Increment S.W.I.F.T. seq. number & update trading file          S01386
     C                     ADD  1         LSWS                            S01386
     C                     UPDATTRADSDF                                   S01386
     C                     END                                            S01386
     C/COPY WNCPYSRC,MG9205C015
      *                                                                   CDL002
      ** If the parameter module id is Netting (NT).Access FXNETMD        CDL002
      *                                                                   CDL002
     C           @MODI     IFEQ 'NT'                                      CDL002
     C                     OPEN FXNETML4                                  CDL002
     C           MTRAN     CHAINFXNETML4             99                   CDL002
      *                                                                   CDL002
      **  Netting transaction not found                                   CDL002
      *                                                                   CDL002
     C           *IN99     IFEQ '1'                                       CDL002
     C           *LOCK     IN   LDA                                       CDL002
     C                     MOVE '015'     DBASE              * * * * * * *CDL002
     C                     MOVELMTRAN     DBKEY              * DBERR 015 *CDL002
     C                     MOVEL'FXNETML4'DBFILE             * * * * * * *CDL002
     C                     OUT  LDA                                       CDL002
     C                     EXSR *PSSR                                     CDL002
     C                     END                                            CDL002
      *                                                                   CDL002
      **  Increment S.W.I.F.T. seq. number & update netting file          CDL002
      *                                                                   CDL002
     C                     ADD  1         LSWS                            CDL002
     C                     UPDATFXNETMD0                                  CDL002
     C                     END                                            CDL002
      *                                                                   CDL002
      *
      ** If CLE007 is switched on, increment the Last Swift Sequence      CLE007
      ** by 1 then update the Fees file.                                  CLE007
      *                                                                   CLE007
     C           CLE007    IFEQ 'Y'                                       CLE007
     C           @MODI     ANDEQ'LF'                                      CLE007
      *                                                                   CLE007
     C                     OPEN LEFEEDL1                                  CLE007
      *                                                                   CLE007
     C                     MOVE *BLANKS   WTNUM                           CLE007
     C                     MOVE *BLANKS   DSFEE                           CLE007
      *                                                                   CLE007
     C                     MOVE @TNUM     WTNUM  16                       CLE007
     C***********          Z-ADDDCNUM     WCNUM   60                CLE007CLE009
     C***********DFIND     IFEQ 'F'                                 CLE007CLE009
     C           DBLNK     IFNE *BLANKS                                   CLE009
     C                     MOVE DFACL     WFACL   50                      CLE007
     C**********           Z-ADD0         WLNRF                                        CLE007 CLE148
     C                     MOVEL*BLANKS   WLNRF                                               CLE148
     C                     Z-ADDDFFSQ     WFSEQ   20                      CLE009
     C**********           Z-ADDDCNUM     WCNUM   60                                   CLE009 CSD027
     C                     MOVE DCNUM     WCNUM   6                                           CSD027
     C                     ELSE                                           CLE007
      *                                                                   CLE009
     C                     OPEN CLOAN                                     CLE009
     C********** MTRAN     CHAINCLOAN                08                                CLE009 186066
     C**********           MOVE DLNRF     WLOAN   60                                   186066 CLE148
     C                     MOVE DLNRF     WLOAN   6                                           CLE148
     C           WLOAN     CHAINCLOAN                08                                       186066
      **  Lending transaction not found                                   CLE009
     C           *IN08     IFEQ '1'                                       CLE009
     C           *LOCK     IN   LDA                                       CLE009
     C                     MOVE '020'     DBASE            * * * * * * * *CLE009
     C                     MOVELMTRAN     DBKEY            *  DBERR 020  *CLE009
     C                     MOVEL'LF/CLOAN'DBFILE           * * * * * * * *CLE009
     C                     OUT  LDA                                       CLE009
     C                     EXSR *PSSR                                     CLE009
     C                     END                                            CLE009
      *                                                                   CLE009
     C**********           Z-ADDCNUM      WCNUM   60                                   CLE009 CSD027
     C                     MOVE CNUM      WCNUM   6                                           CSD027
     C**********           MOVE DLNRF     WLNRF   60                                   CLE007 CLE148
     C                     MOVE DLNRF     WLNRF   6                                           CLE148
     C                     Z-ADD0         WFACL                           CLE007
     C                     Z-ADDDLFSQ     WFSEQ                           CLE009
     C                     ENDIF                                          CLE007
     C***********          Z-ADDDFSEQ     WFSEQ   20                CLE007CLE009
      *                                                                   CLE007
     C           KEYFEE    CHAINLEFEEDL1             12                   CLE007
      *                                                                   CLE007
     C           *IN12     IFEQ '1'                                       CLE007
     C           *LOCK     IN   LDA                                       CLE007
     C                     Z-ADD018       DBASE            ***************CLE007
     C                     MOVEL'LEFEED  'DBFILE           * DB ERROR 18 *CLE007
     C                     MOVELDSFEE     DBKEY            ***************CLE007
     C                     OUT  LDA                                       CLE007
     C                     EXSR *PSSR                                     CLE007
      *                                                                   CLE007
     C                     ELSE                                           CLE007
      *                                                                   CLE007
     C                     ADD  1         FLSWS                           CLE007
     C                     UPDATLEFEEDF                                   CLE007
      *                                                                   CLE007
     C                     ENDIF                                          CLE007
     C                     ENDIF                                          CLE007
      *                                                                   CLE007
     C                     ENDSR
     C/EJECT                                                                                  CSD015
      *****************************************************************                       CSD015
      *                                                               *                       CSD015
      *  SRWTCH - Watch Processing                                    *                       CSD015
      *                                                               *                       CSD015
      *****************************************************************                       CSD015
      *                                                                                       CSD015
     C           SRWTCH    BEGSR                                                              CSD015
      *                                                                                       CSD015
      ** Initialise parameter for Compliance Engine program.                                  CSD015
      *                                                                                       CSD015
     C                     MOVE *BLANK    WLTDS                                               CSD015
      *                                                                                       CSD015
     C                     Z-ADD0         W4VDAT                                              CSD015
     C                     Z-ADD0         W4MDAT                                              CSD015
     C                     Z-ADD0         W4AMT1                                              CSD015
     C                     Z-ADD0         W4AMT2                                              CSD015
      *                                                                                       CSD015
      ** Set up parameter for compliance engine program                                       CSD015
      *                                                                                       CSD015
     C                     MOVELTRNO      W4IDEN                                              CSD015
     C                     MOVELMTPY      W4ITEM                                              CSD015
     C                     MOVELBRCA      W4BRCH                                              CSD015
     C                     MOVEL'MGOREF  'W4FUNT                                              CSD015
     C                     MOVEL'N'       W4ARLS                                              CSD015
     C                     MOVELDECN      W4CNUM                                              CSD015
      *                                                                                       CSD015
      ** Convert MGDE with YYMMDD format into midas day number                                CSD015
      ** to be used as value for W4DDAT                                                       CSD015
      *                                                                                       CSD015
     C                     MOVE MGDE      WKMNTH  4                                           CSD015
     C                     MOVELMGDE      WKYEAR  2                                           CSD015
     C                     MOVELWKMNTH    WKDATE  60                                          CSD015
     C                     MOVE WKYEAR    WKDATE                                              CSD015
     C                     Z-ADD0         W4DDAT                                              CSD015
      *                                                                                       CSD015
     C                     CALL 'ZDATE1'                                                      CSD015
     C                     PARM *BLANKS   PRTCD                                               CSD015
     C                     PARM           WKDATE                                              CSD015
     C                     PARM 'M'       WKFRMT  1                                           CSD015
     C                     PARM           WKCNVT  50                                          CSD015
      *                                                                                       CSD015
     C           PRTCD     IFEQ *BLANKS                                                       CSD015
     C                     Z-ADDWKCNVT    W4DDAT                                              CSD015
     C                     ENDIF                                                              CSD015
      *                                                                                       CSD015
      ***Send*transaction*to compliance engine program                                 CSD015 221145
      *********************                                                            CSD015 221145
     C*********************CALL 'SDWLCLOP'                                             CSD015 221145
     C*********************PARM           WLTDS                                        CSD015 221145
     C*********************PARM *BLANKS   PSTRNG                                       CSD015 221145
      *                                                                                       CSD015
     C           EWATCH    ENDSR                                                              CSD015
      *                                                                                       CSD015
      *****************************************************************                       CSD015
     C/EJECT
      *****************************************************************                       221145
      *                                                               *                       221145
      *  WLCDQ - Submit information to Watch List Check Dtaq          *                       221145
      *                                                               *                       221145
      *****************************************************************                       221145
      *                                                                                       221145
     C           WLCDQ     BEGSR                                                              221145
      *                                                                                       221145
      *  Send transaction to compliance engine program                                        221145
      *                                                                                       221145
     C                     CALL 'SDWLCLOP'                                                    221145
     C                     PARM           WLTDS                                               221145
     C                     PARM *BLANKS   PSTRNG                                              221145
      *                                                                                       221145
      *                                                                                       221145
     C                     ENDSR                                                              221145
      *                                                                                       221145
     C/EJECT                                                                                  221145
      *****************************************************************
      *   WREF  - Output a reference record for the MT202             *
      *   Called by   OUT202  output one part MT203 as an MT202       *
      *   Calls       -                                               *
      *****************************************************************
      *
     C           WREF      BEGSR
      *
      **  Retrieve the reference information about the last live part
      *
     C           FIRST     CHAINMGREFL0              05
      *
      **  Last live part not found on reference file
     C           *IN05     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '015'     DBASE            * * * * * * * *
     C                     MOVELFIRST     DBKEY            *  DBERR 015  *
     C                     MOVEL'MGOREFL0'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
     C                     MOVEL@TRNO     TRNO
     C                     MOVE *BLANKS   TRNF
     C                     MOVE *BLANKS   TRNM
     C                     MOVE '202'     MTPY
      *
     C                     TIME           MGTM
     C                     MOVELBJMRDT    LADT
     C                     MOVE MGTM      LATM
     C                     MOVE 'CRTD'    MGST
     C                     MOVE '1'       MGSG
      *                                                                                       CSD015
      ** If Watch List Checking is Applied and CSW025 is not installed,                       CSD015
      ** do watch list processing.                                                            CSD015
      *                                                                                       CSD015
     C           CSD015    IFEQ 'Y'                                                           CSD015
     C           W1EWLC    ANDEQ'Y'                                                           222385
     C           CSW025    ANDEQ'N'                                                           CSD015
     C                     EXSR SRWTCH                                                        CSD015
     C                     ENDIF                                                              CSD015
      *
     C           MGST      IFEQ 'RSND'
     C                     MOVE MGDE      RELD
      *                                                                   113352
      ** Set-up Message Generation  Century flag                          113352
      *                                                                   113352
     C                     MOVELMGDE      YY      20                      113352
     C           YY        IFLT 72                                        113352
     C                     MOVE '2'       CIND                            113352
     C                     ELSE                                           113352
     C                     MOVE '1'       CIND                            113352
     C                     ENDIF                                          113352
     C                     MOVE MGTM      RELT
     C                     END
      *
     C                     MOVEL*BLANKS   AMTX
      *
     C                     MOVE *BLANKS   NETI
     C*********************MOVE *BLANKS   DELC                            090912
     C                     MOVE *BLANKS   DYST
     C                     MOVE *BLANKS   RSID
     C                     MOVE *BLANKS   MSE1
     C                     MOVE *BLANKS   ELIN
     C                     MOVE *BLANKS   SSAC
     C                     MOVE *BLANKS   MIR
     C/COPY WNCPYSRC,MG9205C007
      *
     C                     WRITEMGOREFD0               05
      *
      **  Error on write to PF/MGOREFPD (message reference file)
     C           *IN05     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '016'     DBASE            * * * * * * * *
     C                     MOVE *BLANKS   DBKEY            *  DBERR 016  *
     C                     MOVEL'MGOREFPD'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   INIT - inital process                                       *
      *   Called by - main process                                    *
      *   Calls     -                                                 *
      *****************************************************************
      *
     C           INIT      BEGSR
      *
     C                     MOVEACPY@      BIS@   80
      *
      **  Set up LDA for database error processing
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'MG9205'  DBPGM
     C                     MOVE *BLANKS   DBASE
     C                     OUT  LDA
      *
      **  Access SDBANKPD for Midas rundate
      *
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE '017'     DBASE            * * * * * * * *
     C                     MOVEL'FIRST'   DBKEY            *  DBERR 017  *
     C                     MOVEL'SDBANKPD'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C/COPY WNCPYSRC,MG9205C004
      *                                                                   CLE007
      ** Check if CLE007 is switched *ON.                                 CLE007
      *                                                                   CLE007
     C                     MOVE 'N'       CLE007  1                       CLE007
     C                     CALL 'AOSARDR0'                                CLE007
     C                     PARM *BLANKS   WRTCD   7                       CLE007
     C                     PARM '*VERIFY' WOPTN   7                       CLE007
     C                     PARM 'CLE007'  WSARD   6                       CLE007
     C           SCSARD    PARM SCSARD    DSFDY                           CLE007
      *                                                                   CLE007
     C           WRTCD     IFEQ *BLANKS                                   CLE007
     C                     MOVE 'Y'       CLE007                          CLE007
      *                                                                   CLE007
     C                     ELSE                                           CLE007
     C                     MOVE 'N'       CLE007                          CLE007
      *                                                                   CLE007
      ** Call to program ended in error                                   CLE007
      *                                                                   CLE007
     C           WRTCD     IFNE '*NRF   '                                 CLE007
     C           *LOCK     IN   LDA                                       CLE007
     C                     Z-ADD019       DBASE            ***************CLE007
     C                     MOVE 'SCSARDPD'DBFILE           * DB ERROR 19 *CLE007
     C                     MOVEL'CLE007'  DBKEY            ***************CLE007
     C                     OUT  LDA                                       CLE007
     C                     EXSR *PSSR                                     CLE007
     C                     ENDIF                                          CLE007
     C                     ENDIF                                          CLE007
      *                                                                                       CSW201
      ** Check if Swift 2001 Standards Update is installed                                    CSW201
      *                                                                                       CSW201
     C                     CALL 'SWIF2001'                                                    CSW201
     C                     PARM *BLANKS   WRTCD                                               CSW201
      *                                                                                       CSW201
     C           WRTCD     IFEQ 'CSW2001'                                                     CSW201
     C                     MOVE 'Y'       CSW201  1                                           CSW201
     C                     ELSE                                                               CSW201
     C                     MOVE 'N'       CSW201                                              CSW201
     C                     ENDIF                                                              CSW201
      *                                                                                       CSW201
      *                                                                                       CSD015
      ** Access SAR file to determine if Midas Compliance Watch                               CSD015
      ** - Watch List Checking (CSD015) is installed                                          CSD015
      *                                                                                       CSD015
     C                     CALL 'AOSARDR0'                                                    CSD015
     C                     PARM *BLANKS   PRTCD   7                                           CSD015
     C                     PARM '*VERIFY' POPTN   7                                           CSD015
     C                     PARM 'CSD015'  PSARD   6                                           CSD015
     C           SCSARD    PARM SCSARD    DSFDY                                               CSD015
      *                                                                                       CSD015
      ** An NRF (No Record Found) return code is valid.                                       CSD015
      ** Issue database error only for error return codes.                                    CSD015
      *                                                                                       CSD015
     C           PRTCD     IFNE *BLANKS                                                       CSD015
     C           PRTCD     ANDNE'*NRF   '                                                     CSD015
     C           *LOCK     IN   LDA                                                           CSD015
     C                     MOVEL'CSD015'  DBKEY                                               CSD015
     C                     MOVEL'SCSARDPD'DBFILE                                              CSD015
     C                     Z-ADD22        DBASE                                               CSD015
     C                     OUT  LDA                                                           CSD015
     C                     EXSR *PSSR                                                         CSD015
     C                     ENDIF                                                              CSD015
      *                                                                                       CSD015
     C           PRTCD     IFEQ *BLANK                                                        CSD015
     C                     MOVEL'Y'       CSD015  1                                           CSD015
      *                                                                                       222385
      ** Access AOWLCCR0 to check if function code MESSGENT is available                      222385
      *                                                                                       222385
     C                     CALL 'AOWLCCR0'                                                    222385
     C                     PARM *BLANKS   PRTCD                                               222385
     C                     PARM '*KEY'    POPTN                                               222385
     C                     PARM 'MESSGENT'PFUNC   8                                           222385
     C           SDWLCC    PARM SDWLCC    DSFDY                                               222385
      *                                                                                       222385
     C           PRTCD     IFNE *BLANKS                                                       222385
     C           PRTCD     ANDNE'*NRF'                                                        222385
     C                     MOVEL'SDWLCCPD'DBFILE                                              222385
     C                     MOVELPFUNC     DBKEY                                               222385
     C                     Z-ADD32        DBASE                                               222385
     C                     EXSR *PSSR                                                         222385
     C                     ENDIF                                                              222385
      *                                                                                       222385
     C                     ELSE                                                               CSD015
     C                     MOVEL'N'       CSD015                                              CSD015
     C                     MOVEL'N'       W1EWLC                                              222385
     C                     ENDIF                                                              CSD015
      *                                                                                       CSD015
      ** Is CSW025 installed?                                                                 CSD015
      *                                                                                       CSD015
     C                     MOVE 'N'       CSW025  1                                           CSD015
     C                     CALL 'AOSARDR0'                                                    CSD015
     C                     PARM '       ' PRTCD                                               CSD015
     C                     PARM '*VERIFY' POPTN                                               CSD015
     C                     PARM 'CSW025'  PSARD                                               CSD015
     C           SCSARD    PARM SCSARD    DSFDY                                               CSD015
      *                                                                                       CSD015
     C           PRTCD     IFNE *BLANK                                                        CSD015
     C           PRTCD     ANDNE'*NRF   '                                                     CSD015
     C                     MOVEL'SCSARDPD'DBFILE                                              CSD015
     C                     Z-ADD23        DBASE                                               CSD015
     C                     MOVELPRTCD     DBKEY                                               CSD015
     C                     EXSR *PSSR                                                         CSD015
     C                     ENDIF                                                              CSD015
      *                                                                                       CSD015
     C           PRTCD     IFEQ *BLANK                                                        CSD015
     C                     MOVE 'Y'       CSW025                                              CSD015
     C                     ENDIF                                                              CSD015
      *                                                                                       CSD015
     C/COPY WNCPYSRC,MG9205C005
      *                                                                   CLE007
      ** Fees Key.                                                        CLE007
      *                                                                   CLE007
     C           KEYFEE    KLIST                                          CLE007
     C                     KFLD           WCNUM                           CLE007
     C                     KFLD           WFACL                           CLE007
     C                     KFLD           WLNRF                           CLE007
     C                     KFLD           WFSEQ                           CLE007
     C/COPY WNCPYSRC,MG9205C008
      *
      **  Initialise constant fields
      *
     C                     MOVE *LOVAL    CR      1
     C                     BITON'457'     CR
     C                     MOVE *LOVAL    LF      1
     C                     BITON'257'     LF
     C                     MOVELCR        CRLF    2
     C                     MOVE LF        CRLF
      *
      **  CRLF Control Character for message data records
     C                     MOVE CRLF      CTRC
      *                                                                   183583
      ** KLIST for MGOEXTLF to access the remaining message.              183583
      *                                                                   183583
     C           KEXTN     KLIST                                          183583
     C                     KFLD           @HEAD                           183583
     C                     KFLD           @MTPY                           183583
     C                     KFLD           @VPOS   20                      183583
     C                     Z-ADD*ZERO     V       20                      183583
     C                     MOVE *BLANKS   SAVMTG  5                       183583
      *
     C                     ENDSR
     C/COPY WNCPYSRC,MG9205C009
     C/EJECT
      *****************************************************************
      *   *PSSR - error handling                                      *
      *****************************************************************
      *
     C           *PSSR     BEGSR
     C                     MOVE 'Y'       @ERCD
     C                     SETON                     U7U8LR
     C                     DUMP                                           E37455
     C                     RETRN
     C                     ENDSR
     C/EJECT
      *
** CPY@   Object Copyright
(c) Misys International Banking Systems Ltd. 2001
