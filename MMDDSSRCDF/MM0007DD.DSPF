      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM Daylight Overnight limits maint')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Money Market (Dealing)
     A*                                                               *
     A* DSPF/MM0007DD- LIMITS BY CURRENCY MAINTAINANCE                *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA03             Date 31Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 112563             Date 31Jan97               *
     A*                 090199             Date 12Jul95               *
     A*                 S01319             DATE 19NOV91               *
     A*                 E23462             DATE  1OCT90               *
     A*                 S01253             DATE 17APR90               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA03 - Webfacing Changes                                   *
     A*  112563  -  Header and footer changes required for GUI        *
     A*             Multilanguage support.                            *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*  S01319  -  Changes for Project Harry - add help              *
     A*  E23462  -  Remove all references to DRS.                     *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS              *
     A*                                                              *
     A****************************************************************
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      CF12
     A  30                                  CF10
     A                                      MSGLOC(22)
      *                          Beginning of change       /S01319/
      *                                End of change       /S01319/
     A*
     A          R MM0007S1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A          R MM0007S0                  TEXT('MAINTENANCE SCREEN')
     A                                      SFLCTL(MM0007S1)
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A N42                                  SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A N39                                  CF08
     A N39                                  CF07
     A N39                                  CF15
     A                                      PUTOVR
     A                                      BLINK
     A            DDPGMQ                    SFLPGMQ
     A                                  1  2'Ref: MM0007'
      *                                                  START OF CHANGE  E23462
     A**********************************1 28'MIDAS DEALING ROOM SYSTEM'
     A                                  1 26'            Midas            '
     A**************************************DSPATR(UL HI)
     A                                      DSPATR(HI)
      *                                                     END OF CHANGE E23462
     A            DDTIME         6  0O  1 63TEXT('TIME')
     A                                      EDTWRD('0 :  :  ')
     A                                      OVRDTA
     A                                  1 72'WID:'
     A            DDWID          3   O  1 77TEXT('WORKSTATION ID')
     A*
     A                                  3 23'MM DEALER - LIMITS BY CURRENCY'
     A                                  3  2'Action:'
     A            DDACTN         1   B  3 10TEXT('ACTION CODE')
     A                                      DSPATR(UL)
     A N29N31N32
     AO 20                                  DSPATR(PC)
     A  20                                  DSPATR(RI)
     A  20                                  OVRATR
     A N20                                  OVRDTA
     A            DDLIST         9   O  3 12TEXT('ACTION CODE TEXT')
     A                                      OVRDTA
     A*
     A                                  6  6'Currency Code:'
     A                                      DSPATR(HI)
     A            DDCCY     R        B  6 22REFFLD(CCY)
     A                                      CHGINPDFT
     A N39                                  DSPATR(UL)
     A  39                                  DSPATR(PR)
     A  21                                  DSPATR(RI)
     A  31
     AO 21                                  DSPATR(PC)
     A  31
     AO 21                                  OVRATR
     A N21                                  OVRDTA
     A*                                                               *
     A                                  6 31'Currency Name:'
     A            DDCCNM        14   O  6 46TEXT('Currency name')
     A                                      OVRDTA
     A                                  9  6'Limit in 000s'
     A*
     A            DDLMIT        11S 0B  9 31TEXT('Limit')
     A  22                                  DSPATR(RI)
     A  32
     AO 22                                  DSPATR(PC)
     A  32
     AO 22                                  OVRATR
     A N22                                  OVRDTA
     A*
     A          R MM0007D0                  TEXT('MAIN SCREEN FOOTER')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A N39                                  CF08
     A N39                                  CF07
     A N39                                  CF15
      *                          Beginning of change       /S01319/
      *                                End of change       /S01319/
     A**                                             Begin /112563/
     A***********                      23  2'F10 - CONFIRM DELETE'
     A                                 23  2'F10=CONFIRM DELETE'
     A**                                             End   /112563/
     A  30                                  DSPATR(HI)
     A N30                                  DSPATR(ND)
     A                                      OVRATR
     A**                                             Begin /112563/
     A***********                      24  1'F3-End  F12-Previous Screen '
     A***********                         +1'F8/F7-Next/Previous Record '
     A                                 24  2'F3=Exit  F12=Prev. Screen'
     A                                    +2'F8=Next Record'
     A  39                                  DSPATR(ND)
     A                                      OVRATR
     A                                    +2'F7=Prev. Record'
     A**                                             End   /112563/
     A  39                                  DSPATR(ND)
     A                                      OVRATR
     A**                                             Begin /112563/
     A***********                         +1'F15-Select Screen'
     A                                    +2'F15=Select Screen'
     A**                                             End   /112563/
     A  39                                  DSPATR(ND)
     A                                      OVRATR
     A*
     A          R MM0007S3                  TEXT('Select Subfile')
     A                                      SFL
     A            DDOPT          1   B  8 14TEXT('Subfile Option')
     A                                      DSPATR(UL)
     A            DDCCY          3   O  8 26TEXT('Currency Code')
     A                                      DSPATR(HI)
     A*
     A            DDLMIT        11  0O  8 41TEXT('limit')
     A                                      EDTWRD('         0 ')
     A*
     A            DDDLUP         2  0H      TEXT('Day of last update')
     A*
     A            DDMLUP         3   H      TEXT('Month of last update')
     A*
     A            DDYLUP         2  0H      TEXT('Year of last update')
     A*
     A            DDTLUP         6  0H      TEXT('Time of last update')
     A*
     A          R MM0007S2                  TEXT('Select Screen')
     A                                      SFLCTL(MM0007S3)
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N41                                  SFLDSP
     A N41                                  SFLDSPCTL
     A  41                                  SFLCLR
     A  70                                  SFLEND
     A N70                                  ROLLUP(50)
      ** Start of change CAAA03
     A                                      OVERLAY
      ** End of change CAAA03
     A                                      PUTOVR
     A                                      BLINK
     A*
     A                                  1  2'Ref: MM0007'
      *                                                  START OF CHANGE  E23462
     A**********************************1 28'MIDAS DEALING ROOM SYSTEM'
     A                                  1 26'            Midas            '
     A**************************************DSPATR(UL HI)
     A                                      DSPATR(HI)
      *                                                     END OF CHANGE E23462
     A            DDTIME         6  0O  1 63TEXT('TIME')
     A                                      EDTWRD('0 :  :  ')
     A                                      OVRDTA
     A                                  1 72'WID:'
     A            DDWID          3   O  1 77TEXT('WORKSTATION ID')
     A*
     A                                  3 24'MM DEALER - LIMITS BY CURRENCY'
     A*
     A                                  4 37'ENQUIRY'
     A*
     A                                  6 11'Select'
     A                                  6 22'Currency Code'
     A                                  6 40'Limit in 000s'
     A            DDSFRN         4  0H      SFLRCDNBR
     A*
     A          R MM0007D1                  TEXT('SELECT SCREEN FOOTER')
     A                                      OVERLAY
     A                                 23  2'Rollup/Rolldown for further'
     A                                    +1'records'
     A**                                             Begin /112563/
     A***********                      24  1'F3-End  F12-Previous Screen'
     A                                 24  2'F3=Exit  F12=Previous Screen'
     A**                                             End   /112563/
