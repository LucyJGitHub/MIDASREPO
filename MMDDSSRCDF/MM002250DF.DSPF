      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM Adjustments to Amortisation Amount')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Dealing Module                          *
      *                                                               *
      *  DSPF/MM002250DF - Midas MM Adjustments to Amortisation Input *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
      *
     A                                      INDARA
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      MSGLOC(24)
      *
     A          R MM002250F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      OVERLAY
      *
     A            #0USER        10A  O  1  2TEXT('User Id')
     A                                  1 13'MM002250DF'
     A                                  1 28'ADJUSTMENTS TO AMORTISED-
     A                                       AMOUNT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            #0RUND         7A  O  1 73
     A            #0WSID        10A  O  2  2TEXT('Workstation Id')
     A                                  2 67TIME
      *
     A          R MM002250F1                TEXT('Details')
     A                                      OVERLAY
      *
     A                                 08 16'Deal no.'
     A            #1DLNO         6A  B 08 31TEXT('Deal No.')
     A                                      DSPATR(HI)
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL)
     A                                 10 16'Customer'
     A            #1CUST        10A  B 10 31TEXT('Customer')
     A                                      DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL)
     A                                 11 16'Adjustment'
     A            #1AMNT        14A  B 11 31TEXT('Adjustment Amount')
     A                                      DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL)
     A                                 12 16'Sign (+ or -)'
     A            #1SIGN         1A  B 12 31TEXT('Sign')
     A                                      DSPATR(HI)
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(UL)
     A                                 13 16'Currency'
     A            #1CURR         3A  B 13 31TEXT('Currency')
     A                                      DSPATR(HI)
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(UL)
      *
     A          R MM002250F2                TEXT('Footer')
     A                                      OVERLAY
     A                                 23  2'F3 = Exit  F5 = Clear'
      *
     A          R MM002250S0                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #0MSGK                    SFLMSGKEY
     A            #0PGM                     SFLPGMQ
      *
     A          R MM002250C0                TEXT('Program messages')
     A                                      SFLCTL(MM002250S0)
     A                                      SFLPAG(01) SFLSIZ(05)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #0PGM                     SFLPGMQ
      *
