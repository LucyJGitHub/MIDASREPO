      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM Range authorization (Deams)')
     A****************************************************************
     A*                                                              *
     A*  Midas - Money Market (Dealing)
     A*                                                              *
     A*     DSPF/MM0025DD - RANGE AUTHORIZATIONS (DEAMS)             *
     A*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 090199             Date 12JUL95               *
     A*  PREV AMEND NO. 080048             DATE 06DEC94               *
     A*       AMEND NO. E39162             DATE 18JUN92               *
     A*                 S01314                  07FEB91               *
     A*                 E23462                  02OCT90               *
     A*                 S01253                  17APR90               *
     A*                                                              *
     A****************************************************************
     A*                                                              *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*  080048 - SPECIFY CORRECT HELP TEXT TAG                       *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*  S01314  -  Release 10 PTF Incorporation changes: add help   *
     A*             facility.                                        *
     A*  E23462  -  Remove all references to DRS.                    *
     A*  S01253  -  RECOMPILED FOR MULTILANGUAGE SCREENS             *
     A*                                                              *
     A****************************************************************
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      MSGLOC(19)
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      CF03
     A*---------------------------------------------------------------
     A          R MM0025S1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*---------------------------------------------------------------
     A                                      SFL
     A                                      SFLMSGRCD(19)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*---------------------------------------------------------------
     A          R MM0025S0
     A*---------------------------------------------------------------
     A                                      SFLCTL(MM0025S1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  59                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(4)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDPGMQ                    SFLPGMQ
     A                                  1  2'Ref: MM0025'
      *                                                  START OF CHANGE  E23462
     A**********************************1 28'MIDAS DEALING ROOM SYSTEMS'
     A                                  1 26'            Midas            '
      *                                                     END OF CHANGE E23462
     A                                      DSPATR(HI)
     A**************************************DSPATR(UL)     /E23462/
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 25'MM DEALER - RANGE AUTHORIZATIONS'
     A                                  4 31'Deal Amendments'
     A                                  6 41'Value'
     A                                  7 32'Deam'
     A                                  7 41'Date'
     A                                  7 50'Seq'
      *========================
      * Start of range
      *========================
     A                                  9  6'First Amendment in Range:'
 
      * Deal number
     A            DDFDLN    R     A  B  9 32REFFLD(DLNO)
     A                                      OVRDTA
     A  13                                  DSPATR(PR)
     A N61
     AO 10                                  DSPATR(PC)
     A  10                                  DSPATR(RI)
 
      * Value date
     A            DDFVLD         6A  B  9 41
     A                                      OVRDTA
     A  13                                  DSPATR(PR)
     A  11                                  DSPATR(RI PC)
 
      * Sequence number
     A            DDFSEQ         3A  B  9 50
     A                                      OVRDTA
     A  13                                  DSPATR(PR)
     A  12                                  DSPATR(RI PC)
      *========================
      * End of range
      *========================
     A                                 11  6'Last Amendment in Range:'
 
      * Deal number
     A            DDLDLN    R     A  B 11 32REFFLD(DLNO)
     A                                      OVRDTA
     A  13                                  DSPATR(PR)
     A  16                                  DSPATR(RI PC)
 
      * Value date
     A            DDLVLD         6A  B 11 41
     A                                      OVRDTA
     A  13                                  DSPATR(PR)
     A  14                                  DSPATR(RI PC)
 
      * Sequence number
     A            DDLSEQ         3A  B 11 50
     A                                      OVRDTA
     A  13                                  DSPATR(PR)
     A  15                                  DSPATR(RI PC)
     A*---------------------------------------------------------------
     A          R MM0025D0
     A*---------------------------------------------------------------
     A                                      OVERLAY
     A                                      BLINK
     A                                 24  1'F3-End'
