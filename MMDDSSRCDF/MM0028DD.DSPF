      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas MM Deal amendments input/Validation')
     A****************************************************************
     A*                                                              *
     A*  Midas - Money Market (Dealing)
     A*                                                              *
     A*     DSPF/MM0028DD - BACK OFFICE DEAL INPUT - DEAL AMENDMENTS *
     A*                      INPUT AND AUTHORIZATION                 *
     A*                                                              *
      *  (c) Finastra International Limited 2001                      *
     A*                                                              *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 112563             Date 31Jan97               *
      *                 S01408             Date 13Sep96               *
     A*                 CAC001             DATE 01FEB96               *
     A*                 S01408             DATE 03AUG95               *
     A*                 090199             DATE 12JUL95               *
     A*                    051307    DATE  26APR93                   *
     A*                    S01314    DATE  7FEB90                    *
     A*                    E23462    DATE  3OCT90                    *
     A*                    S01184    DATE 15NOV89                    *
     A*                    S01253    DATE 17APR90                    *
     A*                    LN0242    DATE 26JUN90                    *
     A*                    E20777    DATE 25APR90                    *
     A*                    S01227    DATE 25APR90                    *
     A*                    E301      DATE 30MAR90                    *
     A*                    S01199    DATE 17/05/89                   *
     A*                    E18139          DATE   01NOV89            *
     A*                    S01199          DATE   17/05/89           *
     A*                    S01177          DATE   28/10/88           *
     A*                                                              *
     A****************************************************************
     A*                                                              *
      *  MD046248 - Finastra Rebranding                               *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  S01408  -  Core hook MM0028DD06 added                       *
     A*             Core hook MM0028DD05 added                       *
     A*             Core hook MM0028DD03 added                       *
     A*             Core hook MM0028DD04 added                       *
     A*  CAC001 - Profit Centre Accounting Development:               *
     A*           New field Funding Spread/Rate will be included if   *
     A*           Analytical Accounting is installed.                 *
     A*  S01408  -  Core hook MM0028DD01 added                       *
     A*             Core hook MM0028DD02 added                       *
     A*  090199  -  Recompile to include new Multi-Language Suport    *
     A*             messages IDs from 10.6 packaging into core.       *
     A*     051307 - Change Sequence Number message to allow         *
     A*              for the fact that the message will be           *
     A*              displayed during the Deal Amendment Input       *
     A*              rather than after it has been written           *
     A*     S01314  -  Incororation changes                          *
     A*     E23462 - Remove all references to DRS.                   *
     A*     S01184 - CONFIRMATION MATCHING (FEEDBACK STATUS)         *
     A*     S01253 - RECOMPILED FOR MULTILANGUAGE SCREENS            *
     A*     LN0242 - DUPLICATE HELP TAG FOR CCY & CUSTOMER,          *
     A*              NO HELP TAG FOR RATE/SPREAD, DEAL NO, SEQ NO    *
     A*     E20777 - EXTRA HIGHLIGHTS ON INPUT FIELDS.               *
     A*     S01227 - EXTENDED SETTLEMENTS INCORPORATION              *
     A*     E301   - ENSURE HELP TAGS MATCH DL INM
     A*     S01199 - HELP SYSTEM.                                *
     A*
     A*     E18139 - Change the Defer Write creation parameter to    *
     A*              *NO so new screen displayed as soon as possible.*
     A*                                                              *
     A*     S01199 - HELP SYSTEM.                                *
     A*                                                              *
     A*     S01177  -  EXTENDED SETTLEMENT INSTRUCTIONS              *
     A*                                                              *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
      *                                                    /S01177/
     A************                          REF(MFRFL)
     A                                      REF(*LIBL/MFRFL)
      *                                                    /S01177/
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
      *                                                    /S01177/
     A*************                         PRINT(MPRINT)
     A                                      PRINT(*LIBL/MPRINT)
      *                                                    /S01177/
     A                                      INDARA
     A          R MM0028S1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A* **MAIN*DISPLAY*SCREEN***                           /*S01199*/      *
     A*   MESSAGE SUBFILE CONTROL                          /*S01199*/      *
     A* -------------------------------------------------------------------*
     A*
     A          R MM0028S0                  SFLCTL(MM0028S1)
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A*                                                    /*S01199*/
     A**************************************TEXT('MAIN DISPLAY SCREEN')
     A                                      TEXT('MSG SUBFILE CONTRL')
     A*                                                    /*S01199*/
     A N50                                  CF03
     A                                      CF12
     A N45N50                               CF08
     A N45N50                               CF07
      *                                                    /S01177/
     A  76N50                               CF14
      *                                                    /S01177/
     A N45N50                               CF15
     A  42N50                               CF10
     A N47                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  59                                  SFLEND
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A*                                                    /*S01199*/
     A* -------------------------------------------------------------------*
     A*   MAIN DISPLAY SCREEN                                              *
     A* -------------------------------------------------------------------*
     A*
     A          R MM0028D2                  TEXT('MAIN DISPLAY SCREEN')
     A N50                                  CF03
     A*/COPY WNCPYSRC,MM0028DD07
     A                                      CF12
     A N45N50                               CF08
     A N45N50                               CF07
     A  76N50                               CF14
     A N45N50                               CF15
     A  42N50                               CF10
     A*/COPY WNCPYSRC,MM0028DD08
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A*                                                    /*S01199*/
     A*                                                    /*S01408*/
     A*/COPY WNCPYSRC,MM0028DD04
     A*                                                    /*S01408*/
     A                                  1  2'Ref: MM0028'
     A  50                                  DSPATR(PC)
      *                                                  START OF CHANGE  E23462
     A**********************************1 28'MIDAS DEALING ROOM SYSTEMS'
     A                                  1 26'            Midas            '
      *                                                     END OF CHANGE E23462
     A                                      DSPATR(HI)
     A**************************************DSPATR(UL)     /E23462/
     A            DDTIME         6Y 0O  1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  1 72'WID:'
     A            DDWID          3A  O  1 77TEXT('WORKSTATION ID')
     A                                  3  2'Action:'
     A            DDACTN         1A  B  3 10TEXT('ACTION CODE')
     A  01                                  DSPATR(RI)
     A N41N43
     A*/COPY WNCPYSRC,MM0028DD09
     A N50
     AO 01N50                               DSPATR(PC)
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(UL)
     A*/COPY WNCPYSRC,MM0028DD10
      *                                                    /E20777/
     A N01                                  DSPATR(HI)
      *                                                    /E20777/
     A                                      OVRDTA
     A            DDLIST        11A  O  3 12TEXT('VALID OPTIONS')
     A                                      OVRDTA
     A                                  3 24'MM DEALER - DEAL AMENDMENTS INPUT -
     A                                      & AUTHORIZATION'
     A                                  4  2'Deal No:'
     A                                      DSPATR(HI)
     A            DDDLNO    R     A  B  4 11REFFLD(DEAL)
     A N41 43
     AO 02N49                               DSPATR(PC)
     A  02N49                               DSPATR(RI)
      *                                                    /E20777/
     A N02                                  DSPATR(HI)
      *                                                    /E20777/
     A*/COPY WNCPYSRC,MM0028DD11
     A  49
     AO 45
     AO 50                                  DSPATR(PR)
     A N45N50N49                            DSPATR(UL)
     A*/COPY WNCPYSRC,MM0028DD12
     A                                      OVRDTA
     A                                  5  2'Value Date:'
     A            DDVDAT    R     A  B  5 16REFFLD(CHGD)
     A                                      TEXT('VALUE DATE')
     A  11N49                               DSPATR(RI)
     A  11N49                               DSPATR(PC)
     A*/COPY WNCPYSRC,MM0028DD13
     A N50N49N45                            DSPATR(UL)
     A  49
     AO 45
     AO 50                                  DSPATR(PR)
     A*/COPY WNCPYSRC,MM0028DD14
      *                                                    /E20777/
     A N11                                  DSPATR(HI)
      *                                                    /E20777/
     A                                      OVRDTA
     A                                  6  2'Sequence No:'
     A            DDDS38    R     A  B  6 16REFFLD(DS38)
     A                                      TEXT('SEQUENCE NUMBER')
     A  15N49                               DSPATR(RI)
      *                                                    /E20777/
     A N15                                  DSPATR(HI)
      *                                                    /E20777/
     A  15N49                               DSPATR(PC)
     A*/COPY WNCPYSRC,MM0028DD15
     A N50N49N45                            DSPATR(UL)
     A  49
     AO 45
     AO 50                                  DSPATR(PR)
     A*/COPY WNCPYSRC,MM0028DD16
     A                                      OVRDTA
     A                                  7  2'Customer :'
     A            DDCSNM    R        B  7 16REFFLD(CSNM)
     A                                      TEXT('CUSTOMER SHORT NAME')
     A  10N49                               DSPATR(RI)
      *                                                    /E20777/
     A N10                                  DSPATR(HI)
      *                                                    /E20777/
     A*/COPY WNCPYSRC,MM0028DD17
     A  10N49                               DSPATR(PC)
     A*/COPY WNCPYSRC,MM0028DD18
     A N50N49                               DSPATR(UL)
     A  49
     AO 50                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8  2'Type:'
     A            DDMTYP    R        B  8 16REFFLD(DTPE)
     A  03N49                               DSPATR(PC)
     A  03N49                               DSPATR(RI)
      *                                                    /E20777/
     A N03                                  DSPATR(HI)
      *                                                    /E20777/
     A*/COPY WNCPYSRC,MM0028DD19
     A  49
     AO 50                                  DSPATR(PR)
     A N50N49                               DSPATR(UL)
     A*/COPY WNCPYSRC,MM0028DD20
     A                                      OVRDTA
     A                                  9  2'Rate/Spread'
     A            DDINTR        12A  B  9 16TEXT('INTEREST/RATE')
     A  20N49                               DSPATR(RI)
      *                                                    /E20777/
     A N20                                  DSPATR(HI)
      *                                                    /E20777/
     A*/COPY WNCPYSRC,MM0028DD21
     A  20N49                               DSPATR(PC)
     A N50N49                               DSPATR(UL)
     A  49
     AO 50                                  DSPATR(PR)
     A*/COPY WNCPYSRC,MM0028DD22
     A                                      OVRDTA
     A                                 10  2'Currency:'
     A            DDCCY     R        B 10 16REFFLD(CCY)
     A                                      TEXT('CURRENCY')
     A  16N49                               DSPATR(RI)
      *                                                    /E20777/
     A N16                                  DSPATR(HI)
      *                                                    /E20777/
     A  16N49                               DSPATR(PC)
     A*/COPY WNCPYSRC,MM0028DD23
     A N50N49                               DSPATR(UL)
     A  49
     AO 50                                  DSPATR(PR)
     A*/COPY WNCPYSRC,MM0028DD24
     A                                      OVRDTA
     A                                 11  2'Amount:'
     A            DDAMNP    R     A  B 11 16REFFLD(AMNP)
     A                                      TEXT('AMOUNT')
     A  18N48                               DSPATR(RI)
      *                                                    /E20777/
     A N18                                  DSPATR(HI)
      *                                                    /E20777/
     A  18N48                               DSPATR(PC)
     A*/COPY WNCPYSRC,MM0028DD25
     A N50N48                               DSPATR(UL)
     A  48
     AO 50                                  DSPATR(PR)
     A                                      OVRDTA
     A*                                                                   S01408
     A*/COPY WNCPYSRC,MM0028DD03                                          S01408
     A*                                                                   S01408
      *                                              Begin /CAC001/
     A                                 12  2'Funding Spread/Rate'
     A N21                                  DSPATR(ND)
     A                                      OVRATR
     A            DDFSRP    R     A  B 12 22REFFLD(ZZ012)
     A                                      TEXT('FUNDING SPREAD/RATE')
     A  21 23N49                            DSPATR(RI PC)
     A  21N23                               DSPATR(HI)
     A  21N50N49                            DSPATR(UL)
     A  21 50
     AO 21 49
     AON21                                  DSPATR(PR)
     A N21                                  DSPATR(ND)
     A                                      OVRDTA
      *                                              End   /CAC001/
     A*                                                    /S01184/
     A                                 13  2'Confirmation Matching Status'
     A                                      DSPATR(UL)
     A N31
     AO 35                                  DSPATR(ND)
     A                                      OVRATR
     A                                 14  5'Counterparty:'
     A N31
     AO 35                                  DSPATR(ND)
     A                                      OVRATR
     A            DDCPTY        15A    14 21
     A N31
     AO 35                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 15  5'Broker      :'
     A N31
     AO 36
     AO 35                                  DSPATR(ND)
     A                                      OVRATR
     A            DDBROK        15A    15 21
     A N31
     AO 36
     AO 35                                  DSPATR(ND)
     A                                      OVRDTA
     A*/COPY WNCPYSRC,MM0028DD29
     A*                                                    /S01184/
     A*****                            14  2'Settlement Instructions'     S01177
     A*****                                 DSPATR(UL)                    S01177
     A*****                            15  2'Settlement Method:'          S01177
     A*****       DDORCM    R     A  B 15 26REFFLD(MOPM)                  S01177
     A*****                                 TEXT('REC/PAY SETTLEMENT METHOD')
     A* 31                                  DSPATR(RI)                    S01177
     A* 31                                  DSPATR(PC)                    S01177
     A*N50                                  DSPATR(UL)                    S01177
     A* 50                                  DSPATR(PR)                    S01177
     A*****                                 OVRDTA                        S01177
     A*****                            16  2'Our Bank/Account:'           S01177
     A*****       DDMORI    R        B 16 26REFFLD(MORI)                  S01177
     A*****                                 TEXT('OUR REC/PAY BANK A/C')  S01177
     A* 33                                  DSPATR(RI)                    S01177
     A* 33                                  DSPATR(PC)                    S01177
     A*N50                                  DSPATR(UL)                    S01177
     A* 50                                  DSPATR(PR)                    S01177
     A*****                                 OVRDTA                        S01177
     A*****                            17  2'C"Party Bank:'               S01177
     A*****       DDMCPI    R        B 17 26REFFLD(MCPI)                  S01177
     A*****                                 TEXT('PARTY PAY/REC BANK')    S01177
     A* 35                                  DSPATR(RI)                    S01177
     A* 35                                  DSPATR(PC)                    S01177
     A*N50                                  DSPATR(UL)                    S01177
     A* 50                                  DSPATR(PR)                    S01177
     A*****                                 OVRDTA                        S01177
     A*****                            18  2'For A-C of:'                 S01177
     A*****       DDMFAO    R        B 18 26REFFLD(MFAC)                  S01177
     A*****                                 TEXT('FOR A/C OF')            S01177
     A* 37                                  DSPATR(RI)                    S01177
     A* 37                                  DSPATR(PC)                    S01177
     A*N50                                  DSPATR(UL)                    S01177
     A* 50                                  DSPATR(PR)                    S01177
     A*****                                 OVRDTA                        S01177
     A*****                            19  2'Special Instructions:'       S01177
     A*****       DDSPEC    R        B 19 26REFFLD(SPEC)                  S01177
     A*****                                 TEXT('SPECIAL INSTRUCTIONS')  S01177
     A*N50                                  DSPATR(UL)                    S01177
     A* 50                                  DSPATR(PR)                    S01177
     A*****                                 OVRDTA                        S01177
     A*                                                    /051307/
     A***********                      21  2'Sequence No. Allocated to last -
     A***********                           Deal Amendment -'
     A                                 21  2'Sequence No. Allocated to this -
     A                                      Deal Amendment -'
     A*                                                    /051307/
     A N40                                  DSPATR(ND)
     A                                      OVRATR
     A            DDLSQN         3A  O 21 50OVRDTA
     A  40                                  DSPATR(HI)
     A N40                                  DSPATR(ND)
 
 
     A          R MM0028D0
     A                                      TEXT('SCREEN FOOTER')
     A N50                                  CF03
     A*/COPY WNCPYSRC,MM0028DD26
     A                                      CF12
     A N45N50                               CF08
     A N45N50                               CF07
      *                                                    /S01177/
     A  76N50                               CF14
      *                                                    /S01177/
     A N45N50                               CF15
     A*/COPY WNCPYSRC,MM0028DD27
     A  42N50                               CF10
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *                                                    /S01177/
     A*****                            23  2'CF24 - CONFIRM DELETE'
     A**                                             Begin /112563/
     A***********                      23  2'F10 - CONFIRM DELETE '
     A                                 23  2'F10=CONFIRM DELETE'
     A**                                             End   /112563/
      *                                                    /S01177/
     A  42                                  DSPATR(HI)
     A N42                                  DSPATR(ND)
     A                                      OVRATR
     A*                                                                   S01408
     A*/COPY WNCPYSRC,MM0028DD01                                          S01408
     A*                                                                   S01408
     A            DDSTS         24A  O 23 45OVRDTA
     A  38                                  DSPATR(RI)
     A N38                                  DSPATR(ND)
     A                                 23 73'DELETED'
     A  39                                  DSPATR(RI)
     A N39                                  DSPATR(ND)
     A                                      OVRATR
     A*                                                                   S01408
     A*/COPY WNCPYSRC,MM0028DD02                                          S01408
     A*                                                                   S01408
      *                                                    /S01177/
     A*****                            24  1'CF1-End CF2-Previous Screen'
     A**                                             Begin /112563/
     A***********                      24  1'F3-End F12-Prev Scrn'
     A                                 24  2'F3=Exit  F12=Cancel'
     A**                                             End   /112563/
      *                                                    /S01177/
     A  50                                  DSPATR(ND)
     A                                      OVRATR
      *                                                    /S01177/
     A*****                            24 29'F8 & F7-Next & Previous Record'
     A**                                             Begin /112563/
     A***********                      24 22'F8/F7-Next/Prev Record'
     A                                 24 23'F8=Next'
     A  50
     AO 45                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 32'F7=Previous'
     A**                                             End   /112563/
      *                                                    /S01177/
     A  50
     AO 45                                  DSPATR(ND)
     A                                      OVRATR
      *                                                    /S01177/
      *                                                    /S01227/
     A*********************************24 46'F14-Settlements'
     A**                                             Begin /112563/
     A***********                      24 45'F14-Settlements'
     A                                 24 45'F14=Settlements'
     A**                                             End   /112563/
      *                                                    /S01227/
     A  50
     AON76                                  DSPATR(ND)
     A                                      OVRATR
      *                                                    /S01177/
      *                                                    /S01177/
     A*****                            24 62'F15-Select Screen'
      *                                                    /S01227/
     A*********************************24 62'F15-Select Screen'
     A**                                             Begin /112563/
     A***********                      24 61'F15-Select Screen'
     A                                 24 62'F15=Select Screen'
     A**                                             End   /112563/
      *                                                    /S01227/
      *                                                    /S01177/
     A  50
     AO 45                                  DSPATR(ND)
     A                                      OVRATR
     A*/COPY WNCPYSRC,MM0028DD28
 
 
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN                                                    *
     A* -------------------------------------------------------------------*
 
 
      *                                                                   S01408
     A*/COPY WNCPYSRC,MM0028DD05                                          S01408
      *                                                                   S01408
     A          R MM0028S3                  SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDDLNO    R     A  O  8  8REFFLD(DEAL)
     A                                      DSPATR(HI)
     A            DDVDAT    R     A  O  8 15REFFLD(CHGD)
     A                                      TEXT('VALUE DATE')
     A            DDDS38    R     A  O  8 22REFFLD(DS38)
     A            DDMTYP    R        O  8 27REFFLD(DTPE)
     A            DDCCY     R        O  8 31REFFLD(CCY)
     A*                                                    /S01184/
     A************DDAMNP    R     A  O  8 36REFFLD(AMNP)
     A************                          TEXT('AMOUNT')
     A************DDINTR        12A  O  8 56TEXT('RATE/SPREAD')
     A************DDSTAT    R        O  8 72REFFLD(DSTS)
     A************                      8 74'DELETED'
     A*N39******                            DSPATR(ND)
     A            DDAMNP    R     A  O  8 35REFFLD(AMNP)
     A                                      TEXT('AMOUNT')
     A            DDINTR        12A  O  8 52TEXT('RATE/SPREAD')
     A            DDSTAT    R        O  8 67REFFLD(DSTS)
     A            DDTEXT         7A     8 71TEXT('"DELETED" OR CP/BK STATUS')
     A N39N31                               DSPATR(ND)
     A*                                                    /S01184/
     A  39                                  DSPATR(RI)
 
 
 
 
     A          R MM0028S2                  SFLCTL(MM0028S3)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  70                                  SFLEND
     A  46                                  SFLCLR
     A N70                                  ROLLUP(51)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A*                                                    /*S01199*/
      *                                              Begin /S01314/
      *                                              End   /S01314/
     A*                                                    /*S01199*/
     A            DDSFRN         4S 0H      SFLRCDNBR
     A                                  1  2'Ref: MM0028'
      *                                                  START OF CHANGE  E23462
     A**********************************1 28'MIDAS DEALING ROOM SYSTEMS'
     A                                  1 26'            Midas            '
      *                                                     END OF CHANGE E23462
     A                                      DSPATR(HI)
     A**************************************DSPATR(UL)     /E23462/
     A            DDTIME         6Y 0O  1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3A  O  1 77TEXT('WORKSTATION ID')
     A                                  3 18'MM DEALER - DEAL AMENDMENTS INPUT -
     A                                      & AUTHORIZATION'
     A                                  4 38'ENQUIRY'
     A                                  6  2'Select'
     A                                  6  9'Deal'
     A                                  6 16'Value'
     A                                  6 22'Seq'
     A                                  6 26'Deal'
     A                                  6 31'Ccy'
     A*                                                    /S01184/
     A**********************************6 40'Amount'
     A**********************************6 58'Rate/'
     A**********************************6 70'Status'
     A                                  6 39'Amount'
     A                                  6 56'Rate/'
     A                                  6 65'Status'
     A                                  6 72'Match'
     A N31                                  DSPATR(ND)
     A                                      OVRATR
     A*                                                    /S01184/
     A                                  7 10'No'
     A                                  7 16'Date'
     A                                  7 22'No.'
     A                                  7 26'Type'
     A*                                                    /S01184/
     A**********************************7 58'Spread'
     A                                  7 56'Spread'
     A                                  7 72'CP BK'
     A N31                                  DSPATR(ND)
     A                                      OVRATR
     A*                                                    /S01184/
      *                                                                   S01408
     A*/COPY WNCPYSRC,MM0028DD06                                          S01408
      *                                                                   S01408
 
 
 
 
     A          R MM0028D1
     A                                      TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record-
     A                                      s'
     A**                                             Begin /112563/
     A***********                      24  2'F3-End F12-Previous Screen'
      *                                                    /S01177/
     A                                 24  2'F3=Exit  F12=Cancel'
     A**                                             End   /112563/
