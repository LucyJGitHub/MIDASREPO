      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM Unauthorised MM Deals Enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Money Market (Dealing)                           *
     A*                                                               *
     A*  DSPF/MM0070 - Unauthorised Deals Enquiry                     *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. 066872 *CREATE     DATE 26JUL94               *
     A*                 000000             DATE DDMMMYY               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  066872 - Include FX/MM Unauthorised Deals Enquiry.           *
     a*           Display file created.                               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  F U N C T I O N   O F   I N D I C A T O R S                  *
     A*                                                               *
     A*   10  - Controls SFLEND on message sub-file.                  *
     A*                                                               *
     A*   20  - Error on field DDDSTS.                                *
     A*   21  - Error on field DDDUSC.                                *
     A*   22  - Error on field DDDN38.                                *
     A*   29  - Error has occurred in screen validation.              *
     A*                                                               *
     A*   41  - Controls SFLCLR on select screen.                     *
     A*   43  - Subfile being displayed                               *
     A*   46  - Cmd12 narrative displayed.                            *
     A*                                                               *
     A*   50  - ROLLUP key pressed and more forward records in        *
     A*         subfile                                               *
     A*   51  - ROLLDOWN key pressed and more previous records in     *
     A*         subfile.                                              *
     A*                                                               *
     A*   70  - Controls SFLEND on main sub-file.                     *
     A*   71  - ROLLUP key control.                                   *
     A*   73  - ROLLDOWN key control.                                 *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      MSGLOC(23)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CA03
     A                                      CA12
     A*
     A*
     A          R MM0070S1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(23)
     A            DDMGKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A          R MM0070S0                  SFLCTL(MM0070S1)
     A*            09:13:48    FOADT       REL-R07M00  5714-UT1
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A                                      TEXT('ENQUIRY SCREEN')
     A N42                                  SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A                                      BLINK
     A                                      OVERLAY
     A            DDPGMQ                    SFLPGMQ
     A                                 24  4'F3 for End of Job'
     A                                 24 29'F12 for Previous Screen'
     A          R MM0070S3                  TEXT('Enquiry Subfile')
     A                                      SFL
     A            DDDNO2    R        O  7  2REFFLD(DN38)
     A            DDVDAT         7   O  7 10
     A            DDDS38    R        O  7 18REFFLD(DS38)
     A                                      EDTWRD('   ')
     A            DDAMNT        18   O  7 22
     A            DDINTR        12   O  7 41
     A            DDCCY     R        O  7 54REFFLD(CCY)
     A            DDMDDT         7   O  7 58
     A            DDTYPE    R        O  7 66REFFLD(DTPE)
     A            DDSTAT         2   O  7 71
     A            DDDLR2    R        O  7 75REFFLD(DUSC)
     A*
     A          R MM0070S2                  SFLCTL(MM0070S3)
     A*            09:13:48    FOADT       REL-R07M00  5714-UT1
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      TEXT('Enquiry Screen')
     A N41 43                               SFLDSP
     A N41                                  SFLDSPCTL
     A  41                                  SFLCLR
     A  70                                  SFLEND
     A N71                                  ROLLUP(50)
     A N73                                  ROLLDOWN(51)
     A                                      BLINK
     A            @RRN           3S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'Ref: MM0070'
     A                                  1 19'MM DEALER - UNAUTHORISED DEALS/DEA-
     A                                      MS'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDDATE         7A  O  1 61
     A            DDTIME         4Y 0O  1 70EDTWRD('0 :  ')
     A            DDWID          3A  O  1 77
     A                                  3  3'Incomplete/Complete:'
     A                                      DSPATR(HI)
     A            DDDSTS    R        B  3 26REFFLD(DSTS)
     A                                      TEXT('Incomplete/Complete')
     A                                      DSPATR(HI)
     A N29
     AO 20                                  DSPATR(PC)
     A  20                                  DSPATR(RI)
     A N20                                  DSPATR(UL)
     A                                  3 33'Dealer:'
     A                                      DSPATR(HI)
     A            DDDUSC    R        B  3 44REFFLD(DUSC)
     A                                      DSPATR(HI)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A N21                                  DSPATR(UL)
     A                                  3 52'Deal Number:'
     A                                      DSPATR(HI)
     A            DDDN38    R        B  3 68REFFLD(DN38)
     A                                      EDTCDE(Z)
     A                                      DSPATR(HI)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A N22                                  DSPATR(UL)
     A                                  5  2'Deal No. Value  Seq  Deal amount/ -
     A                                            Interest    Ccy  Mat   Deal S-
     A                                      tat Dlr  '
     A                                  6 11'Date   no      price          rate-
     A                                      /spread       date  type Inds'
