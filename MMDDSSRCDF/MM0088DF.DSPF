      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM BOI MM Loans/Deposits')                       *
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  MM0088DF - Loans/Deposits Extension Details Display          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2006            *
      *                                                               *
      *  Last Amend No. LUC139             Date 15Jan21               *
      *  Prev Amend No. CER009  *CREATE    Date 16Aug06               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  LUC139 - UCI Italian returns upgrade to FM2.1                *
      *  CER009 - Puma II Upgrade to MidasPlus                        *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
      *
     A          R MM0088F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2006')
     A                                      CF03
     A                                      CF12
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            ##USR         10      1  2
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A                                  1 66'DATE'
     A            ##MRDT         7      1 72TEXT('midas rundate')
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                  2 28'European Reporting - Italy'
     A                                      DSPATR(HI)
     A                                  2 66'TIME'
     A            #0TME          6  0   2 71
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  2'Motivation of Operation :'
     A            #0MOOP         1A  O  4 28DSPATR(HI)
     A                                  4 40'Agricultural Cred. Class:'
     A            #0AGCT         3A  O  4 66DSPATR(HI)
     A                                  5  2'Agricultural Laws Class :'
     A            #0AGCL         3A  O  5 28DSPATR(HI)
     A                                  5 40'Econ. Branch of Dest. . :'
     A            #0ECBD         2A  O  5 66DSPATR(HI)
     A                                  6  2'Subsidized Credit Ind . :'
     A            #0SBCR         1A  O  6 28DSPATR(HI)
     A                                  6 40'Law for Sub. Cred. Code :'
     A            #0SBLW         3A  O  6 66DSPATR(HI)
     A                                  7  2'Subsidized Int. For Expt:'
     A            #0INSB         1A  O  7 28DSPATR(HI)
     A                                  7 40'Type of Product . . . . :'
     A            #0TPPR         6A  O  7 66DSPATR(HI)
     A                                  8  2'Personalisation of Oper.:'
     A            #0PEPR         6A  O  8 28DSPATR(HI)
     A                                  8 40'Global/Partial Operation:'
     A            #0GLPA         1A  O  8 66DSPATR(HI)
     A                                  9  2'Priority Risk Code Prod.:'
     A            #0PTRP         3A  O  9 28DSPATR(HI)
     A                                  9 40'Operation with 3rd Party:'
     A            #0TPFD         1A  O  9 66DSPATR(HI)
     A                                 10  2'Dest. Prov. of Invest . :'
     A            #0DPIV         5A  O 10 28DSPATR(HI)
     A                                 10 40'Econ. Dest. of Invest . :'
     A            #0EDIV         3A  O 10 66DSPATR(HI)
     A                                 11  2'Category Code CR  . . . :'
     A            #0CATC        11A  O 11 28DSPATR(HI)
     A                                 11 40'Type of Activity  . . . :'
     A            #0TPAC         3A  O 11 66DSPATR(HI)
     A                                 12  2'Client Role CR  . . . . :'
     A            #0CLRL         3A  O 12 28DSPATR(HI)
     A                                 12 40'Account Status CR . . . :'
     A            #0ACST         3A  O 12 66DSPATR(HI)
     A                                 13 40'Effective Global Rate . :'
     A            #0TEGL         9A  O 13 66DSPATR(HI)
      *---------------------------------------------------------------*
      ** Main Screen Footer                                           *
      *---------------------------------------------------------------*
     A                                 22  2'F3=Exit   F12=Previous'
     A                                      COLOR(BLU)
      *
     A          R MM0088F1
     A                                      CF03
     A                                      CF12
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            ##USR         10      1  2
     A            ##PGM         10      1 13TEXT('*PROGRAM')
     A                                  1 66'DATE'
     A            ##MRDT         7      1 72TEXT('midas rundate')
     A            ##JOB         10      2  2TEXT('*JOB')
     A                                  2 28'European Reporting - Italy'
     A                                      DSPATR(HI)
     A                                  2 66'TIME'
     A            #0TME          6  0   2 71
     A                                      EDTWRD('  :  :  ')
      *
     A                                  4  2'Motivation of Operation :'
     A            #0MOOP         1A  O  4 28DSPATR(HI)
     A                                  5  2'Type of Product . . . . :'
     A            #0TPPR         6A  O  5 28DSPATR(HI)
     A                                  6  2'Personalisation of Oper.:'
     A            #0PEPR         6A  O  6 28DSPATR(HI)
     A                                  7  2'Global/Partial Operation:'
     A            #0GLPA         1A  O  7 28DSPATR(HI)
     A                                  8  2'Operation with 3rd Party:'
     A            #0TPFD         1A  O  8 28DSPATR(HI)
      *---------------------------------------------------------------*
      ** Main Screen Footer                                           *
      *---------------------------------------------------------------*
     A                                 22  2'F3=Exit   F12=Previous'
     A                                      COLOR(BLU)
      *---------------------------------------------------------------*
      ** Message Subfile Format                                       *
      *---------------------------------------------------------------*
     A          R #MSGRCD                   SFL
     A                                      SFLMSGRCD(16)
     A                                      TEXT('Program messages')
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
      *---------------------------------------------------------------*
      ** Message Subfile Control                                      *
      *---------------------------------------------------------------*
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0001)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ(10)
