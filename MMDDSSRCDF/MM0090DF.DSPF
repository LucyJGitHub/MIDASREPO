      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM Floating Rate CD Issued Input')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  DSPF/MM0090DF - Floating Rate Screen                         *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL033  *CREATE    Date 10Feb05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL033 - Floating Rate CDs Issued                            *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CA12(12 'Previous screen.')
     A N55
     AAN56                                  CF05(05 'Reset.')
     A  55                                  CF10(10 'Delete.')
      *................................................................
     A          R MM0090F0
     A                                      TEXT('(c) Finastra International +
     A                                            Limited +
     A                                            2005')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      KEEP
     A            ##PGM         10A  H
     A            DDDEL1         6A  H
     A            DDLCD          5Y 0H
     A            DDCHTP         1A  H
     A            DDUSER    R        O  1  2REFFLD(USER)
     A                                  1 13'MM0090'
     A                                  1 32'Deals File Enquiry'
     A                                      DSPATR(HI)
     A                                  1 67'Date'
     A            DDRUNA    R        O  1 73REFFLD(RUNA)
     A            DDWSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'Time'
     A                                  2 73TIME
     A                                  4  2'Deal no.:'
     A            DDDEAL         6A  O  4 12
     A                                      DSPATR(HI)
     A                                  4 20'Type:'
     A            DDTYPE        30A  O  4 26
     A                                      DSPATR(HI)
     A                                  6  2'Interest Rate'
     A                                  7  4'Rate Change Date:'
     A            DDRCDT         6A  O  7 20
     A                                      DSPATR(HI)
     A                                  7 29'Freq:'
     A            DDRCFR         1A  O  7 35
     A                                      DSPATR(HI)
     A                                  7 45'Day:'
     A            DDRCDM         2A  O  7 50
     A                                      DSPATR(HI)
     A                                  8  4'Short Term Rate:'
     A            DDSTRT        12A  O  8 20
     A                                      DSPATR(HI)
     A                                  9  4'Rate Fix Days:'
     A            DDFXDM         2A  O  9 20
     A                                      DSPATR(HI)
     A                                  9 29'Forward Yield Curve:'
     A            DDFYLD         5A  O  9 50
     A                                      DSPATR(HI)
     A                                 23  2'F3=Exit   F12=Previous'
      *================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
