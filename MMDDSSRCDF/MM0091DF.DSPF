      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM Date Schedule Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Dealing Module                          *
      *                                                               *
      *  DSPF/MM0091DF - Floating Rate CD Issued Date Schedule Screen *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL033  *CREATE    Date 10Feb05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL033 - Floating Rate CDs Issued                            *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      CF03
     A                                      CF12
     A  25                                  CF14
     A N50                                  CF10
     A N50                                  CF16
      *
     A          R MM0091S0                  TEXT('(c) Misys International +
     A                                            Banking Systems Ltd. +
     A                                            2005')
     A                                      SFL
     A            DDACN1    R        I 10  4REFFLD(IND)
     A                                      DSPATR(HI)
     A  10                                  DSPATR(PR ND)
     A N10                                  DSPATR(UL)
     A                                      AUTO(RA)
     A            DDPRDT    R        O 10 10REFFLD(ZZ006)
     A                                      DSPATR(HI)
     A  15                                  DSPATR(RI)
     A            DDDYWK    R        O 10 17REFFLD(ZZ003)
     A                                      DSPATR(HI)
     A            DDCOH1    R        O 10 22REFFLD(IND)
     A                                      DSPATR(HI)
     A            DDDTPR    R        O 10 31REFFLD(ZZ003)
     A                                      DSPATR(HI)
      *
     A          R MM0091C0                  SFLCTL(MM0091S0)
     A                                      OVERLAY
     A                                      BLINK
     A                                      SFLSIZ(0999)
     A                                      SFLPAG(0011)
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      ROLLUP(05)
     A                                      ROLLDOWN(06)
     A            DDUSER    R        O  1  2REFFLD(USER)
     A                                  1 13'MM0091'
     A            DDTITL    R        O  1 25REFFLD(NARR)
     A                                      DSPATR(HI)
     A                                  1 67'Date'
     A            DDRUNA    R        O  1 73REFFLD(RUNA)
     A            DDWSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'Time'
     A                                  2 73TIME
      *
     A                                  4  2'Deal Number:'
     A            DDDLNO    R        O  4 15REFFLD(DLNO)
     A                                      DSPATR(HI)
     A                                  4 65'Value:'
     A            DDCVDT    R        O  4 75REFFLD(DTSA)
     A                                      DSPATR(HI)
     A                                  5 65'Maturity:'
     A            DDCMDT    R        O  5 75REFFLD(DTSA)
     A                                      DSPATR(HI)
      *
     A                                  7  2'Action'
     A  50                                  DSPATR(ND)
     A                                  7 11'Date'
     A                                  6 18'Confirmed'
     A                                  7 18' Holiday'
     A                                  7 29'Processed'
     A                                  9 62'Rat Pay '
     A  50                                  DSPATR(ND)
     A            DDACTN    R        B  8  4REFFLD(IND)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PR ND)
     A N50N11                               DSPATR(UL)
     A N50 11                               DSPATR(RI PC)
     A            DDSELD    R        B  8 10REFFLD(ZZ007)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PR ND)
     A N50N12                               DSPATR(UL)
     A N50 12                               DSPATR(RI PC)
     A            DDCOHO    R        B  8 22REFFLD(IND)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PR ND)
     A N50N13                               DSPATR(UL)
     A N50 13                               DSPATR(RI PC)
     A                                  8 47'Copy Schedule:'
     A  50                                  DSPATR(ND)
     A            DDCSRT    R        B  8 63REFFLD(IND)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PR ND)
     A N50N14                               DSPATR(UL)
     A N50 14                               DSPATR(RI PC)
     A            DDCSPY    R        B  8 67REFFLD(IND)
     A                                      DSPATR(HI)
     A  50                                  DSPATR(PR ND)
     A N50N14                               DSPATR(UL)
     A N50 14                               DSPATR(RI PC)
      *
     A          R MM0091F0
      *
     A                                 22  2'F3=Exit'
     A  25                             22 11'F12=Previous screen'
     A                                 22 11'F10=Confirm Single Delete  +
     A                                       F12=Previous screen'
     A  50                                  DSPATR(ND)
     A                                 23  2'F16=Delete ALL'
     A  50                                  DSPATR(ND)
      *
     A          R #MSGRCD                   TEXT('Message Subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDMGKY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Message SFLCTL')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A                                      OVERLAY
     A                                      PUTOVR
     A  29
     AON29                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
