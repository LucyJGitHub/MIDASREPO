      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM Penalty Termination of TD Deals')
      *****************************************************************
      *                                                               *
      *  MIDAS¦DBA - Dealing Money Market Module                      *
      *                                                               *
      *  DSPF/MM5290DF - Penalty Termination Maintenance Display      *
      *                                                               *
      *  (C) Copyright Finastra International Limited 2008            *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG23264           Date 25Sep08               *
      *                 256795  *CREATE    Date 25Sep08               *
      *                 xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG23264 - Problem with non-core multilanguage files         *
      *             (Recompile)                                       *
      *  256795 - Penalty Termination of Time Deposits (CDL052).      *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      CA03
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
      *
      *==========================================================================
      *
     A          R MM5290H1                  TEXT('(c) Finastra International -
     A                                      NG SYSTEMS LTD. 2008')
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A            SPGM      R        O  1 13REFFLD(JOPGM)
     A            SRUNA     R        O  1 72REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 23'PENALTY TERMINATION OF TIME +
     A                                       DEPOSITS'
     A                                      DSPATR(UL HI)
     A                                  2 72TIME
      *
      *================================================================
      *
     A          R MM5290F1                  TEXT('Initial Screen')
     A                                      OVERLAY
     A                                      PUTOVR
     A N23                                  CA05
     A  26                                  CA10
     A  22                                  CA12
     A  15                                  CF15
     A                                      ERASE(MM5290C1)
     A                                      ERASE(MM5290S1)
      *
     A                                  4  3'Action:'
     A            SDACD          1A  B  4 11DSPATR(HI)
     A  22N36
     AO 20 22 36
     AO 22 29 36
     AO 22N27                               DSPATR(PR)
     A N41N22N36
     AON20 22 27
     AAN29 36N41                            DSPATR(UL)
     A N20 22 27
     A N29 36
     AO 41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A                                  4 16'Deal Number:'
     A            SDNUM          6A  B  4 29DSPATR(HI)
     A  22                                  DSPATR(PR)
     A N42N22                               DSPATR(UL)
     A  42                                  DSPATR(RI PC)
     A                                      OVRDTA
      *
     A                                  4 39'Customer:'
     A            SCNUM         10A  B  4 49DSPATR(HI)
     A  22                                  DSPATR(PR)
     A N43N22                               DSPATR(UL)
     A  43                                  DSPATR(RI PC)
     A                                      OVRDTA
      *
     A            SSTAT         10A  O  4 66
     A N24 22                               DSPATR(RI)
     A                                      OVRDTA
     A  22 24
     AON22                                  DSPATR(ND)
     A                                      OVRATR
      *
      *================================================================
      *
     A          R MM5290F2                  TEXT('Detail Screen')
     A  23N40                               CSRLOC(SLINE SPOSN)
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            SLINE          3  0H
     A            SPOSN          3  0H
      *
     A                                  6  4'Sub-Type:'
     A            SDSTP          2A  O  6 14
      *
     A                                  6 19'Book:'
     A            SBOOK          2A  O  6 25
      *
     A                                  6 30'Booking Branch:'
     A            SBRCH          3A  O  6 46
      *
     A                                  6 52'Originating Branch:'
     A            SORBR          3A  O  6 72
      *
     A                                  8  4'Deal Date:'
     A            SDDAT          6A  O  8 15
      *
     A                                  8 28'Value Date:'
     A            SVDAT          6A  O  8 40
      *
     A                                  8 54'Maturity Date:'
     A            SMDAT          6A  O  8 69DSPATR(HI)
      *
     A                                  9 54'Previous Date:'
     A            SPMDT          6A  O  9 69
      *
     A                                 10  4'Currency:'
     A            SCCY           3A  O 10 14
      *
     A                                 10 20'Amount:'
     A            SPAMT         15A  O 10 28
      *
     A                                 12  4'Interest Method:'
     A            SINTM          1A  O 12 21
      *
     A                                 12 25'First & Last Day Interest:'
     A            SFLDI          1A  O 12 52
      *
     A                                 12 56'Round Down Interest:'
     A            SRDIN          1A  O 12 77
      *
     A                                 14  4'Base Rate:'
     A            SBSRT          2A  O 14 15
      *
     A                                 14 20'Spread/Rate:'
     A            SSPRT         12A  O 14 33
      *
     A                                 14 48'Total Interest:'
     A            STOTI         15A  O 14 64
      *
     A                                 16 19'Penalty Rate:'
     A            SPRAT         12A  B 16 33DSPATR(HI)
     A  23                                  DSPATR(PR)
     A N51N23                               DSPATR(UL)
     A  51                                  DSPATR(RI PC)
     A                                      OVRDTA
      *
     A                                 16 48'Total Interest:'
     A            SPTOT         15A  O 16 64OVRDTA
      *
     A                                 18 48'Adjustment:'
     A            SADJ          15A  O 18 64OVRDTA
      *
     A N24                             20  4'Inserted/Amended by:'
     A N24        SIUSR         10A  O 20 26
      *
      *
     A N24                             21  4'Authorised by:'
     A N24        SAUSR         10A  O 21 26
     A N24        SANAR         24A  O 21 54
      *
      *================================================================
      *
     A          R MM5290S1                  SFL
      *
     A            S1SEL          1A  B  8  4VALUES('1')
     A                                      CHECK(AB)
     A                                      DSPATR(HI UL)
     A  35                                  DSPATR(PC)
      *
     A            S1DLNO         6A  O  8  8DSPATR(HI)
     A            S1CUST         6A  O  8 16
     A            S1CCY          3A  O  8 24
     A            S1AMT         14A  O  8 30
     A            S1VALD         6A  O  8 49
     A            S1MATD         6A  O  8 57
     A            S1DSTS        10A  O  8 68DSPATR(RI)
      *
      *================================================================
      *
     A          R MM5290C1                  TEXT('Subfile control')
     A                                      SFLCTL(MM5290S1)
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY PUTOVR
     A  36                                  SFLDSPCTL
     A  37                                  SFLDSP
     A N36N37                               SFLCLR
     A  38                                  SFLEND
     A N38                                  ROLLUP(39)
     A  37                                  CA05
     A                                      CA12
     A                                      ERASE(MM5290F1)
      *
     A            C1SFRC         4  0H      SFLRCDNBR
      *
     A                                  5  2'Select'
     A                                  5  9'Deal'
     A                                  5 17'Cust'
     A                                  5 24'Ccy'
     A                                  5 35'Amount'
     A                                  5 49'Value'
     A                                  5 56'Maturity'
     A                                  5 70'Status'
      *
     A                                  6 10'No.'
     A                                  6 16'Number'
     A                                  6 50'Date'
     A                                  6 58'Date'
      *
      *================================================================
      *
     A          R CMDTXTF1                  TEXT('Command keys')
     A                                      OVERLAY
     A                                      ERASE(MM5290F2)
     A N20                             23  2'I=Insert  A=Amend  D=Delete  E=E-
     A                                      nquire  X=Authorise  F3=Exit  F15=S-
     A                                      elect'
     A  20                             23  2'E=Enquire  F3=Main Menu  F15=Sel-
     A                                      ct'
      *
      *================================================================
      *
     A          R CMDTXTF2                  TEXT('Command keys')
     A                                      OVERLAY
     A N23 24
     AON23 25                          23  2'F3=Main Menu  F5=Refresh  F12=Prev-
     A                                      ious Screen'
     A  23 24                          23  2'Enter=Insert  F3=Main Menu  F12=Co-
     A                                      ntinue'
     A  23 25                          23  2'Enter=Amend  F3=Main Menu  F12=Con-
     A                                      tinue'
     A  26                             23  2'F3=Main Menu  F10=Delete  F12=Prev-
     A                                      ious Screen'
     A  27                             23  2'F3=Main Menu  F12=Previous Screen'
     A  28                             23  2'Enter=Authorise  F3=Main Menu -
     A                                      F12=Previous Screen'
      *
      *================================================================
      *
     A          R CMDTXTS1                  TEXT('Command keys')
     A                                      OVERLAY
     A N37                             23  2'F3=Exit  F12=Previous'
     A  37                             23  2'1=Select  F3=Exit  F5=Refresh  F12-
     A                                      =Previous'
      *
      *================================================================
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *================================================================
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
