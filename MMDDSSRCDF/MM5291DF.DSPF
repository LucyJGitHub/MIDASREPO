      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM Penalty Termination Report - Prompt')
      *****************************************************************
      *                                                               *
      *  MIDAS¦DBA - Dealing Money Market Module                      *
      *                                                               *
      *  DSPF/MM5291DF - Penalty Termination I/C Reporting - Prompt   *
      *                                                               *
      *  (C) Copyright MISYS INTERNATIONAL BANKING SYSTEMS LTD 2008   *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. 256795  *Create    Date 25Sep08               *
      *  Prev Amend No. xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  256795 - Penalty Termination of Time Deposits (CDL052)       *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      CA03
     A                                      CA12
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
      *
      *==========================================================================
      *
     A          R MM5291H1                  TEXT('(C) MISYS INTERNATIONAL BANKI-
     A                                      NG SYSTEMST LTD. 2008')
     A                                      OVERLAY
      *
     A            H1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'MM5291P'
     A            H1RUNA    R        O  1 72REFFLD(RUNA)
      *
     A            H1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 17'PENALTY TERMINATION OF TD +
     A                                       DEALS REPORTS - PROMPT'
     A                                      DSPATR(UL HI)
     A                                  2 72TIME
      *
      *================================================================
      *
     A          R MM5291D1
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  8 29'SELECT OPTION - Enter Y'
     A                                      DSPATR(HI)
      *
     A                                 12 29'All Records:'
                                            DSPATR(HI)
     A            F1ALLR         1A  B 12 53
     A                                      DSPATR(UL)
     A  31                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 15 29'Todays Records:'
                                            DSPATR(HI)
     A            F1TDYR         1A  B 15 53
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
      *================================================================
      *
     A          R CMDTXTF1                  TEXT('Command keys')
     A                                      OVERLAY
     A                                 23  2'F3=Main Menu  F12=Previous Screen'
      *
      *================================================================
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      *================================================================
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(04)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
      *
