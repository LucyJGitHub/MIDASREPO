/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas MM Deal Amendments Browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MM Dealer Module                                     *
     A*                                                               *
     A*  MMDEAMBDF - DEAL AMENDMENTS BROWSE                           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.03 ----------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Last Amend No. CAP004             Date 26Aug98               *
     A*  Prev Amend No. CAP002  *CREATE    Date 01Nov97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CAP004 - API's phase 3.                                      *
     A*           Allow browse by Front Office Id.                    *
      *  CAP002 - Conversion of Midas inputs to modular API structure *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R MMDEAMS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDDEAL    R     A     8  8REFFLD(DEAL)
     A                                      DSPATR(HI)
     A            DDVALD    R     A     8 15TEXT('VALUE DATE')
     A                                      REFFLD(CHGD)
     A            DDSEQ     R     A     8 22REFFLD(DS38)
     A            DDTYPE    R           8 27REFFLD(DTPE)
     A            DDCCY     R           8 31REFFLD(CCY)
     A            DDSAMN        17A     8 35TEXT('AMOUNT')
     A            DDINTR        12A     8 53TEXT('RATE/SPREAD')
     A            DDDSTS    R           8 67REFFLD(DSTS)
     A            DDTEXT         7A     8 71TEXT('"DELETED" OR CP/BK STATUS')
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R MMDEAMS0                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
      * Start CAP004
     A                                      CF11
      * End CAP004
     A                                      SFLCTL(MMDEAMS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'MMDEAMB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 18'MM DEALER - DEAL AMENDMENTS INPUT +
     A                                      & AUTHORISATION'
     A                                  4 38'ENQUIRY'
     A*/COPY WNCPYSRC,MMDEAMBD01
     A                                  6  2'Select'
     A                                  6  9'Deal'
     A                                  6 16'Value'
     A                                  6 22'Seq'
     A                                  6 26'Deal'
     A                                  6 31'Ccy'
     A                                  6 39'Amount'
     A                                  6 56'Rate/'
     A                                  6 65'Status'
     A                                  7 10'No'
     A                                  7 16'Date'
     A                                  7 22'No.'
     A                                  7 26'Type'
     A                                  7 56'Spread'
     A*/COPY WNCPYSRC,MMDEAMBD02
      *
      * Start CAP004
     A          R MMDEAMS3                  TEXT('SELECT SCREEN SUBFILE')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDFODN        20A     8  7TEXT('FRONT OFFICE ID.')
     A                                      DSPATR(HI)
     A            DDDEAL    R     A     8 29REFFLD(DEAL)
     A            DDVALD    R     A     8 36TEXT('VALUE DATE')
     A                                      REFFLD(CHGD)
     A            DDSEQ     R     A     8 43REFFLD(DS38)
     A            DDTYPE    R           8 48REFFLD(DTPE)
     A            DDCCY     R           8 52REFFLD(CCY)
     A            DDSAMN        17A     8 56TEXT('AMOUNT')
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R MMDEAMS2                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLCTL(MMDEAMS3)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'MMDEAMB2'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 18'MM DEALER - DEAL AMENDMENTS INPUT +
     A                                      & AUTHORISATION'
     A                                  4 38'ENQUIRY'
     A*/COPY WNCPYSRC,MMDEAMBD03
     A                                  6  2'Select'
     A                                  6  9'Front Office'
     A                                  6 30'Deal'
     A                                  6 37'Value'
     A                                  6 43'Seq'
     A                                  6 47'Deal'
     A                                  6 52'Ccy'
     A                                  6 60'Amount'
     A                                  7  9'Identifier'
     A                                  7 31'No'
     A                                  7 37'Date'
     A                                  7 43'No.'
     A                                  7 47'Type'
     A*/COPY WNCPYSRC,MMDEAMBD04
      * End CAP004
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R MMDEAMF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
      * Start CAP004
     A**********                       24  2'F3=Exit F12=Cancel'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
      * End CAP004
