/*STD *  DSPFBASE                                                    *
/*EXI *  TEXT('Midas MM Deal Amendments Display')                     *
     A*****************************************************************
     A*                                                               *
     A*  Midas - MM Dealer Module                                     *
     A*                                                               *
     A*  MMDEAMDDF  - DEAL AMENDMENTS DISPLAY                         *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL022             Date 03Feb04               *
      *                 CDL020             Date 03Feb04               *
     A*                 CDL017             Date 03Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CAP003             Date 07AUG98               *
     A*                 CAP002  *CREATE    Date 01Nov97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *  CDL022 - Deal Amendment Changes                              *
      *  CDL020 - Apply Base Rate at Value Date                       *
      *  CDL017 - Penalty Fee & Early Maturity of Fixed Term Loan/Dep *
      *  CAP003 - Inclusion of instruction manual help tags.          *
      *  CAP002 - Conversion of Midas inputs to modular API structure *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      *                                              Start /CAP003/
      *                                              End   /CAP003/
     A*
     A* -------------------------------------------------------------------*
     A* * MESSAGE SUBFILE FORMAT                                           *
     A* -------------------------------------------------------------------*
     A*
     A          R MMDEAMS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A*   MESSAGE SUBFILE CONTROL                                          *
     A* -------------------------------------------------------------------*
     A*
     A          R MMDEAMS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A                                      SFLCTL(MMDEAMS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A*   MAIN DISPLAY SCREEN                                              *
     A* -------------------------------------------------------------------*
     A*
     A          R MMDEAMD1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *                                              Start /CAP003/
      *                                              End   /CAP003/
     A                                  1  2'MMDEAMD'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3  2'Action:'
     A            DDACTN         1   B  3 10TEXT('ACTION CODE')
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  68
     AO 10N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDLIST        11      3 12TEXT('VALID OPTIONS')
     A                                      OVRDTA
     A                                  3 24'MM DEALER - DEAL AMENDMENTS INPUT -
     A                                      & AUTHORIZATION'
     A                                  4  2'Deal No:'
     A                                      DSPATR(HI)
     A            DDDLNO    R     A  B  4 11
     A                                      REFFLD(DEAL)
     A  11                                  DSPATR(RI)
     A N11                                  DSPATR(HI)
     A  11N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  5  2'Value Date:'
     A            DDVDAT    R     A  B  5 16
     A                                      REFFLD(CHGD)
     A                                      TEXT('VALUE DATE')
     A  12                                  DSPATR(RI)
     A N12                                  DSPATR(HI)
     A  12N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  6  2'Sequence No:'
     A            DDDS38    R     A  B  6 16REFFLD(DS38)
     A                                      TEXT('SEQUENCE NUMBER')
     A  13                                  DSPATR(RI)
     A N13                                  DSPATR(HI)
     A  13N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  7  2'Customer :'
     A            DDCSNM    R        B  7 16REFFLD(CSNM)
     A                                      TEXT('CUSTOMER SHORT NAME')
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8  2'Type:'
     A            DDMTYP    R        B  8 16REFFLD(DTPE)
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9  2'Rate/Spread'
     A            DDINTR        12A  B  9 16TEXT('INTEREST/RATE')
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 10  2'Currency:'
     A            DDCCY     R        B 10 16REFFLD(CCY)
     A                                      TEXT('CURRENCY')
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      * Start of CDL017
     A************                     11  2'Amount:'
     A                                 11  2'Amount/Penalty Fee:'
     A************DDAMNP    R     A  B 11 16REFFLD(AMNP)
     A            DDAMNP    R     A  B 11 23REFFLD(AMNP)
      * End of CDL017
     A                                      TEXT('AMOUNT')
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 12  2'Funding Spread/Rate:'
     A  87                                  DSPATR(ND)
     A                                      OVRATR
     A            DDFSRP    R     A  B 12 23REFFLD(ZZ012)
     A                                      TEXT('FUNDING SPREAD/RATE')
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A  70
     AO 87                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  87                                  DSPATR(ND)
     A                                      OVRDTA
      * Start of CDL017
     A                                 13  2'Penalty Pay/Receive:'
     A                                      OVRATR
     A            DDPENP         1A  B 13 23TEXT('PENALTY PAY/RECEIVE')
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 14  2'Early Maturity Date:'
     A                                      OVRATR
     A            DDEMDT         6A  B 14 23
     A                                      TEXT('EARLY MATURITY DATE')
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      * End of CDL017
      * Start of CDL022
     A                                 15  2'Base Rate Code:'
     A                                      OVRATR
     A            DDBSRT         2A  B 15 24
     A                                      TEXT('BASE RATE CODE')
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      * End of CDL022
      * Start of CDL020
     A                                 16  2'Actual Interest Rate:'
     A N88                                  DSPATR(ND)
     A                                      OVRATR
     A            DDAIR         12A  B 16 24
     A                                      TEXT('ACTUAL INTEREST RATE')
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     A  70
     AON88                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N88                                  DSPATR(ND)
     A                                      OVRDTA
      * End of CDL020
      * Start of change CER043
     A                                  7 29'Cust.Rep.Twn  :'
     A  80                                  DSPATR(ND)
     A            DDCRTN        10A  O  7 45
     A                                      TEXT('CUSTOMER REPORT TOWN')
     A N81                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  80                                  DSPATR(ND)
     A                                      OVRDTA
     A                                  8 29'Cust.Rep. Nm  :'
     A  80                                  DSPATR(ND)
     A            DDCRNM        20A  O  8 45
     A                                      TEXT('CUSTOMER REPORT NAME')
     A N81                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  80                                  DSPATR(ND)
     A                                      OVRDTA
      * End of change CER043
     A*
     A* -------------------------------------------------------------------*
     A* * MAIN SCREEN FOOTER                                               *
     A* -------------------------------------------------------------------*
     A*
     A          R MMDEAMF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A*
     A            DDFOTX        49A    23  2OVRDTA
     A                                      DSPATR(HI)
     A            DDSTS         24     23 55OVRDTA
     A  79                                  DSPATR(RI)
     A N79                                  DSPATR(ND)
     A                                      OVRATR
     A*
     A                                 24  2'F3=Exit F12=Cancel'
     A                                 24 22'F8=Next'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 31'F7=Prev'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 40'F14=Settlements'
     A N74                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 57'F15=Select'
     A N75                                  DSPATR(ND)
     A                                      OVRATR
