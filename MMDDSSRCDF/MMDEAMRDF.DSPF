/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas MM Invalid Deal Amendments Browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Money Market Module                                  *
     A*                                                               *
     A*  MMDEAMRDF - INVALID DEAL AMENDMENTS BROWSE                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
     A*  Last Amend No. CAP014             Date 08DEC99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. CAP006             Date 08Mar99               *
     A*                 CAP004             Date 01Sep98               *
     A*                 CAP002  *CREATE    Date 01Nov97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CAP014 - Repair function enhancements                        *
     A*  CAP006 - Show error message on screen                        *
     A*  CAP004 - API's phase 3.                                      *
     A*           Enable delete of invalid transactions               *
     A*  CAP002 - Conversion of Midas inputs to modular API structure *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R MMIDEAMS1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1   B  8  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDACTN    R           8  6REFFLD(ACTO)
     A                                      DSPATR(HI)
     A            DDFOTRANIDR     A     8  9REFFLD(FOTRNID)
     A                                      DSPATR(HI)
     A            DDMTYP    R           8 30REFFLD(DTPE)
     A            DDAMNP    R     A     8 34REFFLD(AMNP)
     A                                      TEXT('AMOUNT')
     A            DDCCY     R           8 53REFFLD(CCY)
     A                                      TEXT('CURRENCY')
     A            DDVDAT    R     A     8 62TEXT('VALUE DATE')
     A                                      REFFLD(CHGD)
     A            DDCSNM    R           8 71REFFLD(CSNM)
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
      * Start CAP004
     A            DDDLNO    R     A  H      REFFLD(DEAL)
     A            DDDS38    R     A  H      REFFLD(DS38)
     A            DDINTR        12A  H
      * End CAP004
      * Start CAP014
     A            DDBRSN    R        H      REFFLD(BRSN)
      * End CAP014
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R MMIDEAMS0                 TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(MMIDEAMS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
      * Start CAP004
     A********************************  1  2'MMIDEAMB'
     A                                  1  2'MMDEAMRB'
      * End CAP004
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'MM DEALER - INVALID DEAL MAINTENAN+
     A                                      CE'
     A                                  4 37'ENQUIRY'
     A                                  6  1'Sel'
     A                                  6  5'Act'
     A                                  6  9'Front Office ID'
     A                                  6 29'Deal'
     A                                  6 39'Amount'
     A                                  6 51'Currency'
     A                                  6 62'Value'
     A                                  6 70'Customer'
     A*
      * Start CAP014
      * Start CAP004
     A**********************************7  1'A,D'
      * End CAP004
      * End CAP014
     A                                  7 29'Type'
     A                                  7 62'Date'
     A                                  7 71'Number'
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R MMIDEAMF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      * Start of CAP006
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      * End of CAP006
     A*
     A* -------------------------------------------------------------------*
     A* * END MESSAGE                                                      *
     A* -------------------------------------------------------------------*
     A*
     A          R MMIDEAMEND                TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
      * Start CAP004
     A********************************  1  2'MMIDEAMB'
     A                                  1  2'MMDEAMRB'
      * End CAP004
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'MM DEALER - INVALID DEAL MAINTENAN+
     A                                      CE'
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
