      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM Fiduciary takings subfile')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Dealing Module                          *
      *                                                               *
      *  DSPF/MMFIDDSDF - MM Fiduciary Takings Subfile                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CDL006 - Dealing Fiduciary                                   *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03(03)
     A N72                                  CA12(12)
      *
      ** Format subfile for Deal FL
      *
     A          R MMFIDDS1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            SFDLNO         6A  B 13  3DSPATR(HI)
     A  72
     AO 84                                  DSPATR(PR)
     A N72N74N84                            DSPATR(UL)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
      *
     A            SFCNUM        10A  B 13 13DSPATR(HI)
     A  72
     AO 84                                  DSPATR(PR)
     A N72N76N84                            DSPATR(UL)
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
      *
     A            SFPTFR         4A  B 13 27DSPATR(HI)
     A  81                                  DSPATR(ND)
     A  81
     AO 72
     AO 84                                  DSPATR(PR)
     A N72N77N84                            DSPATR(UL)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
      *
     A            SFAMNT        15A  B 13 35DSPATR(HI)
     A  72
     AO 84                                  DSPATR(PR)
     A N72N78N84                            DSPATR(UL)
     A  78                                  DSPATR(PC)
     A  78                                  DSPATR(RI)
      *
     A**Start of change CGL029
     A**********  SFACOD         4A  B 13 54DSPATR(HI)                                        CGL029
     A            SFACOD        10A  B 13 52DSPATR(HI)
     A**End of change CGL029
     A  72                                  DSPATR(ND)
     A  72
     AO 84                                  DSPATR(PR)
     A N72N79N84                            DSPATR(UL)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
      *
     A**Start of change CGL029
     A**********  SFACSQ         2A  B 13 62DSPATR(HI)                                        CGL029
     A            SFACSQ         2A  B 13 63DSPATR(HI)
     A**End of change CGL029
     A  72                                  DSPATR(ND)
     A  72
     AO 84                                  DSPATR(PR)
     A N72N80N84                            DSPATR(UL)
     A  80                                  DSPATR(PC)
     A  80                                  DSPATR(RI)
     A            SFCSSN        10A  O 13 68DSPATR(HI)
     A N72                                  DSPATR(ND)
      *
     A            SHAMNT        15  0H
     A            SHCNUM         6   H
     A            SHCSSN        10   H
     A            SHFLAG         1   H
     A            SHDLER         1   H
     A            SHCNER         1   H
     A            SHPOER         1   H
     A            SHAMER         1   H
     A            SHACER         1   H
     A            SHSQER         1   H
     A*/COPY WNCPYSRC,MMFIDDSD01
      *
      ** Format subfile control for Deal FL
      *
     A          R MMFIDDC1                  SFLCTL(MMFIDDS1)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0009)
     A                                      OVERLAY
     A N48                                  SFLEND(*MORE)
     A N61                                  SFLDSPCTL
     A N61N64                               SFLDSP
     A  61                                  SFLCLR
     A            WWRRN          4S 0H      SFLRCDNBR
      *
     A            USID          10A  O  1  3
     A                                  1 14'MMFIDT'
     A                                  1 35'Fiduciary Deposits'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 66'DATE'
     A            DDMRDT         7A  O  1 73TEXT('RUN DATE')
     A            WSID          10A  O  2  3
     A                                  2 66'TIME'
     A                                  2 73TIME
      *
     A                                  4  3'Deal No   :'
     A            DDDLNO         6A  O  4 15TEXT('DEAL NUMBER')
     A                                      DSPATR(HI)
     A                                  4 22'Placed-with Bank :'
     A            DDCNUM         6A  O  4 41TEXT('CUSTOMER NUMBER')
     A                                      DSPATR(HI)
     A            DDCSSN        10A  O  4 48TEXT('CUSTOMER NAME')
     A                                      DSPATR(HI)
      *
     A                                  6  3'Currency  :'
     A            DDCCY          3A  O  6 15DSPATR(HI)
     A                                  6 22'Amount     :'
     A            DDAMNT        15A  O  6 35DSPATR(HI)
     A                                  6 51'Interest Rate  :'
     A            DDRATE        13A  O  6 68DSPATR(HI)
      *
     A                                  7  3'Deal Date :'
     A            DDDATE         6A  O  7 15DSPATR(HI)
     A                                  7 22'Value Date :'
     A            DDVDAT         6A  O  7 35DSPATR(HI)
     A                                  7 51'Maturity Date  :'
     A            DDMDAT         6A  O  7 68DSPATR(HI)
      *
     A                                  8 51'Notice Days    :'
     A            DDNOTD         3A  O  8 68DSPATR(HI)
      *
     A                                  9  3'Deal Type :'
     A            DDTYPE         2A  O  9 15DSPATR(HI)
     A                                  9 22'Sub-Type   :'
     A            DDSTYP         2A  O  9 35DSPATR(HI)
     A                                  9 51'Booking Branch :'
     A            DDBRCA         3A  O  9 68DSPATR(HI)
      *
     A            SLINE         79A  O 10  2DSPATR(UL)
      *
     A                                 11  3'Deal No'
     A                                 11 12'Customer'
     A                                 11 25'Portf.'
     A  81                                  DSPATR(ND)
     A                                 11 37'Amount'
     A**Start of change CGL029
     A**********                       11 53'Account'                                         CGL029
     A                                 11 56'Account'
     A**End of change CGL029
     A  72                                  DSPATR(ND)
     A                                 11 71'Customer'
     A N72                                  DSPATR(ND)
     A**Start of change CGL029
     A**********                       12 50'Code'                                            CGL029
     A                                 12 52'Code'
     A**End of change CGL029
     A  72                                  DSPATR(ND)
     A**Start of change CGL029
     A**********                       12 58'Seq.'                                            CGL029
     A                                 12 63'Seq.'
     A**End of change CGL029
     A  72                                  DSPATR(ND)
     A                                 12 71'Shortname'
     A N72                                  DSPATR(ND)
      *
      ** Format trailer
      *
     A          R MMFIDDF1
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A  85N72                          23 27'Enter=Update'
     A N72                             23 12'F12=Previous'
      *
      ** Format MMFIDDS0 : Subfile Record Error Message
      *
     A          R MMFIDDS0                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      ** Format MMFIDDC0 : Subfile Control Error Message
      *
     A          R MMFIDDC0                  SFLCTL(MMFIDDS0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      OVERLAY
     A  50
     AON50                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
      *
      ** Format subfile for Deal type DP/LP
      *
     A          R MMFIDDS3                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            SFDLNO         6A  B 13  3DSPATR(HI)
     A  72
     AO 84                                  DSPATR(PR)
     A N72N74N84                            DSPATR(UL)
     A  74                                  DSPATR(PC)
     A  74                                  DSPATR(RI)
      *
     A            SFCNUM        10A  B 13 12DSPATR(HI)
     A  72
     AO 84                                  DSPATR(PR)
     A N72N76N84                            DSPATR(UL)
     A  76                                  DSPATR(PC)
     A  76                                  DSPATR(RI)
      *
     A            SFPTFR         4A  B 13 25DSPATR(HI)
     A  81                                  DSPATR(ND)
     A  81
     AO 72
     AO 84                                  DSPATR(PR)
     A N72N77N84                            DSPATR(UL)
     A  77                                  DSPATR(PC)
     A  77                                  DSPATR(RI)
      *
     A            SFAMNT        15A  B 13 32DSPATR(HI)
     A  72
     AO 84                                  DSPATR(PR)
     A N72N78N84                            DSPATR(UL)
     A  78                                  DSPATR(PC)
     A  78                                  DSPATR(RI)
      *
     A**Start of change CGL029
     A**********  SFACOD         4A  B 13 50DSPATR(HI)                                        CGL029
     A            SFACOD        10A  B 13 48DSPATR(HI)
     A**End of change CGL029
     A  72                                  DSPATR(ND)
     A  72
     AO 84                                  DSPATR(PR)
     A N72N79N84                            DSPATR(UL)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
      *
     A**Start of change CGL029
     A**********  SFACSQ         2A  B 13 58DSPATR(HI)                                        CGL029
     A            SFACSQ         2A  B 13 59DSPATR(HI)
     A**End of change CGL029
     A  72                                  DSPATR(ND)
     A  72
     AO 84                                  DSPATR(PR)
     A N72N80N84                            DSPATR(UL)
     A  80                                  DSPATR(PC)
     A  80                                  DSPATR(RI)
      *
     A            SFTRAS         1A  B 13 66DSPATR(HI)
     A N67                                  DSPATR(ND)
     A  72
     AON67
     AO 84                                  DSPATR(PR)
     A  67
     AAN72N87N84                            DSPATR(UL)
     A  87 67                               DSPATR(PC)
     A  87 67                               DSPATR(RI)
      *
     A            SFCSSN        10A  O 13 71DSPATR(HI)
     A N72                                  DSPATR(ND)
      *
     A            SHAMNT        15  0H
     A            SHCNUM         6   H
     A            SHCSSN        10   H
     A            SHFLAG         1   H
     A            SHDLER         1   H
     A            SHCNER         1   H
     A            SHPOER         1   H
     A            SHAMER         1   H
     A            SHACER         1   H
     A            SHSQER         1   H
     A            SHTAER         1   H
     A*/COPY WNCPYSRC,MMFIDDSD02
      *
      ** Format subfile control for Deal DP/LP
      *
     A          R MMFIDDC3                  SFLCTL(MMFIDDS3)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A N63                                  SFLDSPCTL
     A N63N66                               SFLDSP
     A  63                                  SFLCLR
      *
     A            WWRRN3         4S 0H      SFLRCDNBR
      *
     A            USID          10A  O  1  3
     A                                  1 14'MMFIDT'
     A                                  1 35'Fiduciary Deposits'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  1 66'DATE'
     A            DDMRDT         7A  O  1 73TEXT('RUN DATE')
     A            WSID          10A  O  2  3
     A                                  2 66'TIME'
     A                                  2 73TIME
      *
     A                                  4  3'Deal No   :'
     A            DDDLNO         6A  O  4 15TEXT('DEAL NUMBER')
     A                                      DSPATR(HI)
     A                                  4 22'Placed-with Bank :'
     A            DDCNUM         6A  O  4 41TEXT('CUSTOMER NUMBER')
     A                                      DSPATR(HI)
     A            DDCSSN        10A  O  4 48TEXT('CUSTOMER NAME')
     A                                      DSPATR(HI)
      *
     A                                  6  3'Currency  :'
     A            DDCCY          3A  O  6 15DSPATR(HI)
     A                                  6 22'Amount     :'
     A            DDAMNT        15A  O  6 35DSPATR(HI)
     A                                  6 51'Interest Rate  :'
     A            DDRATE        13A  O  6 68DSPATR(HI)
      *
     A                                  7  3'Deal Date :'
     A            DDDATE         6A  O  7 15DSPATR(HI)
     A                                  7 22'Value Date :'
     A            DDVDAT         6A  O  7 35DSPATR(HI)
     A                                  7 51'Maturity Date  :'
     A            DDMDAT         6A  O  7 68DSPATR(HI)
      *
     A                                  8 51'Notice Days    :'
     A            DDNOTD         3A  O  8 68DSPATR(HI)
      *
     A                                  9  3'Deal Type :'
     A            DDTYPE         2A  O  9 15DSPATR(HI)
     A                                  9 22'Sub-Type   :'
     A            DDSTYP         2A  O  9 35DSPATR(HI)
     A                                  9 51'Booking Branch :'
     A            DDBRCA         3A  O  9 68DSPATR(HI)
      *
     A            SLINE         79A  O 10  2DSPATR(UL)
      *
     A                                 11  3'Deal No'
     A                                 11 12'Customer'
     A                                 11 25'Portf.'
     A  81                                  DSPATR(ND)
     A                                 11 37'Amount'
     A                                 11 53'Account'
     A  72                                  DSPATR(ND)
     A                                 11 64'Trust'
     A N67                                  DSPATR(ND)
     A                                 11 71'Customer'
     A N72                                  DSPATR(ND)
     A**Start of change CGL029
     A**********                       12 50'Code'
     A                                 12 48'Code'
     A**End of change CGL029
     A  72                                  DSPATR(ND)
     A**Start of change CGL029
     A**********                       12 58'Seq.'                                            CGL029
     A                                 12 59'Seq.'
     A**End of change CGL029
     A  72                                  DSPATR(ND)
     A                                 12 64'Agre.'
     A N67                                  DSPATR(ND)
     A                                 12 71'Shortname'
     A N72                                  DSPATR(ND)
      *
