      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM Fiduciary Taking Trust Status Screen')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Dealing Module                          *
      *                                                               *
      *  MMFIDTSDF - MM Fiduciary Taking Trust Status Screen          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 29Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *  Prev Amend No. CDL006  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL006 - Dealing Fiduciary                                   *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R MMFIDSF1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A  86                                  PUTOVR
      *
     A            SUSER         10A  O  1  3
     A N25                                  OVRDTA
     A                                  1 14'MMFIDTS'
     A                                  1 35'FIDUCIARY DEPOSITS'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SDATE          6A  O  1 73
     A N25                                  OVRDTA
     A            SWSID         10A  O  2  3
     A N25                                  OVRDTA
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 04 04'Deal No : '
     A            SDLNO          6S 0  04 15
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                 04 22'Taking From : '
      * Begin /CSD027/
     A**********  SCUST          6S 0  04 37                                                  CSD027
     A            SCUST          6A    04 37
      * End /CSD027/
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            SCSSN         10A    04 44
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A            SLINE         79A    06 02DSPATR(UL)
      *
     A                                  9  2'Trust Agreement Status: '
     A            STRAS          1A  B  9 28
     A N25                                  OVRDTA
     A  21                                  DSPATR(PR)
     A  20                                  DSPATR(RI PC)
     A N21N20                               DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 10  2'Restr. Narr. 1:'
     A            SNAR1         60A  B 10 18
     A N25                                  OVRDTA
     A  21                                  DSPATR(PR)
     A N21                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 11  2'Restr. Narr. 2:'
     A            SNAR2         60A  B 11 18
     A N25                                  OVRDTA
     A  21                                  DSPATR(PR)
     A N21                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 12  2'Restr. Narr. 3:'
     A            SNAR3         60A  B 12 18
     A N25                                  OVRDTA
     A  21                                  DSPATR(PR)
     A N21                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                 13  2'Restr. Narr. 4:'
     A            SNAR4         60A  B 13 18
     A N25                                  OVRDTA
     A  21                                  DSPATR(PR)
     A N21                                  DSPATR(UL HI)
     A N25                                  OVRATR
      *
     A                                 22 04'F3=Exit      F12=Previous'
      *
     A          R MMFIDSF2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #PGM                      SFLPGMQ
      *
     A          R MMFIDSF3                  TEXT('Program messages')
     A                                      SFLCTL(MMFIDSF2)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #PGM                      SFLPGMQ
      *
