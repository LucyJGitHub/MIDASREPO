/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas MM Loans/deposits browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MM Dealer Module                                     *
     A*                                                               *
     A*  MMLDNIBDF - LOANS/DEPOSITS & NAS ISSUED BROWSE               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*  Last Amend No. CDL011             Date 07Jul02               *
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. CAP004             Date 26Aug98               *
     A*                 CAP002  *CREATE    Date 01Nov97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CDL011 - Expansion of Customer Number on Dealing Transactions*
     A*  CAP004 - API's phase 3.                                      *
     A*           Allow browse by Front Office Id.                    *
     A*  CAP002 - Conversion of MM inputs into modular structure to   *
     A*           use as APIs.                                        *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R MMLDNIS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDDEAL    R     A     8  8REFFLD(DEAL)
     A                                      DSPATR(HI)
     A            DDTYPE    R           8 16REFFLD(DTPE)
     A            DDCCY     R           8 21REFFLD(CCY)
     A            DDSAMN        17A     8 25TEXT('AMOUNT')
     A            DDVDAT    R     A     8 43TEXT('VALUE DATE')
     A                                      REFFLD(CHGD)
     A            DDMDAT    R     A     8 51TEXT('MATURITY DATE')
     A                                      REFFLD(CHGD)
     A            DDCNUM    R     A     8 59REFFLD(CNUM)
     A            DDDSTS    R           8 68REFFLD(DSTS)
     A            DDTEXT         7A     8 72TEXT('"DELETED" OR CP/BK STATUS')
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R MMLDNIS0                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
      * Start CAP004
     A                                      CF11
      * End CAP004
     A                                      SFLCTL(MMLDNIS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'MMLDNIB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 28'MM DEALER - LOAN/DEPOSIT INPUT & A+
     A                                      UTHORISATION'
     A                                  4 37'ENQUIRY'
     A*/COPY WNCPYSRC,MMLDNIBD01
     A                                  6  2'Select'
     A                                  6  9'Deal'
     A                                  6 15'Deal'
     A                                  6 21'Ccy'
     A                                  6 32'Amount'
     A                                  6 44'Value'
     A                                  6 50'Maturity'
     A                                  6 60'Cust'
     A                                  6 66'Status'
     A                                  7 10'No'
     A                                  7 15'Type'
     A                                  7 45'Date'
     A                                  7 50'/Notice'
     A                                  7 59'Number'
     A*/COPY WNCPYSRC,MMLDNIBD02
      *
      * Start CDL011
     A          R MMLDNIS5                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDDEAL    R     A     8  8REFFLD(DEAL)
     A                                      DSPATR(HI)
     A            DDTYPE    R           8 16REFFLD(DTPE)
     A            DDCCY     R           8 21REFFLD(CCY)
     A            DDSAMN        17A     8 25TEXT('AMOUNT')
     A            DDVDAT    R     A     8 43TEXT('VALUE DATE')
     A                                      REFFLD(CHGD)
     A            DDMDAT    R     A     8 50TEXT('MATURITY DATE')
     A                                      REFFLD(CHGD)
     A            DDCSNM        10A     8 57
     A            DDDSTS    R           8 70REFFLD(DSTS)
     A            DDTEXT         7A     8 73TEXT('"DELETED" OR CP/BK STATUS')
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R MMLDNIS4                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
      * Start CAP004
     A                                      CF11
      * End CAP004
     A                                      SFLCTL(MMLDNIS5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'MMLDNIB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 28'MM DEALER - LOAN/DEPOSIT INPUT & A+
     A                                      UTHORISATION'
     A                                  4 37'ENQUIRY'
     A                                  6  2'Select'
     A                                  6  9'Deal'
     A                                  6 15'Deal'
     A                                  6 21'Ccy'
     A                                  6 32'Amount'
     A                                  6 43'Value'
     A                                  6 49'Maturity'
     A                                  6 58'Customer'
     A                                  6 68'Status'
     A                                  7 10'No'
     A                                  7 15'Type'
     A                                  7 44'Date'
     A                                  7 50'Notice'
     A                                  7 60'Name'
      * End CDL011
      *
      * Start CAP004
     A          R MMLDNIS3                  TEXT('SELECT SCREEN SUBFILE')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDFODN        20A     8  7TEXT('FRONT OFFICE ID.')
     A                                      DSPATR(HI)
     A            DDDEAL    R     A     8 29REFFLD(DEAL)
     A            DDTYPE    R           8 37REFFLD(DTPE)
     A            DDCCY     R           8 42REFFLD(CCY)
     A            DDSAMN        17A     8 46TEXT('AMOUNT')
     A            DDVDAT    R     A     8 64TEXT('VALUE DATE')
     A                                      REFFLD(CHGD)
     A            DDMDAT    R     A     8 72TEXT('MATURITY DATE')
     A                                      REFFLD(CHGD)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R MMLDNIS2                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLCTL(MMLDNIS3)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'MMLDNIB2'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 28'MM DEALER - LOAN/DEPOSIT INPUT & A+
     A                                      UTHORISATION'
     A                                  4 37'ENQUIRY'
     A*/COPY WNCPYSRC,MMLDNIBD03
     A                                  6  2'Select'
     A                                  6  9'Front Office'
     A                                  6 30'Deal'
     A                                  6 36'Deal'
     A                                  6 42'Ccy'
     A                                  6 53'Amount'
     A                                  6 65'Value'
     A                                  6 71'Maturity'
     A                                  7  9'Identifier'
     A                                  7 31'No'
     A                                  7 36'Type'
     A                                  7 66'Date'
     A                                  7 71'/Notice'
     A*/COPY WNCPYSRC,MMLDNIBD04
      * End CAP004
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R MMLDNIF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
      * Start CAP004
     A**********                       24  2'F3=Exit F12=Cancel'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
      * End CAP004
