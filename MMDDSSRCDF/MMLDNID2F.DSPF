      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM Loans/deposits display screen 2')
      *****************************************************************
      *                                                               *
      *  Midas - MM Dealer Module                                     *
      *                                                               *
      *  MMLDNID2F - Loans/Deposits & NAS Issued Display Screen 2     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL020             Date 03Feb04               *
      *  Prev Amend No. CDL019             Date 03Feb04               *
      *                 CDL017             Date 03Feb04               *
      *                 CDL016  *CREATE    Date 03Feb04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL020 - Apply Base Rate at Value Date                       *
      *  CDL019 - Allow Base Rate Changes on Fixed Deposits/Loans     *
      *  CDL017 - Penalty Fee & Early Maturity of Fixed Term Loan/Dep *
      *  CDL016 - Autumatic Rollover of Fixed Term Loan/Deposit       *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MMLDNIS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MMLDNIS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A  76                                  CF19
     A  76                                  CF06
     A                                      SFLCTL(MMLDNIS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R MMLDNID1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A  76                                  CF19
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A N78                              1  2'MMLDNID2'
     A  78                              1  2'MIDAS'
     AA 78N90                           2 31'REPO MAINTENANCE'
     AA 78 90                           2 34'REPOS REPAIR'
     A  78                                  DSPATR(UL)
     A  78                                  DSPATR(HI)
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A  77                              2  2'MMRPDLD'
     A  77                              2 28'Cash Deposit/Loan'
     A  77                                  DSPATR(UL)
     A  77                                  DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3  2'Action:'
     A            DDACTN         1   O  3 10TEXT('ACTION CODE')
     A                                      DSPATR(HI)
     A  70                                  DSPATR(PC)
     A                                      OVRDTA
     A N78
     AAN77                              3 24'MM DEALER - LOAN/DEPOSIT INPUT & A+
     A                                      UTHORISATION'
     A                                  4  2'Deal No:'
     A                                      DSPATR(HI)
     A            DDDLNO    R     A  O  4 11REFFLD(DEAL)
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  5  6'Source of Funds:'
     A  80                                  DSPATR(ND)
     A            DDSOFD        20A  B  5 23
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI)
     A  70
     AO 80                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  80                                  DSPATR(ND)
     A                                      OVRDTA
      *
      ** Start of change CDL017
     A                                  6  6'Cancellation Date:'
     A            DDCAND         6A  O  6 25
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                  6 34'Penalty Pay/Receive:'
     A            DDPNPR         1A  O  6 56
     A                                      DSPATR(HI)
     A                                      OVRDTA
      ** End of change CDL017
      *
      ** Start of change CDL019
     A                                  7  6'Fix Base Rate:'
     A  81                                  DSPATR(ND)
     A            DDFBRT         1A  B  7 21
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI)
     A  70
     AO 81                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  81                                  DSPATR(ND)
     A                                      OVRDTA
      ** End of change CDL019
      *
      ** Start of change CDL020
     A                                  7 34'Actual Interest Rate:'
     A  82                                  DSPATR(ND)
     A            DDAIR         12A  B  7 56
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  70
     AO 82                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  82                                  DSPATR(ND)
     A                                      OVRDTA
      ** End of change CDL020
      *
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R MMLDNIF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A  76                                  CF19
     A                                      OVERLAY
     A                                      BLINK
     A            DDFOTX        40A    23  2
     A                                      DSPATR(HI)
     A            DDSTS         24     23 45
     A  79                                  DSPATR(RI)
     A N79                                  DSPATR(ND)
     A            DDCSTS        10     23 70
     A  93                                  DSPATR(HI)
     A N93                                  DSPATR(ND)
     A                                 24  2'F3=Exit F12=Cancel'
     A                                 24 22'F8=Next'
     A N72                                  DSPATR(ND)
     A                                 24 31'F7=Prev'
     A N71                                  DSPATR(ND)
     A                                 24 40'F14=Settlements'
     A N74                                  DSPATR(ND)
     A                                 24 57'F15=Select'
     A N75                                  DSPATR(ND)
     A                                 24 69'F19=Funding'
     A N76                                  DSPATR(ND)
