      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM Loans/Deisplay File - LU Window')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market ILE Module                              *
      *                                                               *
      *  DSPF/MMLDNILDF - MM Loans/Deposits LUX Display File          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CER001  *CREATE    Date 25Apr05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT
      *
      * --------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                         *
      * --------------------------------------------------------------*
      *
     A          R MMLDNIF0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2005')
     A                                      KEEP
     A                                      OVERLAY
     A                                      CF03(03 'Exit')
     A  35                                  CF05(05 'Reset')
     A  40                                  CF10(10 'Delete')
     A                                      CF12(12 'Previous')
     A                                      PUTOVR
      * ---------------------------------------------------------------
     A            SUSER         10A  O  1  2
     A            SJOB          10A  O  2  2
     A            SPGM          10A  O  1 13
     A                                  1 65'Date:'
     A            BJMRDT         7A  O 01 72
     A                                  2 65'Time:'
     A                                  2 71TIME
      * ---------------------------------------------------------------
     A            ##PGM         10A  H
     A                                  2 19'European Reporting - Luxembourg'
     A                                      DSPATR(HI)
      *
     A                                  4  6'Lump-Sum Code ....................+
     A                                      .. :'
     A            L6LXLUSU       2A  B  4 45OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N50N20                               DSPATR(UL)
     A  50                                  DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
      *
     A                                  5  6'LMI Circular 96 / 127 ............+
     A                                      .. :'
     A            L6LXIM93       1A  B  5 45OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N51N20                               DSPATR(UL)
     A  51                                  DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                  6  6'Claims Type ......................+
     A                                      .. :'
     A            L6LXCLAT       1A  B  6 45OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N52N20                               DSPATR(UL)
     A  52                                  DSPATR(RI)
     A  52N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                  7  6'Correction Amount ................+
     A                                      .. :'
     A            L6LXCOAM      15A  B  7 45OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N53N20                               DSPATR(UL)
     A  53                                  DSPATR(RI)
     A  53N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                  8  6'Subordinate ......................+
     A                                      .. :'
     A            L6LXSUBR       1A  B  8 45OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N54N20                               DSPATR(UL)
     A  54                                  DSPATR(RI)
     A  54N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                  9  6'Mobilisable Customer .............+
     A                                      .. :'
     A            L6LXMOBL      10A  B  9 45OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N55N20                               DSPATR(UL)
     A  55                                  DSPATR(RI)
     A  55N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                 10  6'Risk Code ........................+
     A                                      .. :'
     A            L6LXRISC       2A  B 10 45OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N56N20                               DSPATR(UL)
     A  56                                  DSPATR(RI)
     A  56N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                 11  6'Consumer Credit Code .............+
     A                                      .. :'
     A            L6LXGUAC       1A  B 11 45OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N57N20                               DSPATR(UL)
     A  57                                  DSPATR(RI)
     A  57N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                 12  6'Import / Export Code .............+
     A                                      .. :'
     A            L6LXIMEX       1A  B 12 45OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N58N20                               DSPATR(UL)
     A  58                                  DSPATR(RI)
     A  58N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A                                 13  6'Property Sector Code .............+
     A                                      .. :'
     A            L6LXPPSC       6A  B 13 45OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A N59N20                               DSPATR(UL)
     A  59                                  DSPATR(RI)
     A  59N20                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N70                                  DSPATR(ND)
      *
     A                                 23  6'F3=Main Menu'
     A                                      COLOR(BLU)
     A  35                             23 21'F5=Refresh'
     A                                      COLOR(BLU)
     A  40                             23 33'F10=Delete'
     A                                      COLOR(BLU)
     A                                 23 45'F12=Previous'
     A                                      COLOR(BLU)
      *
      * --------------------------------------------------------------*
      *   Message subfile format                                      *
      * --------------------------------------------------------------*
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      * --------------------------------------------------------------*
      *   Message subfile control                                     *
      * --------------------------------------------------------------*
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(02)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
