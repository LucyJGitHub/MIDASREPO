/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas MM Loans/deposits rollover browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MM Dealer Module                                     *
     A*                                                               *
     A*  MMLDNIODF - LOANS/DEPOSITS & NAS ISSUED ROLLOVER BROWSE      *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Last Amend No. 156950             Date 11Mar99               *
     A*  Prev Amend No. CAP004  *CREATE    Date 13Oct98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  156950 - Deal number positioning and insert message.         *
     A*  CAP004 - APIS Phase 3.                                       *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A* -------------------------------------------------------------------*
     A* * MESSAGE SUBFILE FORMAT                                           *
     A* -------------------------------------------------------------------*
     A*
     A          R MMLDNIS3                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A*   MESSAGE SUBFILE CONTROL                                          *
     A* -------------------------------------------------------------------*
     A*
     A          R MMLDNIS2                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      SFLCTL(MMLDNIS3)
     A N91                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  92                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SUBFILE FORMAT                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R MMLDNIS1                  TEXT('SELECT SCREEN SUBFILE')
     A                                      SFL
     A            DDRACT         1   B  9  3TEXT('ROLOVER ACTION')
     A                                      DSPATR(UL)
     A            DDDEAL    R     A     9  8REFFLD(DEAL)
     A                                      DSPATR(HI)
     A            DDMDAT    R     A     9 17TEXT('MATURITY DATE')
     A                                      REFFLD(CHGD)
     A            DDCNUM    R     A     9 26REFFLD(CNUM)
     A            DDCSNM    R     A     9 34REFFLD(CSNM)
     A            DDCCY     R           9 50REFFLD(CCY)
     A            DDSAMN        17A     9 54TEXT('AMOUNT')
     A            DDCCYINCY      1A  H      TEXT('"IN" CURRENCY IND')
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SUBFILE CONTROL                                           *
     A* -------------------------------------------------------------------*
     A*
     A          R MMLDNIS0                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      SFLCTL(MMLDNIS1)
     A  94                                  SFLDSP
     A  95                                  SFLDSPCTL
     A N94N95                               SFLCLR
     A  96                                  SFLEND
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A N96                                  ROLLUP(98)
     A N96                                  PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A            DDWSID    R        O  1  2REFFLD(USER)
     A                                  1 13'MMLDNIO'
     A                                  1 27'ROLLOVER DEALS MAINTENANCE'
     A                                      DSPATR(HI UL)
     A                                  1 67'DATE '
     A            BJMRDT    R        O  1 73REFFLD(RUNA)
     A            DDUSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            DDTIME         6  0   2 73EDTWRD('  :  :  ')
     A                                      OVRDTA
     A*                                                           /*156950*/
     A************                      4 40'Branch To Be Reviewed'
     A************DDBRCA         3A  B  4 62TEXT('Review Branch Key')
     A**10********                          DSPATR(RI PC)
     A*N10********                          DSPATR(HI UL)
     A************                          OVRDTA
     A                                  4 32'Deal Number/Branch To Review'
     A            DDPTDN         6A  B  4 62TEXT('Position To Deal Number')
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI UL)
     A                                      OVRDTA
     A            DDBRCA         3A  B  4 69TEXT('Review Branch Key')
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI UL)
     A                                      OVRDTA
     A*                                                           /*156950*/
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                  7  2'Select'
     A                                  7  9'Deal'
     A                                  7 16'Maturity'
     A                                  7 26'Customer'
     A                                  7 50'Ccy'
     A                                  7 63'Amount'
     A                                  8  8'Number'
     A                                  8 17' Date  '
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R MMLDNIF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit'
     A                                 24 27'* No details to display *'
     A                                      DSPATR(HI)
     A  10
     AO 94                                  DSPATR(ND)
     A                                      OVRATR
