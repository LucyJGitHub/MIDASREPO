/*STD *  DSPFBASE                                                    *
/*EXI *  TEXT('Midas MM Loans/deposits rollover prompt')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MM Dealer Module                                     *
     A*                                                               *
     A*  MMLDNIODF - LOANS/DEPOSITS & NAS ISSUED ROLLOVER PROMPT      *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
     A*  Last Amend No. 190752             Date 05Apr01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. CAP004  *CREATE    Date 13Oct98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  190752 - Do not display the Base rate field, if 'Repo        *
     A*           Rollover Maintenance' option.                       *
     A*  CAP004 - APIS Phase 3.                                       *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A* -------------------------------------------------------------------*
     A* * MESSAGE SUBFILE FORMAT                                           *
     A* -------------------------------------------------------------------*
     A*
     A          R MMLDNIS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A*   MESSAGE SUBFILE CONTROL                                          *
     A* -------------------------------------------------------------------*
     A*
     A          R MMLDNIS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(MMLDNIS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A* * ROLLOVER DETAILS PROMPT                                          *
     A* -------------------------------------------------------------------*
     A*
     A          R MMLDNID1                  TEXT('ROLLOVER DETAILS')
     A                                      CF03
     A                                      CF12
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDWSID    R        O  1  2REFFLD(USER)
     A                                  1 13'MMLDNIP'
     A                                  1 27'ROLLOVER DEALS MAINTENANCE'
     A                                      DSPATR(HI UL)
     A                                  1 67'DATE '
     A            BJMRDT    R        O  1 73REFFLD(RUNA)
     A            DDUSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            DDTIME         6  0O  2 73EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  3  3'Action Code'
     A            DDRACT         1A  B  3 15
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  68
     AO 10N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  4  3'Deal Number'
     A            DDDADN         6A  B  4 15TEXT('DEAL NUMBER')
     A  11                                  DSPATR(RI)
     A N11                                  DSPATR(HI)
     A  11N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  4 30'Customer Shortname '
     A            DDCSNM        10A     4 52TEXT('CUSTOMER SHORTNAME')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                  7  3'New Deal Number '
     A            DDDLNO         6A  B  7 30TEXT('NEW DEAL NUMBER')
     A  12                                  DSPATR(RI)
     A N12                                  DSPATR(HI)
     A  12N70                               DSPATR(PC)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9  3'Principal '
     A            DDRAMN        14A  B  9 30TEXT('PRINCIPAL')
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                  9 48'='
     A  60                                  DSPATR(ND)
     A                                      OVRATR
     A            DDAMNP        14A     9 51TEXT('PRINCIPAL AMT EURO')
     A  60                                  DSPATR(ND)
     A                                      OVRDTA
     A                                  9 69'Euros'
     A  60                                  DSPATR(ND)
     A                                      OVRATR
      *
     A                                 11  3'New Maturity Date '
     A            DDMDAT         6A  B 11 30TEXT('NEW MATURITY DATE')
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 13  3'Base Rate Code    '
     A* Start of Change 190752
     A  18                                  DSPATR(ND)
     A                                      OVRATR
     A* End of Change 190752
     A            DDBSRC         2A  B 13 30TEXT('BASE RATE CODE')
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A* Start of Change 190752
     A  18                                  DSPATR(ND)
     A* End of Change 190752
     A                                      OVRDTA
      *
     A                                 15  3'Spread/Rate       '
     A            DDSPRT        12A  B 15 30TEXT('SPREAD')
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      *
     A                                 17  3'Interest Calculation Method'
     A  60                                  DSPATR(ND)
     A                                      OVRATR
     A            DDICMT         1A  B 17 31TEXT('INTEREST CALCULATION METHOD')
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  70
     AO 60                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  60                                  DSPATR(ND)
     A                                      OVRDTA
     A*
     A* -------------------------------------------------------------------*
     A* * ROLLOVER DETAILS FOOTER                                          *
     A* -------------------------------------------------------------------*
     A*
     A          R MMLDNIF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A*
     A                                 24  2'F3=Exit F12=Cancel'
