/*STDA*  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas MM Invalid Loans/Deposits Browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Money Market Module                                  *
     A*                                                               *
     A*  MMLDNIRDF - INVALID LOANS/DEPOSITS BROWSE                    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
     A*  Last Amend No. 166907             Date 27Dec99               *
      * Midas DBA 3.02 -----------------------------------------------*
     A*  Prev Amend No. CAP014             Date 08Dec99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CAP006             Date 08Mar99               *
     A*                 CAP004             Date 01Sep98               *
     A*                 CAP002  *CREATE    Date 01Nov97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  166907 - Show 'Deal No. 999999 successfully inserted'        *
     A*           message on screen.                                  *
     A*  CAP014 - Repair function enhancements                        *
     A*  CAP006 - Show error message on screen                        *
     A*  CAP004 - API's phase 3.                                      *
     A*           Enable delete of invalid transactions               *
     A*  CAP002 - Conversion of Midas inputs to modular API structure *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R MMILDNIS1                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1A  B  8  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDACTN    R        O  8  6REFFLD(ACTO)
     A                                      DSPATR(HI)
     A            DDFOTRANIDR     A  O  8  9REFFLD(FOTRNID)
     A                                      DSPATR(HI)
     A            DDTYPE    R        O  8 30REFFLD(DTPE)
     A            DDCCY     R        O  8 34REFFLD(CCY)
     A            DDAMNP    R        O  8 38REFFLD(AM14)
     A            DDVDAT    R     A  O  8 54REFFLD(CHGD)
     A                                      TEXT('VALUE DATE')
     A            DDMDAT    R     A  O  8 62REFFLD(CHGD)
     A                                      TEXT('MATURITY DATE')
     A            DDCUST    R        O  8 71REFFLD(CSNM)
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
      * Start CAP004
     A            DDDLNO    R     A  H      REFFLD(DEAL)
     A            DDDLDT    R     A  H      REFFLD(CHGD)
     A            DDSPRT        12A  H
      * End CAP004
      * Start CAP014
     A            DDBRSN    R        H      REFFLD(BRSN)
      * End CAP014
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R MMILDNIS0                 TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(MMILDNIS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
      * Start CAP004
     A********************************  1  2'MMILDNIB'
     A                                  1  2'MMLDNIRB'
      * End CAP004
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6Y 0O  1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3A  O  1 77TEXT('WORKSTATION ID')
     A                                  3 24'MM DEALER - INVALID DEAL MAINTENAN-
     A                                      CE'
     A                                  4 37'ENQUIRY'
     A                                  6  1'Sel Act Front Office ID     Deal C-
     A                                      CY  Amount         Value   Maturity-
     A                                       Customer'
      * Start CAP004
     A********************************* 7  1'                            Type  -
     A*********************************                        Date    Date    -
     A*********************************               '
      * Start CAP014
     A**********************************7  1'A,D'
     A**********************************                       Date    Date    -
     A**********************************              '
      * End CAP014
      * End CAP004
     A*/COPY WNCPYSRC,MMLDNIRD01
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R MMILDNIF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      * Start of CAP006
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      * End of CAP006
     A*
     A* -------------------------------------------------------------------*
     A* * END MESSAGE                                                      *
     A* -------------------------------------------------------------------*
     A*
     A          R MMILDNIEND                TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
      * Start of 166907
     A                                      PUTOVR
      * End of 166907
      * Start CAP004
     A********************************  1  2'MMILDNIB'
     A                                  1  2'MMLDNIRB'
      * End CAP004
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'MM DEALER - INVALID DEAL MAINTENAN+
     A                                      CE'
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
      * Start of 166907
     A            DDFOTX        40A    24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      * End of 166907
