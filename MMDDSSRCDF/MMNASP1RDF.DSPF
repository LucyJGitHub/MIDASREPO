      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM NAs Purchased BoE Repair')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Dealing Module                          *
      *                                                               *
      *  MMNASP1RDF - MM NAs Purchased BOE Repair Window              *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. LLN022  *CREATE    Date 24Jan05               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  LLN022 - BOE Upgrade to Midas Plus                           *
      *                                                               *
      *****************************************************************
     A                                      PRINT
     A                                      CA03(03 'Exit')
     A                                      CF12(12 'Previous')
     A          R MMNASP1RF0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2005')
     A                                      WINDOW(&SWROW &SWCOL 9 50)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'MMNASPF1'
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A            STITL         50A  O  1  1
      *----------------------------------------------------------------
     A                                  3  1'Guarantee Code   '
     A            #0GTEE         2A  B  3 23
     A N50N20                               DSPATR(HI)
     A  50N20                               DSPATR(RI)
     A  50N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                  4  1'Qualifying Flag  '
     A            #0QUAL         1A  B  4 23
     A N51N20                               DSPATR(HI)
     A  51N20                               DSPATR(RI)
     A  51N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                  5  1'Final Maturity Date '
     A            #0FMAT         6A  B  5 23
     A N52N20                               DSPATR(HI)
     A  52N20                               DSPATR(RI)
     A  52N20                               DSPATR(PC)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
      *----------------------------------------------------------------
     A                                  7  1'F3=Main Menu'
     A                                  7 15'F12=Previous'
      *=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(8)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
