/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas MM NAs purchased browse ')
      *****************************************************************
      *                                                               *
      *  Midas - MM Dealer Module                                     *
      *                                                               *
      *  MMNASPBDF - NEGOTIABLE ASSETS PURCHASED BROWSE               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL011             Date 07Jul02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP004             Date 26Aug98               *
      *                 CAP002  *CREATE    Date 12Feb98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CDL011 - Expansion of Customer Number on Dealing Transactions*
     A*  CAP004 - API's phase 3.                                      *
     A*           Allow browse by Front Office Id.                    *
      *  CAP002 - Conversion of MM inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
      *
     A          R MMNASPS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDDEAL    R     A     8  7REFFLD(DEAL)
     A                                      DSPATR(HI)
     A            DDTYPE    R           8 16REFFLD(DTPE)
     A            DDCCY     R           8 21REFFLD(CCY)
     A            DDAMNP    R     A  O  8 26REFFLD(AMNP)
     A                                      TEXT('AMOUNT')
     A            DDVDAT    R     A     8 42TEXT('VALUE DATE')
     A                                      REFFLD(CHGD)
     A            DDMDAT    R     A     8 50TEXT('MATURITY DATE')
     A                                      REFFLD(CHGD)
     A            DDCNUM    R     A     8 58REFFLD(CNUM)
     A            DDDSTS    R           8 68REFFLD(DSTS)
     A            DDTEXT         7A     8 72TEXT('"DELETED" OR CP/BK STATUS')
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN CONTROL                                            *
      * -------------------------------------------------------------------*
      *
     A          R MMNASPS0                  TEXT('SELECT SCREEN CONTROL')
      *
     A                                      CF03
     A                                      CF12
      * Start CAP004
     A                                      CF11
      * End CAP004
     A                                      SFLCTL(MMNASPS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'MMNASPB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'MM DEALER - N/A PURCHASED INPUT & -
     A                                      AUTHORISATION'
     A                                  4 37'ENQUIRY'
     A                                  6  2'Select'
     A                                  6  9'Deal'
     A                                  6 15'Deal'
     A                                  6 21'Ccy'
     A                                  6 31'Price'
     A                                  6 42'Value'
     A                                  6 49'Maturity'
     A                                  6 59'Cust'
     A                                  6 66'Status'
     A                                  6 73'Match'
     A                                  7 10'No'
     A                                  7 15'Type'
     A                                  7 42'Date'
     A                                  7 51'Date'
     A                                  7 58'Number'
     A                                  7 73'CP BK'
 
      * Start CDL011
     A          R MMNASPS5                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDDEAL    R     A     8  7REFFLD(DEAL)
     A                                      DSPATR(HI)
     A            DDTYPE    R           8 16REFFLD(DTPE)
     A            DDCCY     R           8 21REFFLD(CCY)
     A            DDAMNP    R     A  O  8 26REFFLD(AMNP)
     A                                      TEXT('AMOUNT')
     A            DDVDAT    R     A     8 42TEXT('VALUE DATE')
     A                                      REFFLD(CHGD)
     A            DDMDAT    R     A     8 49TEXT('MATURITY DATE')
     A                                      REFFLD(CHGD)
     A            DDCSNM        10A     8 56
     A            DDDSTS    R           8 68REFFLD(DSTS)
     A            DDTEXT         7A     8 72TEXT('"DELETED" OR CP/BK STATUS')
     A N95                                  DSPATR(ND)
     A  95                                  DSPATR(RI)
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN CONTROL                                            *
      * -------------------------------------------------------------------*
      *
     A          R MMNASPS4                  TEXT('SELECT SCREEN CONTROL')
      *
     A                                      CF03
     A                                      CF12
      * Start CAP004
     A                                      CF11
      * End CAP004
     A                                      SFLCTL(MMNASPS5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'MMNASPB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'MM DEALER - N/A PURCHASED INPUT & -
     A                                      AUTHORISATION'
     A                                  4 37'ENQUIRY'
     A                                  6  2'Select'
     A                                  6  9'Deal'
     A                                  6 15'Deal'
     A                                  6 21'Ccy'
     A                                  6 31'Price'
     A                                  6 42'Value'
     A                                  6 48'Maturity'
     A                                  6 57'Customer'
     A                                  6 66'Status'
     A                                  6 73'Match'
     A                                  7 10'No'
     A                                  7 15'Type'
     A                                  7 43'Date'
     A                                  7 50'Date'
     A                                  7 58'Number'
     A                                  7 73'CP BK'
      * End CDL011
 
      * Start CAP004
     A          R MMNASPS3                  TEXT('SELECT SCREEN SUBFILE')
      *
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDFODN        20A     8  7TEXT('FRONT OFFICE ID.')
     A                                      DSPATR(HI)
     A            DDDEAL    R     A     8 28REFFLD(DEAL)
     A            DDTYPE    R           8 37REFFLD(DTPE)
     A            DDCCY     R           8 42REFFLD(CCY)
     A            DDAMNP    R     A  O  8 47REFFLD(AMNP)
     A                                      TEXT('AMOUNT')
     A            DDVDAT    R     A     8 63TEXT('VALUE DATE')
     A                                      REFFLD(CHGD)
     A            DDMDAT    R     A     8 71TEXT('MATURITY DATE')
     A                                      REFFLD(CHGD)
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN CONTROL                                            *
      * -------------------------------------------------------------------*
      *
     A          R MMNASPS2                  TEXT('SELECT SCREEN CONTROL')
      *
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLCTL(MMNASPS3)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'MMNASPB2'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 24'MM DEALER - N/A PURCHASED INPUT & -
     A                                      AUTHORISATION'
     A                                  4 37'ENQUIRY'
     A                                  6  2'Select'
     A                                  6  9'Front Office'
     A                                  6 30'Deal'
     A                                  6 36'Deal'
     A                                  6 42'Ccy'
     A                                  6 52'Price'
     A                                  6 63'Value'
     A                                  6 70'Maturity'
     A                                  7  9'Identifier'
     A                                  7 31'No'
     A                                  7 36'Type'
     A                                  7 63'Date'
     A                                  7 72'Date'
      * End CAP004
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R MMNASPF1                  TEXT('SELECT SCREEN FOOTER')
      *
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
      * Start CAP004
     A**********                       24  2'F3=Exit F12=Cancel'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
      * End CAP004
