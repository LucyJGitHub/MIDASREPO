/*STD *  DSPFBASE                                                    *
/*EXI *  TEXT('Midas MM NAs purchased display  ')                     *
      *****************************************************************
      *                                                               *
      *  Midas - MM Dealer Module                                     *
      *                                                               *
      *  MMNASPDDF - NEGOTIABLE ASSETS PURCHASED DISPLAY              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR814519           Date 10Mar15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL038             Date 10May05               *
      *                 225618             Date 30Mar04               *
      *                 CAS005             Date 16Dec02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP002  *CREATE    Date 03Feb98               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR814519 - Allow negative base rate.                         *
      *           - Applied for MD033488                              *
      *  CDL038 - Extended Deal Sub Type                              *
      *  225618 - Remove the slash in 'Liquidity/Premium' field.      *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *  CAP002 - Conversion of MM inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MMNASPS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MMNASPS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A                                      SFLCTL(MMNASPS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R MMNASPD1
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A** Start of change CAS001
     A** End of change CAS001
     A                                  1  2'MMNASPD'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3  2'Action:'
     A            DDACTN         1   B  3 10TEXT('ACTION CODE')
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  68
     AO 10N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  3 24'MM DEALER - N/A PURCHASED INPUT & -
     A                                      AUTHORISATION'
     A                                  4  2'Deal No:'
     A                                      DSPATR(HI)
     A            DDDLNO    R     A  B  4 11
     A                                      REFFLD(DEAL)
     A  11                                  DSPATR(RI)
     A N11                                  DSPATR(HI)
     A  11N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  5  6'Type:'
     A            DDMTYP    R        B  5 12REFFLD(DTPE)
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  5 16'Sub-Type:'
     A            DDSTYP    R        B  5 26REFFLD(DLST)
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  5 31'Fed Funds:'
     A            DDFEDF         1   B  5 42TEXT('Fed funds indicator')
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  5 46'Deal Date:'
     A            DDDLDT    R     A  B  5 57
     A                                      REFFLD(CHGD)
     A                                      TEXT('DEAL DATE')
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  5 64'Bkg Branch:'
     A  80                                  DSPATR(ND)
     A            DDBRCA    R     A  B  5 76REFFLD(BRCA)
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI)
     A  70
     AO 80                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  80                                  DSPATR(ND)
     A                                      OVRDTA
     A                                  6  6'Broker:'
     A            DDBKCD    R     A  B  6 14REFFLD(BKCD)
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  6 21'Brokerage:'
     A            DDBRKG        14A  B  6 32TEXT('BROKERAGE')
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  7  6'Issued By:'
     A            DDISUR    R        B  7 17REFFLD(CSNM)
     A                                      TEXT('NAB Issuer')
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  7 31'Bought from:'
     A            DDCSNM    R        B  7 44REFFLD(CSNM)
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8  6'Value Date:'
     A            DDVDAT    R     A  B  8 18
     A                                      REFFLD(CHGD)
     A                                      TEXT('Value Date')
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8 26'Maturity Date:'
     A            DDMDAT    R     A  B  8 41
     A                                      REFFLD(CHGD)
     A                                      TEXT('Maturity Date')
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  8 66'A/C Seq.No:'
     A            DDACSQ    R     A  B  8 78REFFLD(ACSQ)
     A  23                                  DSPATR(RI PC)
     A N23                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9  6'Currency:'
     A            DDCCY     R        B  9 16REFFLD(CCY)
     A  24                                  DSPATR(RI PC)
     A N24                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9 21'Face Value:'
     A            DDFVAL    R     A  B  9 33REFFLD(FAVL)
     A                                      TEXT('Face Value')
     A  25                                  DSPATR(RI PC)
     A N25                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  9 49'Purchase Price:'
     A            DDAMNP        15A  B  9 65
     A  26                                  DSPATR(RI PC)
     A N26                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 10  6'Book Code:'
     A            DDBOKC    R     A  B 10 17REFFLD(BOKC)
     A  27                                  DSPATR(RI PC)
     A N27                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 10 21'Linked Reference number:'
     A            DDLNKN    R     A  B 10 46REFFLD(LNKDN)
     A                                      TEXT('Linked Ref. No.')
     A  28                                  DSPATR(RI PC)
     A N28                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 10 54'Bill Depot ID:'
     A            DDBDID        10A  B 10 69TEXT('BILL DEPOT ID')
     A  29                                  DSPATR(RI PC)
     A N29                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 11  2'Interest'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 11 21'Interest Rate:'
     A            DDINTR        12A  B 11 36TEXT('Interest rate')
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 11 50'Calc Method:'
     A            DDICMT    R     A  B 11 63REFFLD(ICMT)
     A                                      TEXT('Calculation Method')
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
      **Start of change AR814519
     A                                 11 65'Negative Int.'
     A            DDNEGV    R     A    11 79REFFLD(NGVI)
     A                                      TEXT('Negative Interest')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      **End of change AR814519
     A                                 12  6'Frequency:'
     A            DDIPFQ    R        B 12 17REFFLD(IPFQ)
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 12 21'Next Int Date:'
     A            DDNDAT         6   B 12 36TEXT('NEXT INTEREST DATE')
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 12 45'Start/Last Int Date:'
     A            DDSLID         6A  B 12 66TEXT('Start/Last Int Date')
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 13  6'Yield/Discount Rate:'
     A            DDDCRT    R     A  B 13 27REFFLD(DCRT)
     A                                      TEXT('Discount Rate')
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 13 41'Int Day:'
     A            DDIPDM    R     A  B 13 50REFFLD(IPDM)
     A                                      TEXT('Interest Payment Day')
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 13 54'Total Int:'
     A            DDMTTI        14A  B 13 65TEXT('Total Interest')
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 14  6'Base Ccy Rate:'
     A            DDBRAT    R   13A  B 14 22REFFLD(BCYR)
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 14 38'N.A.Reference No:'
     A            DDCDRF    R        B 14 56REFFLD(CDRF)
     A                                      TEXT('NA Reference No')
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 15  6'Round-down Ind:'
     A            DDRDFC         1A  B 15 22
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 15 31'Parcel Flag:'
     A            DDPCLI    R        B 15 44REFFLD(PCLI)
     A                                      TEXT('Parcel Flag')
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 15 53'Parcel Size:'
     A            DDPSZE    R     A  B 15 66REFFLD(PSIZ)
     A                                      TEXT('Parcel Size')
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                 16  6'Orig Branch:'
     A  81                                  DSPATR(ND)
     A            DDORBR    R    3A  B 16 19REFFLD(ORBR)
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(HI)
     A  70
     AO 81                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  81                                  DSPATR(ND)
     A                                      OVRDTA
      *                                              Begin /CDL038/
     A                                 16 24'Class:'
     A  90                                  DSPATR(ND)
     A            DDCLAS         4A  B 16 31
     A  50                                  DSPATR(RI PC)
     A N50                                  DSPATR(HI)
     A  70
     AO 90                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  90                                  DSPATR(ND)
     A                                      OVRDTA
      *                                              End   /CDL038/
     A                                 16 38'Profit centre:'
     A  82                                  DSPATR(ND)
     A            DDPRFC    R    4A  B 16 54REFFLD(PRFC)
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(HI)
     A  70
     AO 82                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  82                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 17  2'Funding'
     A N83                                  DSPATR(UL HI)
     A  83                                  DSPATR(ND)
     A                                 17 11'Spread/Rate:'
     A  83                                  DSPATR(ND)
     A            DDFSRP        12A  B 17 24
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(HI)
     A  70
     AO 83                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  83                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 17 37'Profit Centre:'
     A  83                                  DSPATR(ND)
     A            DDFPRC         4A  B 17 52
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(HI)
     A  70
     AO 83                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  83                                  DSPATR(ND)
     A                                      OVRDTA
     A** Start of change CAS001
     A                                 18  6'Yield Curve:'
     A N84                                  DSPATR(ND)
     A            DDOYLC         5A  B 18 19TEXT('Yield Curve')
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(HI)
     A  70
     AON84                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N84                                  DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
     A**84 F of change CAS001
      *
     A**Start of change CAS005
     A**Start of change 225618
     A**********                       18 31'Liquidity/Premium:'                              225618
     A                                 18 31'Liquidity Premium:'
     A**End of change 225618
     A N85                                  DSPATR(ND)
     A            DDLQPR        12A  B 18 50
     A  48                                  DSPATR(RI PC)
     A N48                                  DSPATR(HI)
     A  70
     AON85                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N85                                  DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
     A            DDLQPS         1A  B 18 63
     A  49                                  DSPATR(RI PC)
     A N49                                  DSPATR(HI)
     A  70
     AON85                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A N85                                  DSPATR(ND)
     A                                      OVRATR
     A                                      OVRDTA
     A**End of change CAS005
     A*/COPY WNCPYSRC,MMH00070
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R MMNASPF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A            DDFOTX        40A    23  2OVRDTA
     A                                      DSPATR(HI)
     A            DDSTS         24     23 45OVRDTA
     A  79                                  DSPATR(RI)
     A N79                                  DSPATR(ND)
     A                                      OVRATR
      *
     A                                 24  2'F3=Exit F12=Cancel'
     A                                 24 22'F8=Next'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 31'F7=Prev'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 40'F14=Settlements'
     A N74                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 57'F15=Select'
     A N75                                  DSPATR(ND)
     A                                      OVRATR
