/*STDA*  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas MM Invalid NAs Purchased Browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MM Dealer Module                                     *
     A*                                                               *
     A*  MMNASPRDF - Invalid Negotiable Assets Purchased Browse       *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
     A*  Last Amend No. 166907             Date 27Dec99               *
      * Midas DBA 3.02 -----------------------------------------------*
     A*  Prev Amend No. CAP014             Date 08Dec99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CAP006             Date 08Mar99               *
     A*                 CAP004             Date 01Sep98               *
     A*                 CAP002  *CREATE    Date 11Mar98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  166907 - Show 'Deal No. 999999 successfully inserted'        *
     A*           message on screen.                                  *
     A*  CAP014 - Repair function enhancements                        *
     A*  CAP006 - Show error message on screen                        *
     A*  CAP004 - API's phase 3.                                      *
     A*           Enable delete of invalid transactions               *
     A*  CAP002 - Conversion of Midas inputs to modular API structure *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
     A          R MMINASPS1                 SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            DDOPT          1A  B  8  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDACTN    R        O  8  6REFFLD(ACTO)
     A                                      DSPATR(HI)
     A            DDFOTRANIDR     A  O  8  9REFFLD(FOTRNID)
     A                                      DSPATR(HI)
     A            DDMTYP    R        O  8 32REFFLD(DTPE)
     A            DDCCY     R        O  8 36REFFLD(CCY)
     A            DDAMNP    R     A  O  8 41REFFLD(AMNP)
     A                                      TEXT('AMOUNT')
      * Start CAP004
     A************DDCNUM    R        O  8 58REFFLD(CNUM)
     A************DDMDAT    R     A  O  8 68REFFLD(CHGD)
     A            DDCSNM    R        O  8 58REFFLD(CSNM)
     A            DDMDAT    R     A  O  8 69REFFLD(CHGD)
      * End CAP004
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
      * Start CAP004
     A            DDDLNO    R     A  H      REFFLD(DEAL)
     A            DDDLDT    R     A  H      REFFLD(CHGD)
     A            DDVDAT    R     A  H      REFFLD(CHGD)
     A            DDFVAL        15A  H
      * End CAP004
      * Start CAP014
     A            DDBRSN    R        H      REFFLD(BRSN)
      * End CAP014
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN CONTROL                                            *
      * -------------------------------------------------------------------*
      *
     A          R MMINASPS0                 SFLCTL(MMINASPS1)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
      * Start CAP004
     A********************************  1  2'MMINASPB'
     A                                  1  2'MMNASPRB'
      * End CAP004
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6Y 0O  1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3A  O  1 77TEXT('WORKSTATION ID')
     A                                  3 18'MM DEALER - Invalid NAs Purchased -
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A                                  4 37'ENQUIRY'
     A                                      DSPATR(HI)
     A                                  6  1'Sel'
     A                                  6  5'Act'
     A                                  6  9'Front Office ID'
     A                                  6 31'Deal'
     A                                  6 36'Ccy'
      * Start CAP004
     A********************************  6 41'Price'
     A                                  6 41'Purchase Price'
      * End CAP004
     A                                  6 57'Customer'
     A                                  6 67'Maturity'
      * Start CAP014
      * Start CAP004
     A**********************************7  1'A,D'
      * End CAP004
      * End CAP014
     A                                  7 31'Type'
     A                                  7 58'Number'
     A                                  7 69'Date'
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R MMINASPF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      * Start of CAP006
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      * End of CAP006
      *
      * -------------------------------------------------------------------*
      * * END MESSAGE                                                      *
      * -------------------------------------------------------------------*
      *
     A          R MMINASPEND                TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
      * Start of 166907
     A                                      PUTOVR
      * End of 166907
      * Start CAP004
     A********************************  1  2'MMINASPB'
     A                                  1  2'MMNASPRB'
      * End CAP004
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6Y 0O  1 63EDTWRD('  :  :  ')
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3A  O  1 77TEXT('WORKSTATION ID')
     A                                  3 24'MM DEALER - INVALID NAs PURCHASED -
     A                                      MAINTENANCE'
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
      * Start of 166907
     A            DDFOTX        40A    24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      * End of 166907
