/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas MM NAs sold browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MM Dealer Module                                     *
     A*                                                               *
     A*  MMNASSBDF - Negotiable Assets Sold Browse                    *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 29Nov05               *
      *                 CDL011             Date 07Jul02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CAP004             Date 02Sep98               *
     A*                 CAP002  *CREATE    Date 01Nov97               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  CDL011 - Expansion of Customer Number on Dealing Transactions*
     A*  CAP004 - API's phase 3.                                      *
     A*           Allow browse by Front Office Id.                    *
     A*  CAP002 - Conversion of MM inputs into modular structure to   *
     A*           use as APIs.                                        *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
 
     A          R MMNASSS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDDEAL    R     A  O  8  7REFFLD(DEAL)
     A                                      DSPATR(HI)
     A            DDDLTP    R        O  8 16REFFLD(DTPE)
     A            DDCCY     R        O  8 21REFFLD(CCY)
     A            DDAMNP    R     A  O  8 25REFFLD(AMNP)
     A            DDVDAT    R     A  O  8 41REFFLD(CHGD)
     A            DDDADN    R        O  8 48REFFLD(DADN)
      * Begin /CSD027/
     A**********  DDCNUM    R        O  8 57REFFLD(CNUM)                                      CSD027
     A            DDCNUM         6A  O  8 57TEXT('Customer number')
      * End /CSD027/
     A            DDSTAT    R        O  8 66REFFLD(DSTS)
     A            DDTEXT         7A     8 71TEXT('"DELETED" OR CP/BK STATUS')
     A  95                                  DSPATR(RI)
     A N95                                  DSPATR(ND)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R MMNASSS0                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
      * Start CAP004
     A                                      CF11
      * End CAP004
     A                                      SFLCTL(MMNASSS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
 
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'MMNASSB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 28'MM DEALER - N/A SOLD  INPUT & AUTH-
     A                                      ORISATION'
     A                                  4 37'ENQUIRY'
     A                                  6  2'Select'
     A                                  6  9'Deal'
     A                                  6 15'Deal'
     A                                  6 21'Ccy'
     A                                  6 30'Price'
     A                                  6 41'Value'
     A                                  6 48'Sale of'
     A                                  6 58'Cust'
     A                                  6 64'Status'
     A                                  7 10'No'
     A                                  7 15'Type'
     A                                  7 41'Date'
     A                                  7 48'Deal No.'
     A                                  7 57'Number'
 
      * Start CDL011
     A          R MMNASSS5                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDDEAL    R     A  O  8  7REFFLD(DEAL)
     A                                      DSPATR(HI)
     A            DDDLTP    R        O  8 16REFFLD(DTPE)
     A            DDCCY     R        O  8 21REFFLD(CCY)
     A            DDAMNP    R     A  O  8 25REFFLD(AMNP)
     A            DDVDAT    R     A  O  8 40REFFLD(CHGD)
     A            DDDADN    R        O  8 47REFFLD(DADN)
     A            DDCSNM        10A  O  8 55
     A            DDSTAT    R        O  8 66REFFLD(DSTS)
     A            DDTEXT         7A     8 71TEXT('"DELETED" OR CP/BK STATUS')
     A  95                                  DSPATR(RI)
     A N95                                  DSPATR(ND)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R MMNASSS4                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
      * Start CAP004
     A                                      CF11
      * End CAP004
     A                                      SFLCTL(MMNASSS5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
 
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'MMNASSB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 28'MM DEALER - N/A SOLD  INPUT & AUTH-
     A                                      ORISATION'
     A                                  4 37'ENQUIRY'
     A                                  6  2'Select'
     A                                  6  9'Deal'
     A                                  6 15'Deal'
     A                                  6 21'Ccy'
     A                                  6 30'Price'
     A                                  6 40'Value'
     A                                  6 47'Sale of'
     A                                  6 57'Customer'
     A                                  6 65'Status'
     A                                  7 10'No'
     A                                  7 15'Type'
     A                                  7 40'Date'
     A                                  7 46'Deal No.'
     A                                  7 58'Name'
      * End CDL011
      * Start CAP004
     A          R MMNASSS3                  TEXT('SELECT SCREEN SUBFILE')
     A                                      SFL
     A            DDOPT          1   B  8  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDFODN        20A     8  7TEXT('FRONT OFFICE ID.')
     A                                      DSPATR(HI)
     A            DDDEAL    R     A  O  8 28REFFLD(DEAL)
     A            DDDLTP    R        O  8 37REFFLD(DTPE)
     A            DDCCY     R        O  8 42REFFLD(CCY)
     A            DDAMNP    R     A  O  8 46REFFLD(AMNP)
     A            DDVDAT    R     A  O  8 62REFFLD(CHGD)
     A            DDDADN    R        O  8 69REFFLD(DADN)
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN CONTROL                                            *
     A* -------------------------------------------------------------------*
     A*
     A          R MMNASSS2                  TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      CF11
     A                                      SFLCTL(MMNASSS3)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
 
     A            DDSFRN         4  0H      SFLRCDNBR
     A                                  1  2'MMNASSB'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 28'MM DEALER - N/A SOLD  INPUT & AUTH-
     A                                      ORISATION'
     A                                  4 37'ENQUIRY'
     A                                  6  2'Select'
     A                                  6  9'Front Office'
     A                                  6 30'Deal'
     A                                  6 36'Deal'
     A                                  6 42'Ccy'
     A                                  6 51'Price'
     A                                  6 62'Value'
     A                                  6 69'Sale of'
     A                                  7  9'Identifier'
     A                                  7 31'No'
     A                                  7 36'Type'
     A                                  7 62'Date'
     A                                  7 69'Deal No.'
      * End CAP004
     A*
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A*
     A          R MMNASSF1                  TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
      * Start CAP004
     A**********                       24  2'F3=Exit F12=Cancel'
     A                                 24  2'F3=Exit F12=Cancel F11=Toggle +
     A                                      Front Office Id.'
      * End CAP004
