      *****************************************************************
/*STD *  DSPFBASE                                                    *
/*EXI *  TEXT('Midas MM NAs sold display')                            *
     A*****************************************************************
     A*                                                               *
     A*  Midas - MM Dealer Module                                     *
     A*                                                               *
     A*  MMNASSDDF - Negotiable Assets Sold Display function          *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*  Last Amend No. CDL038             Date 10May05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. CAP002  *CREATE    Date 01Nov97               *
     A*                                    Date ddmmmyy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CDL038 - Extended Deal Sub Type                              *
     A*  CAP002 - Conversion of MM inputs into modular structure to   *
     A*           use as APIs.                                        *
     A*                                                               *
     A*****************************************************************
 
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A*
     A* -------------------------------------------------------------------*
     A* * MESSAGE SUBFILE FORMAT                                           *
     A* -------------------------------------------------------------------*
     A*
     A          R MMNASSS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A*   MESSAGE SUBFILE CONTROL                                          *
     A* -------------------------------------------------------------------*
     A*
     A          R MMNASSS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A                                      SFLCTL(MMNASSS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A*   MAIN DISPLAY SCREEN                                              *
     A* -------------------------------------------------------------------*
     A*
     A          R MMNASSD1
 
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A                                      FRCDTA
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  1  2'MMNASSD'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6Y 0O  1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  1 72'WID:'
     A            DDWID          3A  O  1 77TEXT('WORKSTATION ID')
     A                                  3  2'Action:'
     A            DDACTN         1A  B  3 10TEXT('ACTION CODE')
     A  10                                  DSPATR(RI)
     A N10                                  DSPATR(HI)
     A  68
     AO 10N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDLIST        11A  O  3 12TEXT('VALID OPTIONS')
     A                                      OVRDTA
     A                                  3 24'MM DEALER - N/A SOLD INPUT & AUTHO-
     A                                      RISATION'
     A                                  4  2'Deal No:'
     A                                      DSPATR(HI)
     A            DDDLNO    R     A  B  4 11REFFLD(DEAL)
     A  11                                  DSPATR(RI)
     A N11                                  DSPATR(HI)
     A  11N69                               DSPATR(PC)
     A  69                                  DSPATR(PR)
     A N69                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  5  6'Type:'
     A            DDMTYP    R        B  5 12REFFLD(MDTY)
     A  12                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A N12                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  5 16'Sub-Type:'
     A            DDSTYP    R        B  5 26REFFLD(DLST)
     A  13                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A N13                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  5 32'Fed Funds:'
     A            DDFEDF         1A  B  5 43TEXT('Fed funds indicator')
     A  14                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A N14                                  DSPATR(HI)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A                                      OVRDTA
     A                                  5 48'Deal Date:'
     A            DDDLDT    R     A  B  5 59REFFLD(CHGD)
     A                                      TEXT('DEAL DATE')
     A  15                                  DSPATR(RI)
     A N15                                  DSPATR(HI)
     A  15                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6  6'Broker:'
     A            DDBKCD    R        B  6 14REFFLD(BKCD)
     A                                      TEXT('BROKER CODE')
     A  16                                  DSPATR(RI)
     A N16                                  DSPATR(HI)
     A  16                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6 21'Brokerage:'
     A            DDBRKG    R     A  B  6 32REFFLD(AMNP)
     A                                      TEXT('BROKERAGE')
     A  17                                  DSPATR(RI)
     A N17                                  DSPATR(HI)
     A  17                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  6 49'Sale of Deal No:'
     A            DDDADN    R        B  6 66REFFLD(DADN)
     A                                      TEXT('DEAL ASSOCIATED DEAL NO')
     A  18                                  DSPATR(RI)
     A N18                                  DSPATR(HI)
     A  18                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7  6'Issued By:'
     A            DDISUR    R        B  7 17REFFLD(CSNM)
     A                                      TEXT('NAB ISSUER')
     A  19                                  DSPATR(RI)
     A N19                                  DSPATR(HI)
     A  19                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  7 31'Sold to:'
     A            DDCSNM    R        B  7 44REFFLD(CSNM)
     A                                      TEXT('CUSTOMER SHORT NAME')
     A  20                                  DSPATR(RI)
     A N20                                  DSPATR(HI)
     A  20                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8  6'Value Date:'
     A            DDVDAT    R     A  B  8 18REFFLD(CHGD)
     A                                      TEXT('VALUE DATE')
     A  21                                  DSPATR(RI)
     A N21                                  DSPATR(HI)
     A  21                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9  6'Currency:'
     A            DDCCY     R        B  9 16REFFLD(CCY)
     A                                      TEXT('CURRENCY')
     A  22                                  DSPATR(RI)
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 21'Face Value:'
     A            DDFVAL    R     A  B  9 33REFFLD(FAVL)
     A                                      TEXT('NAB FACE VALUE')
     A  23                                  DSPATR(RI)
     A N23                                  DSPATR(HI)
     A  23                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 51'Sale Price:'
     A            DDAMNP    R     A  B  9 63REFFLD(AMNP)
     A                                      TEXT('SALE PRICE')
     A  24                                  DSPATR(RI)
     A N24                                  DSPATR(HI)
     A  24                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10  6'Yield rate:'
     A            DDINTR        12A  B 10 18TEXT('INTEREST/RATE')
     A  25                                  DSPATR(RI)
     A N25                                  DSPATR(HI)
     A  25                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10 32'Book Code:'
     A            DDBOKC    R     A  B 10 43REFFLD(BOKC)
     A                                      TEXT('BOOK CODE')
     A  26                                  DSPATR(RI)
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 10 47'Linked Ref. No.:'
     A            DDLNKN    R     A  B 10 64REFFLD(LNKDN)
     A                                      TEXT('LINKED REF. NO.')
     A  27                                  DSPATR(RI)
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(PC)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                 11  6'Profit centre:'
     A  82                                  DSPATR(ND)
     A            DDPRFC    R    4A  B 11 22REFFLD(PRFC)
     A  28                                  DSPATR(RI)
     A  28                                  DSPATR(PC)
     A N28                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  82                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 11 32'Orig. Branch:'
     A  81                                  DSPATR(ND)
     A            DDORBR    R    3A  B 11 46REFFLD(ORBR)
     A  29                                  DSPATR(RI)
     A  29                                  DSPATR(PC)
     A N29                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  81                                  DSPATR(ND)
     A                                      OVRDTA
     A                                 11 55'Parcel Size:'
     A            DDPSZE    R     A  B 11 68REFFLD(PSIZ)
     A                                      TEXT('PARCEL SIZE')
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A N30                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
      *                                              Begin /CDL038/
     A                                 12  6'Class:'
     A  90                                  DSPATR(ND)
     A            DDCLAS         4A  B 12 13
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A  70
     AO 90                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  90                                  DSPATR(ND)
     A                                      OVRDTA
      *                                              End   /CDL038/
     A*/COPY WNCPYSRC,MMH00071
     A*
     A* -------------------------------------------------------------------*
     A* * MAIN SCREEN FOOTER                                               *
     A* -------------------------------------------------------------------*
     A*
     A          R MMNASSF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A  71                                  CF07
     A  72                                  CF08
     A  73                                  CF10
     A  74                                  CF14
     A  75                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A*
     A            DDFOTX        40A    23  2OVRDTA
     A                                      DSPATR(HI)
     A            DDSTS         24     23 45OVRDTA
     A  79                                  DSPATR(RI)
     A N79                                  DSPATR(ND)
     A                                      OVRATR
     A*
     A                                 24  2'F3=Exit F12=Cancel'
     A                                 24 22'F8=Next'
     A N72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 31'F7=Prev'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 40'F14=Settlements'
     A N74                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 57'F15=Select'
     A N75                                  DSPATR(ND)
     A                                      OVRATR
