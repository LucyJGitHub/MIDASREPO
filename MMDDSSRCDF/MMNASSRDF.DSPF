/*STDA*  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas MM Invalid NAs Sold Browse')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MM Dealer Module                                     *
     A*                                                               *
     A*  MMNASSBDF - Invalid Negotiable Assets Sold Browse            *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *  Prev Amend No. 166907             Date 27Dec99               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP014             Date 08Dec99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CAP006             Date 08Mar99               *
     A*                 CAP004             Date 01Sep98               *
     A*                 CAP002  *CREATE    Date 24Feb98               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  166907 - Show 'Deal No. 999999 successfully inserted'        *
     A*           message on screen.                                  *
     A*  CAP014 - Repair function enhancements                        *
     A*  CAP006 - Show error message on screen                        *
     A*  CAP004 - API's phase 3.                                      *
     A*           Enable delete of invalid transactions               *
     A*  CAP002 - Conversion of MM inputs into modular structure to   *
     A*           use as APIs.                                        *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      INDARA
     A          R MMINASSS1                 SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            D@TMESTMP R   26A  H      REFFLD(TIMESTMP)
      * Start CAP004
     A            DDDLNO    R     A  H      REFFLD(DEAL)
     A            DDDLDT    R     A  H      REFFLD(CHGD)
     A            DDFVAL        15A  H
      * End CAP004
     A            DDOPT          1A  B  8  2TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDACTN    R        O  8  5REFFLD(ACTO)
     A                                      DSPATR(HI)
     A            DDFOTRANIDR     A  O  8  8REFFLD(FOTRNID)
     A                                      DSPATR(HI)
     A            DDMTYP    R        O  8 30REFFLD(DTPE)
     A            DDCCY     R        O  8 34REFFLD(CCY)
     A            DDDADN    R        O  8 39REFFLD(DADN)
     A            DDAMNP    R     A  O  8 47REFFLD(AMNP)
     A            DDVDAT    R     A  O  8 64REFFLD(CHGD)
     A                                      TEXT('VALUE DATE')
     A            DDCSNM    R        O  8 71REFFLD(CSNM)
      * Start CAP014
     A            DDBRSN    R        H      REFFLD(BRSN)
      * End CAP014
 
     A          R MMINASSS0                 SFLCTL(MMINASSS1)
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A                                      TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
      * Start CAP004
     A********************************  1  2'MMINASSB'
     A                                  1  2'MMNASSRB'
      * End CAP004
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6Y 0O  1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3A  O  1 77TEXT('WORKSTATION ID')
     A                                  3 24'MM DEALER - INVALID NAs SOLD MAINT-
     A                                      ENANCE'
     A                                  4 37'ENQUIRY'
     A                                  6  1'Sel'
     A                                  6  5'Act'
     A                                  6  9'Front Office ID'
     A                                  6 29'Deal'
     A                                  6 34'Ccy'
     A                                  6 39'Sale'
      * Start CAP004
     A********************************  6 47'Amount'
     A                                  6 49'Sale Price'
      * End CAP004
     A                                  6 64'Value'
     A                                  6 71'Customer'
      * Start CAP014
      * Start CAP004
     A**********************************7  1'A,D'
      * End CAP004
      * End CAP014
     A                                  7 29'Type'
     A                                  7 39'of Deal'
     A                                  7 64'Date'
 
     A* -------------------------------------------------------------------*
     A* * SELECT SCREEN FOOTER                                             *
     A* -------------------------------------------------------------------*
     A          R MMINASSF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      * Start of CAP006
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      * End of CAP006
     A*
     A* -------------------------------------------------------------------*
     A* * END MESSAGE                                                      *
     A* -------------------------------------------------------------------*
     A*
     A          R MMINASSEND                TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
      * Start of 166907
     A                                      PUTOVR
      * End of 166907
      * Start CAP004
     A********************************  1  2'MMINASSB'
     A                                  1  2'MMNASSRB'
      * End CAP004
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6Y 0O  1 63EDTWRD('  :  :  ')
     A                                      TEXT('TIME')
     A                                  1 72'WID:'
     A            DDWID          3A  O  1 77TEXT('WORKSTATION ID')
     A                                  3 24'MM DEALER - INVALID NAs SOLD MAINT-
     A                                      ENANCE'
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this function'
      * Start of 166907
     A            DDFOTX        40A    24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      * End of 166907
