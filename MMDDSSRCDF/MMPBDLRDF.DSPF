      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM Deal Range Auth. By Front Office ID')
      *****************************************************************
      *                                                               *
      *  Midas - MM Dealer Module                                     *
      *                                                               *
      *  MMPBDLRDF  - MM DEAL RANGE AUTHORIZATION                     *
      *               By Front Office ID                              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CAP052  *CREATE    Date 24Nov99               *
      *                                    Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP052 - Deal Range Authorization (By Front Office ID)       *
      *                                                               *
      *****************************************************************
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MMPBDLS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MMPBDLS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      SFLCTL(MMPBDLS1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R MMPBDLD1                  TEXT('MAIN DISPLAY SCREEN')
     A                                      CF03
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                  1  2'Ref: MMPBDL'
     A                                  1 26'            Midas            '
     A                                      DSPATR(HI)
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                      TEXT('CURRENT TIME')
     A                                  1 72'WID:'
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  3 25'MM DEALER - RANGE AUTHORIZATIONS'
      *
     A                                  7 16'First Front Office Id. in Range:'
     A  13                                  DSPATR(ND)
     A            DDFDLN    R     A  B  7 49REFFLD(FOTRNID)
     A                                      OVRDTA
     A  10                                  DSPATR(RI)
     A  10
     AON11N13                               DSPATR(PC)
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(PR)
     A                                  7 21'Last Deal in Range:'
     A  13                                  DSPATR(ND)
     A                                  9 16'Last Front Office Id. in Range:'
     A  13                                  DSPATR(ND)
     A            DDLDLN    R     A  B  9 49REFFLD(FOTRNID)
     A                                      OVRDTA
     A  11                                  DSPATR(RI PC)
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(PR)
      *
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R MMPBDLF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                 24  1'F3-End'
