/*STD *  DSPFDFRWN                                                   *
/*EXI *  TEXT('Midas MM Invalid REPO transaction details - browse')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  MMREPORDF - Invalid REPO Transactions Details Browse         *
      *              ("REPAIR FUNCTION")                              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CAP067  *CREATE    Date 24Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAP067 - Repurchase Aggrements API.                          *
      *                                                               *
      *****************************************************************
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CHGINPDFT
     A                                      REF(*LIBL/MFRFL)
     A                                      INDARA
     A          R MMIREPOS1                 SFL
     A                                      TEXT('SELECT SCREEN SUBFILE')
     A            DDOPT          1A  B  7  3TEXT('SELECT OPTION')
     A                                      DSPATR(UL)
     A            DDACTN         1   O  7  8DSPATR(HI)
     A            DDFOTRANID    20   O  7 11DSPATR(HI)
     A            DDTYPE    R        O  7 33REFFLD(DTPE)
     A            DDCCY     R        O  7 37REFFLD(CCY)
     A            DDAMNP    R        O  7 41REFFLD(AM14)
     A            DDVDAT    R     A  O  7 56REFFLD(CHGD)
     A                                      TEXT('VALUE DATE')
     A            DDMDAT    R     A  O  7 63REFFLD(CHGD)
     A                                      TEXT('MATURITY DATE')
     A            DDCUST    R        O  7 70REFFLD(CSNM)
     A            DDFOASOCIDR        H      REFFLD(FOTRNID)
     A            D_TMESTMP R        H      REFFLD(TIMESTMP)
     A            DDDLNO    R     A  H      REFFLD(DEAL)
     A            DDDLDT    R     A  H      REFFLD(CHGD)
     A            DDSPRT        12A  H
     A            DDBRSN    R        H      REFFLD(BRSN)
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN CONTROL                                            *
      * -------------------------------------------------------------------*
      *
     A          R MMIREPOS0                 TEXT('SELECT SCREEN CONTROL')
     A                                      CF03
     A                                      CF12
     A                                      SFLCTL(MMIREPOS1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(15)
     A                                      SFLPAG(14)
     A N96                                  ROLLUP(98)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
     A            DDSFRN         4S 0H      SFLRCDNBR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'MMREPORPB'
     A                                  1 24'Invalid REPO Transa+
     A                                      ctions Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI UL)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                  4  2'Sel'
     A                                  4  7'Act'
     A                                  4 11'Front Office Id.'
     A                                  4 32'Type'
     A                                  4 37'CCY'
     A                                  4 41'Amount'
     A                                  4 56'Value'
     A                                  4 63'Maturity'
     A                                  4 72'Customer'
     A                                  5 56'Date'
     A                                  5 63'Date'
      *
      * -------------------------------------------------------------------*
      * * SELECT SCREEN FOOTER                                             *
      * -------------------------------------------------------------------*
      *
     A          R MMIREPOF1                 TEXT('SELECT SCREEN FOOTER')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit F12=Cancel'
      *
     A            ERRORMSG      56     24 24
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      * * END MESSAGE                                                      *
      * -------------------------------------------------------------------*
      *
     A          R MMIREPOEND                TEXT('END MESSAGE')
     A                                      CF03
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDUSER        10A  O  1  2OVRDTA
     A                                  1 13'MMREPORPB'
     A                                  1 24'Invalid REPO Transa+
     A                                      ctions Maintenance'
     A                                      DSPATR(HI UL)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73OVRDTA
     A            DDWID         10A  O  2  2TEXT('WORKSTATION ID')
     A                                      OVRDTA
     A                                  2 36'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                      EDTWRD('  :  :  ')
     A                                 11 26'NO INVALID TRANSACTIONS +
     A                                      PRESENT'
     A                                      DSPATR(HI)
     A                                 23  2'Press enter to end this Function'
