      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM Securities delivered input')
      *****************************************************************
      *                                                               *
      *  Midas - Securities Trading Module                            *
      *                                                               *
      *  DSPF/MMRPDLDF - REPO DELIVERY DETAILS INPUT                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 29Nov05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP067             Date 24Sep01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 190780             Date 08Mar01               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE006  *CREATE    Date 13May99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP067 - Repurchase Agreements API.                          *
      *  190780 - Possible to delete help text within display causing *
      *           database error.                                     *
      *  CSE006 - Repurchase Agreements Enhancements                  *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A** Start of fix 190780
     A**********                            CSRINPONLY
     A** End of fix 190780
      * -------------------------------------------------------------------*
      * * MAIN SUBFILE FORMAT                                              *
      * -------------------------------------------------------------------*
     A*
     A          R MMRPDLS1                  SFL
     A**Start of change CGL029
     A**********  DDACTN         1A  B 12  2DSPATR(HI)                                        CGL029
     A            DDACTN         1A  B 13  2DSPATR(HI)
     A**End of change CGL029
     A  76                                  DSPATR(PR)
     A N50N76                               DSPATR(UL)
     A  50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A**Start of change CGL029
     A**********  DDSECT        10A  B 12  7DSPATR(HI)                                        CGL029
     A            DDSECT        10A  B 13  7DSPATR(HI)
     A**End of change CGL029
     A  75
     AO 74
     AO 73
     AO 70
     AO 71                                  DSPATR(PR)
     A N73N75
     AAN70N71N51                            DSPATR(UL)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A                                      CHANGE(40)
     A**Start of change CGL029
     A**********  DDNOML        12A  B 12 18DSPATR(HI)                                        CGL029
     A            DDNOML        12A  B 13 18DSPATR(HI)
     A**End of change CGL029
     A  74
     AO 70
     AO 73
     AO 71                                  DSPATR(PR)
     A N73
     AAN70N71N52                            DSPATR(UL)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A                                      CHANGE(41)
     A**Start of change CGL029
     A**********  DDVALP        16A  B 12 31DSPATR(HI)                                        CGL029
     A            DDVALP        16A  B 13 31DSPATR(HI)
     A**End of change CGL029
     A  74
     AO 70
     AO 73
     AO 71                                  DSPATR(PR)
     A N73
     AAN70N71N53                            DSPATR(UL)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                      CHANGE(42)
     A**Start of change CGL029
     A**********  DDVDIN        16A  O 12 48DSPATR(HI)                                        CGL029
     A**********  DDVDVL        16A  O 12 65DSPATR(HI)                                        CGL029
     A**********  DDORDP        10A  B 13  7DSPATR(HI)                                        CGL029
     A            DDVDIN        16A  O 13 48DSPATR(HI)
     A            DDVDVL        16A  O 13 65DSPATR(HI)
     A            DDORDP        10A  B 14  7DSPATR(HI)
     A**End of change CGL029
     A  70
     AO 74
     AO 73
     AO 71                                  DSPATR(PR)
     A N73
     AAN70N71N58                            DSPATR(UL)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(RI)
     A                                      CHANGE(43)
     A**Start of change CGL029
     A**********  DDCPDP        10A  B 13 18DSPATR(HI)                                        CGL029
     A            DDCPDP        10A  B 14 18DSPATR(HI)
     A**End of change CGL029
     A  74
     AO 73
     AO 71                                  DSPATR(PR)
     A N71N59N73                            DSPATR(UL)
     A  59                                  DSPATR(PC)
     A  59                                  DSPATR(RI)
     A                                      CHANGE(44)
     A**Start of change CGL029
     A**********  DDEXCR        15A  B 13 31DSPATR(HI)                                        CGL029
     A            DDEXCR        15A  B 14 31DSPATR(HI)
     A**End of change CGL029
        74
     AO 70
     AO 73
     AO 71                                  DSPATR(PR)
     A N73
     AAN70N71N60                            DSPATR(UL)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A                                      CHANGE(45)
     A**Start of change CGL029
     A**********  DDMDIN        16A  O 13 48DSPATR(HI)                                        CGL029
     A**********  DDMDVL        16A  O 13 65DSPATR(HI)                                        CGL029
     A**********  DDOUTP        10A  O 14  7DSPATR(HI)                                        CGL029
     A            DDMDIN        16A  O 14 48DSPATR(HI)
     A            DDMDVL        16A  O 14 65DSPATR(HI)
     A            DDOUTP        10A  O 15  7DSPATR(HI)
     A**End of change CGL029
     A            D@UPDT         1A  H
     A            D@DPRN         6A  H
     A            D@SECT        10A  H
     A            D@NOML        11  0H
     A            D@VALP        15  8H
     A            D@VDIN        15  0H
     A            D@VDVL        15  0H
     A            D@MDIN        15  0H
     A            D@MDVL        15  0H
      * Begin /CSD027/
     A**********  D@ORDP         6  0H                                                        CSD027
     A**********  D@CPDP         6  0H                                                        CSD027
     A            D@ORDP         6A  H
     A            D@CPDP         6A  H
      * End /CSD027/
     A            D@EXCR        13  8H
     A            D@EXT1         1A  H
     A            D@CHG          1A  H
     A            D@NWRC         1A  H
     A            D@IN51         1A  H
     A            D@IN70         1A  H
      **Start of change CAP067
     A            D@FOAS        20A  H
     A            D@FOID        20A  H
      **End of change CAP067
     A*=========================================================================
     A          R MMRPDLC1                  SFLCTL(MMRPDLS1)
     A                                      CF03
     A  19                                  CF09
     A  20                                  CF10
     A                                      CF12
      **Start of change CAP067
     A  18                                  CF22
      **End of change CAP067
     A                                      OVERLAY
     A                                      RTNDTA
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0003)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLDLT
     A  03                                  SFLEND
     A                                  1  2'MIDAS'
      **Start of change CAP067
     A**********                        1 33'REPOS MAINTENANCE'                               CAP067
     A**********                            DSPATR(HI UL)                                     CAP067
     A N78                              1 33'REPOS MAINTENANCE'
     A N78                                  DSPATR(HI UL)
     A  78                              1 35'REPOS REPAIR'
     A  78                                  DSPATR(HI UL)
      **End of change CAP067
     A                                  1 67'DATE '
     A            BJMRDT         7A  O  1 73
      **Start of change CAP067
     A**********                        2  2'MMREPOSFL'                                       CAP067
     A N78                              2  2'MMREPOSFL'
     A  78                              2  2'MMRPDMRPR'
      **End of change CAP067
     A                                  2 27'Securities Delivered/Received'
     A                                      DSPATR(HI UL)
     A                                  2 73TIME
     A                                  3  2'Deal Number'
     A            DDDLNO         6A  O  3 14TEXT('DEAL NUMBER')
     A                                      DSPATR(HI)
     A                                  3 38'Type'
     A            D@TYPE        16A  O  3 43TEXT('TYPE')
     A                                      DSPATR(HI)
     A                                  4  2'Proceeds'
     A            DDAMNP        14A  O  4 11TEXT('PROCEEDS')
     A                                      DSPATR(HI)
     A            DDCCY          3A  O  4 32TEXT('CURRENCY')
     A                                      DSPATR(HI)
     A                                  4 38'Counterparty'
     A            D@CUST         6A  O  4 51TEXT('CUSTOMER NUMBER')
     A                                      DSPATR(HI)
     A            D@CRNM        20A  O  4 58TEXT('CUSTOMER NAME')
     A                                      DSPATR(HI)
     A                                  5  2'Value Date'
     A            DDVDAT         6A  O  5 13TEXT('VALUE DATE')
     A                                      DSPATR(HI)
     A                                  5 28'Maturity Date'
     A            DDMDAT         6A  O  5 42TEXT('MATURITY DATE')
     A                                      DSPATR(HI)
     A                                  5 54'Repo Rate'
     A            DDSPRT        12A  O  5 64TEXT('REPO RATE')
     A                                      DSPATR(HI)
     A                                  6  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A N77                              7  2'Control Nominal'
     A            DDCNTN        15A  B  7 19TEXT('CONTROL NOMINAL')
     A                                      DSPATR(HI)
     A  77
     AO 70
     AO 71                                  DSPATR(PR)
     A N77
     AAN61N70N71                            DSPATR(UL)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  77                                  DSPATR(ND)
     A                                  7 36'Narrative'
     A            DDNARR        35A  B  7 46TEXT('NARRATIVE')
     A                                      DSPATR(HI)
     A  73
     AO 71                                  DSPATR(PR)
     A N64N71N73                            DSPATR(UL)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A                                  8  2'Total Nominal'
     A            DDTOTN        16A  O  8 18TEXT('TOTAL NOMINAL')
     A                                      DSPATR(HI)
      **Start of change CAP067
     A  62                                  DSPATR(RI)
      **End of change CAP067
     A                                  8 36'Value'
     A            DDTOTV        16A  O  8 42TEXT('VALUE')
     A                                      DSPATR(HI)
     A  63                                  DSPATR(RI)
     A N72                              9  2'Fee: Ccy'
     A            DDFECY         3A  B  9 11TEXT('FEE CURRENCY')
     A                                      DSPATR(HI)
     A  72
     AO 73
     AO 71                                  DSPATR(PR)
     A  72                                  DSPATR(ND)
     A N73
     AAN71N66N72                            DSPATR(UL)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A N72                              9 15'Settlement Method'
     A            DDFESM         2A  B  9 33TEXT('SETTLEMENT METHOD')
     A                                      DSPATR(HI)
     A  72
     AO 73
     AO 71                                  DSPATR(PR)
     A  72                                  DSPATR(ND)
     A N73
     AAN71N67N72                            DSPATR(UL)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A N72                              9 36'Account'
     A**Start of change CGL029
     A**********  DDACCA        12A  B  9 44TEXT('ACCOUNT')                                   CGL029
     A            DDACCA        18A  B  9 44TEXT('ACCOUNT')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A  73
     AO 72
     AO 71                                  DSPATR(PR)
     A  72                                  DSPATR(ND)
     A N73
     AAN71N68N72                            DSPATR(UL)
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A**Start of change CGL029
     A**********  DDACCB         3A  B  9 57TEXT('ACCOUNT')                                   CGL029
     A            DDACCB         3A  B  9 63TEXT('ACCOUNT')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A  73
     AO 72
     AO 71                                  DSPATR(PR)
     A  72                                  DSPATR(ND)
     A N73
     AAN71N68N72                            DSPATR(UL)
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A**Start of change CGL029
     A*N72******                        9 61'Amnt'                                            CGL029
     A**********  DDFEAM        14A  B  9 67TEXT('FEE AMOUNT')                                CGL029
     A N72                             10  7'Amnt'
     A            DDFEAM        14A  B 10 12TEXT('FEE AMOUNT')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A  73
     AO 72
     AO 71                                  DSPATR(PR)
     A  72                                  DSPATR(ND)
     A N73
     AAN71N65N72                            DSPATR(UL)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A**Start of change CGL029
     A*N76******                       10  2'Actn'                                            CGL029
     A**********                       10  7'Security/'                                       CGL029
     A**********                       10 18'Nominal/'                                        CGL029
     A**********                       10 36'Value Price'                                     CGL029
     A**********                       10 48'Interest/'                                       CGL029
     A**********                       10 66'Value/'                                          CGL029
     A**********                       11  7'Our Depot'                                       CGL029
     A**********                       11 18'C''Pty Depot'                                    CGL029
     A**********                       11 31'Exchange Rate'                                   CGL029
     A**********                       11 48'Maturity Interest'                               CGL029
     A**********                       11 66'Value'                                           CGL029
     A N76                             11  2'Actn'
     A                                 11  7'Security/'
     A                                 11 18'Nominal/'
     A                                 11 36'Value Price'
     A                                 11 48'Interest/'
     A                                 11 66'Value/'
     A                                 12  7'Our Depot'
     A                                 12 18'C''Pty Depot'
     A                                 12 31'Exchange Rate'
     A                                 12 48'Maturity Interest'
     A                                 12 66'Value'
     A**End of change CGL029
     A            PRRN           4  0H      SFLRCDNBR(CURSOR)
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MMRPDLM1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MMRPDLM0                  TEXT('MSG SUBFILE CONTROL')
     A                                      CF03
     A                                      SFLCTL(MMRPDLM1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A** Start of fix 190780
     A                                      CSRINPONLY
     A** End of fix 190780
     A            DDPGMQ                    SFLPGMQ
     A                                 23  2'F3 Exit'
     A                                 23 11'F12 Cancel'
     A  19                             23 23'F9 Add'
      **Start of change CAP067
     A**********                       23 31'Action Code:C,M,D'                               CAP067
     A N78                             23 31'Action Code:C,M,D'
     A  78                             23 31'Action Code: D'
      **End of change CAP067
     A                                 23 50'Rollup/down'
     A  20                             23 63'F10 Delete'
     A*                                            /*Start CAP067*/
     A  20                                  DSPATR(RI)
     A*                                            /* End  CAP067*/
     A            DDERR        076A  O 23  2
