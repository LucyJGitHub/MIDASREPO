     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MM Repo margin call display')
     A*****************************************************************
     A*                                                               *
     A*  Midas - MM Dealer Module                                     *
     A*                                                               *
     A*  MMRPMCDF - REPO MARGIN CALLS MAINTENANCE                     *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
     A*  Last Amend No. CSE006   *CREATE   Date 23Jul99               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  CSE006 - Repurchase Agreements                               *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A* -------------------------------------------------------------------*
     A* * MESSAGE SUBFILE FORMAT                                           *
     A* -------------------------------------------------------------------*
     A*
     A          R MMRPMCS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A*   MESSAGE SUBFILE CONTROL                                          *
     A* -------------------------------------------------------------------*
     A*
     A          R MMRPMCS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A  70                                  CF12
     A  71                                  CF15
     A                                      SFLCTL(MMRPMCS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
     A*
     A* -------------------------------------------------------------------*
     A*   MAIN DISPLAY SCREEN                                              *
     A* -------------------------------------------------------------------*
     A*
     A          R MMRPMCD1
     A                                      CF03
     A  70                                  CF12
     A  71                                  CF15
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                  1  2'MIDAS'
     A N41                              1 23'REPOS MARGIN CALLS MAINTENANCE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  41                              1 16'REPOS & BOND LENDING MARGIN CALLS -
     A                                      MAINTENANCE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2  2'MMRPMCDF'
     A            DDTIME         6  0   1 63EDTWRD('  :  :  ')
     A                                      TEXT('CURRENT TIME')
     A                                  1 72'WID:'
     A  41                              2 32'CASH MARGIN'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            DDWID          3      1 77TEXT('WORKSTATION ID')
     A                                  2 67'DATE '
     A            BJMRDT         7   O  2 73
     A                                  5  2'Action Code'
     A            DDACTN         1   B  5 14TEXT('ACTION CODE')
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(HI)
     A  30                                  DSPATR(PC)
     A N30N41                               DSPATR(UL)
     A  41                                  DSPATR(PR)
     A                                  5 20'Deal No.'
     A            DDDLNO         6   B  5 29TEXT('DEAL NUMBER')
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A N31N40N41                            DSPATR(UL)
     A  41
     AO 40                                  DSPATR(PR)
     A                                  5 40'Value Date'
     A            DDVDAT         6   B  5 51TEXT('Value Date')
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(HI)
     A  32                                  DSPATR(PC)
     A N32N40N41                            DSPATR(UL)
     A  41
     AO 40                                  DSPATR(PR)
     A N41                              5 63'Cash/Securities'
     A N41        DDCASE         1   B  5 79TEXT('CASH/SECURITIES')
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(HI)
     A  33                                  DSPATR(PC)
     A N33N40N41                            DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  41                              5 62'Type'
     A  41        DDRTYP        14   O  5 67TEXT('TYPE')
     A  41                              9  6'Type'
     A  41        DDTYPE         2   B  9 13TEXT('TYPE')
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(HI)
     A  34                                  DSPATR(PC)
     A N34N40N42                            DSPATR(UL)
     A  42
     AO 40                                  DSPATR(PR)
     A  41                             10  6'Amount'
     A  41        DDAMNT        15   B 10 13TEXT('AMOUNT')
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(HI)
     A  35                                  DSPATR(PC)
     A N35N40N42                            DSPATR(UL)
     A  42
     AO 40                                  DSPATR(PR)
     A*
     A* -------------------------------------------------------------------*
     A* * MAIN SCREEN FOOTER                                               *
     A* -------------------------------------------------------------------*
     A*
     A          R MMRPMCF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A  70                                  CF12
     A  71                                  CF15
     A                                      OVERLAY
     A                                      BLINK
     A*
     A            DDFOTX        40A    23  2
     A                                      DSPATR(HI)
     A*
     A                                 24  2'F3 Exit'
     A  70
     AO 41                             24 57'F12 Cancel'
     A N41                             24 57'F15 Cash Select Screen'
