      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas MM Cash Flows Using Net-Treasury-Rate File')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Dealing Module                          *
      *                                                               *
      ***PF/MMCFLWPD*-*MM*Cash*Flows*File******************************                       CAS005
      *  PF/MMCFLWPD - MM Cash Flows Using Net-Treasury-Rate File     *                       CAS005
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 05Dec05               *
      *  Prev Amend No. CAS009             Date 04May04               *
      *                 CDL038             Date 10May05               *
      *                 CAS005  *MOVED     Date 16Dec02               *
      *                 212416             Date 26Sep02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004.  Moved   *
      *           from DP to DM library.                              *
      *  212416 - Declaration of MCNDYP is incorrect                  *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
     A          R MMCFLWD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            MCDLNO         6S 0       TEXT('Deal Number')
     A                                      COLHDG('Deal' +
     A                                             'Number')
     A            MCDTYP         2          TEXT('Deal Type')
     A                                      COLHDG('Deal' +
     A                                             'Type')
     A            MCDLST         2          TEXT('Deal Subtype')
     A                                      COLHDG('Deal' +
     A                                             'Subtype')
     A            MCFLDT         5P 0       TEXT('Cash Flow Date(Midas Day No)')
     A                                      COLHDG('Cash Flow Date' +
     A                                             '(Midas Day No)')
     A            MCDATE         7          TEXT('Cash Flow Date Output')
     A                                      COLHDG('Cash Flow'  +
     A                                             'Date Output')
      **Start of change 212416
     A**********  MCNDYP         3P 0       TEXT('No. of Days in Period')                     212416
     A            MCNDYP         5P 0       TEXT('No. of Days in Period')
      **End of change 212416
     A                                      COLHDG('No. of Days' +
     A                                             'in Period')
     A            MCNDYY         3P 0       TEXT('No. of Days in Interest Year')
     A                                      COLHDG('No. of Days' +
     A                                             'in Interest Year')
     A            MCCAMT        15P 0       TEXT('Cash Flow Amount')
     A                                      COLHDG('Cash Flow' +
     A                                             'Amount')
     A            MCFTYP         4          TEXT('Cash Flow Type')
     A                                      COLHDG('Cash Flow' +
     A                                             'Type')
     A            MCIRAT        13P 8       TEXT('Interest Rate')
     A                                      COLHDG('Interest' +
     A                                             'Rate')
     A            MCYLDC         5          TEXT('Yield Curve')
     A                                      COLHDG('Yield' +
     A                                             'Curve')
     A            MCDSCF        13P 9       TEXT('Discount Factor')
     A                                      COLHDG('Discount' +
     A                                             'Factor')
     A            MCNPVA        15P 0       TEXT('NPV Amount')
     A                                      COLHDG('NPV' +
     A                                             'Amount')
     A            MCIOIN         1          TEXT('Incoming/Outgoing Ind.')
     A                                      COLHDG('Incoming/' +
     A                                             'Outgoing Ind.')
      * Begin /CSD027/
     A**********  MCCNUM         6S 0       TEXT('Customer Number')                           CSD027
     A            MCCNUM         6A         TEXT('Customer Number')
      * End /CSD027/
     A                                      COLHDG('Customer' +
     A                                             'Number')
     A            MCBRCA         3          TEXT('Branch Code')
     A                                      COLHDG('Branch' +
     A                                             'Code')
     A            MCCYCD         3          TEXT('Currency Code')
     A                                      COLHDG('Currency' +
     A                                             'Code')
     A            MCBKCD         2          TEXT('Book Code')
     A                                      COLHDG('Book' +
     A                                             'Code')
     A            MCADLN         6S 0       TEXT('Associated Deal No.')
     A                                      COLHDG('Associated' +
     A                                             'Deal No.')
     A            MCICBS         1S 0       TEXT('Int. Calc. Basis')
     A                                      COLHDG('Int. Calc.' +
     A                                             'Basis')
     A*                                              Begin /CDL038/
     A            MCCLAS         4A         TEXT('Deal Class')
     A                                      COLHDG('Deal' 'Class')
     A*                                              End   /CDL038/
      **Start of change CAS009
     A            MCFSEQ         2S 0       TEXT('Fee Sequence')
     A                                      COLHDG('Fee Sequence')
      **End of change CAS009
