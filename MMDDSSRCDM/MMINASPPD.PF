      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas MM Invalid NAs Purchased')                       *
      *****************************************************************
      *                                                               *
      *  Midas - MM ILE Module                                        *
      *                                                               *
      *  PF/MMINASPPD - Invalid NAs Purchased                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL049             Date 31Mar06               *
      *  Prev Amend No. CAP086             Date 08Jun05               *
      *                 CDL038             Date 10May05               *
      *                 CAS005             Date 16Dec02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP002  *CREATE    Date 01Dec97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL049 - Addition of a Reference Rate field                  *
      *  CAP086 - Introduce Auto Authorisation to the API's           *
      *           without it.                                         *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *  CAP002 - Conversion of MM inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R MMINASPD0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            FRONTOFFIDR               REFFLD(FOTRNID)
     A            ASSOCFOID R               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            REPAIRLOC R               REFFLD(RPRLOC)
     A            DDACTN    R               REFFLD(ACTO)
     A                                      TEXT('Action code')
     A                                      COLHDG('Action' 'code')
     A            DDDLNO    R     A         REFFLD(DEAL)
     A                                      TEXT('Deal number')
     A                                      COLHDG('Deal' 'number')
     A            DDMTYP    R               REFFLD(DTPE)
     A                                      TEXT('Deal type')
     A                                      COLHDG('Deal' 'type')
     A            DDSTYP    R               REFFLD(DLST)
     A                                      TEXT('Deal subtype')
     A                                      COLHDG('Deal' 'subtype')
     A            DDFEDF         1
     A                                      TEXT('Federal funds indicator')
     A                                      COLHDG('Federal' 'funds' +
     A                                             'indicator')
     A            DDDLDT    R     A         REFFLD(CHGD)
     A                                      TEXT('Deal date')
     A                                      COLHDG('Deal' 'date')
     A            DDBRCA    R               REFFLD(BRCA)
     A                                      TEXT('Branch Code')
     A                                      COLHDG('Branch' 'Code')
     A            DDBKCD    R               REFFLD(BKCD)
     A                                      TEXT('Broker code')
     A                                      COLHDG('Broker' 'code')
     A            DDBRKG        14A
     A                                      TEXT('Brokerage')
     A                                      COLHDG('Brokerage')
     A            DDISUR    R               REFFLD(CSNM)
     A                                      TEXT('NAB issuer')
     A                                      COLHDG('NAB' 'issuer')
     A            DDCSNM    R               REFFLD(CSNM)
     A                                      TEXT('Bought from customer')
     A                                      COLHDG('Bought' 'from' 'customer')
     A            DDVDAT    R     A         REFFLD(CHGD)
     A                                      TEXT('Value date')
     A                                      COLHDG('Value' 'date')
     A            DDMDAT    R     A         REFFLD(CHGD)
     A                                      TEXT('Maturity date')
     A                                      COLHDG('Maturity' 'date')
     A            DDACSQ    R     A         REFFLD(ACSQ)
     A                                      TEXT('Account Seq. No.')
     A                                      COLHDG('Account' 'Seq. No.')
     A            DDCCY     R               REFFLD(CCY)
     A                                      TEXT('Currency')
     A                                      COLHDG('Currency')
     A            DDFVAL    R     A         REFFLD(FAVL)
     A                                      TEXT('NAB face value')
     A                                      COLHDG('NAB' 'face' 'value')
     A            DDAMNP    R     A         REFFLD(AMNP)
     A                                      TEXT('Purchase price')
     A                                      COLHDG('Purchase' 'price')
     A            DDBOKC    R               REFFLD(BOKC)
     A                                      TEXT('Book code')
     A                                      COLHDG('Book' 'code')
     A            DDLNKN    R     A         REFFLD(LNKDN)
     A                                      TEXT('Linked ref. no.')
     A                                      COLHDG('Linked' 'ref.' 'no.')
     A            DDBDID        10A
     A                                      TEXT('Bill depot Id.')
     A                                      COLHDG('Bill' 'depot Id.')
     A            DDINTR        12A
     A                                      TEXT('Interest rate')
     A                                      COLHDG('Interest' 'rate')
     A            DDICMT    R               REFFLD(ICMT)
     A                                      TEXT('Calculation method')
     A                                      COLHDG('Calculation' 'method')
     A            DDIPFQ    R               REFFLD(IPFQ)
     A                                      TEXT('Frequency')
     A                                      COLHDG('Frequency')
     A            DDNDAT         6A
     A                                      TEXT('Next interest date')
     A                                      COLHDG('Next' 'interest' 'date')
     A            DDSLID         6A
     A                                      TEXT('Start/last int date')
     A                                      COLHDG('Start/last' 'int date')
     A            DDDCRT    R     A         REFFLD(DCRT)
     A                                      TEXT('Discount rate')
     A                                      COLHDG('Discount rate')
     A            DDIPDM    R     A         REFFLD(IPDM)
     A                                      TEXT('Interest payment day')
     A                                      COLHDG('Interest' 'payment day')
     A            DDMTTI        14A
     A                                      TEXT('Total interest')
     A                                      COLHDG('Total' 'interest')
     A            DDBRAT    R     A         REFFLD(BCYR)
     A                                      TEXT('Base currency rate')
     A                                      COLHDG('Base' 'currency' 'rate')
     A            DDCDRF    R               REFFLD(CDRF)
     A                                      TEXT('NA reference No.')
     A                                      COLHDG('NA' 'ref. No.')
     A            DDRDFC         1A
     A                                      TEXT('Round down interest')
     A                                      COLHDG('Round' 'down' 'interest')
     A            DDPCLI    R               REFFLD(PCLI)
     A                                      TEXT('Parcel flag')
     A                                      COLHDG('Parcel' 'flag')
     A            DDPSZE    R     A         REFFLD(PSIZ)
     A                                      TEXT('Parcel size')
     A                                      COLHDG('Parcel' 'size')
     A            DDORBR    R               REFFLD(ORBR)
     A                                      TEXT('Originating branch')
     A                                      COLHDG('Originating' 'branch')
     A            DDPRFC    R               REFFLD(PRFC)
     A                                      TEXT('Profit centre')
     A                                      COLHDG('Profit' 'centre')
     A            DDFSRP    R   +1A         REFFLD(SPRD)
     A                                      TEXT('Funding Spread/Rate')
     A            DDFPRC    R               REFFLD(PRFC)
     A                                      TEXT('Funding Profit Centre')
 
      ** Timestamp
     A            DDTMESTMP R               REFFLD(TIMESTMP)
      **Start of change CAS001
     A            DDOYLC         5          TEXT('Our/Buy Yield Curve')
     A                                      COLHDG('Our/Buy' +
     A                                             'Yield Curve')
      **End of change CAS001
     A**Start of change CAS005
     A            DDLQPR        12A         TEXT('Liquidity/Premium Rate')
     A                                      COLHDG('Liquidity/Premium' +
     A                                      'Rate')
     A            DDLQPS         1A         TEXT('Liquidity/Premium Sign')
     A                                      COLHDG('Liquidity/Premium' +
     A                                      'Sign')
     A**End of change CAS005
     A*                                              Begin /CDL038/
     A            DDCLAS         4A         TEXT('Deal Class')
     A                                      COLHDG('Deal' 'Class')
     A*                                              End   /CDL038/
     A**Start of change CAP086
     A            DDAUTH         1A         TEXT('Automatic Authorisation')
     A                                      COLHDG('Automatic' 'Authorisation')
     A**End of change CAP086
     A**Start of change CDL049
     A            DDRRAT        14A         TEXT('Reference Rate')
     A                                      COLHDG('Reference Rate')
     A**End of change CDL049
