      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas MM Invalid REPO loan and deposit file')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  PF/MMIRPLDPD - Invalid REPO Loan & Deposit File              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001             Date 23Nov01               *
      *                 CAP067  *CREATE    Date 24Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *  CAP067 - Repurchase Agreements API                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R MMIRPLDD0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            DDFOTRANIDR               REFFLD(FOTRNID)
     A            DDFOASOCIDR               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            DDRPRLOCN R               REFFLD(RPRLOC)
     A            DDACTN    R               REFFLD(ACTO)
     A            DDDLNO    R     A         REFFLD(DEAL)
     A            DDTYPE    R               REFFLD(DTPE)
     A            DDSTYP    R               REFFLD(DLST)
     A            DDFEDF    R               REFFLD(FEDF)
     A            DDDADN    R     A         REFFLD(DEAL)
                                            TEXT('Rollover Deal Number')
                                            COLHDG('Rollover' 'Deal' 'Number')
     A            DDDLDT    R     A         REFFLD(CHGD)
                                            TEXT('Deal Date')
                                            COLHDG('Deal' 'Date')
     A            DDBRCA    R               REFFLD(BRCA)
     A            DDBOKC    R               REFFLD(BOKC)
     A            DDLNKN    R     A         REFFLD(LNKDN)
     A            DDBKCD    R               REFFLD(BKCD)
     A            DDBRKG        14A         TEXT('Brokerage Amount')
                                            COLHDG('Brokerage' 'Amount')
     A            DDRACD         1A         TEXT('Brokerage Rate/Amount Flag')
                                            COLHDG('Brkg Rate' '/Amount' 'Flag')
     A            DDCUST    R               REFFLD(CSNM)
     A            DDVDAT    R     A         REFFLD(CHGD)
                                            TEXT('Value Date')
                                            COLHDG('Value' 'Date')
     A            DDMDAT    R     A         REFFLD(CHGD)
                                            TEXT('Maturity Date')
                                            COLHDG('Maturity' 'Date')
     A            DDNTCE    R     A         REFFLD(NTCE)
     A            DDACSQ    R     A         REFFLD(ACSQ)
     A            DDCCY     R               REFFLD(CCY)
     A            DDAMNP        14A         TEXT('Deal Amount')
                                            COLHDG('Deal' 'Amount')
     A            DDBRAT        13A         TEXT('Base Currency Rate')
                                            COLHDG('Base' 'Currency' 'Rate')
     A            DDDRID    R               REFFLD(DRID)
     A            DDDDMR    R               REFFLD(DDMR)
     A            DDBSRC    R               REFFLD(MRBC)
                                            COLHDG('Base' 'Rate' 'Code')
     A            DDSPRT        12A         TEXT('Spread/Rate')
                                            COLHDG('Spread/Rate')
     A            DDICMT    R               REFFLD(ICMT)
     A            DDNGVI    R               REFFLD(NGVI)
     A            DDIPFQ    R               REFFLD(IPFQ)
     A            DDNDAT         6A         TEXT('Next Interest Date')
                                            COLHDG('Next' 'Interest' 'Date')
     A            DDTOTI        14A         TEXT('Total Interest')
                                            COLHDG('Total Interest')
     A            DDIPDM    R     A         REFFLD(IPDM)
     A            DDCPTI    R               REFFLD(CPTI)
     A            DDMTAX    R               REFFLD(MTAX)
     A            DDFUID    R               REFFLD(FUID)
     A            DDFLID    R               REFFLD(JFLID)
     A            DDRDFC    R               REFFLD(JRDFC)
     A            DDCDRF    R               REFFLD(CDRF)
     A            DDCDCH    R               REFFLD(CSNM)
     A                                      TEXT('CD Holder')
     A                                      COLHDG('CD' 'Holder')
     A            DDPRFC    R               REFFLD(PRFC)
     A            DDORBR    R               REFFLD(ORBR)
     A            DDPTFR    R               REFFLD(PTFR)
     A            DDAURO    R               REFFLD(AURO)
     A            DDCNNA    R               REFFLD(CNNA)
     A            DDROFQ    R               REFFLD(ROFQ)
     A            DDRLDN         2A         TEXT('Rollover Day Number')
     A                                      COLHDG('Rollover' 'Day' 'Number')
     A            DDIPDS    R               REFFLD(IPDS)
     A            DDIMET    R               REFFLD(IMET)
     A            DDISCY    R               REFFLD(CCY)
     A                                      TEXT('Interest Settlement Ccy')
     A                                      COLHDG('Int' 'Settl' 'Ccy')
     A**Start of change CGL029
     A**********  DDINSA    R               REFFLD(INSA)                                      CGL029
     A            QQINSA        15A
     A**End of change CGL029
     A            DDINOS    R               REFFLD(INOS)
     A            DDII72    R               REFFLD(CCY)
     A                                      TEXT('In Ccy in Field 72')
     A                                      COLHDG('In' 'Ccy' 'Fld 72')
     A            DDFSRP    R   +1A         REFFLD(SPRD)
     A                                      TEXT('Funding Spread/Rate')
     A            DDFSGN    R               REFFLD(IND)
     A                                      TEXT('Funding Spread Sign')
     A            DDFPRC    R               REFFLD(PRFC)
     A                                      TEXT('Funding Profit Centre')
 
      ** Timestamp
     A            DDTMESTMP R               REFFLD(TIMESTMP)
     A            DDPENA        14A         TEXT('Penalty Charged')
     A                                      COLHDG('Penalty' 'Charge')
     A            DDBUSR        10A         TEXT('Breakdown User Id')
     A                                      COLHDG('Breakdown' 'User Id')
     A            DDAUTH         1          TEXT('Automatic Authorisation')
     A                                      COLHDG('Automatic' 'Authorisation')
     A            DDTRTP         2          TEXT('Transaction Type')
     A                                      COLHDG('Transaction'          +
     A                                             'Type')
      **Start of change CAS001
     A            DDOYLC         5          TEXT('Our/Buy Yield Curve')
     A                                      COLHDG('Our/Buy' +
     A                                             'Yield Curve')
      **End of change CAS001
     A**Start of change CGL029
     A            DDINSA        21A
     A**End of change CGL029
     A*                                              Begin /CDL038/
     A            DDCLAS         4A         TEXT('Deal Class')
     A                                      COLHDG('Deal' 'Class')
     A*                                              End   /CDL038/
