      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXI *  TEXT('Midas MM Penalty Termination of TD')
      *****************************************************************
      *                                                               *
      *  MIDAS¦DBA - Dealing Money Market Module                      *
      *                                                               *
      *  PF/MMPTRMPD - MM Penalty Termination Master file             *
      *                                                               *
      *  (C) Copyright MISYS INTERNATIONAL BANKING SYSTEMS LTD 2008   *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. BUG26488           Date 19Oct09               *
      *  Prev Amend No. 258692             Date 02Feb09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 256795   *Create   Date 25Sep08               *
      *                 xxxxxx             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG26488 - MMPTRMPD compiled with error - attributes are     *
      *             incompatible                                      *
      *  258692 - Incorrect type of PTCNUM field in MMPTRMPD          *
      *  256795 - Penalty Termination of Time Deposits (CDL052).      *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
     A          R MMPTRMD0                  TEXT('(C) MISYS INTERNATIONAL BANKI-
     A                                      NG SYSTEMS LTD 2008')
      *
     A            PTRECI    R               REFFLD(RECI)
     A                                      TEXT('Record ID')
     A                                      COLHDG('Record ID')
     A            PTDLNO    R               REFFLD(DLNO)
     A                                      TEXT('Deal Number')
     A                                      COLHDG('Deal Number')
     A**Start of Change 258692
     A**********  PTCNUM    R               REFFLD(CNUM)                                      258692
     A**Start of Change BUG26488
     A**********  PTCNUM         6                                                          BUG26488
     A            PTCNUM    R               REFFLD(CNUM)
     A**End of Change 258692
     A**********                            TEXT('Customer Number')                         BUG26488
     A**********                            COLHDG('Customer Number')                       BUG26488
     A                                      TEXT('Redundant')
     A                                      COLHDG('Redundant')
     A**End of  Change BUG26488
     A            PTCCY     R               REFFLD(CCY)
     A                                      TEXT('Deal Currency')
     A                                      COLHDG('Deal Currency')
     A            PTPCPL    R               REFFLD(PCPL)
     A                                      TEXT('Principal Amount')
     A                                      COLHDG('Principal Amount')
     A            PTBRCA    R               REFFLD(BRCA)
     A                                      TEXT('Booking Branch')
     A                                      COLHDG('Booking Branch')
     A            PTORBR    R               REFFLD(ORBR)
     A                                      TEXT('Originating Branch')
     A                                      COLHDG('Originating Branch')
     A            PTSTYP    R               REFFLD(TRSU)
     A                                      TEXT('Deal Sub-type')
     A                                      COLHDG('Deal Sub-type')
     A            PTBOOK    R               REFFLD(TRSU)
     A                                      TEXT('Book')
     A                                      COLHDG('Book')
      *
     A            PTDDAT    R               REFFLD(DDAT)
     A                                      TEXT('Deal Date')
     A                                      COLHDG('Deal Date')
     A            PTVDAT    R               REFFLD(VDAT)
     A                                      TEXT('Value Date')
     A                                      COLHDG('Value Date')
     A            PTMDAT    R               REFFLD(MDAT)
     A                                      TEXT('Maturity Date')
     A                                      COLHDG('Maturity Date')
     A            PTPMDT    R               REFFLD(MDAT)
     A                                      TEXT('Prev. Maturity Date')
     A                                      COLHDG('Prev. Maturity Date')
      *
     A            PTDEFR    R               REFFLD(INTR)
     A                                      TEXT('Default Penalty Rate')
     A                                      COLHDG('Default Penalty Rate')
     A            PTPENR    R               REFFLD(INTR)
     A                                      TEXT('Penalty Rate')
     A                                      COLHDG('Penalty Rate')
     A            PTDLRT    R               REFFLD(INTR)
     A                                      TEXT('Deal Rate')
     A                                      COLHDG('Deal Rate')
      *
     A            PTICBS    R               REFFLD(SICBS)
     A                                      TEXT('Interest Method')
     A                                      COLHDG('Interest Method')
     A            PTFLID    R               REFFLD(JFLID)
     A                                      TEXT('First/Last Day Interest')
     A                                      COLHDG('1st/Last Day Int.')
     A            PTRDFC    R               REFFLD(RDFC)
     A                                      TEXT('Round Down Indicator')
     A                                      COLHDG('Round Down Ind.')
     A            PTBRTT    R               REFFLD(BRTT)
     A                                      TEXT('Base Rate Code')
     A                                      COLHDG('Base Rate Code')
     A            PTINTA    R               REFFLD(TOTI)
     A                                      TEXT('New Interest Amount')
     A                                      COLHDG('New Interest Amount')
     A            PTPINT    R               REFFLD(TOTI)
     A                                      TEXT('Previous Interest Amount')
     A                                      COLHDG('Prev. Interest Amt.')
     A            PTADJA    R               REFFLD(TOTI)
     A                                      TEXT('Adjustment Amount')
     A                                      COLHDG('Adjustment Amount')
      *
     A            PTCHTP    R               REFFLD(CHTP)
     A                                      TEXT('Last Change Type')
     A                                      COLHDG('Last Change Type')
     A            PTLCT     R               REFFLD(TATM)
     A                                      TEXT('Last Change Time')
     A                                      COLHDG('Last Change Time')
      *
     A            PTUSRI    R               REFFLD(USER)
     A                                      TEXT('Insert/Amend User')
     A                                      COLHDG('Insert/Amend User')
     A            PTUSRX    R               REFFLD(USER)
     A                                      TEXT('Authorising User')
     A                                      COLHDG('Authorising User')
     A**Start of Change BUG26488
     A            PTCNMA    R               REFFLD(CNUMA)
     A                                      TEXT('Customer Number')
     A                                      COLHDG('Customer Number')
     A**End of Change BUG26488
      *
     A          K PTBRCA
     A          K PTMDAT
     A          K PTDLNO
      *
