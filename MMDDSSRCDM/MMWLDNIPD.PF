      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas MM LDNI format work file')                       *
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  PF/MMWLDNIPD - LDNI Format Work File                         *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL038             Date 10May05               *
      *                 CDL033             Date 10Feb05               *
      *                 BUG7221            Date 10Feb05               *
      *                 CGL029             Date 01Sep03               *
      *                 CAS005             Date 16Dec02               *
      * Midas Release 4.01.02 ----------------------------------------*
      *                 209905  *CREATE    Date 25Sep02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus                                          *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CDL033 - Floating Rate CDs Issued                            *
      *  BUG7221- Some fields on MMLDNIPD don't exist in this source. *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *  209905 - Fiduciary API Enhancement                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R MMWLDNID0                 TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
      ** Action code
      *
     A            DDACTN    R               REFFLD(ACTO)
      *
      ** Deal number
      *
     A            DDDLNO    R     A         REFFLD(DEAL)
      *
      ** Deal type
      *
     A            DDTYPE    R               REFFLD(DTPE)
      *
      ** Deal subtype
      *
     A            DDSTYP    R               REFFLD(DLST)
      *
      ** Federal funds
      *
     A            DDFEDF    R               REFFLD(FEDF)
      *
      ** Rollover deal number
      *
     A            DDDADN    R     A         REFFLD(DEAL)
     A                                      TEXT('Rollover Deal Number')
     A                                      COLHDG('Rollover' 'Deal' 'Number')
      *
      ** Deal date
      *
     A            DDDLDT    R     A         REFFLD(CHGD)
     A                                      TEXT('Deal Date')
     A                                      COLHDG('Deal' 'Date')
      *
      ** Branch code
      *
     A            DDBRCA    R               REFFLD(BRCA)
     A                                      TEXT('Branch code')
      *
      ** Book code
      *
     A            DDBOKC    R               REFFLD(BOKC)
      *
      ** Linked reference number
      *
     A            DDLNKN    R     A         REFFLD(LNKDN)
     A                                      TEXT('Linked ref no')
      *
      ** Broker code
      *
     A            DDBKCD    R               REFFLD(BKCD)
      *
      ** Brokerage
      *
     A            DDBRKG    R   14A         REFFLD(BRKG)
     A                                      TEXT('Brokerage')
      *
      ** Rate/amount
      *
     A            DDRACD         1A         TEXT('Rate/amount')
      *
      ** Customer
      *
     A            DDCUST    R               REFFLD(CSNM)
     A                                      TEXT('Customer Shortname or number')
      *
      ** Value date
      *
     A            DDVDAT    R     A         REFFLD(CHGD)
     A                                      TEXT('Value Date')
     A                                      COLHDG('Value' 'Date')
      *
      ** Maturity date
      *
     A            DDMDAT    R     A         REFFLD(CHGD)
     A                                      TEXT('Maturity Date')
     A                                      COLHDG('Maturity' 'Date')
      *
      ** Notice days
      *
     A            DDNTCE    R     A         REFFLD(NTCE)
      *
      ** A/C Sequence number
      *
     A            DDACSQ    R     A         REFFLD(ACSQ)
      *
      ** Currency
      *
     A            DDCCY     R               REFFLD(CCY)
      *
      ** Amount
      *
     A            DDAMNP    R               REFFLD(AM14)
     A                                      TEXT('Amount')
      *
      ** Base currency rate
      *
     A            DDBRAT    R     A         REFFLD(BCYR)
      *
      ** Repo/Pledge
      *
     A            DDDRID    R               REFFLD(DRID)
     A                                      TEXT('Repo/pledge indicator')
      *
      ** Repo/Pledge Number
      *
     A            DDDDMR    R               REFFLD(DDMR)
     A                                      TEXT('Repo/pledge number')
      *
      ** Base rate
      *
     A            DDBSRC    R               REFFLD(MRBC)
      *
      ** Spread rate
      *
     A            DDSPRT    R   +1A         REFFLD(SPRD)
     A                                      TEXT('Spread/Rate')
      *
      ** Calculation method
      *
     A            DDICMT    R               REFFLD(ICMT)
     A                                      TEXT('Calculation method')
      *
      ** Negative Interest
      *
     A            DDNGVI    R               REFFLD(NGVI)
      *
      ** Frequency
      *
     A            DDIPFQ    R               REFFLD(IPFQ)
      *
      ** Next interest date
      *
     A            DDNDAT    R     A         REFFLD(CHGD)
     A                                      TEXT('Next interest date')
      *
      ** Total interest
      *
     A            DDTOTI    R   +1A         REFFLD(TOTI)
      *
      ** Interest frequency date in month
      *
     A            DDIPDM    R     A         REFFLD(IPDM)
      *
      ** Interest capitalisation ind
      *
     A            DDCPTI    R               REFFLD(CPTI)
      *
      ** Taxable ind
      *
     A            DDMTAX    R               REFFLD(MTAX)
      *
      ** Front up interest
      *
     A            DDFUID    R               REFFLD(FUID)
      *
      ** First & Last day interest
      *
     A            DDFLID    R               REFFLD(JFLID)
      *
      ** Round down interest
      *
     A            DDRDFC    R               REFFLD(JRDFC)
      *
      ** C D Reference number
      *
     A            DDCDRF    R               REFFLD(CDRF)
      *
      ** C D Holder
      *
     A            DDCDCH    R               REFFLD(CSNM)
     A                                      TEXT('Current holder of CD')
     A                                      COLHDG('Current' 'CD' 'Holder')
      *
      ** Profit centre
      *
     A            DDPRFC    R               REFFLD(PRFC)
      *
      ** Originating branch
      *
     A            DDORBR    R               REFFLD(ORBR)
      *
      ** Portfolio reference
      *
     A            DDPTFR    R               REFFLD(PTFR)
      *
      ** Automatic rollover
      *
     A            DDAURO    R               REFFLD(AURO)
      *
      ** Confirmation narrative
      *
     A            DDCNNA    R               REFFLD(CNNA)
      *
      ** Rollover frequency
      *
     A            DDROFQ    R               REFFLD(ROFQ)
     A                                      TEXT('Rollover Frequency')
      *
      ** Rollover day number
      *
     A            DDRLDN    R     A         REFFLD(RLDN)
     A                                      TEXT('Rollover Day No')
      *
      ** Interest paid separately
      *
     A            DDIPDS    R               REFFLD(IPDS)
     A                                      TEXT('Interest Paid Separately')
      *
      ** Interest settlement method
      *
     A            DDIMET    R               REFFLD(IMET)
     A                                      TEXT('Interest settlement method')
      *
      ** Interest settlement currency
      *
     A            DDISCY    R               REFFLD(CCY)
     A                                      TEXT('Interest settlement ccy')
      *
      ** Interest settlement account
      *
     A**Start of change CGL029
     A**********  DDINSA    R               REFFLD(INSA)                                      CGL029
     A**********                            TEXT('Interest Settlement A/C')                   CGL029
      *
      ** Interest counterparty nostro
      *
     A            QQINSA        15A
     A**End of change CGL029
     A            DDINOS    R               REFFLD(INOS)
     A                                      TEXT('Interest Counterparty Nostro')
      *
      ** In currency in Field 72
      *
     A            DDII72    R               REFFLD(CCY)
     A                                      TEXT('In Currency in Field 72')
      *
      ** Funding Spread/Rate
      *
     A            DDFSRP    R   +1A         REFFLD(SPRD)
     A                                      TEXT('Funding Spread/Rate')
      *
      ** Funding Spread Sign
      *
     A            DDFSGN    R               REFFLD(IND)
     A                                      TEXT('Funding Spread Sign')
      *
      ** Funding Profit Centre
      *
     A            DDFPRC    R               REFFLD(PRFC)
     A                                      TEXT('Funding Profit Centre')
      *
      ** Penalty Charged
      *
     A            DDPENA        14A         TEXT('Penalty Charged')
     A            DDTRTP         2          TEXT('Transaction Type')
     A                                      COLHDG('Transaction'          +
     A                                             'Type')
      *
     A            DDOYLC         5          TEXT('Our/Buy Yield Curve')
     A                                      COLHDG('Our/Buy' +
     A                                             'Yield Curve')
      *
     A            WSFOTRANID    20          TEXT('Front Office Id')
     A                                      COLHDG('Front' +
     A                                             'Office Id')
      *
     A            WSFOASOCID    20          TEXT('Associated Front Office Id')
     A                                      COLHDG('Associated Front' +
     A                                             'Office Id')
      *
     A            WSRPRLOCN      1          TEXT('Deal Repair Location')
     A                                      COLHDG('Deal Repair Location')
      *
     A**Start of change CAS005
     A            DDLQPR        12A         TEXT('Liquidity/Premium Rate')
     A                                      COLHDG('Liquidity/Premium' +
     A                                      'Rate')
     A            DDLQPS         1A         TEXT('Liquidity/Premium Sign')
     A                                      COLHDG('Liquidity/Premium' +
     A                                      'Sign')
     A**End of change CAS005
     A**Start of change CGL029
     A            DDINSA        21A         TEXT('Interest Settlement A/C')
     A**End of change CGL029
     A**Start of change BUG7221
     A            DDROLI         1A         TEXT('Rollover Indicator')
     A                                      COLHDG('Rollover' 'Indicator')
     A            DDSOFD        20A         TEXT('Source of Funds')
     A                                      COLHDG('Source of Funds')
     A            DDBLKA         1A         TEXT('Blocked Indicator')
     A                                      COLHDG('Blocked' 'Indicator')
     A            DDCAND         6A         TEXT('Cancellation Date')
     A                                      COLHDG('Cancellation' 'Date')
     A            DDPNPR         1A         TEXT('Penalty Pay/Receive')
     A                                      COLHDG('Penalty' 'Pay/Receive')
     A            DDFBRT         1A         TEXT('Fix Base Rate')
     A                                      COLHDG('Fix' 'Base' 'Rate')
     A            DDAIR         12A         TEXT('Actual Interest Rate')
     A                                      COLHDG('Actual' 'Interest Rate')
     A**End of change BUG7221
     A**Start of change CDL033
     A            WRCDT          6A         TEXT('Rate Change Date')
     A                                      COLHDG('Rate Change Date')
     A            WRCFR          1A         TEXT('Rate Change Frequency')
     A                                      COLHDG('Rate Change Frequen')
     A            WRCDM          2A         TEXT('Rate Change Day')
     A                                      COLHDG('Rate Change Day')
     A            WSTRT         12A         TEXT('Short Term Rate')
     A                                      COLHDG('Short Term Rate')
     A            WFXDM          2A         TEXT('Rate Fix Days')
     A                                      COLHDG('Rate Fix Days')
     A            WFYLD          5A         TEXT('Forward Yield Curve')
     A                                      COLHDG('Forward Yield Curve')
     A            WRCHGD1      492A         TEXT('Rate Change Date')
     A                                      COLHDG('Rate Change Date')
     A            WRCHGDHO1     82A         TEXT('Rate Change Date Confirmed +
     A                                      Holiday indicators')
     A                                      COLHDG('Rte Change Hol. ind.')
     A            WPCHGD1      492A         TEXT('Interest Change Date')
     A                                      COLHDG('Interest Change Date')
     A            WPCHGDHO1     82A         TEXT('Interest Change Date +
     A                                      Confirmed Holiday indicators')
     A                                      COLHDG('Pay Hol. ind.')
     A            WRCHGD2      492A         TEXT('Rate Change Date 2')
     A                                      COLHDG('Rate Change Date 2')
     A            WRCHGDHO2     82A         TEXT('Rate Change Date Confirmed +
     A                                      Holiday indicators 2')
     A                                      COLHDG('RteChnge Hol. ind. 2')
     A            WPCHGD2      492A         TEXT('Interest Change Date 2')
     A                                      COLHDG('Int. Change Date 2')
     A            WPCHGDHO2     82A         TEXT('Interest Change Date +
     A                                      Confirmed Holiday indicators 2')
     A                                      COLHDG('Pay Hol. ind. 2')
     A**End of change CDL033
     A*                                              Begin /CDL038/
     A            DDCLAS         4A         TEXT('Deal Class')
     A                                      COLHDG('Deal' 'Class')
     A*                                              End   /CDL038/
      **Start of change CER020
     A            DDISDT         6A         TEXT('Initial Start Date')
     A                                      COLHDG('Initial Start ' +
     A                                             'Date')
      *
     A            DDFMDT         6A         TEXT('Final Maturity +
     A                                          Date')
     A                                      COLHDG('Final Maturity ' +
     A                                             'Date')
      *
     A            DDORDN         6A         TEXT('Original Deal +
     A                                          Number')
     A                                      COLHDG('Original Deal ' +
     A                                             'Number')
      **End of change CER020
