      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas MM REPO API Loan/deposit work file')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  PF/MMWLNDPD - REPO API Loan and Deposit Work File            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001             Date 23Nov01               *
      *                 CAP067  *CREATE    Date 24Sep01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *  CAP067 - Repurchase Agreements API.                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R MMWLNDD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            WLFOTRANIDR               REFFLD(FOTRNID)
     A            WLFOASOCIDR               REFFLD(FOTRNID)
     A                                      TEXT('Associated Front +
     A                                            Office Transaction Id')
     A                                      COLHDG('Associated' +
     A                                             'Front Office' +
     A                                             'Transaction Id')
     A            WLRPRLOCN R               REFFLD(RPRLOC)
      *
      ** Action code
     A            WLACTN    R               REFFLD(ACTO)
      *
      ** Deal number
     A            WLDLNO    R     A         REFFLD(DEAL)
      *
      ** Deal type
     A            WLTYPE    R               REFFLD(DTPE)
      *
      ** Deal subtype
     A            WLSTYP    R               REFFLD(DLST)
      *
      ** Federal funds
     A            WLFEDF    R               REFFLD(FEDF)
      *
      ** Rollover deal number
     A            WLDADN    R     A         REFFLD(DEAL)
     A                                      TEXT('Rollover Deal Number')
     A                                      COLHDG('Rollover' 'Deal' 'Number')
      *
      ** Deal date
     A            WLDLDT    R     A         REFFLD(CHGD)
     A                                      TEXT('Deal Date')
     A                                      COLHDG('Deal' 'Date')
      *
      ** Branch code
     A            WLBRCA    R               REFFLD(BRCA)
     A                                      TEXT('Branch code')
      *
      ** Book code
     A            WLBOKC    R               REFFLD(BOKC)
      *
      ** Linked reference number
     A            WLLNKN    R     A         REFFLD(LNKDN)
     A                                      TEXT('Linked ref no')
      *
      ** Broker code
     A            WLBKCD    R               REFFLD(BKCD)
      *
      ** Brokerage
     A            WLBRKG    R   14A         REFFLD(BRKG)
     A                                      TEXT('Brokerage')
      *
      ** Rate/amount
     A            WLRACD         1A         TEXT('Rate/amount')
      *
      ** Customer
     A            WLCUST    R               REFFLD(CSNM)
     A                                      TEXT('Customer Shortname or number')
      *
      ** Value date
     A            WLVDAT    R     A         REFFLD(CHGD)
     A                                      TEXT('Value Date')
     A                                      COLHDG('Value' 'Date')
      *
      ** Maturity date
     A            WLMDAT    R     A         REFFLD(CHGD)
     A                                      TEXT('Maturity Date')
     A                                      COLHDG('Maturity' 'Date')
      *
      ** Notice days
     A            WLNTCE    R     A         REFFLD(NTCE)
      *
      ** A/C Sequence number
     A            WLACSQ    R     A         REFFLD(ACSQ)
      *
      ** Currency
     A            WLCCY     R               REFFLD(CCY)
      *
      ** Amount
     A            WLAMNP    R               REFFLD(AM14)
     A                                      TEXT('Amount')
      *
      ** Base currency rate
     A            WLBRAT    R     A         REFFLD(BCYR)
      *
      ** Repo/Pledge
     A            WLDRID    R               REFFLD(DRID)
     A                                      TEXT('Repo/pledge indicator')
      *
      ** Repo/Pledge Number
     A            WLDDMR    R               REFFLD(DDMR)
     A                                      TEXT('Repo/pledge number')
      *
      ** Base rate
     A            WLBSRC    R               REFFLD(MRBC)
      *
      ** Spread rate
     A            WLSPRT    R   +1A         REFFLD(SPRD)
     A                                      TEXT('Spread/Rate')
      *
      ** Calculation method
     A            WLICMT    R               REFFLD(ICMT)
     A                                      TEXT('Calculation method')
      *
      ** Negative Interest
     A            WLNGVI    R               REFFLD(NGVI)
      *
      ** Frequency
     A            WLIPFQ    R               REFFLD(IPFQ)
      *
      ** Next interest date
     A            WLNDAT    R     A         REFFLD(CHGD)
     A                                      TEXT('Next interest date')
      *
      ** Total interest
     A            WLTOTI    R   +1A         REFFLD(TOTI)
      *
      ** Interest frequency date in month
     A            WLIPDM    R     A         REFFLD(IPDM)
      *
      ** Interest capitalisation ind
     A            WLCPTI    R               REFFLD(CPTI)
      *
      ** Taxable ind
     A            WLMTAX    R               REFFLD(MTAX)
      *
      ** Front up interest
     A            WLFUID    R               REFFLD(FUID)
      *
      ** First & Last day interest
     A            WLFLID    R               REFFLD(JFLID)
      *
      ** Round down interest
     A            WLRDFC    R               REFFLD(JRDFC)
      *
      ** C D Reference number
     A            WLCDRF    R               REFFLD(CDRF)
      *
      ** C D Holder
     A            WLCDCH    R               REFFLD(CSNM)
     A                                      TEXT('Current holder of CD')
     A                                      COLHDG('Current' 'CD' 'Holder')
      *
      ** Profit centre
     A            WLPRFC    R               REFFLD(PRFC)
      *
      ** Originating branch
     A            WLORBR    R               REFFLD(ORBR)
      *
      ** Portfolio reference
     A            WLPTFR    R               REFFLD(PTFR)
      *
      ** Automatic rollover
     A            WLAURO    R               REFFLD(AURO)
      *
      ** Confirmation narrative
     A            WLCNNA    R               REFFLD(CNNA)
      *
      ** Rollover frequency
     A            WLROFQ    R               REFFLD(ROFQ)
     A                                      TEXT('Rollover Frequency')
      *
      ** Rollover day number
     A            WLRLDN    R     A         REFFLD(RLDN)
     A                                      TEXT('Rollover Day No')
      *
      ** Interest paid separately
     A            WLIPDS    R               REFFLD(IPDS)
     A                                      TEXT('Interest Paid Separately')
      *
      ** Interest settlement method
     A            WLIMET    R               REFFLD(IMET)
     A                                      TEXT('Interest settlement method')
      *
      ** Interest settlement currency
     A            WLISCY    R               REFFLD(CCY)
     A                                      TEXT('Interest settlement ccy')
      *
      ** Interest settlement account
     A**Start of change CGL029
     A**********  WLINSA    R               REFFLD(INSA)                                      CGL029
     A**********                            TEXT('Interest Settlement A/C')                   CGL029
      *
      ** Interest counterparty nostro
     A            QQINSA        15A
     A**End of change CGL029
     A            WLINOS    R               REFFLD(INOS)
     A                                      TEXT('Interest Counterparty Nostro')
      *
      ** In currency in Field 72
     A            WLII72    R               REFFLD(CCY)
     A                                      TEXT('In Currency in Field 72')
      *
      ** Funding Spread/Rate
     A            WLFSRP    R   +1A         REFFLD(SPRD)
     A                                      TEXT('Funding Spread/Rate')
      *
      ** Funding Spread Sign
     A            WLFSGN    R               REFFLD(IND)
     A                                      TEXT('Funding Spread Sign')
      *
      ** Funding Profit Centre
     A            WLFPRC    R               REFFLD(PRFC)
     A                                      TEXT('Funding Profit Centre')
      *
      ** Penalty Charged
     A            WLPENA        14A         TEXT('Penalty Charged')
      *
      ** Transaction Type
     A            WLTRTP         2          TEXT('Transaction Type')
     A                                      COLHDG('Transaction'          +
     A                                             'Type')
      *
      **Start of change CAS001
     A            WLOYLC         5          TEXT('Our/Buy Yield Curve')
     A                                      COLHDG('Our/Buy' +
     A                                             'Yield Curve')
      **End of change CAS001
     A**Start of change CGL029
     A            WLINSA        21A         TEXT('Interest Settlement A/C')
     A**End of change CGL029
     A*                                              Begin /CDL038/
     A            WLCLAS         4A         TEXT('Deal Class')
     A                                      COLHDG('Deal' 'Class')
     A*                                              End   /CDL038/
