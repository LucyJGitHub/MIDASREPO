      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas MM API LDNI format definition File')             *
      *****************************************************************
      *                                                               *
      *  Midas - Application Program Interface Module                 *
      *                                                               *
      *  PF/MMLDNIPD - API Incoming Message Definition File for       *
      *             Loans, Deposits & NAs Issued                      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CER050             Date 16Jun19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CGL165             Date 23Feb15               *
      *                 CDL096             Date 22Sep14               *
      *                 CSF011A            Date 28Nov11               *
      *                 CRE073             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 31Mar06               *
      *                 CDL038             Date 10May05               *
      *                 CDL033             Date 10Feb05               *
      *                 CDL020             Date 03Feb04               *
      *                 CDL019             Date 03Feb04               *
      *                 CDL017             Date 03Feb04               *
      *                 CDL016             Date 03Feb04               *
      *                 CGL029             Date 01Sep03               *
      *                 CAS005             Date 16Dec02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001             Date 23Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CSE006             Date 20Apr99               *
      *                 CDL007             Date 04Oct99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAP002  *CREATE    Date 17Mar98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CER050 - Annualised Percentage Rate                          *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL165 - Dual Withholding Tax                                *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *  CSF011A - CCR015: Display Order in Confirmation Pages        *
      *  CRE073 - Interest Rate Rounding                              *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus                                          *
      *  CDL049 - Addition of a Reference Rate field                  *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CDL033 - Floating Rate CDs Issued                            *
      *  CDL020 - Apply Base Rate at Value Date                       *
      *  CDL019 - Allow Base Rate Changes on Fixed Deposits/Loans     *
      *  CDL017 - Penalty Fee & Early Maturity of Fixed Term Loan/Dep *
      *  CDL016 - Autumatic Rollover of Fixed Term Loan/Deposit       *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *  CSE006 - Repurchase Agreements (REPOS) enhancement:          *
      *           New field - Transaction Type                        *
      *  CDL007 - Deposit Breakdown Penalty                           *
      *  CAP002 - Conversion of transaction inputs into modular       *
      *           structure to use as APIs.                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R MMLDNID0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
      ** Message contents
      *
      ** Action code
     A            DDACTN    R               REFFLD(ACTO)
      *
      ** Deal number
     A            DDDLNO    R     A         REFFLD(DEAL)
      *
      ** Deal type
     A            DDTYPE    R               REFFLD(DTPE)
      *
      ** Deal subtype
     A            DDSTYP    R               REFFLD(DLST)
      *
      ** Federal funds
     A            DDFEDF    R               REFFLD(FEDF)
      *
      ** Rollover deal number
     A            DDDADN    R     A         REFFLD(DEAL)
     A                                      TEXT('Rollover Deal Number')
     A                                      COLHDG('Rollover' 'Deal' 'Number')
      *
      ** Deal date
     A            DDDLDT    R     A         REFFLD(CHGD)
     A                                      TEXT('Deal Date')
     A                                      COLHDG('Deal' 'Date')
      *
      ** Branch code
     A            DDBRCA    R               REFFLD(BRCA)
     A                                      TEXT('Branch code')
      *
      ** Book code
     A            DDBOKC    R               REFFLD(BOKC)
      *
      ** Linked reference number
     A            DDLNKN    R     A         REFFLD(LNKDN)
     A                                      TEXT('Linked ref no')
      *
      ** Broker code
     A            DDBKCD    R               REFFLD(BKCD)
      *
      ** Brokerage
     A            DDBRKG    R   14A         REFFLD(BRKG)
     A                                      TEXT('Brokerage')
      *
      ** Rate/amount
     A            DDRACD         1A         TEXT('Rate/amount')
      *
      ** Customer
     A            DDCUST    R               REFFLD(CSNM)
     A                                      TEXT('Customer Shortname or number')
      **Start of change CSF011A
     A            DDCRSN        10A         TEXT('Customer Shortname')
     A                                      COLHDG('Customer Shortname')
     A            DDCRNM        20A         TEXT('Customer Report Name')
     A                                      COLHDG('Customer Report Name')
     A            DDCRTN        10A         TEXT('Customer Report Town')
     A                                      COLHDG('Customer Report Town')
      **End of change CSF011A
      *
      ** Value date
     A            DDVDAT    R     A         REFFLD(CHGD)
     A                                      TEXT('Value Date')
     A                                      COLHDG('Value' 'Date')
      *
      ** Maturity date
     A            DDMDAT    R     A         REFFLD(CHGD)
     A                                      TEXT('Maturity Date')
     A                                      COLHDG('Maturity' 'Date')
      *
      ** Notice days
     A            DDNTCE    R     A         REFFLD(NTCE)
      *
      ** A/C Sequence number
     A            DDACSQ    R     A         REFFLD(ACSQ)
      *
      ** Currency
     A            DDCCY     R               REFFLD(CCY)
      *
      ** Amount
     A            DDAMNP    R               REFFLD(AM14)
     A                                      TEXT('Amount')
      *
      ** Base currency rate
     A            DDBRAT    R     A         REFFLD(BCYR)
      *
      ** Repo/Pledge
     A            DDDRID    R               REFFLD(DRID)
     A                                      TEXT('Repo/pledge indicator')
      *
      ** Repo/Pledge Number
     A            DDDDMR    R               REFFLD(DDMR)
     A                                      TEXT('Repo/pledge number')
      *
      ** Base rate
     A            DDBSRC    R               REFFLD(MRBC)
      *
      ** Spread rate
     A            DDSPRT    R   +1A         REFFLD(SPRD)
     A                                      TEXT('Spread/Rate')
      *
      ** Calculation method
     A            DDICMT    R               REFFLD(ICMT)
     A                                      TEXT('Calculation method')
      *
      ** Negative Interest
     A            DDNGVI    R               REFFLD(NGVI)
      *
      ** Frequency
     A            DDIPFQ    R               REFFLD(IPFQ)
      *
      ** Next interest date
     A            DDNDAT    R     A         REFFLD(CHGD)
     A                                      TEXT('Next interest date')
      *
      ** Total interest
     A            DDTOTI    R   +1A         REFFLD(TOTI)
      *
      ** Interest frequency date in month
     A            DDIPDM    R     A         REFFLD(IPDM)
      *
      ** Interest capitalisation ind
     A            DDCPTI    R               REFFLD(CPTI)
      *
      ** Taxable ind
     A            DDMTAX    R               REFFLD(MTAX)
      *
      ** Front up interest
     A            DDFUID    R               REFFLD(FUID)
      *
      ** First & Last day interest
     A            DDFLID    R               REFFLD(JFLID)
      *
      ** Round down interest
     A            DDRDFC    R               REFFLD(JRDFC)
      *
      ** C D Reference number
     A            DDCDRF    R               REFFLD(CDRF)
      *
      ** C D Holder
     A            DDCDCH    R               REFFLD(CSNM)
     A                                      TEXT('Current holder of CD')
     A                                      COLHDG('Current' 'CD' 'Holder')
      **Start of change CSF011A
     A            DDCDSN        10A         TEXT('Current CD Holder +
     A                                           Shortname')
     A                                      COLHDG('Current CD Holder' +
     A                                           'Shortname')
     A            DDCDNM        20A         TEXT('Current CD Holder +
     A                                           Report Name')
     A                                      COLHDG('Current CD Holder' +
     A                                           'Report Name')
     A            DDCDTN        10A         TEXT('Current CD Holder +
     A                                           Report Town')
     A                                      COLHDG('Current CD Holder' +
     A                                           'Report Town')
      **End of change CSF011A
      *
      ** Profit centre
     A            DDPRFC    R               REFFLD(PRFC)
      *
      ** Originating branch
     A            DDORBR    R               REFFLD(ORBR)
      *
      ** Portfolio reference
     A            DDPTFR    R               REFFLD(PTFR)
      *
      ** Automatic rollover
     A            DDAURO    R               REFFLD(AURO)
      *
      ** Confirmation narrative
     A            DDCNNA    R               REFFLD(CNNA)
      *
      ** Rollover frequency
     A            DDROFQ    R               REFFLD(ROFQ)
     A                                      TEXT('Rollover Frequency')
      *
      ** Rollover day number
     A            DDRLDN    R     A         REFFLD(RLDN)
     A                                      TEXT('Rollover Day No')
      *
      ** Interest paid separately
     A            DDIPDS    R               REFFLD(IPDS)
     A                                      TEXT('Interest Paid Separately')
      *
      ** Interest settlement method
     A            DDIMET    R               REFFLD(IMET)
     A                                      TEXT('Interest settlement method')
      *
      ** Interest settlement currency
     A            DDISCY    R               REFFLD(CCY)
     A                                      TEXT('Interest settlement ccy')
      *
      ** Interest settlement account
     A**Start of change CGL029
     A**********  DDINSA    R               REFFLD(INSA)                                      CGL029
     A**********                            TEXT('Interest Settlement A/C')                   CGL029
     A            DDINSA        21A         TEXT('Interest Settlement A/C')
     A**End of change CGL029
      **Start of change CSF011A
     A            DDIASN        10A         TEXT('Int Settlement Acct +
     A                                           Shortname')
     A                                      COLHDG('Int Settlement Acct' +
     A                                             'Shortname')
     A            DDIANM        20A         TEXT('Int Settlement Acct +
     A                                           Report Name')
     A                                      COLHDG('Int Settlement Acct' +
     A                                           'Report Name')
     A            DDIATN        10A         TEXT('Int Settlement Acct +
     A                                           Report Town')
     A                                      COLHDG('Int Settlement Acct' +
     A                                           'Report Town')
     A            DDIAAN        20A         TEXT('Int Settlement Acct +
     A                                           Account Name')
     A                                      COLHDG('Int Settlement Acct' +
     A                                           'Account Name')
     A            DDIASW        12A         TEXT('Int Settlement Acct +
     A                                           SWIFT Id')
     A                                      COLHDG('Int Settlement Acct' +
     A                                           'SWIFT Id')
      **End of change CSF011A
      *
      ** Interest counterparty nostro
     A            DDINOS    R               REFFLD(INOS)
     A                                      TEXT('Interest Counterparty Nostro')
      **Start of change CSF011A
     A            DDINSN        10A         TEXT('Int Countprty Nostro +
     A                                           Shortname')
     A                                      COLHDG('Int Countprty Nostro' +
     A                                             'Shortname')
     A            DDINNM        20A         TEXT('Int Countprty Nostro +
     A                                           Report Name')
     A                                      COLHDG('Int Countprty Nostro' +
     A                                           'Report Name')
     A            DDINTN        10A         TEXT('Int Countprty Nostro +
     A                                           Report Town')
     A                                      COLHDG('Int Countprty Nostro' +
     A                                           'Report Town')
     A            DDINSW        12A         TEXT('Int Countprty Nostro +
     A                                           SWIFT Id')
     A                                      COLHDG('Int Countprty Nostro' +
     A                                           'SWIFT Id')
      **End of change CSF011A
      *
      ** In currency in Field 72
     A            DDII72    R               REFFLD(CCY)
     A                                      TEXT('In Currency in Field 72')
      *
      ** Funding Spread/Rate
     A            DDFSRP    R   +1A         REFFLD(SPRD)
     A                                      TEXT('Funding Spread/Rate')
      *
      ** Funding Spread Sign
     A            DDFSGN    R               REFFLD(IND)
     A                                      TEXT('Funding Spread Sign')
      *
      ** Funding Profit Centre
     A            DDFPRC    R               REFFLD(PRFC)
     A                                      TEXT('Funding Profit Centre')
      *                                              Begin /CDL007/
      ** Penalty Charged
     A            DDPENA        14A         TEXT('Penalty Charged')
      *                                              End   /CDL007/
      *                                              Begin /CSE006/
     A            DDTRTP         2          TEXT('Transaction Type')
     A                                      COLHDG('Transaction'          +
     A                                             'Type')
      *                                              End   /CSE006/
      **Start of change CAS001
     A            DDOYLC         5          TEXT('Our/Buy Yield Curve')
     A                                      COLHDG('Our/Buy' +
     A                                             'Yield Curve')
      **End of change CAS001
     A**Start of change CAS005
     A            DDLQPR        12A         TEXT('Liquidity/Premium Rate')
     A                                      COLHDG('Liquidity/Premium' +
     A                                      'Rate')
     A            DDLQPS         1A         TEXT('Liquidity/Premium Sign')
     A                                      COLHDG('Liquidity/Premium' +
     A                                      'Sign')
     A**End of change CAS005
      **Start of change CDL016
     A            DDROLI         1A         TEXT('Rollover Indicator')
     A                                      COLHDG('Rollover' 'Indicator')
     A            DDSOFD        20A         TEXT('Source of Funds')
     A                                      COLHDG('Source of Funds')
     A            DDBLKA         1A         TEXT('Blocked Indicator')
     A                                      COLHDG('Blocked' 'Indicator')
      **End of change CDL016
      **Start of change CDL017
     A            DDCAND         6A         TEXT('Cancellation Date')
     A                                      COLHDG('Cancellation' 'Date')
     A            DDPNPR         1A         TEXT('Penalty Pay/Receive')
     A                                      COLHDG('Penalty' 'Pay/Receive')
      **End of change CDL017
      **Start of change CDL019
     A            DDFBRT         1A         TEXT('Fix Base Rate')
     A                                      COLHDG('Fix' 'Base' 'Rate')
      **End of change CDL019
      **Start of change CDL020
     A            DDAIR         12A         TEXT('Actual Interest Rate')
     A                                      COLHDG('Actual' 'Interest Rate')
      **End of change CDL020
      **Start of change CDL033
     A            DDRCDT         6A         TEXT('Rate Change Date')
     A                                      COLHDG('Rate Change' ' Date')
     A            DDRCFR         1A         TEXT('Rate Change Frequency')
     A                                      COLHDG('Rate Change' 'Frequency')
     A            DDRCDM         2A         TEXT('Rate Change Day')
     A                                      COLHDG('Rate Change' 'Day')
     A            DDSTRT        12A         TEXT('Short Term Rate')
     A                                      COLHDG('Short Term' 'Rate')
     A            DDFXDM         2A         TEXT('Rate Fix Days')
     A                                      COLHDG('Rate Fix' 'Days')
     A            DDFYLD         5A         TEXT('Forward Yield Curve')
     A                                      COLHDG('Forward' 'Yield Curve')
      **End of change CDL033
     A*                                              Begin /CDL038/
     A            DDCLAS         4A         TEXT('Deal Class')
     A                                      COLHDG('Deal' 'Class')
     A*                                              End   /CDL038/
     A**Start of change CDL049
     A            DDRRAT        14A         TEXT('Reference Rate')
     A                                      COLHDG('Reference Rate')
     A**End of change CDL049
      **Start of change CER020
     A            DDISDT         6A         TEXT('Initial Start Date')
     A                                      COLHDG('Initial Start ' +
     A                                             'Date')
      *
     A            DDFMDT         6A         TEXT('Final Maturity +
     A                                          Date')
     A                                      COLHDG('Final Maturity ' +
     A                                             'Date')
      *
     A            DDORDN         6A         TEXT('Original Deal +
     A                                          Number')
     A                                      COLHDG('Original Deal ' +
     A                                             'Number')
      **End of change CER020
      **Start of change CRE073
     A            DDCNSP        12A         TEXT('Contractual Spread')
     A                                      COLHDG('Contractual' 'Spread')
     A            DDRDMT         7A         TEXT('Rounding Method')
     A                                      COLHDG('Rounding' 'Method')
     A            DDRDFD         4A         TEXT('Rounding Fraction/Decimal')
     A                                      COLHDG('Rounding' 'Fraction/+
     A                                      Decimal')
      **End of change CRE073
      ** Start of change CDL096
     A            DDC1PC    R               REFFLD(CCYD)
     A                                      TEXT('Currency 1 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 1 for' +
     A                                             'Payment Convention')
     A            DDC2PC    R               REFFLD(CCYD)
     A                                      TEXT('Currency 2 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 2 for' +
     A                                             'Payment Convention')
     A            DDC3PC    R               REFFLD(CCYD)
     A                                      TEXT('Currency 3 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 3 for' +
     A                                             'Payment Convention')
     A            DDC4PC    R               REFFLD(CCYD)
     A                                      TEXT('Currency 4 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 4 for' +
     A                                             'Payment Convention')
     A            DDC5PC    R               REFFLD(CCYD)
     A                                      TEXT('Currency 5 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 5 for' +
     A                                             'Payment Convention')
     A            DDC6PC    R               REFFLD(CCYD)
     A                                      TEXT('Currency 6 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 6 for' +
     A                                             'Payment Convention')
     A            DDC7PC    R               REFFLD(CCYD)
     A                                      TEXT('Currency 7 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 7 for' +
     A                                             'Payment Convention')
     A            DDC8PC    R               REFFLD(CCYD)
     A                                      TEXT('Currency 8 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 8 for' +
     A                                             'Payment Convention')
     A            DDC9PC    R               REFFLD(CCYD)
     A                                      TEXT('Currency 9 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 9 for' +
     A                                             'Payment Convention')
     A            DDC0PC    R               REFFLD(CCYD)
     A                                      TEXT('Currency 10 for Payment +
     A                                            Convention')
     A                                      COLHDG('Currency 10 for' +
     A                                             'Payment Convention')
     A            DDPYDC    R               REFFLD(PYTP)
     A                                      TEXT('Payment Date +
     A                                            Convention')
     A                                      COLHDG('Payment Date' +
     A                                             'Convention')
     A            DDAMTC    R               REFFLD(PYTP)
     A                                      TEXT('Amount +
     A                                            Convention')
     A                                      COLHDG('Amount' +
     A                                             'Convention')
      ** End of change CDL096
      **Start of change CGL165
     A            DDTXC1         2A         TEXT('Main Tax Code')
     A            DDTXC2         2A         TEXT('Secondary Tax Code')
      **End of change CGL165
      ** Start of change CER050
     A            DDAPRC         1A         TEXT('APR Calculation Required')
     A            DDAPRR         8A         TEXT('APR Rate')
      ** End of change CER050
