      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Amendable Fields For Comp. Deals aud')
     A****************************************************************
     A*                                                              *
     A*  Midas - Money Market (Dealing)
     A*                                                              *
     A*     PRTF/MM0012AU - COMPLETE DEAL AMENDABLE FIELDS FILE LIST *
     A*                                                              *
     A* (c) Finastra International Limited 2001                      *
     A*                                                              *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     PREV AMEND NO. X00000             DATE   00XXX00         *
     A*                                                              *
     A****************************************************************
     A*                                                              *
     A*  MD046248 - Finastra Rebranding                              *
     A*                                                              *
     A****************************************************************
     A*
     A*
     A**  Indicator Function Summary
     A*
     A* ID F  C  H  L    FUNCTION OF INDICATORS
     A* ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
     A*       63         End of day processing.                        UT
     A*       64         Input cycle processing.                       UT
     A*
     A*
     A**  FILE LEVEL KEYWORDS
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A**  FORMAT FOR HEADERS.
     A          R MMTOTSR0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A*
     A                                     7'REFERENCE MM0012AU'
     A            RRTITL    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            RRRUNA    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A*
     A            RRPRNM    R             21REFFLD(TTIT)
     A                                      SPACEA(1)
     A            RRPRUL    R             21REFFLD(TTIT)
     A                                      SPACEA(2)
     A*
     A**  FORMAT FOR DETAILS.
     A          R MMTOTSR1                  TEXT('DETAIL FORMAT')
     A*
     A N63                                 7'INPUT CYCLE'
     A N63        RRTIME    R             19REFFLD(TIME)
     A N63                                  SPACEA(3)
     A                                      EDTWRD('0 :  :  ')
     A  63                                 7'CLOSE OF BUSINESS'
     A  63                                  SPACEA(3)
     A*
     A  63                                51'INPUT'
     A                                    63'CALCULATED'
     A                                      SPACEA(1)
     A*
     A  63                                51'-----'
     A                                    63'----------'
     A                                      SPACEA(2)
     A*
     A                                    21'NUMBER OF RECORDS ON FILE'
     A  63        RRNREC    R             51REFFLD(NREC)
     A                                      EDTWRD('  0 ')
     A            RRCALC    R             65REFFLD(CALR)
     A                                      EDTWRD('  0 ')
     A  64                                81'***  DIFFERENCE  ***'
     A*
     A*
     A**  DATABASE ERROR MESSAGE.
     A          R MMTOTSR3                  TEXT('DATABASE ERROR MSG')
     A*
     A                                    33'DATABASE ERROR'
     A                                      SKIPB(35)
     A            RRBASE    R             48REFFLD(DBASE)
     A                                    52'IN PROGRAM'
     A            RRPGM     R             63REFFLD(DBPGM)
     A                                      SPACEA(1)
     A*
     A                                    33'FILE'
     A            RRFILE    R             38REFFLD(DBFILE)
     A                                    46', KEY'
     A            RRKEY     R             52REFFLD(DBKEY)
     A*
