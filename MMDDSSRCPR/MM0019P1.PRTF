      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Amend.Fields For Complete Deals')
     F****************************************************************
     F*                                                              *
     A*  Midas - Money Market (Dealing)
     F*                                                              *
     F*    PRTF/MM0019RR - MM DEALER - DEALER AMENDABILITY AUDIT     *
     F*                    REPORT                                    *
     F*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*    LAST AMEND NO. 048630              DATE 27JAN93           *
     F*    PREV AMEND NO. S01319              DATE  1OCT91           *
     F*                                                              *
     A****************************************************************
     A*                                                               *
     A*     048630 - Incorrect ref. field used for Branch code        *
     A*              amendable indicator - should be a 1A field not 3A*
     A*     S01319 - Remove redundant processing
     A*              File renamed from MM0019RR
     F****************************************************************
     F*
     F**  Indicator Function Summary
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F* ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
     F*
     F*       64         ERROR INDICATOR - NOT REFLECTED IN DATABASE
     F*       65         ERROR INDICATOR - INCORRECT NO OF RECS ON FILE
     F*       66         ERROR INDICATOR - EXCEPTION MESSAGE.
     F*       67         NO DETAILS TO REPORT
     F*       68         ERROR INDICATOR - FILE UPDATED BY ANOTHER PGM
     A*
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A*
     A**  FORMAT FOR HEADINGS
     A          R MM0019R0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A*                                              Begin /S01319/
     A*************************************8'REFERENCE'
     A                                     8'REFERENCE MM0019P1'
     A************RRPGM*********10********18TEXT('LDA SUBFIELD:DBPGM')
     A*                                              End   /S01319/
     A            RRTITL    R             38REFFLD(TITL)
     A                                   101'DATE'
     A            RRRUNA    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SKIPA(5)
     A*
     A            RRPRNM        90        21TEXT('REPORT TITLE')
     A                                      SPACEA(1)
     A            RRPRUL        90        21TEXT('REPORT UNDERLINE')
     A                                      SPACEA(3)
     A*
     A**  FORMAT FOR COLUMN HEADINGS
     A          R MM0019R1                  TEXT('COLUMN HEADER RPT FORMAT')
     A                                     1'USER ID/'
     A                                    11'CHANGE TYPE/'
     A                                    42'BRANCH CODE'
     A                                    55'CUSTOMER NAME'
     A                                    70'BROKER CODE'
     A                                    83'BROKERAGE'
     A                                    94'DEAL SUBTYPE'
     A                                   108'INTEREST DETAILS'
     A                                      SKIPA(10)
     A*
     A                                     1'PROGRAM'
     A                                    18'TIME'
     A                                    26'WSID'
     A                                    43'AMENDABLE'
     A                                    57'AMENDABLE'
     A                                    71'AMENDABLE'
     A                                    83'AMENDABLE'
     A                                    95'AMENDABLE'
     A                                   111'AMENDABLE'
     A                                      SKIPA(12)
     A*
     A**  FORMAT FOR DETAILS
     A          R MM0019R2                  TEXT('DETAILS FORMAT')
     A            RRUSER    R              1REFFLD(JOUSER)
     A            RRTYPE         3        15TEXT('CHANGE TYPE')
     A                                    35'BEFORE'
     A*                                                   */048630/*
     A*******     RRBRCB    R             47REFFLD(BRCA)
     A            RRBRCB    R             47REFFLD(BRNA)
     A*                                                   */048630/*
     A            RRCNMB    R             61REFFLD(CNMA)
     A            RRBKRB    R             75REFFLD(BKRA)
     A            RRBKGB    R             87REFFLD(BKGA)
     A            RRDLSB    R             99REFFLD(DLSA)
     A            RRINTB    R            115REFFLD(ITDA)
     A                                      SPACEA(1)
     A*
     A            RRPGMA    R              1REFFLD(JOPGM)
     A            RRTIME    R             13REFFLD(JOTIME)
     A                                      EDTWRD('0 :  :  ')
     A            RRJOB     R             23REFFLD(JOJOB)
     A                                    35'AFTER'
     A*                                                   */048630/*
     A*******     RRBRCA    R             47REFFLD(BRCA)
     A            RRBRCA    R             47REFFLD(BRNA)
     A*                                                   */048630/*
     A            RRCNMA    R             61REFFLD(CNMA)
     A            RRBKRA    R             75REFFLD(BKRA)
     A            RRBKGA    R             87REFFLD(BKGA)
     A            RRDLSA    R             99REFFLD(DLSA)
     A            RRINTA    R            115REFFLD(ITDA)
     A*
     A**  SPACE AFTER ONLY IF NO RECOVERY ERROR OR PROGRAM NAME ERROR
     A**  WAS DETECTED.
     A N64N68                               SPACEA(3)
     A*
     A        68                           1'*********'
     A  68                                  SPACEB(1)
     A N64                                  SPACEA(3)
     A*
     A**  OUTPUT ERROR LINE - SPACE BEFORE ONLY IF PROGRAM NAME WAS
     A**  CORRECT & PROGRAM NAME NOT UNDERLINED.
     A        64                          13'*****  THIS ACTION IS +
     A                                      NOT REFLECTED IN THE DATABASE DUE +
     A                                      TO RECOVERY PROCESSING  *****'
     A  64N68                               SPACEB(1)
     A  64                                  SPACEA(3)
     A*
     A*
     A**  FORMAT FOR END OF REPORT OR NO DETAILS MESSAGE
     A          R MM0019R3                  TEXT('END OF REPORT MESSAGE')
     A*                                              Begin /S01319/
     A****N67**************************** 44'** NO DETAILS TO REPORT **'
     A****N67*****************************  SPACEB(3)
     A****N67*****************************  SPACEA(3)
     A*                                              End   /S01319/
     A     67                             48'*** END OF REPORT ***'
     A*
     A*                                              Begin /S01319/
     A****FORMAT*FOR*END*OF*REPORT*SUMMARY*IF NO. OF RECORDS IS NOT 1.
     A**********R*MM0019R8******************TEXT('CONTROL TOTALS FORM')
     A**************************************SKIPB(10)
     A************************************11'FILE NAME :'
     A************************************23'MMCDAFLL'
     A**************************************SPACEA(2)
     A**************************************
     A************************************11'NUMBER OF RECORDS ON THE FILE'
     A************RRNREC*********9**0*****47EDTCDE(3)
     A**************************************
     A*****65*****************************71'** DIFFERENCE **'
     A**************************************
     A**************************************SPACEA(2)
     A**************************************
     A****PRINT*THIS*MESSAGE*IF*AN*EXCEPTION ITEM IS ON DETAIL REPORT
     A**************************************
     A********66**************************11'***** AN EXCEPTION ITEM APPEARS  +
     A**************************************ON THE DETAIL AUDIT REPORT +
     A**************************************ASSOCIATED WITH THIS CONTROL +
     A**************************************REPORT *****'
     A********66****************************SPACEA(2)
     A**************************************
     A************************************51'*** END OF REPORT ***'
     A**************************************SPACEB(3)
     A*                                              End   /S01319/
     A*
     A**  DATABASE ERROR MESSAGE.
     A
     A          R MM0019R9                  TEXT('DATABASE ERROR MSG')
     A                                      SKIPB(12)
     A                                    41'DATABASE ERROR'
     A            RRDBSE         3        56TEXT('LDA SUBFIELD:DBASE')
     A                                    60'IN PROGRAM'
     A*                                              Begin /S01319/
     A************RRDPGM*** R*************71REFFLD(RRPGM *SRC)
     A            RRDPGM        10        71TEXT('PROGRAM NAME')
     A*                                              End   /S01319/
     A                                      SPACEA(1)
     A*
     A                                    41'FILE'
     A            RRDBFL         8        46TEXT('LDA SUBFIELD:DBFILE')
     A                                    55'KEY'
     A            RRDKEY        29        59TEXT('LDA SUBFIELD:DBKEY')
     A                                    48'***END OF REPORT***'
     A*  Midas - Money Market (Dealing)
