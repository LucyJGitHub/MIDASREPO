      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MM Effective Interest Rate Details Report')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  PRTF/MM004620P1 - Effective Interest Rate Details Report     *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      * -------------------------------*
      ** Report Header Record Format 1 *
      *--------------------------------*
      *
     A          R MM004620H1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A                                      SKIPB(3)
     A                                     6'Reference MM004620P1'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    40'EFFECTIVE INTEREST RATE DETAILS'
     A                                    +1'(NON-LINEAR)'
     A                                      SPACEA(1)
     A                                    40'-------------------------------'
     A                                    +0'-------------'
     A                                      SPACEA(2)
     A  37                                 6'BRANCH:'
     A  37        RBRCA          3A       +1TEXT('BRANCH')
     A  37        RBRNM         30A       +2TEXT('BRANCH DESCRIPTION')
     A  37                                  SPACEA(1)
      *
      *--------------------------------*
      ** Report Detail Record Format 1 *
      *--------------------------------*
      *
     A          R MM004620D1
      *
     A                                      SPACEB(2)
     A                                     2'Deal Number'
     A                                    15'Currency'
     A                                    31'Current Balance'
     A                                    54'Effective Interest Rate'
     A                                      SPACEA(2)
      *
     A            RDLNO          6A        5TEXT('DEAL NUMBER')
     A            RCCY           3A       18TEXT('CURRENCY')
     A            RCBAL         21A       28TEXT('CURRENT BALANCE')
     A            REIR          21A       55TEXT('EIR')
     A                                      SPACEA(2)
      *
     A                                     4'Net Present Value'
     A                                    32'Amortised Cost'
     A                                    50'Total Fees/Discounts/Premiums'
     A                                    81'Total Adj. To Yield Fees'
     A                                   109'Amortised F/D/P to date'
     A                                      SPACEA(2)
      *
     A            RNPV          21A        2TEXT('NPV')
     A            RACST         21A       28TEXT('AMORTISED COST')
     A            RTFDP         21A       55TEXT('TFDP')
     A            RTFDPA        21A       82TEXT('TFDPA')
     A            RAFDP         21A      110TEXT('AMORTISED FDP')
     A                                      SPACEA(1)
      *
      *------------------------------*
      ** Report Footer Record Format *
      *------------------------------*
      *
     A          R MM004620F1
     A                                      SPACEB(3)
      *
     A  37                                48'*** END OF REPORT FOR BRANCH'
     A  37        RBRCA          3A       +1TEXT('BRANCH')
     A  37                                +1'***'
     A N37                                51'*** END OF REPORT ***'
