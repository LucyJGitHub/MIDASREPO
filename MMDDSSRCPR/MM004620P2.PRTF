      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MM EIR & Cash Flows Details Report')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  PRTF/MM004620P2 - EIR & Cash Flows Details Report            *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG9667            Date 29Dec05               *
      *                 CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  Bug9667 - Incorrect report details.                          *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      * -------------------------------*
      ** Report Header Record Format 2 *
      *--------------------------------*
      *
     A          R MM004620H2                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A                                      SKIPB(3)
     A                                     6'Reference MM004620P2'
     A            BJURPT    R             40REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    35'EFFECTIVE INTEREST RATE CASH FLOWS'
     A                                    +1'DETAILS (NON-LINEAR)'
     A                                      SPACEA(1)
     A                                    35'----------------------------------'
     A                                    +0'---------------------'
     A                                      SPACEA(2)
     A  37                                 6'BRANCH:'
     A  37        RBRCA          3A       +1TEXT('BRANCH')
     A  37        RBRNM         30A       +2TEXT('BRANCH DESCRIPTION')
     A  37                                  SPACEA(1)
      *
      *--------------------------------*
      ** Report Detail Record Format 2 *
      *--------------------------------*
      *
     A          R MM004620D2
      *
     A                                      SPACEB(2)
     A                                     2'Deal Number'
     A                                    15'Currency'
     A                                    31'Current Balance'
     A                                    54'Effective Interest Rate'
     A                                      SPACEA(2)
      *
     A            RDLNO          6A        5TEXT('DEAL NUMBER')
     A            RCCY           3A       18TEXT('CURRENCY')
     A            RCBAL         21A       28TEXT('CURRENT BALANCE')
     A            REIR          21A       55TEXT('EIR')
     A                                      SPACEA(2)
      *
     A                                     4'Deal'
     A                                    12'Part'
     A                                    19'Customer'
     A**********                          30'Days to End'                                    Bug9667
     A                                    31'Cash Flow'                                      Bug9667
     A                                    49'Cash Flow'
     A                                    66'Amount'
     A                                    77'Interest'
     A                                    89'Calculation'
     A                                   102'Yield'
     A                                   112'Discount'
     A                                      SPACEA(1)
      *
     A                                     4'Type'
     A                                    12'Sold'
     A                                    20'Number'
     A**********                          32'of EIR'                                         Bug9667
     A                                    33'Date'                                           Bug9667
     A                                    50'Amount'
     A                                    67'Type'
     A                                    79'Rate'
     A                                    91'Method'
     A                                   102'Curve'
     A                                   113'Factor'
     A                                      SPACEA(2)
      *
      *--------------------------------*
      ** Report Detail Record Format 3 *
      *--------------------------------*
      *
     A          R MM004620D3
      *
     A            RDTYP          6A        3TEXT('DEAL')
     A            RSOLD          6A       11TEXT('PART')
     A            RCNUM          6A       20TEXT('CUSTOMER')
     A**********  RDEND          4A       33TEXT('DAYS TO END')                              Bug9667
     A**********  RDENDS         1A       37TEXT('DAYS TO END SIGN')                         Bug9667
     A            RDATE          7A       32TEXT('CASHFLOW DATE')                            Bug9667
     A            RCSFL         21A       43TEXT('CASH FLOW AMOUNT')
     A            RTYPE          4A       67TEXT('AMOUNT TYPE')
     A            RINTR         14A       74TEXT('INTEREST')
     A            RCALC          1A       93TEXT('CALCULATION')
     A            RYIELD         5A      102TEXT('YIELD')
     A            RDISC         14A      109TEXT('DISCOUNT')
     A                                      SPACEA(1)
      *
      *------------------------------*
      ** Report Footer Record Format *
      *------------------------------*
      *
     A          R MM004620F2
     A                                      SPACEB(3)
      *
     A  37                                48'*** END OF REPORT FOR BRANCH'
     A  37        RBRCA          3A       +1TEXT('BRANCH')
     A  37                                +1'***'
     A N37                                51'*** END OF REPORT ***'
