      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MM Non-Linear Fee Amortisation Report')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  PRTF/MM004650P1 - Non-Linear Fee Amortisation Report         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG9356            Date 11Dec05               *
      *  Prev Amend No. CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG9356- Extended Deal Sub Type (CDL038)                     *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A                                      INDARA
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE MM004650P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    37'FEE ACCOUNT KEYS GENERATED +
     A                                      (NON-LINEAR)'
     A                                      SPACEA(1)
      *
     A                                    37'---------------------------+
     A                                      ------------'
     A                                      SPACEA(2)
      *
     A  32                                 6'BRANCH:'
     A  32        A8BRCD    R             15REFFLD(BRCA)
     A  32        A8BRNM    R             19REFFLD(BRNM)
     A  32                                  SPACEA(1)
      *
     A                                    10'CUSTOMER'
     A                                    20'DEAL NO'
     A                                    29'FEE SEQ'
     A                                    38'ACCOUNT KEY'
     A                                    59'AMOUNT'
     A                                    74'CURRENCY'
     A                                    84'REV IND'
     A                                      SPACEA(1)
      *
     A                                    10'--------'
     A                                    20'-------'
     A                                    29'-------'
     A                                    38'-----------'
     A                                    59'------'
     A                                    74'--------'
     A                                    84'-------'
     A                                      SPACEA(1)
      *
      *-----------------------*
      ** Detail Record Format *
      *-----------------------*
      *
     A          R DETAIL11
      *
     A            RCNUM          6        11
     A            RDEAL          6        20
     A            RFSEQ          2        31
      **Start of change BUG9356
     A            RAKEY         14        36                                                 BUG9356
     A************RAKEY*********10        38                                                 BUG9356
      **End of change BUG9356
     A            RAMNT         21        51
     A            RCCY           3        76
     A            RRIND          1        87
     A                                      SPACEA(1)
      *
     A          R DETAIL12
      *
     A                                      SPACEB(3)
     A                                    32'TOTAL ACCOUNT KEYS GENERATED'
     A            RCOUNTF        5  0     67EDTCDE(3)
     A          R DETAIL13
      *
     A                                      SPACEB(3)
     A                                    52'***  NO DETAILS TO REPORT  ***'
      *
      *
      *-----------------*
      ** Report Trailer *
      *-----------------*
      *
     A          R TRAILP1
     A                                      SPACEB(3)
     A N32                                54'***  END OF REPORT  ***'
     A  32                                47'***  END OF REPORT FOR BRANCH'
     A  32        A8BRCD    R             77REFFLD(BRCA)
     A  32                                82'***'
