      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MM Non-Linear Discount Amortisation Report')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  PRTF/MM004650P2 - Non-Linear Discount Amortisation Report    *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG9356            Date 11Dec05               *
      *                 CAS009  *CREATE    Date 04May04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG9356- Extended Deal Sub Type (CDL038)                     *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *------------------------------*
      ** Report Header Record Format *
      *------------------------------*
      *
     A                                      INDARA
     A          R HEADP2                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE MM004650P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    37'DEAL ACCOUNT KEYS GENERATED +
     A                                      (NON-LINEAR)'
     A                                      SPACEA(1)
     A                                    37'----------------------------+
     A                                      ------------'
     A                                      SPACEA(2)
     A  32                                 6'BRANCH:'
     A  32        A8BRCD    R             15REFFLD(BRCA)
     A  32        A8BRNM    R             19REFFLD(BRNM)
     A  32                                  SPACEA(1)
      *
     A                                    10'CUSTOMER'
     A                                    20'DEAL NO'
     A                                    29'ACCOUNT KEY'
     A                                    52'AMOUNT'
     A                                    65'CURRENCY'
     A                                    75'REV IND'
     A                                      SPACEA(1)
      *
     A                                    10'--------'
     A                                    20'-------'
     A                                    29'-----------'
     A                                    52'------'
     A                                    65'--------'
     A                                    75'-------'
     A                                      SPACEA(1)
      *
      *-----------------------*
      ** Detail Record Format *
      *-----------------------*
      *
     A          R DETAIL21
      *
      *
     A            RCNUM          6        11
     A            RDLNO          6        20
      **Start of change BUG9356
     A            RAKEY         14        27                                                 BUG9356
     A************RAKEY*********10        29                                                 BUG9356
      **End of change BUG9356
     A            RAMNT         21        42
     A            RCCY           3        69
     A            RRIND          1        78
     A                                      SPACEA(1)
      *
     A          R DETAIL22
      *
     A                                      SPACEB(3)
     A                                    32'TOTAL ACCOUNT KEYS GENERATED'
     A            RCOUNTD        5  0     67EDTCDE(3)
     A          R DETAIL23
      *
     A                                      SPACEB(3)
     A                                    52'***  NO DETAILS TO REPORT  ***'
      *
      *
      *-----------------*
      ** Report Trailer *
      *-----------------*
      *
     A          R TRAILP2
     A                                      SPACEB(3)
     A N32                                54'***  END OF REPORT  ***'
     A  32                                47'***  END OF REPORT FOR BRANCH'
     A  32        A8BRCD    R             77REFFLD(BRCA)
     A  32                                82'***'
