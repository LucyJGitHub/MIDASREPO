      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MM Revaluation Using All-In-Rate - Report')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Dealing Module                          *
      *                                                               *
      *  MM4410P1 - MM Revaluation using All-In-Rate A/C Key          *
      *             Generation Report                                 *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 CAS005  *CREATE    Date 16Dec02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R MM4410D1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE MM4410P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
      *
      *
     A                                    23'MONEY MARKET REVALUATION USING +
     A                                      ALL-IN RATE ACCOUNT KEY GENERATION'
     A                                    +1'- UP TO'
     A            REVCD          7A       +1TEXT('Event Control Date')
     A                                      SPACEA(1)
      *
     A                                    23'----------------------------+
     A                                      -----------------------------+
     A                                      ------------------------'
     A                                      SPACEA(2)
      *
     A                                    23'BRANCH: '
     A            RBRCA          3A       +1TEXT('Branch')
     A            RBRNM         30A       +2TEXT('Branch Description')
     A                                      SPACEA(2)
      *
     A                                    23'CUSTOMER'
     A                                    36'DEAL NO.'
     A                                    49'ACCOUNT KEY'
     A                                    71'AMOUNT'
     A                                    88'CURRENCY'
     A                                   101'REV IND'
     A                                      SPACEA(2)
      *
     A          R MM4410D2
      *
     A            RCNUM          6A       24TEXT('Customer Number')
     A            RDLNO          6A       37TEXT('Deal Number')
      **Start of change CDL038
     A************RACKY         10A       50TEXT('Account Key')                               CDL038
     A            RACKY         14A       49TEXT('Account Key')                               CDL038
      **End of change CDL038
     A            RYAMT         18A       65TEXT('Deal Number')
     A            RCYCD2         3A       91TEXT('Currency')
     A            RREVI          1A      104TEXT('Reversal Indicator')
     A                                      SPACEA(1)
      *
     A          R MM4410D3
      *
     A                                      SPACEB(1)
      *
     A                                    50'TOTAL ACCOUNT KEYS GENERATED'
     A            RTOTAL         5S 0     82TEXT('Total A/C Keys Generated')
     A                                      SPACEA(2)
      *
     A          R MM4410D4
      *
     A                                      SPACEB(3)
      *
     A                                    46'***  End of Report for +
     A                                      Branch  ***'
      *
