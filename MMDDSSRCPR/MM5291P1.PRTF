      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXI *  TEXT('Midas MM Penalty Termination Report Details')
      *****************************************************************
      *                                                               *
      *  MIDAS¦DBA - Dealing Money Market Module                      *
      *                                                               *
      *  PRTF/MM5291P1 - Penalty Termination Reporting File           *
      *                                                               *
      *  (C) Copyright Finastra International Limited 2008            *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. 256795  *CREATE    Date 25Sep08               *
      *                 XXXXXX             Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  256795 - Penalty Termination of Time Deposits (CDL052).      *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      ** Heading
     A          R MM5291H1                  TEXT('(c) Finastra International -
     A                                      NG SYSTEMS LTD 2008')
      *
     A                                      SKIPB(3)
     A                                     4'REFERENCE MM5291P1'
     A            H1URPT    R             40REFFLD(URPT)
     A                                   109'DATE'
     A            H1MRDT    R            114REFFLD(MRDT)
     A                                   123'PAGE'
     A                                   127PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                    34'PENALTY TERMINATION OF TIME DEPO+
     A                                      SITS -'
     A                                      SPACEB(1)
     A            H1MODE         5A  O    73
     A                                    79'REPORT'
     A  40                                88'BRANCH'
     A  40        P1BRCA    R             95REFFLD(BRCA)
     A  40                                99'-'
     A  40        P1BRNM    R            101REFFLD(BRNM)
     A                                    34'--------------------------------+
     A                                      -------------------'
     A                                      SPACEB(1)
     A                                      SPACEA(2)
      *
      ** Sub-Heading
     A          R MM5291S1
     A                                     4'DEAL'
     A                                    13'DEAL TYPE/'
     A                                    25'BOOK'
     A                                    33'VALUE'
     A                                    42'NEW/OLD'
     A                                    52'CCY'
     A                                    68'AMOUNT'
     A                                    76'BASE'
     A                                    82'SPREAD/RATE'
     A                                   105'INTEREST'
     A                                   116'INSERT/AMEND'
     A                                      SPACEA(1)
     A                                     3'NUMBER'
     A                                    14'SUBTYPE'
     A                                    25'CODE'
     A                                    34'DATE'
     A                                    41'MATURITY'
     A                                    76'RATE'
     A                                   117'AUTHORISED'
     A                                      SPACEA(2)
      *
      ** Detail
     A          R MM5291D1
     A                                      SPACEB(1)
     A            P1DLNO         6A  O     3
     A            P1DTYP         2A  O    15
     A            P1STYP         2A  O    19
     A            P1BOOK         2A  O    26
     A            P1VDAT         6A  O    33
     A            P1MDAT         6A  O    42
     A            P1CCY          3A  O    52
     A            P1PCPL        17A  O    57
     A            P1BRTT         2A  O    77
     A            P1DLRT        11A  O    82
     A            P1PINT        17A  O    96
     A            P1USRI        10A  O   117
     A                                      SPACEA(1)
     A            P1PMDT         6A  O    42
     A                                    68'PENALTY RATE'
     A            P1PENR        11A  O    82
     A  50                                  HIGHLIGHT
     A            P1INTA        17A  O    96
     A            P1USRX        10A  O   117
     A                                      SPACEA(1)
     A                                    68'ADJUSTMENT'
     A            P1ADJA        17A  O    96
     A                                      SPACEA(1)
      *
      ** Trailer
     A          R MM5291T1                  SPACEB(3)
     A N05 40                             48'***  END OF REPORT FOR BRANCH'
     A N05 40     P1BRCA    R             78REFFLD(BRCA)
     A N05 40                             83'***'
     A N05N40                             55'***  END OF REPORT  ***'
     A  05                               120'CONT...'
      *
