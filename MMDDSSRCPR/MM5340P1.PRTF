      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MM Deposits Collateral Released Exception P1')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  PRTF/MM5340P1 - MM Deposits Collateral Released Exceptions   *
      *                  Report                                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CGL011  *CREATE    Date 20Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CGL011 - Collateral Processing for Midas                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE MM5340P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'MM Deposits Collateral +
     A                                      Released Exceptions Report'
     A                                      SPACEA(1)
      *
     A                                    46'-----------------------+
     A                                      --------------------------'
     A                                      SPACEA(2)
      *
      ** Sub-Heading
      *
     A                                     2'|------------------------+
     A                                      SECURITY DETAILS---------+
     A                                      ----------------'
     A                                    68'|--COLLATERAL DETAILS--|'
     A                                    92'-----------ALLOCATION +
     A                                      DETAILS-----------|'
     A                                      SPACEA(2)
      *
      ** Detail Sub-Headings
      *
     A                                     3'Deal'
     A                                    14'Deal'
     A                                    32'Deal'
     A                                    52'Collateral'
     A                                    70'Collateral'
     A                                    81'Collateral'
     A                                    92'Module'
     A                                   103'Transaction Ref/'
     A                                   123'Allocation'
     A                                      SPACEA(1)
      *
     A                                     2'Number'
     A                                    12'Customer'
     A                                    31'Amount'
     A                                    54'Amount'
     A                                    70'Reference'
     A                                    81'Customer'
     A                                    92'Secured'
     A                                   105'Account No.'
     A                                   124'Customer'
     A                                      SPACEA(2)
      *
      ** Detail Format1
      *
     A          R DETAIL1
      *
     A                                      SPACEB(1)
     A            RDLNO          6         2TEXT('Deal Number')
     A  23        RDELET         1         9TEXT('Delete Indicator')
     A            RCSSN         10        11TEXT('Deal Customer')
     A            RPCPL         23        22TEXT('Deal Amount')
     A            RCOLA         23        46TEXT('Collateral Amount')
     A            RCREF         10        70TEXT('Collateral Reference')
     A            RCNUM         10        81TEXT('Collateral Customer')
     A**Start of change CGL029
     A**********  RMODS          2        94TEXT('Module Secured')                            CGL029
     A**********  RTRANS        22       100TEXT('Transaction Ref/Acct. No.')                 CGL029
     A            RMODS          2        92TEXT('Module Secured')
     A            RTRANS        27        95TEXT('Transaction Ref/Acct. No.')
     A**End of change CGL029
     A            RCUST         10       123TEXT('Allocation Customer')
     A                                      SPACEA(1)
      *
      ** Detail Format2
      *
     A          R DETAIL2
      *
     A            RCREF         10        70TEXT('Collateral Reference')
     A            RCNUM         10        81TEXT('Collateral Customer')
     A**Start of change CGL029
     A**********  RMODS          2        94TEXT('Module Secured')                            CGL029
     A**********  RTRANS        22       100TEXT('Transaction Ref/Acct. No.')                 CGL029
     A            RMODS          2        92TEXT('Module Secured')
     A            RTRANS        27        95TEXT('Transaction Ref/Acct. No.')
     A**End of change CGL029
     A            RCUST         10       123TEXT('Allocation Customer')
     A                                      SPACEA(1)
      *
      ** Detail Format3
      *
     A          R DETAIL3
      *
     A**Start of change CGL029
     A**********  RMODS          2        94TEXT('Module Secured')                            CGL029
     A**********  RTRANS        22       100TEXT('Transaction Ref/Acct. No.')                 CGL029
     A            RMODS          2        92TEXT('Module Secured')
     A            RTRANS        27        95TEXT('Transaction Ref/Acct. No.')
     A**End of change CGL029
     A            RCUST         10       123TEXT('Allocation Customer')
     A                                      SPACEA(1)
      *
      ** Report Trailer
      *
     A          R TRAILP1
      *
     A                                    55'***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
