      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MM Deposit Deals Automatically Rolled Over')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  PRTF/MM5350AU - Deposit Deals Automatically Rolled Over      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CGL011  *CREATE    Date 20Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL011 - Collateral Processing for Midas                     *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU
      *
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE MM5350AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    35'DEPOSIT DEALS HELD AS COLLATERAL -
     A                                      AUTOMATICALLY ROLLED OVER AUDIT'
     A                                      SPACEA(1)
     A                                    35'----------------------------------
     A                                      -------------------------------'
     A                                      SPACEA(4)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
      *
     A                                    70'NUMBER                  VALUE'
     A                                      SPACEA(1)
     A                                    70'------           -------------
     A                                      -------'
     A                                      SPACEA(2)
      *
     A                                    29'MM Deals File - MMTTOTPP'
     A                                      SPACEA(1)
     A                                    29'Records Before update'
     A            RNORE          5  0     71EDTCDE(3)
     A                                      SPACEA(1)
     A                                    29'Records Inserted'
     A            RNINS          5  0     71EDTCDE(3)
     A                                      SPACEA(1)
     A                                    29'Records After Update'
     A            RNTOT          5  0     71EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                    29'Deals File - DEALSDF'
     A                                      SPACEA(1)
     A                                    29'Number of Records Before Update'
     A            RNORED         5  0     71EDTCDE(3)
     A            RVLBF         15  0     88EDTCDE(3)
     A            RVLBL          3  0    103
     A                                      SPACEA(1)
     A                                    29'Number of Records Inserted'
     A            RNINSD         5  0     71EDTCDE(3)
     A            RVRIF         15  0     88EDTCDE(3)
     A            RVRIL          3  0    103
     A                                      SPACEA(1)
     A                                    29'Number of Records After Update'
     A            RNLRA          5  0     71EDTCDE(3)
     A            RVLAF         15  0     88EDTCDE(3)
     A            RVLAL          3  0    103
     A                                      SPACEA(2)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
      *
     A                                    53'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
      *
     A                                    55'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    40'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    40'*'
     A                                      SPACEB(1)
     A                                    93'*'
     A                                    40'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       59
     A                                    67''''
     A                                    93'*'
     A                                    40'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        59
     A                                    88 '''    *'
     A                                    40'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       59
     A                                    69''''
     A                                    93'*'
     A                                    40'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     59
     A                                    62''''
     A                                    93'*'
     A                                    40'*'
     A                                      SPACEB(1)
     A                                    93'*'
     A                                    40'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
