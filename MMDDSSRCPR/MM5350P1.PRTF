      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MM Deposit Deals Automatically Rolled Over')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Module                                  *
      *                                                               *
      *  PRTF/MM5350P1 - Deposit Deals Automatically Rolled Over      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. CGL011  *CREATE    Date 20Sep99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CGL011 - Collateral Processing for Midas                     *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
      *
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6
     A                                      'REFERENCE MM5350P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101
     A                                      'TIME'
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A                                    34
     A                                      'DEPOSIT DEALS HELD AS COLLATERAL -
     A                                      AUTOMATICALLY ROLLED OVER REPORT'
     A                                      SPACEA(001)
      *
     A                                    34
     A                                      '----------------------------------
     A                                      --------------------------------'
     A                                      SPACEA(003)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A                                     6'DEAL'
     A                                    14'BRCH'
     A                                    20'CUSTOMER'
     A                                    30'TYPE/'
     A                                    37'BASE'
     A                                    43'RATE/'
     A                                    57'ROLLOVER'
     A                                    67'VALUE'
     A                                    75'MATURITY'
     A                                    85'BASE'
     A                                    91'RATE/'
     A                                   105'CCY'
     A                                   110'PRINCIPAL'
     A                                      SPACEA(001)
      *
     A                                     6'NUMBER'
     A                                    14'CODE'
     A                                    20'NUMBER'
     A                                    30'STYPE'
     A                                    37'RATE'
     A                                    43'SPREAD'
     A                                    57'DEAL'
     A                                    67'DATE'
     A                                    75'DATE'
     A                                    85'RATE'
     A                                    91'SPREAD'
     A                                   110'AMOUNT'
     A                                      SPACEA(001)
      *
     A                                     6'------'
     A                                    14'----'
     A                                    20'--------'
     A                                    30'-----'
     A                                    37'----'
     A                                    43'------------'
     A                                    57'--------'
     A                                    67'------'
     A                                    75'--------'
     A                                    85'----'
     A                                    91'------------'
     A                                   105'---'
     A                                   110'-----------------------'
     A                                      SPACEA(002)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RDN38          6         6TEXT('Deal Number')
     A            RBRCA          3        14TEXT('Branch Code')
     A            RCNUM          6        20TEXT('Customer Number')
     A            RTYPE          2        30TEXT('Type')
     A                                    32'/'
     A            RSTYP          2        33TEXT('Sub-type')
     A            RBSRC          2        37TEXT('Base Rate')
     A            RSPRD         12        43TEXT('Rate/Spread')
     A            RDEAL          6        57TEXT('Rollover Deal')
     A            RVDAT          6        67TEXT('Value Date')
     A            RMDAT          6        75TEXT('Maturity Date')
     A            RBASE          2        85TEXT('Base Rate')
     A            RRATE         12        91TEXT('Rate/Spread')
     A            RCCY           3       105TEXT('Currency')
     A            RAMNP         23       110TEXT('Principal Amount')
     A                                      SPACEA(001)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
      *
     A                                    55'***  END OF REPORT  ***'
     A                                      SPACEB(003)
      *
