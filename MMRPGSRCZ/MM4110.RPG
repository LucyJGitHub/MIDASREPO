     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MM Cash flow revaluation')                       *
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Dealing Module                          *
      *                                                               *
      ***MM4110*-*CASH*FLOW*REVALUATION********************************                       CAS005
      *  MM4110 - Midas MM Cash Flow Using Net-Treasury-Rate Revals   *                       CAS005
      *                                                               *
      *  Function:  This program revalues the cash flow for each      *
      *             money market deal.                                *
      *                                                               *
      *  NOTE: Check MM4310 for changes that may apply to it if there *                       CAS005
      *        are amendments done to this program.                   *                       CAS005
      *                                                               *                       CAS005
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *  Last Amend No. CGL165             Date 17Feb15               *
      *  Prev Amend No. CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      *                 CDL093             Date 08Apr14               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 230229             Date 25Sep06               *
      *                 CDL049             Date 11Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 BUG9561            Date 10Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CAS009             Date 04May04               *
      *                 CDL033             Date 10Feb05               *
      *                 CAS010             Date 09Feb05               *
      *                 228545             Date 23Aug04               *
      *                 CDL028             Date 07Feb05               *
      *                 CGL029             Date 01Sep03               *
      *                 223622             Date 09Dec03               *
      *                 CGL029             Date 01Sep03               *
      *                 CAS005             Date 16Dec02               *
      *                 212416             Date 26Sep02               *
      *                 CAS004             Date 26Jun02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CDL093 - MM Interest calculation type 9.                     *
      *  230229 - Do not need to use I6 record as it is covered       *
      *           by the M2 event for call deal.                      *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG9561 - Rename field FSOYLC for the rolledover deal.       *
      *  CDL038 - Extended Deal Sub Type                              *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *  CDL033 - Floating Rate CDs Issued                            *
      *  CAS010 - IAS + PB Enhancements Upgrade to Midasplus          *
      *           Partially applied 228545. No need to define DSSDY,  *
      *           already defined by CGL029                           *
      *  228545 - Event was posted on a holiday instead of next       *
      *           working day                                         *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  223622 - Incorrect deal number is stored in MMCFLxPD. Thus,  *
      *           causing MMC4210 and MMC4410 to fall over.           *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *  212416 - Declaration of field is wrong (Recompile)           *
      *  CAS004 - Hedge Accounting Phase A                            *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
     FDEALSL  IF  E           K        DISK
     F            DEALSDAF                          KIGNORE
     F            DEALSDBF                          KIGNORE
     F            DEALSDFF                          KIGNORE
     F            DEALSDGF                          KIGNORE
     FDLEVNPL3IF  E           K        DISK
      ** Midas DL Events by Deal Number
      *
     FDLEVNPL5IF  E           K        DISK                                                   CAS004
     F            EVNTXEDF                          KRENAMEEVNTXEF                            CAS004
      ** Midas DL Events by Deal Number for MM Cash Flow Revaluations                         CAS004
      *                                                                                       CAS004
     FDEALSC  IF  E           K        DISK
     F            DEALSDCF                          KRENAMEDEALSCF
     FDLDLDDL1IF  E           K        DISK
     F            DEALSDDF                          KRENAMEDEALSDF
      ** Negotiable Assets Purchased by Sold Number
      *
     FMMCFLWPDO   E           K        DISK
      ** Midas MM Cash Flows Using Net-Treasury-Rate File                                     CAS005
      ***Money*Market*Cash*Flows*File**********************************                       CAS005
     FDLFEEDL1IF  E           K        DISK                                                   CAS009
      ** Dealing Fees File                                                                    CAS009
      *
     FMM4110AUO   E                    PRINTER                        UC
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZFRPEDZ1
      /COPY ZSRSRC,ZHOLE                                                                      228545
     E/COPY ZSRSRC,ZDATE9Z1                                                                   CDL093
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *  23     - READ Indicator for DEALSL                           *
      *  24     - CHAIN Indicator for DEALSDCF Format                 *
      *  25     - CHAIN/READE Indicator for DLEVNPL3                  *
      *  26     - CHAIN Indicator for DLDLDDL1                        *
      *  27     - CHAIN Indicator for DLFEEDL1                        *                       CAS009
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRINIT - Zero Out All Fields in MMCFLWPD                     *
      *  SRDEAL - Process Transaction Details                         *
      *  SREVNT - Process Event Details                               *
      *  SRCALC - Calculate Discount Factor                           *
      *                                                               *
      *  *INZSR - Program Initialisation Routine                      *
      *  *PSSR  - Program Exception Error Routine                     *
      *                                                               *
      *****************************************************************
     IDEALSDF
     I              RCDT                            DRCDT                                     223622
     I              DLNO                            DDLNO
     I              DTYP                            DDTYP
     I              DLST                            DDLST
     I              BOKC                            DBOKC
     I              CLAS                            DCLAS                                     CDL038
     IDEALSCF
     I              RECI                            RRECI
     I              DLNO                            RDLNO
     I              DTYP                            RDTYP
     I              DLST                            RDLST
     I              INTR                            RINTR
     I              CNUM                            RCNUM
     I              BOKC                            RBOKC
     I              RODN                            RRODN
     I              CCY                             RCCY
     I              BRCA                            RBRCA
     I              ICBS                            RICBS
     I              MDAT                            RMDAT
     I              CLAS                            RCLAS                                     CDL038
     I              FSOYLC                          ROYLC                                    BUG9561
     IDEALSDEF
     I              OSACQQ                          OSAC12
      *
     ISDBANK    E DSSDBANKPD
      ** Bank Details File
      *
     ISDDEAL    E DSSDDEALPD
      ** Dealing ICD File
      *
     ISDCURR    E DSSDCURRPD                                                                  CAS009
      ** Currency Details Data Structure                                                      CAS009
      *                                                                                       CAS009
     IDSFDY     E DSDSFDY
      ** First DS for access programs, short data structure
      *
     ISDGELR    E DSSDGELRPD                                                                  CAS004
      ** General Ledger Details accessed via access program                                   CAS004
     I              QQACCD                          QQACD1                                    CGL029
      *                                                                                       CAS004
     ISCSARD    E DSSCSARDPD                                                                  CAS004
      ** Switchable Features Details accessed via access program                              CAS004
      *                                                                                       CAS004
     I/COPY ZSRSRC,ZFRPEDZ2
      *                                                                                       CGL029
     IDSSDY     E DSDSSDY                                                                     CGL029
      *                                                                                       CGL029
      /COPY ZSRSRC,ZHOLI                                                                      228545
     I/COPY ZSRSRC,ZDATE9Z2                                                                   CDL093
      *****************************************************************
      *                                                               *
      *  Main Processing                                              *
      *                                                               *
      *****************************************************************
      *
      ** Process details
      *
     C           *LOVAL    SETLLDEALSL
      *
     C                     READ DEALSL                   23
      *
     C           *IN23     DOWEQ*OFF
      *                                                                                       CAS005
     C           CAS005    IFEQ 'N'                                                           CAS005
      *
     C           FSOYLC    IFNE *BLANKS
      *
      ** Initialise fields
      *
     C                     EXSR SRINIT
      *
     C                     EXSR SRDEAL
     C                     EXSR SREVNT
      *
     C                     ENDIF
      *                                                                                       CAS005
     C                     ELSE                                                               CAS005
     C                     EXSR SRINIT                                                        CAS005
     C                     EXSR SRDEAL                                                        CAS005
     C           FSOYLC    IFNE *BLANKS                                                       CAS005
     C           RCDT      OREQ 'E'                                                           223622
     C                     EXSR SREVNT                                                        CAS005
     C                     ELSE                                                               CAS005
     C                     WRITEMMCFLWD0                                                      CAS005
     C                     ENDIF                                                              CAS005
     C                     ENDIF                                                              CAS005
      *
     C                     READ DEALSL                   23
      *
     C                     ENDDO
      *
      ** Return to calling program
      *
     C                     RETRN
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT - Zero Out All Fields in MMCFLWPD                     *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
     C                     CLEARMMCFLWD0
      *
     C/COPY WNCPYSRC,MM4110C001
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRDEAL - Process Transaction Details                         *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRDEAL    BEGSR
      *
     C                     Z-ADDDLNO      MCDLNO
     C                     MOVE DTYP      MCDTYP
     C                     MOVE DLST      MCDLST
     C                     MOVE CLAS      MCCLAS                                              CDL038
     C                     MOVE BOKC      MCBKCD                                              CAS004
     C                     MOVE CCY       MCCYCD                                              CAS004
     C                     MOVE BRCA      MCBRCA                                              CAS004
     C                     MOVE ' '       WROLL   1
      *
     C           RCDT      IFEQ 'C'
     C           RODN      ANDNE0
      *
     C           RODN      CHAINDEALSC               24
      *
     C           *IN24     IFEQ *OFF
     C           RRECI     ANDEQ'D'
     C                     MOVE 'Y'       WROLL
     C                     Z-ADDRODN      MCDLNO
     C                     Z-ADDDLNO      MCADLN
     C                     MOVE RBOKC     MCBKCD                                              CAS004
     C                     MOVE RCCY      MCCYCD                                              CAS004
     C                     MOVE RBRCA     MCBRCA                                              CAS004
     C                     MOVE RDTYP     MCDTYP
     C                     MOVE RDLST     MCDLST
     C                     MOVE RCLAS     MCCLAS                                              CDL038
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** If CAS005 is set on, consider liquidity premium                                      CAS005
      *                                                                                       CAS005
     C           CAS005    IFEQ 'Y'                                                           CAS005
     C           FSLQPS    IFEQ '-'                                                           CAS005
     C           INTR      SUB  FSLQPR    INTR                                                CAS005
     C                     ELSE                                                               CAS005
     C           INTR      ADD  FSLQPR    INTR                                                CAS005
     C                     ENDIF                                                              CAS005
     C                     ENDIF                                                              CAS005
      *                                                                                       CAS005
     C                     Z-ADDINTR      MCIRAT
     C**********           Z-ADDCNUM      MCCNUM                                              CSD027
     C                     MOVE CNUM      MCCNUM                                              CSD027
     C**********           MOVE BOKC      MCBKCD                                              CAS004
     C**********           MOVE CCY       MCCYCD                                              CAS004
     C**********           MOVE BRCA      MCBRCA                                              CAS004
      *
      ** Retrieve Negotiable Asset Purchased Info                                             223622
      *                                                                                       223622
     C           RCDT      IFEQ 'E'                                                           223622
     C           RPDN      CHAINDLDLDDL1             26                                       223622
     C           *IN26     IFEQ *ON                                                           223622
     C                     MOVELRPDN      DBKEY                                               223622
     C                     MOVEL'DLDLDDL1'DBFILE                                              223622
     C                     Z-ADD3         DBASE                                               223622
     C                     EXSR *PSSR                                                         223622
     C                     ENDIF                                                              223622
     C                     ENDIF                                                              223622
      *                                                                                       223622
     C                     MOVE FSOYLC    MCYLDC
     C                     Z-ADDICBS      MCICBS
      *
      ** Calculation Methods are:
      *
     C                     SELEC
      *
      ** 1  Actual/365
      ** 4  365/365 (excluding 29/02)
      *
     C           ICBS      WHEQ 1
     C           ICBS      OREQ 4
     C                     Z-ADD365       MCNDYY
      *
      ** 2  Actual/360
      ** 3  360/360 (sometimes called 30/360)
      ** 5  365/360 (excluding 29/02)
      *
     C           ICBS      WHEQ 2
     C           ICBS      OREQ 3
     C           ICBS      OREQ 5
     C                     Z-ADD360       MCNDYY
      *
      ** 6  Actual/366
      *
     C           ICBS      WHEQ 6
     C                     Z-ADD366       MCNDYY
      *
     C                     ENDSL
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SREVNT - Process Event Details                               *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: ZDATE2, SRCALC, *PSSR                                 *
      *                                                               *
      *****************************************************************
      *
     C           SREVNT    BEGSR
      *
     C           CAS004    IFEQ 'Y'                                                           CAS004
     C           DLNO      CHAINDLEVNPL5             25                                       CAS004
     C                     ELSE                                                               CAS004
     C           DLNO      CHAINDLEVNPL3             25
     C                     ENDIF                                                              CAS004
      *
     C           *IN25     DOWEQ*OFF
      *                                                                                       CAS005
     C           CAS005    IFEQ 'Y'                                                           CAS005
     C                     MOVE MCCYCD    ZCCY                                                228545
     C           NOTD      IFNE 0                                                             CAS005
     C           CAS004    IFEQ 'Y'                                                           CAS005
      *                                                                                       228545
      ** Use ZFWDT such that NOTD will count working days                                     228545
      *                                                                                       228545
     C********** EVCD      ADD  NOTD      WMDAT                                        CAS005 228545
     C                     Z-ADDEVCD      ZDAYNO                                              228545
     C                     Z-ADDNOTD      ZNRDYS                                              228545
     C                     EXSR ZFWDT                                                         228545
     C                     Z-ADDZDYNBR    WMDAT                                               228545
      *                                                                                       228545
     C                     ELSE                                                               CAS005
      *                                                                                       228545
     C********** BJRDNB    ADD  NOTD      WMDAT                                        CAS005 228545
     C                     Z-ADDBJRDNB    ZDAYNO                                              228545
     C                     Z-ADDNOTD      ZNRDYS                                              228545
     C                     EXSR ZFWDT                                                         228545
     C                     Z-ADDZDYNBR    WMDAT                                               228545
      *                                                                                       228545
     C                     ENDIF                                                              CAS005
     C                     ELSE                                                               CAS005
     C           CAS004    IFEQ 'Y'                                                           CAS005
      *                                                                                       228545
     C********** EVCD      ADD  BNDCMT    WMDAT                                        CAS005 228545
     C                     Z-ADDEVCD      ZDAYNO                                              228545
     C                     Z-ADDBNDCMT    ZNRDYS                                              228545
     C                     EXSR ZFWDT                                                         228545
     C                     Z-ADDZDYNBR    WMDAT                                               228545
      *                                                                                       228545
     C                     ELSE                                                               CAS005
     C********** BJRDNB    ADD  BNDCMT    WMDAT                                        CAS005 228545
     C                     Z-ADDBJRDNB    ZDAYNO                                              228545
     C                     Z-ADDBNDCMT    ZNRDYS                                              228545
     C                     EXSR ZFWDT                                                         228545
     C                     Z-ADDZDYNBR    WMDAT                                               228545
      *                                                                                       228545
     C                     ENDIF                                                              CAS005
     C                     ENDIF                                                              CAS005
     C                     ENDIF                                                              CAS005
      *
     C                     MOVE *BLANK    MCFTYP
     C           CAS004    IFEQ 'Y'                                                           CAS004
     C           IBRE      ANDEQ'Y'                                                           CAS004
     C           DTYP      ANDNE'IL'                                                          CAS004
     C           DTYP      ANDNE'ID'                                                          CAS004
     C                     ELSE                                                               CAS004
      *
     C                     SELEC
      *
      ** Start Date Cash Flow
      ** ____________________
      *
     C           ETYP      WHEQ '15V1'
     C           ETYP      OREQ '16V1'
     C           ETYP      OREQ '17V1'
     C           ETYP      OREQ '17V3'
      *
     C           WROLL     IFEQ 'Y'
     C                     MOVE 'ROLL'    MCFTYP
     C                     ELSE
     C                     MOVE 'SAMT'    MCFTYP
     C                     ENDIF
      *
     C           ETYP      IFEQ '17V1'
     C           DTYP      IFEQ 'C2'
     C           DTYP      OREQ 'BD'
     C           DTYP      OREQ 'BP'
     C           DTYP      OREQ 'TB'
     C           DTYP      OREQ 'DA'
     C           DTYP      OREQ 'TA'
     C                     MOVE *BLANK    MCFTYP
     C                     ENDIF
     C                     ENDIF
      *
      ** Principal Increase
      ** __________________
      **
      ** Only include if date falls within "No. of Days to Call
      ** Maturity Date on Dealing ICD"
      *
     C           ETYP      WHEQ '15V3'
     C           EDAT      ANDLEWMDAT
     C           ETYP      OREQ '16V3'
     C           EDAT      ANDLEWMDAT
      *
     C           WROLL     IFEQ 'Y'
     C                     MOVE 'RPIN'    MCFTYP
     C                     ELSE
     C                     MOVE 'PINC'    MCFTYP
     C                     ENDIF
      *
      ** Principal Decrease
      ** __________________
      **
      ** Only include if date falls within "No. of Days to Call
      ** Maturity Date on Dealing ICD"
      *
     C           ETYP      WHEQ '15M4'
     C           EDAT      ANDLEWMDAT
     C           ETYP      OREQ '16M4'
     C           EDAT      ANDLEWMDAT
      *
     C           WROLL     IFEQ 'Y'
     C                     MOVE 'RPDC'    MCFTYP
     C                     ELSE
     C                     MOVE 'PDEC'    MCFTYP
     C                     ENDIF
      *
      ** Interest Payment Date
      ** _____________________
      *
     C           ETYP      WHEQ '15I2'
     C           CAS005    ANDEQ'N'                                                           CAS005
     C           ETYP      OREQ '16I2'
     C           CAS005    ANDEQ'N'                                                           CAS005
     C           ETYP      OREQ '17I2'
     C           CAS005    ANDEQ'N'                                                           CAS005
     C           ETYP      OREQ '15I6'                                                        CAS005
     C           CAS005    ANDEQ'Y'                                                           CAS005
     C           MDAT      ANDNE0                                                             230229
     C           ETYP      OREQ '16I6'                                                        CAS005
     C           CAS005    ANDEQ'Y'                                                           CAS005
     C           MDAT      ANDNE0                                                             230229
     C           ETYP      OREQ '17I6'                                                        CAS005
     C           CAS005    ANDEQ'Y'                                                           CAS005
     C           MDAT      ANDNE0                                                             230229
      *
     C           WROLL     IFEQ 'Y'
     C                     MOVE 'RINT'    MCFTYP
     C                     ELSE
     C                     MOVE 'INTP'    MCFTYP
     C                     ENDIF
      *
      ** Negotiable Asset Sold
      ** _____________________
      *
     C           ETYP      WHEQ '18M1'
     C           CAS005    ANDEQ'N'                                                           CAS005
     C           ETYP      OREQ '18M6'                                                        CAS005
     C           CAS005    ANDEQ'Y'                                                           CAS005
      *
     C                     MOVE 'NASS'    MCFTYP
      *
      ***Retrieve*Negotiable*Asset*Purchased*Info**********************                       223622
      *
     C********** RPDN      CHAINDLDLDDL1             26                                       223622
     C********** *IN26     IFEQ *ON                                                           223622
     C**********           MOVELRPDN      DBKEY                                               223622
     C**********           MOVEL'DLDLDDL1'DBFILE                                              223622
     C**********           Z-ADD3         DBASE                                               223622
     C**********           EXSR *PSSR                                                         223622
     C**********           ENDIF                                                              223622
      *
     C                     Z-ADDRPDN      MCDLNO
     C                     Z-ADDDLNO      MCADLN
     C                     MOVE DDTYP     MCDTYP
     C                     MOVE DDLST     MCDLST
     C                     MOVE DCLAS     MCCLAS                                              CDL038
     C                     MOVE DBOKC     MCBKCD
      *
      ** Maturity Date Principal
      ** _______________________
      *
     C           ETYP      WHEQ '15M1'
     C           CAS005    ANDEQ'N'                                                           CAS005
     C           ETYP      OREQ '16M1'
     C           CAS005    ANDEQ'N'                                                           CAS005
     C           ETYP      OREQ '17M1'
     C           CAS005    ANDEQ'N'                                                           CAS005
     C           ETYP      OREQ '15M6'                                                        CAS005
     C           CAS005    ANDEQ'Y'                                                           CAS005
     C           ETYP      OREQ '16M6'                                                        CAS005
     C           CAS005    ANDEQ'Y'                                                           CAS005
     C           ETYP      OREQ '17M6'                                                        CAS005
     C           CAS005    ANDEQ'Y'                                                           CAS005
      *
     C           WROLL     IFEQ 'Y'
     C                     MOVE 'RMAT'    MCFTYP
     C                     ELSE
     C                     MOVE 'MAMT'    MCFTYP
     C                     ENDIF
      *
     C           MDAT      IFEQ 0
     C                     Z-ADDWMDAT     EDAT
     C                     ENDIF
      *
      ** Maturity Date Interest
      ** ______________________
      *
     C           ETYP      WHEQ '15M2'
     C           CAS005    ANDEQ'N'                                                           CAS005
     C           ETYP      OREQ '16M2'
     C           CAS005    ANDEQ'N'                                                           CAS005
     C           ETYP      OREQ '17M2'
     C           CAS005    ANDEQ'N'                                                           CAS005
     C           ETYP      OREQ '15M8'                                                        CAS005
     C           CAS005    ANDEQ'Y'                                                           CAS005
     C           ETYP      OREQ '16M8'                                                        CAS005
     C           CAS005    ANDEQ'Y'                                                           CAS005
     C           ETYP      OREQ '17M8'                                                        CAS005
     C           CAS005    ANDEQ'Y'                                                           CAS005
      *
     C           WROLL     IFEQ 'Y'
     C                     MOVE 'RMIN'    MCFTYP
     C                     ELSE
     C                     MOVE 'MINT'    MCFTYP
     C                     ENDIF
      *
     C           MDAT      IFEQ 0
     C                     Z-ADDWMDAT     EDAT
     C                     ENDIF
      *                                                                                       CAS009
      ** Fees                                                                                 CAS009
      ** ____                                                                                 CAS009
      *                                                                                       CAS009
     C           ETYP      WHEQ '15F1'                                                        CAS009
     C           CAS009    ANDEQ'Y'                                                           CAS009
     C           ETYP      OREQ '16F1'                                                        CAS009
     C           CAS009    ANDEQ'Y'                                                           CAS009
     C           ETYP      OREQ '17F1'                                                        CAS009
     C           CAS009    ANDEQ'Y'                                                           CAS009
     C                     EXSR SRFEE                                                         CAS009
      *
     C                     ENDSL
      *                                                                                       CAS004
     C                     ENDIF                                                              CAS004
      *
     C           MCFTYP    IFNE *BLANK
      *
      ** If CAS009 is enabled and the fee detail found is in a currency                       CAS009
      ** different from that of the loan currency, convert the cash flow                      CAS009
      ** amount to loan currency and use this value to update the output                      CAS009
      ** field.                                                                               CAS009
      *                                                                                       CAS009
     C           CAS009    IFEQ 'Y'                                                           CAS009
     C           WFEED     ANDEQ'Y'                                                           CAS009
     C           DFFCCY    ANDNECCY                                                           CAS009
     C                     EXSR SRCCFA                                                        CAS009
     C                     Z-ADDWCAMT     MCCAMT                                              CAS009
     C                     ELSE                                                               CAS009
     C                     Z-ADDEAMT      MCCAMT
     C                     ENDIF                                                              CAS009
     C                     MOVE 'N'       WFEED                                               CAS009
      *
     C                     MOVE INOI      MCIOIN
      *
     C                     Z-ADDEDAT      MCFLDT
      *
     C                     MOVE *BLANK    MCDATE
     C                     Z-ADDEDAT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    MCDATE
      *                                                                                       CAS004
     C           CAS004    IFEQ 'Y'                                                           CAS004
     C           EDAT      SUB  EVCD      MCNDYP                                              CAS004
     C                     ELSE                                                               CAS004
     C           EDAT      SUB  BJRDNB    MCNDYP
     C                     ENDIF                                                              CAS004
      *                                                                                       CDL093
      ** Set Start and End for calculation type 9                                             CDL093
      *                                                                                       CDL093
     C                     Z-ADDBJRDNB    ZZBEG9  50                                          CDL093
     C                     Z-ADDEDAT      ZZEND9  50                                          CDL093
     C           CAS004    IFEQ 'Y'                                                           CDL093
     C                     Z-ADDEVCD      ZZBEG9                                              CDL093
     C                     END                                                                CDL093
      *                                                                                       CDL093
     C           ICBS      IFEQ 9                                                             CDL093
      *                                                                                       CDL093
      ** Convert start date to YYYYMMDD format                                                CDL093
      *                                                                                       CDL093
     C                     Z-ADDZZBEG9    @@DAYN  50                                          CDL093
     C                     EXSR ZDATE9                                                        CDL093
     C                     Z-ADD@@VDT     ZZDATA  80                                          CDL093
     C                     Z-ADD@@YR      ZZYY1   40                                          CDL093
      *                                                                                       CDL093
      ** Convert end date to YYYYMMDD format                                                  CDL093
      *                                                                                       CDL093
     C                     Z-ADDZZEND9    @@DAYN                                              CDL093
     C                     EXSR ZDATE9                                                        CDL093
     C                     Z-ADD@@VDT     ZZDATB  80                                          CDL093
     C                     Z-ADD@@YR      ZZYY2   40                                          CDL093
      *                                                                                       CDL093
     C                     EXSR #FEB29                                                        CDL093
      *                                                                                       CDL093
     C           #29FEB    IFEQ 'Y'                                                           CDL093
     C                     Z-ADD366       MCNDYY                                              CDL093
     C                     ELSE                                                               CDL093
     C                     Z-ADD365       MCNDYY                                              CDL093
     C                     END                                                                CDL093
      *                                                                                       CDL093
     C                     END                                                                CDL093
      *                                                                                       CDL033
      ** Override rate from DEALS file with latest rate for                                   CDL033
      ** the event.                                                                           CDL033
      *                                                                                       CDL033
     C           CDL033    IFEQ 'Y'                                                           CDL033
     C           MCDTYP    IFEQ 'CF'                                                          CDL033
     C           MCDTYP    OREQ 'CI'                                                          CDL033
     C           MCDTYP    OREQ 'IP'                                                          CDL033
     C           MCDTYP    OREQ 'IT'                                                          CDL033
     C           MCDTYP    OREQ 'TI'                                                          CDL033
     C           MCDTYP    OREQ 'TD'                                                          CDL033
     C                     Z-ADDEXRT      MCIRAT                                              CDL033
     C                     ENDIF                                                              CDL033
     C                     ENDIF                                                              CDL033
      *
     C                     EXSR SRCALC
     C                     MOVE PDSCF     MCDSCF
      *
     C           MCCAMT    MULT MCDSCF    MCNPVA    H
     C/COPY WNCPYSRC,MM4110C002
      *
     C                     WRITEMMCFLWD0
      *
     C                     ENDIF
      *
     C           CAS004    IFEQ 'Y'                                                           CAS004
     C           DLNO      READEDLEVNPL5                 25                                   CAS004
     C                     ELSE                                                               CAS004
     C           DLNO      READEDLEVNPL3                 25
     C                     ENDIF                                                              CAS004
      *
     C                     ENDDO
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRCALC - Calculate Discount Factor                           *
      *                                                               *
      *  Called By: SREVNT                                            *
      *                                                               *
      *  Calls: ZADSFCAL                                              *
      *                                                               *
      *****************************************************************
      *
     C           SRCALC    BEGSR
      *
     C                     MOVE ICBS      PICBS
      *
     C                     CALL 'ZADSFCAL'
     C                     PARM *BLANKS   PRTCD
     C                     PARM *BLANKS   PNRTC   5
     C                     PARM MCYLDC    PCYLDC  5
     C                     PARM MCCYCD    PCCYCD  3
     C                     PARM           PICBS   1
     C                     PARM MCFLDT    PCFLDT  50
     C                     PARM *ZERO     PDSCF  139
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************                       CAS009
      *                                                               *                       CAS009
      *  SRFEE - Set Cash Flow type according to adjustment to yeild  *                       CAS009
      *          and Payment indicator.                               *                       CAS009
      *                                                               *                       CAS009
      *  Called By: SREVNT                                            *                       CAS009
      *                                                               *                       CAS009
      *  Calls:                                                       *                       CAS009
      *                                                               *                       CAS009
      *****************************************************************                       CAS009
      *                                                                                       CAS009
     C           SRFEE     BEGSR                                                              CAS009
      *                                                                                       CAS009
     C                     Z-ADDDASN      WDASN   20                                          CAS009
      *                                                                                       CAS009
     C           KDEAL     CHAINDLFEEDL1             27                                       CAS009
      *                                                                                       CAS009
     C           *IN27     IFEQ '0'                                                           CAS009
      *                                                                                       CAS009
     C                     Z-ADDDFFSEQ    MCFSEQ                                              CAS009
      *                                                                                       CAS009
     C                     SELEC                                                              CAS009
      *                                                                                       CAS009
     C           DFADJY    WHEQ 'Y'                                                           CAS009
     C           DFPYON    ANDEQ'E'                                                           CAS009
     C           DFADJY    OREQ 'Y'                                                           CAS009
     C           DFPYON    ANDEQ'N'                                                           CAS009
     C                     MOVE 'EFAC'    MCFTYP                                              CAS009
      *                                                                                       CAS009
     C           DFADJY    WHEQ 'Y'                                                           CAS009
     C           DFPYON    ANDEQ'S'                                                           CAS009
     C                     MOVE 'SFAM'    MCFTYP                                              CAS009
      *                                                                                       CAS009
     C           DFADJY    WHEQ 'Y'                                                           CAS009
     C           DFPYON    ANDEQ' '                                                           CAS009
     C                     MOVE 'SFBT'    MCFTYP                                              CAS009
      *                                                                                       CAS009
     C           DFADJY    WHNE 'Y'                                                           CAS009
     C           DFPYON    ANDEQ'E'                                                           CAS009
     C           DFADJY    ORNE 'Y'                                                           CAS009
     C           DFPYON    ANDEQ'N'                                                           CAS009
     C                     MOVE 'EFAN'    MCFTYP                                              CAS009
      *                                                                                       CAS009
     C           DFADJY    WHNE 'Y'                                                           CAS009
     C           DFPYON    ANDEQ'S'                                                           CAS009
     C                     MOVE 'SFAN'    MCFTYP                                              CAS009
      *                                                                                       CAS009
     C           DFADJY    WHNE 'Y'                                                           CAS009
     C           DFPYON    ANDEQ' '                                                           CAS009
     C                     MOVE 'SFBN'    MCFTYP                                              CAS009
      *                                                                                       CAS009
     C                     ENDSL                                                              CAS009
      *                                                                                       CAS009
      ** Set the fee detail flag.                                                             CAS009
      *                                                                                       CAS009
     C                     MOVE 'Y'       WFEED   1                                           CAS009
      *                                                                                       CAS009
     C                     ENDIF                                                              CAS009
      *                                                                                       CAS009
     C                     ENDSR                                                              CAS009
      *****************************************************************                       CAS009
      /EJECT                                                                                  CAS009
      *****************************************************************                       CAS009
      *                                                               *                       CAS009
      *  SRCCFA - Converts the cash flow amount to loan currency.     *                       CAS009
      *                                                               *                       CAS009
      *****************************************************************                       CAS009
     C           SRCCFA    BEGSR                                                              CAS009
      *                                                                                       CAS009
      ** Amount to convert.                                                                   CAS009
      *                                                                                       CAS009
     C                     Z-ADDEAMT      PZAMTF                                              CAS009
      *                                                                                       CAS009
      ** 'From' currency details.                                                             CAS009
      *                                                                                       CAS009
     C                     MOVE DFFCCY    PCURR                                               CAS009
     C                     EXSR SRCURR                                                        CAS009
     C                     MOVE DFFCCY    PZCCYF                                              CAS009
     C                     Z-ADDA6SPRT    PZRATF                                              CAS009
     C                     MOVE A6MDIN    PZMDIF                                              CAS009
     C                     Z-ADDA6NBDP    PZCDPF                                              CAS009
      *                                                                                       CAS009
      ** 'To' currency details.                                                               CAS009
      *                                                                                       CAS009
     C                     MOVE CCY       PCURR                                               CAS009
     C                     EXSR SRCURR                                                        CAS009
     C                     MOVE CCY       PZCCYT                                              CAS009
     C                     Z-ADDA6SPRT    PZRATT                                              CAS009
     C                     MOVE A6MDIN    PZMDIT                                              CAS009
     C                     Z-ADDA6NBDP    PZCDPT                                              CAS009
      *                                                                                       CAS009
      ** Convert amount to target currency.                                                   CAS009
      *                                                                                       CAS009
     C                     CALL 'ZCCYCN'                                                      CAS009
     C                     PARM           PZAMTF 150                                          CAS009
     C                     PARM           PZCCYF  3                                           CAS009
     C                     PARM           PZCCYT  3                                           CAS009
     C                     PARM           PZRATF 138                                          CAS009
     C                     PARM           PZRATT 138                                          CAS009
     C                     PARM           PZMDIF  1                                           CAS009
     C                     PARM           PZMDIT  1                                           CAS009
     C                     PARM           PZCDPF  10                                          CAS009
     C                     PARM           PZCDPT  10                                          CAS009
     C                     PARM *ZERO     PZAMTT 150                                          CAS009
     C                     PARM *ZERO     PZCRSR 138                                          CAS009
     C                     PARM *ZERO     PZCRSI  1                                           CAS009
      *                                                                                       CAS009
      ** Set the cash flow amount.                                                            CAS009
      *                                                                                       CAS009
     C                     Z-ADDPZAMTT    WCAMT                                               CAS009
      *                                                                                       CAS009
     C                     ENDSR                                                              CAS009
      *****************************************************************                       CAS009
      /EJECT                                                                                  CAS009
      *****************************************************************                       CAS009
      *                                                               *                       CAS009
      *  SRCURR - Gets currency details.                              *                       CAS009
      *                                                               *                       CAS009
      *****************************************************************                       CAS009
     C           SRCURR    BEGSR                                                              CAS009
      *                                                                                       CAS009
     C                     CALL 'AOCURRR0'                                                    CAS009
     C                     PARM *BLANKS   PRTCD                                               CAS009
     C                     PARM '*KEY   ' POPTN                                               CAS009
     C                     PARM           PCURR   3                                           CAS009
     C           SDCURR    PARM SDCURR    DSSDY                                               CAS009
      *                                                                                       CAS009
     C           PRTCD     IFNE *BLANKS                                                       CAS009
     C                     MOVEL'SDCURRPD'DBFILE                                              CAS009
     C                     MOVELPCURR     DBKEY                                               CAS009
     C                     Z-ADD101       DBASE                                               CAS009
     C                     EXSR *PSSR                                                         CAS009
     C                     ENDIF                                                              CAS009
      *                                                                                       CAS009
     C                     ENDSR                                                              CAS009
      *                                                                                       CAS009
      *****************************************************************                       CAS009
      /EJECT                                                                                  CAS009
      *****************************************************************
      *                                                               *
      *  *INZSR - Program Initialisation Routine                      *
      *                                                               *
      *  Called By: Implicitly on program activation                  *
      *                                                               *
      *  Calls: *PSSR                                                 *
      *                                                               *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Read RUNDAT for multi-branching indicator
      *
     C                     IN   RUNDAT
     C           *NAMVAR   DEFN           RUNDAT 13
      *                                                                                       CAS009
      ** Define necessary fields.                                                             CAS009
      *                                                                                       CAS009
     C           *LIKE     DEFN MCCAMT    WCAMT                                               CAS009
      *
     C                     MOVEL'MM4110'  DBPGM
      *
      ** Access Bank Details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST  'POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Database Error
      *
     C           PRTCD     IFNE *BLANKS
     C                     MOVELPOPTN     DBKEY
     C                     MOVEL'SDBANKPD'DBFILE
     C                     Z-ADD1         DBASE
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *
      ** Access Dealing ICD
      *
     C**********           CALL 'AODEALR0'                                                    CGL029
     C                     CALL 'AODEALR1'                                                    CGL029
     C                     PARM '*DBERR ' PRTCD
     C                     PARM '*FIRST ' POPTN
     C********** SDDEAL    PARM SDDEAL    DSFDY                                               CGL029
     C           SDDEAL    PARM SDDEAL    DSSDY                                               CGL029
      *
      ** Database Error
      *
     C           PRTCD     IFNE *BLANKS
     C                     MOVELPOPTN     DBKEY
     C                     MOVEL'SDDEALPD'DBFILE
     C                     Z-ADD2         DBASE
     C                     EXSR *PSSR
     C                     ENDIF
      *                                                                                       CAS004
      ** Call access program for General Ledger details.                                      CAS004
      *                                                                                       CAS004
     C**********           CALL 'AOGELRR0'                                             CAS004 CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*DBERR ' PRTCD                                               CAS004
     C                     PARM '*FIRST ' POPTN                                               CAS004
     C********** SDGELR    PARM SDGELR    DSFDY                                        CAS004 CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *                                                                                       CAS004
      ** Database Error                                                                       CAS004
      *                                                                                       CAS004
     C           PRTCD     IFNE *BLANK                                                        CAS004
     C                     MOVEL'SDGELRPD'DBFILE                                              CAS004
     C                     Z-ADD3         DBASE                                               CAS004
     C                     MOVELPOPTN     DBKEY                                               CAS004
     C                     EXSR *PSSR                                                         CAS004
     C                     ENDIF                                                              CAS004
      *                                                                                       CAS004
      ** Check if Hedge Accounting is installed                                               CAS004
      *                                                                                       CAS004
     C                     CALL 'AOSARDR0'                                                    CAS004
     C                     PARM *BLANKS   PRTCD                                               CAS004
     C                     PARM '*VERIFY' POPTN                                               CAS004
     C                     PARM 'CAS004'  PSARD   6                                           CAS004
     C           SCSARD    PARM SCSARD    DSFDY                                               CAS004
      *                                                                                       CAS004
     C           PRTCD     IFEQ *BLANKS                                                       CAS004
     C                     MOVE 'Y'       CAS004  1                                           CAS004
     C                     ELSE                                                               CAS004
     C                     MOVE 'N'       CAS004                                              CAS004
     C           PRTCD     IFNE '*NRF   '                                                     CAS004
     C                     MOVE 'SCSARDPD'DBFILE                                              CAS004
     C                     Z-ADD4         DBASE                                               CAS004
     C                     MOVEL'CAS004'  DBKEY                                               CAS004
     C                     EXSR *PSSR                                                         CAS004
     C                     ENDIF                                                              CAS004
     C                     ENDIF                                                              CAS004
      *                                                                                       CAS005
      ** Check if Enhancements to CAS001, CAS002 and CAS004 is installed                      CAS005
      *                                                                                       CAS005
     C                     CALL 'AOSARDR0'                                                    CAS005
     C                     PARM *BLANKS   PRTCD                                               CAS005
     C                     PARM '*VERIFY' POPTN                                               CAS005
     C                     PARM 'CAS005'  PSARD                                               CAS005
     C           SCSARD    PARM SCSARD    DSFDY                                               CAS005
      *                                                                                       CAS005
     C           PRTCD     IFEQ *BLANKS                                                       CAS005
     C                     MOVE 'Y'       CAS005  1                                           CAS005
     C                     ELSE                                                               CAS005
     C                     MOVE 'N'       CAS005                                              CAS005
     C           PRTCD     IFNE '*NRF   '                                                     CAS005
     C                     MOVE 'SCSARDPD'DBFILE                                              CAS005
     C                     Z-ADD4         DBASE                                               CAS005
     C                     MOVEL'CAS005'  DBKEY                                               CAS005
     C                     EXSR *PSSR                                                         CAS005
     C                     ENDIF                                                              CAS005
     C                     ENDIF                                                              CAS005
      *                                                                                       CAS009
      ** Check if CAS009 - Effective Interest Rate/Amortised Cost Accounting                  CAS009
      ** is installed.                                                                        CAS009
      *                                                                                       CAS009
     C                     CALL 'AOSARDR0'                                                    CAS009
     C                     PARM *BLANKS   PRTCD                                               CAS009
     C                     PARM '*VERIFY' POPTN                                               CAS009
     C                     PARM 'CAS009'  PSARD                                               CAS009
     C           SCSARD    PARM SCSARD    DSFDY                                               CAS009
      *                                                                                       CAS009
     C           PRTCD     IFEQ *BLANKS                                                       CAS009
     C                     MOVE 'Y'       CAS009  1                                           CAS009
     C                     ELSE                                                               CAS009
     C                     MOVE 'N'       CAS009                                              CAS009
     C           PRTCD     IFNE '*NRF   '                                                     CAS009
     C                     MOVE 'SCSARDPD'DBFILE                                              CAS009
     C                     Z-ADD5         DBASE                                               CAS009
     C                     MOVEL'CAS009'  DBKEY                                               CAS009
     C                     EXSR *PSSR                                                         CAS009
     C                     ENDIF                                                              CAS009
     C                     ENDIF                                                              CAS009
      *
      ** If CAS004 is installed, set up Event Control Date. It is the                         CAS004
      ** lesser of either the (Next Working Day - 1) or the Accrual                           CAS004
      ** Profit Date                                                                          CAS004
      *                                                                                       CAS004
     C           CAS004    IFEQ 'Y'                                                           CAS004
      *                                                                                       CAS004
     C           BJDNWD    SUB  1         EVCD    50                                          CAS004
      *                                                                                       CAS004
     C           BKAPDT    IFLT EVCD                                                          CAS004
     C                     Z-ADDBKAPDT    EVCD                                                CAS004
     C                     ENDIF                                                              CAS004
      *                                                                                       CAS004
     C                     ENDIF                                                              CAS004
      *                                                                                       CAS004
      ** Setup Maturity Date
      *
     C           CAS004    IFEQ 'Y'                                                           CAS004
     C           EVCD      ADD  BNDCMT    WMDAT                                               CAS004
     C                     ELSE                                                               CAS004
     C           BJRDNB    ADD  BNDCMT    WMDAT   50
     C                     ENDIF                                                              CAS004
      *                                                                                       CDL033
      ** Determine if Floating Rate CD Issued feature is                                      CDL033
      ** switched on.                                                                         CDL033
      *                                                                                       CDL033
     C                     MOVE 'N'       CDL033  1                                           CDL033
     C                     CALL 'AOSARDR0'                                                    CDL033
     C                     PARM *BLANKS   PRTCD                                               CDL033
     C                     PARM '*VERIFY' POPTN                                               CDL033
     C                     PARM 'CDL033'  PSARD                                               CDL033
     C           SCSARD    PARM SCSARD    DSFDY                                               CDL033
      *                                                                                       CDL033
     C           PRTCD     IFEQ *BLANKS                                                       CDL033
     C                     MOVE 'Y'       CDL033                                              CDL033
     C                     ELSE                                                               CDL033
     C           PRTCD     IFNE '*NRF   '                                                     CDL033
     C                     MOVE '901'     DBASE                                               CDL033
     C                     MOVEL'CDL033'  DBKEY                                               CDL033
     C                     MOVEL'SCSARDPD'DBFILE                                              CDL033
     C                     EXSR *PSSR                                                         CDL033
     C                     ENDIF                                                              CDL033
     C                     ENDIF                                                              CDL033
      *
     C           KDEAL     KLIST                                                              CAS009
     C                     KFLD           DLNO                                                CAS009
     C                     KFLD           WDASN                                               CAS009
      *                                                                                       CAS009
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR - Program Exception Error Routine                      *
      *                                                               *
      *  Called By: *INZSR, SREVNT                                    *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
      ** Produce audit report
      *
     C                     OPEN MM4110AU
     C                     WRITEHEADAU
     C                     WRITEDBERROR
     C                     CLOSEMM4110AU
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZFRPEDZ3
     C/COPY ZSRSRC,ZFWDT                                                                      228545
     C/COPY ZSRSRC,ZACCH                                                                      228545
     C/COPY ZSRSRC,ZDATE9Z3                                                                   CDL093
     C/COPY ZSRSRC,ZFEB29                                                                     CDL093
     **                                                                                       CDL093
     *** CDL093: Added array @YD and @MD                                                      CDL093
** CPY@
(c) Misys International Banking Systems Ltd. 2001
** ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
** ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
** ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
** @YD  USED BY SR. ZDATE9 - YEARS, IN DAYS, CUMULATIVE IN 4 YEAR SPAN
00366007310109601461
** @MD  USED BY SR. ZDATE9 - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
00000000310005900090001200015100181002120024300273003040033400365
