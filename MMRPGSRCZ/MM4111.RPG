     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MM Money Cash Flows Report')
      *****************************************************************
      *                                                               *
      *  Midas - Money Market Dealing Module                          *
      *                                                               *
      ***MM4111*-*Money*Cash*Flows*Report******************************                       CAS005
      *  MM4111 - Midas MM Cash Flows Net-Treasury-Rate Report        *                       CAS005
      *                                                               *
      *  Function:  This program outputs the details from the Money   *
      *             Market Cash Flow File MMCFLWPD.                   *
      *                                                               *
      *  NOTE: Check MM4311 for changes that may apply to it if there *                       CAS005
      *        are amendments done to this program.                   *                       CAS005
      *                                                               *                       CAS005
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CAS009             Date 04May04               *
      *                 CGL029             Date 01Sep03               *
      *                 CAS005             Date 16Dec02               *
      *                 212420             Date 26Sep02               *
      *                 202416             Date 26Sep02               *
      *                 CAS004             Date 26Jun02               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAS001  *CREATE    Date 23Nov01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CAS009 - Effective Interest Rate/Amortised Cost Accounting   *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CAS005 - Enhancements to CAS001, CAS002 and CAS004           *
      *  212420 - Cashflow with zero amounts is being printed.        *
      *  202416 - Declaration of field is wrong (Recompile)           *
      *  CAS004 - Hedge Accounting Phase A                            *
      *  CAS001 - Net Present Value (NPV) Accounting                  *
      *                                                               *
      *****************************************************************
     FMMCFLWL1IF  E           K        DISK
      ** Money Market Cash Flows by Branch/Currency/Book/Deal Type/Deal No.
      *
     FMM4111P1O   E                    PRINTER      KINFDS SPOOL      UC
      ***Money*Market*Cash*Flow*Report*********************************                       CAS005
      ** Midas MM Cash Flows Net-Treasury-Rate Report                                         CAS005
      *
     FMM4111AUO   E                    PRINTER      KINFDS SPOOLU
      ***Money*Market*Cash*Flow*Audit*Report***************************                       CAS005
      ** Midas MM Cash Flows Net-Treasury-Rate Audit Report                                   CAS005
      *
      *****************************************************************
      /EJECT
      *****************************************************************
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZFRPEDZ1
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *  23     - READ Indicator for MMCFLWL1                         *
      *  24     - CAS004 Installed Indicator                          *                       CAS004
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  SRINIT - Initialisation Program                              *
      *  SRPRNT - Print Transaction Details                           *
      *  SRBLNK - Blank Out Detail Fields                             *
      *  SRLOAD - Load Detail Line                                    *
      *  SRAUDT - Print Cash Flow Audit Report                        *
      *                                                               *
      *  *INZSR - Program Initialisation Routine                      *
      *  *PSSR  - Program Exception Error Routine                     *
      *                                                               *
      *****************************************************************
     IWLVLDS      DS                              6
     I                                        1   1 WL6
     I                                        2   2 WL5
     I                                        3   3 WL4
     I                                        4   4 WL3
     I                                        5   5 WL2
     I                                        6   6 WL1
     ISPOOL       DS
      ** File Information Data Structure
      *
     I                                       83  92 SFILE
     I                                    B 123 1240SFNUM
     I                                    B 367 3680PRTLNE
     ISPOOLU      DS
      ** File Information Data Structure
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
     ISDBANK    E DSSDBANKPD
      ** Bank Details File
      *
     ISDBRCH    E DSSDBRCHPD
      ** Branch codes file
      *
     ISDCURR    E DSSDCURRPD
      ** Currencies file
      *
     IDSFDY     E DSDSFDY
      ** First DS for access programs, short data structure
      *
     IDSSDY     E DSDSSDY
      ** Second DS for access programs, long data structure
      *
     ISDGELR    E DSSDGELRPD                                                                  CAS004
      ** General Ledger Details accessed via access program                                   CAS004
      *                                                                                       CAS004
     ISCSARD    E DSSCSARDPD                                                                  CAS004
      ** Switchable Features Details accessed via access program                              CAS004
      *                                                                                       CAS004
     I/COPY ZSRSRC,ZFRPEDZ2
      *****************************************************************
      *                                                               *
      *  Main Processing                                              *
      *                                                               *
      *****************************************************************
      *
      ** Initialise fields
      *
     C                     EXSR SRINIT
      *
      ** Process details
      *
     C           *LOVAL    SETLLMMCFLWL1
      *
     C                     READ MMCFLWL1                 23
      *
     C           *IN23     IFEQ *ON
     C                     EXSR SRAUDT
     C                     ENDIF
      *
     C           *IN23     DOWEQ*OFF
      *
     C           MCFTYP    IFEQ 'EFAN'                                                        CAS009
     C           CAS009    ANDEQ'Y'                                                           CAS009
     C           MCFTYP    OREQ 'SFAN'                                                        CAS009
     C           CAS009    ANDEQ'Y'                                                           CAS009
     C           MCFTYP    OREQ 'SFBN'                                                        CAS009
     C           CAS009    ANDEQ'Y'                                                           CAS009
      *                                                                                       CAS009
     C                     ELSE                                                               CAS009
      *                                                                                       CAS009
     C                     EXSR SRPRNT
      *                                                                                       CAS009
     C                     ENDIF                                                              CAS009
      *
     C                     READ MMCFLWL1                 23
      *
     C                     ENDDO
      *
     C           WCOUNT    IFNE *ZERO
      *
      ** Write 'End of Report for Branch'
      *
     C           PRTLNE    IFGT 56
     C                     WRITEMM4111D1
     C                     ENDIF
      *
     C                     WRITEMM4111D4
      *
     C                     ENDIF
      *
      ** Return to calling program
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRINIT - Initialisation Routine                              *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: ZSFILE                                                *
      *                                                               *
      *****************************************************************
      *
     C           SRINIT    BEGSR
      *
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ
     C                     PARM *BLANK    PPARM   3
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANK    ZSERR   1
      *
      ** Error during ZSFILE processing, return to calling program
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
     C                     Z-ADD0         WCOUNT  30
      *
     C                     MOVE *BLANKS   WBRCA   3
     C                     MOVE *BLANKS   WCYCD   3
     C                     MOVE *BLANK    WBKCD   2
     C                     MOVE *BLANK    WDTYP   2
     C                     MOVE *BLANK    WDLST   2
     C                     MOVE *BLANK    WCLAS   4                                           CDL038
     C                     Z-ADD*ZERO     WDLNO   60
     C                     MOVE *BLANK    WLVLDS
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRPRNT - Print Transaction Details                           *
      *                                                               *
      *  Called By: Main Processing                                   *
      *                                                               *
      *  Calls: ZSFILE, SRBLNK, SRLOAD, *PSSR                         *
      *                                                               *
      *****************************************************************
      *
     C           SRPRNT    BEGSR
      *
      ** Determine which field has changed upon read to MMCFLWL1
      *
     C                     MOVE *ALL'N'   WLVLDS
      *
     C                     SELEC
      *
     C           MCBRCA    WHNE WBRCA
     C                     MOVEL'YYYYYY'  WLVLDS
     C           MCCYCD    WHNE WCYCD
     C                     MOVEL'NYYYYY'  WLVLDS
     C           MCBKCD    WHNE WBKCD
     C                     MOVEL'NNYYYY'  WLVLDS
     C           MCDTYP    WHNE WDTYP
     C                     MOVEL'NNNYYY'  WLVLDS
     C           MCDLST    WHNE WDLST
     C           MCCLAS    ORNE WCLAS                                                         CDL038
     C                     MOVEL'NNNNYY'  WLVLDS
     C           MCDLNO    WHNE WDLNO
     C                     MOVEL'NNNNNY'  WLVLDS
      *
     C                     ENDSL
      *
      ** At change of Branch
      *
     C           WL6       IFEQ 'Y'
      *
     C           WBRCA     IFNE *BLANK
     C           PRTLNE    IFGT 56
     C                     WRITEMM4111D1
     C                     ENDIF
     C                     WRITEMM4111D4
     C                     ENDIF
      *
      ** Ensure report spool file recorded by RCF
      ** transfer binary data into numeric field
      *
     C                     CLOSEMM4111P1
     C                     OPEN MM4111P1
      *
     C                     Z-ADDSFNUM     ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ
     C                     PARM MCBRCA    PPARM
     C                     PARM           SFILE
     C                     PARM           ZSNUM
     C                     PARM *BLANK    ZSERR
      *
      ** Error during ZSFILE processing, return to calling program
      *
     C           ZSERR     IFEQ 'Y'
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
     C                     RETRN
     C                     ENDIF
      *
      ** Initialise page number field
      *
     C                     Z-ADD*ZERO     PAGE
      *
      ** Use access program to read branch codes file
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM MCBRCA    PDSNB   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
     C           PRTCD     IFNE *BLANKS
     C                     MOVELMCBRCA    DBKEY
     C                     MOVEL'SDBRCHPD'DBFILE
     C                     Z-ADD2         DBASE
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVELMCBRCA    RBRCA
     C                     MOVELA8BRNM    RBRNM
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** At change of currency
      *
     C           WL5       IFEQ 'Y'
      *
     C                     CALL 'AOCURRR0'
     C                     PARM '*MSG   ' PRTCD
     C                     PARM '*KEY   ' POPTN
     C                     PARM MCCYCD    PAJCD   3
     C           SDCURR    PARM SDCURR    DSSDY
      *
     C           PRTCD     IFNE *BLANKS
     C                     MOVELMCCYCD    DBKEY
     C                     MOVEL'SDCURRPD'DBFILE
     C                     Z-ADD3         DBASE
     C                     EXSR *PSSR
     C                     ELSE
     C                     MOVELMCCYCD    RCYCD
     C                     MOVELA6CYNM    RCYNM
     C                     WRITEMM4111D1
     C                     ENDIF
      *
     C                     ENDIF
      *
      ** At change of deal number, skip line
      *
     C           WL1       IFEQ 'Y'
      *
     C           PRTLNE    IFGT 58
     C                     WRITEMM4111D1
     C                     ENDIF
      *
     C                     WRITEMM4111D3
     C                     ENDIF
      *
      ** Load details onto detail line
      *
     C                     EXSR SRBLNK
     C           MCCAMT    IFNE 0                                                             212420
     C                     EXSR SRLOAD
      *
     C           PRTLNE    IFGT 58
     C                     WRITEMM4111D1
     C                     ENDIF
      *
     C                     WRITEMM4111D2
     C                     ENDIF                                                              212420
      *
     C                     MOVELMCBRCA    WBRCA
     C                     MOVELMCCYCD    WCYCD
     C                     MOVE MCBKCD    WBKCD
     C                     MOVE MCDTYP    WDTYP
     C                     MOVE MCDLST    WDLST
     C                     MOVE MCCLAS    WCLAS                                               CDL038
     C                     Z-ADDMCDLNO    WDLNO
      *
     C           MCCAMT    IFNE 0                                                             212420
     C                     ADD  1         WCOUNT
     C                     ENDIF                                                              212420
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRBLNK - Blank Out Detail Fields                             *
      *                                                               *
      *  Called By: SRPRNT                                            *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRBLNK    BEGSR
      *
     C                     MOVE *BLANKS   RBKCD
     C                     MOVE *BLANKS   RDTYP
     C*********************MOVE *BLANKS   RDLST                                               CDL038
     C                     MOVE *BLANKS   RDSTC                                               CDL038
     C                     MOVE *BLANKS   RDLNO
     C                     MOVE *BLANKS   RADLN
     C                     MOVE *BLANKS   RCNUM
     C                     MOVE *BLANKS   RDATE
     C                     MOVE *BLANKS   RNDYP
     C                     MOVE *BLANKS   RCAMT
     C                     MOVE *BLANKS   RFTYP
     C                     MOVE *BLANKS   RIRAT
     C                     MOVE *BLANKS   RYLDC
     C                     MOVE *ZEROS    RICBS
     C                     MOVE *BLANKS   RDSCF
     C                     MOVE *BLANKS   RNPVA
      *
     C                     ENDSR
      *
      *****************************************************************
      *                                                               *
      *  SRLOAD - Load Detail Line                                    *
      *                                                               *
      *  Called By: SRPRNT                                            *
      *                                                               *
      *  Calls: ZFRPED                                                *
      *                                                               *
      *****************************************************************
      *
     C           SRLOAD    BEGSR
      *
     C                     MOVE MCBKCD    RBKCD
     C                     MOVE MCDTYP    RDTYP
     C*********************MOVE MCDLST    RDLST                                               CDL038
     C                     MOVELMCDLST    RDSTC                                               CDL038
     C                     MOVE MCCLAS    RDSTC                                               CDL038
     C                     MOVE MCDLNO    RDLNO
      *
     C           MCADLN    IFNE 0
     C                     MOVE MCADLN    RADLN
     C                     ENDIF
      *
     C                     MOVE MCCNUM    RCNUM
     C                     MOVE MCDATE    RDATE
     C                     Z-ADDMCICBS    RICBS
     C                     MOVE MCFTYP    RFTYP
     C                     MOVE MCYLDC    RYLDC
      *
      ** Edit the Number of Days in Interest Period
      *
     C                     Z-ADDMCNDYP    ZFLD15
     C                     Z-ADD0         ZDECS
     C                     MOVE 'C'       ZECODE
     C                     EXSR ZFRPED
     C                     MOVELZOUT22    WTMP20 20
     C                     MOVE WTMP20    RNDYP
      *
      ** Edit the Cash flow amount according to the number of
      ** decimal places of the Currency, if currency is not blanks
      *
     C           MCCYCD    IFNE *BLANKS
     C           MCIOIN    IFEQ 'O'
     C                     Z-SUBMCCAMT    MCCAMT
     C                     ENDIF
     C                     Z-ADDMCCAMT    ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    RCAMT                                               CDL038
     C*********************MOVELZOUT22    RCAMT                                               CDL038
     C                     ENDIF
      *
      ** Edit the NPV amount according to the number of
      ** decimal places of the Currency, if currency is not blanks
      *
     C           MCCYCD    IFNE *BLANKS
     C           MCIOIN    IFEQ 'O'
     C                     Z-SUBMCNPVA    MCNPVA
     C                     ENDIF
     C                     Z-ADDMCNPVA    ZFLD15
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZFRPED
     C                     MOVE ZOUT22    RNPVA                                               CDL038
     C*********************MOVELZOUT22    RNPVA                                               CDL038
     C                     ENDIF
      *
      ** Edit the Interest rate
      ** Print Interest Rate only for Interest Amount Events
      *
     C           RFTYP     IFEQ 'MINT'
     C           RFTYP     OREQ 'INTP'
     C           RFTYP     OREQ 'RINT'
     C           RFTYP     OREQ 'RMIN'
     C           MCIRAT    MULT 10000000  ZFLD15
     C                     Z-ADD7         ZDECS
     C                     MOVE 'C'       ZECODE
     C                     EXSR ZFRPED
     C                     MOVE ZOUT1     RIRAT
     C                     ENDIF
      *
      ** Edit the Discount factor
      *
     C           MCDSCF    MULT 1000000000ZFLD15
     C                     Z-ADD9         ZDECS
     C                     MOVE 'C'       ZECODE
     C                     EXSR ZFRPED
     C                     MOVE ZOUT1     RDSCF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  SRAUDT - Print Cash Flow Audit Report                        *
      *                                                               *
      *  Called By: Main Processing, *PSSR                            *
      *                                                               *
      *  Calls: None                                                  *
      *                                                               *
      *****************************************************************
      *
     C           SRAUDT    BEGSR
      *
     C                     WRITEHEADAU
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
     C           WCOUNT    IFEQ *ZERO
     C                     WRITENODTLS
     C                     ENDIF
      *
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *INZSR - Program Initialisation Routine                      *
      *                                                               *
      *  Called By: Implicitly on program activation                  *
      *                                                               *
      *  Calls: *PSSR                                                 *
      *                                                               *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C                     PARM           PBRNCH  3
      *
     C                     MOVEACPY@      CPY2@  80
      *
     C                     MOVEL'MM4111'  DBPGM
      *
      ** Read RUNDAT for multi-branching indicator
      *
     C                     IN   RUNDAT
     C           *NAMVAR   DEFN           RUNDAT 13
      *
      ** Access Bank Details
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   PRTCD   7
     C                     PARM '*FIRST  'POPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Database Error
      *
     C           PRTCD     IFNE *BLANKS
     C                     MOVELPOPTN     DBKEY
     C                     MOVEL'SDBANKPD'DBFILE
     C                     Z-ADD1         DBASE
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE *ON       *IN98
     C                     ENDIF
      *                                                                                       CAS004
      ** Call access program for General Ledger details.                                      CAS004
      *                                                                                       CAS004
     C**********           CALL 'AOGELRR0'                                             CAS004 CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*DBERR ' PRTCD                                               CAS004
     C                     PARM '*FIRST ' POPTN                                               CAS004
     C********** SDGELR    PARM SDGELR    DSFDY                                        CAS004 CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
      *                                                                                       CAS004
      ** Database Error                                                                       CAS004
      *                                                                                       CAS004
     C           PRTCD     IFNE *BLANK                                                        CAS004
     C                     MOVEL'SDGELRPD'DBFILE                                              CAS004
     C                     Z-ADD2         DBASE                                               CAS004
     C                     MOVELPOPTN     DBKEY                                               CAS004
     C                     EXSR *PSSR                                                         CAS004
     C                     ENDIF                                                              CAS004
      *                                                                                       CAS004
      ** Check if Hedge Accounting is installed                                               CAS004
      *                                                                                       CAS004
     C                     CALL 'AOSARDR0'                                                    CAS004
     C                     PARM *BLANKS   PRTCD                                               CAS004
     C                     PARM '*VERIFY' POPTN                                               CAS004
     C                     PARM 'CAS004'  PSARD   6                                           CAS004
     C           SCSARD    PARM SCSARD    DSFDY                                               CAS004
      *                                                                                       CAS004
     C           PRTCD     IFEQ *BLANKS                                                       CAS004
     C                     MOVE 'Y'       CAS004  1                                           CAS004
     C                     MOVE *ON       *IN24                                               CAS004
     C                     ELSE                                                               CAS004
     C                     MOVE 'N'       CAS004                                              CAS004
     C                     MOVE *OFF      *IN24                                               CAS004
     C           PRTCD     IFNE '*NRF   '                                                     CAS004
     C                     MOVE 'SCSARDPD'DBFILE                                              CAS004
     C                     Z-ADD3         DBASE                                               CAS004
     C                     MOVEL'CAS004'  DBKEY                                               CAS004
     C                     EXSR *PSSR                                                         CAS004
     C                     ENDIF                                                              CAS004
     C                     ENDIF                                                              CAS004
      *                                                                                       CAS004
      ** If CAS004 is installed, set up Event Control Date. It is the                         CAS004
      ** lesser of either the (Next Working Day - 1) or the Accrual                           CAS004
      ** Profit Date                                                                          CAS004
      *                                                                                       CAS004
     C           CAS004    IFEQ 'Y'                                                           CAS004
      *                                                                                       CAS004
     C           BJDNWD    SUB  1         EVCD    50                                          CAS004
      *                                                                                       CAS004
     C           BKAPDT    IFLT EVCD                                                          CAS004
     C                     Z-ADDBKAPDT    EVCD                                                CAS004
     C                     ENDIF                                                              CAS004
      *                                                                                       CAS004
      ** Convert the event control date to DDMMMYY                                            CAS004
      *                                                                                       CAS004
     C                     Z-ADDEVCD      ZDAYNO                                              CAS004
     C                     EXSR ZDATE2                                                        CAS004
     C                     MOVELZADATE    REVCD                                               CAS004
      *                                                                                       CAS004
     C                     ENDIF                                                              CAS004
      *
      ** Check if CAS009 is installed                                                         CAS009
      *                                                                                       CAS009
     C                     CALL 'AOSARDR0'                                                    CAS009
     C                     PARM *BLANKS   PRTCD                                               CAS009
     C                     PARM '*VERIFY' POPTN                                               CAS009
     C                     PARM 'CAS009'  PSARD   6                                           CAS009
     C           SCSARD    PARM SCSARD    DSFDY                                               CAS009
      *                                                                                       CAS009
     C           PRTCD     IFEQ *BLANKS                                                       CAS009
     C                     MOVE 'Y'       CAS009  1                                           CAS009
     C                     ELSE                                                               CAS009
     C                     MOVE 'N'       CAS009                                              CAS009
     C           PRTCD     IFNE '*NRF   '                                                     CAS009
     C                     MOVE 'SCSARDPD'DBFILE                                              CAS009
     C                     Z-ADD9         DBASE                                               CAS009
     C                     MOVEL'CAS009'  DBKEY                                               CAS009
     C                     EXSR *PSSR                                                         CAS009
     C                     ENDIF                                                              CAS009
     C                     ENDIF                                                              CAS009
      *                                                                                       CAS009
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR - Program Exception Error Routine                      *
      *                                                               *
      *  Called By: *INZSR, SRPRNT                                    *
      *                                                               *
      *  Calls: SRAUDT                                                *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     DUMP
     C                     ENDIF
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
      *
     C                     EXSR SRAUDT
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZFRPEDZ3
** CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
