     H        1
      *****************************************************************
/*S*D****RPGBASE*******************************************************                       CCB023
     F****************************************************************
     F*                                                              *
     F*  Midas - MM Dealer Module
     F*                                                              *
     F*     MM8040    - MM DEALER CHECK STATUS OF DEALS              *
     F*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                              *
      *  Last Amend No. CCB023 *REDUNDANT  Date 06Aug12              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CDL049             Date 11Jul06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 CDL022             Date 03Feb04               *
      *                 CDL020             Date 03Feb04               *
      *                 CDL019             Date 03Feb04               *
      *                 CDL018             Date 03Feb04               *
      *                 CDL017             Date 03Feb04               *
      *                 CDL016             Date 03Feb04               *
      *                 222727             Date 05Nov03               *
      *                 CDL010             Date 02Aug02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                    E38012             DATE   03APR92         *
     F*                    S01319             DATE   20JAN92         *
     F*                    E27501             DATE   03SEP91         *
     F*                    DT0181             DATE   22MAY91         *
     F*                    DT0180             DATE   22MAY91         *
     F*                    S01218             DATE   11/08/89        *
     F*                                                              *
     F****************************************************************
     F*                                                              *
      *  CCB023 - COB Restructure - Input Cycle Termination          *
      *           Restructuring (Redundant)                          *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  CDL022 - Deal Amendment Changes (Recompile)                  *
      *  CDL020 - Apply Base Rate at Value Date (Recompile)           *
      *  CDL019 - Allow Base Rate Changes on Fixed Deposits/Loans     *
      *           (Recompile)                                         *
      *  CDL018 - Multiple Settlement Accounts on Loans/Deposits      *
      *           (Recompile)                                         *
      *  CDL017 - Penalty Fee & Early Maturity of Fixed Term Loan/Dep *
      *           (Recompile)                                         *
      *  CDL016 - Automatic rollover of Fixed Term Loans/Deposits     *
      *           (Recompile)                                         *
      *  222727 - Release 5.0 errors  (Recompile)                     *
      *  CDL010 - Prevent last user that actioned the trade from      *
      *           authorising it. Recompile due to changes in         *
      *           MMDEAMPP, MMDELDPP, MMDENBPP AND MMDENSPP .         *
     F*     E38012 - This program was made redundant by the DL/DRS   *
     F*              rationalisation in error and is being reinstated*
     F*     S01319 - DL/DRS Rationalisation - program made redundant *
     F*     E27501 - New DS/LDA added in DT0181 was not defined as   *
     F*              256 long, causing crash when tried to lock in   *
     F*     DT0181 - Release 10 changes to Header Box,Copyright & LDA*
     F*     DT0180 - Recompiled for Release 10.                      *
     F*     S01218 - Recompiled to include new file layouts for      *
     F*              capital markets dealing interface               *
     F*                                                              *
     F****************************************************************
     F*
     F**  Indicator Function Summary
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F* ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
     F*
     F*       U6         Program exception/error condition has occurred
     F*       U7         Set on with U8 to signify file handling error.
     F*       U8         Set on with U7 to signify file handling error.
     F*
     F*
     F*
      /EJECT
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FMMICRDLLIF  E           K        DISK         KINFSR INFSR
     F                                              KINFDS @DLINF
      /EJECT
     E*****************************************************************   DT0181
     E*                                                                   DT0181
     E**  ARRAY TO HOLD COPYRIGHT STATEMENT                               DT0181
     E                    CPY@    1   1 80                                DT0181
     E*                                                                   DT0181
     E*****************************************************************   DT0181
     I            DS                                                      DT0181
     I*                                                                   DT0181
     I**  DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION            DT0181
     I                                        1  80 CPY@                  DT0181
     I                                        1  20 CPY@##                DT0181
     I*
     I**  Program status data structure.
     IPSDS       SDS
     I**  Field containing workstation ID.
     I                                      244 253 @JOB
     I**  Field containing user ID.
     I                                      254 263 @USER
     I*
     I**  Local data area for data base errors.
     I***********UDS                                                      DT0181
     I***LDA******   DS                                             DT0181E27501
     ILDA         DS                            256                       E27501
     I**  Field containing name of database file in error.
     I                                      134 141 DBFILE
     I**  Field containing key value causing database error.
     I                                      142 170 DBKEY
     I**  Field containing name of program causing database error.
     I                                      171 180 DBPGM
     I**  Field containing number of database error.
     I                                      181 183 DBASE
     I*
     I**  File information data structure.
     I@DLINF      DS
     I**  Field containing name of record being processed.
     I                                     *RECORD  @DLREC
     I*
      /EJECT
     C**VERSION**
     C**MM DEALER CHECK STATUS OF DEALS
     C                     MOVE '00.00.01'@VERS   8
     C*
     C**  Initial processing.
     C                     EXSR #A
     C*
     C**  Main processing
     C                     EXSR #B
     C*
     C**  Termination processing
     C                     EXSR #C
     C*
     C           ENDPGM    TAG                             **ENDPGM**
      /EJECT
     C*****************************************************************
     C* SUBROUTINES USED                                              *
     C*                                                               *
     C*  01  #B       - Main processing.                              *
     C*  02  #A       - Initial processing.                           *
     C*  03  #C       - Termination processing.                       *
     C*  04  INFSR    - Error handling SR for file errors.            *
     C*  05  *PSSR    - Error handling SR for pgm errors.             *
     C*                                                               *
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE: #B     - Main Processing.                        *
     C*                                                               *
     C* FLDS USED  @DLTF  - Logical delete flag.                      *
     C*            @DDLT  - Deal deleted flag.                        *
     C*            @OKAH  - 'OK for after hours' flag.                *
     C*                                                               *
     C*  CALLED BY :           MAIN PROGRAM                           *
     C*  CALLS     :                                                  *
     C*                                                               *
     C*****************************************************************
     C           #B        BEGSR
     C*
     C           *LIKE     DEFN HKDLTF    @DLTF
     C           *LIKE     DEFN HKDDLT    @DDLT
     C*
     C**  Access each record in file.
     C           *INLR     DOUEQ'1'                        B1
     C                     READ MMICRDLL                 LR*1
     C*
     C           *INLR     IFEQ '0'                        B*2
     C*
     C**  Determine record format, and extract logical delete flag and
     C**  deal deleted indicator.
     C           @DLREC    IFEQ 'MMDELDP0'                 B**3
     C                     MOVE HKDLTF    @DLTF            ***3
     C                     MOVE HKDDLT    @DDLT            ***3
     C                     END                             E**3
     C*
     C           @DLREC    IFEQ 'MMDENBP0'                 B**3
     C                     MOVE HLDLTF    @DLTF            ***3
     C                     MOVE HLDDLT    @DDLT            ***3
     C                     END                             E**3
     C*
     C           @DLREC    IFEQ 'MMDENSP0'                 B**3
     C                     MOVE HMDLTF    @DLTF            ***3
     C                     MOVE HMDDLT    @DDLT            ***3
     C                     END                             E**3
     C*
     C           @DLREC    IFEQ 'MMDEAMP0'                 B**3
     C                     MOVE HNDLTF    @DLTF            ***3
     C                     MOVE HNDDLT    @DDLT            ***3
     C                     END                             E**3
     C*
     C**  If valid record, and deal deleted indicator is blank.
     C           @DLTF     IFNE '*'                        B**3
     C           @DDLT     ANDEQ*BLANKS                    X**3
     C*
     C**  Set OK for after hours flag to 'N'.
     C                     MOVE 'N'       @OKAH            ***3
     C*
     C**  Terminate processing.
     C                     MOVE '1'       *INLR            ***3
     C*
     C                     END                             E**3
     C*
     C                     END                             E*2
     C*
     C                     END                             E1
     C*
     C           #B9       ENDSR                           **#B9**
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE:  #A     - Initial processing.                    *
     C*                                                               *
     C* FLDS USED  @OKAH  - 'OK for after hours' flag.                *
     C*            @FRUN  - First time through cycle flag.            *
     C*                                                               *
     C*  CALLED BY :          MAIN PROGRAM                            *
     C*  CALLS     :                                                  *
     C*                                                               *
     C*****************************************************************
     C           #A        BEGSR
     C*
     C**  Parameter list for returning the 'OK for after hours' flag.
     C           *ENTRY    PLIST
     C                     PARM           @OKAH   1
     C*
     C**  Default 'OK after hours' flag.
     C                     MOVE 'Y'       @OKAH
     C*
     C**  Set @FRUN to 'N'.
     C                     MOVE 'N'       @FRUN   1
     C*                                                                   DT0181
     C** Define data area LDA to Program                                  DT0181
     C*                                                                   DT0181
     C           *NAMVAR   DEFN           LDA                             DT0181
     C           *LOCK     IN   LDA                                       DT0181
     C*
     C**  Move program name into *LDA field.
     C                     MOVEL'MM8040'  DBPGM
     C                     OUT  LDA                                       DT0181
     C*
     C           #A9       ENDSR                           **#A9**
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE: #C     - Terminates program.                     *
     C*                                                               *
     C*  FIELDS USED:       NONE.                                     *
     C*                                                               *
     C*  CALLED BY :          Main processing                         *
     C*  CALLS     :                                                  *
     C*                                                               *
     C*****************************************************************
     C*
     C           #C        BEGSR
     C*
     C           #C9       ENDSR                           **#C9**
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE: INFSR  - Handle error on file handling.          *
     C*                                                               *
     C*  FIELDS USED:        @ERR2  - Field to prevent looping.       *
     C*                      @OKAH  - 'OK for after hours' flag.      *
     C*                                                               *
     C*  CALLED BY :                                                  *
     C*  CALLS     :                                                  *
     C*                                                               *
     C*****************************************************************
     C*
     C           INFSR     BEGSR
     C*
     C**  Set @ERR2 to 'Y' to prevent looping if further errors occur.
     C           @ERR2     IFNE 'Y'                        B1
     C*
     C                     MOVE 'Y'       @ERR2   1        *1
     C                     MOVE '1'       *INU7            *1
     C                     MOVE '1'       *INU8            *1
     C*
     C**  Set 'OK for after hours' flag to 'N'.
     C                     MOVE 'N'       @OKAH
     C*
     C**  Set up fields in local data area.
     C           *LOCK     IN   LDA                                       DT0181
     C                     MOVEL'MMICRDLL'DBFILE           *1
     C                     MOVEL'MM8040'  DBPGM            *1
     C                     MOVEL'992'     DBASE            *1
     C                     OUT  LDA                                       DT0181
     C                     DUMP                            *1
     C*
     C                     END                             E1
     C*
     C**  Terminate processing immediately.
     C                     MOVE '1'       *INLR
     C*
     C                     RETRN
     C*
     C           INFSR9    ENDSR                           **INFSR9**
     C*                                                               *
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE: *PSSR - Error handling subroutine.               *
     C*                                                               *
     C*  FIELDS USED:        @ERR1  - Field to prevent looping.       *
     C*                      @OKAH  - 'OK for after hours' flag.      *
     C*                                                               *
     C*  CALLED BY : This routine is called if there are any program  *
     C*                      errors.                                  *
     C*  CALLS :                                                      *
     C*                                                               *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*
     C**  Set @ERR1 to 'Y' to prevent looping if further errors occur.
     C           @ERR1     IFNE 'Y'                        B1
     C*
     C                     MOVE 'Y'       @ERR1   1        *1
     C                     MOVE '1'       *INU6            *1
     C*
     C**  Set up fields in local data area.
     C           *LOCK     IN   LDA                                       DT0181
     C                     MOVEL'MM8040'  DBPGM            *1
     C                     MOVEL'991'     DBASE            *1
     C                     OUT  LDA                                       DT0181
     C                     DUMP                            *1
     C*
     C**  Set 'OK for after hours' flag to 'N'.
     C                     MOVE 'N'       @OKAH
     C*
     C                     END                             E1
     C*
     C**  Terminate processing immediately.
     C                     MOVE '1'       *INLR
     C*
     C                     RETRN
     C*
     C           #PSSR9    ENDSR                            ***PSSR9**
     C*
**   CPY@ - COPYRIGHT STATEMENT
(c) Misys International Banking Systems Ltd. 2001
