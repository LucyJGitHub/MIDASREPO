      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MQ PIIP-sname pmt(MANUAL CRT IN 38MODE)')
     A*****************************************************************
     A*                                                               *
     A*     Midas - Midas/Q Module                                    *
     A*                                                               *
     A*     MQ0030DF - CUSTOMER SHORTNAME PROMPT                      *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01179             Date 05Nov97               *
      *                 071986             Date 09Jun94               *
     A*                 E39162                  18JUN92               *
     A*                 E13250                  20MAY88               *
     A*                 E11812                  18NOV87               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*     S01179 - AS400 CONVERSION. KEYWORD RTGAID IS NOT SUPPORTED*
     A*              ON AS400. DISPLAY FILE AMENDED SO THAT FUNCTION  *
     A*              KEYS F1-F24 SET ON INDICATORS 31-54 RESPECTIVELY *
     A*              WHEN PRESSED. THESE ARE THEN CHECKED AGAINST THE *
     A*              KEYS DEFINED IN MIDASQ FOR EXIT, BYPASS & CHANGE.*
     A*     071986 - CONVERT TO NATIVE AS400 DISPLAY FILE             *
     A*     E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.               *
     A*     E13250 - CUSTOMER SHORTNAME SELECTION IS NOT STANDARD.    *
     A*              SHOULD USE '*' AT END OF GENERIC NAME.           *
     A*                                                               *
     A*     E11812 - CUSTOMER SHORTNAME SELECTION NOT WORKING.        *
     A*                                                               *
     A*****************************************************************
     A*                                                                       *
     A*         /////////////////////////////////////////                     *
     A*        // WARNING: When creating this display //                      *
     A*       //   file, take options RSTDSP(*YES)   //                       *
     A*      //                       SHARE (*YES)  //                        *
     A*     //                        LVLCHK(*NO)  //
     A*    /////////////////////////////////////////                          *
     A*                                                                       *
     A*************************************************************************
     A* All cmd keys are enabled, pgm checks for valid keys
     A*************************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A*                                                          BEGIN /S01179/
     A**01*******                           CF01
     A**02*******                           CF02
     A**03*******                           CF03
     A**04*******                           CF04
     A**05*******                           CF05
     A**06*******                           CF06
     A**07*******                           CF07
     A**08*******                           CF08
     A**09*******                           CF09
     A**10*******                           CF10
     A**11*******                           CF11
     A**12*******                           CF12
     A**13*******                           CF13
     A**14*******                           CF14
     A**15*******                           CF15
     A**16*******                           CF16
     A**17*******                           CF17
     A**18*******                           CF18
     A**19*******                           CF19
     A**20*******                           CF20
     A**21*******                           CF21
     A**22*******                           CF22
     A**23*******                           CF23
     A**24*******                           CF24
     A  01                                  CF01(31)
     A  02                                  CF02(32)
     A  03                                  CF03(33)
     A  04                                  CF04(34)
     A  05                                  CF05(35)
     A  06                                  CF06(36)
     A  07                                  CF07(37)
     A  08                                  CF08(38)
     A  09                                  CF09(39)
     A  10                                  CF10(40)
     A  11                                  CF11(41)
     A  12                                  CF12(42)
     A  13                                  CF13(43)
     A  14                                  CF14(44)
     A  15                                  CF15(45)
     A  16                                  CF16(46)
     A  17                                  CF17(47)
     A  18                                  CF18(48)
     A  19                                  CF19(49)
     A  20                                  CF20(50)
     A  21                                  CF21(51)
     A  22                                  CF22(52)
     A  23                                  CF23(53)
     A  24                                  CF24(54)
     A*                                                          END   /S01179/
     A                                      VLDCMDKEY(26)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
      *
     A          R MQ0030F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*************                         RTGAID(01)       /*071986*/
     A                                      OVERLAY
     A*************                         LOCK                          HUW
     A*************                         KEEP                          HUW
     A                                      PUTOVR
     A                                  1  3'MIDAS/Q'
     A            O#SRLS         3Y 1O  1 11EDTCDE(4)
     A            O#SSIT        20A  O  1 31
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2  3'Run number:'
     A            TIF#RN         7S 0O  2 15
     A                                  2 31'   PRODUCE REPORT   '
     A                                  4  3'Report name            :'
     A            TIF#NM        10A  O  4 28
     A                                  5  3'Report description     :'
     A            TIF#RD        40A  O  5 28
     A                                  7  8'Select customers to be included in-
     A                                       this production run.'
     A                                  9  5'Enter :'
     A                                 19  8'A customer shortname enquiry is av-
     A                                      ailable which may be positioned by'
     A                                 15 17'or'
     A                                      DSPATR(HI)
     A            KEYVAL        10A  B 17 27
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(HI)
     A  51                                  DSPATR(RI)
     A                                      OVRDTA
     A                                 13 21'A partial name is denoted by enter-
     A                                      ing "*" after the name.'
     A                                 23  4'F:'
     A            CEX            2Y 0O 23  7EDTCDE(Z)
     A                                 23 10'Exit'
     A            CBY            2Y 0O 23 15EDTCDE(Z)
     A                                 23 18'Bypass'
     A                                  9 13'either'
     A                                      DSPATR(HI)
     A                                  9 20'a'
     A                                  9 22'full'
     A                                      DSPATR(UL)
     A                                  9 27'shortname to select an individual -
     A                                      customer.'
     A                                 11 17'or'
     A                                      DSPATR(HI)
     A                                 12 21'with the entry.'
     A                                 11 20'a'
     A                                 11 22'partial'
     A                                      DSPATR(UL)
     A                                 11 30'shortname to select all customers -
     A                                      begining'
     A                                 15 20'leave'
     A                                 15 26'blank'
     A                                      DSPATR(UL)
     A                                 15 32'if all customers are to be include-
     A                                      d.'
     A                                 20  8'entering a "?" following part of t-
     A                                      he name.'
      *
      ** Error Message Subfile.
     A          R MQ0030S0                  TEXT('Message subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MQMGKY                    SFLMSGKEY
     A            MQPGMQ                    SFLPGMQ
      *
      ** Error Message Subfile control.
     A          R MQ0030C0                  TEXT('Message subfile control')
     A                                      OVERLAY
     A                                      SFLCTL(MQ0030S0)
     A                                      SFLINZ
     A                                      SFLSIZ(13)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A  31                                  SFLEND
     A            MQPGMQ                    SFLPGMQ
