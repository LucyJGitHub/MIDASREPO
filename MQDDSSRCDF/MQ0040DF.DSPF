      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MQ PIIP-sname enqry')
     A*************************************************************************
     A*                                                                       *
     A*     Midas Midas/Q                                                     *
     A*                                                                       *
     A*     MQ0040DF - CUSTOMER SHORTNAME SELECTION                           *
     A*                                                                       *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                       *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 29Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. E39162             Date 18JUN92               *
     A*                                                                       *
     A*****************************************************************
     A*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*                                                               *
     A*****************************************************************
     A*                                                                       *
     A*         /////////////////////////////////////////                     *
     A*        // WARNING: When creating this display //                      *
     A*       //   file, take options RSTDSP(*YES)   //                       *
     A*      //                       SHARE (*YES)  //                        *
     A*     //                        LVLCHK(*NO)  //
     A*    /////////////////////////////////////////                          *
     A*                                                                       *
     A*************************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A          R MQ0040SF                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            ACTN           1A  I  5  2DSPATR(UL)
     A            STAT           1A  O  5  4DSPATR(BL)
     A            CSNM         010A  O  5  6
      *BEGIN /CSD027/
     A**********  CNUM         006S 0O  5 17                                                  CSD027
     A            CNUM         006A  O  5 17
      *END /CSD027/
     A            CRNM          20A  O  5 24
     A            CTWN          10A  O  5 45
     A          R MQ0040SC                  SFLCTL(MQ0040SF)
     A                                      TEXT('SUBFILE CONTROL')
     A                                      OVERLAY
     A N02                                  SFLDSPCTL
     A N02N03                               SFLDSP
     A  02                                  SFLCLR
     A                                      SFLSIZ(20)
     A                                      SFLPAG(19)
     A N02                                  SFLEND
     A  03                             24 25'*** No Details to display ***'
     A                                      DSPATR(HI)
     A          R MQ0040TP
     A                                      TEXT('TOP OF SCREEN')
     A                                      KEEP
     A                                  1  3'MIDAS/Q'
     A            O#SRLS         3Y 1O  1 11EDTCDE(4)
     A            O#SSIT        20A  O  1 31
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 32'CUSTOMER SELECTION'
     A                                  4  1'Sel__Shortname__Number_Name_______-
     A                                      __________Town_____________________-
     A                                      __________'
     A                                      DSPATR(HI)
