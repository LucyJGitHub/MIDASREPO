      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MQ Display file for PIIP-Postings pmt')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Midas/Q Module                                       *
     A*                                                               *
     A*     MQ0060DF - POSTINGS SELECTION PROMPT                      *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01179             Date 31Oct97               *
      *                 CPK008             Date 11Jun97               *
     A*                 071986             DATE 09JUN94               *
     A*                 E39162             DATE 18JUN92               *
     A*                 E22311                  09AUG90               *
     A*                 S01182                  19DEC89               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*    S01179 - AS400 CONVERSION. KEYWORD RTGAID IS NOT SUPPORTED *
     A*             ON AS400. DISPLAY FILE AMENDED SO THAT FUNCTION   *
     A*             KEYS F1-F24 SET ON INDICATORS 31-54 RESPECTIVELY  *
     A*             WHEN PRESSED. THESE ARE THEN CHECKED AGAINST THE  *
     A*             KEYS DEFINED IN MIDASQ FOR EXIT, BYPASS & CHANGE. *
     A*    CPK008 - Convert BS/P&L references to hidden fields.       *
     A*    071986 - CONVERT TO NATIVE AS400 DISPLAY FILE              *
     A*    E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                *
     A*    E22311 - CONDITIONING OF DISPLAY ATTR. INCORRECT, CHANGED  *   E22311
     A*             TO BE CONDITIONED ON *IN50 INSTEAD OF *IN30.      *   E22311
     A*    S01182 - NEW FILE FOR MANAGEMENT INFORMATION SYSTEM        *   S01182
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*           /////////////////////////////////////////           *
     A*          // WARNING: When creating this display //            *
     A*         //   file, take options RSTDSP(*YES)   //             *
     A*        //                       SHARE (*YES)  //              *
     A*       //                        LVLCHK (*NO) //               *
     A*      /////////////////////////////////////////                *
     A*                                                               *
     A*****************************************************************
     A* All cmd keys are enabled, pgm checks for valid keys
     A                                      DSPSIZ(24 80 *DS3)
     A*                                                          BEGIN /S01179/
     A**01*******                           CF01
     A**02*******                           CF02
     A**03*******                           CF03
     A**04*******                           CF04
     A**05*******                           CF05
     A**06*******                           CF06
     A**07*******                           CF07
     A**08*******                           CF08
     A**09*******                           CF09
     A**10*******                           CF10
     A**11*******                           CF11
     A**12*******                           CF12
     A**13*******                           CF13
     A**14*******                           CF14
     A**15*******                           CF15
     A**16*******                           CF16
     A**17*******                           CF17
     A**18*******                           CF18
     A**19*******                           CF19
     A**20*******                           CF20
     A**21*******                           CF21
     A**22*******                           CF22
     A**23*******                           CF23
     A**24*******                           CF24
     A  01                                  CF01(51)
     A  02                                  CF02(52)
     A  03                                  CF03(53)
     A  04                                  CF04(54)
     A  05                                  CF05(55)
     A  06                                  CF06(56)
     A  07                                  CF07(57)
     A  08                                  CF08(58)
     A  09                                  CF09(59)
     A  10                                  CF10(60)
     A  11                                  CF11(61)
     A  12                                  CF12(62)
     A  13                                  CF13(63)
     A  14                                  CF14(64)
     A  15                                  CF15(65)
     A  16                                  CF16(66)
     A  17                                  CF17(67)
     A  18                                  CF18(68)
     A  19                                  CF19(69)
     A  20                                  CF20(70)
     A  21                                  CF21(71)
     A  22                                  CF22(72)
     A  23                                  CF23(73)
     A  24                                  CF24(74)
     A*                                                          END   /S01179/
     A                                      VLDCMDKEY(26)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
      *
      ** Header format
     A          R MQ0060F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                  1  3'MIDAS/Q'
     A            O#SRLS         3Y 1O  1 11EDTCDE(4)
     A            O#SSIT        20A  O  1 31
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2  3'Run number:'
     A            TIF#RN         7S 0O  2 15
     A                                  2 31'   PRODUCE REPORT   '
     A                                  4  3'Report name            :'
     A            TIF#NM        10A  O  4 28
     A                                  5  3'Report description     :'
     A            TIF#RD        40A  O  5 28
      *
      ** Postings prompt format
     A          R MQ0060F2                  TEXT('Postings prompt format')
     A                                      KEEP
     A**********                            RTGAID(01)       /*071986*/
     A                                      OVERLAY
      *
     A                                  8  2'Leaving a field blank will +
     A                                      lead to the inclusion of all +
     A                                      records for that field'
     A**                                                   /*E22311*/
     A*N30***********                       DSPATR(RI)
     A**30***********                       DSPATR(ND)
     A  50                                  DSPATR(RI)
     A N50                                  DSPATR(ND)
     A**                                                   /*E22311*/
     A                                 10 23'POSTINGS Selection Available +
     A                                      by :-'
     A                                      DSPATR(UL)
     A                                 11  6'Start date - End date'
     A                                 11 30'from'
     A            FLD001         6  0B 11 36
     A                                      EDTCDE(Y)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(BL)
     A                                 11 46'to'
     A            FLD002         6  0B 11 52
     A                                      EDTCDE(Y)
     A N52                                  DSPATR(UL)
     A  52                                  DSPATR(HI)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(BL)
     A                                 12  6'Currency'
     A            FLD003         3   B 12 52
     A N53                                  DSPATR(UL)
     A  53                                  DSPATR(HI)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(BL)
     A                                 13  6'Account section'
     A            FLD004         2   B 13 52
     A N54                                  DSPATR(UL)
     A  54                                  DSPATR(HI)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(BL)
     *************                                /Start CPK008*/
     A************                     14  6'BS/P&L code, Set No, + /*CPK008*/
     A************                          BSPL Ind. & DR/CR Ind.' /*CPK008*/
     A************FLD005         4   B 14 52                        /*CPK008*/
     A*N55********                          DSPATR(UL)              /*CPK008*/
     A**55********                          DSPATR(HI)              /*CPK008*/
     A**55********                          DSPATR(RI)              /*CPK008*/
     A**55********                          DSPATR(PC)              /*CPK008*/
     A**55********                          DSPATR(BL)              /*CPK008*/
     A************                     14 57'/'                     /*CPK008*/
     A************FLD015         1   B 14 59                        /*CPK008*/
     A*N65********                          DSPATR(UL)              /*CPK008*/
     A**65********                          DSPATR(HI)              /*CPK008*/
     A**65********                          DSPATR(RI)              /*CPK008*/
     A**65********                          DSPATR(PC)              /*CPK008*/
     A**65********                          DSPATR(BL)              /*CPK008*/
     A************                     14 61'/'                     /*CPK008*/
     A************FLD025         1   B 14 63                        /*CPK008*/
     A*N75********                          DSPATR(UL)              /*CPK008*/
     A**75********                          DSPATR(HI)              /*CPK008*/
     A**75********                          DSPATR(RI)              /*CPK008*/
     A**75********                          DSPATR(PC)              /*CPK008*/
     A**75********                          DSPATR(BL)              /*CPK008*/
     A************                     14 65'/'                     /*CPK008*/
     A************FLD035         1   B 14 67                        /*CPK008*/
     A*N76********                          DSPATR(UL)              /*CPK008*/
     A**76********                          DSPATR(HI)              /*CPK008*/
     A**76********                          DSPATR(RI)              /*CPK008*/
     A**76********                          DSPATR(PC)              /*CPK008*/
     A**76********                          DSPATR(BL)              /*CPK008*/
     A            FLD005         4   H
     A            FLD015         1   H
     A            FLD025         1   H
     A            FLD035         1   H
     *************                                  /End CPK008*/
     A                                 15  6'Account type'
     A            FLD006         1   B 15 52
     A N56                                  DSPATR(UL)
     A  56                                  DSPATR(HI)
     A  56                                  DSPATR(RI)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(BL)
     A                                 16  6'Account code'
     A**Start of change CGL029
     A**********  FLD007         4   B 16 52                                                  CGL029
     A            FLD007        10   B 16 52
     A**End of change CGL029
     A N57                                  DSPATR(UL)
     A  57                                  DSPATR(HI)
     A  57                                  DSPATR(RI)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(BL)
     A                                 17  6'Customer Short Name'
     A            FLD008        10   B 17 52
     A N58                                  DSPATR(UL)
     A  58                                  DSPATR(HI)
     A  58                                  DSPATR(RI)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(BL)
     A                                 18  6'Originating Counterparty +
     A                                      Short Name'
     A            FLD009        10   B 18 52
     A N59                                  DSPATR(UL)
     A  59                                  DSPATR(HI)
     A  59                                  DSPATR(RI)
     A  59                                  DSPATR(PC)
     A  59                                  DSPATR(BL)
     A                                 19  6'Book code'
     A            FLD010         2   B 19 52
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(HI)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(BL)
     A                                 20  6'Product type / sub-type'
     A            FLD011         2   B 20 52
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(HI)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(BL)
     A                                 20 55'/'
     A            FLD012         2   B 20 57
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(HI)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(BL)
      *
      ** Profit centre is not going to be used until full Multibranching
      ** comes into effect. It is put in for ease of upgrade at that time.
     A                                 21  6'Profit centre'
     A                                      DSPATR(ND)
     A            FLD013         4   B 21 52
     A                                      DSPATR(PR)
     A                                      DSPATR(ND)
     A*N63********                          DSPATR(UL)
     A**63********                          DSPATR(HI)
     A**63********                          DSPATR(RI)
     A**63********                          DSPATR(PC)
     A**63********                          DSPATR(BL)
      *
      ** End of Profit centre code
      *
     A                                 22  6'**** Validation is SUCCESSFUL +
     A                                      - Please press enter to continue +
     A                                      ****'
     A**                                                   /*E22311*/
     A**30***********                       DSPATR(RI)
     A*N30***********                       DSPATR(ND)
     A N50                                  DSPATR(RI)
     A  50                                  DSPATR(ND)
     A**                                                   /*E22311*/
      *
      ** Type / Subtype information screen.
     A          R MQ0060F3                  TEXT('Type/Subtype information')
     A**********                            RTGAID(01)       /*071986*/
     A                                      OVERLAY
      *
     A                                  8 10'The original transaction type +
     A                                      for each module can be '
     A                                  9 10'determined by different means.'
     A                                 10 10'This is also true of the orig+
     A                                      inal transaction subtypes.'
     A                                 11 10'The general rule would be to +
     A                                      look up the types and subtypes'
     A                                 12 10'in the documentation for the +
     A                                      corresponding module.'
     A                                 13 10'Information about the Lending +
     A                                      module is also available from'
     A                                 14 10 'Standing Data enquiries '
      *
     A                                 20 10'Please press ENTER to continue'
      *
      ** Footer format
     A          R MQ0060F9
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                 23  2'F:'
     A            CEX            2Y 0O 23  5EDTCDE(Z)
     A                                 23  8'Exit'
     A            CBY            2Y 0O 23 13EDTCDE(Z)
     A                                 23 16'Bypass'
      *
      ** Error Message Subfile.
     A          R MQ0060S0                  TEXT('Message subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MQMGKY                    SFLMSGKEY
     A            MQPGMQ                    SFLPGMQ
      *
      ** Error Message Subfile control.
     A          R MQ0060C0                  TEXT('Message subfile control')
     A                                      OVERLAY
     A                                      SFLCTL(MQ0060S0)
     A                                      SFLINZ
     A                                      SFLSIZ(13)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A  31                                  SFLEND
     A            MQPGMQ                    SFLPGMQ
