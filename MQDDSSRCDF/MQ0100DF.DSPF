      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MQ MQDEALS RDIP (MANUAL CRT IN 38 MODE)')
     A*****************************************************************
     A*                                                               *
     A*     Midas - Midas/Q Module                                    *
     A*                                                               *
     A*     MQ0100DF - REPORT DEFINITION INTERFACE FOR SUBSET MQDEALS *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. S01179             DATE 31OCT97               *
     A*                 E20810             DATE 31OCT97               *
     A*                 071986             DATE 09JUN94               *
     A*                 E39162             DATE 18JUN92               *
     A*                 S01159                  05MAY88               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CAAA02 - Remove use of LOCK keyword to enable WebFacing      *
      *           to process screens.                                 *
     A*     S01179 - AS400 CONVERSION. KEYWORD RTGAID IS NOT SUPPORTED*
     A*              ON AS400. DISPLAY FILE AMENDED SO THAT FUNCTION  *
     A*              KEYS F1-F24 SET ON INDICATORS 31-54 RESPECTIVELY *
     A*              WHEN PRESSED. THESE ARE THEN CHECKED AGAINST THE *
     A*              KEYS DEFINED IN MIDASQ FOR EXIT, BYPASS & CHANGE.*
     A*     E20810 - ALLOW SELECTION OF MATURED DEALS                 *
     A*     071986 - CONVERT TO NATIVE AS400 DISPLAY FILE             *
     A*     E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.               *
     A*     S01159 - ALLOWING FOR THE PRESENCE OF FRA/IRS DEALS       *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     //                        LVLCHK(*NO)  //                 *
     A*    /////////////////////////////////////////                  *
     A*                                                               *
     A*****************************************************************
     A* All cmd keys are enabled, pgm checks for valid keys
     A*                                                               *
     A                                      DSPSIZ(24 80 *DS3)
     A*                                                          BEGIN /S01179/
     A**01*******                           CF01
     A**02*******                           CF02
     A**03*******                           CF03
     A**04*******                           CF04
     A**05*******                           CF05
     A**06*******                           CF06
     A**07*******                           CF07
     A**08*******                           CF08
     A**09*******                           CF09
     A**10*******                           CF10
     A**11*******                           CF11
     A**12*******                           CF12
     A**13*******                           CF13
     A**14*******                           CF14
     A**15*******                           CF15
     A**16*******                           CF16
     A**17*******                           CF17
     A**18*******                           CF18
     A**19*******                           CF19
     A**20*******                           CF20
     A**21*******                           CF21
     A**22*******                           CF22
     A**23*******                           CF23
     A**24*******                           CF24
     A  01                                  CF01(31)
     A  02                                  CF02(32)
     A  03                                  CF03(33)
     A  04                                  CF04(34)
     A  05                                  CF05(35)
     A  06                                  CF06(36)
     A  07                                  CF07(37)
     A  08                                  CF08(38)
     A  09                                  CF09(39)
     A  10                                  CF10(40)
     A  11                                  CF11(41)
     A  12                                  CF12(42)
     A  13                                  CF13(43)
     A  14                                  CF14(44)
     A  15                                  CF15(45)
     A  16                                  CF16(46)
     A  17                                  CF17(47)
     A  18                                  CF18(48)
     A  19                                  CF19(49)
     A  20                                  CF20(50)
     A  21                                  CF21(51)
     A  22                                  CF22(52)
     A  23                                  CF23(53)
     A  24                                  CF24(54)
     A*                                                          END   /S01179/
     A                                      VLDCMDKEY(26)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A          R MQ0100F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      ** Start of CAAA02
     A******************                    LOCK
      ** End   of CAAA02
     A************                          RTGAID(01)       /*071986*/
     A                                  1  3'MIDAS/Q'
     A            O#SRLS         3Y 1O  1 11EDTCDE(4)
     A            O#SSIT        20A  O  1 31
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2  3'Report:'
     A            EXT#NM        10A  O  2 11
     A            EXT#PD        20A  O  2 31
     A                                  4  3'Report name            :'
     A            CPY#NM        10A  O  4 28
     A                                  5  3'Report description     :'
     A            EXT#RD        40A  O  5 28
     A                                  7  8'Deal Selection'
     A                                      DSPATR(UL)
     A                                  9  8'Status of deals to be included in -
     A                                      report -'
     A                                  9 52'Live'
     A                                      DSPATR(HI)
     A            INCLLD         3A  B  9 62CHGINPDFT
     A                                      VALUES('YES' 'NO')
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                 10 52'Reversed'
     A                                      DSPATR(HI)
     A            INCLRD         3A  B 10 62CHGINPDFT
     A                                      VALUES('YES' 'NO')
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A* MATURED DEALS SELECTION NOT ALLOWED IN THIS RELEASE SO NOT DISPLAYED
     A*** Matured deals have been reinstated for R10      Huw   140390
     A                                 11 52'Matured'
     A                                      DSPATR(HI)
     A*                                                          BEGIN /E20810/
     A***********                           DSPATR(ND)
     A*                                                          END   /E20810/
     A            INCLMD         3A  B 11 62CHGINPDFT
     A                                      VALUES('YES' 'NO')
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A*                                                          BEGIN /E20810/
     A***********                           DSPATR(ND)
     A*                                                          END   /E20810/
     A                                 12  8'Types of deal to be included -'
     A                                 12 40'FX'
     A                                      DSPATR(HI)
     A            INCLFX         3A  B 12 56CHGINPDFT
     A                                      VALUES('YES' 'NO')
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                 13 40'Money market'
     A                                      DSPATR(HI)
     A            INCLMM         3A  B 13 56CHGINPDFT
     A                                      VALUES('YES' 'NO')
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                 14 40'NAs purchased'
     A                                      DSPATR(HI)
     A            INCLNP         3A  B 14 56CHGINPDFT
     A                                      VALUES('YES' 'NO')
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                 15 40'NAs sold'
     A                                      DSPATR(HI)
     A            INCLNS         3A  B 15 56CHGINPDFT
     A                                      VALUES('YES' 'NO')
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A*          */S01159/*
     A  60                             16 40'FRA/IRS'
     A                                      DSPATR(HI)
     A  60        INCLFR         3A  B 16 56CHGINPDFT
     A                                      VALUES('YES' 'NO')
     A  30 60                               DSPATR(UL)
     A N30 60                               DSPATR(PR)
     A*          */S01159/*
     A                                 17  8'Customer Selection'
     A                                      DSPATR(UL)
     A                                 19  8'Enter'
     A                                 19 14'YES'
     A                                      DSPATR(HI)
     A                                 19 18'if selection of customers by full -
     A                                      or partial'
     A                                 20  8'customer shortname at run time is -
     A                                      required, or enter'
     A                                 20 61'NO '
     A                                      DSPATR(HI)
     A                                 21  8'if all customers are to be selecte-
     A                                      d.'
     A                                 21 46'?'
     A                                      DSPATR(HI)
     A            ENQUIR         3A  B 21 48CHGINPDFT
     A                                      VALUES('YES' 'NO')
     A N30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A                                 23  2'Next:'
     A            EXT#PR        10A  B 23  8
     A                                 23 21'F:'
     A            CEX            2Y 0O 23 24EDTCDE(Z)
     A                                 23 27'Exit'
     A            CBY            2Y 0O 23 32EDTCDE(Z)
     A                                 23 35'Bypass'
     A N30        CCH            2Y 0O 23 42EDTCDE(Z)
     A N30                             23 45'Change'
