      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MQ SYNDLOAN RDIP(MANUAL CRT IN 38MODE)')
     A*************************************************************************
     A*                                                                       *
     A*     Midas Midas/Q                                                     *
     A*                                                                       *
     A*     MQ0400DF - REPORT DEFINITION INTERFACE FOR SBS MQSYNDLOAN         *
     A*                                                                       *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                       *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CAAA02             Date 16Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 071986             Date 09Jun94               *
     A*                 E39162             DATE 18JUN92               *
     A*                                                                       *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA02 - Remove use of LOCK keyword to enable WebFacing      *
      *           to process screens.                                 *
     A*  071986 - CONVERT TO NATIVE AS400 DISPLAY FILE                        *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*                                                               *
     A*****************************************************************
     A*                                                                       *
     A*         /////////////////////////////////////////                     *
     A*        // WARNING: When creating this display //                      *
     A*       //   file, take options RSTDSP(*YES)   //                       *
     A*      //                       SHARE (*YES)  //                        *
     A*     //                        LVLCHK(*NO)  //
     A*    /////////////////////////////////////////                          *
     A*                                                                       *
     A*************************************************************************
     A* All cmd keys are enabled, pgm checks for valid keys
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A  01                                  CF01
     A  02                                  CF02
     A  03                                  CF03
     A  04                                  CF04
     A  05                                  CF05
     A  06                                  CF06
     A  07                                  CF07
     A  08                                  CF08
     A  09                                  CF09
     A  10                                  CF10
     A  11                                  CF11
     A  12                                  CF12
     A  13                                  CF13
     A  14                                  CF14
     A  15                                  CF15
     A  16                                  CF16
     A  17                                  CF17
     A  18                                  CF18
     A  19                                  CF19
     A  20                                  CF20
     A  21                                  CF21
     A  22                                  CF22
     A  23                                  CF23
     A  24                                  CF24
     A                                      VLDCMDKEY(26)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A          R MQ0400F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      ** Start of CAAA02
     A******************                    LOCK
      ** End   of CAAA02
     A************                          RTGAID(01)       /*071986*/
     A                                  1  3'MIDAS/Q'
     A            O#SRLS         3Y 1O  1 11EDTCDE(4)
     A            O#SSIT        20A  O  1 31
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2  3'Report:'
     A            EXT#NM        10A  O  2 11
     A            EXT#PD        20A  O  2 31
     A                                  4  3'Report name            :'
     A            CPY#NM        10A  O  4 28
     A                                  5  3'Report description     :'
     A            EXT#RD        40A  O  5 28
     A                                  7  8'Loan Selection'
     A                                      DSPATR(UL)
     A                                  9  8'Status of loans to be included in -
     A                                      report -'
     A                                  9 51'Live'
     A                                      DSPATR(HI)
     A            INCLLL         3A  B  9 61CHGINPDFT
     A                                      VALUES('YES' 'NO')
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                 10 51'Matured'
     A                                      DSPATR(HI)
     A            INCLML         3A  B 10 61CHGINPDFT
     A                                      VALUES('YES' 'NO')
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                 11 51'Reversed'
     A                                      DSPATR(HI)
     A            INCLRL         3A  B 11 61CHGINPDFT
     A                                      VALUES('YES' 'NO')
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                 17  8'Customer Selection'
     A                                      DSPATR(UL)
     A                                 19  8'Enter'
     A                                 19 14'YES'
     A                                      DSPATR(HI)
     A                                 19 18'if selection of customers by full -
     A                                      or partial'
     A                                 20  8'customer shortname at run time is -
     A                                      required, or enter'
     A                                 20 61'NO '
     A                                      DSPATR(HI)
     A                                 21  8'if all customers are to be selecte-
     A                                      d.'
     A                                 21 46'?'
     A                                      DSPATR(HI)
     A            ENQUIR         3A  B 21 48CHGINPDFT
     A                                      VALUES('YES' 'NO')
     A N30                                  DSPATR(PR)
     A  30                                  DSPATR(UL)
     A                                 23  2'Next:'
     A            EXT#PR        10A  B 23  8
     A                                 23 21'F:'
     A            CEX            2Y 0O 23 24EDTCDE(Z)
     A                                 23 27'Exit'
     A            CBY            2Y 0O 23 32EDTCDE(Z)
     A                                 23 35'Bypass'
     A N30        CCH            2Y 0O 23 42EDTCDE(Z)
     A N30                             23 45'Change'
