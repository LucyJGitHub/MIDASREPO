      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MQ Event Cde sel(MANUAL CRT IN 38MODE)')
     A*************************************************************************
     A*                                                                       *
     A*     Midas Midas/Q                                                     *
     A*                                                                       *
     A*     MQ0510 - EVENT CODE REVIEW & SELECTION                            *
     A*                                                                       *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                       *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 071986             Date 09Jun94               *
      *                 E39162             Date 18Jun92               *
     A*                                                                       *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  071986 - CONVERT TO NATIVE AS400 DISPLAY FILE                        *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*                                                               *
     A*****************************************************************
     A*                                                                       *
     A*         /////////////////////////////////////////                     *
     A*        // WARNING: When creating this display //                      *
     A*       //   file, take options RSTDSP(*YES)   //                       *
     A*      //                       SHARE (*YES)  //                        *
     A*     //                        LVLCHK(*NO)  //
     A*    /////////////////////////////////////////                          *
     A*                                                                       *
     A*************************************************************************
     A* All cmd keys are enabled, pgm checks for valid keys
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A  01                                  CF01
     A  02                                  CF02
     A  03                                  CF03
     A  04                                  CF04
     A  05                                  CF05
     A  06                                  CF06
     A  07                                  CF07
     A  08                                  CF08
     A  09                                  CF09
     A  10                                  CF10
     A  11                                  CF11
     A  12                                  CF12
     A  13                                  CF13
     A  14                                  CF14
     A  15                                  CF15
     A  16                                  CF16
     A  17                                  CF17
     A  18                                  CF18
     A  19                                  CF19
     A  20                                  CF20
     A  21                                  CF21
     A  22                                  CF22
     A  23                                  CF23
     A  24                                  CF24
     A                                      VLDCMDKEY(26)
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A          R MQ0510SF                  SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            ACTN           1A  B  5  3CHGINPDFT
     A  30                                  DSPATR(UL PC)
     A N30                                  DSPATR(PR)
     A            EVCD           4A  O  5  7
     A            DESC          60A  O  5 13
     A          R MQ0510SC                  SFLCTL(MQ0510SF)
     A                                      TEXT('SUBFILE CONTROL')
     A***********                           RTGAID(01)       /*071986*/
     A                                      OVERLAY
     A N32                                  SFLDSPCTL
     A N32N33                               SFLDSP
     A  32                                  SFLCLR
     A                                      SFLSIZ(17)
     A                                      SFLPAG(16)
     A N32                                  SFLEND
     A  33                             22 25'*** No Details to display ***'
     A                                      DSPATR(HI)
     A                                 23  2'Next:'
     A            EXT#PR        10A  B 23  8
     A                                 23 21'F:'
     A            CEX            2Y 0O 23 24EDTCDE(Z)
     A                                 23 27'Exit'
     A            CBY            2Y 0O 23 32EDTCDE(Z)
     A                                 23 35'Bypass'
     A N30        CCH            2Y 0O 23 42EDTCDE(Z)
     A N30                             23 45'Change'
     A          R MQ0510TP
     A                                      TEXT('TOP OF SCREEN')
     A                                      KEEP
     A                                  1  3'MIDAS/Q'
     A            O#SRLS         3Y 1O  1 11EDTCDE(4)
     A            O#SSIT        20A  O  1 31
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                      EDTWRD('  :  :  ')
     A                                  2 31'EVENT CODE SELECTION'
     A**
     A                                  4  2'Sel__Code__Transaction_________Dat-
     A                                      e________________Amount____________-
     A                                      __'
     A                                      DSPATR(HI)
