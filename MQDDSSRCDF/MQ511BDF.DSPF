      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MQ RTDEALS production initiation I/F')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Midas/Q Module                                       *
     A*                                                               *
     A*  DSPF/MQ511BDF - Midas RTDEALS Production Initiation I/F.     *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Last Amend No. CMQ001             Date 31OCT97               *
     A*  Prev Amend No. 059501             Date 24AUG93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*   CMQ001   -   MidasQ rationalisation.                        *
     A*   059501   -   ALLOW SELECTION OF HISTORIC DEALS              *
     A*                                                               *
     A*****************************************************************
     F*                                                               *
     F* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     F*                                                               *
     F* 01 - 24          Function Keys 01 to 24                       *
     F* 50               Include Live Deals is invalid                *
     F* 51               Include Reversed Deals is Invalid            *
     F* 52               Include Matured Deals is Invalid             *
     F* 53               Include FX is Invalid                        *
     F* 54               Include MM is Invalid                        *
     F* 55               Include N/A's Purchased is Invalid           *
     F* 56               Include N/A's Sold is Invalid                *
     F* 57               Include FRA/IRS is Invalid                   *
     F* 58               Do Not Include FRA/IRS                       *
     F* 59               Include FO Cust/Brok is Invalid              *
     F* 60               Include FO Book is Invalid                   *
     F* 61               Include Historic Deals is Invalid            *   059501
     F* 65               Position Cursor at ##LIVE                    *
     F* 99               Subfile End                                  *
     F*                                                               *
     F*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R ##HEAD                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CHGINPDFT
     A                                      PUTOVR
     A                                      OVERLAY
     A  01                                  CF01
     A  02                                  CF02
     A  03                                  CF03
     A  04                                  CF04
     A  05                                  CF05
     A  06                                  CF06
     A  07                                  CF07
     A  08                                  CF08
     A  09                                  CF09
     A  10                                  CF10
     A  11                                  CF11
     A  12                                  CF12
     A  13                                  CF13
     A  14                                  CF14
     A  15                                  CF15
     A  16                                  CF16
     A  17                                  CF17
     A  18                                  CF18
     A  19                                  CF19
     A  20                                  CF20
     A  21                                  CF21
     A  22                                  CF22
     A  23                                  CF23
     A  24                                  CF24
     A                                      CSRLOC(#ROW       #COL)
     A            #ROW           3S 0H
     A            #COL           3S 0H
     A            #BOX1          1I  B  1  3DSPATR(RI)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                  1 33'PRODUCE REPORT'
     A            #RNAME        13A  O  1 51
     A                                  1 71'#04'
     A            #BOX2         10I  B  2  2DSPATR(RI)
     A                                      OVRDTA
     A            #BOX3         10I  B  2 13DSPATR(RI)
     A                                      OVRDTA
     A            #BOX4         10I  B  2 24DSPATR(RI)
     A                                      OVRDTA
     A            #BOX5         10I  B  2 35DSPATR(RI)
     A                                      OVRDTA
     A            #BOX6         10I  B  2 46DSPATR(RI)
     A                                      OVRDTA
     A            #BOX7         10I  B  2 57DSPATR(RI)
     A                                      OVRDTA
     A            #BOX8          6I  B  2 68DSPATR(RI)
     A                                      OVRDTA
     A            #BOX9          5I  B  2 75DSPATR(RI)
     A                                      OVRDTA
     A            #BOX10         1I  B  1 78DSPATR(ND)
     A                                      OVRDTA
     A                                  6 13'Status of Deals to be included'
     A                                  6 50'Live     '
     A            ##LIVE         3   B  6 61OVRDTA
     A N50                                  DSPATR(UL HI)
     A  50                                  DSPATR(RI)
     A  50
     AO 65                                  DSPATR(PC)
     A                                  7 50'Reversed '
     A            ##REV          3   B  7 61OVRDTA
     A N51                                  DSPATR(UL HI)
     A  51                                  DSPATR(RI PC)
     A                                  8 50'Matured  '
     A            ##MAT          3   B  8 61OVRDTA
     A N52                                  DSPATR(UL HI)
     A  52                                  DSPATR(RI PC)
     A*                                                        /*059501*/
     A                                  9 50'Historic '
     A            ##HIST         3   B  9 61OVRDTA
     A N61                                  DSPATR(UL HI)
     A  61                                  DSPATR(RI PC)
     A*                                                        /*059501*/
     A                                 10 13'Type of Deal to be included'
     A                                 11 44'Foreign Exchange'
     A            ##FX           3   B 11 65OVRDTA
     A N53                                  DSPATR(UL HI)
     A  53                                  DSPATR(RI PC)
     A                                 12 44'Money Market'
     A            ##MM           3   B 12 65OVRDTA
     A N54                                  DSPATR(UL HI)
     A  54                                  DSPATR(RI PC)
     A                                 13 44'NA''s Purchased'
     A            ##NP           3   B 13 65OVRDTA
     A N55                                  DSPATR(UL HI)
     A  55                                  DSPATR(RI PC)
     A                                 14 44'NA''s Sold'
     A            ##NS           3   B 14 65OVRDTA
     A N56                                  DSPATR(UL HI)
     A  56                                  DSPATR(RI PC)
     A                                 15 44'FRA/IRS'
     A  58                                  DSPATR(ND)
     A            ##FR           3   B 15 65OVRDTA
     A N57                                  DSPATR(UL HI)
     A  57                                  DSPATR(RI PC)
     A  58                                  DSPATR(ND PR)
     A                                 16 44'FO Customer Broker'
     A            ##FOCB         3   B 16 65OVRDTA
     A N59                                  DSPATR(UL HI)
     A  59                                  DSPATR(RI PC)
     A                                 17 44'FO Book'
     A            ##FOBK         3   B 17 65OVRDTA
     A N60                                  DSPATR(UL HI)
     A  60                                  DSPATR(RI PC)
     A*
     A*=========================================================================
     A          R ##FOOT
     A                                 23 21'F:'
     A            CEX            2Y 0O 23 24EDTCDE(Z)
     A                                 23 27'Exit'
     A            CBY            2Y 0O 23 32EDTCDE(Z)
     A                                 23 35'Bypass'
     A*=========================================================================
     A          R MSGSUB                    TEXT('Message subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #MSGKY                    SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*=========================================================================
     A          R MSGCTL                    SFLCTL(MSGSUB)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0001)
     A                                      TEXT('Message subfile control')
     A                                      OVERLAY
     A                                      SFLINZ
     A                                      SFLDSP
     A  99                                  SFLEND
     A            #PGMQ                     SFLPGMQ
