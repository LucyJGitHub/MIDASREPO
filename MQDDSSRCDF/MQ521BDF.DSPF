      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MQ RTACCOUNTS production initiation I/F')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Midas/Q Module                                       *
     A*                                                               *
     A*  DSPF/MQ521BDF - Midas RTACCOUNTS Production Initiation I/F.  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Last Amend No. CMQ001             Date 31OCT97               *
     A*  Prev Amend No. 059926             Date 26AUG93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*   CMQ001   -   MidasQ rationalisation.                        *
     A*   059926   -   Include the option to omit historic postings   *
     A*                in subset RTACCOUNTS.                          *
     A*                                                               *
     A*****************************************************************
     F*                                                               *
     F* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     F*                                                               *
     F* 01 - 24          Function Keys 01 to 24                       *
     F* 50               Include Open Accounts is Invalid             *
     F* 51               Include Closed Accounts is Invalid           *
     F* 52               Include Historic Postings is Invalid         *   059926
     F* 65               Position Cursor at ##OPEN                    *
     F* 99               Subfile End                                  *
     F*                                                               *
     F*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R ##HEAD                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CHGINPDFT
     A                                      PUTOVR
     A                                      OVERLAY
     A  01                                  CF01
     A  02                                  CF02
     A  03                                  CF03
     A  04                                  CF04
     A  05                                  CF05
     A  06                                  CF06
     A  07                                  CF07
     A  08                                  CF08
     A  09                                  CF09
     A  10                                  CF10
     A  11                                  CF11
     A  12                                  CF12
     A  13                                  CF13
     A  14                                  CF14
     A  15                                  CF15
     A  16                                  CF16
     A  17                                  CF17
     A  18                                  CF18
     A  19                                  CF19
     A  20                                  CF20
     A  21                                  CF21
     A  22                                  CF22
     A  23                                  CF23
     A  24                                  CF24
     A                                      CSRLOC(#ROW       #COL)
     A            #ROW           3S 0H
     A            #COL           3S 0H
     A            #BOX1          1I  B  1  3DSPATR(RI)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                  1 33'PRODUCE REPORT'
     A            #RNAME        13A  O  1 51
     A                                  1 71'#04'
     A            #BOX2         10I  B  2  2DSPATR(RI)
     A                                      OVRDTA
     A            #BOX3         10I  B  2 13DSPATR(RI)
     A                                      OVRDTA
     A            #BOX4         10I  B  2 24DSPATR(RI)
     A                                      OVRDTA
     A            #BOX5         10I  B  2 35DSPATR(RI)
     A                                      OVRDTA
     A            #BOX6         10I  B  2 46DSPATR(RI)
     A                                      OVRDTA
     A            #BOX7         10I  B  2 57DSPATR(RI)
     A                                      OVRDTA
     A            #BOX8          6I  B  2 68DSPATR(RI)
     A                                      OVRDTA
     A            #BOX9          5I  B  2 75DSPATR(RI)
     A                                      OVRDTA
     A            #BOX10         1I  B  1 78DSPATR(ND)
     A                                      OVRDTA
     A                                  6 13'Status of Accounts to be included'
     A                                  6 53'Open '
     A            ##OPEN         3   B  6 61OVRDTA
     A N50                                  DSPATR(UL HI)
     A  50                                  DSPATR(RI)
     A  50
     AO 65                                  DSPATR(PC)
     A                                  7 53'Closed'
     A            ##CLOS         3   B  7 61OVRDTA
     A N51                                  DSPATR(UL HI)
     A  51                                  DSPATR(RI PC)
     A*                                                                 /059926/
     A                                  9 13'Historic Postings to be +
     A                                      included'
     A            ##HIST         3   B  9 61OVRDTA
     A N52                                  DSPATR(UL HI)
     A  52                                  DSPATR(RI PC)
     A*                                                                 /059926/
     A*
     A*=========================================================================
     A          R ##FOOT
     A                                 23 21'F:'
     A            CEX            2Y 0O 23 24EDTCDE(Z)
     A                                 23 27'Exit'
     A            CBY            2Y 0O 23 32EDTCDE(Z)
     A                                 23 35'Bypass'
     A*=========================================================================
     A          R MSGSUB                    TEXT('Message subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #MSGKY                    SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*=========================================================================
     A          R MSGCTL                    SFLCTL(MSGSUB)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0001)
     A                                      TEXT('Message subfile control')
     A                                      OVERLAY
     A                                      SFLINZ
     A                                      SFLDSP
     A  99                                  SFLEND
     A            #PGMQ                     SFLPGMQ
