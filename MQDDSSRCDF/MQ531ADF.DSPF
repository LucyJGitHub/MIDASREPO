      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MQ RTCUSTLEND report definition I/F')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Midas/Q Module                                       *
     A*                                                               *
     A*  DSPF/MQ531ADF - Midas RTCUSTLEND Report Definition I/F       *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CMQ001             Date 31Oct97               *
      *                 XXXXXX             Date DDMmmYY               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CMQ001 - MidasQ rationalisation.                             *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     A*                                                               *
     A* 01 - 24          Function Keys 01 to 24                       *
     A* 26               Valid Command Key Pressed                    *
     A* 30               '0' - Display Mode  '1' - Change Mode        *
     A* 31               '0' - Allow Change  '1' - Change Not Allowed *
     A* 50               Include Live Facilities is Invalid           *
     A* 51               Include Reversed Facilities is Invalid       *
     A* 52               Include Expired Facilities is Invalid        *
     A* 53               Include Live Loans is Invalid                *
     A* 54               Include Reversed Loans is Invalid            *
     A* 55               Include Matured Loans is Invalid             *
     A* 65               Position Cursor at ##FLIV                    *
     A* 99               Subfile End                                  *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R ##HEAD                    TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      CHGINPDFT
     A                                      OVERLAY
     A  01                                  CF01
     A  02                                  CF02
     A  03                                  CF03
     A  04                                  CF04
     A  05                                  CF05
     A  06                                  CF06
     A  07                                  CF07
     A  08                                  CF08
     A  09                                  CF09
     A  10                                  CF10
     A  11                                  CF11
     A  12                                  CF12
     A  13                                  CF13
     A  14                                  CF14
     A  15                                  CF15
     A  16                                  CF16
     A  17                                  CF17
     A  18                                  CF18
     A  19                                  CF19
     A  20                                  CF20
     A  21                                  CF21
     A  22                                  CF22
     A  23                                  CF23
     A  24                                  CF24
     A                                      VLDCMDKEY(26)
     A                                      PUTOVR
     A                                      CSRLOC(#ROW       #COL)
     A            #ROW           3S 0H
     A            #COL           3S 0H
     A            #BOX1          1I  B  1  3DSPATR(RI)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                  1 31'EXECUTION CRITERIA'
     A            #RNAME        13A  O  1 51
     A                                  1 71'#02'
     A            #BOX2         10I  B  2  2DSPATR(RI)
     A                                      OVRDTA
     A            #BOX3         10I  B  2 13DSPATR(RI)
     A                                      OVRDTA
     A            #BOX4         10I  B  2 24DSPATR(RI)
     A                                      OVRDTA
     A            #BOX5         10I  B  2 35DSPATR(RI)
     A                                      OVRDTA
     A            #BOX6         10I  B  2 46DSPATR(RI)
     A                                      OVRDTA
     A            #BOX7         10I  B  2 57DSPATR(RI)
     A                                      OVRDTA
     A            #BOX8          6I  B  2 68DSPATR(RI)
     A                                      OVRDTA
     A            #BOX9          5I  B  2 75DSPATR(RI)
     A                                      OVRDTA
     A            #BOX10         1I  B  1 78DSPATR(ND)
     A N30                                  DSPATR(PC)
     A                                  6 13'Status of Facilities to be include-
     A                                      d'
     A                                  6 53'Live'
     A            ##FLIV         3A  B  6 63OVRDTA
     A  30N50                               DSPATR(UL)
     A  30N50                               DSPATR(HI)
     A  30 50                               DSPATR(RI)
     A N30                                  DSPATR(PR)
     A  30 50
     AO 65                                  DSPATR(PC)
     A                                  7 53'Reversed'
     A            ##FREV         3A  B  7 63OVRDTA
     A  30N51                               DSPATR(UL)
     A  30N51                               DSPATR(HI)
     A  30 51                               DSPATR(RI)
     A  30 51                               DSPATR(PC)
     A N30                                  DSPATR(PR)
     A                                  8 53'Expired'
     A            ##FEXP         3A  B  8 63OVRDTA
     A  30N52                               DSPATR(UL)
     A  30N52                               DSPATR(HI)
     A  30 52                               DSPATR(RI)
     A  30 52                               DSPATR(PC)
     A N30                                  DSPATR(PR)
     A                                 10 13'Status of Loans to be included'
     A                                 10 53'Live'
     A            ##LLIV         3A  B 10 63OVRDTA
     A  30N53                               DSPATR(UL)
     A  30N53                               DSPATR(HI)
     A  30 53                               DSPATR(RI)
     A  30 53                               DSPATR(PC)
     A N30                                  DSPATR(PR)
     A                                 11 53'Reversed'
     A            ##LREV         3A  B 11 63OVRDTA
     A  30N54                               DSPATR(UL)
     A  30N54                               DSPATR(HI)
     A  30 54                               DSPATR(RI)
     A  30 54                               DSPATR(PC)
     A N30                                  DSPATR(PR)
     A                                 12 53'Matured'
     A            ##LMAT         3A  B 12 63OVRDTA
     A  30N55                               DSPATR(UL)
     A  30N55                               DSPATR(HI)
     A  30 55                               DSPATR(RI)
     A  30 55                               DSPATR(PC)
     A N30                                  DSPATR(PR)
     A          R ##FOOT
     A                                 23 21'F:'
     A            CEX            2Y 0O 23 24EDTCDE(Z)
     A                                 23 27'Exit'
     A            CBY            2Y 0O 23 32EDTCDE(Z)
     A                                 23 35'Bypass'
     A N30N31     CCH            2Y 0O 23 42EDTCDE(Z)
     A N30N31                          23 45'Change'
     A*=========================================================================
     A          R MSGSUB                    TEXT('Message subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #MSGKY                    SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*=========================================================================
     A          R MSGCTL                    TEXT('Message subfile control')
     A                                      OVERLAY
     A                                      SFLCTL(MSGSUB)
     A                                      SFLINZ
     A                                      SFLSIZ(13)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A  99                                  SFLEND
     A            #PGMQ                     SFLPGMQ
