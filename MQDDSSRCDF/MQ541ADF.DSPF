      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MQ RTEVENTS report definition I/F')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Midas/Q Module                                       *
     A*                                                               *
     A*  DSPF/MQ541ADF - Midas RTEVENTS Report Definition I/F         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Last Amend No. CMQ001             Date 31OCT97               *
     A*  Prev Amend No. xxxxxx             Date ddmmmyy               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  CMQ001 - MidasQ rationalisation.                             *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     A*                                                               *
     A* 01 - 24          Function Keys 01 to 24                       *
     A* 26               Valid Command Key Pressed                    *
     A* 30               '0' - Display Mode  '1' - Change Mode        *
     A* 31               '0' - Allow Change  '1' - Change Not Allowed *
     A* 50               Include Lending is invalid                   *
     A* 51               Include Dealing is Invalid                   *
     A* 52               Include F&O is Invalid                       *
     A* 53               Include Funds Transfer is Invalid            *
     A* 54               Include Book Positions is Invalid            *
     A* 55               Include Customer Positions is Invalid        *
     A* 56               Include Depot Postions is Invalid            *
     A* 57               Include Trades is Invalid                    *
     A* 58               Include Portfolio Customer Trades is Invalid *
     A* 59               Include Depot Movements is Invalid           *
     A* 60               Select ALL is Invalid                        *
     A* 61               Select from ALL is Invalid                   *
     A* 62               Select Generic 1 and 2 is Invalid            *
     A* 63               Select Wild is Invalid                       *
     A* 65               Position Cursor at ##LE                      *
     A* 66               Display SEL1 (1) Non Display (0)             *
     A* 67               Display SEL2 (1) Non Display (0)             *
     A* 68               Display SEL3 (1) Non Display (0)             *
     A* 99               Subfile End                                  *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R ##HEAD                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CHGINPDFT
     A                                      OVERLAY
     A  01                                  CF01
     A  02                                  CF02
     A  03                                  CF03
     A  04                                  CF04
     A  05                                  CF05
     A  06                                  CF06
     A  07                                  CF07
     A  08                                  CF08
     A  09                                  CF09
     A  10                                  CF10
     A  11                                  CF11
     A  12                                  CF12
     A  13                                  CF13
     A  14                                  CF14
     A  15                                  CF15
     A  16                                  CF16
     A  17                                  CF17
     A  18                                  CF18
     A  19                                  CF19
     A  20                                  CF20
     A  21                                  CF21
     A  22                                  CF22
     A  23                                  CF23
     A  24                                  CF24
     A                                      VLDCMDKEY(26)
     A                                      PUTOVR
     A                                      CSRLOC(#ROW       #COL)
     A            #ROW           3S 0H
     A            #COL           3S 0H
     A            #BOX1          1I  B  1  3DSPATR(RI)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                  1 31'EXECUTION CRITERIA'
     A            #RNAME        13A  O  1 51
     A                                  1 71'#02'
     A            #BOX2         10I  B  2  2DSPATR(RI)
     A                                      OVRDTA
     A            #BOX3         10I  B  2 13DSPATR(RI)
     A                                      OVRDTA
     A            #BOX4         10I  B  2 24DSPATR(RI)
     A                                      OVRDTA
     A            #BOX5         10I  B  2 35DSPATR(RI)
     A                                      OVRDTA
     A            #BOX6         10I  B  2 46DSPATR(RI)
     A                                      OVRDTA
     A            #BOX7         10I  B  2 57DSPATR(RI)
     A                                      OVRDTA
     A            #BOX8          6I  B  2 68DSPATR(RI)
     A                                      OVRDTA
     A            #BOX9          5I  B  2 75DSPATR(RI)
     A                                      OVRDTA
     A            #BOX10         1I  B  1 78DSPATR(ND)
     A                                  5  6'Type of Events to be included'
     A                                  7  6'Lending'
     A            ##LE           3A  B  7 31OVRDTA
     A  30N50                               DSPATR(UL HI)
     A  30 50                               DSPATR(RI)
     A N30                                  DSPATR(PR)
     A  30 50
     AO 65                                  DSPATR(PC)
     A                                  7 39'Dealing'
     A            ##DL           3A  B  7 70OVRDTA
     A  30N51                               DSPATR(UL HI)
     A  30 51                               DSPATR(RI PC)
     A N30                                  DSPATR(PR)
     A                                  8  6'Futures & Options'
     A            ##FF           3A  B  8 31OVRDTA
     A  30N52                               DSPATR(UL HI)
     A  30 52                               DSPATR(RI PC)
     A N30                                  DSPATR(PR)
     A                                  8 39'Funds Transfer'
     A            ##FT           3A  B  8 70OVRDTA
     A  30N53                               DSPATR(UL HI)
     A  30 53                               DSPATR(RI PC)
     A N30                                  DSPATR(PR)
     A                                  9  6'SE Book Positions'
     A            ##BP           3A  B  9 31OVRDTA
     A  30N54                               DSPATR(UL HI)
     A  30 54                               DSPATR(RI PC)
     A N30                                  DSPATR(PR)
     A                                  9 39'SE Portfolio Customer Trades'
     A            ##PC           3A  B  9 70OVRDTA
     A  30N58                               DSPATR(UL HI)
     A  30 58                               DSPATR(RI PC)
     A N30                                  DSPATR(PR)
     A                                 10  6'SE Depot Positions'
     A            ##DP           3A  B 10 31OVRDTA
     A  30N56                               DSPATR(UL HI)
     A  30 56                               DSPATR(RI PC)
     A N30                                  DSPATR(PR)
     A                                 10 39'SE Trades'
     A            ##TR           3A  B 10 70OVRDTA
     A  30N57                               DSPATR(UL HI)
     A  30 57                               DSPATR(RI PC)
     A N30                                  DSPATR(PR)
     A                                 11  6'SE Customer Positions'
     A            ##CP           3A  B 11 31OVRDTA
     A  30N55                               DSPATR(UL HI)
     A  30 55                               DSPATR(RI PC)
     A N30                                  DSPATR(PR)
     A                                 11 39'SE Depot Movements'
     A            ##DM           3A  B 11 70OVRDTA
     A  30N59                               DSPATR(UL HI)
     A  30 59                               DSPATR(RI PC)
     A N30                                  DSPATR(PR)
     A                                 13  6'Event Code Selection'
     A                                 15 15'Select All Event Codes'
     A            ##ALL          3A  B 15 57OVRDTA
     A  30N60                               DSPATR(UL HI)
     A  30 60                               DSPATR(RI PC)
     A N30                                  DSPATR(PR)
     A                                 16 15'Or'
     A            ##SEL1         1A  B 17 13OVRDTA
     A  66N61                               DSPATR(UL HI)
     A  66 61                               DSPATR(RI)
     A N66                                  DSPATR(ND)
     A  61
     AON30 66                               DSPATR(PC)
     A N30N66                               DSPATR(PR)
     A                                 17 15'Select from a list of All Event Co-
     A                                      des'
     A            ##LIST         3A  O 17 57OVRDTA
     A            ##SEL2         1A  B 18 13OVRDTA
     A  67N62                               DSPATR(UL HI)
     A  67 62                               DSPATR(RI)
     A N67                                  DSPATR(ND)
     A  62
     AON30 67                               DSPATR(PC)
     A N30N67                               DSPATR(PR)
     A                                 18 15'Select on First and Second Charact-
     A                                      ers'
     A            ##12           3A  O 18 57OVRDTA
     A            ##SEL3         1A  B 19 13OVRDTA
     A  68N63                               DSPATR(UL HI)
     A  68 63                               DSPATR(RI)
     A N68                                  DSPATR(ND)
     A  63
     AON30 68                               DSPATR(PC)
     A N30N68                               DSPATR(PR)
     A                                 19 15'Wild Card Selection'
     A            ##WILD         3A  O 19 57OVRDTA
     A          R ##FOOT
     A                                 23 21'F:'
     A            CEX            2Y 0O 23 24EDTCDE(Z)
     A                                 23 27'Exit'
     A            CBY            2Y 0O 23 32EDTCDE(Z)
     A                                 23 35'Bypass'
     A N30N31     CCH            2Y 0O 23 42EDTCDE(Z)
     A N30N31                          23 45'Change'
     A*=========================================================================
     A          R MSGSUB                    TEXT('Message subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #MSGKY                    SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*=========================================================================
     A          R MSGCTL                    TEXT('Message subfile control')
     A                                      OVERLAY
     A                                      SFLCTL(MSGSUB)
     A                                      SFLINZ
     A                                      SFLSIZ(13)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A  99                                  SFLEND
     A            #PGMQ                     SFLPGMQ
