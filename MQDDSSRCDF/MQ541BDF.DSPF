      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MQ RTEVENTS production initiation I/F')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Midas/Q Module                                       *
     A*                                                               *
     A*  DSPF/MQ541BDF - Midas RTEVENTS Production Initiation I/F.    *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CMQ001             Date 31Oct97               *
      *                 XXXXXX             Date DDMmmYY               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CMQ001 - MidasQ rationalisation.                             *
     A*                                                               *
     A*****************************************************************
     F*                                                               *
     F* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     F*                                                               *
     F* 01 - 24          Function Keys 01 to 24                       *
     F* 65               Position Cursor at ##LIVE                    *
     A* 50               Include Lending is invalid                   *
     A* 51               Include Dealing is Invalid                   *
     A* 52               Include F&O is Invalid                       *
     A* 53               Include Funds Transfer is Invalid            *
     A* 54               Include Book Positions is Invalid            *
     A* 55               Include Customer Positions is Invalid        *
     A* 56               Include Depot Postions is Invalid            *
     A* 57               Include Trades is Invalid                    *
     A* 58               Include Portfolio Customer Trades is Invalid *
     A* 59               Include Depot Movements is Invalid           *
     A* 60               Select ALL is Invalid                        *
     A* 61               Select from ALL is Invalid                   *
     A* 62               Select Generic 1 and 2 is Invalid            *
     A* 63               Select Wild is Invalid                       *
     A* 65               Position Cursor at ##LE                      *
     F* 99               Subfile End                                  *
     F*                                                               *
     F*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R ##HEAD                    TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      CHGINPDFT
     A                                      PUTOVR
     A                                      OVERLAY
     A  01                                  CF01
     A  02                                  CF02
     A  03                                  CF03
     A  04                                  CF04
     A  05                                  CF05
     A  06                                  CF06
     A  07                                  CF07
     A  08                                  CF08
     A  09                                  CF09
     A  10                                  CF10
     A  11                                  CF11
     A  12                                  CF12
     A  13                                  CF13
     A  14                                  CF14
     A  15                                  CF15
     A  16                                  CF16
     A  17                                  CF17
     A  18                                  CF18
     A  19                                  CF19
     A  20                                  CF20
     A  21                                  CF21
     A  22                                  CF22
     A  23                                  CF23
     A  24                                  CF24
     A                                      CSRLOC(#ROW       #COL)
     A            #ROW           3S 0H
     A            #COL           3S 0H
     A            #BOX1          1I  B  1  3DSPATR(RI)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                  1 33'PRODUCE REPORT'
     A            #RNAME        13A  O  1 51
     A                                  1 71'#04'
     A            #BOX2         10I  B  2  2DSPATR(RI)
     A                                      OVRDTA
     A            #BOX3         10I  B  2 13DSPATR(RI)
     A                                      OVRDTA
     A            #BOX4         10I  B  2 24DSPATR(RI)
     A                                      OVRDTA
     A            #BOX5         10I  B  2 35DSPATR(RI)
     A                                      OVRDTA
     A            #BOX6         10I  B  2 46DSPATR(RI)
     A                                      OVRDTA
     A            #BOX7         10I  B  2 57DSPATR(RI)
     A                                      OVRDTA
     A            #BOX8          6I  B  2 68DSPATR(RI)
     A                                      OVRDTA
     A            #BOX9          5I  B  2 75DSPATR(RI)
     A                                      OVRDTA
     A            #BOX10         1I  B  1 78DSPATR(ND)
     A                                      OVRDTA
     A                                  5  6'Type of Events to be included'
     A                                  7  6'Lending'
     A            ##LE           3   B  7 31OVRDTA
     A N50                                  DSPATR(UL HI)
     A  50                                  DSPATR(RI)
     A  50
     AO 65                                  DSPATR(PC)
     A                                  7 39'Dealing'
     A            ##DL           3A  B  7 70OVRDTA
     A N51                                  DSPATR(UL HI)
     A  51                                  DSPATR(RI PC)
     A                                  8  6'Futures & Options'
     A            ##FF           3A  B  8 31OVRDTA
     A N52                                  DSPATR(UL HI)
     A  52                                  DSPATR(RI PC)
     A                                  8 39'Funds Transfer'
     A            ##FT           3A  B  8 70OVRDTA
     A N53                                  DSPATR(UL HI)
     A  53                                  DSPATR(RI PC)
     A                                  9  6'SE Book Positions'
     A            ##BP           3A  B  9 31OVRDTA
     A N54                                  DSPATR(UL HI)
     A  54                                  DSPATR(RI PC)
     A                                  9 39'SE Portfolio Customer Trades'
     A            ##PC           3A  B  9 70OVRDTA
     A N58                                  DSPATR(UL HI)
     A  58                                  DSPATR(RI PC)
     A                                 10  6'SE Depot Positions'
     A            ##DP           3A  B 10 31OVRDTA
     A N56                                  DSPATR(UL HI)
     A  56                                  DSPATR(RI PC)
     A                                 10 39'SE Trades'
     A            ##TR           3A  B 10 70OVRDTA
     A N57                                  DSPATR(UL HI)
     A  57                                  DSPATR(RI PC)
     A                                 11  6'SE Customer Positions'
     A            ##CP           3A  B 11 31OVRDTA
     A N55                                  DSPATR(UL HI)
     A  55                                  DSPATR(RI PC)
     A                                 11 39'SE Depot Movements'
     A            ##DM           3A  B 11 70OVRDTA
     A N59                                  DSPATR(UL HI)
     A  59                                  DSPATR(RI PC)
     A                                 13  6'Event Code Selection'
     A                                 15 15'Select All Event Codes'
     A            ##ALL          3A  B 15 57OVRDTA
     A N60                                  DSPATR(UL HI)
     A  60                                  DSPATR(RI PC)
     A                                 16 15'Or'
     A            ##SEL1         1A  B 17 13OVRDTA
     A N61                                  DSPATR(UL HI)
     A  61                                  DSPATR(RI PC)
     A                                 17 15'Select from a list of All Event Co-
     A                                      des'
     A            ##LIST         3A  O 17 57OVRDTA
     A            ##SEL2         1A  B 18 13OVRDTA
     A N62                                  DSPATR(UL HI)
     A  62                                  DSPATR(RI)
     A                                 18 15'Select on First and Second Charact-
     A                                      ers'
     A            ##12           3A  O 18 57OVRDTA
     A            ##SEL3         1A  B 19 13OVRDTA
     A N63                                  DSPATR(UL HI)
     A  63                                  DSPATR(RI)
     A                                 19 15'Wild Card Selection'
     A            ##WILD         3A  O 19 57OVRDTA
     A*
     A*=========================================================================
     A          R ##FOOT
     A                                 23 21'F:'
     A            CEX            2Y 0O 23 24EDTCDE(Z)
     A                                 23 27'Exit'
     A            CBY            2Y 0O 23 32EDTCDE(Z)
     A                                 23 35'Bypass'
     A*=========================================================================
     A          R MSGSUB                    TEXT('Message subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #MSGKY                    SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*=========================================================================
     A          R MSGCTL                    SFLCTL(MSGSUB)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0001)
     A                                      TEXT('Message subfile control')
     A                                      OVERLAY
     A                                      SFLINZ
     A                                      SFLDSP
     A  99                                  SFLEND
     A            #PGMQ                     SFLPGMQ
