      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MQ RTEVENTS event code selection')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Midas/Q Module                                       *
     A*                                                               *
     A*  DSPF/MQ542XDF - Midas RTEVENTS Event Code Selection.         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CMQ001             Date 31OCT97               *
      *                 xxxxxx             Date ddMmmyy               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  CMQ001 - MidasQ rationalisation.                             *
     A*                                                               *
     A*****************************************************************
     F*                                                               *
     F* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     F*                                                               *
     F* 01 - 24          Function Keys 01 to 24                       *
     F* 26               Valid Command Key Pressed                    *
     F* 30               '0' - Display Mode  '1' - Change Mode        *
     F*                                                               *
     F*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R ##SRCD                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A            ##SEL          1   B  7  5
     A  30                                  DSPATR(UL HI PC)
     A N30                                  DSPATR(PR)
      **Start of change CGL029
     A**********  ##CODE         4   O  7 10                                                  CGL029
     A            ##CODE        10   O  7  8
      **End of change CGL029
     A            ##DESC        60   O  7 20
     A          R ##SCTL                    SFLCTL(##SRCD)
     A                                      CHGINPDFT
     A                                      OVERLAY
     A                                      CF03(03)
     A                                      PUTOVR
     A N52                                  ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A N50N51                               SFLCLR
     A  52                                  SFLEND
     A                                      SFLSIZ(0040)
     A                                      SFLPAG(0015)
     A                                      CSRLOC(#ROW       #COL)
     A            #ROW           3S 0H
     A            #COL           3S 0H
     A            #BOX1          1I  B  1  3DSPATR(RI)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A            #TITLE        25A  O  1 25OVRDTA
     A            #RNAME        13A  O  1 51
     A                                  1 71'#02'
     A            #BOX2         10I  B  2  2DSPATR(RI)
     A                                      OVRDTA
     A            #BOX3         10I  B  2 13DSPATR(RI)
     A                                      OVRDTA
     A            #BOX4         10I  B  2 24DSPATR(RI)
     A                                      OVRDTA
     A            #BOX5         10I  B  2 35DSPATR(RI)
     A                                      OVRDTA
     A            #BOX6         10I  B  2 46DSPATR(RI)
     A                                      OVRDTA
     A            #BOX7         10I  B  2 57DSPATR(RI)
     A                                      OVRDTA
     A            #BOX8          6I  B  2 68DSPATR(RI)
     A                                      OVRDTA
     A            #BOX9          5I  B  2 75DSPATR(RI)
     A                                      OVRDTA
     A            #BOX10         1I  B  1 78DSPATR(ND)
     A N30                                  DSPATR(PC)
     A*
     A                                  4 10'Event'
     A                                      DSPATR(HI UL)
     A                                  5 10'Code'
     A                                      DSPATR(HI UL)
     A                                  5 20'Transaction'
     A                                      DSPATR(HI UL)
     A                                  5 40'Date'
     A                                      DSPATR(HI UL)
     A                                  5 60'Amount'
     A                                      DSPATR(HI UL)
     A            DSPREC         4S 0H      SFLRCDNBR
     A          R ##FOOT                    OVERLAY
     A                                 23 21'F:  3 Exit'
