      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MQ RTALM Subset Extract Parameters')
     A*****************************************************************
     A*                                                               *
     A*  Midas Midas/Q Module                      *
     A*                                                               *
     A*   DSPF/MQ553XDF - MIDAS/Q SUBSET EXTRACT PARAMETERS           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
     A*     Last Amend No. BUG3179              Date. 12Jun04         *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*     Prev Amend No. XXXXXX               Date. DDMMMYY         *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*     BUG3179 - Add indicator to Message line so that it does   *
     A*               not appear in Reverse Image when no error       *
     A*     XXXXXX  -                                                 *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      CF03
     A*
     A          R MQ553XF0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A            USER          10A  O 01 02
     A                                 01 13'MQ553X'
     A                                 01 20'MIDAS/Q SUBSET RTALM EXTRACT -
     A                                      PARAMETERS MAINTENANCE'
     A                                      DSPATR(UL HI)
     A            BJMRDT         7A  O 01 72
     A*
     A            WSID          10A  O 02 02
     A                                 02 71TIME
     A*
     A                                 04 02'Enter ''Y'' against the Modules -
     A                                      to be extracted for Midas/Q -
     A                                      subset RTALM:'
     A                                      DSPATR(HI)
     A*
     A  01        STXT1         20A  O 06 02
     A            SSEL1          1A  B 06 24
     A  01N31                               DSPATR(UL HI)
     A  31                                  DSPATR(RI PC)
     A N01                                  DSPATR(ND PR)
     A  02        STXT2         20A  O 08 02
     A            SSEL2          1A  B 08 24
     A*
     A  02N32                               DSPATR(UL HI)
     A  32                                  DSPATR(RI PC)
     A N02                                  DSPATR(ND PR)
     A*
     A  03        STXT3         20A  O 10 02
     A            SSEL3          1A  B 10 24
     A  03N33                               DSPATR(UL HI)
     A  33                                  DSPATR(RI PC)
     A N03                                  DSPATR(ND PR)
     A*
     A  04        STXT4         20A  O 12 02
     A            SSEL4          1A  B 12 24
     A  04N34                               DSPATR(UL HI)
     A  34                                  DSPATR(RI PC)
     A N04                                  DSPATR(ND PR)
     A*
     A  05        STXT5         20A  O 06 32
     A            SSEL5          1A  B 06 54
     A  05N35                               DSPATR(UL HI)
     A  35                                  DSPATR(RI PC)
     A N05                                  DSPATR(ND PR)
     A*
     A  06        STXT6         20A  O 08 32
     A            SSEL6          1A  B 08 54
     A  06N36                               DSPATR(UL HI)
     A  36                                  DSPATR(RI PC)
     A N06                                  DSPATR(ND PR)
     A*
     A  07        STXT7         20A  O 10 32
     A            SSEL7          1A  B 10 54
     A  07N37                               DSPATR(UL HI)
     A  37                                  DSPATR(RI PC)
     A N07                                  DSPATR(ND PR)
     A*
     A  08        STXT8         20A  O 12 32
     A            SSEL8          1A  B 12 54
     A  08N38                               DSPATR(UL HI)
     A  38                                  DSPATR(RI PC)
     A N08                                  DSPATR(ND PR)
     A*/COPY WNCPYSRC,MQ553XDF01
     A*
     A                                 15 02'Enter the Run Conditions for the -
     A                                      Extract:'
     A                                      DSPATR(HI)
     A*
     A                                 17 02'Extract Frequency'
     A            SFREQ          1A  B 17 22
     A N39                                  DSPATR(UL HI)
     A  39                                  DSPATR(RI PC)
     A                                 17 30'D = Daily, W = Weekly, M = Monthly'
     A*
     A                                 18 02'Base Day'
     A            SBSDY          2A  B 18 22
     A N40                                  DSPATR(UL HI)
     A  40                                  DSPATR(RI PC)
     A*
     A                                 19 02'Date of Next Run'
     A            SNRUN          6A  B 19 22
     A N41                                  DSPATR(UL HI)
     A  41                                  DSPATR(RI PC)
     A*/COPY WNCPYSRC,MQ553XDF02
     A*
     A                                 21 02'Run Extract Today?'
     A            SRNTD          1A  B 21 22
     A N42                                  DSPATR(UL HI)
     A  42                                  DSPATR(RI PC)
     A                                 21 30'Y = run today as an exception to -
     A                                      defined frequency'
     A*
     A**Start of change BUG3179
     A**********  MSG           79A  O 23 02DSPATR(RI)                                   /*BUG3179*/
     A            MSG           79A  O 23 02
     A  30                                  DSPATR(RI)
      *********
     A**End of change BUG3179
     A                                      DSPATR(HI)
     A N30                                  DSPATR(ND)
     A*
     A                                 24 02'F3=Exit'
     A*
