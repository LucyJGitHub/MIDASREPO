      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MQ RTMANAGE report definition i/f')
     A*****************************************************************
     A*                                                               *
     A*  Midas Midas Q                                                *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01419             Date 17May93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*   S01419 - CREATED FOR RTMANAGE SUBSET                        *
     A*                                                               *
     A*****************************************************************
     F*                                                               *
     F* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     F*                                                               *
     F* 01 - 24          Function Keys 01 to 24                       *
     F* 26               Valid Command Key Pressed                    *
     F* 30               '0' - Display Mode  '1' - Change Mode        *
     F*                                                               *
     F*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R ##SRCD                    TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SFL
     A            ##SEL          1   B 10  5
     A                                      CHECK(ER)
     A  30                                  DSPATR(UL HI PC)
     A N30                                  DSPATR(PR)
     A            ##CODE         2   O 10 10
     A            ##DESC        30   O 10 20
     A            ##ABAC         1   H
     A            ##ABCY         1   H
     A            ##ABBR         1   H
     A            ##ABCU         1   H
     A            ##ABAS         1   H
     A            ##ABPC         1   H
     A            ##ABBK         1   H
     A            ##ABTT         1   H
     A            ##ABTS         1   H
     A            ##ASCU         1   H
     A          R ##SCTL                    SFLCTL(##SRCD)
     A                                      CHGINPDFT
     A                                      OVERLAY
     A N52                                  ROLLUP(25)
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A N50N51                               SFLCLR
     A  52                                  SFLEND
     A                                      SFLSIZ(0025)
     A                                      SFLPAG(0005)
     A  01                                  CF01
     A  02                                  CF02
     A  03                                  CF03
     A  04                                  CF04
     A  05                                  CF05
     A  06                                  CF06
     A  07                                  CF07
     A  08                                  CF08
     A  09                                  CF09
     A  10                                  CF10
     A  11                                  CF11
     A  12                                  CF12
     A  13                                  CF13
     A  14                                  CF14
     A  15                                  CF15
     A  16                                  CF16
     A  17                                  CF17
     A  18                                  CF18
     A  19                                  CF19
     A  20                                  CF20
     A  21                                  CF21
     A  22                                  CF22
     A  23                                  CF23
     A  24                                  CF24
     A                                      VLDCMDKEY(26)
     A                                      PUTOVR
     A                                      CSRLOC(#ROW       #COL)
     A            #ROW           3S 0H
     A            #COL           3S 0H
     A            #BOX1          1I  B  1  3DSPATR(RI)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                  1 31'EXECUTION CRITERIA'
     A            #RNAME        13A  O  1 51
     A                                  1 71'#02'
     A            #BOX2         10I  B  2  2DSPATR(RI)
     A                                      OVRDTA
     A            #BOX3         10I  B  2 13DSPATR(RI)
     A                                      OVRDTA
     A            #BOX4         10I  B  2 24DSPATR(RI)
     A                                      OVRDTA
     A            #BOX5         10I  B  2 35DSPATR(RI)
     A                                      OVRDTA
     A            #BOX6         10I  B  2 46DSPATR(RI)
     A                                      OVRDTA
     A            #BOX7         10I  B  2 57DSPATR(RI)
     A                                      OVRDTA
     A            #BOX8          6I  B  2 68DSPATR(RI)
     A                                      OVRDTA
     A            #BOX9          5I  B  2 75DSPATR(RI)
     A                                      OVRDTA
     A            #BOX10         1I  B  1 78DSPATR(ND)
     A N30                                  DSPATR(PC)
     A*
     A                                  5 10'Group code currently selected:'
     A                                       DSPATR(HI)
     A            $$CGCD         2   O  5 41
     A                                       DSPATR(HI)
     A                                  7 10'Group'
     A                                      DSPATR(HI UL)
     A                                  8 10'Code'
     A                                      DSPATR(HI UL)
     A                                  8 20'Description'
     A                                      DSPATR(HI UL)
     A            DSPREC         4S 0H      SFLRCDNBR
     A          R ##FOOT                    OVERLAY
     A                                 23 21'F:'
     A            CEX            2Y 0O 23 24EDTCDE(Z)
     A                                 23 27'Exit'
     A            CBY            2Y 0O 23 32EDTCDE(Z)
     A                                 23 35'Bypass'
     A N30N31     CCH            2Y 0O 23 42EDTCDE(Z)
     A N30N31                          23 45'Change'
     A*=========================================================================
     A          R MSGSUB                    TEXT('Message subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #MSGKY                    SFLMSGKEY
     A            #PGMQ                     SFLPGMQ
     A*=========================================================================
     A          R MSGCTL                    TEXT('Message subfile control')
     A                                      OVERLAY
     A                                      SFLCTL(MSGSUB)
     A                                      SFLINZ
     A                                      SFLSIZ(13)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A  99                                  SFLEND
     A            #PGMQ                     SFLPGMQ
