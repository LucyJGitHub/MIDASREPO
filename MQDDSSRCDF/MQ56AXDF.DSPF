      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MQ RTMANAGE RDI Analysis code prompts')
     A*****************************************************************
     A*                                                               *
     A*  Midas      Midas/Q                                *
     A*                                                               *
     A*     MQ56AXDF - ANALYSIS CODE SELECTION PROMPT                 *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 104199             Date 10Jun96               *
      *                 S01419             Date 17May93               *
     A*                                                               *
     A*****************************************************************
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*     104199 - Add program name to DSPF for GUI identification  *
     A*     S01419 - CREATED FOR RTMANAGE SUBSET                      *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*    /////////////////////////////////////////                  *
     A*                                                               *
     A*****************************************************************
     A*
     A* All cmd keys are enabled, pgm checks for valid keys
     A                                      DSPSIZ(24 80 *DS3)
     A*                                                                   S01179
     A  01                                  CF01
     A  02                                  CF02
     A  03                                  CF03
     A  04                                  CF04
     A  05                                  CF05
     A  06                                  CF06
     A  07                                  CF07
     A  08                                  CF08
     A  09                                  CF09
     A  10                                  CF10
     A  11                                  CF11
     A  12                                  CF12
     A  13                                  CF13
     A  14                                  CF14
     A  15                                  CF15
     A  16                                  CF16
     A  17                                  CF17
     A  18                                  CF18
     A  19                                  CF19
     A  20                                  CF20
     A  21                                  CF21
     A  22                                  CF22
     A  23                                  CF23
     A  24                                  CF24
     A*                                                                   S01179
     A                                      VLDCMDKEY(26)
     A                                      PRINT
      *
      ** Header format
     A          R MQ56AXF1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CHGINPDFT
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      CSRLOC(#ROW       #COL)
     A            #ROW           3S 0H
     A            #COL           3S 0H
     A            #BOX1          1I  B  1  3DSPATR(RI)
     A                                      DSPATR(PR)
     A                                      OVRDTA
      *                                                    /104199/
     A                                  1 13'MQ56AX'
      *                                                    /104199/
     A                                  1 31'EXECUTION CRITERIA'
     A            #RNAME        13A  O  1 51
     A                                  1 71'#02'
     A            #BOX2         10I  B  2  2DSPATR(RI)
     A                                      OVRDTA
     A            #BOX3         10I  B  2 13DSPATR(RI)
     A                                      OVRDTA
     A            #BOX4         10I  B  2 24DSPATR(RI)
     A                                      OVRDTA
     A            #BOX5         10I  B  2 35DSPATR(RI)
     A                                      OVRDTA
     A            #BOX6         10I  B  2 46DSPATR(RI)
     A                                      OVRDTA
     A            #BOX7         10I  B  2 57DSPATR(RI)
     A                                      OVRDTA
     A            #BOX8          6I  B  2 68DSPATR(RI)
     A                                      OVRDTA
     A            #BOX9          5I  B  2 75DSPATR(RI)
     A                                      OVRDTA
     A            #BOX10         1I  B  1 78DSPATR(ND)
      *
      ** Postings prompt format
     A          R MQ56AXF2                  TEXT('Analysis prompt format')
     A                                      KEEP
     A*
     A                                      OVERLAY
      *
     A                                  5  2'Analysis for control group :-'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            $$CGCD         2   O  5 34
     A                                      DSPATR(HI)
     A            $$DESC        30   O  5 37
     A                                      DSPATR(HI)
     A                                  8  2'Leaving a field blank will +
     A                                      lead to the inclusion of all +
     A                                      records for that field'
     A  50                                  DSPATR(RI)
     A N50                                  DSPATR(ND)
     A                                 10 43'Selection available by :-'
     A                                      DSPATR(UL)
     A                                 11  6'Account code'
     A**Start of change CGL029
     A**********  FLD001         4   B 11 52                                                  CGL029
     A            FLD001        10   B 11 52
     A**End of change CGL029
     A N51N80                               DSPATR(UL)
     A  51                                  DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51
     AO 65                                  DSPATR(PC)
     A  51                                  DSPATR(BL)
     A  80                                  DSPATR(PR)
     A                                 12  6'Currency'
     A            FLD002         3   B 12 52
     A N52N81                               DSPATR(UL)
     A  52                                  DSPATR(HI)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(BL)
     A  81                                  DSPATR(PR)
     A                                 13  6'Branch'
     A            FLD003         3   B 13 52
     A N53N82                               DSPATR(UL)
     A  53                                  DSPATR(HI)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(BL)
     A  82                                  DSPATR(PR)
     A                                 14  6'Customer'
     A            FLD004        10   B 14 52
     A N54N83                               DSPATR(UL)
     A  54                                  DSPATR(HI)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(BL)
     A  83                                  DSPATR(PR)
     A                                 15  6'Account sequence no.'
     A            FLD005         2   B 15 52
     A N55N84                               DSPATR(UL)
     A  55                                  DSPATR(HI)
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(BL)
     A  84                                  DSPATR(PR)
     A                                 16  6'Profit centre'
     A            FLD006         4   B 16 52
     A N56N85                               DSPATR(UL)
     A  56                                  DSPATR(HI)
     A  56                                  DSPATR(RI)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(BL)
     A  85                                  DSPATR(PR)
     A                                 17  6'Book'
     A            FLD007         2   B 17 52
     A N57N86                               DSPATR(UL)
     A  57                                  DSPATR(HI)
     A  57                                  DSPATR(RI)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(BL)
     A  86                                  DSPATR(PR)
     A                                 18  6'Transaction type'
     A            FLD008        10   B 18 52
     A N58N87                               DSPATR(UL)
     A  58                                  DSPATR(HI)
     A  58                                  DSPATR(RI)
     A  58                                  DSPATR(PC)
     A  58                                  DSPATR(BL)
     A  87                                  DSPATR(PR)
     A                                 19 06'Transaction sub-type'
     A            FLD009        10   B 19 52
     A N59N88                               DSPATR(UL)
     A  59                                  DSPATR(HI)
     A  59                                  DSPATR(RI)
     A  59                                  DSPATR(PC)
     A  59                                  DSPATR(BL)
     A  88                                  DSPATR(PR)
     A                                 20  6'Associated customer'
     A            FLD010        10   B 20 52
     A N60N89                               DSPATR(UL)
     A  60                                  DSPATR(HI)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(BL)
     A  89                                  DSPATR(PR)
     A*
     A                                 22  6'**** Validation is SUCCESSFUL +
     A                                      - Please press enter to continue +
     A                                      ****'
     A N50                                  DSPATR(RI)
     A  50                                  DSPATR(ND)
      *
      ** Type / Subtype information screen.
     A          R MQ56AXF3                  TEXT('Type/Subtype information')
     A*
     A                                      OVERLAY
      *
     A                                  8 10'The original transaction type +
     A                                      for each module can be '
     A                                  9 10'determined by different means.'
     A                                 10 10'This is also true of the orig+
     A                                      inal transaction subtypes.'
     A                                 11 10'The general rule would be to +
     A                                      look up the types and subtypes'
     A                                 12 10'in the documentation for the +
     A                                      corresponding module.'
     A                                 13 10'Information about the Lending +
     A                                      module is also available from'
     A                                 14 10 'Standing Data enquiries '
      *
     A                                 20 10'Please press ENTER to continue'
      *
      ** A/c seq information screen.
     A          R MQ56AXF4                  TEXT('A/C seq. information')
     A*
     A                                      OVERLAY
      *
     A                                  8 10'The Account sequence number +
     A                                      differentiates between '
     A                                  9 10'otherwise identical Accounts'
     A                                 10 10'for a customer.'
      *
     A                                 20 10'Please press ENTER to continue'
      *
      ** Footer format
     A          R MQ56AXF9
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                 23  2'F:'
     A            CEX            2Y 0O 23  5EDTCDE(Z)
     A                                 23  8'Exit'
      *
      ** Error Message Subfile.
     A          R MQ56AXS0                  TEXT('Message subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MQMGKY                    SFLMSGKEY
     A            MQPGMQ                    SFLPGMQ
      *
      ** Error Message Subfile control.
     A          R MQ56AXC0                  TEXT('Message subfile control')
     A                                      OVERLAY
     A                                      SFLCTL(MQ56AXS0)
     A                                      SFLINZ
     A                                      SFLSIZ(13)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A  31                                  SFLEND
     A            MQPGMQ                    SFLPGMQ
