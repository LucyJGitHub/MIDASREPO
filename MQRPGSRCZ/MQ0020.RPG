      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MQ Pgm to Convrt MIDAS Day No to Dates')
0002 F********************************************************************
0004 F*                                                                  *
     F*  Midas   Midas/Q MODULE
0004 F*                                                                  *
0005 F*    MQ0020 - DATE EDITING 'SUBROUTINE'                            *
0006 F*                                                                  *
     F*     (c) Finastra International Limited 2001                      *
0008 F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
0009 F*    PREV AMEND NO. E12517           DATE 25/02/88                 *
0010 F*                                                                  *
0010 F*------------------------------------------------------------------*
0008 F*                                                                  *
     F*    MD046248 - Finastra Rebranding                                *
0009 F*    E12517  -  RE-CREATED AS NOT IN RELEASE 22 OBJECT LIBRARY.    *E12517
0010 F*                                                                  *
0011 F********************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*
     F* PARAMETERS PASSED:    DAYNO - DAY NUMBER
     F*
     F* PARAMETERS RETURNED:  DATE  - DATE IN CCYYMMDD FORMAT
     F*                       DATEA - DATE IN DDMMMYY FORMAT
     F*
     F* INVALID DAY NUMBERS HAVE ZERO/BLANK DATES RETURNED.
     F*
     F* N.B. SINCE 'DATE' IS A PACKED FIELD IT WILL BE 9,0
     F* SO CALLING PROGRAM SHOULD BE CODED ACCORDINGLY.
     F*
     E                    CPY@    1   1 80
     E                    ZYDY    4   4  4 0
     E                    ZMDY   13  13  3 0
     E                    ZMNM   12  12  3
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     C           *ENTRY    PLIST
     C                     PARM           DAYNO   50
     C                     PARM           DATE    80
     C                     PARM           DATEA   7
     C*
     C* CLEAR OUTPUT FIELDS
     C                     Z-ADD0         DATE
     C                     MOVE '       ' DATEA
     C*
     C* RETURN IF DAY NUMBER IS INVALID
     C           DAYNO     IFLE 0
     C                     RETRN
     C                     END
     C*
     C* DETERMINE YEAR NUMBER.
     C*
     C* ADJUST DAY NUMBER IN CASE LAST DAY OF YEAR.
     C           DAYNO     SUB  1         ZDAYN1  50
     C*
     C* CALCULATE NUMBER OF 4 YEAR SPANS SINCE 31/12/1971.
     C           ZDAYN1    DIV  1461      ZLYR    20
     C                     MVR            ZDAYN1           SAVE REMAINING
     C*                                                    DAYS
     C* CALCULATE NUMBER OF REMAINING YEARS.
     C                     Z-ADD1         ZWRK2   20
     C           ZDTAG1    TAG
     C           ZDAYN1    COMP ZYDY,ZWRK2             90
     C  N90      ZWRK2     ADD  1         ZWRK2
     C  N90                GOTO ZDTAG1
     C           ZWRK2     SUB  1         ZWRK2          91LEAP YR, 91 ON
     C**
     C**   DECREMENT DAY NO. BY DAYS IN REMAINING YEARS.
     C  N91      ZDAYN1    SUB  ZYDY,ZWRK2ZDAYN1
     C**
     C**   CALCULATE ACTUAL YEAR NUMBER.
     C           ZLYR      MULT 4         ZWRK3   30
     C           ZWRK3     ADD  72        ZWRK3            BASE IS 1972
     C                     Z-ADDZWRK3     ZYEAR   20
     C           ZYEAR     ADD  ZWRK2     ZYEAR            YEAR
     C**
     C**   DETERMINE MONTH NUMBER.
     C**
     C**   ADJUST DAY NO. IN CASE LAST DAY OF LEAP YEAR FEBRUARY.
     C                     SETOF                     9293
     C   91      ZDAYN1    COMP 59                     9293
     C   91N92   ZDAYN1    SUB  1         ZDAYN1
     C**
     C**   CALCULATE MONTH NUMBER.
     C                     Z-ADD2         ZWRK2
     C           ZDTAG2    TAG                             ** ZDTAG2 TAG *
     C           ZDAYN1    COMP ZMDY,ZWRK2             94
     C  N94      ZWRK2     ADD  1         ZWRK2
     C  N94                GOTO ZDTAG2
     C           ZWRK2     SUB  1         ZWRK2
     C**
     C                     Z-ADDZWRK2     ZMTH    20       MONTH
     C**
     C**   DETERMINE DAY OF MONTH.
     C**
     C**   ADD BACK LAST DAY OF YEAR ADJUSTMENT.
     C           ZDAYN1    ADD  1         ZDAYN1
     C**
     C**   CALCULATE DAY OF MONTH.
     C           ZDAYN1    SUB  ZMDY,ZWRK2ZDAY    20       DAY
     C**
     C**   ADD BACK LEAP YEAR 29TH FEBUARY ADJUSTMENT, IF REQUIRED.
     C   93      ZDAY      ADD  1         ZDAY
     C**
     C**   FORMAT DATE, CCYYMMDD
     C                     MOVE ZYEAR     ZWRK4   40
     C           ZYEAR     IFLT 72
     C                     MOVEL20        ZWRK4
     C                     ELSE
     C                     MOVEL19        ZWRK4
     C                     END
     C                     MOVELZMTH      ZWRK4A  40
     C                     MOVE ZDAY      ZWRK4A
     C                     MOVELZWRK4     DATE
     C                     MOVE ZWRK4A    DATE
     C**
     C**   FORMAT ALPHA DATE, DDMMMYY.
     C           ZDAY      COMP 10                     95
     C                     MOVELZDAY      ZWRK5   5
     C   95                MOVEL' '       ZWRK5
     C                     MOVE ZMNM,ZMTH ZWRK5
     C                     MOVELZWRK5     DATEA
     C                     MOVE ZYEAR     DATEA
     C                     RETRN
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE IN FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
