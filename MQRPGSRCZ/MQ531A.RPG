     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MQ RTCUSTLEND report definition I/F')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Midas/Q Module                                       *
     F*                                                               *
     F*         MQ531A -  Report Definition I/Face for RTCUSTLEND     *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  Last Amend No. CMQ001             Date 04NOV97               *
     F*  Prev Amend No.                    Date ddmmmyy               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  CMQ001 - MidasQ rationalisation.                             *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     F*                                                               *
     F* 01 - 24          Function Keys 01 to 24                       *
     F* 26               Valid Command Key Pressed                    *
     F* 30               '0' - Display Mode  '1' - Change Mode        *
     F* 31               '0' - Allow Change  '1' - Change Not Allowed *
     A* 50               Include Live Facilities is Invalid           *
     A* 51               Include Reversed Facilities is Invalid       *
     A* 52               Include Matured Facilities is Invalid        *
     A* 53               Include Live Loans is Invalid                *
     A* 54               Include Reversed Loans is Invalid            *
     A* 55               Include Matured Loans is Invalid             *
     F* 65               Position Cursor at ##FLIV                    *
     F* 85               Command Key found in LOKUP Operation         *
     F* 90               End Of File for SDMMODPD                     *
     F* 98               Error Occured in Validation.                 *
     F* 99               Subfile End                                  *
     F*                                                               *
     F*****************************************************************
     FMQ531ADFCF  E                    WORKSTN
     F                                              KINFDS FINFDS
     F*****************************************************************
     F/EJECT
      **
      ** Array to retrieve function key number
      **
     E                    TAB1    1  24  1   TAB2    2
      **
      ** Copyright insertion.
      **
     E                    CPY@    1   1 80
     E/EJECT
      **
      ** Program Status Data Structure
      **
     IPGMDS      SDS
     I                                        1  10 #PGMQ
      **
      ** File Information Data Structure
      **
     IFINFDS      DS
     I                                      369 369 KEY
     I                                    B 370 3710ROWCOL
      **
      ** Title Data Structure
      **
     I#RNAME      DS                             13
     I                                        1   2 A
     I                                        3  12 CPY#NM
     I                                       13  13 B
      **
      ** System definition data area - O#XA01
      **
     IO#XA01      DS                           1000
      **
      ** Bring in the system definition block
      **
     I/COPY O#XP04,O#SDBLOCKP
      **
      ** Permanent Interface Data Structure
      **
     ICPY#IA      DS                            200
     I                                        1   3 $$FLIV
     I                                        4   6 $$FREV
     I                                        7   9 $$FEXP
     I                                       10  12 $$LLIV
     I                                       13  15 $$LREV
     I                                       16  18 $$LMAT
      **
      ** Copyright Array for compilation
      **
     IA@CPY       DS
     I                                        1  80 CPY@
     I/EJECT
      **
      ** Parameter List Declaration
      **
     C           *ENTRY    PLIST
     C                     PARM           EXT#RC  1        Return code
     C                     PARM           EXT#CA  1        Change allowed
     C                     PARM           EXT#PR 10        Next process
     C                     PARM           EXT#PD           Current process
     C                     PARM           EXT#RL  31       ODX release
     C                     PARM           EXT#ST 20        Site name
     C           CPY#NM    PARM           EXT#NM 10        Report name
     C                     PARM           EXT#RD 40        Report title
     C           CPY#IA    PARM           EXT#IA200        200 byte area
     C                     MOVE '1'       EXT#PD  1
      **
      ** Initialise Program
      **
     C                     EXSR INIT
      **
      ** Display Screen
      **
     C                     MOVE 'NO '     EXIT    3
     C           EXIT      DOUEQ'YES'
      **
      ** Write to message subfile if an error has occured
      **
     C           *IN98     IFEQ '1'
     C                     WRITEMSGCTL
     C                     END
      **
      ** Re-Initialsise Action Bar Header Text in Case it has Changed
      **
     C                     EXSR SACT
      **
      ** Execute Header Format
      **
     C                     EXFMT##HEAD
      **
      ** Clear Subfile Message Queue
      ** And Set Off Error Message Indicators
      **
     C           *IN98     IFEQ '1'
     C                     CALL 'ZA0250'
     C                     SETOF                     985051
     C                     SETOF                     525354
     C                     SETOF                     55
     C                     END
     C                     SETOF                     65
      **
      ** Has a valid Command Key Been Pressed
      **
     C           *IN26     IFEQ '1'
      **
      ** Process Command Keys
      **
     C                     SETOF                       85
     C           KEY       LOKUPTAB1      TAB2           85
     C   85                MOVE TAB2      CMDKEY  2
      **
      ** Exit Pressed?
      **
     C           CMDKEY    IFEQ O#SCEX
      **
      ** Display Mode?
      **
     C           *IN30     IFEQ '0'
     C                     MOVE ##FLIV    $$FLIV
     C                     MOVE ##FREV    $$FREV
     C                     MOVE ##FEXP    $$FEXP
     C                     MOVE ##LLIV    $$LLIV
     C                     MOVE ##LREV    $$LREV
     C                     MOVE ##LMAT    $$LMAT
     C                     MOVELCPY#IA    EXT#IA
     C                     MOVE 'U'       EXT#RC
     C                     MOVE 'YES'     EXIT
     C                     ELSE
      **
      ** Change Mode?
      **
     C                     MOVE 'E'       EXT#RC
     C                     MOVE 'YES'     EXIT
     C                     END
     C                     END
      **
      ** Bypass Pressed?
      **
     C           CMDKEY    IFEQ O#SCBY
      **
      ** Display Mode?
      **
     C           *IN30     IFEQ '0'
     C                     MOVE ##FLIV    $$FLIV
     C                     MOVE ##FREV    $$FREV
     C                     MOVE ##FEXP    $$FEXP
     C                     MOVE ##LLIV    $$LLIV
     C                     MOVE ##LREV    $$LREV
     C                     MOVE ##LMAT    $$LMAT
     C                     MOVELCPY#IA    EXT#IA
     C                     MOVE 'U'       EXT#RC
     C                     MOVE 'YES'     EXIT
      **
      ** Change Mode?
      **
     C                     ELSE
     C                     MOVE 'B'       EXT#RC
     C                     MOVE 'YES'     EXIT
     C                     END
     C                     END
      **
      ** Change Pressed?
      **
     C           CMDKEY    IFEQ O#SCCH
     C           *IN30     IFEQ '1'
     C                     MOVE '0'       *IN30
     C                     Z-ADDCCH       I
     C                     MOVE '1'       *IN,I
     C                     ELSE
     C                     MOVE '1'       *IN30
     C                     Z-ADDCCH       I
     C                     MOVE '0'       *IN,I
     C                     END
      **
      ** Ensure Cursor Position at ##FLIV
      **
     C                     SETON                     65
     C                     WRITE##FOOT
     C                     END
      *
     C                     ELSE
      **
      ** Enter Pressed, Check Cursor position
      **
     C           ROWCOL    DIV  256       ROW     20
     C                     MVR            COL     20
     C           ROW       IFEQ 1
     C           COL       ANDEQ3
     C           ROW       OREQ 2
     C           COL       ANDNE1
     C           COL       ANDNE12
     C           COL       ANDNE23
     C           COL       ANDNE34
     C           COL       ANDNE45
     C           COL       ANDNE56
     C           COL       ANDNE67
     C           COL       ANDNE74
     C           COL       ANDNE80
      **
      ** Cursor in Action Bar, Send Error Message
      **
     C                     SETON                     98
     C                     CALL 'MQ501XC'
     C                     PARM 'R5E0013' @MSGID 10
     C                     PARM '       ' @MSDTA 50
     C                     ELSE
      **
      ** Enter Pressed, Cursor In Valid position, Mode is
      ** Display so Exit Program
      **
     C           *IN30     IFEQ '0'
     C                     MOVE 'YES'     EXIT
     C                     END
     C                     END
      **
      ** Enter Pressed, Mode is Change, So Validate
      **
     C           *IN30     IFEQ '1'
      **
      ** Validate Live Facilities
      **
     C                     MOVEL##FLIV    IN1     1
     C           IN1       IFEQ 'Y'
     C                     MOVE 'YES'     ##FLIV
     C                     END
     C           IN1       IFEQ 'N'
     C                     MOVE 'NO '     ##FLIV
     C                     END
      **
      ** Validate Reversed Facilities
      **
     C                     MOVEL##FREV    IN1
     C           IN1       IFEQ 'Y'
     C                     MOVE 'YES'     ##FREV
     C                     END
     C           IN1       IFEQ 'N'
     C                     MOVE 'NO '     ##FREV
     C                     END
      **
      ** Validate Matured Facilities
      **
     C                     MOVEL##FEXP    IN1
     C           IN1       IFEQ 'Y'
     C                     MOVE 'YES'     ##FEXP
     C                     END
     C           IN1       IFEQ 'N'
     C                     MOVE 'NO '     ##FEXP
     C                     END
      **
      ** Validate Live Loans
      **
     C                     MOVEL##LLIV    IN1     1
     C           IN1       IFEQ 'Y'
     C                     MOVE 'YES'     ##LLIV
     C                     END
     C           IN1       IFEQ 'N'
     C                     MOVE 'NO '     ##LLIV
     C                     END
      **
      ** Validate Reversed Loans
      **
     C                     MOVEL##LREV    IN1
     C           IN1       IFEQ 'Y'
     C                     MOVE 'YES'     ##LREV
     C                     END
     C           IN1       IFEQ 'N'
     C                     MOVE 'NO '     ##LREV
     C                     END
      **
      ** Validate Matured Loans
      **
     C                     MOVEL##LMAT    IN1
     C           IN1       IFEQ 'Y'
     C                     MOVE 'YES'     ##LMAT
     C                     END
     C           IN1       IFEQ 'N'
     C                     MOVE 'NO '     ##LMAT
     C                     END
      **
      ** Live Facilities are not Valid
      ** Send Error Message
      **
     C           ##FLIV    IFNE 'YES'
     C           ##FLIV    ANDNE'NO '
     C                     SETON                     9850
     C                     CALL 'MQ501XC'
     C                     PARM 'R5E0014' @MSGID 10
     C                     PARM '       ' @MSDTA 50
     C                     END
      **
      ** Reversed Facilities are not Valid
      ** Send Error Message
      **
     C           ##FREV    IFNE 'YES'
     C           ##FREV    ANDNE'NO '
     C                     SETON                     9851
     C                     CALL 'MQ501XC'
     C                     PARM 'R5E0015' @MSGID 10
     C                     PARM '       ' @MSDTA 50
     C                     END
      **
      ** Matured Facilities are not Valid
      ** Send Error Message
      **
     C           ##FEXP    IFNE 'YES'
     C           ##FEXP    ANDNE'NO '
     C                     SETON                     9852
     C                     CALL 'MQ501XC'
     C                     PARM 'R5E0016' @MSGID 10
     C                     PARM '       ' @MSDTA 50
     C                     END
      **
      ** Live Loans are not Valid
      ** Send Error Message
      **
     C           ##LLIV    IFNE 'YES'
     C           ##LLIV    ANDNE'NO '
     C                     SETON                     9853
     C                     CALL 'MQ501XC'
     C                     PARM 'R5E0017' @MSGID 10
     C                     PARM '       ' @MSDTA 50
     C                     END
      **
      ** Reversed Loans are not Valid
      ** Send Error Message
      **
     C           ##LREV    IFNE 'YES'
     C           ##LREV    ANDNE'NO '
     C                     SETON                     9854
     C                     CALL 'MQ501XC'
     C                     PARM 'R5E0018' @MSGID 10
     C                     PARM '       ' @MSDTA 50
     C                     END
      **
      ** Matured Loans are not Valid
      ** Send Error Message
      **
     C           ##LMAT    IFNE 'YES'
     C           ##LMAT    ANDNE'NO '
     C                     SETON                     9855
     C                     CALL 'MQ501XC'
     C                     PARM 'R5E0016' @MSGID 10
     C                     PARM '       ' @MSDTA 50
     C                     END
      **
      ** Details are Valid If *IN98 is Off
      ** Enable Change Key and move into Display Mode
      **
     C           *IN98     IFEQ '0'
     C                     MOVE ##FLIV    $$FLIV
     C                     MOVE ##FREV    $$FREV
     C                     MOVE ##FEXP    $$FEXP
     C                     MOVE ##LLIV    $$LLIV
     C                     MOVE ##LREV    $$LREV
     C                     MOVE ##LMAT    $$LMAT
     C                     MOVELCPY#IA    EXT#IA
     C                     MOVE 'U'       EXT#RC
     C                     MOVE '0'       *IN30
     C                     Z-ADDCCH       I
     C                     MOVE '1'       *IN,I
     C                     WRITE##FOOT
     C                     END
      **
      ** End of IF ENTER Pressed
      **
     C                     END
      **
      ** End of IF Mode Not Display
      **
     C                     END
      **
      ** End of DO Until Loop
      **
     C                     END
      **
      ** End Program
      **
     C                     SETON                     LR
     C                     RETRN
      **
      ** Subroutine to Initialise Program
      **
     C           INIT      BEGSR
      **
      ** Initialise Data Structure Numeric Fields
      **
     C                     Z-ADD0         ROWCOL
      **
      ** Set Up Action Bar Header Text
      **
     C                     EXSR SACT
     C                     MOVE '-'       #BOX1
     C                     MOVE '-('      A
     C                     MOVE ')'       B
      **
      ** Output Footer
      **
      **
      ** Retrieve MIDAS/Q system definition block...
      **
     C           *NAMVAR   DEFN           O#XA01
     C                     IN   O#XA01
     C                     MOVELO#XA01    O#SDBK
      **
      ** Enable installation defined command keys required in this pgm.
      **
     C                     Z-ADDCEX       I       30       Exit
     C                     MOVE '1'       *IN,I
     C                     Z-ADDCBY       I                Bypass
     C                     MOVE '1'       *IN,I
      **
      ** Only enable use of CHANGE if user is authorised
      **
     C           EXT#CA    IFEQ 'Y'
     C                     SETOF                     31
     C                     Z-ADDCCH       I                Change
     C                     MOVE '1'       *IN,I
     C                     ELSE
     C                     SETON                     31
     C                     END
      **
      ** Set up Program to be in Display Mode
      **
     C                     SETOF                     30
     C                     WRITE##FOOT
      **
      ** Set up Screen Fields
      **
     C                     MOVE $$FLIV    ##FLIV
     C                     MOVE $$FREV    ##FREV
     C                     MOVE $$FEXP    ##FEXP
     C                     MOVE $$LLIV    ##LLIV
     C                     MOVE $$LREV    ##LREV
     C                     MOVE $$LMAT    ##LMAT
      **
      ** Set up Message File
      ** Set on Message Subfile end indicator.
      **
     C                     MOVE '1'       *IN99
      **
      ** Set up Next Process to CATALOG
      **
     C                     MOVEL'CATALOG' EXT#PR
     **
     C                     ENDSR
     **
     **  Subroutine to Set up Action Bar Header Text
     **
     C           SACT      BEGSR
     C                     MOVEL'Report'  #BOX2
     C                     MOVEL'Options' #BOX3
     C                     MOVE *BLANKS   #BOX4
     C                     MOVEL'Refine'  #BOX5
     C                     MOVEL'Subsets' #BOX6
     C                     MOVEL'House'   #BOX7
     C                     MOVE 'keep '   #BOX7
     C                     MOVEL'AS/400'  #BOX8
     C                     MOVEL' Help'   #BOX9
     C                     ENDSR
     **
     **
     **
** TAB1, TAB2 Array for command Key Usage
 01
 02
 03
 04
 05
 06
 07
 08
 09
 10
 11
 12
£13
¥14
·15
©16
§17
¶18
¼19
½20
¾21
[22
]23
¯24
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
