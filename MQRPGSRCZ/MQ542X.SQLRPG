     H        1
      *****************************************************************
/**** *  RPGBASE                                                      *                     MD058079
/*STD *  RPGSQL2                                                      *                     MD058079
/*EXI *  TEXT('Midas MQ RTEVENTS event codes selection')
      *****************************************************************
     F*                                                               *
     F*  Midas - Midas/Q Module                                       *
     F*                                                               *
     F*    MQ542X    -  Select Event Codes for subset RTEVENTS        *
     F*                                                               *
     F*  (c) Finastra International Limited 2001                      *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  Last Amend No. MD058079           Date 11May21               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 BUG1894            Date 06Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CMQ001             Date 04NOV97               *
      *                                    Date ddmmmyy               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  MD058079 - Deliverable Data Split for sundry files           *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG1894- CGL029 changed MQ540XLF by making ACODE 10 long     *
      *           rather than 4.  This was wrong and has been         *
      *           reversed.  Recompile.                               *
     F*  CMQ001 - MidasQ rationalisation.                             *
     F*                                                               *
      *****************************************************************
      **
      ** Screen Display File
      **
     FMQ542XDFCF  E                    WORKSTN
     F                                        RRN   KSFILE ##SRCD
      **
      ** File to Store Selected records
      **
     FMQ542XLFUF  E           K        DISK                      A
      **
      ** File to Retrieve ALL Event Codes and Descriptions
      **
     F*MQ540XLF* IF   E           K DISK                                                    MD058079
     F/EJECT
      **
     IMQ540X    E DSMQ540JW0                                                                MD058079
      **
      ** Title Data Structure
      **
     I#RNAME      DS                             13
     I                                        1   2 A
     I                                        3  12 CPY#NM
     I                                       13  13 B
      **
      ** Report Title Data Structure
      **
     ITTLE        DS                            125
     I I            '    SELECT LENDING E-    1  25 TTLE1
     I              'VENTS'
     I I            '    SELECT DEALING E-   26  50 TTLE2
     I              'VENTS'
     I I            '    SELECT FUTOPTN E-   51  75 TTLE3
     I              'VENTS'
     I I            '   SELECT FUNDTRAN E-   76 100 TTLE4
     I              'VENTS'
     I I            ' SELECT SECURITIES E-  101 125 TTLE5
     I              'VENTS'
      **
      ** System definition data area - O#XA01
      **
     IO#XA01      DS                           1000
      **
      ** Bring in the system definition block
      **
     I/COPY O#XP04,O#SDBLOCKP
      **
     C           *ENTRY    PLIST
     C                     PARM           MQID1   8
     C                     PARM           #RNAME
     C                     PARM           MODE    1
     C                     PARM           USED    3
     C                     PARM           RTN     1
     C                     PARM           LE      3
     C                     PARM           DL      3
     C                     PARM           FF      3
     C                     PARM           FT      3
     C                     PARM           SE      3
      **
      ** Initialise
      **
     C                     EXSR INIT
      **
      ** Load Subfile for Lending if required
      **
     C           LE        IFEQ 'YES'
     C                     MOVE 'LE'      MOD     2
     C                     MOVE TTLE1     #TITLE
     C                     EXSR PROCES
     C                     END
      **
      ** Load Subfile for Dealing if required
      **
     C           DL        IFEQ 'YES'
     C           *IN03     ANDEQ'0'
     C                     MOVE 'DL'      MOD
     C                     MOVE TTLE2     #TITLE
     C                     EXSR PROCES
     C                     END
      **
      ** Load Subfile for F&O if required
      **
     C           FF        IFEQ 'YES'
     C           *IN03     ANDEQ'0'
     C                     MOVE 'FF'      MOD
     C                     MOVE TTLE3     #TITLE
     C                     EXSR PROCES
     C                     END
      **
      ** Load Subfile for Fundtran if required
      **
     C           FT        IFEQ 'YES'
     C           *IN03     ANDEQ'0'
     C                     MOVE 'FT'      MOD
     C                     MOVE TTLE4     #TITLE
     C                     EXSR PROCES
     C                     END
      **
      ** Load Subfile for Securities if required
      **
     C           SE        IFEQ 'YES'
     C           *IN03     ANDEQ'0'
     C                     MOVE 'SE'      MOD
     C                     MOVE TTLE5     #TITLE
     C                     EXSR PROCES
     C                     END
      **
      ** Set up USED Flag depending on whether or not any
      ** Event Codes are selected
      **
     C           MQID1     CHAINOPT1                 99
     C           *IN99     IFEQ '0'
     C                     MOVE 'YES'     USED
     C                     ELSE
     C                     MOVE 'NO '     USED
     C                     END
      **
     C                     SETON                     LR
     C                     RETRN
      **
      ** Subroutines
      **
      ** Process a request to see the subfile
      **
     C           PROCES    BEGSR
      **
     C                     EXSR LOAD
      **
      ** Exit Not Pressed?
      **
     C           *IN03     IFEQ '0'
      **
      ** Save Data Input if in Change Mode
      **
     C           MODE      IFEQ 'C'
     C                     MOVE 'C'       RTN
     C                     EXSR SAVE
     C                     END
     C                     END
      **
     C                     ENDSR
      **
      ** Load Subfile Subroutine
      **
     C           LOAD      BEGSR
      **
      ** Clear the Subfile
      **
     C                     MOVEA'000'     *IN,50
     C                     WRITE##SCTL
      **
      ** Initialise
      **
     C                     Z-ADD1         RRN     40
     C                     Z-ADD0         X       30
     C                     MOVEA'110'     *IN,50
      **
      ** Get First Record
      **
     C********** KL0       SETLLMQ540XLF                                                    MD058079
     C**********           EXSR GETREC                                                      MD058079
     C/EXEC SQL                                                                             MD058079
     C+ declare ACursor insensitive scroll cursor for                                       MD058079
     C+ select * from MQ540JW0                                                              MD058079
     C+ where AMOD = :MOD                                                                   MD058079
     C+ order by AMOD, ACODE                                                                MD058079
     C/END-EXEC                                                                             MD058079
      *                                                                                     MD058079
     C/EXEC SQL                                                                             MD058079
     C+ close ACursor                                                                       MD058079
     C/END-EXEC                                                                             MD058079
      *                                                                                     MD058079
     C/EXEC SQL                                                                             MD058079
     C+ open ACursor                                                                        MD058079
     C/END-EXEC                                                                             MD058079
      *                                                                                     MD058079
     C/EXEC SQL                                                                             MD058079
     C+ fetch next from ACursor into :MQ540X                                                MD058079
     C/END-EXEC                                                                             MD058079
      *                                                                                     MD058079
      **
      ** Do While Rolldown Requested
      **
     C           *IN25     DOUEQ'0'
      **
      ** Ensure First record is at RRN
      **
     C                     Z-ADDRRN       DSPREC
      **
      ** Load Next 8 Records
      **
     C                     DO   15
     C                     WRITE##SRCD
     C                     ADD  1         RRN
     C                     EXSR GETREC
     C  N52                END
      **
     C                     WRITE##FOOT
      **
     C                     EXFMT##SCTL
      **
     C                     END
      **
     C                     ENDSR
      **
      ** Get Data for Subfile
      **
     C           GETREC    BEGSR
     C********** KL0       READEMQ540XLF                 52                                 MD058079
     C/EXEC SQL                                                                             MD058079
     C+ fetch next from ACursor into :MQ540X                                                MD058079
     C/END-EXEC                                                                             MD058079
     C                     SETOF                     52                                     MD058079
     C           SQLCOD    IFEQ 100                                                         MD058079
     C                     SETON                     52                                     MD058079
     C                     ENDIF                                                            MD058079
      *                                                                                     MD058079
     C           *IN52     IFEQ '0'
     C                     MOVELADESC     ##DESC
     C                     MOVELACODE     ##CODE
      **
      ** Load Selection Field Depending on Whether already Selected
      **
     C           KL1       CHAINOPT1                 99
     C           *IN99     IFEQ '0'
     C                     MOVE 'X'       ##SEL
     C                     ELSE
     C                     MOVE ' '       ##SEL
     C                     END
      **
     C                     END
      **
     C                     ENDSR
      **
      ** Save Data Input back to File
      **
     C           SAVE      BEGSR
      **
     C                     READC##SRCD                 9898
      **
     C           *IN98     DOWEQ'0'
      **
     C           KL1       CHAINOPT1                 97
      **
     C           ##SEL     IFEQ *BLANK
     C           *IN97     IFEQ '0'
     C                     DELETOPT1
     C                     END
     C                     ELSE
     C           *IN97     IFEQ '1'
     C                     MOVE ##CODE    ECOD1
     C                     WRITEOPT1
     C                     END
     C                     END
      **
     C                     READC##SRCD                 9898
     C                     END
      **
     C                     ENDSR
      **
      ** Initialise
      **
     C           INIT      BEGSR
      **
     C           KL0       KLIST
     C                     KFLD           MOD
     C           KL1       KLIST
     C                     KFLD           MQID1
     C                     KFLD           ##CODE
      **
      ** Set Up Return Code
      **
     C                     MOVE *BLANKS   RTN
      **
      ** Set Up Mode
      **
     C           MODE      IFEQ 'C'
     C                     SETON                     30
     C                     ELSE
     C                     SETOF                     30
     C                     END
      **
      ** Set Up Action Bar Header Text
      **
     C                     EXSR SACT
     C                     MOVE '-'       #BOX1
     C                     MOVE '-('      A
     C                     MOVE ')'       B
      **
     C                     ENDSR
     **
     **  Subroutine to Set up Action Bar Header Text
     **
     C           SACT      BEGSR
     C                     MOVEL'Report'  #BOX2
     C                     MOVEL'Options' #BOX3
     C                     MOVE *BLANKS   #BOX4
     C                     MOVEL'Refine'  #BOX5
     C                     MOVEL'Subsets' #BOX6
     C                     MOVEL'House'   #BOX7
     C                     MOVE 'keep '   #BOX7
     C                     MOVEL'AS/400'  #BOX8
     C                     MOVEL' Help'   #BOX9
     C                     ENDSR
      **
