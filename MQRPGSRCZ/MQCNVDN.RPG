     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MQ Convert CCYYMMDD dates into Midas day no')
      *****************************************************************
     F*                                                               *
     F*    Midas - Midas/Q Module                                     *
     F*                                                               *
     F*    MQCNVDN   -  Routine to convert CCYYMMDD format dates      *
     F*                 into Midas Day Number format.                 *
     F*                                                               *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CMQ001             Date 03Nov97               *
      *                                    Date DDMmmYY               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     F*  CMQ001 - MidasQ rationalisation.                             *
     F*                                                               *
     F*****************************************************************
     ** @Z1 - Years in Days  -  Cumulative in Four Year Span
     E                    @Z1     4   4  5 0A
     ** @Z2 - Months in Days  -  Cumulative Through Year
     E                    @Z2    13  13  5 0A
      *
      ** INPUTS  - Date CCYYMMDD Format - ZZDTIN
      ** OUTPUTS - Dayno                - ZZDAYN
      *
     C           *ENTRY    PLIST
     C                     PARM           ZZDTIN
     C                     PARM           ZZDAYN
      *
      ** Initialise Output Field
      *
     C                     Z-ADD0         ZZDAYN  50
      * Clear work field ZWRK2
     C                     Z-ADD0         ZWRK2
      *
      ** Calc To define Input Field
      *
     C                     Z-ADDZZDTIN    ZZDTIN  80
      *
      ** If input is zero then end Subroutine
      *
     C           ZZDTIN    CABEQ0         DNEND
      *
      ** Divide Date into Cent,Year,Month And Day
      *
     C                     Z-ADD0         ZCNYR   40
     C                     Z-ADD0         ZCENTN  20
     C                     Z-ADD0         ZMTHN   20
     C                     Z-ADD0         ZDAYN   20
     C                     MOVELZZDTIN    ZCNYR
     C                     MOVELZZDTIN    ZCENTN
     C                     MOVE ZZDTIN    ZWRK4   4
     C                     MOVE ZWRK4     ZDAYN
     C                     MOVELZWRK4     ZMTHN
     C*
     C** If date greater than 31/12/2071 then calculate number of
     C** centuries passed since 1971, by subtracting 1972 from the
     C** year field.
     C           ZCNYR     IFGE 2072
     C           ZCNYR     SUB  1972      ZCNYR
     C*
     C** Multiply by number of days in century (since 1971)
     C** then restore the year which was input back to the
     C** original value before continuing with normal calculations.
     C           ZCENTN    MULT 36525     ZZDAYN
     C           ZCNYR     ADD  1972      ZCNYR
     C                     END
     C*
     C** Add 28 to the year - the right two digits of the result will
     C** give the number of years from 1971 - disregarding centuries
     C           ZCNYR     ADD  28        ZWRK2   20
     C*
     C** calculate how many groups of four years there are
     C           ZWRK2     DIV  4         ZWRK2
     C                     MVR            ZWRK10  10
     C*
     C** and multiply by number of days in four year period
     C           ZWRK2     MULT 1461      ZWRK5   50
     C                     ADD  ZWRK5     ZZDAYN
     C*
     C** If a leap year and past February then add an extra day
     C           ZWRK10    IFEQ 0
     C           ZMTHN     ANDGT2
     C                     ADD  1         ZZDAYN
     C                     END
     C*
     C** Year not leap year,  add cumulative days for year
     C           ZWRK10    IFNE 0
     C                     ADD  @Z1,ZWRK10ZZDAYN
     C                     END
     C*
     C** Add months this year
     C                     ADD  @Z2,ZMTHN ZZDAYN
     C*
     C** Add days this month
     C                     ADD  ZDAYN     ZZDAYN
     C*
     C           DNEND     TAG
     C*
     C                     SETON                     LR
     C*
     C*****************************************************************
**   @Z1 - Years In Days  -  Cumulative in Four Year Span
00366007310109601461
**   @Z2 - Months in Days  -  Cumulative Through Year
00000000310005900090001200015100181002120024300273003040033400365
