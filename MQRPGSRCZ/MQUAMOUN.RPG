     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MQ Edit Amount field using file SDCURRL1')       *
      *****************************************************************
     F*                                                               *
     F*    Midas - Midas/Q Module                                     *
     F*                                                               *
     F*    MQUAMOUN  -  Routine to edit an amount field using the     *
     F*                 number of decimal places of the currency.     *
     F*                                                               *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CMQ001             Date 03Nov97               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
     F*  CMQ001 - MidasQ rationalisation.                             *
     F*                                                               *
      *****************************************************************
      **
      ** Format an amount with decimal places defined on SDCURRL1
      **
      ** INPUTS  - The Amount           - AMXXXX
      **         - The Currency         - AMCYCD
      ** OUTPUTS - The Formatted Amount - AMEDIT
      **
      ** DEFINE THE CURRENCY TABLE
     FSDCURRL1IF  E           K        DISK
      **
     C           *ENTRY    PLIST
     C                     PARM           AMXXIN 150
     C                     PARM           AMCYCD  3
     C                     PARM           AMEDIT 153
     C                     MOVE *BLANKS   A6CYCD
     C                     MOVE AMCYCD    A6CYCD
     C                     Z-ADDAMXXIN    AMXXXX
      **
      ** Get the Currency
      **
     C           A6CYCD    CHAIN@A6REA3              70
     C           *IN70     IFEQ '1'
      **
      ** Return Zero, if the currency was not found
      **
     C                     Z-ADD0         AMXXXX 194
      *
     C                     ELSE
      **
      ** Format the Amount Fields.
      **
      ** For speed assume that currency has 2 d.p., then check
      ** that this is so. If it is not adjust as apprpriate.
      **
     C           AMXXXX    DIV  100       AMXXXX
     C           A6NBDP    IFNE 2
      **
      ** Edit 0 DP
      **
     C           A6NBDP    IFEQ 0
     C           AMXXXX    MULT 100       AMXXXX
      *
     C                     ELSE
      **
      ** Edit 3 DP
      **
     C           A6NBDP    IFEQ 3
     C           AMXXXX    DIV  10        AMXXXX    H
      *
     C                     ELSE
      **
      ** Edit 1 DP
      **
     C           AMXXXX    MULT 10        AMXXXX
     C                     END
     C                     END
     C                     END
      *
     C                     END
      *
     C                     Z-ADDAMXXXX    AMEDIT
     C                     SETON                     LR
