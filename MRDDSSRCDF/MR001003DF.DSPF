      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MR File to Upload to MBI Maint/Enq Display')     *
      *****************************************************************
      *                                                               *
      *  Midas - MidasPlus Reporting Module                           *
      *                                                               *
      *  DSPF/MR001003DF - Midas MR File to Upload to MBI File        *
      *                    Maintenance Display File                   *
      *                                                               *
      *  (c) Finastra International Limited 2011                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR828013 *CREATE   Date 30Sep11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR828013 - Incremental database for MBI (CCR036)             *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03(03)
     A                                      CF05(05)
     A N11                                  CF09(09)
     A                                      CF12(12)
      *
     A          R MR001003F1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2011')
      *
     A                                  1  2'MR001003DF'
     A            #1RUNA    R        O  1 63REFFLD(RUNA)
     A                                  1 72TIME
     A                                  2 27'MBI Extract File Maintenance'
     A                                      DSPATR(HI)
      *
     A          R MR001003S1                SFL
     A  95                                  SFLNXTCHG
     A            #1FIL         10A  O  9  4
     A            #1DNL          1   B  9 18
     A                                      DSPATR(HI)
     A N22N90                               DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
     A            #1PRG         10   B  9 24
     A                                      DSPATR(HI)
     A N23N90                               DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
     A            #1DIR          1   B  9 39
     A                                      DSPATR(HI)
     A N24N90                               DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
     A            #1MIR          1   B  9 53
     A                                      DSPATR(HI)
     A N25N90                               DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
     A            #1YIR          1   B  9 65
     A                                      DSPATR(HI)
     A N26N90                               DSPATR(UL)
     A  26                                  DSPATR(RI PC)
     A  90                                  DSPATR(PR)
     A            #1DEX          7   O  9 73
      *
     A          R MR001003C1                SFLCTL(MR001003S1)
     A N93                                  ROLLUP(94)
     A                                      OVERLAY
     A  92                                  SFLDSP
     A N91                                  SFLDSPCTL
     A  91                                  SFLCLR
     A  93                                  SFLEND(*MORE)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
      *
     A            #1SFRN         4S 0H      SFLRCDNBR
     A            #1KFIL        10A  B  7  4TEXT('Module Id')
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A            #1KDNL         1A  B  7 18TEXT('Extract to GRI')
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A            #1KPRG        10A  B  7 24
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A                                  5  4'Filename'
     A                                  5 15'Download'
     A                                  5 24'External'
     A                                  6 24'Program'
     A                                  5 35'Incremental/'
     A                                  4 37'Daily'
     A                                  6 36'Refresh'
     A                                  4 50'Monthly'
     A                                  5 48'Incremental/'
     A                                  6 50'Refresh'
     A                                  4 63'Yearly'
     A                                  5 61'Incremental/'
     A                                  6 63'Refresh'
     A            #1KDIR         1A  B  7 39
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A            #1KMIR         1A  B  7 53
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A            #1KYIR         1A  B  7 65
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A                                  6 16'(Y/N)'
     A                                  5 75'Last'
     A                                  4 73'Date of'
     A                                  6 73'Extract'
     A            #1KDEX         6  0B  7 73DSPATR(HI)
      *
     A          R MR001003S2                TEXT('Program Messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            #2MSGK                    SFLMSGKEY
     A            #2PGMQ                    SFLPGMQ
      *
     A          R MR001003C2                TEXT('Program Messages')
     A                                      SFLCTL(MR001003S2)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            #2PGMQ                    SFLPGMQ
      *
     A          R MR001003F2
     A                                      OVERLAY
     A N90                             23  2'F3=Main menu    F5=Refresh    F12=-
     A                                      Previous'
     A  90                             23  2'F3=Main menu    F12=Previous'
