      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_NMSN                                                      *
/*EXI *  TEXT('Midas MR Average Balances')
/*XAIA*  ADDPFM FILE(MRAVBLPD) MBR(CAL) SHARE(*YES)                   *
/*XAIB*  ADDPFM FILE(MRAVBLPD) MBR(FIN) SHARE(*YES)                   *
      *****************************************************************
      *                                                               *
      *  Midas - MidasPlus Reporting Module                           *
      *                                                               *
      *  MRAVBLPD - Average Balances                                  *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 25Nov05               *
      *                 CMR001             Date 01Sep04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CMR001 - MidasPlus Reporting                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R MRAVBLD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
     A            BRCA      R
      * Begin /CSD027/
     A**********  CNUM      R                                                                 CSD027
     A            CNUM           6A
     A                                      TEXT('Customer number')
     A                                      COLHDG('Customer            ' +
     A                                      'Number')
      * End /CSD027/
     A            CCY       R
     A            ACOD      R               REFFLD(ACOD)
     A            ACSQ      R
     A*
     A            CAFI           1A         TEXT('Calendar/Financial')
     A                                      COLHDG('Calendar/Financial')
     A            PERTYP         1A         TEXT('Period Type')
     A                                      COLHDG('Period Type')
     A            YR             2S 0       TEXT('Year')
     A                                      COLHDG('Year')
     A            PR             2S 0       TEXT('Period')
     A                                      COLHDG('Period')
     A*
     A            SPADAT         5P 0       TEXT('SOP Date')
     A                                      COLHDG('SOP Date')
     A            EPADAT         5P 0       TEXT('EOP Date')
     A                                      COLHDG('EOP Date')
     A*
     A            AVBLNT        18P 3       TEXT('Average Balance - NET')
     A                                      COLHDG('Average Bal - NET')
     A            AVBLDR        18P 3       TEXT('Average Balance - DR')
     A                                      COLHDG('Average Bal - DR')
     A            AVBLCR        18P 3       TEXT('Average Balance - CR')
     A                                      COLHDG('Average Bal - CR')
     A*
     A            TWBLDR        21P 3       TEXT('Total Weighted Balance - DR')
     A                                      COLHDG('Tot Weight Bal - DR')
     A            TWBLCR        21P 3       TEXT('Total Weighted Balance - CR')
     A                                      COLHDG('Tot Weight Bal - CR')
     A*
     A            NODYFL         3P 0       TEXT('Number of Days FLAT')
     A                                      COLHDG('Number of Days FLAT')
     A            NODYDR         3P 0       TEXT('Number of Days in DR')
     A                                      COLHDG('Number of Days DR')
     A            NODYCR         3P 0       TEXT('Number of Days in CR')
     A                                      COLHDG('Number of Days CR')
     A*
     A            MAXBAL        18P 3       TEXT('Maximum Balance')
     A                                      COLHDG('Maximum Balance')
     A            MINBAL        18P 3       TEXT('Minimum Balance')
     A                                      COLHDG('Minimum Balance')
