     H DEBUG
     H COPYRIGHT('(C) Copyright Finastra International Limited, 2004')
      *****************************************************************
/*STD *  RPGBASEMOD                                                   *
/*EXI *  TEXT('Midas MR Trade Innovation CSV Extract')
      *****************************************************************
      *                                                               *
      *  Midas - MidasPlus Reporting module                           *
      *                                                               *
      *  RPGLE/MRTDINCSV - Trade Innovation CSV Extract               *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CMR001  *CREATE    Date 16Aug04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CMR001 - MidasPlus Reporting                                 *
      *                                                               *
      *****************************************************************
 
     FFINTXN    IP   F  132        DISK
     FMRTDINPD  O    E             DISK
 
 
     D                 DS
     D Rec                     1    132
     D Record                  1    132    DIM(132)
 
      ** Parameter used for ZALIGN
     D ZFIELD          S             16A
     D ZADEC           S              1P 0
     D ZADIG           S              2P 0
     D ZALIGNok        S              1A
 
     D                 DS
     D DayMonthYear            1      6  0
     D Day                     1      2  0
     D Month                   3      4  0
     D Year                    5      6  0
 
     D TDIN_TRAN     E DS                  EXTNAME(MRTDINPD)
 
     IFINTXN    NS  01
     I                                  1  132  Record
 
     C                   CLEAR                   TDIN_TRAN
 
      * Extract the field instances from the incoming FINTXN.CSV file
      * (File FINTXN is generated by Trade Innovation).
 
     C                   Z-ADD     1             X                 3 0
     C                   Z-ADD     *ZERO         Inst              2 0
     C     *IN99         DOUEQ     *OFF
     C     '"'           LOOKUP    Record(X)                              99    *
     C     *IN99         IFEQ      *ON
     C                   ADD       1             Inst
     C                   EXSR      INSTANCE
     C                   MOVE      *ON           *IN99
     C                   ADD       1             X
     C                   ENDIF
     C                   ENDDO
 
      * Write to the TI Transaction File
 
     C     PRINCIPAL     IFNE      *ZERO
     C                   WRITE     MRTDIND0
     C                   ENDIF
 
      **********************************************************************
     C     INSTANCE      BEGSR
 
      * CUSTOMER
     C     Inst          IFEQ      1
     C                   EXSR      EXTRACT_CHAR
     C                   MOVEL     Field         CUSTOMER
     C                   ENDIF
 
      * TRANREF
     C     Inst          IFEQ      3
     C                   EXSR      EXTRACT_CHAR
     C                   MOVEL     Field         TRANREF
     C                   ENDIF
 
      * PRINCIPAL
     C     Inst          IFEQ      5
     C                   EXSR      EXTRACT_AMNT
     C                   MOVE      Field         PRINCIPAL
     C                   ENDIF
 
      * CURRENCY
     C     Inst          IFEQ      7
     C                   EXSR      EXTRACT_CHAR
     C                   MOVEL     Field         CURRENCY
     C                   ENDIF
 
      * VALUEDATE
     C     Inst          IFEQ      9
     C                   EXSR      EXTRACT_DATE
     C                   MOVEL     Field         VALUEDATE
     C                   ENDIF
 
      * MATURDATE
     C     Inst          IFEQ      11
     C                   EXSR      EXTRACT_DATE
     C                   MOVEL     Field         MATURDATE
     C                   ENDIF
 
      * BRANCH
     C     Inst          IFEQ      15
     C                   EXSR      EXTRACT_CHAR
     C                   MOVEL     Field         BRANCH
     C                   ENDIF
 
      * PRODUCTTYP
     C     Inst          IFEQ      19
     C                   EXSR      EXTRACT_CHAR
     C                   MOVEL     Field         PRODUCTTYP
     C                   ENDIF
     C                   ENDSR
      **********************************************************************
      /SPACE 5
      **********************************************************************
     C     EXTRACT_CHAR  BEGSR
     C                   MOVEL     *BLANK        Field            20
     C     X             ADD       1             StrPos            3 0
     C                   Z-ADD     StrPos        EndPos            3 0
     C     '"'           LOOKUP    Record(EndPos)                         99
     C     *IN99         CABEQ     *OFF          END_CHAR
     C     EndPos        SUB       StrPos        Length            3 0
     C                   EVAL      Field = %SUBST(Rec: StrPos: Length)
     C     END_CHAR      ENDSR
      **********************************************************************
      /SPACE 5
      **********************************************************************
     C     EXTRACT_AMNT  BEGSR
 
     C                   EXSR      EXTRACT_CHAR
     C     *IN99         CABEQ     *OFF          END_AMNT
 
     C                   MOVE      *BLANKS       ZFIELD
     C                   MOVEL     Field         ZFIELD
     C                   CALLB     'ZALIGN'
     C                   PARM                    ZALIGNOK
     C                   PARM                    ZFIELD
     C                   PARM      2             ZADEC
     C                   PARM      11            ZADIG
 
     C                   MOVE      *ZEROS        Field
     C     ZALIGNOK      IFEQ      'Y'
     C                   MOVE      ZFIELD        Field
     C                   ENDIF
 
     C     END_AMNT      ENDSR
      **********************************************************************
      /SPACE 5
      **********************************************************************
     C     EXTRACT_DATE  BEGSR
 
     C                   EXSR      EXTRACT_CHAR
     C     *IN99         CABEQ     *OFF          END_DATE
 
      * DAY
     C                   MOVE      *BLANK        A_DAY             2
     C     '/'           SCAN      Field:1       S                 2 0    99
     C     *IN99         CABEQ     *OFF          END_DATE
     C     S             IFEQ      2
     C                   EVAL      A_DAY = '0' + %SUBST(Field:1:1)
     C                   ELSE
     C                   EVAL      A_DAY = %SUBST(Field:1:2)
     C                   ENDIF
     C                   MOVEL     A_DAY         Day
     C                   EVAL      %SUBST(Field: S:1) = ' '
 
      * MONTH
     C                   MOVE      '00'          A_MONTH           2
     C     '/'           SCAN      Field:1       S                 2 0    99
     C     *IN99         CABEQ     *OFF          END_DATE
     C                   SUB       1             S
     C                   EVAL      %SUBST(A_MONTH:2:1) = %SUBST(Field:S:1)
     C                   SUB       1             S
     C                   IF        %SUBST(Field:S:1) <> ' '
     C                   EVAL      %SUBST(A_MONTH:1:1) = %SUBST(Field:S:1)
     C                   ENDIF
     C                   MOVEL     A_MONTH       Month
 
      * YEAR
     C                   MOVE      '00'          A_YEAR            2
     C     '/'           SCAN      Field:1       S                 2 0    99
     C     *IN99         CABEQ     *OFF          END_DATE
     C                   ADD       3             S
     C                   EVAL      A_YEAR  = %SUBST(Field:S:2)
     C                   MOVEL     A_YEAR        Year
 
     C                   CALL      'ZDATE1'
     C                   PARM      *blank        ZERR              7
     C                   PARM      DayMonthYear  ZDATE             6 0
     C                   PARM      'D'           ZDATFM            1
     C                   PARM      *zero         ZDAYNO            5 0
 
     C                   MOVEL     *ZEROS        Field
     C                   MOVEL     ZDAYNO        Field
     C     END_DATE      ENDSR
      **********************************************************************
      /SPACE 5
      **********************************************************************
     C     *INZSR        BEGSR
     C                   ENDSR
      **********************************************************************
