/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas MS WRKMAIF command processing program')         */
/*********************************************************************/
/*                                                                   */
/*       Midas SWIFT Direct Link                                     */
/*                                                                   */
/*       MSC0330 - Work with Midas-SWIFT Alliance Automated File     */
/*                 Transfer interface                                */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2001           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. CSC023             Date 30Jan04              */
/*       Prev Amend No. CSF002             Date 11Aug03              */
/* Midas Release 4 --------------- Base -----------------------------*/
/*                      202192             Date 21Jan02              */
/*                      CPK014             Date 14Nov01              */
/* Midas DBA 3.00 ---------------- Base -----------------------------*/
/*                      CSW009 *CREATE     Date 24FEB97              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/* This program is called via command WRKMAIF                        */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       CSC023 - MidasPlus additional packaging.  SBMJOB change.    */
/*                OUTQ must always be *JOBD.                         */
/*       CSF002 - Global Processing                                  */
/*       202192 - Replace the DLTDTAQ and CRTDTAQ commands with      */
/*                calls to the QCLRDTAQ API.                         */
/*       CPK014 - Submit jobs with user *JOBD                        */
/*       CSW009 - Midas-SWIFT Automated File Transfer                */
/*                                                                   */
/*********************************************************************/
 
             PGM        PARM(&OP)
 
             DCL        VAR(&OP) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INJOB) TYPE(*CHAR) LEN(26)
             DCL        VAR(&OUTJOB) TYPE(*CHAR) LEN(26)
             DCL        VAR(&INSTS) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OUTSTS) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSGSTS) TYPE(*CHAR) LEN(20)
             DCL        VAR(&INASTS) TYPE(*CHAR) LEN(4)
             DCL        VAR(&OUTASTS) TYPE(*CHAR) LEN(4)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGDTA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&PREFIX) TYPE(*CHAR) LEN(2)
/**********  DCL        VAR(&RTCD) TYPE(*DEC) LEN(1 0)                                    /*CSF002*/
             DCL        VAR(&XLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ENABLED) TYPE(*CHAR) LEN(1)
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(1)                                     /*CSF002*/
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2001')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Set off switches */
             CHGJOB     SWS(XXXXXX00)
 
/* Check whether link is enabled in this system before performing */
/* any further processing */
             RTVDTAARA  DTAARA(MS_ENABLED (1 1)) RTNVAR(&ENABLED)
             IF         COND(&ENABLED *NE 'Y') THEN(DO)
/**********                                                                               /*CSF002*/
/**********  CALL       PGM(UTC9001) PARM('SF0420' &RTCD)                                 /*CSF002*/
/**********  IF         COND(&RTCD *EQ 1) THEN(DO)                                        /*CSF002*/
             RTVJOBA    TYPE(&TYPE)                                                       /*CSF002*/
             IF         COND(&TYPE *EQ '1') THEN(DO)                                      /*CSF002*/
             RTVMSG     MSGID(MEM7004) MSGF(MIDAS) MSGDTA(&MSGSTS) +
                          MSG(&MESSAGE)
             CHGDTAARA  DTAARA(MIDASMSG (301 50)) VALUE(&MESSAGE)
             CALL       PGM(SCC0010) PARM('WRKMAIF' 'ENTER' ' ')
             ENDDO
             ELSE       CMD(SNDPGMMSG MSGID(MEM7004) MSGF(GBMIDAS) +
                          MSGDTA(&MSGSTS))
             GOTO       CMDLBL(END)
             ENDDO
 
/* Build xxXLIB */
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&PREFIX)
             CHGVAR     VAR(&XLIB) VALUE(&PREFIX *CAT 'XLIB')
 
/* Establish current status for both jobs */
             RTVDTAARA  DTAARA(MSSTAT2 (1 26)) RTNVAR(&OUTJOB)
             RTVDTAARA  DTAARA(MSSTAT2 (27 26)) RTNVAR(&INJOB)
             CALL       PGM(MSC3301) PARM(&INJOB &INSTS &INASTS)
             CALL       PGM(MSC3301) PARM(&OUTJOB &OUTSTS &OUTASTS)
 
/* Goto processing for operation */
             IF         COND(&OP *EQ '*PROMPT') THEN(GOTO +
                          CMDLBL(PROMPT))
             IF         COND(&OP *EQ '*STARTUP') THEN(GOTO +
                          CMDLBL(STARTUP))
             IF         COND(&OP *EQ '*SHUTDOWN') THEN(GOTO +
                          CMDLBL(SHUTDOWN))
             IF         COND(&OP *EQ '*CHECK') THEN(GOTO CMDLBL(CHECK))
 
/* Unknown operation: terminate */
             GOTO       CMDLBL(END)
 
/* *PROMPT -------------------------------------------------------- */
/* If status of either job is DEQW prompt job to start now */
 PROMPT:     IF         COND(&OUTASTS *EQ 'DEQW') THEN(CALL +
                          PGM(QSNDDTAQ) PARM(MS_M2A_AFT *LIBL +
                          X'00001F' C))
             IF         COND(&INASTS *EQ 'DEQW') THEN(CALL +
                          PGM(QSNDDTAQ) PARM(MS_A2M_AFT *LIBL +
                          X'00001F' C))
             GOTO       CMDLBL(END)
 
/* *STARTUP ------------------------------------------------------- */
/*  o Outgoing message transmission */
 STARTUP:    IF         COND(&OUTSTS *EQ '*OUTQ') THEN(DO)
             ALCOBJ     OBJ((MS_M2A_AFT *DTAARA *EXCL)) WAIT(0)
             MONMSG     MSGID(CPF1002) EXEC(GOTO CMDLBL(STARTIN))
/**********  DLTDTAQ    DTAQ(MS_M2A_AFT) */                                               /*202192*/
             CALL       PGM(QCLRDTAQ) PARM('MS_M2A_AFT' '*LIBL ')                         /*202192*/
             MONMSG     MSGID(CPF0000)
/**********  CRTDTAQ    DTAQ(&XLIB/MS_M2A_AFT) MAXLEN(1) TEXT('Midas + */                 /*202192*/
/**********               MS control data queue - Tx')                  */                /*202192*/
/************SBMJOB     CMD(CALL PGM(MSC3200)) JOB(MS_M2A_AFT) +                          /*CPK014*/
/************             JOBD(MBATCH) JOBQ(MSJOBQ) +                                     /*CPK014*/
/************             RTGDTA(MULTILANGUAGE) MSGQ(MOPERQ)                              /*CPK014*/
/************SBMJOB     CMD(CALL PGM(MSC3200)) JOB(MS_M2A_AFT) +                          /*CSC023*/
/************             JOBD(MBATCH) JOBQ(MSJOBQ) USER(*JOBD) +                         /*CSC023*/
/************             RTGDTA(MULTILANGUAGE) MSGQ(MOPERQ)                   /*CPK014*/ /*CSC023*/
             SBMJOB     CMD(CALL PGM(MSC3200)) JOB(MS_M2A_AFT) +
                          JOBD(MBATCH) JOBQ(MSJOBQ) USER(*JOBD) +
                          RTGDTA(MULTILANGUAGE) MSGQ(MOPERQ) OUTQ(*JOBD)                  /*CSC023*/
             RCVMSG     PGMQ(*SAME) MSGTYPE(*LAST) MSGDTA(&MSGDTA) +
                          MSGID(&MSGID)
             IF         COND(&MSGID *EQ 'CPC1221') THEN(DO)
             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(&MSGDTA)
             CHGVAR     VAR(&OUTJOB) VALUE(%SST(&MSGDTA 1 26))
             CHGDTAARA  DTAARA(MSSTAT2 (1 26)) VALUE(&OUTJOB)
             ENDDO
             ELSE       CMD(GOTO CMDLBL(ABNOR))
             ENDDO
 
/*  o Incoming message reception */
 STARTIN:    IF         COND(&INSTS *EQ '*OUTQ') THEN(DO)
             ALCOBJ     OBJ((MS_A2M_AFT *DTAARA *EXCL)) WAIT(0)
             MONMSG     MSGID(CPF1002) EXEC(GOTO CMDLBL(END))
/**********  DLTDTAQ    DTAQ(MS_A2M_AFT) */                                               /*202192*/
             CALL       PGM(QCLRDTAQ) PARM('MS_A2M_AFT' '*LIBL ')                         /*202192*/
             MONMSG     MSGID(CPF0000)
/**********  CRTDTAQ    DTAQ(&XLIB/MS_A2M_AFT) MAXLEN(1) TEXT('Midas + */                 /*202192*/
/**********               MS control data queue - Rx')                 */                 /*202192*/
/************SBMJOB     CMD(CALL PGM(MSC3250)) JOB(MS_A2M_AFT) +                          /*CPK014*/
/************             JOBD(MBATCH) JOBQ(MSJOBQ) +                                     /*CPK014*/
/************             RTGDTA(MULTILANGUAGE) MSGQ(MOPERQ)                              /*CPK014*/
/************SBMJOB     CMD(CALL PGM(MSC3250)) JOB(MS_A2M_AFT) +                          /*CSC023*/
/************             JOBD(MBATCH) JOBQ(MSJOBQ) USER(*JOBD) +                         /*CSC023*/
/************             RTGDTA(MULTILANGUAGE) MSGQ(MOPERQ)                   /*CPK014*/ /*CSC023*/
             SBMJOB     CMD(CALL PGM(MSC3250)) JOB(MS_A2M_AFT) +
                          JOBD(MBATCH) JOBQ(MSJOBQ) USER(*JOBD) +
                          RTGDTA(MULTILANGUAGE) MSGQ(MOPERQ) OUTQ(*JOBD)                  /*CSC023*/
             RCVMSG     PGMQ(*SAME) MSGTYPE(*LAST) MSGDTA(&MSGDTA) +
                          MSGID(&MSGID)
             IF         COND(&MSGID *EQ 'CPC1221') THEN(DO)
             SNDPGMMSG  MSGID(&MSGID) MSGF(QCPFMSG) MSGDTA(&MSGDTA)
             CHGVAR     VAR(&INJOB) VALUE(%SST(&MSGDTA 1 26))
             CHGDTAARA  DTAARA(MSSTAT2 (27 26)) VALUE(&INJOB)
             CHGDTAARA  DTAARA(MEDTA (441 26)) VALUE(&INJOB)
             ENDDO
             ELSE       CMD(GOTO CMDLBL(ABNOR))
             ENDDO
 
/* Normal end */
             GOTO       CMDLBL(END)
 
/* *SHUTDOWN------------------------------------------------------- */
/*  o Outgoing message transmission */
 SHUTDOWN:   IF         COND(&OUTSTS *EQ '*ACTIVE') THEN(DO)
             CALL       PGM(QSNDDTAQ) PARM(MS_M2A_AFT *LIBL +
                          X'00001F' T)
             ALCOBJ     OBJ((MS_M2A_AFT *DTAARA *EXCL)) WAIT(360)
             MONMSG     MSGID(CPF1002) EXEC(DO)
             SNDPGMMSG  MSGID(MEM7002) MSGF(GBMIDAS)
             GOTO       CMDLBL(ABNOR)
             ENDDO
             ENDDO
 
/*  o Incoming message reception */
             IF         COND(&INSTS *EQ '*ACTIVE') THEN(DO)
             CALL       PGM(QSNDDTAQ) PARM(MS_A2M_AFT *LIBL +
                          X'00001F' T)
             ALCOBJ     OBJ((MS_A2M_AFT *DTAARA *EXCL)) WAIT(360)
             MONMSG     MSGID(CPF1002) EXEC(DO)
             SNDPGMMSG  MSGID(MEM7002) MSGF(GBMIDAS)
             GOTO       CMDLBL(ABNOR)
             ENDDO
             ENDDO
 
/* Normal end */
             GOTO       CMDLBL(END)
 
/* *CHECK---------------------------------------------------------- */
 CHECK:      CHGVAR     VAR(&MSGSTS) VALUE(&OUTSTS *CAT &INSTS)
/**********  CALL       PGM(UTC9001) PARM('SF0420' &RTCD)                                 /*CSF002*/
/**********  IF         COND(&RTCD *EQ 1) THEN(DO)                                        /*CSF002*/
             RTVJOBA    TYPE(&TYPE)                                                       /*CSF002*/
             IF         COND(&TYPE *EQ '1') THEN(DO)                                      /*CSF002*/
             RTVMSG     MSGID(MEM7003) MSGF(MIDAS) MSGDTA(&MSGSTS) +
                          MSG(&MESSAGE)
             CHGDTAARA  DTAARA(MIDASMSG (301 50)) VALUE(&MESSAGE)
             CALL       PGM(SCC0010) PARM('WRKMAIF' 'ENTER' ' ')
             ENDDO
             ELSE       CMD(SNDPGMMSG MSGID(MEM7003) MSGF(GBMIDAS) +
                          MSGDTA(&MSGSTS))
 
/* Normal end */
             GOTO       CMDLBL(END)
 
/* ---------------------------------------------------------------- */
/* Abnormal end */
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('WRKMAIF +
                          ended abnormally - see job log')
             MONMSG     MSGID(CPF0000 MCH0000)
 
 END:        DLCOBJ     OBJ((MS_M2A_AFT *DTAARA *EXCL))
             MONMSG     MSGID(CPF0000)
             DLCOBJ     OBJ((MS_A2M_AFT *DTAARA *EXCL))
             MONMSG     MSGID(CPF0000)
 
             ENDPGM
