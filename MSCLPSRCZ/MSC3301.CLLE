/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas MS WRKMAIF access job details')                 */
/*********************************************************************/
/*                                                                   */
/*       Midas SWIFT Direct Link                                     */
/*                                                                   */
/*       MSC3301 - WRKMAIF access job details                        */
/*                                                                   */
/*       (c) Finastra International Limited 2001                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/* Midas Release 4 --------------- Base -----------------------------*/
/* Midas DBA 3.00 ---------------- Base -----------------------------*/
/*       Prev Amend No. CSW009 *CREATE     Date 24FEB97              */
/*                      Xnnnnn             Date ddmmmyy              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/* This program is called via command WRKMAIF                        */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CSW009 - Midas-SWIFT Automated File Transfer                */
/*                                                                   */
/*********************************************************************/
 
             PGM        PARM(&JOB &STATUS &ACTSTS)
 
             DCL        VAR(&JOB) TYPE(*CHAR) LEN(26)
             DCL        VAR(&STATUS) TYPE(*CHAR) LEN(10) VALUE(' ')
             DCL        VAR(&ACTSTS) TYPE(*CHAR) LEN(4) VALUE(' ')
 
             DCL        VAR(&RETRY) TYPE(*CHAR) LEN(5)
             DCL        VAR(&MSGID) TYPE(*CHAR) LEN(7)
             DCL        VAR(&MSGDTA) TYPE(*CHAR) LEN(256)
             DCL        VAR(&RTN_INFO) TYPE(*CHAR) LEN(512)
             DCL        VAR(&RTN_BIN) TYPE(*CHAR) LEN(4)
             DCL        VAR(&FORMAT) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INT_JOB_ID) TYPE(*CHAR) LEN(16) VALUE(' ')
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2001')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Use API/QUSRJOBI to access job details (if CPF3C54 received wait */
/* for 30 seconds and retry) */
             CHGVAR     VAR(&RTN_BIN) VALUE(X'00000200')
             CHGVAR     VAR(&FORMAT) VALUE('JOBI0200')
             CHGVAR     VAR(&RETRY) VALUE('FALSE')
 RETRY:      CALL       PGM(QUSRJOBI) PARM(&RTN_INFO &RTN_BIN +
                          &FORMAT &JOB &INT_JOB_ID)
             MONMSG     MSGID(CPF3C53 CPF3C55 CPF3C54 CPF3C58) EXEC(DO)
             RCVMSG     RMV(*NO) MSGDTA(&MSGDTA) MSGID(&MSGID)
             IF         COND(&MSGID *EQ 'CPF3C54') THEN(DO)
             IF         COND(&RETRY *EQ 'FALSE') THEN(DO)
             CHGVAR     VAR(&RETRY) VALUE('TRUE ')
             DLYJOB     DLY(30)
             GOTO       CMDLBL(RETRY)
             ENDDO
             ELSE       CMD(DO)
             SNDPGMMSG  MSGID(CPF3C54) MSGF(QCPFMSG) MSGDTA(&MSGDTA) +
                          TOPGMQ(*PRV (MSC3300))
             GOTO       CMDLBL(ABNOR)
             ENDDO
             ENDDO
             CHGVAR     VAR(&STATUS) VALUE('*OUTQ')
             GOTO END
             ENDDO
             CHGVAR     VAR(&STATUS) VALUE(%SST(&RTN_INFO 51 10))
             CHGVAR     VAR(&ACTSTS) VALUE(%SST(&RTN_INFO 108 4))
             GOTO END
 
/* Abnormal end */
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) +
                          MSGDTA('WRKMAIF ended abnormally - see +
                          job log') TOPGMQ(*PRV (MSC3300)) +
                          TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
 END:
             ENDPGM
