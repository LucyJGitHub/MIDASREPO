      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MS CASmf session maintenance')
      *****************************************************************
      *                                                               *
      *   Midas SWIFT Direct Link : Midas/CAS interface               *
      *                                                               *
      *   MS8020DF - CASmf session maintenance                        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *    This source is centrally supported and must ONLY be        *
      *    amended by Core Development Personnel                      *
      *                                                               *
      *    /COPY, Client or Country amendments must not be            *
      *    applied to this source.                                    *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *   Last amend no. CSW008 *CREATE    Date 22AUG96               *
      *   Prev amend no. nnnnnn            Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   CSW008 - Midas/CAS interface                                *
      *   nnnnnn - (fix details)                                      *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      CA03
      *
     A                                      HELP
     A                                      HLPDOC(H500@000 MSINS ABSENG/DBAR4)
     A          R MS8020F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            ##USR         10A  O  1  2
     A                                  1 13'MS8020P'
     A                                  1 27'CASmf session maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O  1 72
     A            ##JOB         10A  O  2  2
     A                                  2 71TIME
      *
     A          R MS8020F2
     A                                      OVERLAY
      *
     A                                  4  2'Action'
     A                                  4 12'Session'
     A                                  4 44'Input/Output'
     A                                  4 60'Status'
      *
     A          R MS8020F3                  SFL
     A  24                                  SFLNXTCHG
     A            S3ACTN         1A  B  6  2
     A  30                                  DSPATR(RI PC)
     A            S3MAPI        26A  O  6 12
     A            S3IPOP         7A  O  6 44
     A            S3STAT        15A  O  6 60
     A  35                                  DSPATR(HI)
      *
     A          R MS8020F4                  SFLCTL(MS8020F3)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A  20                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N20N21                               SFLCLR
     A  22                                  SFLEND
     A N22                                  ROLLUP(23)
     A                                      OVERLAY
      *
     A          H                           HLPDOC(H500@005 MSINS ABSENG/DBAR4)
     A                                      HLPARA(06 01 21 02)
     A            DSPREC         4S 0H      SFLRCDNBR
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
     A                                 23 25'F9=Add'
     A                                 23 34'F12=Previous'
     A                                 23 49'Valid action codes:'
     A            S4ACTS         4A  O 23 69
      *
     A          R MS8020F5
     A                                      OVERLAY
     A                                      CA12
     A          H                           HLPDOC(H500@010 MSINS ABSENG/DBAR4)
     A                                      HLPARA(06 01 06 53)
     A          H                           HLPDOC(H500@015 MSINS ABSENG/DBAR4)
     A                                      HLPARA(07 01 07 28)
     A          H                           HLPDOC(H500@020 MSINS ABSENG/DBAR4)
     A                                      HLPARA(08 01 08 28)
     A          H                           HLPDOC(H500@025 MSINS ABSENG/DBAR4)
     A                                      HLPARA(09 01 09 28)
     A          H                           HLPDOC(H500@030 MSINS ABSENG/DBAR4)
     A                                      HLPARA(10 01 10 32)
     A          H                           HLPDOC(H500@035 MSINS ABSENG/DBAR4)
     A                                      HLPARA(12 01 12 37)
     A          H                           HLPDOC(H500@040 MSINS ABSENG/DBAR4)
     A                                      HLPARA(13 01 13 37)
     A          H                           HLPDOC(H500@045 MSINS ABSENG/DBAR4)
     A                                      HLPARA(14 01 14 37)
     A          H                           HLPDOC(H500@050 MSINS ABSENG/DBAR4)
     A                                      HLPARA(16 01 16 29)
     A                                  4  2'Action..................:'
     A            S5ACTN         1A  O  4 28
     A                                  6  2'Session.................:'
     A            S5MAPI        26A  B  6 28CHECK(LC)
     A  70                                  DSPATR(PR)
     A N70                                  DSPATR(UL)
     A  80                                  DSPATR(RI PC)
     A                                  7  2'Input/Ouput (I/O).......:'
     A            S5IPOP         1A  B  7 28
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  81                                  DSPATR(RI PC)
     A                                  8  2'Startup (A/M)...........:'
     A            S5STUP         1A  B  8 28
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  82                                  DSPATR(RI PC)
     A                                  9  2'Open type (F/W).........:'
     A            S5OTYP         1A  B  9 28
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  83                                  DSPATR(RI PC)
     A                                 10  2'Wait time for open......:'
     A            S5WAIT         5  0B 10 28
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  84                                  DSPATR(RI PC)
     A                                 12  2'Alternative access path.:'
     A            S5ACCP        10A  B 12 28CHECK(VNE AB)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  85                                  DSPATR(RI PC)
     A                                 13  2'Ext. formatting program.:'
     A            S5XPGM        10A  B 13 28CHECK(VNE AB)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  86                                  DSPATR(RI PC)
     A                                 14  2'Job name................:'
     A            S5JNAM        10A  B 14 28CHECK(VNE AB)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  87                                  DSPATR(RI PC)
     A                                 16  2'Midas system............:'
     A            S5SYTM         2A  B 16 28
     A  72                                  DSPATR(PR)
     A N72                                  DSPATR(RI PC UL)
     A  71 73                          18  2'Communications job......:'
     A  71 73     S5FULJ        28A  O 18 28
     A                                 23  2'F3=Exit'
     A                                 23 12'F12=Previous'
      *
     A          R MS8020F6
     A                                      OVERLAY
     A                                  6 31'--- No Details ---'
      *
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
      *
      **  Message Subfile Control Record format
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
