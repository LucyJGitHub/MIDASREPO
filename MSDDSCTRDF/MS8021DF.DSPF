     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MS CASmf session selection  - Subfile')
     A*****************************************************************
     A*                                                               *
     A*   Midas SWIFT Direct Link : Midas/CAS interface               *
     A*                                                               *
     A*   MS8021DF - CASmf session selection                          *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      *    This source is centrally supported and must ONLY be        *
      *    amended by Core Development Personnel                      *
      *                                                               *
      *    /COPY, Client or Country amendments must not be            *
      *    applied to this source.                                    *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*   Last amend no. CSW008 *CREATE    Date 22AUG96               *
     A*   Prev amend no. nnnnnn            Date ddmmmyy               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*   CSW008 - Midas/CAS interface                                *
     A*   nnnnnn - (fix details)                                      *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      HLPDOC(H500@000 MSINS ABSENG/DBAR4)
     A                                      HELP
     A                                      CA12(12 'Cancel')
     A          R MS8021F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            ##USR         10A  O  1  2
     A                                  1 13'MS8020P'
     A                                  1 27'CASmf session selection'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            AGMRDT         7A  O  1 72
     A            ##JOB         10A  O  2  2
     A                                  2 71TIME
      *
     A          R MS8021F2                  SFL
     A  24                                  SFLNXTCHG
     A            S2SELT         1A  B  7 23
     A  30                                  DSPATR(RI PC)
     A            S2SESN        26A  O  7 32
      *
     A          R MS8021F3                  SFLCTL(MS8021F2)
     A                                      SFLSIZ(0040)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  55                                  SFLDSP
     A  56                                  SFLDSPCTL
     A N55N56                               SFLCLR
     A  57                                  SFLEND(*MORE)
     A N57                                  ROLLUP
     A            DSPREC         4S 0H      SFLRCDNBR
      *
     A                                  5 32'Session'
      *
     A          R MS8021F4
     A                                      OVERLAY
     A                                 23  2'1=Select request'
     A                                 23 22'F12=Previous'
      *
     A          R MS8021F5
     A                                      OVERLAY
     A                                 10 31'--- No Details ---'
      *
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *
      **  Message Subfile Control Record format
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N60
     AO 60                                  SFLEND
     A                                      OVERLAY
     A            ##PGM                     SFLPGMQ
