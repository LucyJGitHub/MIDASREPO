      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas MS File reflecting DTAARA layout')
      *****************************************************************
      *                                                               *
      *  Midas - Midas-Swift Module                                   *
      *                                                               *
      *  PF/MSTMPR - RPTMPR Data Area Layout                          *
      *                                                               *
      *  Note: Any fix applied to this file may also need to be       *
      *        applied to the core DTAARA source.                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. 092583             Date 11OCT95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  092583 - New file created to reflect layout of DTAARA/MSTMPR.*
      *                                                               *
      *****************************************************************
     A          R MSTMPRD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            SWPOS          1          TEXT('SWIFT postings file required +
     A                                      Y,N')
     A            ZZ004          4
     A            ACCRO          3          TEXT('SWIFT accounts file +
     A                                      reorganised Y,N')
     A            CSIDX         12          TEXT('Branch Internal Customer    +
     A                                      Swift ID')
     A            TRACF          1          TEXT('Trace Facility active, T')
     A            QFIND          1          TEXT('Ready queue full Y,N')
     A            CPAIND         1          TEXT('Transmission program active +
     A                                      Y,N')
     A            PDEIND         1          TEXT('Possible duplicate error Y,N')
     A            MTCIND         1          TEXT('Call transmission program +
     A                                      closedown program Y,N')
     A            CJNAM         10          TEXT('Transmission program job +
     A                                      name')
     A            CUNAM         10          TEXT('Transmission program user +
     A                                      name')
     A            CJNO           6          TEXT('Transmission program job +
     A                                      number 6N')
     A            JAMIND         1          TEXT('Transmission program waiting +
     A                                      to send status command to STS +
     A                                      device Y,blank')
     A            OTIME          4          TEXT('Time transmission program +
     A                                      last sent status command to STS +
     A                                      device HHMM')
     A            IMRIND         1          TEXT('Incoming message reference +
     A                                      generation program active Y,N')
     A            GLSNT          1          TEXT('GL SWIFT statement messages +
     A                                      may already have been sent')
     A            ZZ001          1
     A            STATRN        16          TEXT('Merva/2 - TRN of failed msg')
     A            ZZ024         24
     A            LSTORN        19          TEXT('SWIFT output reference +
     A                                      number of last incoming message +
     A                                      processed by reset SPD flag +
     A                                      program')
     A            RRNLIM         9          TEXT('relative record number of +
     A                                      last incoming message processed +
     A                                      by reset SPD flag program 9N')
     A            LSTMIR        28
     A            RTCPIP        20          TEXT('TCP/IP Remote system Id.')
     A            RINCF         20          TEXT('TCP/IP Incoming File Name')
     A            RWAIT          2  0       TEXT('TCP/IP Remote Wait Time')
     A            RTRY           1  0       TEXT('TCP/IP Maximum Retry')
     A            RCNN           3          TEXT('TCP/IP Link Type (LAN|PTP)')
     A            ZZ053         53
     A            MSLID          2          TEXT('LIBID of live SWIFT. Update +
     A                                      manually')
