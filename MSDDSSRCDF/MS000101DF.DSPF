      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas Bank Directory Plus Enquiry')                    *
      *****************************************************************
      *                                                               *
      *  Midas - S.W.I.F.T Direct Link                                *
      *                                                               *
      *  DSPF/MS000101DF - Bank Directory Plus Enquiry                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. MD035218           Date 29Jul15               *
      *  Prev Amend No. MD023856           Date 28Nov13               *
      *                 CFT050  *CREATE    Date 03Jun13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD035218 - Incorrect message on SWIFT/SWIFTRef 2014          *
      *             related screens (Recompile)                       *
      *  MD023856 - Incorrect Branch length displayed in Midas Bank   *
      *             Directory Plus Details Enquiry                    *
      *  CFT050 - SWIFTRef Directories upload                         *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R MS000101F1                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. 2013')
     A            SUSER     R   10   O  1  2REFFLD(USER)
     A            SPGM      R   10   O  1 13REFFLD(JOPGM)
     A            SRUNA     R    7   O  1 72REFFLD(RUNA)
     A            SWSID     R   10   O  2  2REFFLD(JOJOB)
     A N60                              2 26'Bank Directory Plus Enquiry'
     A  60                              2 26'IBAN Plus Directory Enquiry'
     A                                      DSPATR(HI)
     A                                  2 71TIME
      *
     A          R MS000101S0                SFL
     A            S0SEL     R        I  7  3REFFLD(ACTN)
     A                                      VALUES('E')
     A                                      CHECK(AB)
     A                                      DSPATR(UL HI PC)
     A            S0BICC    R        O  7  6REFFLD(B3BICC *LIBL/MEBIB3PD)
     A            S0BRCD    R        O  7 16REFFLD(B3BRCD *LIBL/MEBIB3PD)
      **Start of change MD023856
     A            H0BRCD    R        H      REFFLD(B3BRCD *LIBL/MEBIB3PD)
      **End of change MD023856
     A            S0INS1    R        O  7 21REFFLD(B3INS1 *LIBL/MEBIB3PD)
     A            S0CITY        20A  O  7 58TEXT('BIC City')
      *
     A          R MS000101C0                TEXT('Subfile control')
     A                                      SFLCTL(MS000101S0)
     A                                      SFLPAG(15) SFLSIZ(16)
     A                                      BLINK OVERLAY PUTOVR
     A  30                                  SFLDSPCTL
     A  31                                  SFLDSP
     A N30N31                               SFLCLR
     A  32                                  SFLEND
     A N32                                  ROLLUP(39)
      *
     A            C0SFRC         4  0H      SFLRCDNBR
     A                                  4  6'BIC Code'
     A                                  4 16'Brc'
     A                                  4 21'Institution Name 1'
     A                                  4 58'City'
     A            C0BICC    R        B  6  6REFFLD(B3BICC *LIBL/MEBIB3PD)
     A                                      DSPATR(HI UL)
     A            C0BRCD    R        B  6 16REFFLD(B3BRCD *LIBL/MEBIB3PD)
     A                                      DSPATR(HI UL)
     A            C0INS1    R        B  6 21REFFLD(B3INS1 *LIBL/MEBIB3PD)
     A                                      CHECK(LC)
     A                                      DSPATR(HI UL)
     A            C0CITY        20A  B  6 58TEXT('BIC City')
     A                                      DSPATR(HI UL)
      *
     A          R MS000101F2                TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'E=Enquire  F3=Exit'
      *
     A          R MS000101S1                TEXT('(C) MISYS +
     A                                      INTERNATIONAL LTD 2013')
     A                                      SFL
     A            S1DESC        30   O  5  3TEXT('Field Description')
     A            S1FVAL        45   O  5 34TEXT('Field Value')
     A                                      DSPATR(HI)
      *
     A          R MS000101C1                TEXT('Subfile control')
     A                                      SFLCTL(MS000101S1)
     A                                      SFLPAG(17) SFLSIZ(18)
     A                                      BLINK OVERLAY PUTOVR
     A  50                                  SFLDSPCTL
     A  51                                  SFLDSP
     A N50N51                               SFLCLR
     A  52                                  SFLEND
     A                                      CA12
     A  61                                  CA15
      *
     A          R MS000101F3                TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A  63                             22  2'No Data to Display'
     A                                      COLOR(WHT)
     A                                 23  2'F3=Exit  F12=Previous'
     AAN60 61                          23 26'F15=IBAN Plus'
     A  60                             23 26'F15=BANK Directory'
      *
     A          R MS000101M0                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R MS000101C2                TEXT('Program messages')
     A                                      SFLCTL(MS000101M0)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
