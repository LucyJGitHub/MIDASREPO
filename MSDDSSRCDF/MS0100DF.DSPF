      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas BIC Directory Details Enquiry')                  *
      *****************************************************************
      *                                                               *
      *  Midas - S.W.I.F.T Direct Link                                *
      *                                                               *
      *  DSPF/MS0100DF - BIC Directory Details Enquiry                *
      *                                                               *
      *  (c) Finastra International Limited 2012                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CMS002  *CREATE    Date 28Sep12               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CMS002 - BIC Directory Details Enquiry                       *
      *                                                               *
      *****************************************************************
      *
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
     A          R MS0100HH                  TEXT('(c) Finastra International +
     A                                      Limited 2012')
     A            SUSER         10   O  1  2
     A            SPGM          10   O  1 13
     A            SRUNA          7   O  1 72
     A            SWSID         10   O  2  2
     A                                  2 26'MIDAS BIC DIRECTORY ENQUIRY'
     A                                      DSPATR(HI)
     A                                  2 71TIME
      *
     A          R MS0100S0                  SFL
     A            S0SEL          1   I  7  3
     A                                      VALUES('E')
     A                                      CHECK(AB)
     A                                      DSPATR(UL HI PC)
     A            S0BICC         8   O  7  6TEXT('BIC (Bank, Country, & Loc)')
     A            S0BICB         3   O  7 16TEXT('BIC (Branch Code)')
     A            S0INS1        35   O  7 21TEXT('Institution Name 1')
     A            S0CITY        20A  O  7 58TEXT('BIC City')
      *
     A          R MS0100C0                  TEXT('Subfile control')
     A                                      SFLCTL(MS0100S0)
     A                                      SFLPAG(15) SFLSIZ(16)
     A                                      BLINK OVERLAY PUTOVR
     A  30                                  SFLDSPCTL
     A  31                                  SFLDSP
     A N30N31                               SFLCLR
     A  32                                  SFLEND
     A N32                                  ROLLUP(39)
      *
     A            C0SFRC         4  0H      SFLRCDNBR
     A                                  4  6'BIC Code'
     A                                  4 16'Brc'
     A                                  4 21'Institution Name 1'
     A                                  4 58'City'
     A            C0BICC         8   B  6  6TEXT('BIC (Bank, Country, & Loc)')
     A                                      DSPATR(HI UL)
     A            C0BICB         3   B  6 16TEXT('BIC (Branch Code)')
     A                                      DSPATR(HI UL)
     A            C0INS1        35   B  6 21TEXT('Institution Name 1')
     A                                      DSPATR(HI UL)
     A            C0CITY        20A  B  6 58TEXT('BIC City')
     A                                      DSPATR(HI UL)
      *
     A          R #CMDTXT0                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'E=Enquire  F3=Exit'
      *
     A          R MS0100S1                  TEXT('(C) Finastra +
     A                                      INTERNATIONAL LTD 1998')
     A                                      SFL
     A            S1DESC        30   O  5  3TEXT('Field Description')
     A            S1FVAL        45   O  5 34TEXT('Field Value')
     A                                      DSPATR(HI)
      *
     A          R MS0100C1                  TEXT('Subfile control')
     A                                      SFLCTL(MS0100S1)
     A                                      SFLPAG(17) SFLSIZ(18)
     A                                      BLINK OVERLAY PUTOVR
     A  50                                  SFLDSPCTL
     A  51                                  SFLDSP
     A N50N51                               SFLCLR
     A  52                                  SFLEND
     A                                      CA12
      *
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit  F12=Previous'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            SPGM                      SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            SPGM                      SFLPGMQ
      *
