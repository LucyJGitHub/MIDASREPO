      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MS Query/answer message input')
      *****************************************************************
      *                                                               *
      *  Midas - SWIFT DIRECT LINK                                    *
      *                                                               *
      *   MS5020DF - QUERY/ANSWER MESSAGE INPUT                       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 130041             Date 25Feb98               *
      *                 113152             Date 13May97               *
      *                 105395             DATE 08MAY97               *
      *                 106066             DATE 11APR97               *
      *                 CSW095             DATE 10APR95               *
      *                 E39162             DATE 18JUN92               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *   130041 - Swift Messages MT195 and MT196 are generated with  *
      *            branch code of incoming message instead of user    *
      *            assigned.                                          *
      *            Fix 106066 removed.                                *
      *   113152 - Allow lower case input in MIR & TRNO               *
      *   105395 - Allow lower case in transaction reference.         *
      *   106066 - Branch field not required on first screen          *
      *   CSW095 - S.W.I.F.T 1995 Message Changes.                    *
      *            The subfile should only have 6 input lines.        *
      *            Ensure that MT n95/n96 messages do not exceed      *
      *            2,000 characters.                                  *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
      *   S01344 - RE WRITTEN FOR SWIFT RATIONALISATION PHASE 2       *
      *                                                               *
      *****************************************************************
      *                                                               *
      *         /////////////////////////////////////////             *
      *        // WARNING: When creating this display //              *
      *       //   file, take options RSTDSP(*YES)   //               *
      *      //                       SHARE (*YES)  //                *
      *     /////////////////////////////////////////                 *
      *                                                               *
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
      *
     A          R MS5020F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            USRID         10   O  1  2
     A                                  1 13'MS5020'
     A  01                              1 30'QUERY MESSAGE INPUT'
     A                                      DSPATR(UL HI)
     A N01                              1 30'ANSWER MESSAGE INPUT'
     A                                      DSPATR(UL HI)
     A            RUNA           7   O  1 72
     A            WSID          10   O  2  2
     A                                  2 71TIME
     A  01 20                           4  2'Query message text'
     A N01 20                           4  2'Answer message text'
      *
     A          R MS5020F2
     A                                      OVERLAY
      *
     A************     START OF CHANGE                     /106066/
     A**41********                     21  2'Branch'
     A**41********SBCH           3     21  9
     A*N31********                          DSPATR(HI)
     A**31********                          DSPATR(RI PC)
     A************     END OF CHANGE                       /106066/
     A************     START OF CHANGE                     /130041/
     A  41                             21  2'Branch'
     A  41        SBCH           3     21  9
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A************     END OF CHANGE                       /130041/
     A                                 21 13'MIR'
     A            SMIR          28   B 21 17
     A*                                                         /*Start 113152*/
     A                                      CHECK(LC)
     A*                                                           /*End 113152*/
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A                                 21 46'TRN'
     A            STRN          16   B 21 50
      *                                                         /*Start 105395*/
     A                                      CHECK(LC)
      *                                                          /*End  105395*/
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A            SFMG          78     22  2
     A  30                                  DSPATR(RI)
     A                                 23  2'F3-Exit'
      *
     A          R MS5020F3                  SFL
     A  24                                  SFLNXTCHG
     A            RCD           35   B  5 23
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI PC)
      *
     A          R MS5020F4                  SFLCTL(MS5020F3)
     A                                      CA12
     A  20                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N20N21                               SFLCLR
     A  23                                  SFLINZ
     A*                                              Begin /CSW095/
     A***********                           SFLSIZ(0035)
     A                                      SFLSIZ(0006)
     A*                                              End   /CSW095/
     A                                      SFLPAG(0006)
     A  22                                  SFLEND
     A                                      OVERLAY
     A                                 12  2'Description of original +
     A                                      message'
     A            SDS1          50   B 13 15
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A            SDS2          50   B 14 15
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A            SDS3          50   B 15 15
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A            SDS4          50   B 16 15
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A            SDS5          50   B 17 15
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A            SDS6          50   B 18 15
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A            SDS7          50   B 19 15
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A  41                             21  2'Branch'
     A  41        SBCH           3     21  9
     A                                 21 13'MIR'
     A            SMIR          28     21 17
     A                                 21 46'TRN'
     A            STRN          16     21 50
     A            SFMG          78     22  2
     A  30                                  DSPATR(RI)
     A                                 23  2'F3-Exit F12-Previous'
     A                                 23 24'Rollup/Rolldown'
