      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MS Incoming message rerouting')
      *****************************************************************
      *                                                               *
      *  Midas - SWIFT Direct Link Module                             *
      *                                                               *
      *   MS5110DF - INCOMING MESSAGE REROUTING                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. E39162             DATE 18JUN92               *
     A*  PREV AMEND NO. E00000                  DDMMMYY               *
      *                                                               *
     A*****************************************************************
     A*                                                               *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
     A*                                                               *
     A*****************************************************************
      *                                                               *
      *         /////////////////////////////////////////             *
      *        // WARNING: When creating this display //              *
      *       //   file, take options RSTDSP(*YES)   //               *
      *      //                       SHARE (*YES)  //                *
      *     /////////////////////////////////////////                 *
      *                                                               *
      *  C R E A T I O N     P A R A M E T E R S                      *
     A*****************************************************************
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
      *
      *
     A          R MS5110F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
      *
     A            USRID         10A  O  1  2
     A                                  1 13'MS5110'
     A                                  1 20'S.W.I.F.T. II INCOMING +
     A                                      MESSAGE REROUTING'
     A                                      DSPATR(UL HI)
     A            RUNA           7A  O  1 72
     A            WSID          10   O  2  2
     A                                  2 71TIME
      *
     A          R MS5110F2                  OVERLAY
     A                                 20  2'Branch:'
     A            SBCH           3   B 20 10
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A                                 21  2'Transaction reference:'
     A            STRN          16   B 21 25
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A                                 21 42'Output date:'
     A            SOSD           6   B 21 55
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A                                 21 62'Output time:'
     A            SOST           4   B 21 75
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI PC)
     A            SFMG          78A  O 22  2
     A  30                                  DSPATR(RI)
     A                                 23  2'F3-Exit'
      *
     A          R MS5110F3                  OVERLAY
     A                                  3  2'Branch'
     A                                  3  9'Output'
     A                                  3 21'Transaction'
     A                                  3 38'Message'
     A                                  3 46'MIR'
     A                                  4  9'date'
     A                                  4 16'time'
     A                                  4 21'reference'
     A                                  4 38'type'
      *
     A          R MS5110F4                  SFL
     A  35                                  SFLNXTCHG
     A            BRCA           3   B  6  2
     A  35                                  DSPATR(RI PC)
     A            MODE           6  0   6  9
     A            MOTM           4  0   6 16
     A            TRNO          16      6 21
     A            MTPY           3      6 38
     A            MIR           28      6 46
      *
     A          R MS5110F5                  SFLCTL(MS5110F4)
     A                                      CA12
     A  20                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N20N21                               SFLCLR
     A                                      SFLSIZ(17)
     A                                      SFLPAG(16)
     A  22                                  SFLEND
     A N22                                  ROLLUP(23)
     A                                      OVERLAY
      *
      *
     A            DSPREC         4  0H      SFLRCDNBR
     A            SFMG          78A  O 22  2
     A  30                                  DSPATR(RI)
     A                                 23  2'F3-Exit'
     A                                 23 11'F12-Previous'
     A                                 23 25'Rollup/Rolldown'
