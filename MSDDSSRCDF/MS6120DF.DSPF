      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MS Midas/SWIFT meridian comms id maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Midas SWIFT                                          *
      *                                                               *
      *  MS6120DF - Midas/SWIFT Meridian communications ID maintenance*
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *    This source is centrally supported and must ONLY be        *
      *    amended by Core Development Personnel                      *
      *                                                               *
      *    /COPY, Client or Country amendments must not be            *
      *    applied to this source.                                    *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSW038             Date 21Apr05               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CSW025             Date 15Apr02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CSW017 *CREATE     Date 11Feb99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSW038 - Change to allow communication with MMM using        *
      *           MQSeries instead of data queues.  Here we add the   *
      *           queue manager name, so that a queue manager other   *
      *           than the default may be used.                       *
      *  CSW025 - Midas Message Manager                               *
      *           - Extract messages to Meridian Link via Data Queue  *
      *  CSW017 - Midas/Swift Meridian interface                      *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      CA03
      *
     A          R MS6120F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            ##USR         10A  O  1  2
     A                                  1 13'MS6120P'
     A                                  1 21'Midas/Swift Meridian Communication-
     A                                       IDs Maintenance'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O  1 72
     A            ##JOB         10A  O  2  2
     A                                  2 71TIME
      *
     A          R MS6120F2
     A                                      OVERLAY
      *
     A                                  4  2'Action'
     A                                  4 12'Comms ID'
     A                                  4 44'Input/Output'
     A                                  4 60'Status'
      *
     A          R MS6120F3                  SFL
     A  24                                  SFLNXTCHG
     A            S3ACTN         1A  B  6  2
     A  30                                  DSPATR(RI PC)
     A            S3MCID        26A  O  6 12
     A            S3IPOP         7A  O  6 44
     A            S3STAT        15A  O  6 60
     A  35                                  DSPATR(HI)
      *
     A          R MS6120F4                  SFLCTL(MS6120F3)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      CA05
     A                                      CA09
     A                                      CA12
     A  20                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N20N21                               SFLCLR
     A  22                                  SFLEND
     A N22                                  ROLLUP(23)
     A                                      OVERLAY
      *
     A            DSPREC         4S 0H      SFLRCDNBR
     A                                 23  2'F3=Exit'
     A                                 23 12'F5=Refresh'
     A                                 23 25'F9=Add'
     A                                 23 34'F12=Previous'
     A                                 23 49'Valid action codes:'
     A            S4ACTS         4A  O 23 69
      *
     A          R MS6120F5
     A                                      OVERLAY
     A                                      CA12
     A                                  4  2'Action..................:'
     A            S5ACTN         1A  O  4 28
     A                                  6  2'Communications ID.......:'
     A            S5MCID        26A  B  6 28CHECK(LC)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  80                                  DSPATR(RI PC)
     A**Start of change CSW025
     A* Start of change LNO
     A N74                              7  2'MQ Series Queue Manager.:'
     A N74        S5MQMN        48A  B  7 28
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  87                                  DSPATR(RI PC)
     A**********                        7  2'MQ Series Queue Name....:'                       CSW025
     A**********  S5MQQN        50A  B  7 28                                                  CSW025
     A**71******                            DSPATR(PR)                                        CSW025
     A*N71******                            DSPATR(UL)                                        CSW025
     A**81******                            DSPATR(RI PC)                                     CSW025
     A*N74******                        7  2'MQ Series Queue Name....:'                       CSW038
     A*N74******  S5MQQN        48A  B  7 28                                                  CSW038
     A N74                              8  2'MQ Series Queue Name....:'
     A N74        S5MQQN        48A  B  8 28
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  81                                  DSPATR(RI PC)
     A**74******                        7  2'Data Queue .............:'                       CSW038
     A**74******  S5DTQN        10A  B  7 28                                                  CSW038
     A  74                              8  2'Data Queue .............:'
     A  74        S5DTQN        10A  B  8 28
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  81                                  DSPATR(RI PC)
     A**End of change CSW025
     A**********                        8  2'Input/Ouput (I/O).......:'                       CSW038
     A**********  S5IPOP         1A  B  8 28                                                  CSW038
     A                                  9  2'Input/Ouput (I/O).......:'
     A            S5IPOP         1A  B  9 28
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  82                                  DSPATR(RI PC)
     A**********                        9  2'Startup (A/M)...........:'                       CSW038
     A**********  S5STUP         1A  B  9 28                                                  CSW038
     A                                 10  2'Startup (A/M)...........:'
     A            S5STUP         1A  B 10 28
     A* End of change LNO
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  83                                  DSPATR(RI PC)
     A                                 12  2'Alternative access path.:'
     A            S5ACCP        10A  B 12 28CHECK(VNE AB)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  84                                  DSPATR(RI PC)
     A                                 13  2'Ext. formatting program.:'
     A            S5XPGM        10A  B 13 28CHECK(VNE AB)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  85                                  DSPATR(RI PC)
     A                                 14  2'Job name................:'
     A            S5JNAM        10A  B 14 28CHECK(VNE AB)
     A  71                                  DSPATR(PR)
     A N71                                  DSPATR(UL)
     A  86                                  DSPATR(RI PC)
     A                                 16  2'Midas system............:'
     A            S5SYTM         2A  B 16 28
     A  72                                  DSPATR(PR)
     A N72                                  DSPATR(RI PC UL)
     A  71 73                          18  2'Communications job......:'
     A  71 73     S5FULJ        28A  O 18 28
     A                                 23  2'F3=Exit'
     A                                 23 12'F12=Previous'
      *
     A          R MS6120F6
     A                                      OVERLAY
     A                                  6 31'--- No Details ---'
      *
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
      *
      **  Message Subfile Control Record format
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
