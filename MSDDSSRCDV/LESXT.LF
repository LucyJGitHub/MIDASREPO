      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas MS P/R EXTRACT FILE')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Midas Swift Module                               *
     A*                                                               *
     A*  LF/LESXT    SWIFT C/LENDING SORTED EXTRACTS                  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 05Dec05               *
      *  Prev Amend No. CLE042             Date 06Sep05               *
      *                 230521             Date 28Oct04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 148104             Date 10Dec98               *
      *                 CEU004             Date 02Feb98               *
     A*                 S01518             Date 03Oct94               *
     A*                 E16444             Date 23Jan89               *
     A*                 E0163              DATE 02APR84               *
     A*                 S01117             DATE 14FEB89               *
     A*                                                               *
     A*---------------------------------------------------------------*
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  CLE042 - Extended Loan Sub Type (Recompile)                  *
     A*  230521 - Added Field for PF/LEPXTAB. Applied fix 215643      *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
     A*  148104 - Add fields OTAM and OTCY to record formats LEPXTA1F *
     A*           and LEPXTA2F.                                       *
     A*  CEU004 - LE Settlement Ccy Conversion Upgrade to DBA R2.0    *
     A*           Recompiled due to changes in file LEPXTAB           *
     A*  S01518 - BLI Lending Swift Enhancements.                     *
     A*  S01117 - MULTIBRANCHING                                      *
     A*  E16444 - FILE MISSING IN RELEASE 1 DATABASE                  *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A          R LEPXTAAF                  PFILE(LEPXTAA)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            RECI
     A          K RECI
     A          R LEPXTABF                  PFILE(LEPXTAB)
     A          K *NONE
     A*                                                   /S01117/
     A********* K BRCD
     A          K BRCA
     A*                                                   /S01117/
     A          K *NONE
     A          K *NONE
     A          K CCY
     A          K CSID
     A          K VDAT
     A          K *NONE
     A          K EAMT
     A          S PYRC                      COMP(EQ 'R')
     A          O                           ALL
     A          R LEPXTA1F                  PFILE(LEPXTAB)
     A            RECI
     A            LNRF                      RENAME(LNRFA)
     A            VDAT
     A            LASNS
     A            ZZ001
     A            LTYP
     A            SUTP
     A            PTYP
     A            AMTP
     A            ZZ010
     A            CCY
     A            ZZ028
     A            EAMT
     A*                                                   /S01117/
     A*********   BRCD
     A            BRCA
     A*                                                   /S01117/
     A            CNUM
     A*                                                   /S01117/
     A************ZZ003
     A            OSBR
     A*                                                   /S01117/
     A            SETP
     A            OSAC
     A            TSEN
     A            FACO
     A            SPI1
     A            SPI2
     A            SPI3
     A            ZZ010A
     A            RCDT
     A            ZZ031
     A            PYRC
     A            ZZ001A
     A            INTC
     A            ZZ002
     A            CSID
     A            PRI1
     A            PRI2
     A            PRI3
     A            PRI4
     A            TIND
     A            BICN
     A*                                              Begin /S01518/
     A            CVMI
     A            RRST
     A            RRON
     A            RRIB
     A            RRBA
     A            RROB
     A            RROC
     A*                                              End   /S01518/
      *                                              Begin /148104/
     A            OTAM
     A            OTCY
      *                                              End   /148104/
      **Start of change 230521
     A            LFCN
      **End of change 230521
     A          K *NONE
     A*                                                   /S01117/
     A********* K BRCD
     A          K BRCA
     A*                                                   /S01117/
     A          K *NONE
     A          K PYRC
     A          K CCY
     A          K CSID
     A          K VDAT
     A          K *NONE
     A          K EAMT
     A          S PYRC                      COMP(NE 'R')
     A            FACO                      COMP(EQ ' ')
     A          O                           ALL
     A          R LEPXTA2F                  PFILE(LEPXTAB)
     A            RECI
     A            LNRF                      RENAME(LNRFA)
     A            VDAT
     A            LASNS
     A            ZZ001
     A            LTYP
     A            SUTP
     A            PTYP
     A            AMTP
     A            ZZ010
     A            CCY
     A            ZZ028
     A            EAMT
     A*                                                   /S01117/
     A*********   BRCD
     A            BRCA
     A*                                                   /S01117/
     A            CNUM
     A*                                                   /S01117/
     A************ZZ003
     A            OSBR
     A*                                                   /S01117/
     A            SETP
     A            OSAC
     A            TSEN
     A            FACO
     A            SPI1
     A            SPI2
     A            SPI3
     A            ZZ010A
     A            RCDT
     A            ZZ031
     A            PYRC
     A            ZZ001A
     A            INTC
     A            ZZ002
     A            CSID
     A            PRI1
     A            PRI2
     A            PRI3
     A            PRI4
     A            TIND
     A            BICN
     A*                                              Begin /S01518/
     A            CVMI
     A            RRST
     A            RRON
     A            RRIB
     A            RRBA
     A            RROB
     A            RROC
     A*                                              End   /S01518/
      *                                              Begin /148104/
     A            OTAM
     A            OTCY
      *                                              End   /148104/
      **Start of change 230521
     A            LFCN
      **End of change 230521
     A          K *NONE
     A*                                                   /S01117/
     A********* K BRCD
     A          K BRCA
     A*                                                   /S01117/
     A          K PYRC
     A          K *NONE
     A          K CCY
     A          K CSID
     A          K VDAT
     A          K CNUM
     A          K EAMT
     A          S PYRC                      COMP(NE 'R')
     A            FACO                      COMP(NE ' ')
     A          O                           ALL
     A          R LEPXTACF                  PFILE(LEPXTAC)
     A          K RECI
