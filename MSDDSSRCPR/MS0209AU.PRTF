/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MS Msg compression error report')                *
      *****************************************************************
      *                                                               *
      *  Midas - SWIFT Direct Link Module                             *
      *                                                               *
      *   MS0209AU - 2780 MESSAGE COMPRESSION ERROR REPORT            *
      *                                                               *
      *  (c) Finastra International Limited 2021                      *
      *                                                               *
      *  Last Amend No. CGL094   *CREATE   Date 02Nov21               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL094 - MT940 Interface to Corona (External Network         *
      *           Destination)                                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R HEADER                    SKIPB(3)
     A                                      TEXT('(c) Finastra +
     A                                      International Limited 2021')
     A                                     7'REFERENCE MS0209AU'
     A            #URPT         53        45
     A                                   101'DATE'
     A            #MRDT          7       106
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    40'OUTGOING MESSAGE COMPRESSION +
     A                                      ERROR REPORT - MT940 Interface'
     A          R DETAIL                    SKIPB(17)
     A                                     7'MESSAGE WITH TRANSACTION +
     A                                      REFERENCE'
     A            TRNO          16        42
     A                                    59'CANNOT BE SENT AS COMPRESSED +
     A                                      MESSAGE IS GREATER THAN 8100 BYTES'
     A                                      SKIPB(43)
      *
     A          R DBERROR
     A                                      SKIPB(17)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SKIPA(43)
     A          R ENDRPT
     A                                      SPACEB(3)
     A                                    35'*** END OF REPORT ***'
