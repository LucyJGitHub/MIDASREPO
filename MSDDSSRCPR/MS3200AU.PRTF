      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MS create o/g message file in DOS-PCC format')
      *****************************************************************
      *                                                               *
      *   Midas SWIFT Direct Link                                     *
      *                                                               *
      *   MS3200AU - Create outgoing message file in DOS-PCC format   *
      *                                              - AUDIT          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *   Last amend no. CSW009 *CREATE    Date 26FEB97               *
      *   Prev amend no. nnnnnn            Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *   CSW009 - Midas-SWIFT Alliance Automated File Transfer       *
      *   nnnnnn - (fix details)                                      *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A          R MS3200F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                  3  7'REFERENCE MS3200'
     A            BJURPT        53      3 35
     A                                  3 81'Date'
     A            BJMRDT         7      3 86
     A                                  3101'Time'
     A                                  3106TIME
     A                                  5 41'Midas-SWIFT Alliance TRANSMISSION'
     A                                  6 41'---------------------------------'
     A                                  8  5'Transaction Reference No.'
     A                                  8 40'Message type'
     A                                  8 60'Destination'
     A                                  8 85'Released by'
     A                                  9  1' '
      *
     A          R MS3200F2
     A                                      SPACEB(1)
     A            TRNO      R              5
     A            MTPY      R             40
     A            NWDS      R             60
     A            RUSR      R             85
      *
     A          R MS3200F3
     A                                      SKIPB(10)
     A                                    50'*** No messages to report ***'
     A          R MS3200F4
     A                                      SPACEB(2)
     A                                     5'Alliance filename :'
                  W@ALFIL       14        25
      *
     A          R MS3200F5
     A                                      SKIPB(10)
     A                                    60'Error Report'
     A                                      SPACEA(1)
     A                                    60'------------'
     A                                      SPACEA(12)
     A                                    20'In                    :'
     A            W0FILE        10        45
     A                                      SPACEA(2)
     A                                    20'Key/Operation         :'
     A            W0KEY         30        45
     A                                      SPACEA(2)
     A                                    20'Program               :'
     A            W0PGM         10        45
     A                                      SPACEA(2)
     A                                    20'At                    :'
     A            W0ERNB         5  0     45
      *
     A          R MS3200F6                  SPACEB(2)
     A                                    54'*** End of Report ***'
