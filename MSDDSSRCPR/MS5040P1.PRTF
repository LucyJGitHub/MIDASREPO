      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MS Incoming messages lost during rec')
     A*****************************************************************
     A*                                                               *
     A*  Midas - SWIFT DIRECT LINK
     A*                                                               *
     A*   MS5040P1 - INCOMING MESSAGES LOST DURING RECOVERY REPORT    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*   LAST AMEND NO. S01310            DATE 06DEC90               *
     A*   PREV AMEND NO.  00000            DATE 00XXX00               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*   S01310 - SWIFT RATIONALISATION                              *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R HEADER                    SKIPB(3)
     A                                      SPACEA(2)
      *                                                                 /S01310/
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *                                                                 /S01310/
     A                                     7'REFERENCE MS5040'
      *                                                                 /S01310/
     A            BJURPT        53        45
     A************TITL***** R             37
      *                                                                 /S01310/
     A                                   101'DATE'
      *                                                                 /S01310/
     A            BJMRDT         7       106
     A************RUNA***** R            106
      *                                                                 /S01310/
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    36'S.W.I.F.T. II INCOMING MESSAGES  +
     A                                       LOST DURING RECOVERY REPORT'
     A                                      UNDERLINE
     A*
     A          R BLOCK1                    SPACEA(2)
     A                                     7'BASIC HEADER (BLOCK 1)' UNDERLINE
     A                                      SPACEA(1)
     A                                     7'RECIPIENT`S LOGICAL TERMINAL ADD+
     A                                      RESS'
     A                                    46'SESSION NUMBER'
     A                                    62'SEQUENCE NUMBER'
     A                                      SPACEA(1)
     A            ORAD          12         7
     A            OSNN           4        46
     A            OSQN           6        62
     A                                      SPACEA(2)
     A                                     7'APPLICATION HEADER (BLOCK 2)'
     A                                      UNDERLINE
     A                                      SPACEA(1)
     A                                     7'MESSAGE TYPE'
     A                                    21'INPUT TIME'
     A                                    33'MESSAGE INPUT REFERENCE'
     A                                    62'OUTPUT DATE'
     A                                    75'OUTPUT TIME'
     A                                    88'PRIORITY'
     A                                      SPACEA(1)
     A            OMTP           3         7
     A            OITM           4        21
     A            OMIR          28        33
     A            OODT           6  0     62EDTCDE(Y)
     A            OOTM           4        75
     A            OPTY           6        88
     A*
     A          R BLOCK3                    SPACEA(2)
     A                                     7'USER HEADER (BLOCK 3)' UNDERLINE
     A                                      SPACEA(1)
     A                                     7'BANKING PRIORITY'
     A                                    25'MESSAGE USER REFERENCE'
     A                                      SPACEA(1)
     A            OBPY           4         7
     A            OMUR          16        25
     A*
     A          R BLOCK4                    SPACEB(2)
     A                                     7'TEXT (BLOCK 4)' UNDERLINE
     A*
     A          R FIELD                     SPACEB(1)
     A            RCD          132         7
     A*
     A          R BLOCK5                    SPACEB(2)
     A                                     7'TRAILER (BLOCK 5)' UNDERLINE
     A*
     A          R SBLOCK                    SPACEB(2)
     A                                     7'S.W.I.F.T TERMINAL' UNDERLINE
     A                                    27'TRAILER (BLOCK S)' UNDERLINE
     A*
     A          R NORECS                    SPACEB(3)
     A                                    55'***  NO MESSAGES TO REPORT ***'
     A*
     A          R PAGHEAD                   SKIPB(7)
     A                                     7'NETWX'
     A                                    14'SENDER'
     A                                    27'SESSION  SEQ.'
     A                                    43'DATE'
     A                                    51'TIME'
     A                                    57'T.R.N'
     A                                    75'STATUS'
     A                                    85'MSG'
     A                                    90'DESTINATION'
     A                                   104'PRIORITY'
     A                                   117'ERROR'
     A                                      SPACEA(1)
     A                                    27'NUMBER   NUMBER'
     A                                    85'TYPE'
     A                                   116'CODE TAG'
     A*
     A          R LGLACK                    SPACEB(1)
     A            NTWK           5         7
     A            SEND          12        13
     A            SESS           4        27
     A            SEQ            6        36
     A            DAT            6        43
     A            TIM            4        51
     A            TRNO          16        57
     A            STAT           8        75
     A            MSTYP          3        85
     A            DEST          12        90
     A            PRTY           6       104
     A            CODE           3       116
     A            TAG            3       121
     A*
     A          R EOREPT                    SPACEB(3)
     A                                    55'*** END OF REPORT ***'
     A*
     A          R DBERROR
     A                                      SKIPB(17)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SKIPA(43)
     A                                    35'*** END OF REPORT ***'
