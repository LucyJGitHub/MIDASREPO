/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas MS Merva/2 - Outgoing Messages Tx')
      *****************************************************************
      *                                                               *
      *  Midas - SWIFT Direct Link Module                             *
      *                                                               *
      *   Written for S01431 - Midas to Merva/2 interface             *
      *                                                               *
      *   MS6010AU - Merva/2  - Outgoing Message Tx error report      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 091250             Date 13Mar95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  091250 - AU reports should go to MPRINTAU. Change to         *
      *           standard creation parameters.                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R MS6010F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  3  7'REFERENCE MS6010'
     A            BJURPT        53      3 45
     A                                  3101'DATE'
     A            BJMRDT         7      3106
     A                                 10 20'MIDAS TO MERVA/2 INTERFACE - TX'
     A                                 15 20'ABNORMAL TERMINATION:'
      *
     A          R MS6010F2                  SPACEB(5)
     A                                    20'CODE  : '
     A            @RC            4        30
     A            @NAR          70        40
      *
     A          R MS6010F3                  SPACEB(5)
     A                                    58'DATABASE ERROR REPORT'
     A                                      SPACEA(12)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
      *
     A          R MS6010F4                  SPACEB(5)
     A                                    20'DATA QUEUE ERROR - MSDTQB'
      *
      *
     A          R MS6010F9                  SPACEB(15)
     A                                    55'*** END OF REPORT ***'
