     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MS Drop msgs before retention date')
      *****************************************************************
      *                                                               *
      *  Midas - SWIFT Direct Link Module                             *
      *                                                               *
      *   MS0505 - COPY MESSAGES WITHIN HISTORIC PERIOD               *  *S01200
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD049128           Date 20Mar18               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 BUG4717            Date 03Dec04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 124673             Date 04Jun98               *
      *                  105045            Date 11APR97               *
      *                  S01487            DATE 28OCT94               *
      *                  S01435            DATE 03JAN94               *
      *                  S01449            DATE 15DEC93               *
      *                  E38191            DATE 30MAR92               *
      *                  S01310            DATE 06DEC90               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD049128 - Future-valued incoming messages are purged before *
      *             being routed into payments.                       *
      *             Patterned fix from GEMS 230172 to consider value  *
      *             date to identify messages to be purged.           *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *   BUG4717- Incoming Messages are dropped too early.           *
      *   124673 - Add Century field for validation.                  *
      *   105045 - Prevent duplicate records from multiplying by      *
      *            scanning for end of text.                          *
      *            Process only one index record for each mir.        *
      *   S01487 - Midas Leo Integration                              *
      *            Recompile for MSIXI2*  change.                     *
      *   S01435 - Incoming Message Management                        *
      *            Recompile for MSIXI2*  change.                     *
      *   S01449 - AutoRecs II: don't drop messages if not downloaded *
      *            to PC component - status 'A' (arrived) or 'R'      *
      *            (flagged for re-send).                             *
      *   E38191 - SDMSDLPD REDUNDANT - USE SDMGMEPD INSTEAD          *
      *   S01310 - RE WRITTEN FOR SWIFT RATIONALISATION               *
      *                                                               *
      *****************************************************************
     F*                                                               *
     F*****************************************************************
     F/COPY WNCPYSRC,MS0505F001
     FSDBANKL1IF  E           K        DISK                               S01200
     F*DMSDLL1IF**E***********K        DISK                         S01200E38191
     FSDMGMEL1IF  E           K        DISK                               E38191
     FMSIXI2L2IF  E           K        DISK                               S01285
     FMSMSI2L9IF  E           K        DISK                               S01285
     FMSIXI2BKO   E                    DISK                               S01200
     F            MSIXI2D0                          KRENAMEMSIXIBF        S01200
     FMSMSI2BKO   E                    DISK                               S01200
     F            MSMSI2D0                          KRENAMEMSMSIBF        S01200
      *
      * ID C  F  H  L    Function of indicators
      *    01            EOF MSIXI2L2
      *    02            EOF MSMSI2L9
      *****88************EOF SDMSDLL1                                     E38191
      *    88            EOF SDMGMEL1                                     E38191
      *    89            EOF SDBANKL1
      *
     E                    CPY@    1   1 80
      *
     E/COPY ZSRSRC,ZDATE1Z1                                                                 MD049128
      *                                                                                     MD049128
     ISDMMOD    E DSSDMMODPD                                              S01449
      ** Module details                                                   S01449
      *
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      **  Local data area
      *                                                                   105045
      * End of text                                                       105045
     I              X'03'                 C         C@EOT                 105045
      *
      *****************************************************************
      *                   Index to subroutines                        *
      *   main process                                                *
      *   INIT   - initial process                                    *
      *   *PSSR  - error handling                                     *
      *****************************************************************
     C/EJECT
      *****************************************************************
      *   main process                                                *
      *   Calls - INIT initial process                                *
      *****************************************************************
      *
      **  Perform initial process
     C                     EXSR INIT
      *
      **  READ FIRST SWIFT II INCOMING MESSAGE                            S01285
     C                     READ MSIXI2L2                 01               S01285
      *                                                                   S01285
     C           *IN01     DOWEQ'0'                                   1   S01285
     C                     MOVE MIR       KMIR   28                       S01285
      *                                                                   S01285
      ** Give earliest retention date a century.                          124673
     C                     MOVELEDATE     ECENT   2                       124673
     C           ECENT     IFLT '72'                                      124673
     C                     MOVE '20'      ECENT                           124673
     C                     ELSE                                           124673
     C                     MOVE '19'      ECENT                           124673
     C                     ENDIF                                          124673
      *                                                                                      BUG4717
     C           MODEC     IFEQ *BLANK                                                       BUG4717
     C                     MOVE '20'      MODEC                                              BUG4717
     C                     ENDIF                                                             BUG4717
      *                                                                   124673
      ** Compute for the message value date                                                 MD049128
      *                                                                                     MD049128
     C                     MOVE SVDT      WMMDD   40                                        MD049128
     C                     MOVELSVDT      WYY     20                                        MD049128
     C                     MOVE WYY       WNDAT   6                                         MD049128
     C                     MOVELWMMDD     WNDAT                                             MD049128
     C                     MOVE WNDAT     ZDATE                                             MD049128
     C                     MOVE '1'       *IN98                                             MD049128
     C                     EXSR ZDATE1                                                      MD049128
     C                     Z-ADDZDAYNO    WKVDAT  50                                        MD049128
      *                                                                                     MD049128
     C           EDATE     IFLT MODE                                  2   S01285
     C           ECENT     ANDEQMODEC                                     124673
     C           ECENT     ORLT MODEC                                     124673
     C           R3RPTS    OREQ 'A'                                       S01449
     C           BGAURC    ANDEQ'Y'                                       S01449
     C           R3RPTS    OREQ 'R'                                       S01449
     C           BGAURC    ANDEQ'Y'                                       S01449
     C           EDATE     ORGE MODE                                                        MD049128
     C           ECENT     ANDEQMODEC                                                       MD049128
     C           WKVDAT    ANDGTBJRDNB                                                      MD049128
      *                                                                   S01449
     C                     WRITEMSIXIBF                                   S01285
      *                                                                   S01285
     C           KMIR      CHAINMSMSI2L9             02                   S01285
      *                                                                   S01285
     C           *IN02     DOWEQ'0'                                   3   S01285
     C                     WRITEMSMSIBF                                   S01285
      *                                                                   105045
      * Check for end of text; if not found continue reading records.     105045
     C           C@EOT     SCAN MDTA                     02               105045
     C           *IN02     IFEQ *OFF                                      105045
     C           KMIR      READEMSMSI2L9                 02               S01285
     C                     ENDIF                                          105045
      *                                                                   105045
     C                     END                                       E3   S01285
     C/COPY WNCPYSRC,MS0505C001
     C                     END                                       E2   S01285
      *                                                                   S01285
     C           KMIR      SETGTMSIXI2L2                                  105045
     C                     READ MSIXI2L2                 01               S01285
     C                     END                                       E1   S01285
      *
     C                     SETON                         LR
     C/EJECT
      *****************************************************************
      *   INIT - inital process                                       *
      *   Called by main process                                      *
      *****************************************************************
      *
     C           INIT      BEGSR
      *
     C                     MOVEACPY@      BIS@   80
      *
      **  Set up LDA
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'MS0505'  DBPGM
     C                     MOVE *BLANKS   DBASE
     C                     OUT  LDA
      *
      **  GET RUN DATE FROM SDBANKL1                                      S01200
     C                     READ SDBANKL1                 89               S01200
      *                                                                   S01200
     C           *IN89     IFEQ '1'                                       S01200
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE
     C                     MOVE 'FIRST'   DBKEY            * DB ERROR 01 *
     C                     MOVE 'SDBANKPD'DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END                                       E2
      *                                                                   S01449
      **  Get module details via access object - database error if        S01449
      **  not found.                                                      S01449
     C                     CALL 'AOMMODR0'                                S01449
     C                     PARM '*DBMSG  '@RTCD   7                       S01449
     C                     PARM '*FIRST  '@OPTN   7                       S01449
     C                     PARM           SDMMOD                          S01449
     C           @RTCD     IFNE *BLANKS                                   S01449
     C           *LOCK     IN   LDA                                       S01449
     C                     Z-ADD3         DBASE            * * * * * * * *S01449
     C                     MOVEL'FIRST'   DBKEY            *  DBERR 003  *S01449
     C                     MOVEL'SDMMODPD'DBFILE           * * * * * * * *S01449
     C                     OUT  LDA                                       S01449
     C                     EXSR *PSSR                                     S01449
     C                     ENDIF                                          S01449
      *
      ****GET*RETENTION*DAYS*FROM*SDMSDLL1                                E38191
     C*********************READ SDMSDLL1                 88               E38191
      **  GET RETENTION DAYS FROM SDMGMEL1                                E38191
     C                     READ SDMGMEL1                 88               E38191
      *
     C           *IN88     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE
     C                     MOVE 'FIRST'   DBKEY            * DB ERROR 02 *
     C*********************MOVE 'SDMSDLPD'DBFILE                          E38191
     C                     MOVE 'SDMGMEPD'DBFILE                          E38191
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END                                       E2
      *                                                                   S01200
      **  SET EARLIEST RETENTION DATE                                     S01200
     C***********BJRDNB****SUB**BSDSMN****ZMDAY                     S01251E38191
     C           BJRDNB    SUB  ENDSMN    ZMDAY                           E38191
     C                     CALL 'ZM0060'
     C                     PARM           ZMDAY   50
     C                     PARM           ZMDATE  6
     C                     MOVE ZMDATE    EDATE   60
     C/COPY WNCPYSRC,MS0505C002
      *
     C                     ENDSR                                          S01285
     C/EJECT                                                                                MD049128
     C/COPY ZSRSRC,ZDATE1Z2                                                                 MD049128
      *                                                                                     MD049128
     C/EJECT
      *****************************************************************
      *   *PSSR - error handling                                      *
      *****************************************************************
     C           *PSSR     BEGSR
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
** CPY@   **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN                                       MD049128
0366073110961461                                                                           MD049128
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR                                         MD049128
000031059090120151181212243273304334365                                                    MD049128
