     H        1   Y
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MS Update FTP Input Cmd Files -TRANSFER-')
     F*****************************************************************
     F*                                                               *
     F*  Midas   Midas S.W.I.F.T. DIRECT LINK                 *
     F*                                                               *
     F*  MS3001  -  Setup FTP Input Command File                      *
     F*                                                               *
     F*  Function : This program updates the FTP Command file with    *
     F*             the user and password.                            *
     F*             It also updates files with the source and         *
     F*             target names of the file to be sent.              *
     F*                                                               *
     F*  Called By: MSC3001 - SWIFT File Transfer Protocol Functions  *
     F*                                                               *
     F*  Calls:     -                                                 *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      *  Prev Amend No. 179551             Date 05Apr01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSW001  *CREATE    Date 20Mar95               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  179551 - Add the second when building file name to be        *
      *           transferred.  If program run twice during same      *
      *           second --> target file will be overwritten.         *
      *           Applied fix 167414.                                 *
      *   CSW001   SWIFT Alliance Batch Interface                     *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     F*
     FMSSENDPDUF  E                    DISK         KINFSR *PSSR
     F** TCP/IP FTP Input Command File -
     F*
     FSDBANKL1IF  E                    DISK         KINFSR *PSSR
     F** Midas ICD for Run Date
     F*
     F/EJECT
     F*****************************************************************
     F*                     INDICATOR USAGE                           *
     F*                     ---------------                           *
     F*                                                               *
     F*   01      End of file for MSSENDPD                            *
     F*   02      End of file for SDBANKL1                            *
     F*   11      Record identifying indicator for PF/MSSENDPD        *
     F*   50      Work indicator                                      *
     F*   U7&U8   Error occured                                       *
     F*   LR      End of program                                      *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E*
     E                    CPY@    1   1 80
     E** Copyright Statement
     E*
     E                    ARA        80  1
     E** Array to hold FTP Command
     E*
     I/EJECT
     ICPYR@#      DS
     I** Data Structure for Compilation - Copyright Insertion
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     I            DS
     I** Input File Record
     I                                        1  80 FTIN
     I                                        1   3 WCMD
     I                                        5   6 WLIBID
     I*
     I            DS
     I** Remote File Name Data Structure
     I***********                             1  15 RMTF                  179551
     I                                        1  17 RMTF                  179551
     I                                        1   3 RMTF1
     I                                        4  10 RMTF2
     I                                        4   4 RMTF21
     I                                       11  11 RMTF3
     I                                       12  15 RMTF4
     I                                       12  17 RMTF5                 179551
     I*
     IFSCAN       DS                             80
     I*
     IMSSTAT      DS                            256
     I                                      255 256 MSLID
     I*
     ILDA        UDS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I** Local data area
     I*
     C/EJECT
     C*
     C** Entry Parameter List.
     C*
     C           *ENTRY    PLIST
     C                     PARM           PMODE   4
     C*
     C** Execute INIT Subroutine
     C*
     C                     EXSR INIT
     C*
     C** Read First record of the FTP Input Command file (this will
     C** contain the profile and password used on the remote system)
     C*
     C                     READ MSSENDPD                 01
     C*
     C** If the file is empty treat as a database error
     C*
     C           *IN01     IFEQ '1'                        B1
     C           *LOCK     IN   LDA
     C                     MOVEL'MSSENDPD'DBFILE
     C                     MOVEL*BLANKS   DBKEY            ************
     C                     Z-ADD2         DBASE            * DBERR 02 *
     C                     MOVEL'MS3001'  DBPGM            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END                             E1
     C*
     C** Determine User Id and Password from MSSENDPD
     C** This processing has no effect -> will be reviewed for Security Audit.
     C*
     C                     MOVEA*BLANKS   ARA
     C                     MOVEAFTIN      ARA
     C                     MOVEAARA,1     WUSER  10
     C                     MOVEAARA,12    WPASS  10
     C*
     C** Processing to update correct remote file name
     C*  =============================================
     C*
     C** Read succeeding records of FTP Command file until end of
     C** file reached or a command line using 'PUT' is found.
     C*
     C                     READ MSSENDPD                 01
     C*
     C** Do while not end of file & FTP command is not 'PUT'
     C*
     C           *IN01     DOWEQ'0'                        B1
     C           WCMD      ANDNE'PUT'
     C*
     C** Read next record from file
     C*
     C                     READ MSSENDPD                 01
     C*
     C                     ENDDO                           E1
     C*
     C** If End of file reached without finding a 'PUT' command
     C** record then treat as an abnormal error
     C*
     C           *IN01     IFEQ '1'                        B1
     C                     EXSR *PSSR
     C                     ELSE                            X1
     C/COPY WNCPYSRC,MS3001C001
     C*
     C** Check for Library name in MSSENDPD to be the same as the current
     C** system id. from MSSTAT.
     C*
     C           WLIBID    IFNE MSLID
     C           *LOCK     IN   LDA                        *B1
     C                     MOVEL'MSSENDPD'DBFILE
     C                     MOVEL'        'DBKEY            ************
     C                     Z-ADD3         DBASE            * DBERR 03 *
     C                     MOVEL'MS3001'  DBPGM            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END                             *E2
     C*
     C** Form the name of the file to send to the remote system
     C** by adding the Remote system file name.
     C** RMTFILNAME  = "MSF + DDMMMYY + .HHMM" (e.g. MSF08NOV94.1105)
     C*
     C                     Z-ADD0         Z       20
     C                     MOVEA*BLANKS   ARA
     C                     MOVEAFTIN      ARA
     C                     MOVELFTIN      FSCAN
     C                     MOVE 'MSF'     ARG3    3
     C           ARG3      SCAN FSCAN     Z              50
     C           *IN50     IFEQ '1'                        *B2
     C                     MOVEA*BLANK    ARA,Z
     C                     MOVEARMTF      ARA,Z
     C*
     C** If no name found then database error
     C                     ELSE                            *X2
     C           *LOCK     IN   LDA
     C                     MOVEL'MSSENDPD'DBFILE
     C                     MOVEL'        'DBKEY            ************
     C                     Z-ADD4         DBASE            * DBERR 04 *
     C                     MOVEL'MS3001'  DBPGM            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END                             *E2
     C*
     C** Line formatted to 'Libname/Filename.Membername RemoteName'
     C** Update record and exit loop
     C*
     C                     MOVEAARA       FTIN
     C                     UPDATMSSENDF
     C/COPY WNCPYSRC,MS3001C002
     C*
     C                     END                             E1
     C*
     C** End program
     C*
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE : INIT   - Initialisation Subroutine              *
     C*                                                               *
     C*  CALLED BY  : This routine is called at the beginning of the  *
     C*               program to initilaise fields                    *
     C*                                                               *
     C*  FIELDS USED: WPSSR  - Flag to dump program only once         *
     C*                                                               *
     C*****************************************************************
     C*
     C           INIT      BEGSR                            ** INIT  **
     C*
     C** Copyright Statement.
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C** Define Dataarea.
     C*
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN           MSSTAT
     C                     IN   MSSTAT
     C*
     C** Access SDBANKPD for Run Date
     C*
     C                     READ SDBANKL1                 02
     C           *IN02     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVEL'        'DBKEY            ************
     C                     Z-ADD1         DBASE            * DBERR 01 *
     C                     MOVEL'MS3001'  DBPGM            ************
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
     C*
     C** Build Remote File name
     C***(must*be*in*form*"MID"DDMMMYY.HHMM)***************************   179551
     C** (must be in form "MID"DDMMMYY.HHMMSS)                            179551
     C*
     C                     TIME           #TIME   60
     C*
     C                     MOVE *BLANKS   RMTF
     C                     MOVE 'MSF'     RMTF1
     C                     MOVE BJMRDT    RMTF2
     C                     MOVE '.'       RMTF3
     C                     MOVEL#TIME     RMTF4
     C                     MOVEL#TIME     RMTF5                           179551
     C*
     C*  First pos. of date for File Name could not be *blank.
     C           RMTF21    IFEQ ' '
     C                     MOVE '0'       RMTF21
     C                     END
     C*
     C                     ENDSR
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*  SUBROUTINE : *PSSR  - Error Handling Subroutine              *
     C*                                                               *
     C*  CALLED BY  : This routine is called if there are any program *
     C*               or file errors.                                 *
     C*                                                               *
     C*  FIELDS USED: WPSSR  - Flag to dump program only once         *
     C*                                                               *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR                           ** *PSSR  **
     C*
     C** Ensure dump is executed only once.
     C*
     C           WPSSR     IFEQ *BLANK
     C                     MOVEL'Y'       WPSSR   1
     C                     DUMP
     C                     END
     C*
     C**  Exit program.
     C*
     C                     SETON                     U7U8LR
     C                     RETRN
     C*
     C           #PSSR9    ENDSR                           ** #PSSR9 **
     C*****************************************************************
     C/EJECT
     C*****************************************************************
**  CPY@
(c) Finastra International Limited 2001
