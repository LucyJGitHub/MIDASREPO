     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MS Logical ack'ment linked function')
      *****************************************************************
      *                                                               *
      *  Midas - SWIFT DIRECT LINK                               *
      *                                                               *
      *   MS5105 - LOGICAL ACKNOWLEDGEMENTS LINKED FUNCTION           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 141572             Date 21SEP98               *
      *                 113352             Date 13FEB97               *
      *                  CSW095            DATE 10APR95               *
      *                  S01310            DATE 06DEC90               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *   141572 - Recompile over changes in MGOREFPD                 *
      *   113352 - Recompile over changes in MGOREFPD                 *
      *   CSW095 - S.W.I.F.T 1995 Message Changes.                    *
      *            Increase arrays to handle a 10,000 character       *
      *            message.                                           *
      *   S01310 - SWIFT RATIONALISATION                              *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  C R E A T I O N     P A R A M E T E R S                      *
      *****************************************************************
     F*ABTB10*IF**E********   K        DISK                               S01310
     FMS5105DFCF  E                    WORKSTN
     F                                        RRN   KSFILE MS5105F2
     FMSMSI2L8IF  E           K        DISK
     FMGOREFL0IF  E           K        DISK                               S01310
     F*SIXA***IF**E********   K        DISK                               S01310
      *
      **  ID C  F  H  L    Function of indicators
      *      10            Chain fail on MSMSI2L8
      *      11            Chain fail on MGOREFL0                         S01310
      *******11************Chain fail on MSIXA                            S01310
      *      20            SFLDSP
      *      21            SFLDSPCTL
      *******89************Chain fail on TABTB10                          S01310
      *      KL            F12                                            S01310
      *      U7U8LR        DB error
      *
     E***********         IMSG     3072  1                                CSW095
     E                    IMSG     9999  1                                CSW095
     E                    LINE       65  1
     E                    CPY@    1   1 80                                S01310
      *
     I            DS
     I                                        1 256 MDTA
     I                                        7  18 SEND
     I                                       19  22 SESS
     I                                       23  28 SEQN
     I                                       38  43 ADAT
     I                                       44  47 ATIM
     I                                       54  54 @WK1
     I                                       56  60 @WK5
     I                                       61  63 @CDE
     I                                       64  66 @TAG
     I                                       68  72 @WL5
      **  Message header data structure
      *
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      **  Local data area data structure
      *
     I           SDS
     I                                      244 253 WSID
     I                                      254 263 USRID                 S01310
      **  Workstation ID
      *
     IDSRUN       DS                             13                       S01310
     I                                        1   7 RUNA                  S01310
     I                                       13  13 MBIN                  S01310
      **  RUNDAT data area                                                S01310
      *
     C           *ENTRY    PLIST
     C                     PARM           TRNO
     C/EJECT
      *****************************************************************
      *                   Index to subroutines                        *
      *   main process                                                *
      *   INIT  - initial process                                     *
      *   *PSSR - error handling                                      *
      *****************************************************************
     C/EJECT
      *****************************************************************
      *   main process                                                *
      *   Calls - INIT initial process                                *
      *****************************************************************
      *
      **  Perform initialisation
     C                     EXSR INIT
      *
      **  Chain to LF/MSMSI2L8 using TRNO passed from MS5005
     C           TRNO      CHAINMSMSI2L8             10
      *
      **  If no record found then perform Database Error
     C           *IN10     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '02'      DBASE
     C                     MOVE TRNO      DBKEY            * DB ERROR 02 *
     C                     MOVE 'MSMSI2L8'DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      **  Format screen
     C                     EXSR FORMAT
      *
      **  Display Logical Acknowledgement until F12 pressed
     C           *INKL     DOWEQ'0'
     C                     WRITEMS5105F1
     C                     EXFMTMS5105F3
     C                     END
      *
     C                     SETON                     LR
     C/EJECT
      *****************************************************************
      *   INIT - inital process                                       *
      *   Called by - main process                                    *
      *****************************************************************
      *
     C           INIT      BEGSR
      *
      ****Define*key*******                                               S01310
     C***********IXKY******KLIST                                          S01310
     C*********************KFLD           MSMI                            S01310
     C*********************KFLD           TNRF                            S01310
      *
     C                     MOVEACPY@      BIS@   80                       S01310
      *
      **  Set up LDA
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE 'MS5105  'DBPGM
     C                     OUT  LDA
      *
      **  Initialise 'CR' and 'LF' fields
     C                     MOVE *LOVAL    CR      1
     C                     BITON'457'     CR
     C                     MOVE *LOVAL    LF      1
     C                     BITON'257'     LF
     C                     MOVELCR        CRLF    2
     C                     MOVE LF        CRLF
      *
      ****Access*TABTB10*to find run date                                 S01310
     C*********************READ TABTB10                  89               S01310
      *********************                                               S01310
     C************IN89*****IFEQ '1'                                       S01310
     C***********RECI******ORNE 'D'                                       S01310
     C************LOCK*****IN   LDA                                       S01310
     C*********************MOVE '01'      DBASE                           S01310
     C*********************MOVE 'FIRST'   DBKEY            * DB ERROR 01 *S01310
     C*********************MOVE 'TABTB10' DBFILE                          S01310
     C*********************OUT  LDA                                       S01310
     C*********************EXSR *PSSR                                     S01310
     C*********************END                                            S01310
      *
      **  Access RUNDAT to find run date & multibranching indicator       S01310
     C           *NAMVAR   DEFN RUNDAT    DSRUN                           S01310
     C                     IN   DSRUN                                     S01310
      *                                                                   S01310
     C                     Z-ADD1         RRN
     C                     SETON                     21
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   FORMAT - Format fields for screen                           *
      *   Called by - main process                                    *
      *****************************************************************
      *
     C           FORMAT    BEGSR
      *
      **  Set up network field
     C           @WK5      IFEQ '{311:'
     C           @WL5      OREQ '{311:'
     C                     MOVE 'STTX '   NTWK
     C                     ELSE
     C                     MOVE 'SWIFT'   NTWK
     C                     END
      *
      **  Set up status field
     C                     MOVE *BLANKS   CODE
     C                     MOVE *BLANKS   TAG
     C           @WK1      IFEQ '0'
     C                     MOVE 'ACCEPTED'STAT
     C                     ELSE
     C                     MOVE 'REJECTED'STAT
     C                     MOVE @CDE      CODE
     C                     MOVE @TAG      TAG
     C                     END
      *
      **  Obtain Message Type, Destination, Message Input Reference and
      **  Message Priority from MSIXA
     C                     MOVE *BLANKS   MSTYP
     C                     MOVE *BLANKS   DEST
     C                     MOVE *BLANKS   PRTY
     C*********************MOVELTRNO      MSMI                            S01310
     C*********************MOVE TRNO      TNRF                            S01310
     C***********IXKY******CHAINMSIXA                11                   S01310
     C           TRNO      CHAINMGOREFL0             11                   S01310
     C           *IN11     IFEQ '0'
     C                     MOVE MTPY      MSTYP
     C                     MOVELNWDS      DEST                            S01310
     C*********************MOVE DTID      DEST                            S01310
      *
     C***********MSGP******IFEQ '01'                                      S01310
     C           MPRY      IFEQ 'U'                                       S01310
     C                     MOVE 'URGENT'  PRTY
     C                     ELSE
     C                     MOVE 'NORMAL'  PRTY
     C                     END
     C                     END
      *
      **  Set up fields for Telex Answerback, if present
     C           @WK5      IFEQ '{311:'
     C                     Z-ADD61        X
     C                     END
      *
     C           @WL5      IFEQ '{311:'
     C                     Z-ADD73        X
     C                     END
      *
     C           @WK5      IFEQ '{311:'
     C           @WL5      OREQ '{311:'
     C                     SETON                     20
     C                     MOVEAMDTA      IMSG
     C                     Z-ADD1         Z
      *
     C           @WK2      DOUEQ'}}'
      *
     C           @WK2      DOUEQCRLF
     C           @WK2      OREQ '}}'
     C                     MOVE IMSG,X    LINE,Z
     C***********          ADD  1         X       40                      CSW095
     C                     ADD  1         X       50                      CSW095
     C                     ADD  1         Z       30
     C                     MOVEAIMSG,X    @WK2    2
     C                     END
      *
     C                     MOVEALINE      RCD
     C                     WRITEMS5105F2
     C                     ADD  1         RRN     40
     C                     MOVE *BLANKS   LINE
      *
     C                     ADD  2         X
     C                     Z-ADD1         Z
     C                     END
     C                     END
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   *PSSR - error handling                                      *
      *   Called by - INIT initial process                            *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
