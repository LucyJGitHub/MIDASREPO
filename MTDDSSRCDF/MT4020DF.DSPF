      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MT Testkey Request by Destination')
      *****************************************************************
      *                                                               *
      *  Midas - TELEX INTERFACE                                      *
      *                                                               *
      *   MT4020 - TESTKEY REQUEST BY DESTINATION                     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. E39162             Date 18Jun92               *
      *                                    Date                       *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  E39162 - CHANGE 'PRINT' TO 'PRINT(MPRINT)'.                  *
      *   RE-WRITTEN FOR SWIFT RATIONALISATION PHASE 2.               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *         /////////////////////////////////////////             *
      *        // WARNING: When creating this display //              *
      *       //   file, take options RSTDSP(*YES)   //               *
      *      //                       SHARE (*YES)  //                *
      *     /////////////////////////////////////////                 *
      *                                                               *
      *  C R E A T I O N     P A R A M E T E R S                      *
     A*****************************************************************
     A*********        START OF CHANGE                     /E39162/
     A*********                             PRINT
     A                                      PRINT(MPRINT)
     A*                END OF CHANGE                       /E39162/
     A                                      CA03
      *
     A          R MT4020F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            USRID         10   O  1  2
     A                                  1 13'MT4020'
     A                                  1 25'TESTKEY REQUEST BY DESTINATION'
     A                                      DSPATR(UL HI)
     A            RUNA           7   O  1 72
     A            WSID          10   O  2  2
     A                                  2 71TIME
      *
     A          R MT4020F2                  OVERLAY
     A  41                             20  2'Branch'
     A  41        SBCH           3   B 20  9
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A                                 20 13'Destination customer'
     A            SCUS          10   B 20 34
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A                                 20 45'or address'
     A            SADR          20   B 20 61
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A                                 21  2'Module'
     A            SMOD           2   B 21  9
     A N34                                  DSPATR(HI)
     A  34                                  DSPATR(RI PC)
     A                                 21 13'Start date'
     A            SDAT           6   B 21 34
     A N35                                  DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A                                 21 45'External system'
     A            SSYS           6   B 21 61
     A N36                                  DSPATR(HI)
     A  36                                  DSPATR(RI PC)
     A            SFMG          78   O 22  2
     A  30                                  DSPATR(RI)
     A                                 23  2'F3-Exit'
      *
     A          R MT4020F3                  OVERLAY
     A                                  3  4'Destination'
     A                                  3 26'Module'
     A                                  3 33'Date'
     A                                  3 40'System'
     A                                  3 47'TRN'
     A                                  3 64'Type'
     A  41                              3 69'Branch'
      *
     A          R MT4020F4                  SFL
     A            SELECT         1   B  4  2
     A            ADDR          10      4  4
     A            DEST          10      4 15
     A            MODI           2      4 28
     A            MGDE           6  0   4 33
     A            SYTM           6      4 40
     A            TRNO          16      4 47
     A            MTPY           3      4 64
     A            BRCA           3      4 69
      *
     A          R MT4020F5                  SFLCTL(MT4020F4)
     A                                      CA12
     A  20                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N20N21                               SFLCLR
     A                                      SFLSIZ(18)
     A                                      SFLPAG(17)
     A  22                                  SFLEND
     A N22                                  ROLLUP(23)
     A                                      OVERLAY
     A            DSPREC         4  0H      SFLRCDNBR
     A            SFMG          78   O 22  2
     A  30                                  DSPATR(RI)
     A                                 23  2'F3-Exit'
     A                                 23 11'F12-Previous'
     A                                 23 25'Rollup/Rolldown'
