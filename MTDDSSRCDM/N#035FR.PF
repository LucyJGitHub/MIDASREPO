      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas MT Network Midas field reference file')
     A****************************************************************
     A*                                                              *
     A* NETWORK MIDAS                                                *
     A* =============                                                *
     A*                                                              *
     A* Release Number : 4.1                                         *
     A*                                                              *
     A* File Title     : Network MIDAS Field Reference File          *
     A*                                                              *
     A* File Name      : N#FR                                        *
     A*                                                              *
     A* File Type      : Physical                                    *
     A*                                                              *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                              *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *   LAST AMENDMENT NO. 00000            DATE 00/00/00          *
     A*                                                              *
     A****************************************************************
     A* Amendment Log  :                                             *
     A*                                                              *
     A* 030184         : LOCT (Location Type)                        *
     A* Judith Hind      list of values amended to include           *
     A*                      WPW - Wang OIS                          *
     A*                      552 - 5520                              *
     A*                                                              *
     A* 270184         : LCAD (5520 Local Address)                   *
     A* Judith Hind      NODE (5520 Node Name)                       *
     A*                      added for 5520 comms.                   *
     A*                                                              *
     A* 100284         : Added for Telex                             *
     A* Judith Hind      AORL, AORS, CNAM, ERRO, RCAB, RTRY,         *
     A*                  SCDE, TXCC, TXDL, TXKY, TXST                *
     A*                                                              *
     A*                : OSTT amended (values)                       *
     A*                  TXNO amended (length)                       *
     A*                                                              *
     A* 270284         : Added for Telex configuration options       *
     A* Judith Hind      TXHD, TXWT, PFLG                            *
     A*                                                              *
     A* 140484         : Added for Telex (rates)                     *
     A* Judith Hind      BAND, COST, CTRY, PERD, SUBS.               *
     A*                                                              *
     A* Sometime       : Added for ITT                               *
     A* Judith Hind      ITAB, ITBC, ITCS, ITPW.                     *
     A*                                                              *
     A* 270984         : Added for Autodial and Dialmode             *
     A* SCG              DIAL                                        *
     A*                                                              *
     A* 171084         : Added for Telex File N#XH (already used on  *
     A* Judith Hind      DEPT                       N#UP)            *
     A*                                                              *
     A* 7/11/84  SCG   : Added NOTICE Fields                         *
     A*                                                              *
     A* 13/12/84       : Added INF Value for LOCT                    *
     A* Mick Grainger                                                *
     A*                                                              *
     A* 26/06/85       : PERD and COST changed for international tlx *
     A* Judith Hind                                                  *
     A*                                                              *
     A* 18/1/86        : Location types TON and DON added            *
     A* Anne Skinner   (Merged by SCG into above change.)            *
     A*                                                              *
     A* 03/7/87        : Fields added for NMITEM file                *
     A* J W Bower      ICDE, INAR, MGRP, PARA, PCMD & UPSCPT.        *
     A*                                                              *
     A* 1/10/87        DEST, TYPE, MCNO, PTSN , RTNO, CMD, CMDLGT    *
     A* J W Bower                                                    *
     A*                                                              *
     A* 06/05/86       : Added HUB Value for LOCT                    *
     A* S.C.Gillings.                                                *
     A*                                                              *
     A* 05/05/88       : Fields added for N#CFA file                 *
     A* S P Curtis     TKIF,DFPO,DNM                                 *
     A*                                                              *
     A****************************************************************
     A*
     A          R N#FR01                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            ACKN           1          COLHDG('Ack./' 'Reply' 'Request')
     A                                      VALUES('A' 'R' ' ')
     A            ACTN           1          COLHDG('Action' 'Ind.')
     A                                      VALUES('1' '2' '3' '4' '5' '6' '7')
     A*                                     1 - Call if EDQ
     A*                                     2 - Call anyway
     A*                                     3 - Call due to EDQ
     A*                                     4 - Defer
     A*                                     5 - Default Defer
     A*                                     6 - ???
     A*
     A            ADDR          10          COLHDG('Address')
     A***************** ******************  CHECK(VN) REMOVE FOR ESL
     A            ADES          20          COLHDG('Address' 'Description')
     A                                      CMP(GT ' ')
     A            AORL           1          COLHDG('Address/' 'Locn' 'Ind')
     A                                      VALUES('1' '2')
     A            AORS           1          COLHDG('Standard' 'Text' 'Ind')
     A                                      VALUES(' ' '1')
     A            AUTO           1          COLHDG('Auto Answer')
     A                                      VALUES('Y' ' ' 'N')
     A            BAND           4          COLHDG('Telex Cost' 'Band')
     A            CACD           3          COLHDG('Calendar' 'Code')
     A                                      CMP(GT ' ')
     A            CIND           1          COLHDG('Call' 'Ind.')
     A                                      VALUES('1' '0' ' ')
     A            CMD          213          COLHDG('Passthrough' 'Command')
     A            CMDLGT        15  5       COLHDG('Command' 'Length')
     A*
     A* 0 - Do Not Call if no Items to Send
     A* 1 - Call even if no Items to Send
     A*
     A            CONI           1          COLHDG('Control' 'Record' 'Ind.')
     A                                      VALUES('1' '0' ' ')
     A            COST           7 03       COLHDG('Telex' 'Cost per' 'Period')
     A            CNAM          30          COLHDG('Company' 'Name')
     A                                      CMP(GT ' ')
     A            CSNO           2 00       COLHDG('Call' 'Schedule' 'Number')
     A            CTRY          30          COLHDG('Telex' 'Country' 'Name')
     A            CURR           1          COLHDG('Current' 'Event')
     A                                      VALUES('Y' 'L' ' ')
     A            DAY            2 00       COLHDG('Day')
     A                                      RANGE(01 31)
     A            DATE           6 00       COLHDG('Date')
     A                                      EDTCDE(Y)
     A            DEPT          10          COLHDG('Department')
     A            DEST          12          COLHDG('Destination')
     A            DFPO          10          COLHDG('DEFAULT PAYT OFFICER')
     A            DIAL           1          COLHDG('Dial Mode')
     A                                      VALUES('M' ' ' 'A')
     A            DNM           10          COLHDG('DEVICE DSCRIPTION')
     A            EACD           6          COLHDG('Logged' 'Event' 'Code')
     A            ECDQ           5 00       COLHDG('Economic' 'Delivery' +
     A                                      '(Records)')
     A            ERRO           2 00       COLHDG('Telex' 'Error' 'Count')
     A            EVTP           2          COLHDG('Event' 'Type')
     A                                      CMP(GT ' ')
     A            EVDESC        30          COLHDG('Event' 'Description')
     A            FREF          10          COLHDG('Filing' 'Reference')
     A            ICDE           4          COLHDG('Item Code')
     A            ICDS           4S00       COLHDG('ITT Sequence No.')
     A            INAR          45          COLHDG('Item Narrative')
     A            IQOQ           1          COLHDG('Inq' 'Outq' 'Indicator')
     A                                      VALUES('I' 'O')
     A            IREV           1          COLHDG('Items to' 'Review' 'Indicat+
     A                                      or')
     A                                      VALUES('1' ' ')
     A            ITAB          15          COLHDG('ITT Signon' 'Answerback')
     A            ITBC           1          COLHDG('ITT Billing' 'Code Flag')
     A            ITCS           3          COLHDG('ITT CSA')
     A*
     A* Applies only to incoming items
     A*
     A*                                     1 - there are unreviewed items
     A*                                     blank - there are no unreviewed item
     A*
     A            ITEM           7 00       COLHDG('Item' 'No.')
     A            ITIT          30          COLHDG('Item' 'Title')
     A
     A            ITPW          15          COLHDG('ITT Signon' 'Password')
     A            ITTD          15          COLHDG('Item' 'Type' 'Description')
     A            ITTP           4          COLHDG('Item' 'Type')
1    A            JOB            6          COLHDG('Job Details-Comms')
     A            KSCH          30          COLHDG('Keyword' 'Search')
     A            LCAD           8          COLHDG('5520' 'Local' 'Address')
     A            LCLS           2          COLHDG('Line' 'Class')
     A            LDES          20          COLHDG('List' 'Description')
     A                                      COMP(GT ' ')
     A            LINE           1          COLHDG('Line Number')
     A            LNAM          10          COLHDG('List' 'Name')
     A                                      COMP(GT ' ')
     A            LOCC           5          COLHDG('Location' 'Code')
     A            LOCD          20          COLHDG('Location' 'Description')
     A                                      COMP(GT ' ')
     A            LOCT           3          COLHDG('Location' 'Type')
     A                                      VALUES('S34' 'S38' 'S36' +
     A                                      'TLX' 'QCM' 'WPD' 'WPW' 'HUB' +
     A                                      '552' 'TXB' 'INF' 'MK3' 'TON' 'DON')
     A            MCNO           2          COLHDG('Machine' 'Number')
     A            MGRP           4 00       COLHDG('Menu Group')
     A            MNTH           2 00       COLHDG('Month')
     A                                      RANGE(01 12)
     A            MREV           1          COLHDG('Module' 'to Review' 'Indica+
     A                                      tor')
     A                                      VALUES('T' 'R' 'C')
     A            NITM           6 00       COLHDG('NOTICE Item No.')
     A            NODE           8          COLHDG('5520' 'Node' 'Name')
     A            NOIT           5 00       COLHDG('Number' 'of' 'Items')
     A            NORC           5 00       COLHDG('Number' 'of' 'Records')
     A            NPW           12A         COLHDG('NOTICE' 'User Pass')
     A            NUSR          20A         COLHDG('NOTICE' 'User ID  ')
     A            OPGM          10          COLHDG('Operative' 'Program')
     A            OSTT           1          COLHDG('Outgoing' 'Status')
     A                                      VALUES(' ' '1' '2' '3' '4' '5' '6' +
     A                                      '7' '8' '9' '0' 'A' 'B' 'C')
     A*                                 blank - not available to de    spatch
     A*                                         monitor
     A*                                     1 - Awaiting Send Time
     A*                                     2 - Available to Send
     A*                                     3 - Locked for Despatch
     A*                                     4 - In Despatch
     A*                                     5 - Despatched
     A*                                     6 - Complete
     A*                                     7 - Held by Originator
     A*                                     8 - Held by Error
     A*                                     9 - Outside Time Constraints
     A*                                     0 - Cancelled (Telex)
     A*                                     A - Held by Telex Operator
     A*                                     B - In Geisco Mailbox
     A*                                     C - In NOTICE Mailbox
     A*
     A            PADD          40          COLHDG('Package' 'Address Line')
     A            PARA          70          COLHDG('Menu Program')
     A            PASS          10          COLHDG('Network' 'Password')
     A            PCMD          20          COLHDG('Program/Command')
     A            PERD           5 02       COLHDG('Telex Period' 'in seconds')
     A            PFLG           1          COLHDG('Print' 'Flag')
     A                                      VALUES('1' '2')
     A            PHON          20          COLHDG('Phone' 'Number')
     A            PPIN           1          COLHDG('Public' '/Private' 'Ind.')
     A                                      VALUES('1' '2')
     A*                                     1 - Public List
     A*                                     2 - Private List
     A*
     A            PRTY           1 00       COLHDG('Priority')
     A                                      RANGE(1 3)
     A            PTSN          10          COLHDG('Passthrough' 'Shortname')
     A            QADD           8          COLHDG('Quikcomm' 'Address')
     A            QGAN          12          COLHDG('Geisco' 'Account' 'Number')
     A            QGPS          12          COLHDG('Geisco' 'Password')
     A            QLTI           8          COLHDG('Geisco' 'LTID')
     A            QOBT           1          COLHDG('Quikcomm' 'Obtain' 'Indic.')
     A                                      VALUES('Y' 'N' ' ')
     A            QPSS          12          COLHDG('Quikcomm' 'Password')
     A            RATE           7  2       COLHDG('Rate' 'Units/' 'Minute')
     A            RCAB          30          COLHDG('Answerback' 'Received')
     A            ROFI           1          COLHDG('Re-route' 'Operator' 'Ind.')
     A                                      VALUES('1' '0' ' ')
     A            RTNO           2S 0       COLHDG('Route' 'Number')
     A            RTRY           1          COLHDG('Telex' 'Retry' 'Flag')
     A                                      VALUES('R' 'H' ' ')
     A            SDES          20          COLHDG('Archive' 'Reference' +
     A                                      'Description')
     A            SCDE           6          COLHDG('Standard' 'Text' 'Code')
     A            SNNO           5  0       COLHDG('Session' 'Number')
     A            SOFI           1          COLHDG('System' 'Operator' 'Ind.')
     A                                      VALUES('2' '1' '0' ' ')
     A*                                     2 - Full System Op. Function
     A*                                     1 - Comms. Control only
     A*
     A            SPIN          60          COLHDG('Special' 'Instructions')
     A            SQNO           7 00       COLHDG('Sequence' 'Number')
     A            SREF          10          COLHDG('Archive' 'Reference')
     A            SUBS           8          COLHDG('Telex' 'Dial Code' 'Subset')
     A            TIME           4 00       COLHDG('Time')
     A                                      EDTWRD('  :  ')
     A            TKIF           1          COLHDG('TESTKEY INDICATOR')
     A            TORC           1          COLHDG('To or CC')
     A                                      VALUES('1' '2')
     A            TXAB          10          COLHDG('Telex' 'Answerback')
     A            TXCC           5          COLHDG('Telex' 'Country' 'Code')
     A            TXDL          30          COLHDG('Telexbox' 'Format' +
     A                                      'Dial Code')
     A            TXHD          40          COLHDG('Telex' 'Heading' 'Name')
     A            TXKY           3          COLHDG('Telex' 'Operator' +
     A                                      'KEY+ Code')
     A            TXNO          10          COLHDG('Telex' 'Number')
     A            TXST           2          COLHDG('Telexbox' 'Reply at' +
     A                                      'Last try')
     A            TXWT           1 00       COLHDG('Telex' 'Wait' 'Time')
     A                                      RANGE(0 9)
     A            TYPE           3          COLHDG('Machine''Type')
     A            UPSCPT         1          COLHDG('Screen Pass Thru')
     A            WSTN          10          COLHDG('Workstation')
     A            XRAT           5 00       COLHDG('Transmission' 'Rate' +
     A                                      '(Recs./min)')
     A            XSTT           1          COLHDG('Transmission' 'Status')
     A                                      VALUES(' ' '1' '2' '3' '4' '5' '6' +
     A                                            '7' '8' '9')
     A*                                     ' ' - Not available to N.M.
     A*                                     1   - Available to N.M.
     A*                                     2   - In Transmission
     A*                                     3   - Awaiting Receive
     A*                                     4   - In Receive
     A*                                     5   - Permanent Error
     A*                                     6   - Recoverable Error
     A*                                     7   - Awaiting Transmit
     A*                                     8   - In Recovery
     A*                                     9   - In Cancel
     A            YEAR           2 00       COLHDG('Year')
     A                                      RANGE(83 90)
     A            ZONE         144          COLHDG('Time' 'Zone' 'to Xmit')
