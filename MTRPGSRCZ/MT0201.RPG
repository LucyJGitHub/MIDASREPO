     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MT Drop N#MO interface file records')
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  MT0201  - Drop Historic Messages from Midas-Orion            *
      *            Interface file (for Mailer/Telex Ver. 3.3)         *
      *                                                               *
      *  Function:  This program physically deletes Telex messages    *
      *             from Midas-Orion interface file N#MO after        *
      *             checking that the telex history retention date    *
      *             in Midas outgoing message file MGOREFPD has       *
      *             been reached.                                     *
      *                                                               *
      *  Called By: MEC0200 - Drop Historic Messages                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CMT001 *CREATE     Date 02NOV98               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CMT001 - Program created based on MT0200 for Midas Mailer/   *
      *           Telex Version 3.3 which now uses Orion logical      *
      *           file N$MO3 (previously N$MO)                        *
      *                                                               *
      *****************************************************************
      /EJECT
      *
     FSDBANKPDIF  E                    DISK
     FMGOREFL1IF  E           K        DISK
     FN$MO3   UF  E           K        DISK
      *
     E                    CPY@    1   1 80
     E* Copy point for ZDATE1 subroutine E specs
     E/COPY ZSRSRC,ZDATE1Z1
      *
      * ID C  F  H  L    Function of indicators
      *    10            EOF MGOREFPD
      *    U7U8LR        DB error
      *
      *
      ** Data area to re-format date
      *
     I            DS
     I                                        1   60HRFORM
     I                                        1   20HRYY
     I                                        5   60HRDD
     I                                        7  120ZDATE
     I                                        7   80ZDD
     I                                       11  120ZYY
      *
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      **  Local data area data structure
      *
      *****************************************************************
      *                   Index to subroutines                        *
      *   MAIN  - main process                                        *
      *   INIT  - initial process                                     *
      *   DELMO - delete Midas-Orion interface file record            *
      *   *PSSR - error handling                                      *
      *****************************************************************
     C/EJECT
      *****************************************************************
      *   MAIN - Main process                                         *
      *   Calls -  INIT initial process                               *
      *            DELMO - delete Midas-Orion interface file record   *
      *****************************************************************
      *
      **  Perform initial process
     C                     EXSR INIT
      *
      **  Read multiple headers & single messages
     C                     READ MGOREFL1                 10
      *
     C           *IN10     DOWEQ'0'
      *
      *
      **  Change History Retention Date to Midas day no. format
      *
     C                     MOVE HRDT      HRFORM
     C                     MOVE HRDT      ZDATE
     C                     MOVE HRDD      ZDD
     C                     MOVE HRYY      ZYY
     C                     MOVE '0'       *IN98
     C                     EXSR ZDATE1
     C                     Z-ADDZDAYNO    MHRDT   50
      *
      **  If before retention date, delete from Midas-Orion i/f file
     C           MHRDT     IFLT BJRDNB
      *
     C                     EXSR DELMO
     C                     END
      *
      **  Read next multiple header or single message
     C                     READ MGOREFL1                 10
     C                     END
      *
     C                     SETON                     LR
     C/EJECT
      *****************************************************************
      *   INIT - Inital process                                       *
      *   Called by main process                                      *
      *****************************************************************
      *
     C           INIT      BEGSR
     C                     MOVEACPY@      BIS@   80
      *
      **  Set up LDA
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'MT0201'  DBPGM
     C                     MOVE *BLANKS   DBASE
     C                     OUT  LDA
      *
      **  Access SDBANKPD for date format
     C           1         CHAINSDBANKPD             87
      *
     C           *IN87     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE
     C                     MOVEL'FIRST'   DBKEY            * DB ERROR 01 *
     C                     MOVEL'SDBANKPD'DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   DELMO - Delete Telex messages from Midas-Orion i/f file     *
      *   Called by - main process                                    *
      *****************************************************************
      *
     C           DELMO     BEGSR
      *
      **  If Telex message to be dropped, delete record from
      **  Midas-Orion interface file
     C                     MOVELTRNO      @KEY   15
     C           @KEY      CHAINN#MO01               40
     C           *IN40     IFEQ '0'
     C                     DELETN#MO01
     C                     ENDIF
      *
     C                     ENDSR
      *
     C/EJECT
     C/COPY ZSRSRC,ZDATE1Z2
     C/EJECT
      *****************************************************************
      *   *PSSR - error handling                                      *
      *   Called by - INIT initial process                            *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
     C* ZYDY & ZMDY INSERTED BY FIX 111008                                111008
** CPY@   **      Object copyright
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
