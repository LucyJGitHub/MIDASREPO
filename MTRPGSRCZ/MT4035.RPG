     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas MT Reset pending testkey messages')
      *****************************************************************
      *                                                               *
      *  Midas - Midas Testkey Module                                 *
      *                                                               *
      *  MT4035 - Reset Pending Testkey Messages                      *
      *                                                               *
      *  Function:  This program resets the Testkey Status field      *
      *             for Telex messages which has been submitted       *
      *             for Automated BISTKO testkey calculations.        *
      *                                                               *
      *  (phase(s)) I/C                                               *
      *                                                               *
      *  Called By: MTC4035 - Reset Pending Testkey Messages          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 TDA035             Date 02Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CDL008             Date 02May00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 170748  *CREATE    Date 01Aug00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE026 - Consumer Banking                                    *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  TDA035 - RTS Signon changes for MidasPlus. (Recompile)       *
      *   CDL008 - Continuous Linked Settlement (Recompiled MGOREFPD) *
      *  170748 - Incorrect testkey are calculated for multiple       *
      *           users using Automated BISTKO calculation. This      *
      *           program ensures that those messages are reset       *
      *           for re-submission in cases where the data are       *
      *           erroneously left in the shared folder and where     *
      *           testkey are not properly generated.                 *
      *                                                               *
      *****************************************************************
      *
     FMGOREFL0UF  E           K        DISK
     FMUSER   IF  E           K        DISK
      *
      * ID C  F  H  L    Function of indicators
      *    05            EOF MGOREFPD
      *    06            EOF MUSER
      *    U7,U8         DB Error
      *
     E                    CPY@    1   1 80
      *
     ILDA         DS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      **  Local data area data structure
      *
     IPSDS       SDS
     I                                      244 253 WSID
     I                                      254 263 USRID
      **  Workstation ID
      *
     IDSRUN       DS                             13
     I                                        1   7 RUNA
      **  RUNDAT data area
      *
      *****************************************************************
      *                   Index to subroutines                        *
      *   MAIN  - main process                                        *
      *   INIT  - initial processing                                  *
      *   *PSSR - error handling                                      *
      *****************************************************************
     C/EJECT
      *****************************************************************
      *   MAIN - Main process                                         *
      *   Calls -  INIT initial process                               *
      *****************************************************************
     C           *ENTRY    PLIST
     C                     PARM           AUTH    1
      *
      **  Perform initial process
      *
     C                     EXSR INIT
      *
      **  Allow resetting of Testkey status if the user has
      **  Routing Officer authority as defined in MUSERDD.
      *
     C           ROUF      IFEQ 'Y'
      *
      ** Change passed parameter to indicate that user is a Routing
      ** Officer
      *
     C                     MOVE 'Y'       AUTH
      *
      ** Change all the messages to show ready for testkey
     C                     READ MGOREFD0                 05
     C           *IN05     DOWEQ'0'
      *
      ** Only reset messages if Testkey Status (TSKS) is 'R'.
      ** Otherwise, skip to the next record.
      *
     C           TSKS      IFEQ 'R'
     C                     MOVE 'Y'       TSKS
     C                     UPDATMGOREFD0
     C                     ENDIF
      *
     C                     READ MGOREFD0                 05
     C                     END
      *
      **  Indicate to the CL that the user has not got
      **  Routing Officer authority
     C                     ELSE
     C                     MOVE 'N'       AUTH
     C                     END
      *
     C                     SETON                     LR
     C/EJECT
      *****************************************************************
      *   INIT - Inital process                                       *
      *   Called by main process                                      *
      *****************************************************************
      *
     C           INIT      BEGSR
      *
     C                     MOVEACPY@      BIS@   80
      *
      **  Set up LDA
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'MT4030'  DBPGM
     C                     MOVE *BLANKS   DBASE
     C                     OUT  LDA
      *
      **  Access RUNDAT to find run date
     C           *NAMVAR   DEFN RUNDAT    DSRUN
     C           *LOCK     IN   DSRUN
     C                     OUT  DSRUN
      *
      ** Access MUSER for user's authority (Routing officer ?)
      *
     C           USRID     CHAINMUSER                89
      *
     C           *IN89     IFEQ '1'
     C           RECI      ORNE 'D'
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE
     C                     MOVELUSRID     DBKEY
     C                     MOVEL'MUSER'   DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      *   *PSSR - Error handling                                      *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C                     SETON                     U7U8LR
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR
      *
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
