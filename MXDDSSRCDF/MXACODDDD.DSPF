      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MX Account codes for export definition')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  DSPF/MXACODDD - Account Codes For Export Definition          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 190783             Date 15Apr01               *
      *                 CMX001  *CREATE    Data 01Jan00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  190783 - Allow definition of a default 'include indicator'   *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A  31
     AO 32                                  SFLNXTCHG
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A**Start of change CGL029
     A**********  A5ACCD         4   H      TEXT('Account Code')                              CGL029
     A            A5ACCD        10   H      TEXT('Account Code')
     A**End of change CGL029
     A            A5ICUR         1   H      TEXT('Inc. in Current')
     A            A5IACT         1   H      TEXT('Inc. in Activity')
     A            A5ANC1         4   H      TEXT('Analysis Code 1')
     A            A5ANC2         4   H      TEXT('Analysis Code 2')
     A**Start of change CGL029
     A**********  DDACCD         4A  B  8  5                                                  CGL029
     A            DDACCD        10A  B  8  2
     A**End of change CGL029
     A                                      DSPATR(PR HI)
     A  30                                  DSPATR(RI)
     A**Start of change CGL029
     A**********  DDACCN        25A  B  8 11                                                  CGL029
     A            DDACCN        25A  B  8 13
     A**End of change CGL029
     A                                      DSPATR(PR HI)
     A            DDICUR         1A  B  8 39
     A  31                                  DSPATR(RI)
     A  33
     AO 31                                  DSPATR(PC)
     A N31                                  DSPATR(HI UL)
     A            DDIACT         1A  B  8 43
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(HI UL)
     A            DDANC1         4A  B  8 47
     A                                      DSPATR(HI UL)
     A            DDANC2         4A  B  8 52
     A                                      DSPATR(HI UL)
     A            DDACTY         1A  B  8 59DSPATR(PR)
     A                                      DSPATR(HI)
     A            DDACST         1A  B  8 66DSPATR(PR)
     A                                      DSPATR(HI)
     A            DDACSC         2A  B  8 74DSPATR(PR)
     A                                      DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'MXACODDEF'
     A                                  1 28'Account Codes for Export +
     A                                      Definition'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
      *                                                     /*Start 190783*/
     A                                  2 28'Composite:'
     A            DDCOMI         8   O  2 39DSPATR(HI)
     A                                  2 49'Default Include:'
     A            DPDINC         1   B  2 66DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                      OVRDTA
      *                                                       /*End 190783*/
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  3 35'Include In'
     A                                  3 47'Analysis'
     A**Start of change CGL029
     A**********                        4  5'Account'                                         CGL029
     A                                  4  2'Account'
     A**End of change CGL029
     A                                  4 15'Name'
     A                                  4 38'Export:'
     A                                  4 47'  Codes '
     A                                  4 58'Acc  Account  Account'
     A**Start of change CGL029
     A**********                        5  6'Code  '                                          CGL029
     A                                  5  3'Code  '
     A**End of change CGL029
     A                                  5 38'Cur Act'
     A                                  5 48' 1    2'
     A                                  5 58'Typ  Sub-type Section'
     A**Start of change CGL029
     A**********  DPACCD         4A  B  6  5DSPATR(HI UL)                                     CGL029
     A            DPACCD        10A  B  6  2DSPATR(HI UL)
     A**End of change CGL029
     A                                      OVRDTA
     A**Start of change CGL029
     A**********  DPACCN        25A  B  6 11DSPATR(HI UL)                                     CGL029
     A            DPACCN        25A  B  6 13DSPATR(HI UL)
     A**End of change CGL029
     A                                      OVRDTA
     A            DPICUR         1A  B  6 39DSPATR(HI UL)
     A                                      OVRDTA
     A            DPIACT         1A  B  6 43DSPATR(HI UL)
     A                                      OVRDTA
     A            DPANC1         4A  B  6 47DSPATR(HI UL)
     A                                      OVRDTA
     A            DPANC2         4A  B  6 52DSPATR(HI UL)
     A                                      OVRDTA
     A            DPACTY         1A  B  6 59DSPATR(HI UL)
     A                                      OVRDTA
     A            DPACST         1A  B  6 66DSPATR(HI UL)
     A                                      OVRDTA
     A            DPACSC         2A  B  6 74DSPATR(HI UL)
     A                                      OVRDTA
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit  Rollup  Rolldown'
