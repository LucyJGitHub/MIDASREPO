      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MX Branches for export definition')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  DSPF/MXBRCADD - Branches For Export Definition               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. 190783             Date 15Apr01               *
      *                         *CREATE    Date                       *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  190783 - Allow definition of a default 'include indicator'   *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A  31
     AO 32                                  SFLNXTCHG
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            A8BRCD         3   H      TEXT('Branch Code')
     A            A8ICUR         1   H      TEXT('Inc. in Current')
     A            A8IACT         1   H      TEXT('Inc. in Activity')
     A            A8ANC1         4   H      TEXT('Analysis Code 1')
     A            A8ANC2         4   H      TEXT('Analysis Code 2')
     A            DDBRCD         3A  B  8  5
     A                                      DSPATR(PR HI)
     A  30                                  DSPATR(RI)
     A            DDBRNM        30A  B  8 11
     A                                      DSPATR(PR HI)
     A            DDICUR         1A  B  8 44
     A  31                                  DSPATR(RI)
     A  33
     AO 31                                  DSPATR(PC)
     A N31                                  DSPATR(HI UL)
     A            DDIACT         1A  B  8 48
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(HI UL)
     A            DDANC1         4A  B  8 52
     A                                      DSPATR(HI UL)
     A            DDANC2         4A  B  8 57
     A                                      DSPATR(HI UL)
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'MXBRCADEF'
     A                                  1 31'BRANCHES FOR EXPORT +
     A                                      DEFINITION'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
      *                                                     /*Start 190783*/
     A                                  2 28'Composite:'
     A            DDCOMI         8   O  2 39DSPATR(HI)
     A                                  2 49'Default Include:'
     A            DPDINC         1   B  2 66DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                      OVRDTA
      *                                                       /*End 190783*/
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  3 40'Include In'
     A                                  3 52'Analysis'
     A                                  4  5'Branch'
     A                                  4 15'Name'
     A                                  4 43'Export:'
     A                                  4 52'  Codes '
     A                                  5  6'Code  '
     A                                  5 43'Cur Act'
     A                                  5 53' 1    2'
     A            DPBRCD         3A  B  6  5DSPATR(HI UL)
     A                                      OVRDTA
     A            DPBRNM        30A  B  6 11DSPATR(HI UL)
     A                                      OVRDTA
     A            DPICUR         1A  B  6 44DSPATR(HI UL)
     A                                      OVRDTA
     A            DPIACT         1A  B  6 48DSPATR(HI UL)
     A                                      OVRDTA
     A            DPANC1         4A  B  6 52DSPATR(HI UL)
     A                                      OVRDTA
     A            DPANC2         4A  B  6 57DSPATR(HI UL)
     A                                      OVRDTA
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit  Rollup  Rolldown'
