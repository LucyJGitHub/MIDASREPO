      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MX Bulk export of composites')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  DSPF/MXBULKCDD - Bulk Export of Composites                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CMX001  *CREATE    Data 01Jan00               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            DDACTN         1A  I  6  2DSPATR(HI UL)
     A            DDSDES        42A  B  6  4DSPATR(HI PR)
     A                                  6 47'EXT' DSPATR(HI)
     A            DDLDTX         7A  O  6 51DSPATR(HI)
     A            CDLETX         8A  O  6 59DSPATR(HI)
     A            DDNOTX         7A  O  6 68DSPATR(HI)
     A            DDNTSX         4A  O  6 76DSPATR(HI)
     A            DDLIN2        44A  O  7  2DSPATR(HI UL)
     A                                  7 47'TRM' DSPATR(HI UL)
     A            DDLDTT         7A  O  7 51DSPATR(HI UL)
     A            CDLETT         8A  O  7 59DSPATR(HI UL)
     A            DDNOTT         7A  O  7 68DSPATR(HI UL)
     A            DDNTST         4A  O  7 76DSPATR(HI UL)
     A            CDCOMI         8A  H
     A            CDEXTT         1A  H
     A            CDDESC        42A  H
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0008)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'MXBULKECO'
     A                                  1 30'BULK EXPORT OF COMPOSITES'
     A                                      DSPATR(HI UL)
     A                                  1 56 '-'
     A                                      DSPATR(HI)
     A N70                              1 58'''CURRENT'''
     A  70                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      OVRATR
     A  70                              1 58'''ACTIVITY'''
     A N70                                  DSPATR(ND)
     A                                      DSPATR(HI)
     A                                      OVRATR
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  3 50'  Last    End   '
     A                                  3 69'Cmps  Cmps '
     A                                  4  4'Description'
     A                                  4 30'Next Export Date'
     A                                  4 50'  Date    Time  '
     A                                  4 69'Prcd  /Sec '
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit  Rollup  Rolldown'
     A                                      OVRATR
