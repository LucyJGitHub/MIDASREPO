      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MX Bulk export prompt')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  DSPF/MXBULKEDD - Bulk Export Prompt                          *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CMX001  *CREATE    Data 01Jan00               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      CF03
     A                                      CF12
      *
      * -------------------------------------------------------------------*
      *   MAIN DETAILS                                                     *
      * -------------------------------------------------------------------*
      *
     A          R MAIN                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'MXBULKEPR'
     A                                  1 31'BULK EXPORTS PROMPT'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  8 12'Do you want to export -
     A                                      the following type of data :'
     A            I#DESC        42A  O 10 12DSPATR(HI)
     A                                      OVRDTA
     A                                 12 12'Input Y or N:'
     A  70                                  DSPATR(ND)
     A                                      OVRATR
     A            DDEXPT         1A  B 12 27DSPATR(HI UL)
     A  70                                  DSPATR(PR ND)
     A                                      VALUES('Y' 'N')
     A                                      OVRDTA
      *
      * -------------------------------------------------------------------*
      *   CONFIRMATION                                                     *
      * -------------------------------------------------------------------*
      *
     A          R CONFIRM
     A                                      PUTOVR
     A                                 13  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 16 12'This export has been submitted -
     A                                      for execution.'
     A                                 18 12'Press enter to return to the -
     A                                      previous screen'
     A                                 24  2'                                  -
     A                                                                         -
     A                                             '
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit  F12=Cancel               -
     A                                         '
     A                                      OVRATR
