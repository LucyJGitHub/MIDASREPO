      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MX Composite message formats maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  DSPF/MXCOMFMDD - Composite Message Formats Maintenance       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CMX001  *CREATE    Date 01Jan00               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A  61                                  CF06
     A  61                                  CF10
     A                                      CF12
     A                                      CF16
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            WHFLDI        10A  O 12  5DSPATR(HI)
     A            DDMFNM        38A  O 12 16DSPATR(HI)
     A            DDATRB        11A  O 12 55DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      OVRATR
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'MXCOMFMNT'
     A  61                              1 30'COMPOSITE MESSAGE FORMATS MAINTEN-
     A                                      ANCE'
     A N61                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A N61                              1 30'COMPOSITE MESSAGE FORMATS ENQUIRY'
     A  61                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  2'Composite ID'
     A            CFCOMI         8A  B  4 17DSPATR(HI)
     A                                      OVRDTA
     A                                      DSPATR(PR)
     A                                  4 27'Extract Type'
     A            CFEXTT         1A  B  4 42DSPATR(HI)
     A                                      OVRDTA
     A                                      DSPATR(PR)
     A                                  5  2'Sequence in Message'
     A            CFSEQM         2A  B  5 22DSPATR(HI)
     A                                      OVRDTA
     A N10 60 61                            DSPATR(UL)
     A  10                                  DSPATR(RI PC)
     A N60
     AON61                                  DSPATR(PR)
     A                                  6  2'Format'
     A            CFFMT         10A  B  6 22DSPATR(HI)
     A                                      OVRDTA
     A N11 60 61                            DSPATR(UL)
     A  11                                  DSPATR(RI PC)
     A N60
     AON61                                  DSPATR(PR)
     A                                  6 33'?'
     A N60
     AON61                                  DSPATR(ND)
     A                                  6 35'Processing Sequence'
     A            CFPRSQ         2A  B  6 55DSPATR(HI)
     A                                      OVRDTA
     A N12 60 61                            DSPATR(UL)
     A  12                                  DSPATR(RI PC)
     A N60
     AON61                                  DSPATR(PR)
     A                                  7  2'Description'
     A            CFDESC        42A  B  7 22DSPATR(HI)
     A                                      OVRDTA
     A N13 61                               DSPATR(UL)
     A  13                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  7 65'Multiple?'
     A            CFMULT         1A  B  7 75DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                  8  2'Export Y/N'
     A            CFEXPT         1A  B  8 22DSPATR(HI)
     A                                      OVRDTA
     A N15 61                               DSPATR(UL)
     A  15                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  8 27'MQ Series File Type'
     A            CFFILT         1A  B  8 47DSPATR(HI)
     A                                      OVRDTA
     A N16 61                               DSPATR(UL)
     A  16                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  8 51'Optimize File Type'
     A            CFOPTF         1A  B  8 71DSPATR(HI)
     A                                      OVRDTA
     A N17 61                               DSPATR(UL)
     A  17                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  9  2'Link-To Proc. Seq.'
     A            CFLPRS         2A  B  9 22DSPATR(HI)
     A                                      OVRDTA
     A N18 61                               DSPATR(UL)
     A  18                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  9 25'?'
     A N61                                  DSPATR(ND)
     A            CFLFMT        10A  B  9 27DSPATR(HI)
     A                                      DSPATR(PR)
     A                                      OVRDTA
     A                                  9 51'Show Link-To Fields'
     A            DDSHLF         1A  B  9 71DSPATR(HI)
     A                                      OVRDTA
     A N19 61                               DSPATR(UL)
     A  19                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                 11  2'   Code       Field Name          -
     A                                                         Attributes      -
     A                                             '
     A                                      DSPATR(UL)
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A  61                             24  2'F3=Exit  F6=Update  F10=Delete  -
     A                                      F12=Cancel  F16=Text  Rollup  -
     A                                      Rolldown'
     A N61                             24  2'F3=Exit  F12=Cancel  F16-
     A                                      =Text  Rollup  Rolldown'
     A                                      OVRATR
