      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MX Composite ID Prompt')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  DSPF/MXCOMIPDD - Composite ID Prompt                         *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CMX003  *CREATE    Data 05Nov00               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CMX003 - Meridian Export Enhancements - Phase 2              *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MXCOMIS1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MXCOMIS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      SFLCTL(MXCOMIS1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R MXCOMIP1                  TEXT('MAIN DISPLAY SCREEN')
     A                                      CF03
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'MXCOMIPRM'
     A                                  2 25'COMPOSITE ID PROMPT'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A*
     A                                  5 10'Composite ID:'
     A            DDCOMI         8A  B  5 24OVRDTA
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI UL)
     A                                  7 24'Leave blank or input composite ID'
     A                                  8 24'Press enter to continue or F3 to-
     A                                       end'
      *
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R MXCOMIF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                 24  1'F3=Exit'
