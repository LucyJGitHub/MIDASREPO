      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MX Data group details maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  DSPF/MXDGDTMDD - Data Group Details Maintenance              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CMX001  *CREATE    Date 01Jan00               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A  61
     AO 62                                  CF06
     A  61                                  CF09
     A  61                                  CF10
     A                                      CF12
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            DDACTN         1A  I 11  2DSPATR(HI UL)
     A            DECATC         2A  O 11  6DSPATR(HI)
     A            DEDESC        30A  O 11 12DSPATR(HI)
     A            DETRIG         1A  O 11 43DSPATR(HI)
     A            DEACKY        20A  O 11 49DSPATR(HI)
     A            DESNM1         4A  O 11 70DSPATR(HI)
     A            DESNM2         4A  O 11 75DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      OVRATR
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'MXDGDTMNT'
     A  61
     AO 62                              1 30'DATA GROUP DETAILS MAINTENANCE'
     A N61N62                               DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A N61N62                           1 32'DATA GROUP DETAILS ENQUIRY'
     A  61
     AO 62                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  2'Data Group'
     A            DGDTAG         4A  B  4 17DSPATR(HI)
     A                                      OVRDTA
     A N10 60 61                            DSPATR(UL)
     A  10                                  DSPATR(RI PC)
     A N60
     AON61                                  DSPATR(PR)
     A                                  5  2'Description'
     A            DGDESC        42A  B  5 17DSPATR(HI)
     A                                      OVRDTA
     A N11 61                               DSPATR(UL)
     A  11                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  6  2'Account Key Events?'
     A            DGAKEV         1A  B  6 23DSPATR(HI)
     A                                      OVRDTA
     A N12 61                               DSPATR(UL)
     A  12                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  6 27'1 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 -
     A                                      8 9 0'
     A                                  7  2'Account Key Template'
     A            DGAK01         1A  B  7 27DSPATR(HI)
     A                                      OVRDTA
     A N13 61                               DSPATR(UL)
     A  13                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK02         1A  B  7 29DSPATR(HI)
     A                                      OVRDTA
     A N14 61                               DSPATR(UL)
     A  14                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK03         1A  B  7 31DSPATR(HI)
     A                                      OVRDTA
     A N15 61                               DSPATR(UL)
     A  15                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK04         1A  B  7 33DSPATR(HI)
     A                                      OVRDTA
     A N16 61                               DSPATR(UL)
     A  16                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK05         1A  B  7 35DSPATR(HI)
     A                                      OVRDTA
     A N17 61                               DSPATR(UL)
     A  17                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK06         1A  B  7 37DSPATR(HI)
     A                                      OVRDTA
     A N18 61                               DSPATR(UL)
     A  18                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK07         1A  B  7 39DSPATR(HI)
     A                                      OVRDTA
     A N19 61                               DSPATR(UL)
     A  19                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK08         1A  B  7 41DSPATR(HI)
     A                                      OVRDTA
     A N20 61                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK09         1A  B  7 43DSPATR(HI)
     A                                      OVRDTA
     A N21 61                               DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK10         1A  B  7 45DSPATR(HI)
     A                                      OVRDTA
     A N22 61                               DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK11         1A  B  7 47DSPATR(HI)
     A                                      OVRDTA
     A N23 61                               DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK12         1A  B  7 49DSPATR(HI)
     A                                      OVRDTA
     A N24 61                               DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK13         1A  B  7 51DSPATR(HI)
     A                                      OVRDTA
     A N25 61                               DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK14         1A  B  7 53DSPATR(HI)
     A                                      OVRDTA
     A N26 61                               DSPATR(UL)
     A  26                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK15         1A  B  7 55DSPATR(HI)
     A                                      OVRDTA
     A N27 61                               DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK16         1A  B  7 57DSPATR(HI)
     A                                      OVRDTA
     A N28 61                               DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK17         1A  B  7 59DSPATR(HI)
     A                                      OVRDTA
     A N29 61                               DSPATR(UL)
     A  29                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK18         1A  B  7 61DSPATR(HI)
     A                                      OVRDTA
     A N30 61                               DSPATR(UL)
     A  30                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK19         1A  B  7 63DSPATR(HI)
     A                                      OVRDTA
     A N31 61                               DSPATR(UL)
     A  31                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A            DGAK20         1A  B  7 65DSPATR(HI)
     A                                      OVRDTA
     A N32 61                               DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  8  2'No of Months Balances'
     A            DGNOMB         1A  B  8 27DSPATR(HI)
     A                                      OVRDTA
     A N33 61                               DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  9  3'Cat.Code'
     A                                  9 13'Description'
     A                                  9 40'Trigger'
     A                                  9 50'Account Key'
     A                                  9 70'Shortnames'
     A                                 10  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A  61                             24  2'F3=Exit  F6=Update  F9=Add  +
     A                                      F10=Delete  F12=Cancel  +
     A                                      Rollup  Rolldown'
     A N61                             24  2'F3=Exit  F12=Cancel  -
     A                                      Rollup  Rolldown'
     A                                      OVRATR
