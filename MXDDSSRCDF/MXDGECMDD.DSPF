      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MX Data group event categories maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  DSPF/MXDGECMDD - Data Group Event Categories Maintenance     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CMX001  *CREATE    Date 01Jan00               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A  61
     AO 62                                  CF06
     A  61                                  CF10
     A                                      CF12
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DETAILS                                                     *
      * -------------------------------------------------------------------*
      *
     A          R DETAILS
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'MXDGECMNT'
     A  61
     AO 62                              1 27'DATA GROUP EVENT CATEGORIES MAINTE-
     A                                      NANCE'
     A N61N62                               DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A N61N62                           1 29'DATA GROUP EVENT CATEGORIES ENQUIR-
     A                                      Y'
     A  61
     AO 62                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  2'Data Group'
     A            DEDTAG         4A  B  4 17DSPATR(HI)
     A                                      OVRDTA
     A                                      DSPATR(PR)
     A                                  5  2'Category Code'
     A            DECATC         2A  B  5 17DSPATR(HI)
     A                                      OVRDTA
     A N10 60 61                            DSPATR(UL)
     A  10                                  DSPATR(RI PC)
     A N60
     AON61                                  DSPATR(PR)
     A                                  7  2'Description'
     A            DEDESC        30A  B  7 17DSPATR(HI)
     A                                      OVRDTA
     A N11 61                               DSPATR(UL)
     A  11                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  9  2'Trigger?'
     A            DETRIG         1A  B  9 17DSPATR(HI)
     A                                      OVRDTA
     A N12 62                               DSPATR(UL)
     A  12                                  DSPATR(RI PC)
     A N62                                  DSPATR(PR)
     A                                 11  2'Account Key'
     A            DEACKY        20A  B 11 17DSPATR(HI)
     A                                      OVRDTA
     A N13 61                               DSPATR(UL)
     A  13                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                 13  2'Shortname -1'
     A            DESNM1         4A  B 13 17DSPATR(HI)
     A                                      OVRDTA
     A N14 62                               DSPATR(UL)
     A  14                                  DSPATR(RI PC)
     A N62                                  DSPATR(PR)
     A                                 15  2'Shortname -2'
     A            DESNM2         4A  B 15 17DSPATR(HI)
     A                                      OVRDTA
     A N15 62                               DSPATR(UL)
     A  15                                  DSPATR(RI PC)
     A N62                                  DSPATR(PR)
     A                                 17  2'Accumulation'
     A                                 18  2'Method'
     A            DEACUM         1A  B 17 17DSPATR(HI)
     A                                      OVRDTA
     A N16 62                               DSPATR(UL)
     A  16                                  DSPATR(RI PC)
     A N62                                  DSPATR(PR)
     A                                 19  2'Capital'
     A                                 20  2'Change'
     A            DECAPI         1A  B 19 17DSPATR(HI)
     A                                      OVRDTA
     A N17 62                               DSPATR(UL)
     A  17                                  DSPATR(RI PC)
     A N62                                  DSPATR(PR)
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A  61                             24  2'F3=Exit  F6=Update  F10=Delete  +
     A                                      F12=Cancel'
     A                                      OVRATR
     A N61N62                          24  2'F3=Exit  F12=Cancel'
     A                                      OVRATR
     A N61 62                          24  2'F3=Exit  F6=Update  F12=Cancel'
     A                                      OVRATR
