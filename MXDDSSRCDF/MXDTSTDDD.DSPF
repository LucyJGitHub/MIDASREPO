      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MX Deal type/sub-types for export definition')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  DSPF/MXDTSTDD - Deal Type/Sub Types For Export Definition    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CDL038             Date 10May05               *
      *  Prev Amend No. 190783             Date 15Apr01               *
      *                 CMX001  *CREATE    Data 01Jan00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  190783 - Allow definition of a default 'include indicator'   *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A  31
     AO 32                                  SFLNXTCHG
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            DTDTPE         2   H      TEXT('Deal Type')
     A            DTDLST         2   H      TEXT('Deal Sub Type')
     A            DTICUR         1   H      TEXT('Inc. in Current')
     A            DTIACT         1   H      TEXT('Inc. in Activity')
     A            DTANC1         4   H      TEXT('Analysis Code 1')
     A            DTANC2         4   H      TEXT('Analysis Code 2')
     A            DDDTPE         2A  B  8  5
     A                                      DSPATR(PR HI)
     A  30                                  DSPATR(RI)
     A            DDDLST         2A  B  8  8
     A                                      DSPATR(PR HI)
     A  30                                  DSPATR(RI)
     A            DDNARR        35A  B  8 11
     A                                      DSPATR(PR HI)
     A            DDICUR         1A  B  8 49
     A  31                                  DSPATR(RI)
     A  33
     AO 31                                  DSPATR(PC)
     A N31                                  DSPATR(HI UL)
     A            DDIACT         1A  B  8 53
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(HI UL)
     A            DDANC1         4A  B  8 57
     A                                      DSPATR(HI UL)
     A            DDANC2         4A  B  8 62
     A                                      DSPATR(HI UL)
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'MXDTSTDEF'
     A                                  1 28'DEAL TYPES/SUB TYPES FOR +
     A                                      EXPORT DEFINITION'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
      *                                                     /*Start 190783*/
     A                                  2 28'Composite:'
     A            DDCOMI         8   O  2 39DSPATR(HI)
     A                                  2 49'Default Include:'
     A            DPDINC         1   B  2 66DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                      OVRDTA
      *                                                       /*End 190783*/
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  3  5'Deal'
     A                                  3 45'Include In'
     A                                  3 57'Analysis'
     A                                  4  5'Type/'
     A                                  4 15'Narrative'
     A                                  4 48'Export:'
     A                                  4 57'  Codes '
     A                                  5  5'Sub Type'
     A                                  5 48'Cur Act'
     A                                  5 58' 1    2'
     A            DPDTPE         2A  B  6  5DSPATR(HI UL)
     A                                      OVRDTA
     A            DPDLST         2A  B  6  8DSPATR(HI UL)
     A                                      OVRDTA
     A            DPNARR        35A  B  6 11DSPATR(HI UL)
     A                                      OVRDTA
     A            DPICUR         1A  B  6 49DSPATR(HI UL)
     A                                      OVRDTA
     A            DPIACT         1A  B  6 53DSPATR(HI UL)
     A                                      OVRDTA
     A            DPANC1         4A  B  6 57DSPATR(HI UL)
     A                                      OVRDTA
     A            DPANC2         4A  B  6 62DSPATR(HI UL)
     A                                      OVRDTA
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit  Rollup  Rolldown'
