      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MX Export control information definition')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  DSPF/MXEXCTDDD - Input Export Control Information            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CMX001  *CREATE    Data 01Jan00               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MXEXCTS1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MXEXCTS0                  TEXT('MSG SUBFILE CONTRL')
     A                                      CF03
     A                                      SFLCTL(MXEXCTS1)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DISPLAY SCREEN                                              *
      * -------------------------------------------------------------------*
      *
     A          R MXEXCTD1                  TEXT('MAIN DISPLAY SCREEN')
     A                                      CF03
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'MXEXCTDCT'
     A                                  2 25'EXPORT CONTROL INFORMATION'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A*
     A                                  4 10'Current End of Month Date:'
     A            DDCEOM         7A  O  4 47OVRDTA
     A                                      DSPATR(HI)
     A                                  4 58'*CUREOM'
     A                                  5 10'Current Start of Month Date:'
     A            DDCSOM         7A  O  5 47OVRDTA
     A                                      DSPATR(HI)
     A                                  5 58'*CURSOM'
     A                                  6 10'Previous Start of Month Date:'
     A            DDPSOM         7A  O  6 47OVRDTA
     A                                      DSPATR(HI)
     A                                  6 58'*PRVSOM'
     A                                  7 10'Current Run Date:'
     A            DDCRUN         7A  O  7 47OVRDTA
     A                                      DSPATR(HI)
     A                                  7 58'*CURRUN'
     A                                  8 10'Current Next Working Day:'
     A            DDCNWD         7A  O  8 47OVRDTA
     A                                      DSPATR(HI)
     A                                  8 58'*CURNWD'
     A                                  9 10'Previous Run Date:'
     A            DDPRUN         7A  O  9 47OVRDTA
     A                                      DSPATR(HI)
     A                                  9 58'*PRVRUN'
     A                                 11 10'Trigger Start of Month Processing:'
     A            DDTSOM         1A  B 11 47OVRDTA
     A  10                                  DSPATR(RI PC)
     A N10                                  DSPATR(HI UL)
     A                                 12 10'Trace Drip-Feed Extract:'
     A            DDTRDX         1A  B 12 47OVRDTA
     A  11                                  DSPATR(RI PC)
     A N11                                  DSPATR(HI UL)
     A                                 13 10'Trace Drip-Feed Transmission:'
     A            DDTRDT         1A  B 13 47OVRDTA
     A  12                                  DSPATR(RI PC)
     A N12                                  DSPATR(HI UL)
     A                                 14 10'Trace Bulk Extract:'
     A            DDTRBX         1A  B 14 47OVRDTA
     A  13                                  DSPATR(RI PC)
     A N13                                  DSPATR(HI UL)
     A                                 15 10'Trace Bulk Transmission:'
     A            DDTRBT         1A  B 15 47OVRDTA
     A  14                                  DSPATR(RI PC)
     A N14                                  DSPATR(HI UL)
     A                                 16 10'Diagnose Events:'
     A            DDDIAG         1A  B 16 47OVRDTA
     A  15                                  DSPATR(RI PC)
     A N15                                  DSPATR(HI UL)
     A                                 17 10'Maintain Retail Accruals:'
     A            DDMRAC         1A  B 17 47OVRDTA
     A  16                                  DSPATR(RI PC)
     A N16                                  DSPATR(HI UL)
     A                                 18 47'DL'
     A                                 18 52'GL'
     A                                 18 57'LE'
     A                                 18 62'RE'
     A                                 18 67'SE'
     A                                 19 10'Save Data Frequency:'
     A            DDSFDL         1A  B 19 47OVRDTA
     A  17                                  DSPATR(RI PC)
     A N17                                  DSPATR(HI UL)
     A            DDSFGL         1A  B 19 52OVRDTA
     A  18                                  DSPATR(RI PC)
     A N18                                  DSPATR(HI UL)
     A            DDSFLE         1A  B 19 57OVRDTA
     A  19                                  DSPATR(RI PC)
     A N19                                  DSPATR(HI UL)
     A            DDSFRE         1A  B 19 62OVRDTA
     A  20                                  DSPATR(RI PC)
     A N20                                  DSPATR(HI UL)
     A            DDSFSE         1A  B 19 67OVRDTA
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(HI UL)
      *
      * -------------------------------------------------------------------*
      * * MAIN SCREEN FOOTER                                               *
      * -------------------------------------------------------------------*
      *
     A          R MXEXCTF1                  TEXT('SCREEN FOOTER')
     A                                      CF03
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                 24  1'F3=Exit'
