      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MX Export Format Details Copy')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  DSPF/MXEXFDCDD - Export Format Details Copy                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CMX001  *CREATE    Data 01Jan00               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A                                      CF06
     A                                      CF12
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DETAILS                                                     *
      * -------------------------------------------------------------------*
      *
     A          R DETAILS
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'MXEXFDCPY'
     A                                  1 30'EXPORT FORMAT DETAILS COPY'
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  3  2 'FROM'
     A                                  4  2'File'
     A            DDFFIL        10A  O  4 16DSPATR(HI)
     A                                      OVRDTA
     A                                  4 30'Sequence'
     A            DDFFSQ         1A  O  4 40DSPATR(HI)
     A                                      OVRDTA
     A                                  4 49'Format'
     A            DDFFMT        10A  O  4 56DSPATR(HI)
     A                                      OVRDTA
     A                                  6  2 'TO  '
     A                                  7  2'File'
     A            DDTFIL        10A  O  7 16DSPATR(HI)
     A                                      OVRDTA
     A                                  7 30'Sequence'
     A            DDTFSQ         1A  B  7 40DSPATR(HI)
     A                                      OVRDTA
     A N11                                  DSPATR(UL)
     A  11                                  DSPATR(RI PC)
     A                                  7 49'Format'
     A            DDTFMT        10A  B  7 56DSPATR(HI)
     A                                      OVRDTA
     A N12                                  DSPATR(UL)
     A  12                                  DSPATR(RI PC)
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit  F6=Update, F12=Cancel'
