      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MX Export format details maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  DSPF/MXEXFDMDD - Export Format Details Maintenance           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CMX001  *CREATE    Data 01Jan00               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A  61                                  CF06
     A                                      CF08
     A  61                                  CF10
     A                                      CF12
     A                                      CF16
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            DDACTN         1A  I 14  2DSPATR(HI UL)
     A            DDFLDI         9A  O 14  4
     A  62                                  DSPATR(HI)
     A            DDMFNM        38A  O 14 14
     A  62                                  DSPATR(HI)
     A            DDATRB        11A  O 14 53
     A  62                                  DSPATR(HI)
     A            WHINEX         1A  O 14 67
     A  62                                  DSPATR(HI)
     A            WHIBO          5S 0O 14 73
     A  62                                  DSPATR(HI)
     A            WHFILE        10A  H
     A            WHFSQN         1A  H
     A            WHFOBO         5S 0H
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0008)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      OVRATR
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'MXEXFDMNT'
     A  61                              1 30'EXPORT FORMAT DETAILS MAINTENANCE'
     A N61                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A N61                              1 32'EXPORT FORMAT DETAILS ENQUIRY'
     A  61                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  2'File'
     A            EXFILE        10A  B  4 16DSPATR(HI)
     A                                      OVRDTA
     A N10 60 61                            DSPATR(UL)
     A  10                                  DSPATR(RI PC)
     A N60
     AON61                                  DSPATR(PR)
     A                                  4 30'Sequence'
     A            EXFSQN         1A  B  4 40DSPATR(HI)
     A                                      OVRDTA
     A N11 60 61                            DSPATR(UL)
     A  11                                  DSPATR(RI PC)
     A N60
     AON61                                  DSPATR(PR)
     A            DDSELC        10A  O  4 45DSPATR(HI BL)
     A                                      OVRDTA
     A                                  5  2'Description'
     A            EXDESC        42A  B  5 16DSPATR(HI)
     A                                      OVRDTA
     A N12 61                               DSPATR(UL)
     A  12                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  5 59'Format'
     A            EXFMT         10A  B  5 66DSPATR(HI)
     A                                      OVRDTA
     A N13 60 61                            DSPATR(UL)
     A  13                                  DSPATR(RI PC)
     A N60
     AON61                                  DSPATR(PR)
     A                                  6  2'Message Queue'
     A            EXMSGQ        48A  B  6 16DSPATR(HI)
     A                                      OVRDTA
     A N14 61                               DSPATR(UL)
     A  14                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  6 65'Drip-Feed'
     A            EXDRIP         1A  B  6 75DSPATR(HI)
     A                                      OVRDTA
     A N15 61                               DSPATR(UL)
     A  15                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  7  2'Recs Per MQ Message'
     A            DDRECM         3A  B  7 22DSPATR(HI)
     A                                      OVRDTA
     A N16 61                               DSPATR(UL)
     A  16                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  7 27'Log Performance'
     A            EXLOGP         1A  B  7 43DSPATR(HI)
     A                                      OVRDTA
     A N17 61                               DSPATR(UL)
     A  17                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  7 45'Message Transmission Report Type'
     A            EXMTRT         1A  B  7 78DSPATR(HI)
     A                                      OVRDTA
     A N18 61                               DSPATR(UL)
     A  18                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  8  2'Export Frequency'
     A            EXEXFQ         1A  B  8 22DSPATR(HI)
     A                                      OVRDTA
     A N19 61                               DSPATR(UL)
     A  19                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  8 26'Export Day'
     A            DDEXDY         2A  B  8 42DSPATR(HI)
     A                                      OVRDTA
     A N20 61                               DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  8 47'Next Export Date'
     A            DDEXND         6A  B  8 66DSPATR(HI)
     A                                      OVRDTA
     A N21 61                               DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  9  2'Export Member'
     A            DDXMEM         5A  B  9 20DSPATR(HI)
     A                                      OVRDTA
     A N22 61                               DSPATR(UL)
     A  22                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                 10  2'Access Module'
     A            EXACSM        10A  B 10 20DSPATR(HI)
     A                                      OVRDTA
     A N23 61                               DSPATR(UL)
     A  23                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                 10 31'Access Module No. of Parameters'
     A            DDACSP         1A  B 10 63DSPATR(HI)
     A                                      OVRDTA
     A N24 61                               DSPATR(UL)
     A  24                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                 10 65'Multiple'
     A            EXACSX         1A  B 10 74DSPATR(HI)
     A                                      OVRDTA
     A N25 61                               DSPATR(UL)
     A  25                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                 11  2'Access Through:'
     A            EXACST        60A  B 11 20DSPATR(HI)
     A                                      OVRDTA
     A N26 61                               DSPATR(UL)
     A  26                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                 12  2'Create Fields'
     A            DDCRFD         1A  B 12 20DSPATR(HI)
     A                                      OVRDTA
     A N27 61                               DSPATR(UL)
     A  27                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                 13  2'  Code      Field Name            -
     A                                                      Attributes  Export -
     A                                      Sequence'
     A                                      DSPATR(UL)
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A  61                             24  2'F3=Exit  F6=Update  F8=Select  F1-
     A                                      0=Delete  F12=Cancel  F16=Text  Rol-
     A                                      lup/down'
     A N61                             24  2'F3=Exit  F8=Select  F12=Cancel  -
     A                                      F16=Text  Rollup  Rolldown'
     A                                      OVRATR
