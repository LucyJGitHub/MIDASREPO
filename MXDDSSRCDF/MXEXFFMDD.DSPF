      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MX Export format fields maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  DSPF/MXEXFFMDD - Export Format Fields Maintenance            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CMX001  *CREATE    Data 01Jan00               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A  61                                  CF06
     A                                      CF12
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MAIN DETAILS                                                     *
      * -------------------------------------------------------------------*
      *
     A          R DETAILS
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'MXEXFFMNT'
     A  61                              1 27'EXPORT FORMAT FIELDS MAINTENANCE'
     A N61                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A N61                              1 29'EXPORT FORMAT FIELDS ENQUIRY'
     A  61                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  2'File Format'
     A            WHFILE        10A  B  4 24DSPATR(HI PR)
     A                                      OVRDTA
     A                                  4 36'Field Name'
     A            WHFLDI        10A  B  4 47DSPATR(HI PR)
     A                                      OVRDTA
     A                                  5  2'Field Text'
     A            WHFTXT        50A  B  5 24DSPATR(HI PR)
     A                                      OVRDTA
     A                                  6  2'Meridian Field Name'
     A            DDMFNM        29A  B  6 24DSPATR(HI)
     A                                      CHECK(LC)
     A                                      OVRDTA
     A N11 61                               DSPATR(UL)
     A  11                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  7  2'Include in Export'
     A            WHINEX         1A  B  7 24DSPATR(HI)
     A                                      OVRDTA
     A N12 61                               DSPATR(UL)
     A  12                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  8  2'Sequence'
     A            DDIBO          5A  B  8 24DSPATR(HI)
     A                                      OVRDTA
     A N13 61                               DSPATR(UL)
     A  13                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                  9  2'Sequence in Reference'
     A            WHSREF         1A  B  9 24DSPATR(HI)
     A                                      OVRDTA
     A N14 61                               DSPATR(UL)
     A  14                                  DSPATR(RI PC)
     A N61                                  DSPATR(PR)
     A                                 10  2'Description:'
     A            WHDS01        78A  B 11  2DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            WHDS02        78A  B 12  2DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            WHDS03        78A  B 13  2DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            WHDS04        78A  B 14  2DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            WHDS05        78A  B 15  2DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            WHDS06        78A  B 16  2DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            WHDS07        78A  B 17  2DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            WHDS08        78A  B 18  2DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            WHDS09        78A  B 19  2DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            WHDS10        78A  B 20  2DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
     A            WHDS11        78A  B 21  2DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A                                      OVRDTA
     A                                      CHECK(LC)
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A N61                             24  2'F3=Exit  F12=Cancel'
     A                                      OVRATR
     A  61                             24  2'F3=Exit  F6=Update  F12=Cancel'
     A                                      OVRATR
