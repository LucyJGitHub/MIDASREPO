      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MX Export format selection criteria maint')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  DSPF/MXEXFSMNT - Export Format Selection Criteria Maintenance*
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CMX001  *CREATE    Data 01Jan00               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A  61                                  CF06
     A                                      CF12
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCTL                 TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD 1                                                 *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLRE1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            DDSIAO         3A  B  6  5DSPATR(HI)
     A N41                                  DSPATR(UL)
     A  41                                  DSPATR(PC RI)
     A            DDSFLD        10A  B  6 13DSPATR(HI)
     A N42                                  DSPATR(UL)
     A  42                                  DSPATR(PC RI)
     A            DDSCMP         2A  B  6 28DSPATR(HI)
     A N43                                  DSPATR(UL)
     A  43                                  DSPATR(PC RI)
     A            DDSCPV        10A  B  6 38DSPATR(HI)
     A N44                                  DSPATR(UL)
     A  44                                  DSPATR(PC RI)
     A            WHFOBO         5S 0H
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL 1                                                *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCT1                  SFLCTL(SUBFLRE1)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLSIZ(0900)
     A                                      SFLPAG(0009)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A  03                                  SFLEND
     A                                      ROLLUP(05)
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'MXEXFSMNT'
     A  61                              1 30'FORMAT SELECTION CRITERIA MAINTENA-
     A                                      NCE'
     A N61                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A N61                              1 32'FORMAT SELECTION CRITERIA ENQUIRY'
     A  61                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  2'Format'
     A            EXFMT         10A  O  4 14
     A                                      DSPATR(HI)
     A            EXDESC        42A  O  4 25
     A                                      DSPATR(HI)
     A                                  5  2'IF/AND/OR'
     A                                  5 14'FIELD'
     A                                  5 25'COMPARISON'
     A                                  5 38'COMPARISON VALUE'
     A            PRRN1          4  0H      SFLRCDNBR(CURSOR)
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD 2                                                 *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLRE2                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            DDFLDI         9A  O 16  4DSPATR(HI)
     A            DDMFNM        38A  O 16 14DSPATR(HI)
     A            DDATRB        11A  O 16 53DSPATR(HI)
     A            WHINEX         1A  O 16 67DSPATR(HI)
     A            WHIBO          5S 0O 16 73DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL 2                                                *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCT2                  SFLCTL(SUBFLRE2)
     A                                      SFLSIZ(0600)
     A                                      SFLPAG(0006)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  11                                  SFLDSP
     A  12                                  SFLDSPCTL
     A N11N12                               SFLCLR
     A  13                                  SFLEND
     A                                 15  2'  Code      Field Name            -
     A                                                      Attributes  Export -
     A                                      Sequence'
     A                                      DSPATR(UL)
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A  61                             24  2'F3=Exit  F6=Update  F12=Cancel  -
     A                                      Rollup  Rolldown'
     A N61                             24  2'F3=Exit  F12=Cancel  -
     A                                      Rollup  Rolldown'
     A                                      OVRATR
