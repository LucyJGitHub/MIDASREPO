      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SE Depot movements maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - System Control                                       *
      *                                                               *
      *  DSPF/MXGENMDNDF - Midas MX Generate Meridian sub-message     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG2617  *CREATE   Date 20May04               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG2617- Conversion of MXGENMDN to comply with webfacing.    *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CF03(03)
     A                                      PRINT(MPRINT)
      *
     A          R MXGENMDNF0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            USER          10A  O  1  2TEXT('*USER')
     A                                      OVRDTA
     A                                  1 20'Export a Format to +
     A                                       Meridian (MXGENMNDN)'
     A                                      DSPATR(HI)
     A            DATE           6Y 0O  1 72TEXT('*DATE')
     A                                      EDTWRD('  /  /  ')
     A                                      OVRDTA
     A            WSID          10   O  2  2
     A            RUNA      R        O  2 55
     A                                  2 72TIME
      *
     A          R MXGENMDNF1
      *
     A                                      OVERLAY
     A                                  5  2'Format to Export'
     A            FMTNAM        10A  B  5 36TEXT('Stream file')
     A  91                                  DSPATR(RI)
     A  92                                  DSPATR(PC)
     A                                  5 50'*Name'
     A                                  6  2'Meridian (Sub)Message name'
     A            MDNNAM        32A  B  6 36TEXT('Meridian (Sub)Message name')
     A                                  7  2'Stream file to write to'
     A            STRMFL        50A  B  7 36TEXT('Streamfile')
     A                                  9  2'Prefix for field names'
     A            PREFLD        10A  B  9 36TEXT('Prefix field names')
     A                                  9 50'*Name, *DFT, *FROMDBF'
     A                                 10  2'Gen. message or sub-message'
     A            SUBMSG        11A  B 10 36TEXT('Gen./Sub. message')
     A                                 10 50'*MESSAGE, *SUBMESSAGE'
     A                                 11  2'Type Match'
     A            TYPMTC         1A  B 11 36TEXT('Type Match')
     A                                 11 50'Name'
      *
     A          R MXGENMDNF2
      *
     A                                      OVERLAY
     A                                 23  3'F3=End'
     A            ERRMSG        75A    24  2TEXT('Error Message')
     A                                      DSPATR(HI)
      *
