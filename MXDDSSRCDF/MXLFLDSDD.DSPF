      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas MX Link fields selection')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  DSPF/MXLFLDSDD - Export Format Details Maintenance           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CMX001  *CREATE    Data 01Jan00               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A  61                                  CF06
     A  61                                  CF10
     A                                      CF12
     A                                      CF16
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            DDACTN         1A  B 12  2DSPATR(HI)
     A  61                                  DSPATR(UL)
     A N61                                  DSPATR(PR)
     A                                      VALUES(' ' '1' '2' '3' '4' '5' '6')
     A            DDFLDI        10A  O 12  5
     A  62N63                               DSPATR(HI)
     A  63                                  DSPATR(RI)
     A            DDMFNM        38A  O 12 16
     A  62N63                               DSPATR(HI)
     A  63                                  DSPATR(RI)
     A            DDATRB        11A  O 12 55
     A  62N63                               DSPATR(HI)
     A  63                                  DSPATR(RI)
     A            WHINEX         1A  O 12 72
     A  62N63                               DSPATR(HI)
     A  63                                  DSPATR(RI)
     A            WHFILE        10A  H
     A            WHFSQN         1A  H
     A            WHIBO          5S 0H
     A            WHFOBO         5S 0H
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0010)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      OVRATR
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'MXLFLDSEL'
     A  61                              1 30'LINK FIELDS SELECTION'
     A N61                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A N61                              1 32'LINK FIELDS ENQUIRY'
     A  61                                  DSPATR(ND)
     A                                      OVRATR
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  2'Composite ID'
     A            CFCOMI         8A  O  4 17DSPATR(HI)
     A                                      OVRDTA
     A                                  4 27'Extract Type'
     A            CFEXTT         1A  O  4 42DSPATR(HI)
     A                                      OVRDTA
     A                                  5  2'Sequence in Message'
     A            CFSEQM         2A  O  5 22DSPATR(HI)
     A                                      OVRDTA
     A                                  6  2'Format'
     A            CFFMT         10A  O  6 22DSPATR(HI)
     A                                      OVRDTA
     A                                  6 35'Processing Sequence'
     A            CFPRSQ         2A  O  6 55DSPATR(HI)
     A                                      OVRDTA
     A                                  7  2'Description'
     A            CFDESC        42A  O  7 22DSPATR(HI)
     A                                      OVRDTA
     A                                  7 65'Multiple?'
     A            CFMULT         1A  O  7 75DSPATR(HI)
     A                                      OVRDTA
     A                                  8  2'Export Y/N'
     A            CFEXPT         1A  O  8 22DSPATR(HI)
     A                                      OVRDTA
     A                                  8 27'MQ Series File Type'
     A            CFFILT         1A  O  8 47DSPATR(HI)
     A                                      OVRDTA
     A                                  8 51'Optimize File Type'
     A            CFOPTF         1A  O  8 71DSPATR(HI)
     A                                      OVRDTA
     A                                  9  2'Link-To Proc. Seq.'
     A            CFLPRS         2A  O  9 22DSPATR(HI)
     A                                      OVRDTA
     A            CFLFMT        10A  O  9 27DSPATR(HI)
     A                                      OVRDTA
     A            DDDESC        42A  O  9 38DSPATR(HI)
     A                                      OVRDTA
     A                                 10  2'Link-To Fields'
     A            DDACST        60A  O 10 17DSPATR(HI RI)
     A                                      OVRDTA
     A                                 11  2'   Code       Field Name          -
     A                                                         Attributes     E-
     A                                      xport  '
     A                                      DSPATR(UL)
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A  61                             24  2'F3=Exit  F6=Update  F12-Cancel  -
     A                                      Rollup  Rolldown'
     A N61                             24  2'F3=Exit  F12-Cancel  -
     A                                      Rollup  Rolldown'
     A                                      OVRATR
