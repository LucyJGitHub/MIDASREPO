      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas MX Composite Message Details')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  PF/MXCOMDPD - Composite Message Details                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      *   If the attributes of this file are changed then the         *
      *   associated BRIDGE upgrade utilities must also be amended.   *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. BUG17937           Date 24Apr08               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01.02 ----------------------------------------*
      *  Prev Amend No. CPK015             Date 28Oct02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CMX001  *CREATE    Date 01Jan00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG17937 - SEC3300 is Failing with an error.  Applied fix    *
      *             245268                                            *
      *  CPK015 - Make the file uniquely keyed.                       *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
      * Start of CPK015
     A                                      UNIQUE
      * End of CPK015
     A                                      REF(MFRFL)
     A          R MXCOMDP0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            CDCOMI         8A
     A                                      TEXT('Composite ID')
                                            COLHDG('Comp' 'ID')
     A            CDEXTT         1A
     A                                      TEXT('Extract Type')
                                            COLHDG('Extract' 'Type')
     A            CDDESC        42A
     A                                      TEXT('Description')
                                            COLHDG('Description')
     A            CDDTAG         4A
     A                                      TEXT('Data Group')
                                            COLHDG('Data' 'Group')
     A            CDMSGT        32A
     A                                      TEXT('Message Type')
                                            COLHDG('Msg' 'Type')
     A            CDMSGQ        48A
     A                                      TEXT('Message Queue Name')
                                            COLHDG('Msg' 'Queue' 'Name')
     A            CDCOMM         3S 0
     A                                      TEXT('COMs Per MQ Msg')
                                            COLHDG('COMs' 'Per' 'MQ Msg')
     A            CDLOGP         1A
     A                                      TEXT('Log Performance Y/N')
                                            COLHDG('Log' 'Perf')
     A            CDMTRT         1A
     A                                      TEXT('Message Trans. Report Type')
                                            COLHDG('Msg' 'Trm' 'Rpt Typ')
     A            CDEXFQ         1A
     A                                      TEXT('Export Frequency')
                                            COLHDG('Exp' 'Freq')
     A            CDEXDY         2S 0
     A                                      TEXT('Export Day')
                                            COLHDG('Exp' 'Day')
     A            CDEXND         5S 0
     A                                      TEXT('Next Export Date')
                                            COLHDG('Next' 'Exp' 'Date')
     A            CDENAB         1A
     A                                      TEXT('Enabled')
                                            COLHDG('Enab')
 
      * EXTRACT STATISTICS
 
     A            CDLDTX         5S 0
     A                                      TEXT('Last Date of Extract')
                                            COLHDG('Last' 'Ext' 'Date')
     A            CDLSTX         8A
     A                                      TEXT('Last Start Time of Extract')
                                            COLHDG('Last' 'Str' 'Time')
     A            CDLETX         8A
     A                                      TEXT('Last End Time of Extract')
                                            COLHDG('Last' 'End' 'Time')
     A            CDNOTX         7S 0
     A                                      TEXT('Number of COMs - Ext')
                                            COLHDG('COMs' 'Ext')
     A            CDNTSX         4S 0
     A                                      TEXT('Number of COMs/second - Ext')
                                            COLHDG('COMs/Sec' 'Ext')
     A            CDXCTL         1S 0
     A                                      TEXT('Extract control total')
                                            COLHDG('Ext' 'Ctl' 'Tot')
     A            CDXMEM        10A
     A                                      TEXT('Extract Member')
                                            COLHDG('Extract' 'Member')
 
      * TRANSMISSION STATISTICS
 
     A            CDLDTT         5S 0
     A                                      TEXT('Last Date of Transmission')
                                            COLHDG('Last' 'Trm' 'Date')
     A            CDLSTT         8A
     A                                      TEXT('Last Start Time of Transm')
                                            COLHDG('Last' 'Str' 'Time')
     A            CDLETT         8A
     A                                      TEXT('Last End Time of Transm')
                                            COLHDG('Last' 'End' 'Time')
     A            CDNOTT         7S 0
     A                                      TEXT('Number of COMs - Trm')
                                            COLHDG('COMs' 'Trm')
     A            CDNTST         4S 0
     A                                      TEXT('Number of COMs/second - Trm')
                                            COLHDG('COMs/Sec' 'Trm')
     A            CDTCTL         1S 0
     A                                      TEXT('Transmission control total')
                                            COLHDG('Trm' 'Ctl' 'Tot')
      **Start of change BUG17937
     A            CDQMGR        48A
     A                                      TEXT('Message Queue Manager')
     A                                      COLHDG('Msg' 'Queue' 'Manager')
      **End of change BUG17937
      *
     A          K CDCOMI
     A          K CDEXTT
