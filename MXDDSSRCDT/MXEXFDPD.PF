      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas MX Export Format Details')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  PF/MXEXFDPD - Export Format Details                          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CMX001  *CREATE    Date 01Jan00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R MXEXFDP0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            EXFILE        10A
                                            TEXT('File')
                                            COLHDG('File')
     A            EXFSQN         1A
                                            TEXT('File Sequence')
                                            COLHDG('File' 'Seq')
     A            EXDESC        42A
                                            TEXT('Description')
                                            COLHDG('Description')
     A            EXFMT         10A
                                            TEXT('Format')
                                            COLHDG('Format')
     A            EXMSGQ        48A
                                            TEXT('Message Queue Name')
                                            COLHDG('Msg' 'Queue' 'Name')
     A            EXDRIP         1A
                                            TEXT('Drip-feed')
                                            COLHDG('Drip-Feed')
     A            EXRECM         3S 0
     A                                      TEXT('Recs Per MQ Msg')
                                            COLHDG('Recs' 'Per' 'MQ Msg')
     A            EXLOGP         1A
     A                                      TEXT('Log Performance Y/N')
                                            COLHDG('Log' 'Perf')
     A            EXMTRT         1A
     A                                      TEXT('Message Trans. Report Type')
                                            COLHDG('Msg' 'Trm' 'Rpt Typ')
     A            EXEXFQ         1A
     A                                      TEXT('Export Frequency')
                                            COLHDG('Exp' 'Freq')
     A            EXEXDY         2S 0
     A                                      TEXT('Export Day')
                                            COLHDG('Exp' 'Day')
     A            EXEXND         5S 0
     A                                      TEXT('Next Export Date')
                                            COLHDG('Next' 'Exp' 'Date')
 
     A            EXACSM        10A
     A                                      TEXT('Access Module')
                                            COLHDG('Access' 'Module')
 
     A            EXACSP         1S 0
     A                                      TEXT('Access Module NoofParameters')
                                            COLHDG('Access' 'Mod' 'No.ofParms')
 
     A            EXACSX         1A
     A                                      TEXT('Access Multiple')
                                            COLHDG('Access' 'Multiple')
 
     A            EXACST        60A
     A                                      TEXT('Access Through')
                                            COLHDG('Access' 'Through')
 
     A            EXFDCR         1A
                                            TEXT('Fields created')
                                            COLHDG('Fields' 'Created')
     A            EXENAB         1A
     A                                      TEXT('Enabled')
                                            COLHDG('Enab')
 
      * EXTRACT STATISTICS
 
     A            EXLDTX         5S 0
     A                                      TEXT('Last Date of Extract')
                                            COLHDG('Last' 'Ext' 'Date')
     A            EXLSTX         8A
     A                                      TEXT('Last Start Time of Extract')
                                            COLHDG('Last' 'Str' 'Time')
     A            EXLETX         8A
     A                                      TEXT('Last End Time of Extract')
                                            COLHDG('Last' 'End' 'Time')
     A            EXNOTX         7S 0
     A                                      TEXT('Number of Recs - Ext')
                                            COLHDG('Recs' 'Ext')
     A            EXNTSX         4S 0
     A                                      TEXT('Number of Recs/second - Ext')
                                            COLHDG('Recs/Sec' 'Ext')
     A            EXXCTL         1S 0
     A                                      TEXT('Extract control total')
                                            COLHDG('Ext' 'Ctl' 'Tot')
     A            EXXMEM        10A
     A                                      TEXT('Extract Member')
                                            COLHDG('Extract' 'Member')
 
      * TRANSMISSION STATISTICS
 
     A            EXLDTT         5S 0
     A                                      TEXT('Last Date of Transmission')
                                            COLHDG('Last' 'Trm' 'Date')
     A            EXLSTT         8A
     A                                      TEXT('Last Start Time of Transm')
                                            COLHDG('Last' 'Str' 'Time')
     A            EXLETT         8A
     A                                      TEXT('Last End Time of Transm')
                                            COLHDG('Last' 'End' 'Time')
     A            EXNOTT         7S 0
     A                                      TEXT('Number of Recs - Trm')
                                            COLHDG('Recs' 'Trm')
     A            EXNTST         4S 0
     A                                      TEXT('Number of Recs/second - Trm')
                                            COLHDG('Recs/Sec' 'Trm')
     A            EXTCTL         1S 0
     A                                      TEXT('Transmission control total')
                                            COLHDG('Trm' 'Ctl' 'Tot')
     A          K EXFILE
     A          K EXFSQN
