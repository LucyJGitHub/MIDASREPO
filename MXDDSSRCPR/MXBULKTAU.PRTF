      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas MX Bulk Transmission Audit Report')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  PRTF/MXBULKTAU - Bulk Transmission Audit Report              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CMX001  *CREATE    Data 01Jan00               *
      *  Prev Amend No.                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R PAGEHEAD                   SKIPB(1)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2000')
     A                                     1 'REF.MXBULKTRM'
     A            BJURPT        53        37
     A                                   101 'DATE'
     A            BJMRDT         7       106
     A                                   119 'PAGE'
     A            PAGNUM         3  0    125 EDTCDE(Z) SPACEA(1)
     A            #TIME          6S 0      1EDTWRD('  :  :  ')
     A                                    40 'BULK TRANSMISSION REPORT -'
     A            ZZDESC        42A       72
     A                                       SPACEA(1)
     A                                    40 '------------------------'
     A  50                                72 '* ACTIVITY *'
     A N50                                72 '* CURRENT * '
     A                                   132 '.'
     A                                       SPACEA(1)
     A                                     1'----+----1----+----2'
     A                                    21'----+----3----+----4'
     A                                    41'----+----5----+----6'
     A                                    61'----+----7----+----8'
     A                                    81'----+----9----+----0'
     A                                   108'End Position'
      *
     A          R ENDOFMSG                   SPACEB(1)
     A                                     1'----+----1----+----2'
     A                                    21'----+----3----+----4'
     A                                    41'----+----5----+----6'
     A                                    61'----+----7----+----8'
     A                                    81'----+----9----+----0'
      *
     A          R SPACEFMT                   SPACEB(1)
     A                                   132'.'
      *
     A          R STROFFMT                   SPACEB(1)
     A                                     1'Format    '
     A                                    19'End Position'
     A                                    40'Occurrences'
     A                                    55'Cumulative End Position'
      *
     A          R FMT                        SPACEB(1)
     A            #FMT          10         1
     A            #EPOS          4  0     23EDTWRD('  0 ')
     A            #OCUR          4  0     42EDTWRD('  0 ')
     A            CUM_EPOS       6  0     63EDTWRD('    0 ')
      *
     A          R ENDOFFMT                   SPACEB(1)
     A                                     1'========================'
     A                                    25'========================'
     A                                    49'========================'
     A                                    73'========================'
     A                                    97'========================'
     A                                   121'==========='
      *
     A          R REFER                      SPACEB(1)
     A            MQREF         40A        1
      *
     A          R MSG                        SPACEB(1)
     A            MSGDTA       100A        1
     A            PRT_EPOS       6S 0    110EDTWRD('    0 ')
      *
     A          R ENDOFREP                   SPACEB(6)
     A                                    40'NUMBER OF INPUT RECORDS:  '
     A            REC_CNT        6S 0     67EDTWRD('    0 ')
     A                                      SPACEA(2)
     A                                    40'NUMBER OF INPUT COMS:     '
     A            COM_CNT        6  0     67EDTWRD('    0 ')
     A                                      SPACEA(2)
     A                                    40'NUMBER OF OUTPUT MESSAGES:'
     A            MSG_CNT        6  0     67EDTWRD('    0 ')
