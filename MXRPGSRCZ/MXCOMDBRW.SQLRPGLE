     H DEBUG
     H COPYRIGHT('(c) Finastra International Limited 2002')
      *****************************************************************
/**** *  RPGBASEMOD                                                   *          MD056612
/*STD *  RPGSQLMOD                                                    *          MD056612
/*EXI *  CLOSQLCSR(*ENDMOD)                                           *          MD056612
/*EXI *  TEXT('Midas MX Composite Message Details Browse')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  MXCOMDBRW - Composite Message Details Browse                 *
      *                                                               *
      *  Function:  This module browses composite message details     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD056612           Date 12Sep20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 256564             Date 17Sep08               *
      *                 BUG17937           Date 24Apr08               *
      *                 251519             Date 20Nov07               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CAAA03             Date 12Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CMX001  *CREATE    Data 01Jan00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD056612 - Deliverable Data Split for MX group               *
      *  MD046248 - Finastra Rebranding                               *
      *  256564 - Recompile due to PF changes done by fix 256330      *
      *  BUG17937 - SEC3300 is Failing with an error.  Applied fix    *
      *             245268 (Recompile)                                *
      *  251519 - Moved 'write MSGSUBCT' after FOOTER to display      *
      *           message subfile on screen.                          *
      *  CAAA03 - Webfacing Changes                                   *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************

     FMXCOMDBDD CF   E             WORKSTN
     F                                     SFILE(SUBFLREC:SUBF_RRN)
     F*MXCOMDPD* IF   E           K DISK                                                    MD056612

      ** +--------------------------------------+
      ** ¦ Automatically included D-specs       ¦
      ** ¦ ==============================       ¦
      ** +--------------------------------------+
      **
      ** Standard D-specs
      ** ================
      **
      ** The following /COPY line includes the LDA layout,
      ** the copyright array definition,
      ** and the following named constants:
      **    True       logical = *on (for indcator processing)
      **    False      logical = *off (for indcator processing)
      **    DBErrCtl   10A     = 'DBERRCTL' (the name of the database error
      **                                    handler)
      ** and the following variables:
      **    RunBefore  1A (for the PSSR)

     D/COPY ZACPYSRC,STD_D_SPEC

      ** Program Status Data Structure
      ** =============================
      ** The following /COPY line includes all the defined fields in the
      ** PSDS.  They have meaningful names, prefixed by 'PS'.

     D/COPY ZACPYSRC,PSDS


     D SDBANK        E DS                  EXTNAME(SDBANKPD)
      ** External DS for BANK details
     D DSFDY         E DS                  EXTNAME(DSFDY)

     D*MXCOMD***     E DS                  EXTNAME(MXCOMDPD)                                MD056612
     D MXCOMD        E DS                  EXTNAME(MXCMDJW0)                                MD056612
      * Composite Message Details



     C     *ENTRY        PLIST
     C                   PARM                    I#RTCD            7
     C                   PARM                    I#ERMS           30
     C                   PARM                    I#MODE            1

      * Display subfile screen until F3 taken

     C     *INKC         DOWEQ     '0'
     C                   EXSR      DSPLAY_SCN
     C                   END

      * End of program

     C                   SETON                                        LR

      /SPACE 5
      *****************************************************************
      * Display subfile screen
      *****************************************************************
     C     DSPLAY_SCN    BEGSR

      * Fill current subfile page

     C     *IN05         IFEQ      '1'
     C     *IN06         OREQ      '1'
     C     SUBF_RRN      OREQ      *ZERO
     C  N50              EXSR      FILL_SUBF
     C                   ENDIF

      * Write subfile control and foooter

     C                   TIME                    DDTIME
     C**********         WRITE     MSGSUBCT                                                   251519
     C**********         WRITE     SUBFLCTL                                                   CAAA03
     C                   WRITE     FOOTER

      * Read subfile control

     C                   WRITE     MSGSUBCT                                                   251519
     C                   WRITE     SUBFLCTL                                                   CAAA03
     C                   READ      SUBFLCTL                               99    *

      ** Clear the message queue & init error indicators

     C                   EXSR      CLR_MSGQ

      ** If F3 was not taken

     C     *INKC         IFEQ      '0'

      * Rolldown requested

     C     *IN05         CASEQ     '1'           ROLLDOWN

      * Add a record to the subfile

     C     *INKI         CASEQ     '1'           MNT_SflREC

      * Check for a record selected from the subfile

     C     *IN(04)       CASEQ     '0'           CHK_SUBF
     C                   END

     C                   ENDIF

     C                   ENDSR
      ********************************************************************
      /SPACE 5
      *****************************************************************
      ** Clear the message queue & init error indicators
      *****************************************************************
     C     CLR_MSGQ      BEGSR

     C                   CALL      'Y2CLMSC'
     C                   PARM      DDPGM         ZAPGMQ           10
     C                   PARM      '*SAME'       ZAPGRL            5

     C                   MOVEA     '0'           *IN(50)
     C                   MOVEA     '0000000000'  *IN(10)

     C                   ENDSR
      ********************************************************************
      /SPACE 5
      *****************************************************************
      * Check for a record selected from the subfile
      *****************************************************************
     C     CHK_SUBF      BEGSR

     C                   READC     SUBFLREC                               99    *RECORD CHANGED

      * Do until all records processed

     C     *IN99         DOWEQ     '0'

      * If an action code is specified
      *   Maintain the record in the subfile

     C     DDACTN        IFNE      *BLANK
     C                   EXSR      MNT_SflREC
     C                   ELSE
     C                   MOVE      '0'           @INKC
     C                   ENDIF

      * Return to top of subfile page

     C                   MOVEL     ToP_COMI      CDCOMI
     C                   MOVEL     ToP_EXTT      CDEXTT
     C                   Z-ADD     *ZERO         SUBF_RRN

      * Read next if F3 not requested

     C     @INKC         IFEQ      '0'
     C                   READC     SUBFLREC                               99    *RECORD CHANGED
     C                   ELSE
     C                   MOVE      '1'           *IN99
     C                   END

     C                   ENDDO

     C                   ENDSR
      ********************************************************************
      /SPACE 5
      *****************************************************************
      * Fill subfile
      *****************************************************************
     C     FILL_SUBF     BEGSR

     C                   MOVEA     '000'         *IN(1)
     C                   MOVEA     '1'           *IN(4)
     C                   Z-ADD     *ZERO         SUBF_RRN

      * Write subfile control

     C                   WRITE     SUBFLCTL                                     *

      * Get first record

     C*****MXCOMDK       SETLL     MXCOMDP0                                                 MD056612
     C**********         READ      MXCOMDP0                               99    *           MD056612
     C/EXEC SQL                                                                             MD056612
     C+ declare BCursor insensitive scroll cursor for                                       MD056612
     C+ select * from MXCMDJW0                                                              MD056612
     C+ where CDCOMI >= :CDCOMI                                                             MD056612
     C+ order by CDCOMI, CDEXTT                                                             MD056612
     C/END-EXEC                                                                             MD056612
                                                                                            MD056612
     C/EXEC SQL                                                                             MD056612
     C+ open BCursor                                                                        MD056612
     C/END-EXEC                                                                             MD056612
                                                                                            MD056612
     C/EXEC SQL                                                                             MD056612
     C+ fetch next from BCursor into :MXCOMD                                                MD056612
     C/END-EXEC                                                                             MD056612

      * If not end of file

     C******IN99         IFEQ      '0'                                                      MD056612
     C     SQLCODE       IFEQ      0                                                        MD056612
     C                   MOVEL     CDCOMI        ToP_COMI          8
     C                   MOVEL     CDEXTT        ToP_EXTT          1
     C                   ELSE
     C                   MOVEL     *BLANK        ToP_COMI
     C                   MOVEL     *BLANK        ToP_EXTT
     C                   MOVEL     *BLANK        CDCOMI
     C                   MOVEL     *BLANK        CDEXTT

      * Else, if end of file
      * Issue message

     C                   MOVEL     'MX00001'     ZAMSID
     C                   EXSR      ZASNMS
     C                   ENDIF

      * Do while not end of file

     C******IN99         DOWEQ     '0'                                                      MD056612
     C     SQLCODE       DOWEQ     0                                                        MD056612

     C                   ADD       1             SUBF_RRN          4 0    04    *

      * Enabled?
     C     CDENAB        IFEQ      'Y'
     C                   MOVEL     'YES'         DDENA
     C                   ELSE
     C                   MOVEL     ' NO'         DDENA
     C                   ENDIF

      * Write a record to the subfile

     C                   WRITE     SUBFLREC
     C     SUBF_RRN      IFEQ      NumPerPag
     C***********        READ      MXCOMDP0                               99    *           MD056612
     C/EXEC SQL                                                                             MD056612
     C+ fetch next from BCursor into :MXCOMD                                                MD056612
     C/END-EXEC                                                                             MD056612
     C******IN99         IFEQ      '1'                                                      MD056612
     C     SQLCODE       IFEQ      100                                                      MD056612
     C                   MOVEL     *BLANK        CDCOMI
     C                   MOVEL     *BLANK        CDEXTT
     C                   MOVEL     'MX00001'     ZAMSID
     C                   EXSR      ZASNMS
     C                   ELSE
     C**********         SETON                                            99    *           MD056612
     C                   eval      SQLCODE = 100                                            MD056612
     C                   END
     C                   ELSE
     C***********        READ      MXCOMDP0                               99    *           MD056612
     C/EXEC SQL                                                                             MD056612
     C+ fetch next from BCursor into :MXCOMD                                                MD056612
     C/END-EXEC                                                                             MD056612
     C******IN99         IFEQ      '1'                                                      MD056612
     C     SQLCODE       IFEQ      100                                                      MD056612
     C                   MOVEL     *BLANK        CDCOMI
     C                   MOVEL     *BLANK        CDEXTT
     C                   MOVEL     'MX00001'     ZAMSID
     C                   EXSR      ZASNMS
     C                   END
     C                   END

     C                   ENDDO

     C/EXEC SQL                                                                             MD056612
     C+ close BCursor                                                                       MD056612
     C/END-EXEC                                                                             MD056612
                                                                                            MD056612
      * Set on subfile control indicators

     C     *IN(4)        IFEQ      '1'
     C                   MOVEA     '010'         *IN(1)
     C                   ELSE
     C                   MOVEA     '111'         *IN(1)
     C                   END

     C                   ENDSR
      ********************************************************************
      /SPACE 5
      *****************************************************************
      * Maintain a record on the subfile
      ********************************************************************
     C     MNT_SflREC    BEGSR

     C   KI              MOVEL     *BLANK        CDCOMI
     C   KI              MOVEL     *BLANK        CDEXTT

     C                   CALL      'MXCOMDMNT'
     C                   PARM      *BLANK        W#RTCD            7
     C                   PARM      *BLANK        W#ERMS           30
     C                   PARM      I#MODE        W#MODE            1
     C                   PARM                    MXCOMD
     C                   PARM      '0'           @INKC             1
     C                   PARM      '0'           @INKL             1

      * Error
     C     W#RTCD        IFEQ      '*ERROR'
     C                   EVAL      I#ERMS = 'ERROR IN CALL TO MXCOMDMNT'
     C                   EXSR      *PSSR
     C                   ENDIF

      * Pass back F3 function key

     C                   MOVE      @INKC         *INKC

      * Return to top of subfile page

     C                   MOVEL     ToP_COMI      CDCOMI
     C                   MOVEL     ToP_EXTT      CDEXTT
     C                   Z-ADD     *ZERO         SUBF_RRN

     C                   ENDSR
      ********************************************************************
      /SPACE 5
      *****************************************************************
      * Rolldown
      ********************************************************************
     C     ROLLDOWN      BEGSR

     C                   Z-ADD     *ZERO         SUBF_RRN
     C                   MOVEL     ToP_COMI      CDCOMI
     C                   MOVEL     ToP_EXTT      CDEXTT

     C*****MXCOMDK       SETGT     MXCOMDP0                                                 MD056612
     C**********         READP     MXCOMDP0                               99    *           MD056612
     C/EXEC SQL                                                                             MD056612
     C+ declare CCursor insensitive scroll cursor for                                       MD056612
     C+ select * from MXCMDJW0                                                              MD056612
     C+ where CDCOMI <= :CDCOMI                                                             MD056612
     C+ order by CDCOMI desc , CDEXTT desc                                                  MD056612
     C/END-EXEC                                                                             MD056612
                                                                                            MD056612
     C/EXEC SQL                                                                             MD056612
     C+ open CCursor                                                                        MD056612
     C/END-EXEC                                                                             MD056612
                                                                                            MD056612
     C/EXEC SQL                                                                             MD056612
     C+ fetch next  from CCursor into :MXCOMD                                               MD056612
     C/END-EXEC                                                                             MD056612

      * Read previous
      * until count matches count of records previous read

     C******IN99         DOWEQ     '0'                                                      MD056612
     C     SQLCODE       DOWEQ     0                                                        MD056612
     C     SUBF_RRN      ANDLT     NumPerPag
     C**********         READP     MXCOMDP0                               99    *           MD056612
     C/EXEC SQL                                                                             MD056612
     C+ fetch next  from CCursor into :MXCOMD                                               MD056612
     C/END-EXEC                                                                             MD056612
     C                   ADD       1             SUBF_RRN
     C                   ENDDO

      * Issue start of file message

     C******IN99         IFEQ      '1'                                                      MD056612
     C     SQLCODE       IFEQ      100                                                      MD056612
     C                   MOVEL     *BLANK        CDCOMI
     C                   MOVEL     *BLANK        CDEXTT
     C                   MOVEL     'MX00002'     ZAMSID
     C                   EXSR      ZASNMS
     C                   END

     C/EXEC SQL                                                                             MD056612
     C+ close CCursor                                                                       MD056612
     C/END-EXEC                                                                             MD056612
     C                   ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************
      * ZASNMS - SEND A MESSAGE
      *****************************************************************
     C     ZASNMS        BEGSR

     C                   CALL      'Y2SNMGC'                            0909    *
     C                   PARM      DDPGM         ZAPGMQ           10
     C                   PARM                    ZAPGRL            5
     C                   PARM                    ZAMSID            7
     C                   PARM      'MIDAS'       ZAMSGF           10
     C                   PARM                    ZAMSDA          132
     C                   PARM                    ZAMSTP            7

      * Error encountered

     C     ZAMSID        IFNE      'MX00001'
     C     ZAMSID        ANDNE     'MX00002'
     C                   MOVE      '1'           *IN50
     C                   ENDIF

     C                   ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************
      * *INZSR - Initial subroutine called automatically at program start
      *****************************************************************
     C     *INZSR        BEGSR
      *
      ** Initialize program name
      *
     C                   MOVEL     'MXCOMDBRW'   DDPGM
      *
      ** Set up subfile message queue information
      *
     C                   MOVEL     '*'           DDPGMQ
     C                   MOVE      '1'           *IN40
      *
      ** Set up standard screen fields.
      *
     C                   MOVEL     PsJobName     DDJOB
     C                   MOVEL     PsUser        DDUSR

      *  Access Bank Details

     C                   CALL      'AOBANKR0'
     C                   PARM      '*DBERR  '    @RTCD             7
     C                   PARM      '*FIRST  '    @OPTN             7
     C     SDBANK        PARM      SDBANK        DSFDY

      * Initialize constants

     C                   Z-ADD     16            NumPerPag         2 0

      * Mode (U = Allow updates)

     C     I#MODE        IFEQ      'U'
     C                   MOVE      '1'           *IN61
     C                   ENDIF

      * Key list

     C     MXCOMDK       KLIST
     C                   KFLD                    CDCOMI
     C                   KFLD                    CDEXTT

     C                   ENDSR
      *****************************************************************
      /SPACE 5
      ********************************************************************
      * * P S S R  --- A B N O R M A L   E R R O R   C O N D I T I O N S
      /COPY MXCPYSRC,MXPSSR
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2001
