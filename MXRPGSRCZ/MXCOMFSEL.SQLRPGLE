     H DEBUG
     H COPYRIGHT('(c) Finastra International Limited 2002')
      *****************************************************************
/**** *  RPGBASEMOD                                                   *          MD056612
/*STD *  RPGSQLMOD                                                    *          MD056612
/*EXI *  CLOSQLCSR(*ENDMOD)                                           *          MD056612
/*EXI *  TEXT('Midas MX Composite Message Formats Selection')
      *****************************************************************
      *                                                               *
      *  Midas - Meridian Export Module                               *
      *                                                               *
      *  MXCOMFSEL - Composite Message Formats Selection              *
      *                                                               *
      *  Function:  This module allows selection of composite message *
      *             formats.                                          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD056612           Date 12Sep20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CAAA03             Date 12Mar04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                         *CREATE    Date                       *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD056612 - Deliverable Data Split for MX group               *
      *  MD046248 - Finastra Rebranding                               *
      *  CAAA03 - Webfacing Changes                                   *
      *  CMX001 - Meridian Export                                     *
      *                                                               *
      *****************************************************************

     FMXCOMFSDD CF   E             WORKSTN
     F                                     SFILE(SUBFLREC:SUBF_RRN)
     F*MXCOMFL1* IF   E           K DISK                                                    MD056612

      ** +--------------------------------------+
      ** ¦ Automatically included D-specs       ¦
      ** ¦ ==============================       ¦
      ** +--------------------------------------+
      **
      ** Standard D-specs
      ** ================
      **
      ** The following /COPY line includes the LDA layout,
      ** the copyright array definition,
      ** and the following named constants:
      **    True       logical = *on (for indcator processing)
      **    False      logical = *off (for indcator processing)
      **    DBErrCtl   10A     = 'DBERRCTL' (the name of the database error
      **                                    handler)
      ** and the following variables:
      **    RunBefore  1A (for the PSSR)

     D/COPY ZACPYSRC,STD_D_SPEC

      ** Program Status Data Structure
      ** =============================
      ** The following /COPY line includes all the defined fields in the
      ** PSDS.  They have meaningful names, prefixed by 'PS'.

     D/COPY ZACPYSRC,PSDS


     D SDBANK        E DS                  EXTNAME(SDBANKPD)
      ** External DS for BANK details
     D DSFDY         E DS                  EXTNAME(DSFDY)
     D MXCOMF        E DS                  EXTNAME(MXCMFJW0)                                MD056612


     C     *ENTRY        PLIST
     C                   PARM                    I#RTCD            7
     C                   PARM                    I#ERMS           30
     C                   PARM                    I#MODE            1
     C                   PARM                    I#COMI            8
     C                   PARM                    I#EXTT            1
     C                   PARM                    I#FMT            10
     C                   PARM                    I#PRSQ            2
     C                   PARM                    I#DESC           42
     C                   PARM                    I#LPRS            2
     C                   PARM                    @INKC             1
     C                   PARM                    @INKL             1

      * Display subfile screen until F3 or F12 taken

     C     *INKC         DOWEQ     '0'
     C     *INKL         ANDEQ     '0'
     C                   EXSR      DSPLAY_SCN
     C                   END

      * Pass back function keys taken

     C                   MOVE      *INKC         @INKC
     C                   MOVE      *INKL         @INKL

      * End of program

     C                   SETON                                        LR

      /SPACE 5
      *****************************************************************
      * Display subfile screen
      *****************************************************************
     C     DSPLAY_SCN    BEGSR

      * Fill current subfile page

     C     *IN05         IFEQ      '1'
     C     *IN06         OREQ      '1'
     C     SUBF_RRN      OREQ      *ZERO
     C  N50              EXSR      FILL_SUBF
     C                   ENDIF

      * Write subfile control and foooter

     C                   TIME                    DDTIME
     C                   WRITE     MSGSUBCT
     C**********         WRITE     SUBFLCTL                                                   CAAA03
     C                   WRITE     FOOTER

      * Read subfile control

     C                   WRITE     SUBFLCTL                                                   CAAA03
     C                   READ      SUBFLCTL                               99    *

      ** Clear the message queue & init error indicators

     C                   EXSR      CLR_MSGQ

      ** If F3 and F12 were not taken

     C     *INKC         IFEQ      '0'
     C     *INKL         ANDEQ     '0'

      * Rolldown requested

     C     *IN05         CASEQ     '1'           ROLLDOWN

      * Check for a record selected from the subfile

     C     *IN(04)       CASEQ     '0'           CHK_SUBF
     C                   END

     C                   ENDIF

     C                   ENDSR
      ********************************************************************
      /SPACE 5
      *****************************************************************
      ** Clear the message queue & init error indicators
      *****************************************************************
     C     CLR_MSGQ      BEGSR

     C                   CALL      'Y2CLMSC'
     C                   PARM      DDPGM         ZAPGMQ           10
     C                   PARM      '*SAME'       ZAPGRL            5

     C                   MOVEA     '0'           *IN(50)
     C                   MOVEA     '0000000000'  *IN(10)
     C                   MOVEA     '0000000000'  *IN(20)

     C                   ENDSR
      ********************************************************************
      /SPACE 5
      *****************************************************************
      * Check for a record selected from the subfile
      *****************************************************************
     C     CHK_SUBF      BEGSR

     C                   READC     SUBFLREC                               99    *RECORD CHANGED

      * Do until all records processed

     C     *IN99         DOWEQ     '0'

      * If a record is selected

     C     DDSEL         IFNE      *BLANK

      * End the program

     C                   MOVE      CFPRSQ        I#LPRS
     C                   MOVE      '1'           *IN99
     C                   MOVE      '1'           *INKL

      * Else, read the next changed subfile record

     C                   ELSE
     C                   READC     SUBFLREC                               99    *RECORD CHANGED
     C                   END

     C                   ENDDO

     C                   ENDSR
      ********************************************************************
      /SPACE 5
      *****************************************************************
      * Fill subfile
      *****************************************************************
     C     FILL_SUBF     BEGSR

     C                   MOVEA     '000'         *IN(1)
     C                   MOVEA     '1'           *IN(4)
     C                   Z-ADD     *ZERO         SUBF_RRN

      * Write subfile control

     C                   WRITE     SUBFLCTL                                     *

      * Get first record

     C*****MXCOMFK       SETLL     MXCOMFP0                                                 MD056612
     C*****MXCOMDK       READE     MXCOMFP0                               99    *           MD056612
     C/EXEC SQL                                                                             MD056612
     C+ declare CCursor insensitive scroll cursor for                                       MD056612
     C+ select * from MXCMFJW0                                                              MD056612
     C+ where CFCOMI  = :I#COMI and CFEXTT = :I#EXTT                                        MD056612
     C+ order by CFCOMI, CFEXTT, CFPRSQ                                                     MD056612
     C/END-EXEC                                                                             MD056612
                                                                                            MD056612
     C/EXEC SQL                                                                             MD056612
     C+ open CCursor                                                                        MD056612
     C/END-EXEC                                                                             MD056612
                                                                                            MD056612
     C/EXEC SQL                                                                             MD056612
     C+ fetch prior from CCursor into :MXCOMF                                               MD056612
     C/END-EXEC                                                                             MD056612

      * If not end of file

     C******IN99         IFEQ      '0'                                                      MD056612
     C     SQLCODE       IFEQ      0                                                        MD056612
     C                   MOVEL     CFPRSQ        ToP_PRSQ          2
     C                   ELSE
     C                   MOVEL     *BLANK        ToP_PRSQ
     C                   MOVEL     *BLANK        CFPRSQ

      * Else, if end of file
      * Issue message

     C                   MOVEL     'MX00001'     ZAMSID
     C                   EXSR      ZASNMS
     C                   ENDIF

      * Do while not end of file

     C******IN99         DOWEQ     '0'                                                      MD056612
     C     SQLCODE       DOWEQ     0                                                        MD056612

     C                   ADD       1             SUBF_RRN          4 0    04    *

      * Protect selection

     C                   MOVEL     CFPRSQ        @LPRS             2 0
     C                   MOVEL     I#PRSQ        @PRSQ             2 0
     C     @LPRS         IFLE      @PRSQ
     C                   MOVE      '1'           *IN61
     C                   ELSE
     C                   MOVE      '0'           *IN61
     C                   ENDIF

      * Export?
     C     CFEXPT        IFEQ      'Y'
     C                   MOVEL     'YES'         DDEXPT
     C                   ELSE
     C                   MOVEL     ' NO'         DDEXPT
     C                   ENDIF

      * Write a record to the subfile

     C                   WRITE     SUBFLREC
     C     SUBF_RRN      IFEQ      NumPerPag
     C*****MXCOMDK       READE     MXCOMFP0                               99    *           MD056612
     C/EXEC SQL                                                                             MD056612
     C+ fetch prior from CCursor into :MXCOMF                                               MD056612
     C/END-EXEC                                                                             MD056612
     C******IN99         IFEQ      '1'                                                      MD056612
     C     SQLCODE       IFEQ      100                                                      MD056612
     C                   MOVEL     *BLANK        CFPRSQ
     C                   MOVEL     'MX00001'     ZAMSID
     C                   EXSR      ZASNMS
     C                   ELSE
     C**********         SETON                                            99    *           MD056612
     C                   eval      SQLCODE = 100                                            MD056612
     C                   END
     C                   ELSE
     C*****MXCOMDK       READE     MXCOMFP0                               99    *           MD056612
     C/EXEC SQL                                                                             MD056612
     C+ fetch prior from CCursor into :MXCOMF                                               MD056612
     C/END-EXEC                                                                             MD056612
     C******IN99         IFEQ      '1'                                                      MD056612
     C     SQLCODE       IFEQ      100                                                      MD056612
     C                   MOVEL     *BLANK        CFPRSQ
     C                   MOVEL     'MX00001'     ZAMSID
     C                   EXSR      ZASNMS
     C                   END
     C                   END

     C                   ENDDO

     C/EXEC SQL                                                                             MD056612
     C+ close CCursor                                                                       MD056612
     C/END-EXEC                                                                             MD056612
      * Set on subfile control indicators

     C     *IN(4)        IFEQ      '1'
     C                   MOVEA     '010'         *IN(1)
     C                   ELSE
     C                   MOVEA     '111'         *IN(1)
     C                   END

     C                   ENDSR
      ********************************************************************
      /SPACE 5
      *****************************************************************
      * Rolldown
      ********************************************************************
     C     ROLLDOWN      BEGSR

     C                   Z-ADD     *ZERO         SUBF_RRN
     C                   MOVEL     ToP_PRSQ      CFPRSQ

     C*****MXCOMFK       SETGT     MXCOMFP0                                                 MD056612
     C*****MXCOMDK       READPE    MXCOMFP0                               99    *           MD056612
     C/EXEC SQL                                                                             MD056612
     C+ declare ECursor insensitive scroll cursor for                                       MD056612
     C+ select * from MXCMFJW0                                                              MD056612
     C+ where CFCOMI >= :I#COMI and CFEXTT >= :I#EXTT and CFPRSQ >= :CFPRSQ                 MD056612
     C+ order by CFCOMI, CFEXTT, CFPRSQ                                                     MD056612
     C/END-EXEC                                                                             MD056612
                                                                                            MD056612
     C/EXEC SQL                                                                             MD056612
     C+ open ECursor                                                                        MD056612
     C/END-EXEC                                                                             MD056612
                                                                                            MD056612
     C/EXEC SQL                                                                             MD056612
     C+ fetch prior from ECursor into :MXCOMF                                               MD056612
     C/END-EXEC                                                                             MD056612

      * Read previous
      * until count matches count of records previous read

     C******IN99         DOWEQ     '0'                                                      MD056612
     C     SQLCODE       DOWEQ     0                                                        MD056612
     C     SUBF_RRN      ANDLT     NumPerPag
     C*****MXCOMDK       READPE    MXCOMFP0                               99    *           MD056612
     C/EXEC SQL                                                                             MD056612
     C+ fetch prior from ECursor into :MXCOMF                                               MD056612
     C/END-EXEC                                                                             MD056612
     C                   ADD       1             SUBF_RRN
     C                   ENDDO

      * Issue start of file message

     C******IN99         IFEQ      '1'                                                      MD056612
     C     SQLCODE       IFEQ      100                                                      MD056612
     C                   MOVEL     *BLANK        CFPRSQ
     C                   MOVEL     'MX00002'     ZAMSID
     C                   EXSR      ZASNMS
     C                   END

     C/EXEC SQL                                                                             MD056612
     C+ close ECursor                                                                       MD056612
     C/END-EXEC                                                                             MD056612
     C                   ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************
      * ZASNMS - SEND A MESSAGE
      *****************************************************************
     C     ZASNMS        BEGSR

     C                   CALL      'Y2SNMGC'                            0909    *
     C                   PARM      DDPGM         ZAPGMQ           10
     C                   PARM                    ZAPGRL            5
     C                   PARM                    ZAMSID            7
     C                   PARM      'MIDAS'       ZAMSGF           10
     C                   PARM                    ZAMSDA          132
     C                   PARM                    ZAMSTP            7

      * Error encountered

     C     ZAMSID        IFNE      'MX00001'
     C     ZAMSID        ANDNE     'MX00002'
     C                   MOVE      '1'           *IN50
     C                   ENDIF

     C                   ENDSR
      *****************************************************************
      /SPACE 5
      *****************************************************************
      * *INZSR - Initial subroutine called automatically at program start
      *****************************************************************
     C     *INZSR        BEGSR
      *
      ** Initialize program name
      *
     C                   MOVEL     'MXCOMFSEL'   DDPGM
      *
      ** Set up subfile message queue information
      *
     C                   MOVEL     '*'           DDPGMQ
     C                   MOVE      '1'           *IN40
      *
      ** Set up standard screen fields.
      *
     C                   MOVEL     PsJobName     DDJOB
     C                   MOVEL     PsUser        DDUSR

      *  Access Bank Details

     C                   CALL      'AOBANKR0'
     C                   PARM      '*DBERR  '    @RTCD             7
     C                   PARM      '*FIRST  '    @OPTN             7
     C     SDBANK        PARM      SDBANK        DSFDY

      * Initialize constants

     C                   Z-ADD     12            NumPerPag         2 0

      * Key lists

     C     MXCOMDK       KLIST
     C                   KFLD                    I#COMI
     C                   KFLD                    I#EXTT
     C     MXCOMFK       KLIST
     C                   KFLD                    I#COMI
     C                   KFLD                    I#EXTT
     C                   KFLD                    CFPRSQ

     C                   ENDSR
      *****************************************************************
      /SPACE 5
      ********************************************************************
      * * P S S R  --- A B N O R M A L   E R R O R   C O N D I T I O N S
      /COPY MXCPYSRC,MXPSSR
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2001
