/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas OF Bank Position Report')                       */
/*********************************************************************/
/*                                                                   */
/*       Midas - General Ledger Module                               */
/*                                                                   */
/*       GLC000596 - Midas OF Bank Position Report                   */
/*                                                                   */
/*       (c) Finastra International Limited 2014                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. MD026777           Date 12May14              */
/*                      CGL153  *CREATE    Date 13Feb14              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       MD026777 - Pools are not displayed in the report            */
/*       CGL153 - Bank Position Report                               */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&MODE &RSEQ &RLEV &RENT)
 
             DCL        VAR(&RSEQ) TYPE(*CHAR) LEN(5)
             DCL        VAR(&RLEV) TYPE(*CHAR) LEN(1)
             DCL        VAR(&RENT) TYPE(*CHAR) LEN(3)
             DCL        VAR(&MODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&RETCOD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RTCD)   TYPE(*CHAR) LEN(7)                                 /*MD026777*/
             DCL        VAR(&ERMS)   TYPE(*CHAR) LEN(50)                                /*MD026777*/
             DCL        VAR(&FULLCHECK) TYPE(*CHAR) LEN(1) VALUE('N')                   /*MD026777*/
             DCL        VAR(&ZOZONE) TYPE(*CHAR) LEN(10)                                /*MD026777*/
             DCL        VAR(&ZOSHTC) TYPE(*CHAR) LEN(4)                                 /*MD026777*/
             DCL        VAR(&ZORDNB) TYPE(*DEC) LEN(5 0)                                /*MD026777*/
             DCL        VAR(&ZODNWD) TYPE(*DEC) LEN(5 0)                                /*MD026777*/
             DCL        VAR(&ZOBCCY) TYPE(*CHAR) LEN(3)                                 /*MD026777*/
             DCL        VAR(&ZONJOB) TYPE(*DEC) LEN(1 0)                                /*MD026777*/
 
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited +
                          2014')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256) +
                          VALUE(' ')
             MONMSG     MSGID(CPF0000)
             CHGJOB SWS(00000000)
 
/* Copy Nostro UDF to Pool file for this zone */                                        /*MD026777*/
                                                                                        /*MD026777*/
             CALL       PGM(GOGETZONE) PARM(&RTCD &ERMS &FULLCHECK +
                          &ZOZONE &ZOSHTC &ZORDNB &ZODNWD &ZOBCCY +
                          &ZONJOB)                                                      /*MD026777*/
                                                                                        /*MD026777*/
             IF         COND(&RTCD *NE '       ' *OR +
                          %SWITCH(XXXXXX11)) THEN(GOTO CMDLBL(ABNOR))                   /*MD026777*/
                                                                                        /*MD026777*/
             CPYF       FROMFILE(T_GZNOSREX) TOFILE(SDBPOLTD) +
                          MBROPT(*REPLACE) INCREL((*IF ZONE *EQ +
                          &ZOZONE)) FMTOPT(*MAP *DROP)                                  /*MD026777*/
 
/* Call the program */
 
             CLRPFM     FILE(SDBPOWTD)
             CALL       PGM(GL000596P) PARM(&RETCOD &MODE &RSEQ &RLEV &RENT)
 
             IF COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO CMDLBL(ABNOR)
             ENDDO
 
             GOTO       CMDLBL(END)
 
/* Abnormal termination */
 
 ABNOR:      CHGJOB     SWS(XXXXXX11)
             MONMSG     MSGID(CPF0000 MCH0000)
 
             SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          GLC000596 ended abnormally - see job log') +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
END:         RCLRSC
             MONMSG     MSGID(CPF0000 MCH0000)
             RCLRSC     LVL(*CALLER)
             MONMSG     MSGID(CPF0000 MCH0000)
             RCLACTGRP  ACTGRP(*ELIGIBLE)
             MONMSG     MSGID(CPF0000 MCH0000)
             ENDPGM
