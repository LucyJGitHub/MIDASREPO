/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas OF Accounting Rules DTAARA Creation')           */
/*********************************************************************/
/*                                                                   */
/*       Midas - General Ledger Module                               */
/*                                                                   */
/*       GLC000599 - Midas OF Accounting Rules Posting Generation    */
/*                   Dtaara Deallocation                             */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2013           */
/*                                                                   */
/*       Last Amend No. MD029739           Date 15Aug14              */
/*       Prev Amend No. MD023629  *CREATE  Date 22Nov13              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD029739 - File renaming for integration layer.             */
/*       MD023629 - Accounting Rules Processing                      */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&RTCD &BTID &TMSTM)
 
             DCL        VAR(&RTCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&BTID) TYPE(*CHAR) LEN(60)
             DCL        VAR(&TMSTM) TYPE(*CHAR) LEN(26)
             DCL        VAR(&DTAR) TYPE(*CHAR) LEN(10)
 
             DCL        VAR(&DAY) TYPE(*CHAR) LEN(2)
             DCL        VAR(&A) TYPE(*CHAR) LEN(1)
             DCL        VAR(&BTSQ) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DTAR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PREF) TYPE(*CHAR) LEN(2)
 
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2013')
             CHGVAR     VAR(&RTCD) VALUE('       ')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256) +
                          VALUE(' ')
             MONMSG     MSGID(CPF0000)
             CHGJOB SWS(00000000)
 
/* Check for DTAARA exists for Batch ID */
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&PREF)
             CHGVAR     VAR(&DPLIB) VALUE(&PREF *CAT 'DPLIB')
             CHGVAR     VAR(&A) VALUE('A')
             CHGVAR     VAR(&DAY) VALUE(%SUBSTRING(&BTID 7 2))
             CHGVAR     VAR(&BTSQ) VALUE(%SUBSTRING(&BTID 9 7))
             CHGVAR     VAR(&DTAR) VALUE(&A *CAT &DAY *CAT &BTSQ)
             CHKOBJ     OBJ(&DTAR) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9801) EXEC(CRTDTAARA +
                          DTAARA(&DPLIB/&DTAR) TYPE(*CHAR) LEN(1) +
                          VALUE(' ') TEXT('Midas OF Accounting +
                          Rules Batch ID') AUT(*USE))
 
             ALCOBJ     OBJ((&DTAR *DTAARA *EXCLRD)) WAIT(0)
             MONMSG     MSGID(CPF1002) EXEC(DO)
             CHGVAR     VAR(&RTCD) VALUE('PROCESS')
             GOTO       CMDLBL(END)
             ENDDO
 
/* Call the program that will process each step of the Posting Generation */
 
/**********  CALL       PGM(GL000600) PARM(&BTID &TMSTM)             */                 /*MD029739*/
             CALL       PGM(DI000600) PARM(&BTID &TMSTM)                                /*MD029739*/
 
             IF COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO CMDLBL(ABNOR)
             ENDDO
 
 
             DLCOBJ OBJ((&DTAR *DTAARA *EXCLRD))
             MONMSG     MSGID(CPF0000)
             GOTO       CMDLBL(END)
 
/* Abnormal termination */
 
 ABNOR:      CHGJOB     SWS(XXXXXX11)
             DMPCLPGM
             MONMSG     MSGID(CPF0000 MCH0000)
 
             SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          GLC000599 ended abnormally - see job log') +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
END:         RCLRSC
             MONMSG     MSGID(CPF0000 MCH0000)
             RCLRSC     LVL(*CALLER)
             MONMSG     MSGID(CPF0000 MCH0000)
             RCLACTGRP  ACTGRP(*ELIGIBLE)
             MONMSG     MSGID(CPF0000 MCH0000)
             ENDPGM
