/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas OF Forecast Total Avail Balance Rebuild-COB')   */
/*********************************************************************/
/*                                                                   */
/*       Midas - General Ledger Module                               */
/*                                                                   */
/*       GLC000602 - Midas OF Forecast Total Available Balance       */
/*                   Rebuild (COB)                                   */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2013           */
/*                                                                   */
/*       Last Amend No. CGL153             Date 05Feb14              */
/*       Prev Amend No. CGL145B  *CREATE   Date 29Oct13              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CGL153 - Bank Position Report                               */
/*       CGL145B - Subsidiaries Daily Sweeping Forecast              */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&PRTCD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&POPTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PSARD) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SCSARD) TYPE(*CHAR) LEN(200)
             DCL        VAR(&CGL153) TYPE(*CHAR) LEN(1)                                   /*CGL153*/
 
             COPYRIGHT  TEXT('(c) Misys International Banking +
                          Systems Ltd. 2013')
 
/** Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             SNDPGMMSG  MSG('GLC000602 - Midas OF Forecast Total +
                          Available Balance Rebuild') TOMSGQ(MRUNQ)
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)
             CHGDTAARA  DTAARA(QTEMP/LDA) VALUE(*BLANK)
             CHGJOB     SWS(XXXXXX00)
 
             STRCMTCTL  LCKLVL(*CHG) CMTSCOPE(*JOB)
             MONMSG     MSGID(CPF8351)
 
/* Check if CGL145 is installed */
 
             CHGVAR     VAR(&PRTCD) VALUE('       ')
             CHGVAR     VAR(&POPTN) VALUE('*VERIFY')
             CHGVAR     VAR(&PSARD) VALUE('CGL145')
 
             CALL       PGM(AOSARDR0) PARM(&PRTCD &POPTN &PSARD &SCSARD)
 
             IF         COND(&PRTCD *NE '       ') THEN(DO)
                GOTO       CMDLBL(END)
             ENDDO
                                                                                          /*CGL153*/
/* Check if CGL153 is installed */                                                        /*CGL153*/
                                                                                          /*CGL153*/
             CHGVAR     VAR(&PRTCD) VALUE('       ')                                      /*CGL153*/
             CHGVAR     VAR(&POPTN) VALUE('*VERIFY')                                      /*CGL153*/
             CHGVAR     VAR(&PSARD) VALUE('CGL153')                                       /*CGL153*/
             CHGVAR     VAR(&CGL153) VALUE('N')                                           /*CGL153*/
                                                                                          /*CGL153*/
             CALL       PGM(AOSARDR0) PARM(&PRTCD &POPTN &PSARD &SCSARD)                  /*CGL153*/
                                                                                          /*CGL153*/
             IF         COND(&PRTCD *NE '       ') THEN(DO)                               /*CGL153*/
                GOTO       CMDLBL(END)                                                    /*CGL153*/
             ENDDO                                                                        /*CGL153*/
             CHGVAR     VAR(&CGL153) VALUE('Y')                                           /*CGL153*/
 
/* Call program to update Forecast Available Balance for Total List view */
 
             CLRPFM     FILE(GLFOAVTD)
             MONMSG     MSGID(CPF0000)
 
             CALL       PGM(GL000502)
 
             IF COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO CMDLBL(ABNOR)
             ENDDO
 
/* Call program to update Forecast Total Available Balance */
 
             CLRPFM     FILE(GLFTABTD)
             MONMSG     MSGID(CPF0000)
 
             CALL       PGM(GL000602)
 
             IF COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO CMDLBL(ABNOR)
             ENDDO
 
/* Call program to update Forecast Credit Line Available Balance */
 
             CLRPFM     FILE(GLFOCLTD)
             MONMSG     MSGID(CPF0000)
 
             CALL       PGM(GL000702)
 
             IF COND(%SWITCH(XXXXXX11)) THEN(DO)
                GOTO CMDLBL(ABNOR)
             ENDDO
 
/* Call program to update Forecast Total for Nostro Code Balance Rebuild */               /*CGL153*/
                                                                                          /*CGL153*/
             IF COND(&CGL153 *EQ 'Y')   THEN(DO)                                          /*CGL153*/
                                                                                          /*CGL153*/
             CLRPFM     FILE(GLTNOSTD)                                                    /*CGL153*/
             MONMSG     MSGID(CPF0000)                                                    /*CGL153*/
                                                                                          /*CGL153*/
             CALL       PGM(GL000802)                                                     /*CGL153*/
                                                                                          /*CGL153*/
             IF COND(%SWITCH(XXXXXX11)) THEN(DO)                                          /*CGL153*/
                GOTO CMDLBL(ABNOR)                                                        /*CGL153*/
             ENDDO                                                                        /*CGL153*/
                                                                                          /*CGL153*/
             ENDDO                                                                        /*CGL153*/
                                                                                          /*CGL153*/
             GOTO       CMDLBL(END)
 
/* Abnormal termination */
 
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
             DMPCLPGM
             ROLLBACK
 
             SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          GLC000602 ended abnormally - see job log') +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
END:
             ENDCMTCTL
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
 
             ENDPGM
