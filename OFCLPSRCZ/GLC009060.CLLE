/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas OF Submit Proj Bal View Update Job in I/C')     */
/*********************************************************************/
/*                                                                   */
/*       Midas - Retail Module                                       */
/*                                                                   */
/*       GLC009060 - Submit Projected Balance View Update Job in I/C */
/*                                                                   */
/*       (c) Finastra International Limited 2014                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. CGL155  *CREATE    Date 25Mar14              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CGL155 - Forecast Group Limits                              */
/*                                                                   */
/*********************************************************************/
             PGM
             DCL        VAR(&PREFIX) TYPE(*CHAR) LEN(2)
             DCL        VAR(&PHASEOFDAY) TYPE(*CHAR) LEN(1)
             DCL        VAR(&JOBNAME) TYPE(*CHAR) LEN(10)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2010')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Retrieve system prefix */
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&PREFIX)
 
/* Retrieve phase of day */
 
             RTVDTAARA  DTAARA(MPHAS (1 1)) RTNVAR(&PHASEOFDAY)
 
/* Check if the FORIUPD_xx job is running */
 
             ALCOBJ     OBJ((GLFORILCK *DTAARA *EXCL)) WAIT(0)
             MONMSG     MSGID(CPF1002) EXEC(GOTO CMDLBL(ENDPGM))
             DLCOBJ     OBJ((GLFORILCK *DTAARA *EXCL))
 
/* Submit FORIUPD_xx background job */
 
             IF         COND((&PHASEOFDAY *EQ 'A') *OR +
                             (&PHASEOFDAY *EQ 'G')) THEN(DO)
             CHGVAR     VAR(&JOBNAME) VALUE('FORIUPD_' *CAT &PREFIX)
             SBMJOB     CMD(CALL PGM(GLC009061)) JOB(&JOBNAME) +
                          JOBD(*LIBL/MBATCH) JOBQ(INTERFACE) +
                          OUTQ(*JOBD) USER(*JOBD)
             ENDDO
             GOTO       CMDLBL(ENDPGM)
 
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          GLC009060 ended abnormally - see job +
                          log') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 ENDPGM:
             ENDPGM
