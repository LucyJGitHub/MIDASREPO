/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas OF Proj Bal View Check Update Job')             */
/*********************************************************************/
/*                                                                   */
/*       Midas - Retail Module                                       */
/*                                                                   */
/*       GLC009061 - Projected Balance View Check Update Job         */
/*                                                                   */
/*       (c) Finastra International Limited 2014                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. CGL155  *CREATE    Date 24Mar14              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       CGL155 - Forecast Group Limits                              */
/*                                                                   */
/*********************************************************************/
             PGM
             DCL        VAR(&JOBNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DTQNAM)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTQLIB)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTQLEN)  TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&DTQDTA)  TYPE(*CHAR) LEN(4021)
             DCL        VAR(&DTQWAIT) TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&JOBDET) TYPE(*CHAR) LEN(28)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2014')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Create data area in QTEMP */
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)
 
/* Issue start commitment control */
 
             STRCMTCTL  LCKLVL(*CHG) NFYOBJ(MNTYF (*FILE)) +
                          CMTSCOPE(*JOB)
             MONMSG     MSGID(CPF8351)
 
/* Retrieve job information */
 
             RTVJOBA    JOB(&JOBNAME) USER(&JOBUSER) NBR(&JOBNBR)
 
             ALCOBJ     OBJ((GLFORILCK *DTAARA *EXCL)) WAIT(0)
             MONMSG     MSGID(CPF1002) EXEC(GOTO CMDLBL(ENDPGM))
 
             CHGVAR     VAR(&DTQNAM) VALUE('CGLFORIQ')
             CHGVAR     VAR(&DTQLIB) VALUE('*LIBL')
             CHGVAR     VAR(&DTQLEN) VALUE(1000)
             CHGVAR     VAR(&DTQWAIT) VALUE(-1)
 
/* Receive message from data que until 'END' message is encountered or error */
/* has been encountered */
 
RECEIVE:
             CHGVAR     VAR(&DTQDTA) VALUE(' ')
             CALL       PGM(QRCVDTAQ) PARM(&DTQNAM &DTQLIB &DTQLEN +
                          &DTQDTA &DTQWAIT)
 
             IF         COND(&DTQDTA *EQ ' ') THEN(DO)
             GOTO       CMDLBL(RECEIVE)
             ENDDO
 
             IF         COND(%SST(&DTQDTA 1 3) = 'END') THEN(DO)
             GOTO       CMDLBL(ENDPGM)
             ENDDO
 
/* Call Project Balance View update program */
 
             CALL       PGM(GL009071) PARM(&DTQDTA)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
 
             ROLLBACK
             CHGVAR     VAR(&JOBDET) VALUE(&JOBNBR *TCAT '/' *TCAT +
                          &JOBUSER *TCAT '/' *TCAT &JOBNAME)
 
             SNDMSG     MSG('Project Balance Updater job +
                          FORIUPD_XX has failed ' *CAT &JOBDET +
                          *TCAT '. Refer to logs for details. The +
                          job will skip failed message and continue +
                          processing succeeding messages from FORI +
                          Data Queue.') TOMSGQ(MOPERQ)
             CHGJOB     SWS(XXXXXX00)
 
/* Carry on with processing succeeding messages. */
             GOTO       CMDLBL(RECEIVE)
 
             ENDDO
             ELSE       CMD(GOTO CMDLBL(RECEIVE))
 
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          GLC009061 ended abnormally - see job +
                          log') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
             ROLLBACK
 ENDPGM:
             ENDCMTCTL
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             ENDPGM
