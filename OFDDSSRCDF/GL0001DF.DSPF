     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas OF General Fixed Asset Details Window')          *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Midas SD Module                                      *
     A*                                                               *
     A*  GL0001DF - General Fixed Asset Details Window                *
     A*                                                               *
     A*  (c) Finastra International Limited 2012                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL141  *CREATE    Date 12Dec12               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CGL141 - Amount Accruals Extended Narrative                  *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      CA03(03 'Exit')
     A N20                                  CF05(05 'Reset')
     A                                      CF12(12 'Previous')
     A          R GL0001F1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2012')
     A                                      KEEP
     A                                      OVERLAY
     A                                      WINDOW(&SWROW &SWCOL 22 72)
     A                                      USRRSTDSP
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A                                  2  1'Asset Type..........:'
     A            #ASTP         10A  B  2 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 20  1'F3=Exit'
     A N20                             20 10'F5=Refresh'
     A                                 20 22'F12=Previous'
     A                                  3  1'Asset Type Desc.....:'
     A                                 18  1'Rate of Depreciation:'
     A                                  1 28'Fixed Asset Details'
     A                                      DSPATR(HI)
     A            #ASTD         30A  B  3 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4  1'Asset Category......:'
     A                                  5  1'Asset Category Desc.:'
     A                                  6  1'Asset Desc..........:'
     A                                  7  1'Purch Date .........:'
     A                                  8  1'Purch Value in For..:'
     A            #ACAT         10A  B  4 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            #ACAD         30A  B  5 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            #ADES         30A  B  6 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  8 39'Ccy:'
     A            #PCCY          3A  B  8 44
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A N20 34                               DSPATR(RI)
     A N20 34                               DSPATR(PC)
     A N34                                  DSPATR(HI)
     A                                  9  1'Conv Rate...........:'
     A                                 10  1'Quantity............:'
     A                                 11  1'Supplier Name.......:'
     A                                 12  1'Loc Code............:'
     A                                 14  1'Specific Loc........:'
     A                                 15  1'Remarks.............:'
     A            #SPNM         30A  B 11 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            #LOCN          3A  B 12 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            #LOC1         30A  B 14 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            #REM1         30A  B 15 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A N20 39                               DSPATR(RI)
     A N20 39                               DSPATR(PC)
     A N39                                  DSPATR(HI)
     A            #REM2         30A  B 16 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A N20 39                               DSPATR(RI)
     A N20 39                               DSPATR(PC)
     A N39                                  DSPATR(HI)
     A                                  9 39'Purch Value Local:'
     A            #CNRT         12A  B  9 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A N20 35                               DSPATR(RI)
     A N20 35                               DSPATR(PC)
     A N35                                  DSPATR(HI)
     A            #QNTY          5A  B 10 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A N20 37                               DSPATR(RI)
     A N20 37                               DSPATR(PC)
     A N37                                  DSPATR(HI)
     A            #PVFC         14A  B  8 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A N20 33                               DSPATR(RI)
     A N20 33                               DSPATR(PC)
     A N33                                  DSPATR(HI)
     A            #RTDP         12A  B 18 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A N20 31                               DSPATR(RI)
     A N20 31                               DSPATR(PC)
     A N31                                  DSPATR(HI)
     A            #PDAT          6A  B  7 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A N20 32                               DSPATR(RI)
     A N20 32                               DSPATR(PC)
     A N32                                  DSPATR(HI)
     A                                 13  1'Loc Description.....:'
     A            #LODE         30A  B 13 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 17  1'Pay Mode/No.........:'
     A            #PMOD          2A  B 17 23
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 17 26'/'
     A                                      DSPATR(HI)
     A            #PYNO         30A  B 17 28
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A                                      DSPATR(HI)
     A            #PVLC         14A  B  9 58
     A  20                                  DSPATR(PR)
     A N20                                  DSPATR(UL)
     A N20 36                               DSPATR(RI)
     A N20 36                               DSPATR(PC)
     A N36                                  DSPATR(HI)
     A*=========================================================================
     A          R GL0001F2
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2012')
     A                                      KEEP
     A                                      OVERLAY
     A                                      WINDOW(&SWROW &SWCOL 22 72 *NOMSGLI-
     A                                      N)
     A                                      USRRSTDSP
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A                                  3  1'Partial Write off:'
     A                                      DSPATR(UL)
     A                                 20  1'F3=Exit'
     A N20                             20 10'F5=Refresh'
     A                                 20 22'F12=Previous'
     A                                  1 21'Asset Write off and Sale Details'
     A                                      DSPATR(HI)
     A                                  3 21'Write off Date:'
     A                                  3 57'Quantity:'
     A                                  4  1'Purch Value for w/f Qty.........-
     A                                      ..:'
     A                                  5  1'Purch Value for w/f Qty(Local Ccy-
     A                                      ):'
     A                                  6  1'Dep Value for w/f Qty............-
     A                                      .:'
     A                                  7  1'Book Value for w/f Qty...........-
     A                                      .:'
     A                                  8  1'Complete Write off:'
     A                                      DSPATR(UL)
     A                                  8 21'Write off Date:'
     A                                  9  1'Purch Value for w/f Qty..........-
     A                                      .:'
     A                                 10  1'Purch Value for w/f Qty(Local Ccy-
     A                                      ):'
     A                                 11  1'Dep Value for w/f Qty............-
     A                                      .:'
     A                                 12  1'Book Value for w/f Qty...........-
     A                                      .:'
     A                                 13  1'Complete Sale:'
     A                                      DSPATR(UL)
     A                                 13 21'Date of Sale..:'
     A                                 14  1'Amount Recv.....................-
     A                                      ..:'
     A                                 15  1'Buyer''s Name...................-
     A                                      ...:'
     A                                 16  1'Buyer''s Address................-
     A                                      ...:'
     A            #BNAM         30A  B 15 37
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A            #BAD1         30A  B 16 37
     A N20 53                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 53                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N53                                  DSPATR(HI)
     A            #BAD2         30A  B 17 37
     A N20 53                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 53                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N53                                  DSPATR(HI)
     A            #PPVQ         14A  B  4 37
     A N20 42                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 42                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N42                                  DSPATR(HI)
     A            #PWQT          5A  B  3 67
     A N20 41                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 41                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N41                                  DSPATR(HI)
     A            #PPLC         14A  B  5 37
     A N20 43                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 43                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N43                                  DSPATR(HI)
     A            #PDQT         14A  B  6 37
     A N20 44                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 44                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N44                                  DSPATR(HI)
     A            #PBQT         14A  B  7 37
     A N20 45                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 45                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N45                                  DSPATR(HI)
     A            #CPVQ         14A  B  9 37
     A N20 47                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 47                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N47                                  DSPATR(HI)
     A            #CPLC         14A  B 10 37
     A N20 48                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 48                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N48                                  DSPATR(HI)
     A            #CDQT         14A  B 11 37
     A N20 49                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 49                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N49                                  DSPATR(HI)
     A            #CBQT         14A  B 12 37
     A N20 50                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 50                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N50                                  DSPATR(HI)
     A            #ARCV         14A  B 14 37
     A N20 52                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 52                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N52                                  DSPATR(HI)
     A            #PWDT          6A  B  3 37
     A N20 40                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 40                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N40                                  DSPATR(HI)
     A            #CWDT          6A  B  8 37
     A N20 46                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 46                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N46                                  DSPATR(HI)
     A            #DTSL          6A  B 13 37
     A N20 51                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 51                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N51                                  DSPATR(HI)
     A                                  6 52'Last Dep Date:'
     A            #PLDD          6A  B  6 67
     A N20 54                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 54                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N54                                  DSPATR(HI)
     A                                 11 52'Last Dep Date:'
     A            #CLDD          6A  B 11 67
     A N20 55                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 55                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N55                                  DSPATR(HI)
     A                                 14 52'Last Dep Date:'
     A            #SLDD          6A  B 14 67
     A N20 56                               DSPATR(RI)
     A N20                                  DSPATR(UL)
     A N20 56                               DSPATR(PC)
     A  20                                  DSPATR(PR)
     A N56                                  DSPATR(HI)
     A                                 18  1'Pay Recv Mode/No................-
     A                                      .:'
     A            #PRMO          2A  B 18 37
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A                                 18 40'/'
     A                                      DSPATR(HI)
     A            #PRNO         30A  B 18 42
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(PR)
     A                                      DSPATR(HI)
     A*=========================================================================
     A*    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      KEEP
     A                                      ASSUME
     A                                      OVERLAY
     A                                      PUTOVR
     A            DUMMYF         1A  O  1 80DSPATR(ND)
     A*=========================================================================
     A          R #MSGRCD                   SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(21)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
     A*=========================================================================
     A          R #MSGCTL                   SFLCTL(#MSGRCD)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0001)
     A                                      WINDOW(GL0001F1)
     A            ##PGM                     SFLPGMQ(10)
     A*=========================================================================
     A          R #MSGRCD2                  SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(21)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
     A*=========================================================================
     A          R #MSGCTL2                  SFLCTL(#MSGRCD2)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0001)
     A                                      WINDOW(GL0001F2)
     A            ##PGM                     SFLPGMQ(10)
