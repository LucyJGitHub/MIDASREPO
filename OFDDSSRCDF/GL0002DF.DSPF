     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas OF Customer Account Balances Enquiry Display')   *
     A*****************************************************************
     A*                                                               *
     A*  Midas - General Ledger Module                                *
     A*                                                               *
     A*  DSPF/GL0002DF - Midas GL Statement Of Accounts               *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2012            *
     A*                                                               *
     A*  Last Amend No. CGL140    *CREATE  Date 12Dec12               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CGL140 - Online Statement                                    *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A*================================================================
     A          R #SFLRCD                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2012')
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #PSDT          7      8  2TEXT('Posting Date')
     A                                      DSPATR(HI)
     A            #VDAT          7        +2TEXT('Value Date')
     A                                      DSPATR(HI)
     A            #CHEQ          8        +2TEXT('Cheque No')
     A                                      DSPATR(HI)
     A            #NARR         30        +2TEXT('Narrative')
     A                                      DSPATR(HI)
     A            #DEBT         21        +2TEXT('Debits')
     A                                      DSPATR(HI)
     A            #CRDT         21        +2TEXT('Credits')
     A                                      DSPATR(HI)
     A            #BAL          22        +2TEXT('Balance')
     A                                      DSPATR(HI)
     A*================================================================
     A          R #SFLCTL                   TEXT('Subfile control.')
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(13) SFLSIZ(14)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys...................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CA09(09 'Print.')
     A                                      CA12(12 'Previous.')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS........................................................
     A                                      SETOF(99 'Global error flag')
     A*................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'GL0002DF'
     A            ##MRDT         7      1117TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 55'Statement of Account'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2116TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYLC         1   H      TEXT('Type of Last Change')
     A                                  4  2'Account'
     A            #ACCT         28        +2TEXT('Account')
     A                                    +5'Account Name 1'
     A            #ANAM         20        +1TEXT('Account Name')
     A                                  6  2'POSTING'
     A                                    +3'VALUE'
     A                                    +3'CHEQUE'
     A                                    +4'NARRATIVE'
     A                                   +23'DEBITS'
     A                                   +17'CREDITS'
     A                                   +16'BALANCE'
     A                                  7  3'DATE'
     A                                    +5'DATE'
     A                                    +6'NO'
     A*================================================================
     A          R #CMDTXT1                  TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=End  F12=Previous  F9=Print'
     A*================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
