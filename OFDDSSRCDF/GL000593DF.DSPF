     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas OF Accounting Rules Process for Nostro Recon')   *
     A*****************************************************************
     A*                                                               *
     A*  Midas - General Ledger Module                                *
     A*                                                               *
     A*  DSPF/GL000593DF - Midas OF Accounting Rules Process for      *
     A*                    Nostro Reconciliation                      *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2013            *
     A*                                                               *
     A*  Last Amend No. MD023629            Date 22Nov13              *
     A*  Prev Amend No. MD023392            Date 12Nov13              *
     A*                 CGL135A  *CREATE    Date 08Oct13              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  MD023629 - Accounting Rules Process (restart process)        *
     A*  MD023392 - Functions keys are incomplete and missing.        *
     A*  CGL135A - Accounting Rules Process                           *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(25)
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Reset')
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*
     A*--------------------------*
     A***  Subfile Record Format *
     A*--------------------------*
     A*
     A          R GL000593S1                SFL
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2013')
      **Start of change MD023629
     A**********                            SETOF(83 'Force input format')
      **End of change MD023629
     A  84                                  SFLNXTCHG
      **Start of change MD023629
     A            #0BATD        60   H      TEXT('Batch ID')
     A            #0NBTF         1   H      TEXT('New Batch Id Flag')
     A            #0TMST        26   H      TEXT('Timestamp')
 
     A            #1ACTN         1A  B  9  3TEXT('OPTION')
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A N70                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A**********  #1CCY          3A  O  9  4TEXT('Currency')
     A            #1CCY          3A  O  9 11TEXT('Currency')
      **End of change MD023629
     A                                      DSPATR(HI)
     A            #1NOSC         2A  O  9 32TEXT('Nostro Code')
     A                                      DSPATR(HI)
     A            #1ANAM        20A  O  9 42TEXT('Account Name')
     A                                      DSPATR(HI)
     A            #1BRCH         3A  O  9 68TEXT('A/C Branch')
     A                                      DSPATR(HI)
     A            #1LSTN         5A  O  9 82TEXT('Last Bank Statement No.')
     A                                      DSPATR(HI)
     A            #1SLSH1        1A  O  9 88TEXT('Slash1')
     A                                      DSPATR(HI)
     A            #1LSTS         5A  O  9 90TEXT('Last Bank Statement Seq.')
     A                                      DSPATR(HI)
     A            #1LDAT         7A  O  9110TEXT('Date Last Initiated')
     A                                      DSPATR(HI)
     A            #1SLSH2        1A  O  9118TEXT('Slash1')
     A                                      DSPATR(HI)
     A            #1LTIM         8A  O  9120TEXT('Time Last Initiated')
     A                                      DSPATR(HI)
      **Start of change MD0023629
     A**********  #1STAT        22A  O 10  2TEXT('Status')
     A            #1STAT        30A  O 10  9TEXT('Status')
      **End of change MD023629
     A                                      DSPATR(HI)
      **Start of change MD023629
     A**********  #1ERCD        10A  O 10 44TEXT('Error Code')
     A            #1ERCD        80A  O 10 44TEXT('Error Desc')
      **End of change MD023629
     A                                      DSPATR(HI)
     A*
     A*--------------------*
     A***  Subfile Control *
     A*--------------------*
     A*
     A          R GL000593C1                SFLCTL(GL000593S1)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      TEXT('Subfile control.')
      **Start of change MD023629
     A**********                            SETOF(83 'Force input format')
      **End of change MD023629
     A N82                                  ROLLUP(27 'Next page.')
      **Start of change MD023629
     A**********                            SETOF(40 'Global error flag')
      **End of change MD023629
     A                                      OVERLAY
      **Start of change MD023629
     A**86******                            PUTOVR
     A                                      PUTOVR
      **End of change MD023629
     A N80 81                               SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 82                            SFLEND
     A            SRLRN          4S 0H      SFLRCDNBR
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'GL000593'
     A                                  1117'DATE '
     A            #1RUNA    R        O  1123REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 34'MONITOR ACCOUNTING RULES PROCESS F-
     A                                      OR NOSTRO RECONCILIATION RESULTS'
     A                                      DSPATR(HI)
     A                                  2117'TIME '
     A            #1TIME    R        O  2123REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
      **Start of change MD023629
     A**********                        6  2'Currency/'
     A                                  6  2'Action'
     A                                  6  9'Currency'
      **End of change MD023629
     A                                  6 28'Nostro Code'
      **Start of change MD023629'
     A**********                        6 42'Account Name/'
     A                                  6 42'Account Name'
      **End of change MD023629
     A                                  6 64'A/C Branch'
     A                                  6 77'Last Bank Stmt. No./Seq.'
     A                                  6107'Date/Time Last Initiated'
      **Start of change MD023629
     A**********                        7  2'Status'
     A**********                        7 44'Error Code'
     A                                  7  2'Code'
     A                                  7  9'Status'
     A                                  7 44'Error Description'
      **End of change MD023629
     A            #1POS1         3A  B  4 12TEXT('Currency')
     A N32                                  DSPATR(HI)
     A N32                                  DSPATR(UL)
     A  32                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
      **Start of change MD023629
     A**********  #1POS2        22A  B  4 31TEXT('Status')
     A            #1POS2        30A  B  4 31TEXT('Status')
      **End of change MD023629
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
      **Start of change MD023629
     A**********  #1POS3         2A  B  4 73TEXT('Nostro Code')
     A            #1POS3         2A  B  4 76TEXT('Nostro Code')
      **End of change MD023629
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      OVRATR
     A                                      OVRDTA
     A            #1POS4         3A  B  4 95TEXT('A/C Branch')
     A N33                                  DSPATR(HI)
     A N33                                  DSPATR(UL)
     A  33                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
     A            #1POS5         1A  B  4127TEXT('Not Initiated Today')
     A N34                                  DSPATR(HI)
     A N34                                  DSPATR(UL)
     A  34                                  DSPATR(RI PC)
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4  2'Currency'
     A                                  4 23'Status'
      **Start of change MD023629
     A**********                        4 60'Nostro Code'
     A                                  4 64'Nostro Code'
      **End of change MD023629
     A                                  4 83'A/C Branch'
     A                                  4106'Not Initiated Today'
     A**Start of change MD023629
     A          R GL000593F3
     A                                      OVERLAY
     A  29                                  CA10(10 'Delete')
     A                                      CA12(12 'Previous')
     A*
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'GL000593'
     A                                  1117'DATE '
     A            #1RUNA    R        O  1123REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 34'MONITOR ACCOUNTING RULES PROCESS F-
     A                                      OR NOSTRO RECONCILIATION RESULTS'
     A                                      DSPATR(HI)
     A                                  2117'TIME '
     A            #1TIME    R        O  2123REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A*
     A                                  6  5'Action Code:'
     A            #2ACTN        20   O  6 40TEXT('Action Code')
     A                                      DSPATR(HI)
     A                                  8  5'Nostro:'
     A            #2NOST         5A  O  8 40TEXT('Nostro')
     A                                      DSPATR(HI)
     A                                  9  5'Account name:'
     A            #2ANAM        20A  O  9 40TEXT('Account Name')
     A                                      DSPATR(HI)
     A                                 10  5'Branch:'
     A            #2BRCH         3A  O 10 40TEXT('Branch')
     A                                      DSPATR(HI)
     A                                 12  5'Last Bank Statement No./Sequence:'
     A            #2LSTN         5A  O 12 40
     A                                      DSPATR(HI)
     A                                 12 46'/'
     A            #2LSTS         5A  O 12 48
     A                                      DSPATR(HI)
     A                                 13  5'Date/Time Last Initiated:'
     A            #2LDAT         7A  O 13 40
     A                                      DSPATR(HI)
     A                                 13 48'/'
     A            #2LTIM         8A  O 13 50
     A                                      DSPATR(HI)
     A                                 15  5'Last Batch Id Received:'
     A            #2BATD        60A  O 15 40TEXT('Batch Id')
     A                                      DSPATR(HI)
     A                                 17  5'Status:'
     A            #2STAT        30   O 17 40TEXT('Status')
     A                                      DSPATR(HI)
     A                                 18  5'Error Description:'
     A            #2ERCD        80   O 18 40TEXT('Error')
     A                                      DSPATR(HI)
      **End of change MD023629
     A*
     A*-----------------*
     A***  Command Line *
     A*-----------------*
     A*
     A          R GL000593F1
     A                                      TEXT('Command keys')
     A                                      OVERLAY
     A                                      PUTOVR
      ** Start of change MD023392
     A**********                       27  1'F3=Exit                    F5=Refr-
     A**********                            esh                                -
     A**********                                              ENTER            -
     A**********                                   Page Up / Page Down'
      **Begin of change MD023629
     A**********                       27  1'F3=Exit'
     A**********                       27 29'F5=Refresh'
     A**********                       27 89'ENTER'
     A**********                       27113'Page Up / Page Down'
      **End of change MD023629
      ** End of change MD023392
      **Start of change MD023629
     A  26                             27  1'F3=Exit'
     A  26                             27 29'F5=Refresh'
     A  26                             27 45'Action Code: E, D, R'
     A  26                             27 89'ENTER'
     A  26                             27113'Page Up / Page Down'
     A  28                             27  1'F3=Exit'
     A  28 29                          27 17'F10=Confirm Delete'
     A  28                             27 40'F12=Previous'
     A  28N29                          27 89'ENTER'
      **End of change MD023629
      *
      *--------------------------*
      ***  Message Record Format *
      *--------------------------*
      *
     A          R GL000593S2                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(25)
     A            MSGKEY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
      *--------------------*
      ***  Message Control *
      *--------------------*
      *
     A          R GL000593C2                TEXT('Program messages')
     A                                      SFLCTL(GL000593S2)
     A                                      SFLPAG(01) SFLSIZ(03)
      **Start of change MD023629
     A**86******                            OVERLAY PUTOVR
      **End of change MD023629
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
