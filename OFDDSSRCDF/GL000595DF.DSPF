      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('MIDAS GL Forecast balance enquiry')                    *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  DSPF/GL000595DF - Midas GL Forecast balance Enquiry          *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL145B *CREATE    Date 28Oct13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL145B - Subsidiaries Daily Sweeping Forecast               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      INDARA
     A                                      CF03(03)
     A                                      CF05(05)
     A                                      PRINT(*LIBL/MPRINT)
      *
     A          R GL000595F1
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2013')
      *
     A            DDUSER    R        O  1  2REFFLD(USER)
     A                                  1 13'GL000595C1'
     A            DDRUNA    R        O  1 72REFFLD(RUNA)
     A            DDWSID    R        O  2  2REFFLD(WSTI)
     A                                  2 25'FORECAST BALANCE ENQUIRY'
     A                                      DSPATR(HI)
     A                                  2 72TIME
     A                                  4  1'Account'
     A N50                                  DSPATR(ND)
     A            DDBRCA         3A  O  4  9TEXT('Branch code')
     A                                      DSPATR(HI)
     A N50                                  DSPATR(ND)
     A            DDCNUM6        6A  O  4 13TEXT('Customer number')
     A                                      DSPATR(HI)
     A N50                                  DSPATR(ND)
     A            DDCCY          3A  O  4 20TEXT('Currency')
     A                                      DSPATR(HI)
     A N50                                  DSPATR(ND)
     A            DDACOD        10A  O  4 24TEXT('Account code')
     A                                      DSPATR(HI)
     A N50                                  DSPATR(ND)
     A            DDACSQ         2A  O  4 35TEXT('Account seq')
     A                                      DSPATR(HI)
     A N50                                  DSPATR(ND)
     A            DDANAM        20A  O  4 38TEXT('Account name')
     A                                      DSPATR(HI)
     A N50                                  DSPATR(ND)
 
     A                                  6  1'Today''s Opening Ledger-
     A                                       Balance'
     A N50                                  DSPATR(ND)
     A            DDCLBL        21A  O  6 43TEXT('Opening balance')
     A N50                                  DSPATR(ND)
     A                                  7  1'Today''s Projected Closing-
     A                                       Cleared Balance'
     A N50                                  DSPATR(ND)
     A            DDPROJC       21A  O  7 43TEXT('Projected balance')
     A N50                                  DSPATR(ND)
     A                                  8  1'Today''s Closing Forecast-
     A                                       Balance'
     A N50                                  DSPATR(ND)
     A            DDFORC        21A  O  8 43TEXT('Forecast balance')
     A N50                                  DSPATR(ND)
     A                                 10  2'VALUE'
     A N50                                  DSPATR(ND)
     A                                 10 12'OPERATION TYPE'
     A N50                                  DSPATR(ND)
     A                                 10 33'NARRATIVE'
     A N50                                  DSPATR(ND)
     A                                 10 64'AMOUNT'
     A N50                                  DSPATR(ND)
      *
     A          R GL000595S1                SFL
     A  95                                  SFLNXTCHG
      *
     A            S1DATE         6A  O 11  2TEXT('Date')
     A            S1OPTY        20A  O 11  9TEXT('Operation type')
     A  51                                  DSPATR(HI)
     A            S1NARR        30A  O 11 30TEXT('Narrative')
     A  51                                  DSPATR(HI)
     A            S1AMNT        19A  O 11 61TEXT('Amount')
     A  51                                  DSPATR(HI)
     A*
     A          R GL000595C1                SFLCTL(GL000595S1)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      CF13(13)
     A                                      CF14(14)
     A                                      CF15(15)
     A                                      CF16(16)
     A                                      OVERLAY
     A**                                    SFLINZ
     A  91                                  SFLCLR
     A N91                                  SFLDSPCTL
     A  92                                  SFLDSP
     A  93                                  SFLEND(*MORE)
     A N93                                  ROLLUP(94)
      *
     A            #1SFRN         4S 0H      SFLRCDNBR
     A                                 23  2'Enter Account'
     A            #1CNUM        10A  B 23 16TEXT('Customer number')
     A                                      DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI)
     A  20                                  DSPATR(PC)
     A            #1CCY          3A  B 23 27TEXT('Currency')
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI)
     A  21                                  DSPATR(PC)
     A            #1ACOD        10A  B 23 31TEXT('Account code')
     A                                      DSPATR(HI)
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(RI)
     A  22                                  DSPATR(PC)
     A            #1ACSQ         2A  B 23 42TEXT('Account seq')
     A                                      DSPATR(HI)
     A N23                                  DSPATR(UL)
     A  23                                  DSPATR(RI)
     A  23                                  DSPATR(PC)
     A            #1BRCA         3A  B 23 45TEXT('Branch code')
     A                                      DSPATR(HI)
     A N24                                  DSPATR(UL)
     A  24                                  DSPATR(RI)
     A  24                                  DSPATR(PC)
     A                                 24  2'Enter Value date'
     A            #1VDAT         6A  B 24 19TEXT('Value date')
     A                                      DSPATR(HI)
     A N25                                  DSPATR(UL)
     A  25                                  DSPATR(RI)
     A  25                                  DSPATR(PC)
     A                                 24 27'Amount'
     A            #1SIGN         1A  B 24 34TEXT('Sign')
     A                                      DSPATR(HI)
     A N26                                  DSPATR(UL)
     A  26                                  DSPATR(RI)
     A  26                                  DSPATR(PC)
     A            #1AMNT        14A  B 24 36TEXT('Amount')
     A                                      DSPATR(HI)
     A N27                                  DSPATR(UL)
     A  27                                  DSPATR(RI)
     A  27                                  DSPATR(PC)
      *
     A          R GL000595S2                TEXT('Program Messages')
     A                                      SFL
     A                                      SFLMSGRCD(21)
     A            #2MSGK                    SFLMSGKEY
     A            #2PGMQ                    SFLPGMQ
      *
     A          R GL000595C2                TEXT('Program Messages')
     A                                      SFLCTL(GL000595S2)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(03)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLINZ
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A            #2PGMQ                    SFLPGMQ
      *
     A          R GL000595F2                OVERLAY
     A                                 22  2'F3=Exit F5=Refresh F13=Acct detail+
     A                                      s F14=Acct Mvmts F15=Proj Mvmts F16+
     A                                      =It Clear'
