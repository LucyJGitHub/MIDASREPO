      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas ME MT940/950 Production on Request Screen')      *
      *****************************************************************
      *                                                               *
      *  Midas - Messages Management Module                           *
      *                                                               *
      *  DSPF/ME000051DF - MT94x Production on Request                *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL146B *CREATE    Date 10Jul13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL146B - MT940/MT950 Production in Input Cycle              *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      CA03(03)
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A          R ME000051F0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'ME000051'
     A  26                              1 30'MT940/950 Production on Request'
     A                                      DSPATR(HI)
     AA 29N30                           1 30'MT940 Production on Request'
     A                                      DSPATR(HI)
     AAN29N30                           1 30'MT950 Production on Request'
     A                                      DSPATR(HI)
     A  30                              1 30'MT940/950 Production on Request'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A            #0TIME    R        O  2 73REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
 
      *-------------------------
      ** Subfile Record Format
      *-------------------------
 
     A          R ME000051S0                SFL
     A  84                                  SFLNXTCHG
     A            #0NATY         1A  H      TEXT('Network Account Type')
     A            #0ANAM        20A  H      TEXT('Account Name')
     A            #0ALLOW        1A  H
     A            #0LSDM         5D 0H
     A            #0ACTN         1A  B 11  4TEXT('Action Code')
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A N40                                  DSPATR(HI)
     A N40                                  DSPATR(UL)
     A            #0NWRK         6A  O 11  7TEXT('Network')
     A                                      DSPATR(HI)
     A            #0ACCT        24A  O 11 15TEXT('Account Number')
     A                                      DSPATR(HI)
     A            #0ACNO        10A  O 11 41TEXT('Retail Account')
     A                                      DSPATR(HI)
     A            #0DSTN        11A  O 11 53TEXT('MT94x Destination')
     A                                      DSPATR(HI)
     A            #0MTPY         3A  O 11 68TEXT('Message Type')
     A                                      DSPATR(HI)
     A                                 12  7'Last Statement Date:'
     A            #0LASD         7A  O 12 28TEXT('Last Statement Date')
     A                                      DSPATR(HI)
     A                                 12 37'Time:'
     A            #0LAST         8A  O 12 43TEXT('Last Statement Time')
     A                                      DSPATR(HI)
     A                                 12 53'Page:'
     A            #0LASP         5A  O 12 59TEXT('Last Statement Page')
     A                                      DSPATR(HI)
     A          R ME000051C0                SFLCTL(ME000051S0)
     A N82                                  ROLLUP(31)
     A  26                                  CA05(05)
     A                                      CA11(11)
     A                                      CA06(06)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A                                      SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 82                            SFLEND
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A            #0RLRN         4S 0H      SFLRCDNBR
     A                                  4  2'Select on:'
     A                                  4 14'Network'
     A            #0NETW         6A  B  4 22TEXT('Network')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5 14'Branch'
     A            #0BRCA         3A  B  5 22TEXT('Branch')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5 27'Customer'
     A            #0CNUM         6A  B  5 36TEXT('Customer')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5 43'Currency'
     A            #0CCY          3A  B  5 52TEXT('Currency')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5 57'A/c Code'
     A            #0ACCD        10A  B  5 66TEXT('Account Code')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5 77'A/c Seq'
     A            #0ACSQ         2A  B  5 85TEXT('Account Sequence')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  6 14'Retail account'
     A            #0REAC        10A  B  6 29TEXT('Retail Account')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 14'Message Type'
     A            #0MSGT         3A  B  7 27TEXT('Message Type')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      VALUES('   ' '940' '950')
     A                                  7 32'Destination'
     A            #0DEST        11A  B  7 44TEXT('MT94x Destination')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9  7'Network'
     A            #0POS1         6A  B 10  7TEXT('Network')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 15'Account Number'
     A            #0POS2        24A  B 10 15TEXT('Account Number')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 41'Retail'
     A                                  9 53'Destination'
     A            #0POS3        11A  B 10 53TEXT('Destination')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  9 68'Msg Type'
     A          R ME000051F1
     A  28                                  CA08(08)
     A                                      CA12(12)
     A                                      OVERLAY
     A            #1MTPY         3A  H      TEXT('Message Type')
     A            #1ALLOW        1A  H
     A                                  5  3'Network:'
     A            #1NWRK         6A  O  5 12TEXT('Network')
     A                                      DSPATR(HI)
     A                                  5 21'Account no.:'
     A            #1ACCT        28A  O  5 34TEXT('Account Number')
     A                                      DSPATR(HI)
     A                                  5 65'Retail:'
     A            #1ACNO        10A  O  5 73TEXT('Retail Account')
     A                                      DSPATR(HI)
     A                                  6  3'Type:'
     A                                      DSPATR(ND)
     A            #1NATY         1A  O  6  9TEXT('Network Account Type')
     A                                      DSPATR(ND)
     A                                  6 12'Name:'
     A            #1ANAM        20A  O  6 18TEXT('Account Name')
     A                                      DSPATR(HI)
     A                                  6 41'Destination:'
     A            #1DSTN        11A  O  6 54TEXT('MT94x Destination')
     A                                      DSPATR(HI)
     A                                 12 10'Field 21: Related Reference.:'
     A            #1RREF        16A  B 12 40TEXT('Related Reference')
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A N42                                  DSPATR(HI)
     A  28                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
     A                                 09 10'Message Type . . . . . . . .:'
     A            #1MSGT         6A  O 09 40DSPATR(HI)
     A                                 10 10'Statement Date . . . . . . .:'
     A            #1STDT         6D  B 10 40TEXT('Statement Date')
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N43                                  DSPATR(HI)
     A  28                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
     A                                 08 10'Last Statement Date. . . . .:'
     A            #1LASD         6A  O 08 40DSPATR(HI)
     A                                 14 10'Field 86:'
     A  70                                  DSPATR(ND)
     A                                 14 20'Information to the Account Owner'
     A  70                                  DSPATR(ND)
     A            #1MIO1        65   B 15 10TEXT('Info to the a/c owner 1')
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(HI)
     A  28
     AO 70                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
     A  70                                  DSPATR(ND)
     A            #1MIO2        65   B 16 10TEXT('Info to the a/c owner 2')
     A  45                                  DSPATR(RI PC)
     A N45                                  DSPATR(HI)
     A  28
     AO 70                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
     A  70                                  DSPATR(ND)
     A            #1MIO3        65   B 17 10TEXT('Info to the a/c owner 3')
     A  46                                  DSPATR(RI PC)
     A N46                                  DSPATR(HI)
     A  28
     AO 70                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
     A  70                                  DSPATR(ND)
     A            #1MIO4        65   B 18 10TEXT('Info to the a/c owner 4')
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(HI)
     A  28
     AO 70                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
     A  70                                  DSPATR(ND)
     A            #1MIO5        65   B 19 10TEXT('Info to the a/c owner 5')
     A  48                                  DSPATR(RI PC)
     A N48                                  DSPATR(HI)
     A  28
     AO 70                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
     A  70                                  DSPATR(ND)
     A            #1MIO6        65   B 20 10TEXT('Info to the a/c owner 6')
     A  49                                  DSPATR(RI PC)
     A N49                                  DSPATR(HI)
     A  28
     AO 70                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
     A  70                                  DSPATR(ND)
     A          R ME000051F2
     A*%%TS  SD  20131009  163620  TFUSER01    REL-V6R1M0  5761-WDS
     A                                      OVERLAY
     A  26                             22  2'X=Request Message'
     A                                 23  2'F3=Main Menu'
     A  26                             23 17'F5=Refresh   F11=Display MT941/942-
     A                                       List   Rollup/Rolldown'
     A  27                             23 17'F12=Previous'
     A  28                             23 17'F8=Submit Request   F12=Previous'
     A  26                             22 22'F6=Select All'
     A          R ME000051F3
     A  28                                  CA08(08)
     A                                      CA12(12)
     A                                      OVERLAY
     A            #3MTPY         3A  H      TEXT('Message Type')
     A                                 14 10'Statement Date . . . . . . .:'
     A            #3STDT         6D  B 14 40TEXT('Related Reference')
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A N43                                  DSPATR(HI)
     A  28                                  DSPATR(PR)
     A N28                                  DSPATR(UL)
      *
      *=====================================================================
      *  Format #WINDOW provides the Changes reminder display.             =
      *=====================================================================
      *
     A          R ME000051W0
     A                                      WINDOW(10 12 5 65)
     A                                      CA12(12)
     A                                  3  3'Display prompt screen for each sel-
     A                                      ected account?. .:'
     A            #WEACH         1A  B  3 56DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DFTVAL('N')
     A                                      VALUES('Y' 'N')
     A                                  3 58'(Y/N)'
     A                                  4 43'ENTER   F12=Cancel'
     A          R ME000051S1                SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ(10)
     A          R ME000051C1                SFLCTL(ME000051S1)
     A                                      TEXT('Program Messages')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0002)
     A                                      SFLPAG(0001)
     A            #1PGMQ                    SFLPGMQ
