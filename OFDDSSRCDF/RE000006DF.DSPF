     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  DSPF/RE000006DF - Midas OF Modelo 123 Request Screen Display *
     A*                                                               *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2015            *
     A*                                                               *
     A*  Last Amend No. CRE104   *CREATE    Date 04Mar15              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CRE104 - Modelo 123 and 193 Reports                          *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      CA03(03 'Exit')
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A          R RE000006F1
     A                                      OVERLAY
     A                                      SETOF(40 'GLOBAL ERROR FLAG')
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 14'RE000006'
     A                                  1 60'DATE '
     A            #1RUNA    R        O  1 66REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 30'Modelo 123 Selection'
     A                                      DSPATR(HI)
     A                                  2 60'TIME '
     A            #1TIME    R        O  2 66REFFLD(JOTIME)
     A*
     A                                  6 31'Branch. . :'
     A            #1BRCA         3A  B  6 43TEXT('Branch')
     A N28                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  28                                  DSPATR(RI PC)
     A                                  8 10'Reporting Month and Year :'
     A            #1MNTH         2A  B  8 43TEXT('Month')
     A N29                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  29                                  DSPATR(PC)
     A  29                                  DSPATR(RI)
     A                                  8 45'/'
     A            #1YEAR         4A  B  8 46TEXT('Year')
     A N29                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  29                                  DSPATR(PC)
     A  29                                  DSPATR(RI)
     A                                  8 51'(MM / YYYY)'
     A                                 10 10'Tax Return Identifier  . . . . :'
     A            #1TRID        13A  B 10 43TEXT('Tax Return Identifier')
     A N30                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                 12 10'Amount from Previous Tax Return:'
     A            #1AMNT        18A  B 12 43TEXT('Amount from Prev. Tax')
     A N31                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                 14 10'Complementary Tax Return . . . :'
     A            #1RTRN         1A  B 14 43TEXT('Complemetary Tax Return')
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A                                 14 45'(Y/N)'
     A                                 16 10'Previous Tax Return Identifier :'
     A            #1PTID        13A  B 16 43TEXT('Prev. Tax Return Identifier')
     A N33                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A                                 18 10'Contact Person . .:'
     A            #1CNAM        40A  B 18 30TEXT('Contact Person')
     A                                      CHECK(LC)
     A N34                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PC RI)
     A                                 20 10'Contact Telephone :'
     A            #1PHON         9A  B 20 30TEXT('Contact Telephone')
     A N35                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  35                                  DSPATR(PC RI)

     A*
     A*-----------------*
     A***  Command Line *
     A*-----------------*
     A*
     A          R RE000006F2
     A*%%TS  SD  20150310  161905  TXUSER01    REL-V6R1M0  5761-WDS
     A                                      TEXT('Command keys')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  3'Press Enter to Process'
     A                                 23 66'F3=Exit'
      *
      *--------------------------*
      ***  Message Record Format *
      *--------------------------*
      *
     A          R RE000006S1                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
      *--------------------*
      ***  Message Control *
      *--------------------*
      *
     A          R RE000006C1                TEXT('Program messages')
     A                                      SFLCTL(RE000006S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
