     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  DSPF/RE000008DF - Midas OF Modelo 193 Request Screen Display *
     A*                                                               *
     A*                                                               *
     A*  (c) Misys International Banking Systems Ltd. 2015            *
     A*                                                               *
     A*  Last Amend No. CRE104   *CREATE    Date 04Mar15              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CRE104 - Modelo 123 and 193 Reports                          *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03(03 'Exit')
     A          R RE000008F1
     A                                      SETOF(40 'GLOBAL ERROR FLAG')
     A                                      OVERLAY
     A*
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'RE000008'
     A                                  1 67'DATE'
     A            #1RUNA    R        O  1 72REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 32'Modelo 193 Selection'
     A                                      DSPATR(HI)
     A                                  2 67'TIME'
     A            #1TIME    R        O  2 72REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A*
     A                                  8 11'Reporting Year . . . . . . . . . .-
     A                                       . :'
     A            #1YEAR         4A  B  8 51TEXT('Year')
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 10 11'Tax Return Identifier  . . . . . .-
     A                                       . :'
     A                                 12 11'Previous Tax Return Identifier . .-
     A                                       . :'
     A                                 14 11'Replacement Tax Return . . . . . .-
     A                                       . :'
     A            #1RTRN         1A  B 14 51TEXT('Replacement Tax Return')
     A N34                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A                                 14 54'(Y/N)'
     A                                  8 57'(YYYY)'
     A            #1TRID        13A  B 10 51TEXT('Tax Return Identifier')
     A N32                                  DSPATR(HI)
     A  32                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PC)
     A            #1PTID        13A  B 12 51TEXT('Previous Tax Return Identifie-
     A                                      r')
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A  33                                  DSPATR(PC)
     A                                  6 36'Branch  . . :'
     A            #1BRCA         3A  B  6 51TEXT('Branch Code')
     A N30                                  DSPATR(HI)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 16 11'Contact Person  . . . :'
     A                                 18 11'Contact Telephone . . :'
     A            #1CONP        40A  B 16 36TEXT('Contact Person')
     A N35                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A                                      CHECK(LC)
     A            #1CONT         9A  B 18 36TEXT('Contact Telephone')
     A N36                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A*
     A*-----------------*
     A***  Command Line *
     A*-----------------*
     A*
     A          R RE000008F2
     A                                      TEXT('Command keys')
     A                                      OVERLAY
     A                                      PROTECT
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  5'Press Enter to Process'
     A                                 23 49'F3=Exit'
     A*
     A*--------------------------*
     A***  Message Record Format *
     A*--------------------------*
     A*
     A          R RE000008S1                SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ(10)
     A*
     A*--------------------*
     A***  Message Control *
     A*--------------------*
     A*
     A          R RE000008C1                SFLCTL(RE000008S1)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  25
     AON25                                  SFLEND
     A                                      SFLSIZ(0003)
     A                                      SFLPAG(0001)
     A            #1PGMQ                    SFLPGMQ(10)
