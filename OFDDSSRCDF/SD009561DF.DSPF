     A*****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas SD Forecast Static Data Maintenance')            *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Standing Data Module                                 *
     A*                                                               *
     A*  DSPF/SD009561DF - Forecast Static Data Display File          *
     A*                                                               *
     A*  (c) Finastra International Limited 2013                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD023479           Date 15Nov13               *
      *                 MD023372           Date 13Nov13               *
     A*                 CGL145  *CREATE    Date 10Jul13               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  MD023479  - Action codes still appear on detail screen       *
     A*  MD023372 - Incomplete text for footer when multi-language is *
     A*             supported                                         *
     A*  CGL145 - Forecast Static Data                                *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      CA03(03 'Exit')
     A N38N39                               CA05(05 'Reset')
     A  39                                  CA10(10 'Delete')
     A                                      CA12(12 'Previous')
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      *--------------------------*
      ***  Subfile Record Format *
      *--------------------------*
      *
     A          R SD009561S1                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2013')
     A                                      SFL
     A                                      SETOF(83 'Force input format')
     A  84                                  SFLNXTCHG
     A            #1ACTN         1A  B  8  2TEXT('Action Code')
     A  79                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A  79N32                               DSPATR(ND)
     A N79N32                               DSPATR(HI UL)
     A            #1CUST         6   O  8  5TEXT('Customer Number')
     A                                      DSPATR(HI)
     A            #1CSSN        10   O  8 14TEXT('Customer Shortname')
     A                                      DSPATR(HI)
     A            #1CSRC        34   O  8 28TEXT('Customer Source Account')
     A                                      DSPATR(HI)
     A            #1CURR         3   O  8 67TEXT('Currency')
     A                                      DSPATR(HI)
     A            #1NOTR         2   O  8 77TEXT('Nostro Code')
     A                                      DSPATR(HI)
     A            #1DLMT        16   O  8 84TEXT('Nostro Daily Limit')
     A                                      DSPATR(HI)
     A            #1RETL        24   O  8103TEXT('Retail Account')
     A                                      DSPATR(HI)
     A            #1NARR        30   H      TEXT('Narrative')
     A            #1ACSA        34   H      TEXT('Alt. Cust. Source A/C')
     A            #1RNAM        20   H      TEXT('Retail Account Name')
     A            #1BRCD         3   H      TEXT('Branch Code')
      *
      *--------------------*
      ***  Subfile Control *
      *--------------------*
      *
     A          R SD009561C1                TEXT('Subfile control.')
     A                                      SFLCTL(SD009561S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      SETOF(83 'Force input format')
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N80 81                               SFLDSP
     A N80 81 82                            SFLEND
     A N82                                  ROLLUP(27 'Next page.')
     A N31                                  CF09(09 'Change mode.')
     A                                      SETOF(40 'Global error flag')
     A            SRLRN          4  0H      SFLRCDNBR
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD009561'
     A                                  1117'DATE '
     A            #1RUNA    R        O  1123REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A N31                              2 51'FORECAST STATIC DATA MAINTENANCE'
     A                                      DSPATR(HI)
     A  31                              2 53'FORECAST STATIC DATA ENQUIRY'
     A                                      DSPATR(HI)
     A                                  2117'TIME '
     A            #1TIME    R        O  2123REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4  4'Customer'
     A                                  4 14'Customer'
     A                                  4 28'Customer Source Account'
     A                                  4 65'Nostro'
     A                                  4 73'Nostro Code'
     A                                  4 89'Nostro'
     A                                  4103'Retail Account'
     A                                  5 14'Shortname'
     A                                  5 64'Currency'
     A                                  5 87'Daily Limit'
     A            #1POS1         6A  B  6  5TEXT('Customer Number')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A            #1POS2        10A  B  6 14TEXT('Customer Shortname')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A            #1POS3        34A  B  6 28TEXT('Customer Source Account')
      **Start of change MD023372
     A                                      CHGINPDFT(LC)
      **End of change MD023372
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A            #1POS4         3A  B  6 67TEXT('Currency')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A            #1POS5         2A  B  6 77TEXT('Nostro Code')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A            #1POS6        24A  B  6103TEXT('Retail Account')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
      *
      *--------------------------------------*
      ***  Forecast Static Data Input Screen *
      *--------------------------------------*
      *
     A          R SD009561D1                TEXT('Forecast Static Data nput')
     A                                      OVERLAY
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD009561'
     A                                  1117'DATE '
     A            #1RUNA    R        O  1123REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 57'FORECAST STATIC DATA'
     A                                      DSPATR(HI)
     A                                  2117'TIME '
     A            #1TIME    R        O  2123REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                  5  6'Mode....................:'
     A            #1MODE        10   O  5 32DSPATR(RI)
     A                                  7  6'Customer................:'
     A            #1CUST2       10A  B  7 32TEXT('Customer Number')
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(HI)
     A  70N71N73                            DSPATR(UL)
     A N70
     AO 71
     AO 73                                  DSPATR(PR)
     A            #1CRNM        20   O  7 43TEXT('Customer Report Name')
     A                                      DSPATR(HI)
     A                                  8  6'Cust. Source A/C........:'
     A            #1CSRC        34A  B  8 32TEXT('Customer Source Account')
     A                                      CHGINPDFT(LC)
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(HI)
     A  70N71N73                            DSPATR(UL)
     A N70
     AO 71
     AO 73                                  DSPATR(PR)
     A                                  9  6'Currency................:'
     A            #1CURR         3A  B  9 32TEXT('Currency')
     A  43                                  DSPATR(RI PC)
     A N43                                  DSPATR(HI)
     A  70N71N73                            DSPATR(UL)
     A N70
     AO 71
     AO 73                                  DSPATR(PR)
     A                                 10  6'Nostro Code.............:'
     A            #1NOTR         2A  B 10 32TEXT('Nostro Code')
     A  44                                  DSPATR(RI PC)
     A N44                                  DSPATR(HI)
     A  70N71N73                            DSPATR(UL)
     A N70
     AO 71
     AO 73                                  DSPATR(PR)
     A            #1NNAM        20   O 10 35DSPATR(HI)
     A            #1CSSN        10   O  7 64DSPATR(HI)
     A*
     A*------------------------*
     A***  Insert/Amend Screen *
     A*------------------------*
     A*
     A          R SD009561D2
     A                                      TEXT('Forecast Static Data Details')
     A                                      OVERLAY
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'SD009561'
     A                                  1117'DATE '
     A            #1RUNA    R        O  1123REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(JOJOB)
     A N31                              2 50'FORECAST STATIC DATA MAINTENANCE '
     A                                      DSPATR(HI)
     A  31                              2 52'FORECAST STATIC DATA ENQUIRY '
     A                                      DSPATR(HI)
     A                                  2117'TIME '
     A            #1TIME    R        O  2123REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                  5  6'Mode....................:'
     A            #2MODE        10A  O  5 32DSPATR(RI)
     A                                  7  6'Customer................:'
     A            #2CUST        10A  O  7 32TEXT('Customer Number')
     A                                      DSPATR(HI)
     A            #2CRNM        20A  O  7 43TEXT('Customer Report Name')
     A                                      DSPATR(HI)
     A            #2CSSN        10A  O  7 64TEXT('Customer Shortname')
     A                                      DSPATR(HI)
     A                                  8  6'Cust. Source A/C........:'
     A            #2CSRC        34A  O  8 32TEXT('Customer Source Account')
     A                                      DSPATR(HI)
     A                                  9  6'Currency................:'
     A            #2CURR         3A  O  9 32TEXT('Currency')
     A                                      DSPATR(HI)
     A                                 10  6'Nostro Code.............:'
     A            #2NOTR         2A  O 10 32TEXT('Nostro Code')
     A                                      DSPATR(HI)
     A            #2NNAM        20A  O 10 35TEXT('Nostro Name')
     A                                      DSPATR(HI)
     A                                 13  6'Nostro Daily Limit......:'
     A            #2DLMT        16A  B 13 32TEXT('Nostro Daily Limit')
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A N45                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
     A                                 14  6'Retail Account..........:'
     A            #2RETL        24A  B 14 32TEXT('Retail Account')
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A N46                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
     A            #2RNAM        20A  O 14 57TEXT('Retail Account Name')
     A                                      DSPATR(HI)
     A                                 15  6'Narrative...............:'
     A            #2NARR        30A  B 15 32TEXT('Narrative')
     A                                      DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
     A                                 16  6'Alt. Cust. Source A/C...:'
     A            #2ACSA        34A  B 16 32TEXT('Alt. Cust. Source A/C')
     A                                      CHGINPDFT(LC)
     A  47                                  DSPATR(RI PC)
     A N47                                  DSPATR(HI)
     A  55                                  DSPATR(UL)
     A N55                                  DSPATR(PR)
      *
      *-----------------*
      ***  Command Line *
      *-----------------*
      *
     A          R SD009561F1                TEXT('Command keys')
     A                                      OVERLAY PUTOVR
     A  37                             26  3'F3=Main menu     F5=Refresh       -
     A                                      F12=Previous'
     A  39                             26  3'F3=Main menu      F10=Delete      -
     A                                          F12=Previous'
     A  38                             26  3'F3=Main menu            F12=Previo-
     A                                      us'
      **Start of change MD023372
     A**31                             26  3'F3=Exit              F5=Refresh   -            MD023372
     A**********                                      Action Code: E           -            MD023372
     A**********                                      Enter                    -            MD023372
     A**********                               Page Up / Page Down'                         MD023372
      ** Start of change MD023479
     A*N31******                       26  3'F3=Exit           F5=Refresh      -            MD023479
     A**********                                  F9=Insert            Action C-            MD023479
     A**********                            odes: A, E, D           Enter      -            MD023479
     A**********                               Page Up / Page Down'                         MD023479
     A*N31******                       26  3'F3=Exit           F5=Refresh      -            MD023372
     A**********                                  F9=Insert            Action C-            MD023372
     A**********                            odes: A, E, D           Enter      -            MD023372
     A**********                               Page Up / Page Down'                         MD023372
     A*N31******                       26  3'F3=Exit           F5=Refresh      -   MD023372 MD023479
     A**********                                  F9=Insert'                       MD023372 MD023479
     A*N31******                       26 64'Action Codes: A, E, D           En-   MD023372 MD023479
     A**********                            ter         Page Up / Page Down'       MD023372 MD023479
     A N31                                                                                  MD023479
     AAN37N38N39                       26  3'F3=Exit           F5=Refresh'                  MD023479
     A N31                                                                                  MD023479
     AAN37N38N39                       26 43'F9=Insert'                                     MD023479
     A N31                                                                                  MD023479
     AAN37N38N39                       26 64'Action Codes: A, E, D'                         MD023479
     A N31                                                                                  MD023479
     AAN37N38N39                       26 96'Enter          Page Up / Page -                MD023479
     A                                      Down'                                           MD023479
      ** End of change MD023479
      **End of change MD023372
      *
      *--------------------------*
      ***  Message Record Format *
      *--------------------------*
      *
     A          R SD009561S2                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
      *
      *--------------------*
      ***  Message Control *
      *--------------------*
      *
     A          R SD009561C2                TEXT('Program messages')
     A                                      SFLCTL(SD009561S2)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
