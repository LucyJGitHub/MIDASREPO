      *****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas OF Nostro Group Transaction Limit Display')      *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD009562DF - Midas OF Nostro Group Transaction Limit    *
      *                                                               *
      *  (c) Finastra International Limited 2014                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD026948           Date 19May14               *
      *                 CGL155A            Date 24Apr14               *
      *                 CGL155  *CREATE    Date 06Mar14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD026948 - F12 is not available                              *
      *  CGL155A - Forecast Group Limits                              *
      *  CGL155 - Forecast Group Limits                               *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      CA03(03 'Exit')
     A  39                                  CA10(10 'Delete')
      **Start of change MD026948
     A  88                                  CA12(12 'Cancel')
      **End of change MD026948
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*
     A*--------------------------*
     A***  Subfile Record Format *
     A*--------------------------*
     A*
     A          R SD009562S1                SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2014')
     A                                      SETOF(83 'Force input format')
     A            S1ACTN         1A  B  8 19TEXT('Action Code')
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A            S1NOGRP       10A  O  8 30TEXT('Nostro Group')
     A                                      DSPATR(HI)
     A            S1NODES       20A  O  8 50TEXT('Nostro Group Description')
     A                                      DSPATR(HI)
     A            S1DLMT        16A  O  8 80TEXT('Daily Limit')
     A                                      DSPATR(HI)
     A            S1CURR         3A  O  8106TEXT('Currency')
     A                                      DSPATR(HI)
     A*
     A*--------------------*
     A***  Subfile Control *
     A*--------------------*
     A*
     A          R SD009562C1                SFLCTL(SD009562S1)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(83 'Force input format')
     A N82                                  ROLLUP(27 'Next page.')
     A N31                                  CF09(09 'Change mode.')
     A                                      SETOF(40 'Global error flag')
     A                                      OVERLAY
     A  86                                  PUTOVR
     A N80 81                               SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 82                            SFLEND
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A            SRLRN          4S 0H      SFLRCDNBR
     A                                  1 13'SD009562'
     A                                  1117'DATE '
     A            S1RUNA    R        O  1123REFFLD(RUNA)
     A            S1WSID    R        O  2 13REFFLD(JOJOB)
     A N31                              2 51'NOSTRO GROUPS TRANSACTION LIMITS'
     A                                      DSPATR(HI)
     A                                  2117'TIME '
     A            S1TIME    R        O  2123REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4  8'Nostro Group'
     A                                  6 14'Action Code'
     A                                  6 30'Nostro Group'
     A                                  6 50'Description'
     A                                  6 80'Daily Limit'
     A                                  6104'Currency'
     A                                  7 16'(A,E,D)'
     A            S1POS1        10A  B  4 23TEXT('Nostro Group')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      OVRATR
     A                                      OVRDTA
     A*--------------------------*
     A***  Subfile Record Format *
     A*--------------------------*
     A*
     A          R SD009562S2                SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2014')
     A                                      SETOF(83 'Force input format')
     A            S2NOSTH        5A  H
     A            S2NOST         5A  B 10 19TEXT('Nostro')
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A  38
     AO 39                                  DSPATR(PR)
     A  36
     AO 37                                  DSPATR(UL)
     A            S2ACNM        20A  O 10 34TEXT('Account Name')
     A                                      DSPATR(HI)
     A            S2RPTN        20A  O 10 64TEXT('Report Name')
     A                                      DSPATR(HI)
     A            S2RPTT        16A  O 10 94TEXT('Report Town')
     A                                      DSPATR(HI)
     A*--------------------*
     A***  Subfile Control *
     A*--------------------*
     A*
     A          R SD009562C2                SFLCTL(SD009562S2)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(83 'Force input format')
     A                                      SETOF(40 'Global error flag')
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  90                                  SFLCLR
     A N90                                  SFLDSPCTL
     A N90 91                               SFLDSP
     A N90 91 92                            SFLEND
     A N92                                  ROLLUP(27 'Next page.')
     A            SRLRN2         4S 0H      SFLRCDNBR
     A            S2POS1         5A  H      TEXT('Nostro Code')
     A                                  1 13'SD009562'
     A                                  1117'DATE '
     A            S2RUNA    R        O  1123REFFLD(RUNA)
     A            S2WSID    R        O  2 13REFFLD(JOJOB)
     A N31                              2 51'NOSTRO GROUPS TRANSACTION LIMITS'
     A                                      DSPATR(HI)
     A                                  2117'TIME '
     A            S2TIME    R        O  2123REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4109'Mode'
     A                                  8 19'Nostro'
     A                                  8 34'Account Name'
     A                                  8 64'Report Name'
     A                                  8 94'Report Town'
     A*
     A                                  5 23'Nostro Group'
     A            S2NOGRP       10A  B  5 38TEXT('Nostro Group')
     A N41                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N36                                  DSPATR(PR)
     A  36                                  DSPATR(UL)
     A                                  5 72'Description'
     A            S2NODES       20A  B  5 86TEXT('Description')
     A N42                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A  42                                  DSPATR(RI)
     A  36
     AO 37                                  DSPATR(UL)
     A  42                                  DSPATR(PC)
     A  38
     AO 39                                  DSPATR(PR)
     A                                  6 24'Daily Limit'
     A            S2DLMT        16A  B  6 38TEXT('Daily Limit')
     A N43                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A  43                                  DSPATR(RI)
     A  36
     AO 37                                  DSPATR(UL)
     A  43                                  DSPATR(PC)
     A  38
     AO 39                                  DSPATR(PR)
     A                                  6 75'Currency'
     A            S2CURR         3A  B  6 86TEXT('Currency')
     A N44                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A  44                                  DSPATR(RI)
     A  36
     AO 37                                  DSPATR(UL)
     A  44                                  DSPATR(PC)
     A  38
     AO 39                                  DSPATR(PR)
     A            S2MODE        10A  O  4114TEXT('Mode')
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      *
      *--------------------------------------*
      ***  Nostro Group Transaction Insert
      *--------------------------------------*
      *
      *-----------------*
      ***  Command Line *
      *-----------------*
      *
     A          R SD009562F1
     A                                      OVERLAY PUTOVR
      **Start of change CGL155A
     A**********                       26  3'F3 Exit'
     A                                 26  3'F3=Exit'
      **End of change CGL155A
      **Start of change MD026948
     A  88                             26 13'F12=Cancel'
      **End of change MD026948
     A  39                             26 99'F10=Delete'
     A N31
      **Start of change CGL155A
     A***AN89***                          26 19'F9 Insert'
      **Start of change MD026948
     A***AN89***                          26 19'F9=Insert'                                  MD026948
     AAN89                             26 26'F9=Insert'
      **End of change MD026948
      **End of change CGL155A
     A
     A                                 26 52'ENTER'
     A
     A                                 26 77'Page Up / Page Down'
      *
      *--------------------------*
      ***  Message Record Format *
      *--------------------------*
      *
     A          R SD009562S3                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            S1PGMQ                    SFLPGMQ
      *
      *--------------------*
      ***  Message Control *
      *--------------------*
      *
     A          R SD009562C3                TEXT('Program messages')
     A                                      SFLCTL(SD009562S3)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            S1PGMQ                    SFLPGMQ
