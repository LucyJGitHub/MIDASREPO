      *****************************************************************
/*STDA*  DSPFBASE                                                     *
/*EXI *  TEXT('Midas OF MNCP Pools Maintenance Display File')         *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  DSPF/SD009563DF - Midas OF MNCP Pools Maintenance            *
      *                                                               *
      *  (c) Finastra International Limited 2014                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL176             Date 20Jul15               *
      *                 CGL159  *CREATE    Date 27Aug14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL176 - Optimisation of Nostro Balances by Company          *
      *  CGL159 - Optimisation of Cash Positions                      *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      CA03(03 'Exit')
     A  39                                  CA10(10 'Delete')
     A  88                                  CA12(12 'Cancel')
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
     A*
     A*--------------------------*
     A***  Subfile Record Format *
     A*--------------------------*
     A*
     A          R SD009563S1                SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2014')
     A                                      SETOF(83 'Force input format')
     A            S1ACTN         1A  B  8 19TEXT('Action Code')
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A N32                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A            S1POOL        10A  O  8 30TEXT('Pool')
     A                                      DSPATR(HI)
     A            S1PDES        30A  O  8 50TEXT('Description')
     A                                      DSPATR(HI)
     A            S1PCUR         3A  O  8 90TEXT('Pivot Currency')
     A                                      DSPATR(HI)
     A*
     A*--------------------*
     A***  Subfile Control *
     A*--------------------*
     A*
     A          R SD009563C1                SFLCTL(SD009563S1)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(83 'Force input format')
     A N82                                  ROLLUP(27 'Next page.')
     A N31                                  CF09(09 'Change mode.')
     A                                      SETOF(40 'Global error flag')
     A                                      OVERLAY
     A  86                                  PUTOVR
     A N80 81                               SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81 82                            SFLEND
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A            SRLRN          4S 0H      SFLRCDNBR
     A                                  1 13'SD009563'
     A                                  1117'DATE '
     A            S1RUNA    R        O  1123REFFLD(RUNA)
     A            S1WSID    R        O  2 13REFFLD(JOJOB)
     A N31                              2 51'MNCP Pools'
     A                                      DSPATR(HI)
     A                                  2117'TIME '
     A            S1TIME    R        O  2123REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4 17'Pool'
     A                                  6 14'Action Code'
     A                                  6 30'Pool'
     A                                  6 50'Description'
     A                                  6 85'Pivot Currency'
     A                                  7 16'(A,E,D)'
     A            S1POLS        10A  B  4 23TEXT('Pool Search')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      OVRATR
     A                                      OVRDTA
     A*--------------------------*
     A***  Subfile Record Format *
     A*--------------------------*
     A*
     A          R SD009563S2                SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2014')
     A                                      SETOF(83 'Force input format')
     A            S2NOSTH        5A  H
     A            S2NOST         5A  B 10 19TEXT('Nostro')
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A  38
     AO 39                                  DSPATR(PR)
     A  36
     AO 37                                  DSPATR(UL)
     A            S2ACNM        20A  O 10 34TEXT('Account Name')
     A                                      DSPATR(HI)
     A            S2RPTN        20A  O 10 64TEXT('Report Name')
     A                                      DSPATR(HI)
     A            S2RPTT        16A  O 10 94TEXT('Report Town')
     A                                      DSPATR(HI)
      **Start of Change CGL176
     A            S2CMPY         3A  O 10120TEXT('Company Code')
     A N15                                  DSPATR(HI)
     A  15                                  DSPATR(ND)
      **End of Change CGL176
     A*--------------------*
     A***  Subfile Control *
     A*--------------------*
     A*
     A          R SD009563C2                SFLCTL(SD009563S2)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      TEXT('Subfile control.')
     A                                      SETOF(83 'Force input format')
     A                                      SETOF(40 'Global error flag')
     A                                      OVERLAY
     A  86                                  PUTOVR
     A  90                                  SFLCLR
     A N90                                  SFLDSPCTL
     A N90 91                               SFLDSP
     A N90 91 92                            SFLEND
     A N92                                  ROLLUP(27 'Next page.')
     A            SRLRN2         4S 0H      SFLRCDNBR
     A            S2POOL        10A  H      TEXT('Pool Code')
     A                                  1 13'SD009563'
     A                                  1117'DATE '
     A            S2RUNA    R        O  1123REFFLD(RUNA)
     A            S2WSID    R        O  2 13REFFLD(JOJOB)
     A N31                              2 51'MNCP Pools'
     A                                      DSPATR(HI)
     A                                  2117'TIME '
     A            S2TIME    R        O  2123REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      OVRATR
     A                                      OVRDTA
     A                                  4109'Mode'
     A                                  8 19'Nostro'
     A                                  8 34'Account Name'
     A                                  8 64'Report Name'
     A                                  8 94'Report Town'
     A*
     A                                  5 24'Pool'
     A            S2POLS        10A  B  5 41TEXT('Pool')
     A N41                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A N36                                  DSPATR(PR)
     A  36                                  DSPATR(UL)
     A                                  5 62'Description'
     A            S2PDES        30A  B  5 76TEXT('Description')
     A N42                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A  42                                  DSPATR(RI)
     A  36
     AO 37                                  DSPATR(UL)
     A  42                                  DSPATR(PC)
     A  38
     AO 39                                  DSPATR(PR)
     A                                  6 24'Pivot Currency'
     A            S2PCUR         3A  B  6 41TEXT('Pivot Currency')
     A N44                                  DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
     A  44                                  DSPATR(RI)
     A  36
     AO 37                                  DSPATR(UL)
     A  44                                  DSPATR(PC)
     A  38
     AO 39                                  DSPATR(PR)
     A            S2MODE        10A  O  4114TEXT('Mode')
     A                                      DSPATR(HI)
     A                                      OVRATR
     A                                      OVRDTA
      ** Start of Change CGL176
     A                                  8118'Company'
     A  15                                  DSPATR(ND)
      ** End of Change CGL176
      *
      *--------------------------------------*
      ***  MNCP pool Transaction Insert
      *--------------------------------------*
      *
      *-----------------*
      ***  Command Line *
      *-----------------*
      *
     A          R SD009563F1
      *
     A                                      OVERLAY PUTOVR
      **Start of change CGL155A
     A                                 26  3'F3=Exit'
     A  88                             26 13'F12=Cancel'
     A  39                             26 99'F10=Delete'
     A N31
     AAN89                             26 26'F9=Insert'
     A
     A                                 26 52'ENTER'
     A
     A                                 26 77'Page Up / Page Down'
      *--------------------------*
      ***  Message Record Format *
      *--------------------------*
      *
     A          R SD009563S3                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            S1PGMQ                    SFLPGMQ
      *
      *--------------------*
      ***  Message Control *
      *--------------------*
      *
     A          R SD009563C3                TEXT('Program messages')
     A                                      SFLCTL(SD009563S3)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            S1PGMQ                    SFLPGMQ
