      *****************************************************************
/*S*D****PRTFBASE******************************************************                     MD029739
/*E*S****PR_STD60******************************************************                     MD029739
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000583P1 - Midas OF Missing Accounting Rules List     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. MD029739*REDUNDANT Date 15Aug14               *
      *  Prev Amend No. MD024772           Date 03Feb14               *
      *                 CGL135A  *CREATE   Date 10Jul13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD029739 - File renaming for integration layer.              *
      *  MD024772 - Problems with Accounting Interface postings and   *
      *             enquiries.                                        *
      *  CGL135A - Accounting Rules Process                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER1
     A                                      SKIPB(001)
     A                                     1
     A                                      'Reference GL000583P1'
     A                                      SPACEB(001)
     A            RRPTDS        53A  O    40
     A                                   +26
     A                                      'Date'
     A            BJMRDT    R    7   O    +1
     A                                      REFFLD(MRDT MFRFL)
     A                                   119
     A                                      'Page'
     A                                      SPACEB(001)
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
      *----------------------*
      ***  Report Header     *
      *----------------------*
      *
     A          R HEADER2
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    52
     A                                      'MISSING ACCOUNTING RULES LIST'
     A                                    52
     A                                      '-----------------------------'
     A                                      SPACEB(001)
      *
      *-----------------*
      ***  Details      *
      *-----------------*
      *
     A          R DETAIL
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      'Batch ID'
     A            PABTID        60A  O    +1
     A                                    +7
     A                                      'Status'
     A            PASTAT        45A  O    +1
     A                                     1
     A                                      'Booking Branch'
     A                                      SPACEB(001)
     A            PABKBR         3A  O    +1
     A                                    +1
     A                                      'Deparment'
     A            PADPCD         3A  O    +1
     A                                    +1
     A                                      'Currency'
     A            PACCY          3A  O    +1
     A                                    +2
     A                                      'Event Amount'
     A            PAEVTA        19A  O    61
     A                                    +1
     A                                      'DR/CR'
     A            PADCIP         1A  O    87
     A                                    +2
     A                                      'Posting Date'
     A            PAPSTD         6S 0O    +1
     A                                      EDTWRD('  /  /  ')
     A                                    +3
     A                                      'Event Date'
     A            PAEVTD         6S 0O    +1
     A                                      EDTWRD('  /  /  ')
     A                                     2
     A                                      'Narrative'
     A                                      SPACEB(001)
     A            PANARR        30A  O    +1
     A            PANAR2        65A  O    12
     A                                      SPACEB(001)
     A            PANAR3        65A  O    12
     A                                      SPACEB(001)
     A            PANAR4        65A  O    12
     A                                      SPACEB(001)
     A            PANAR5        65A  O    12
     A                                      SPACEB(001)
     A            PANAR6        65A  O    12
     A                                      SPACEB(001)
     A            PANAR7        65A  O    12
     A                                      SPACEB(001)
     A                                     1
     A                                      'Sett A/c DR'
     A                                      SPACEB(001)
     A            PASADR        24A  O    +1
     A                                    40
     A                                      'Sett A/c CR'
     A            PASACR        24A  O    +1
     A                                     1
     A                                      'Customers:'
     A                                      SPACEB(001)
     A                                    +1
     A                                      'Counterparty'
     A            PACTPY        10A  O    +1
     A                                    +3
     A                                      'Depot'
     A            PADPCN        10A  O    +1
     A                                    +3
     A                                      'Issuing'
     A            PAISCN        10A  O    +1
     A                                    +3
     A                                      'Associated'
     A            PAASCN        10A  O    +1
     A                                     1
     A                                      'MatchField1'
     A                                      SPACEB(001)
     A            PASMT1        25A  O    +1
     A                                    +2
     A                                      'MatchField2'
     A            PASMT2        25A  O    +1
     A                                    +2
     A                                      'MatchField3'
     A            PASMT3        25A  O    +1
     A                                     1
     A                                      'MatchField4'
     A                                      SPACEB(001)
     A            PASMT4        25A  O    +1
     A                                    +2
     A                                      'MatchField5'
     A            PASMT5        25A  O    +1
     A                                    +2
     A                                      'MatchField6'
     A            PASMT6        25A  O    +1
     A                                     1
     A                                      'MatchField7'
     A                                      SPACEB(001)
     A            PASMT7        25A  O    +1
     A                                    +2
     A                                      'MatchField8'
     A            PASMT8        25A  O    +1
     A                                    +2
     A                                      'MatchField9'
     A            PASMT9        25A  O    +1
     A                                     1
     A                                      'MatchField10'
     A                                      SPACEB(001)
     A            PASMT10       25A  O    +1
     A                                    +2
     A                                      'DR/CR'
     A            PADRCR         1A  O    +1
     A            PAPFCT         4A  O    15
     A                                      SPACEB(001)
     A                                     1
     A                                      'Profit Centre'
     A                                    +9
     A                                      'SWIFT Ref'
     A            PASWFR        16A  O    +1
     A                                    +3
     A                                      'Cheque No'
     A            PACHQN         8A  O    +1
     A                                    +3
     A                                      'Book Code'
     A            PABKCD         2A  O    +1
     A                                      SPACEA(001)
     A  80                                 1
     A                                      'Customer Source A/c Id'
     A  80        PACSAI        25A  O    +1
     A                                    +3
     A                                      'Source Customer'
     A            PASRCN         6A  O    +1
     A                                      SPACEA(001)
     A  80                                 1
     A                                      'Operation Type'
     A  80        PAOPTP        20A  O    +1
     A  80                                +3
     A                                      'Forecast Ref'
     A  80        PAFCRF        30A  O    +1
     A                                    +3
     A                                      'Reversal Ind'
     A            PARVID         1A  O    +1
      *
      ** Start of change MD024772
     A                                     1
     A                                      'Customer Account from Interface'
     A                                      SPACEB(001)
     A            PICUSA        24A  O    +1
     A                                    +3
     A                                      'Transaction Sub Type from Interfac-
     A                                      e'
     A            PITSTY        10A  O    +1
      ** Start of change MD024772
      *
      *
      *-----------------*
      ***  Footer       *
      *-----------------*
      *
     A          R FOOTER
     A                                    46
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(003)
      *
