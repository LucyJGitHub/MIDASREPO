/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas OF Forecast List/Audit report')                  *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000591P1 - Midas OF Forecast List Audit Report        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. CGL145A            Date 08Oct13               *
      *  Prev Amend No. CGL145   *CREATE   Date 10Jul13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL145A - Subsidiaries Daily Sweeping Forecast               *
      *  CGL145 - Subsidiaries Daily Sweeping Forecast                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER1
     A                                      SKIPB(001)
     A                                     1
     A                                      'Reference GL000591P1'
     A                                      SPACEB(001)
     A            RRPTDS        53A  O    40
     A                                   +26
     A                                      'Date'
     A            BJMRDT    R    7   O    +1
     A                                      REFFLD(MRDT MFRFL)
     A                                   119
     A                                      'Page'
     A                                      SPACEB(001)
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A          R HEADER2
     A                                      SPACEB(001)
     A                                      SPACEA(002)
     A            FTITLE        28A  O    53
     A            FUNDRS        28A  O    53
     A                                      SPACEB(002)
     A                                     1
     A                                      'FORECAST TOTALS'
     A                                      SPACEB(001)
     A                                     1
     A                                      'Customer'
     A                                      SPACEB(001)
     A            FCUSTNUM       6A  O    18
     A            FCUSTNAM      20A  O    25
     A            FCUSTTWN      10A  O    46
     A                                    58
     A                                      'Date'
     A            FDATE          7A  O    70
     A                                    95
     A                                      'Total Amount'
     A            FTOTAMNT      21A  O   112
     A                                     1
     A                                      'Cust. Source A/C'
     A                                      SPACEB(001)
     A            FCUSTAC       34A  O    18
     A                                    58
     A                                      'Currency'
     A            FCCYS          3A  O    70
     A            FCCYL         14A  O    74
     A                                    95
     A                                      'Actual Amount'
     A            FACTAMNT      21A  O   112
     A                                     1
     A                                      'Retail A/C'
     A                                      SPACEB(001)
     A            FRETACCT      24A  O    18
     A                                    58
     A                                      'Nostro Code'
     A            FNOSTCDE       2A  O    70
     A            FACCTNAM      20A  O    73
     A                                    95
     A                                      'Total Actl. Amt.'
     A            FTOTACAMT     21A  O   112
      **Start of change CGL145A
     A                                     1
     A                                      'Alt. Cust. Source A/C'
     A                                      SPACEB(001)
     A            FALCUSTAC     34A  O    24
      **End of change CGL145A
     A          R HEADER3
     A                                     1
     A                                      'Last Change User'
     A                                      SPACEB(002)
     A                                    19
     A                                      '/'
     A                                    22
     A                                      'Date'
     A                                    31
     A                                      '/'
     A                                    36
     A                                      'Time'
     A                                    46
     A                                      '/'
     A                                    51
     A                                      'Action'
     A            PUSER1        10A  O     1
     A                                      SPACEB(001)
     A            PDATE1         7A  O    22
     A            PTIME1         8A  O    +7
     A            PACT1         10A  O    +7
     A          R HEADER4
     A                                     1
     A                                      'FORECAST DETAILS'
     A                                      SPACEB(002)
     A            PRAMNT         7A       14
     A                                      SPACEB(001)
     A                                    33
     A                                      'Actual Amount/'
     A                                    53
     A                                      'Reference/'
     A            PROPTP        15A       88
     A                                   110
      **Start of change CGL145A
     A**********                            'Proj. on Nostro/'
     A                                      'Incl. Nostro Bal.'
      **End of change CGL145A
     A                                   130
     A                                      'Seq'
     A                                      SPACEA(001)
     A  01                                 5
     A                                      'Last Change User'
     A            PROAMT        16A       31
     A            PRNARR        10A       53
     A  01                                88
     A                                      'Action'
     A                                   110
      **Start of change CGL145A
     A**********                            'Proj. on Retail'
     A                                      'Incl. Retail Bal.'
      **End of change CGL145A
     A                                   130
     A                                      'No.'
     A                                      SPACEA(001)
     A  01                                43
     A                                      'Date'
     A  01                                53
     A                                      'Time'
      *
      *-----------------*
      ***  Details      *
      *-----------------*
      *
     A          R DETAIL
     A                                      SPACEB(001)
     A            FDAMNT        21A  O     1
     A            FDACTAMT      21A  O    27
     A            FDREFN        30A  O    53
     A            FDOPTYP       20A  O    88
     A            FDPROJN        1A  O   118
     A            FDSEQN         3S  O   130
     A                                      SPACEA(001)
     A  01        DUSER1        10A  O    11
     A            FDORGAMT      21A  O    27
     A            FDNARR        30A  O    53
     A  01        DACT1         10A  O    88
     A            FDPROJR        1A  O   118
     A                                      SPACEA(001)
     A  01        DDATE1         7A  O    40
     A  01        DTIME1         8A  O    53
      *
      *-----------------*
      ***  Footer       *
      *-----------------*
      *
     A          R FOOTER
     A                                    46
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(003)
