      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas OF Forecast Housekeeping')                       *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000592AU - Midas OF Forecast Housekeeping             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2013            *
      *                                                               *
      *  Last Amend No. CGL145   *CREATE   Date 10Jul13               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL145 - Subsidiaries Daily Sweeping Forecast                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2013')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(003)
     A                                     6
     A                                      'REFERENCE GL000592AU'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    55
     A                                      'FORECAST HOUSEKEEPING'
     A                                      SPACEB(001)
     A                                    55
     A                                      '---------------------'
     A                                      SPACEB(001)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                      SPACEA(001)
     A                                    92
     A                                      'CALCULATED'
     A                                    92
     A                                      '----------'
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    46
     A                                      'DELETED RECORDS'
     A                                    46
     A                                      '---------------'
     A                                      SPACEB(001)
     A                                    46
     A                                      'TOTAL NO. OF TOTAL FORECASTS-
     A                                       DROPPED      :'
     A                                      SPACEB(001)
     A            TFDCOUNT       8S 0     92
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A                                    46
     A                                      'TOTAL NO. OF INDIVIDUAL FORE-
     A                                      CASTS DROPPED :'
     A                                      SPACEB(001)
     A            IFDCOUNT       8S 0     92
     A                                      EDTCDE(3)
     A                                      SPACEA(002)
     A                                    46
     A                                      'DROPPED AFTER RETENTION'
     A                                    46
     A                                      '---------------'
     A                                      SPACEB(001)
     A                                    46
     A                                      'TOTAL NO. OF TOTAL FORECASTS-
     A                                       DROPPED      :'
     A                                      SPACEB(001)
     A            TFRCOUNT       8S 0     92
     A                                      EDTCDE(3)
     A                                      SPACEA(001)
     A                                    46
     A                                      'TOTAL NO. OF INDIVIDUAL FORE-
     A                                      CASTS DROPPED :'
     A                                      SPACEB(001)
     A            IFRCOUNT       8S 0     92
     A                                      EDTCDE(3)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25
     A                                      '**** DATABASE ERROR ****'
     A                                      SPACEB(002)
     A                                    11
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(002)
     A                                    11
     A                                      '*'
     A                                      SPACEB(001)
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    FILE NAME    '''
     A                                      SPACEB(001)
     A            DBFILE         8A       30
     A                                    38
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    KEY          '''
     A                                      SPACEB(001)
     A            DBKEY         29        30
     A                                    59
     A                                      '''    *'
     A                                    11
     A                                      '*    PROGRAM NAME '''
     A                                      SPACEB(001)
     A            DBPGM         10A       30
     A                                    40
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    AT           '''
     A                                      SPACEB(001)
     A            DBASE          3  0     30
     A                                    33
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*'
     A                                      SPACEB(001)
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(001)
