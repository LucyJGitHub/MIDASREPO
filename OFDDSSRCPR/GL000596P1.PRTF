      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas OF Bank Position List report')                   *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000596P1 - Midas OF Bank Position Details Report      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2014            *
      *                                                               *
      *  Last Amend No. MD026759            Date 12May14              *
      *  Prev Amend No. CGL153    *CREATE   Date 03Mar14              *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD026759 - Incorrect column headers in Bank Position COB     *
      *             Report                                            *
      *  CGL153 - Bank Position Report                                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER1
     A                                      SKIPB(001)
     A                                     1
     A                                      'Reference GL000596P1'
     A                                      SPACEB(001)
     A            RRPTDS        53A  O    40
     A                                   119
     A                                      'Date'
     A            BJMRDT    R    7   O   124
     A                                      REFFLD(MRDT MFRFL)
     A                                   119
     A                                      'Page'
     A                                      SPACEB(001)
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
      *----------------------*
      ***  Report Header     *
      *----------------------*
      *
     A          R HEADER2
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A  50                                57
     A                                      'BANK POSITION - LIST MODE'
     A  50                                57
     A                                      '-------------------------'
     A                                      SPACEB(001)
     A N50                                57
     A                                      'BANK POSITION - AUDIT MODE'
     A N50                                57
     A                                      '-------------------------'
     A                                      SPACEB(001)
      *
      *
      *
      *--------------------------------*
      ***  Report Header Audit Mode    *
      *--------------------------------*
      *
     A          R HEADER3
     A                                      SPACEB(001)
     A                                     1
     A                                      'Nostro/Pool'
     A                                    13
     A                                      'Nostro/Pool Desc'
     A                                    32
     A                                      'A/C CCY'
     A                                    40
     A                                      'Ledger Balance'
     A                                    61
     A                                      'Forecast Balance'
     A                                    84
     A                                      'Projected Balance'
     A                                   106
     A                                      'Unreconciled Balance'
     A                                    16
      ** Start of change MD026759
     A**********                            'Account details'                               MD026759
     A                                      'Account No.'
      ** End of change MD026759
     A                                      SPACEB(001)
     A                                    42
     A                                      'Statement Balance'
     A                                    64
     A                                      'Estimated Balance'
     A                                    86
     A                                      'Est Exch Rate'
     A                                   101
     A                                      'Spot'
     A                                   108
     A                                      'Estimated Bal in'
     A            PDBCUR         3A  O   125
      *
      *-----------------*
      ***  Details      *
      *-----------------*
      *
     A          R DETAIL
     A                                      SPACEB(001)
     A            PDPOOL        10A  O     1
     A            PDNOSN        20A  O    13
     A            PDCCY          3A  O    35
     A            PDCLBL        21A  O    39
     A            PDFAMT        21A  O    61
     A            PDPAMT        21A  O    83
     A            PDUAMT        21A  O   105
     A            PDACCT        24A  O    16
     A                                      SPACEB(001)
     A            PDSAMT        21A  O    42
     A            PDESBL        21A  O    64
     A            PDERAT        13S 8O    86
     A                                      EDTCDE(3)
     A            PDSPOT         1A  O   103
     A            PDBASE        21A  O   108
      *
      *-----------------*
      ***  Details 2    *
      *-----------------*
      *
     A          R DETAIL2
     A                                      SPACEB(001)
     A            PDPOOL2       10A  O     1
     A            PDNOSN2       20A  O    13
     A            PDCCY2         3A  O    35
      *
      *-----------------*
      ***  SUB TOTAL    *
      *-----------------*
      *
     A          R SUBPOOL
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    66
     A                                      'Sub-total of Estimated Balances in'
     A                                      SPACEB(001)
     A            PDBCUR         3A  O   101
     A                                   104
     A                                      ':'
     A            PDSUB         22A  O   107
      *
      *-----------------*
      ***     TOTAL     *
      *-----------------*
      *
     A          R TOTAL
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    70
     A                                      'Total of Estimated Balances in'
     A                                      SPACEB(001)
     A            PDBCUR         3A  O   101
     A                                   104
     A                                      ':'
     A            PDTOT         22A  O   107
      *
      *-----------------*
      ***  Footer       *
      *-----------------*
      *
     A          R FOOTER
     A                                    46
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(002)
      *
