      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas OF Estimated Exchange Rates Dropped - Detail')   *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL000598P1 - Midas OF Estimated Exchange Rate Report    *
      *                                                               *
      *  (c) Finastra International Limited 2014                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL153  *CREATE    Date 13Feb14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL153 - Bank Position Report                                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER1
     A                                      SKIPB(001)
     A                                     1
     A                                      'Reference GL000598P1'
     A                                      SPACEB(001)
     A            RRPTDS        53A  O    46
     A                                   119
     A                                      'Date'
     A            BJMRDT    R    7   O    +1
     A                                      REFFLD(MRDT MFRFL)
     A                                   119
     A                                      'Page'
     A                                      SPACEB(001)
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
      *----------------------*
      ***  Report Header     *
      *----------------------*
      *
     A          R HEADER2
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A  50                                38
     A                                      'ESTIMATED EXCHANGE RATES DROPPED B-
     A                                      Y THE SYSTEM - LIST MODE'
     A  50                                38
     A                                      '-----------------------------------
     A                                      ------------------------'
     A                                      SPACEB(001)
     A N50                                38
     A                                      'ESTIMATED EXCHANGE RATES DROPPED B-
     A                                      Y THE SYSTEM - AUDIT MODE'
     A N50                                38
     A                                      '-----------------------------------
     A                                      ------------------------'
     A                                      SPACEB(001)
      *
      *
      *--------------------------------*
      ***  Report Header Audit Mode    *
      *--------------------------------*
      *
     A          R HEADER3
     A                                      SPACEB(001)
     A                                     1
     A                                      'Bank'
     A                                    10
     A                                      'Bank'
     A                                    25
     A                                      'Bank'
     A                                    44
     A                                      'Bank'
     A                                    57
     A                                      'CCY'
     A                                    62
     A                                      'Nostro'
     A                                    71
     A                                      'Nostro'
     A                                    80
     A                                      'Nostro'
     A                                    87
     A                                      'Date'
     A                                    97
     A                                      'To'
     A                                   103
     A                                      'Estimated'
     A                                   118
     A                                      'M/D'
     A                                    10
     A                                      'Shortname'
     A                                      SPACEB(001)
     A                                    24
     A                                      'Report Name'
     A                                    43
     A                                      'Report Town'
     A                                    63
     A                                      'Code'
     A                                    72
     A                                      'Name'
     A                                    80
     A                                      'Branch'
     A                                    96
     A                                      'CCY'
     A                                   102
     A                                      'Exchange Rate'
     A                                   118
     A                                      'Ind'
      *
      *-----------------*
      ***  Details      *
      *-----------------*
      *
     A          R DETAIL
     A                                      SPACEB(001)
     A            PDCUST         6A  O     1
     A            PDSHRT        10A  O    10
     A            PDRPNM        20A  O    21
     A            PDRPTW        10A  O    43
     A            PDCURR         3A  O    57
     A            PDNOST         2A  O    64
     A            PDNTNM        10A  O    69
     A            PDBRCH         3A  O    81
     A            PDDATE         7A  O    86
     A            PDTCUR         3A  O    96
     A            PDEXRT        13S 8O   101
     A                                      EDTCDE(1)
     A            PDMDIN         1A  O   119
      *
      *-----------------*
      ***  Footer       *
      *-----------------*
      *
     A          R FOOTER
     A                                    46
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(002)
      *
