/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas OF Bank Position Optimisation Audit Rpt')        *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/GL000900P1 - Midas OF Bank Position Optimisation        *
      *                      Audit Report                             *
      *                                                               *
      *  (c) Finastra International Limited 2014                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CGL176             Date 28Jul15               *
      *                 CGL158  *CREATE    Date 02Dec14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL176 - Optimisation of Nostro Balances by Company          *
      *  CGL158 - Bank Position Management                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER1
     A                                     1'REFERENCE GL000900P1'
     A                                      SKIPB(002)
     A            RRPTDS        53A  O    40
     A                                   +10'DATE'
     A            BJMRDT    R    7   O    +1REFFLD(MRDT MFRFL)
     A                                   121'PAGE'
     A                                    +1PAGNBR
     A                                      EDTCDE(Z)
     A            FTITLE        65A  O    36SPACEB(002)
     A            FUNDRS        65A  O    36SPACEB(001)
      **Start of Change CGL176
     A  40                                 2'Company :'
     A                                      SPACEB(001)
     A  40        H2CMCD         3A  O    +1
      **End of Change CGL176
     A
      *
      *-----------------*
      ***  Header 2     *
      *-----------------*
      *
     A          R HEADER2                   SPACEB(002)
     A                                    28'Confirmed Suggested Transfers '
     A                                    58'Date/Time/User :'
     A            CDATE          7A  O    +1
     A            CTIME          8A  O    +2
     A            CUSER         10A  O    +2
     A                                    33'Initiated Suggested Transfers '
     A                                      SPACEB(001)
     A                                    63'Date/Time :'
     A            IDATE          7A  O    +1
     A            ITIME          8A  O    +2
      *
      *-----------------*
      ***  Pool Header  *
      *-----------------*
      *
     A          R POOLH                     SPACEB(002)
     A                                     2'Pool :'
     A            PRPOOL        10A  O    +1
     A                                    62'Pivot Currency :'
     A            PRPCCY         3A  O    +1
     A                                     2'Pool Description :'
     A                                      SPACEB(001)
     A            PRPOOD        30A  O    +1
     A                                    62'Exchange Rate Pivot to Base :'
     A            PRERTP        13  8     +1EDTCDE(L)
      *
      *-----------------*
      ***  Header 3     *
      *-----------------*
      *
     A          R HEADER3                   SPACEB(002)
     A                                     2'Nostro /'
     A                                    39'Cleared Balance /'
     A                                    71'Estimated Balance /'
     A                                   106'Suggested Transfers /'
     A                                     2'Description /'
     A                                      SPACEB(001)
     A                                    38'Forecast Balance /'
     A  87                                61'Estimated Bal. in Pivot Ccy /'
     A                                   100'Suggested Bal. in A/C Ccy /'
     A                                     2'A/C Number /'
     A                                      SPACEB(001)
     A                                    37'Projected Balance /'
     A                                    62'Estimated Bal. in Base Ccy /'
     A                                    99'Suggested Bal. in Base Ccy /'
     A                                     2'Initiated User'
     A                                      SPACEB(001)
     A                                    34'Unreconciled Balance'
     A                                    65'Estimated Exchange Rate'
     A                                   106'Spot Rate Indicator'
      *
      *-----------------*
      ***  Details      *
      *-----------------*
      *
     A          R DETAIL                    SPACEB(002)
     A            PRNOST         5A  O     2
     A  20        PRCLD0    R             35REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  21        PRCLD1    R             34REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  22        PRCLD2    R             34REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  23        PRCLD3    R             35REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A            PRCCYC         3A  O    56
     A  20        PREST0    R             69REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  21        PREST1    R             68REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  22        PREST2    R             68REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  23        PREST3    R             69REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A            PRCCYE         3A  O    90
     A  20        PRSUT0    R            106REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  21        PRSUT1    R            105REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  22        PRSUT2    R            105REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  23        PRSUT3    R            106REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A            PRCCYS         3A  O   127
     A            PRNOSD        30A  O     2SPACEB(001)
     A  20        PRFOR0    R             35REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  21        PRFOR1    R             34REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  22        PRFOR2    R             34REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  23        PRFOR3    R             35REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A            PRCCYF         3A  O    56
     A  88 89 30  PRESP0    R             69REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  88 89 31  PRESP1    R             68REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  88 89 32  PRESP2    R             68REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  88 89 33  PRESP3    R             69REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A  88 89     PRCYEP         3A  O    90
     A  20        PRSBA0    R            106REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  21        PRSBA1    R            105REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  22        PRSBA2    R            105REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  23        PRSBA3    R            106REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A            PRCYSA         3A  O   127
     A            PRACNO        24A  O     2SPACEB(001)
     A  20        PRPRO0    R             35REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  21        PRPRO1    R             34REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  22        PRPRO2    R             34REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  23        PRPRO3    R             35REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A            PRCCYP         3A  O    56
     A  10        PRESB0    R             69REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  11        PRESB1    R             68REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  12        PRESB2    R             68REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  13        PRESB3    R             69REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A            PRCYEB         3A  O    90
     A  10        PRSBB0    R            106REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  11        PRSBB1    R            105REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  12        PRSBB2    R            105REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  13        PRSBB3    R            106REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A            PRCYSB         3A  O   127
     A            PRIUSR        10A  O     2SPACEB(001)
     A  20        PRUNR0    R             35REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  21        PRUNR1    R             34REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  22        PRUNR2    R             34REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  23        PRUNR3    R             35REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A            PRCCYU         3A  O    56
     A            PREXRT        13  8     74EDTCDE(L)
     A            PRSPRT         1A      118
      *
      *-----------------*
      ***  Pool Total   *
      *-----------------*
      *
     A          R POOLT                     SPACEB(002)
     A                                     2'Pool Balance in Pivot Ccy : '
     A  30        PRTEP0    R             69REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  31        PRTEP1    R             68REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  32        PRTEP2    R             68REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  33        PRTEP3    R             69REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A            PRCESP         3A  O    90
     A  30        PRTSP0    R            106REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  31        PRTSP1    R            105REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  32        PRTSP2    R            105REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  33        PRTSP3    R            106REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A            PRCSSP         3A  O   127
     A                                     2'Pool Balance in Base Ccy  : '
     A                                      SPACEB(001)
     A  10        PRTEB0    R             69REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  11        PRTEB1    R             68REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  12        PRTEB2    R             68REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  13        PRTEB3    R             69REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A            PRCESB         3A  O    90
     A  10        PRTSB0    R            106REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  11        PRTSB1    R            105REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  12        PRTSB2    R            105REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  13        PRTSB3    R            106REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A            PRCSSB         3A  O   127
      *
      *-----------------*
      ***  Footer       *
      *-----------------*
      *
     A          R FOOTER
      **Start of Change CGL176
     A**********                          55
     A**********                            '*** END OF REPORT ***'
     A**********                            SPACEB(003)
     A N40                                55'*** END OF REPORT ***'
     A N40                                  SPACEB(003)
     A  40                                50'*** END OF REPORT FOR COMPANY ***'
     A  40                                  SPACEB(003)
      **End of Change CGL176
