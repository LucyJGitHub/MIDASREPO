/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas OF Suggested Transfers Audit Report')            *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/GL000901P1 - Midas OF Suggested Transfers Audit Report  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2014            *
      *                                                               *
      *  Last Amend No. CGL176             Date 28Jul15               *
      *  Prev Amend No. MD033486           Date 27Feb15               *
      *                 CGL158   *CREATE   Date 20Aug14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL176 - Optimisation of Nostro Balances by Company          *
      *  MD033486 - Add new Beneficiary Account field                 *
      *  CGL158 - Nostro Sweeping Rules Maintenance                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER1
     A                                     1'REFERENCE GL000901P1'
     A                                      SKIPB(002)
     A            RRPTDS        53A  O    40
     A                                   +10'DATE'
     A            BJMRDT    R    7   O    +1REFFLD(MRDT MFRFL)
     A                                   121'PAGE'
     A                                    +1PAGNBR
     A                                      EDTCDE(Z)
     A            FTITLE        65A  O    36SPACEB(002)
     A            FUNDRS        65A  O    36SPACEB(001)
      **Start of Change CGL176
     A  40                                 2'Company :'
     A                                      SPACEB(001)
     A  40        H2CMCD         3A  O    +1
      **End of Change CGL176
      *
      *-----------------*
      ***  Header 2     *
      *-----------------*
      *
     A          R HEADER2                   SPACEB(002)
     A                                    28'Confirmed Suggested Transfers '
     A                                    58'Date/Time/User :'
     A            CDATE          7A  O    +1
     A            CTIME          8A  O    +2
     A            CUSER         10A  O    +2
     A                                    33'Initiated Suggested Transfers '
     A                                      SPACEB(001)
     A                                    63'Date/Time :'
     A            IDATE          7A  O    +1
     A            ITIME          8A  O    +2
      *
      *-----------------*
      ***  Details      *
      *-----------------*
      *
     A          R DETAIL                    SKIPB(010)
     A                                     2'Status:'
     A            PRSTAT        30A  O    +1
     A                                    60'Book Code:'
     A            PRBOOK        33A  O    +1
     A                                     2'Rule Name:'
     A                                      SPACEB(001)
     A            PRRULE         8A  O    +1
     A                                    60'Settlement Type:'
     A            PRSTTP        22A  O    +1
     A                                     2'Manual:'
     A                                      SPACEB(001)
     A            PRMANU         3A  O    +1
     A                                    60'SWIFT Priority Code:'
     A            PRSWPC         6A  O    +1
     A                                     2'Transfer Amt.:'
     A                                      SPACEB(001)
     A  10        PRSTR0    R             18REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  11        PRSTR1    R             18REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  12        PRSTR2    R             18REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  13        PRSTR3    R             18REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A                                    60'Ordering Cust./Beneficiary:'
     A            PROCUS        24A  O    +1
     A                                     2'Currency:'
     A                                      SPACEB(001)
     A            PRCCY          3A  O    +1
     A                                    60'Ordering Cust./Beneficiary:'
     A                                    +1'Shortname:'
     A            PROCSN        20A  O    +1
     A                                     2'To/From:'
     A                                      SPACEB(001)
     A            PRTOFR         4A  O    +1
     A                                    60'Ordering Cust./Beneficiary'
     A                                    +1'Report Name:'
     A            PROCNM        34A  O    +1
     A                                     2'Base Nost. Code:'
     A                                      SPACEB(001)
     A            PRBNOC         2A  O    +1
     A                                    60'Ordering Cust./Beneficiary'
     A                                    +1'Report Town:'
     A            PROCTN        10A  O    +1
     A                                     2'Base A/C No.:'
     A                                      SPACEB(001)
     A            PRBNAC        24A  O    +1
     A                                    60'Ordering Cust./Beneficiary'
     A                                    +1'Account Name:'
     A            PROCAM        20A  O    +1
     A                                     2'Base A/C Name.:'
     A                                      SPACEB(001)
     A            PRBNAM        20A  O    +1
     A                                    60'Ordering Cust./Beneficiary'
     A                                    +1'SWIFT Id:'
     A            PROCSI        12A  O    +1
      *
      ** Start of change MD033486
     A                                    60
     A                                      'Beneficiary Account:'
     A                                      SPACEB(001)
     A            PRBENE        35A  O    +1
      ** End of change MD033486
      *
     A                                     2'Base Nost. Branch:'
     A                                      SPACEB(001)
     A            PRBNBR        34A  O    +1
     A                                    60'A/C with Bank/Ordering Bank 1:'
     A            PRORC1        35A  O    +1
     A                                     2'Est. Base A/C Bal. Before:'
     A                                      SPACEB(001)
     A  10        PRBBB0    R             29REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  11        PRBBB1    R             29REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  12        PRBBB2    R             29REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  13        PRBBB3    R             29REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A                                    60'A/C with Bank/Ordering Bank'
     A                                    +1'Shortname:'
     A            PRACSN        10A  O    +1
     A                                     2'Est. Base A/C Bal. After:'
     A                                      SPACEB(001)
     A  10        PRBBA0    R             28REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  11        PRBBA1    R             28REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  12        PRBBA2    R             28REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  13        PRBBA3    R             28REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A                                    60'A/C with Bank/Ordering Bank'
     A                                    +1'Report Name:'
     A            PRACNM        20A  O    +1
     A  88                                 2'Base Pool:'
     A                                      SPACEB(001)
     A  88        PRPOOL        10A  O    +1
     A                                    60'A/C with Bank/Ordering Bank'
     A N88                                  SPACEB(001)
     A                                    +1'Report Town:'
     A            PRACTN        10A  O    +1
     A  88                                 2'Est. Pool Bal. Before:'
     A                                      SPACEB(001)
     A  88 89 20  PRPBB0    R             25REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  88 89 21  PRPBB1    R             25REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  88 89 22  PRPBB2    R             25REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  88 89 23  PRPBB3    R             25REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A                                    60'A/C with Bank/Ordering Bank'
     A N88                                  SPACEB(001)
     A                                    +1'Account Name:'
     A            PRACAM        20A  O    +1
     A  88                                 2'Est. Pool Bal. After:'
     A                                      SPACEB(001)
     A  88 89 20  PRPBA0    R             24REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  88 89 21  PRPBA1    R             24REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  88 89 22  PRPBA2    R             24REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  88 89 23  PRPBA3    R             24REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A                                    60'A/C with Bank/Ordering Bank'
     A N88                                  SPACEB(001)
     A                                    +1'SWIFT Id:'
     A            PRACID        12A  O    +1
     A                                     2'Other Nost. Code:'
     A                                      SPACEB(001)
     A            PRONOC         2A  O    +1
     A                                    60'A/C with Bank/Ordering Bank 2:'
     A            PRORC2        35A  O    +1
     A                                     2'Other A/C No.:'
     A                                      SPACEB(001)
     A            PRONAC         2A  O    +1
     A                                    60'A/C with Bank/Ordering Bank 3:'
     A            PRORC3        35A  O    +1
     A                                     2'Other A/C Name:'
     A                                      SPACEB(001)
     A            PRONAM        20A  O    +1
     A                                    60'A/C with Bank/Ordering Bank 4:'
     A            PRORC4        35A  O    +1
     A                                     2'Other Nost. Branch:'
     A                                      SPACEB(001)
     A            PRONBR        34A  O    +1
     A                                    60'A/C with Bank/Ordering Bank 5:'
     A            PRORC5        35A  O    +1
     A                                     2'Est. Other A/C Bal. Before:'
     A                                      SPACEB(001)
     A  10        PROBB0    R             30REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  11        PROBB1    R             30REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  12        PROBB2    R             30REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  13        PROBB3    R             30REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A                                    60'Intermediary 1:'
     A            PRCIB1        35A  O    +1
     A                                     2'Est. Other A/C Bal. After:'
     A                                      SPACEB(001)
     A  10        PROBA0    R             29REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  11        PROBA1    R             29REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  12        PROBA2    R             29REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  13        PROBA3    R             29REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A                                    60'Intermediary Shortname:'
     A            PRIBSN        10A  O    +1
     A                                     2'External Party:'
     A                                      SPACEB(001)
     A            PREXTP        21A  O    +1
     A                                    60'Intermediary Report Name:'
     A            PRIBNM        20A  O    +1
     A                                     2'External Party Shortname:'
     A                                      SPACEB(001)
     A            PREXSN        10A  O    +1
     A                                    60'Intermediary Report Town:'
     A            PRIBTN        10A  O    +1
     A                                     2'External Party Report Name:'
     A                                      SPACEB(001)
     A            PREXNM        20A  O    +1
     A                                    60'Intermediary Account Name:'
     A            PRIBAM        20A  O    +1
     A                                     2'External Party Report Town:'
     A                                      SPACEB(001)
     A            PREXTN        10A  O    +1
     A                                    60'Intermediary SWIFT Id:'
     A            PRIBID        12A  O    +1
     A                                     2'External Party Account Name:'
     A                                      SPACEB(001)
     A            PREXAM        20A  O    +1
     A                                    60'Intermediary 2:'
     A            PRCIB2        35A  O    +1
     A                                     2'External Party Swift Id:'
     A                                      SPACEB(001)
     A            PREXSI        12A  O    +1
     A                                    60'Intermediary 3:'
     A            PRCIB3        35A  O    +1
     A                                     2'Sweep Level:'
     A                                      SPACEB(001)
     A            PRSLVL         2A  O    +1
     A                                    60'Intermediary 4:'
     A            PRCIB4        35A  O    +1
     A                                     2'Sweep Type:'
     A                                      SPACEB(001)
     A            PRSTYP        35A  O    +1
     A                                    60'Intermediary 5:'
     A            PRCIB5        35A  O    +1
     A                                     2'Threshold Amount:'
     A                                      SPACEB(001)
     A  20        PRTA10    R             20REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  21        PRTA11    R             20REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  22        PRTA12    R             20REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  23        PRTA13    R             20REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A                                    60'Bank to Bank Info. 1:'
     A            PRBBI1        35A  O    +1
     A                                     2'Threshold Currency:'
     A                                      SPACEB(001)
     A            PRCCY1         3A  O    +1
     A                                    60'Bank to Bank Info. 2:'
     A            PRBBI2        35A  O    +1
     A                                     2'Debit/Credit:'
     A                                      SPACEB(001)
     A            PRDRCR         6A  O    +1
     A                                    60'Bank to Bank Info. 3:'
     A            PRBBI3        35A  O    +1
     A                                     2'Threshold Amount 2:'
     A                                      SPACEB(001)
     A  30        PRTA20    R             22REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  31        PRTA21    R             22REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  32        PRTA22    R             22REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  33        PRTA23    R             22REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A                                    60'Bank to Bank Info. 4:'
     A            PRBBI4        35A  O    +1
     A                                     2'Threshold Currency 2:'
     A                                      SPACEB(001)
     A            PRCCY2         3A  O    +1
     A                                    60'Initiate/Manual User:'
     A            PRIUSR        10A  O    +1
     A                                     2'Debit/Credit 2:'
     A                                      SPACEB(001)
     A            PRDRC2         6A  O    +1
     A                                    60'Reject User:'
     A            PRRUSR        10A  O    +1
     A                                     2'Multiples of Sweep Amount:'
     A                                      SPACEB(001)
     A  20        PRMOS0    R             29REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   ,   , 0 -')
     A  21        PRMOS1    R             29REFFLD(LDBLN)
     A                                      EDTWRD('  ,   ,   ,   , 0 . -')
     A  22        PRMOS2    R             29REFFLD(LDBLN)
     A                                      EDTWRD(' ,   ,   ,   , 0 .  -')
     A  23        PRMOS3    R             29REFFLD(LDBLN)
     A                                      EDTWRD('   ,   ,   , 0 .   -')
     A                                    60'Transaction Type:'
     A            PRAPIN        16A  O    +1
     A                                     2'Payment Sub-Type:'
     A                                      SPACEB(001)
     A            PRPSTY        38A  O    +1
     A                                    60'Transaction Reference:'
     A            PRPREF        15A  O    +1
     A                                     2'Profit Centre:'
     A                                      SPACEB(001)
     A            PRPRFC        35A  O    +1
     A                                    60'Front Office Id:'
     A            PRFOID        20A  O    +1
      *
      *-----------------*
      ***  Footer       *
      *-----------------*
      *
     A          R FOOTER
      **Start of change CGL176
     A**********                          46
     A**********                            '*** END OF REPORT ***'
     A**********                            SPACEB(003)
     A N40                                46'*** END OF REPORT ***'
     A N40                                  SPACEB(003)
     A  40                                41'*** END OF REPORT FOR COMPANY ***'
     A  40                                  SPACEB(003)
      **End of change CGL176
