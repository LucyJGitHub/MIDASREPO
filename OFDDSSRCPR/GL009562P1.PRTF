      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas OF Update Actl Amnts against Forecast Report')   *
      *****************************************************************
      *                                                               *
      *  Midas - General Ledger Module                                *
      *                                                               *
      *  PRTF/GL009562P1 - Midas OF Update Actual Amounts against     *
      *                    Forecast Report                            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2014            *
      *                                                               *
      *  Last Amend No. MD025967 *CREATE   Date 24Apr14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD025967 - Produce a report of the Updates/Writes of the     *
      *             Forecasts from the Accounting Rules.              *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER1
     A                                      SKIPB(001)
     A                                     1
     A                                      'Reference GL009562P1'
     A                                      SPACEB(001)
     A            RRPTDS        53A  O    40
     A                                   +26
     A                                      'Date'
     A            BJMRDT    R    7   O    +1
     A                                      REFFLD(MRDT MFRFL)
     A                                   119
     A                                      'Page'
     A                                      SPACEB(001)
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
      *----------------------*
      ***  Report Header     *
      *----------------------*
      *
     A          R HEADER2
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                    51
     A                                      'Generated Forecasts from +
     A                                       Accounting Rules'
     A                                    51
     A                                      '-------------------------------+
     A                                       ----------'
     A                                      SPACEB(001)
      *
      *-----------------*
      ***  Details      *
      *-----------------*
      *
     A          R DETAIL
     A                                      SPACEB(001)
     A                                     1
     A                                      'Batch ID'
     A            PEBTID        60A  O    +1
     A                                     1
     A                                      'Booking Branch'
     A                                      SPACEB(001)
     A            PEBKBR         3A  O    +1
     A                                    +1
     A                                      'Deparment'
     A            PEDPCD         3A  O    +1
     A                                    +2
     A                                      'Currency'
     A            PECCY          3A  O    +1
     A                                    +1
     A                                      'Event Amount'
     A            PEEVTA        19A  O    61
     A                                    +1
     A                                      'DR/CR'
     A            PEDCIP         1A  O    +1
     A                                    +2
     A                                      'Posting Date'
     A            PEPSTD         6S 0O    +1
     A                                      EDTWRD('  /  /  ')
     A                                    +3
     A                                      'Event Date'
     A            PEEVTD         6S 0O    +1
     A                                      EDTWRD('  /  /  ')
     A                                     2
     A                                      'Narrative'
     A                                      SPACEB(001)
     A            PENARR        30A  O    +1
     A            PENAR2        65A  O    12
     A                                      SPACEB(001)
     A            PENAR3        65A  O    12
     A                                      SPACEB(001)
     A            PENAR4        65A  O    12
     A                                      SPACEB(001)
     A            PENAR5        65A  O    12
     A                                      SPACEB(001)
     A            PENAR6        65A  O    12
     A                                      SPACEB(001)
     A            PENAR7        65A  O    12
     A                                      SPACEB(001)
     A                                     1
     A                                      'Sett A/c DR'
     A                                      SPACEB(001)
     A            PESADR        24A  O    +1
     A                                    40
     A                                      'Sett A/c CR'
     A            PESACR        24A  O    +1
     A                                     1
     A                                      'Customers:'
     A                                      SPACEB(001)
     A                                    +1
     A                                      'Counterparty'
     A            PECTPY        10A  O    +1
     A                                    +3
     A                                      'Depot'
     A            PEDPCN        10A  O    +1
     A                                    +3
     A                                      'Issuing'
     A            PEISCN        10A  O    +1
     A                                    +3
     A                                      'Associated'
     A            PEASCN        10A  O    +1
     A                                     1
     A                                      'MatchField1'
     A                                      SPACEB(001)
     A            PESMT1        25A  O    +1
     A                                    +2
     A                                      'MatchField2'
     A            PESMT2        25A  O    +1
     A                                    +2
     A                                      'MatchField3'
     A            PESMT3        25A  O    +1
     A                                     1
     A                                      'MatchField4'
     A                                      SPACEB(001)
     A            PESMT4        25A  O    +1
     A                                    +2
     A                                      'MatchField5'
     A            PESMT5        25A  O    +1
     A                                    +2
     A                                      'MatchField6'
     A            PESMT6        25A  O    +1
     A                                     1
     A                                      'MatchField7'
     A                                      SPACEB(001)
     A            PESMT7        25A  O    +1
     A                                    +2
     A                                      'MatchField8'
     A            PESMT8        25A  O    +1
     A                                    +2
     A                                      'MatchField9'
     A            PESMT9        25A  O    +1
     A                                     1
     A                                      'MatchField10'
     A                                      SPACEB(001)
     A            PESMT10       25A  O    +1
     A                                    +1
     A                                      'DR/CR'
     A            PEDCRD         1A  O    +1
     A            PEPFCT         4A  O    15
     A                                      SPACEB(001)
     A                                     1
     A                                      'Profit Centre'
     A                                    +9
     A                                      'SWIFT Ref'
     A            PESWFR        16A  O    +1
     A                                    +3
     A                                      'Cheque No'
     A            PECHQN         8A  O    +1
     A                                    +3
     A                                      'Book Code'
     A            PEBKCD         2A  O    +1
     A                                      SPACEA(001)
     A  80                                 1
     A                                      'Customer Source A/c Id'
     A  80        PECSAI        25A  O    +1
     A                                    +3
     A                                      'Source Customer'
     A            PESRCN         6A  O    +1
     A                                     1
     A                                      'Unique ID Indicator'
     A                                      SPACEB(001)
     A            PEUNID        32A  O    +1
     A                                    +3
     A                                      'Description'
     A            PEDESC        30A  O    +1
     A                                     1
     A                                      'Customer Account from Interface'
     A                                      SPACEB(001)
     A            PICUSA        24A  O    +1
     A                                    +3
     A                                      'Transaction Sub Type from Interfac-
     A                                      e'
     A            PITSTY        10A  O    +1
      *
     A                                      SPACEA(001)
     A  80                                 1
     A                                      'Operation Type'
     A  80        PEOPTP        20A  O    +1
     A  80                                +3
     A                                      'Forecast Ref'
     A  80        PEFCRF        30A  O    +1
     A                                    +3
     A                                      'Reversal Ind'
     A            PERVID         1A  O    +1
      *
      *
      *-----------------*
      ***  Detail 01    *
      *-----------------*
      *
     A          R DET01
     A                                      SPACEB(001)
     A            PFORTL        23A  O     1
     A                                    +1
     A                                      'Customer'
     A            PFCUST         6A  O    +1
     A                                    +1
     A                                      'Customer Source A/C'
     A            PFCSAC        34A  O    +1
     A                                    +1
     A                                      'Currency'
     A            PFCCY          3A  O    +1
     A                                    +1
     A                                      'Nostro'
     A            PFNOSC         2A  O    +1
     A                                     1'Retail Account'
     A                                      SPACEB(001)
     A            PFFACN        24A  O    +1
     A                                    +1
     A                                      'Date'
     A            PFDATE         6S 0O    +1
     A                                      EDTWRD('  /  /  ')
     A  91                                +1
     A                                      'Operation Type'
     A  91        PFOPTP        20A  O    +1
     A            PFORTLX       15A  O     1
     A                                      SPACEB(001)
     A            PFAMNT        19A  O    +1
     A                                    +1
     A                                      'Actual Amount'
     A            PFACTL        19A  O    +1
      *
      *-----------------*
      ***  Footer       *
      *-----------------*
      *
     A          R FOOTER
     A                                    46
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(003)
      *
