      *****************************************************************
/*STD *  PRTFP1                                                       *
/*EXI *  TEXT('Midas OF Transfer of MTCASH MT103s to SWIFT MT103s')   *
      *****************************************************************
      *                                                               *
      *  Midas - Message Management Module                            *
      *                                                               *
      *  PRTF/ME0008P1 - Midas ME Transfer of Multicash MT100s to     *
      *                  Swift MT103s Audit                           *
      *                                                               *
      *  (c) Finastra International Limited 2013                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CFT135  *CREATE    Date 20Feb13               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CFT135 - MT100 MultiCash Interface                           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R ME0008F1                  TEXT('(c) Finastra International +
     A                                      Limited 2013')
     A                                      SPACEA(002)
     A                                     5
     A                                      'REFERENCE ME0008P1'
     A                                      SKIPB(003)
     A            BJURPT        53        45
     A                                   101
     A                                      'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                    51
     A                                      'MULTICASH TO MIDAS INTER'
     A                                      SPACEB(002)
     A                                    75
     A                                      'FACE'
     A                                    +1
     A                                      '- MT100s Received'
     A                                    51
     A                                      '~~~~~~~~~~~~~~~~~~~~~~~~'
     A                                      SPACEB(001)
     A                                    75
     A                                      '~~~~~~~~~~~~~~~~~~~~~~'
     A                                     5
     A                                      'REFERENCE'
     A                                      SPACEB(002)
     A                                    35
     A                                      'SENDER'
     A                                    46
     A                                      'NAME        '
     A                                    73
     A                                      'BENEFICIARY '
     A                                    +0
     A                                      'NAME'
     A                                   +24
     A                                      'AMOUNT'
      *
     A          R ME0008F2
     A                                      SPACEB(001)
     A            MOR           28A  O     5
     A            SENDR         10        35
     A            NAME          26        46
     A            BENF          10        73
     A            NAM2          27A  O    +2
     A            AMT           20A  O    +1
     A          R ERR
     A                                      SPACEB(001)
     A                                     5
     A                                      '***** This record already processe-
     A                                      d *****'
     A          R ERR2
     A                                      SPACEB(001)
     A                                     5
     A                                      '***** Incomplete Message ******'
      *
      **------------------*
      **  Report Trailer  *
      **------------------*
      *
     A          R ENDAU
     A                                    10'Audit Information'
     A                                      SPACEB(2)
     A                                      SPACEA(2)
     A                                    10'RECORDS PROCESSED'
     A            NOKN           5  0     27TEXT('Records processed')
     A                                      EDTCDE(J)
     A                                      SPACEA(2)
     A                                    10'RECORDS CORRECT'
     A            OKN            5  0     27TEXT('Records correct')
     A                                      EDTCDE(J)
     A                                      SPACEA(1)
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
