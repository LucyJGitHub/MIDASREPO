/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas OF Modelo 123 Report Production')                *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE000007P1 - Midas OF Modelo 123 Report Production      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2015            *
      *                                                               *
      *  Last Amend No. CRE104   *CREATE   Date 20Apr15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE104 - Modelo 123 and 193 Reports                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER1
     A                                      SPACEB(5)
     A                                     1
     A                                      ' IR PF Impuesto sobre Sociedades. -
     A                                      IR N R (establecimientos permanente-
     A                                      s)'
     A                                      SPACEA(001)
     A                                      HIGHLIGHT
     A                                      COLOR(BLU)
     A                                     1
     A                                      ' Retención e ingreso a cuenta sobr-
     A                                      e determinados rendimientos del cap-
     A                                      ital mobiliario o determinadas rent-
     A                                      as'
     A                                      SPACEA(001)
     A                                      HIGHLIGHT
     A                                      COLOR(BLU)
     A                                     1
     A                                      ' Modelo 123'
     A                                      SPACEA(1)
     A                                      HIGHLIGHT
     A                                      COLOR(BLU)
      *
      *-----------------*
      ***  Header 2     *
      *-----------------*
      *
     A          R HEADER2
     A                                      SPACEB(2)
     A                                     1
     A                                      ' NIF del contribuyente'
     A                                      SPACEA(001)
     A            PRENIF         9A        2
     A                                      SPACEA(001)
     A                                      HIGHLIGHT
     A                                     2
     A                                      'Nombre/razón social'
     A                                      SPACEA(001)
     A            PRESUR        60A        2
     A                                      SPACEA(001)
     A                                      HIGHLIGHT
     A                                     2
     A                                      'Ejercicio             Periodo'
     A                                      SPACEA(001)
     A            PRYEAR         4A        2HIGHLIGHT
     A            PRPERD         2A       24
     A                                      SPACEA(001)
     A                                      HIGHLIGHT
      *
      *-----------------*
      ***  Details      *
      *-----------------*
      *
     A          R DETAIL
     A                                      SPACEB(2)
     A                                     7
     A                                      'Los códigos sombreados corresponde-
     A                                      n a las casillas del modelo oficial'
     A                                      SPACEA(002)
     A                                     1
     A                                      '__________________________________-
     A                                      ___________________________________-
     A                                      ___________________________________-
     A                                      ___________________________'
     A                                      SPACEA(001)
     A                                     1
     A                                      '|Nº de perceptores                -
     A                                                                         -
     A                                                                 [01]'
     A            PRNRCL        15S 0    105
     A                                      EDTCDE(2)
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A                                   131'|'
     A                                      SPACEA(001)
     A                                     1
     A                                      '|Base retenciones e ingresos a cue-
     A                                      nta                                -
     A                                                                 [02]'
     A  10N14     PGINT0        17S 0    102
     A                                      EDTWRD('  .   .   .   .   . 0 -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  11N14     PGINT1        17S 0    102
     A                                      EDTWRD(' .   .   .   .   . 0 , -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  12N14     PGINT2        17S 0    102
     A                                      EDTWRD('   .   .   .   . 0 ,  -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  13N14     PGINT3        17S 0    102
     A                                      EDTWRD('  .   .   .   . 0 ,   -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  14        PGINTB        17S 0    102
     A                                      EDTWRD('  .   .   .   .   .   -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A                                   131'|'
     A                                      SPACEA(001)
     A                                     1
     A                                      '|Retenciones e ingresos a cuenta  -
     A                                                                         -
     A                                                                 [03]'
     A  10N15     PTWHD0        17S 0    102
     A                                      EDTWRD('  .   .   .   .   . 0 -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  11N15     PTWHD1        17S 0    102
     A                                      EDTWRD(' .   .   .   .   . 0 , -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  12N15     PTWHD2        17S 0    102
     A                                      EDTWRD('   .   .   .   . 0 ,  -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  13N15     PTWHD3        17S 0    102
     A                                      EDTWRD('  .   .   .   . 0 ,   -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  15        PTWHDB        17S 0    102
     A                                      EDTWRD('  .   .   .   .   .   -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A                                   131'|'
     A                                      SPACEA(001)
     A                                     1
     A                                      '|Periodificación'
     A                                      HIGHLIGHT
     A                                   131'|'
     A                                      SPACEA(001)
     A                                     1
     A                                      '|Ingresos ejercicios anteriores   -
     A                                                                         -
     A                                                                 [04]'
     A  10N16     PIP990        17S 0    102
     A                                      EDTWRD('  .   .   .   .   . 0 -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  11N16     PIP991        17S 0    102
     A                                      EDTWRD(' .   .   .   .   . 0 , -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  12N16     PIP992        17S 0    102
     A                                      EDTWRD('   .   .   .   . 0 ,  -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  13N16     PIP993        17S 0    102
     A                                      EDTWRD('  .   .   .   . 0 ,   -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  16        PIP99B        17S 0    102
     A                                      EDTWRD('  .   .   .   .   .   -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A                                   131'|'
     A                                      SPACEA(001)
     A                                     1
     A                                      '|Regularización                   -
     A                                                                         -
     A                                                                 [05]'
     A  10N17     PNP990        17S 0    102
     A                                      EDTWRD('  .   .   .   .   . 0 -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  11N17     PNP991        17S 0    102
     A                                      EDTWRD(' .   .   .   .   . 0 , -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  12N17     PNP992        17S 0    102
     A                                      EDTWRD('   .   .   .   . 0 ,  -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  13N17     PNP993        17S 0    102
     A                                      EDTWRD('  .   .   .   . 0 ,   -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  17        PNP99B        17S 0    102
     A                                      EDTWRD('  .   .   .   .   .   -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A                                   131'|'
     A                                      SPACEA(001)
     A                                     1
     A                                      '|_________________________________-
     A                                      ___________________________________-
     A                                      ___________________________________-
     A                                      __________________________|'
     A                                      SPACEA(003)
     A                                     3
     A                                      'Total liquidación'
     A                                      SPACEA(001)
     A                                      HIGHLIGHT
     A                                     1
     A                                      '__________________________________-
     A                                      ___________________________________-
     A                                      ___________________________________-
     A                                      ___________________________'
     A                                      SPACEA(001)
     A                                     1
     A                                      '|Suma de retenciones e ingresos a -
     A                                      cuenta y regularización en su caso([-
     A                                      03] + [05])              [06]'
     A  10N18     PTWHT0        17S 0    102
     A                                      EDTWRD('  .   .   .   .   . 0 -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  11N18     PTWHT1        17S 0    102
     A                                      EDTWRD(' .   .   .   .   . 0 , -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  12N18     PTWHT2        17S 0    102
     A                                      EDTWRD('   .   .   .   . 0 ,  -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  13N18     PTWHT3        17S 0    102
     A                                      EDTWRD('  .   .   .   . 0 ,   -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  18        PTWHTB        17S 0    102
     A                                      EDTWRD('  .   .   .   .   .   -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A                                   131'|'
     A                                      SPACEA(001)
     A                                     1
     A                                      '|A deducir (exclusivamente en caso-
     A                                       de declaración complementaria)'
     A                                   131'|'
     A                                      SPACEA(001)
     A                                     1
     A                                      '|Resultado a ingresar de anteriore-
     A                                      s declaraciones por el mismo concep-
     A                                      to, ejercicio y periodo    [07]'
     A  10N19     PTWPT0        17S 0    102
     A                                      EDTWRD('  .   .   .   .   . 0 -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  11N19     PTWPT1        17S 0    102
     A                                      EDTWRD(' .   .   .   .   . 0 , -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  12N19     PTWPT2        17S 0    102
     A                                      EDTWRD('   .   .   .   . 0 ,  -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  13N19     PTWPT3        17S 0    102
     A                                      EDTWRD('  .   .   .   . 0 ,   -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  19        PTWPTB        17S 0    102
     A                                      EDTWRD('  .   .   .   .   .   -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A                                   131'|'
     A                                      SPACEA(001)
     A                                     1
     A                                      '|Resultado a ingresar [06] - [07] -
     A                                                                         -
     A                                                                 [08]'
     A  10N20     PTINC0        17S 0    102
     A                                      EDTWRD('  .   .   .   .   . 0 -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  11N20     PTINC1        17S 0    102
     A                                      EDTWRD(' .   .   .   .   . 0 , -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  12N20     PTINC2        17S 0    102
     A                                      EDTWRD('   .   .   .   . 0 ,  -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  13N20     PTINC3        17S 0    102
     A                                      EDTWRD('  .   .   .   . 0 ,   -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A  20        PTINCB        17S 0    102
     A                                      EDTWRD('  .   .   .   .   .   -')
     A                                      HIGHLIGHT
     A                                      UNDERLINE
     A                                   131'|'
     A                                      SPACEA(001)
     A                                     1
     A                                      '|_________________________________-
     A                                      ___________________________________-
     A                                      ___________________________________-
     A                                      __________________________|'
     A                                      SPACEA(001)
      *
      *-----------------*
      ***  Footer       *
      *-----------------*
      *
     A          R FOOTER
     A                                      SPACEB(3)
     A                                     2
     A                                      'Persona de contacto - Nombre:'
     A            PRPRSN        40A       32
     A                                      SPACEA(001)
     A                                      HIGHLIGHT
     A                                     2
     A                                      'Teléfono de contacto:'
     A            PRPHON         9A       24
     A                                      SPACEA(001)
     A                                      HIGHLIGHT
     A                                     2'Observaciones:'
