/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas OF Nostro Group Transaction Limit - List')       *
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  PRTF/SD009562P1 - Midas OF Nostro Group Transaction Limit    *
      *                    Report                                     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2014            *
      *                                                               *
      *  Last Amend No. CGL155   *Create   Date 06Mar14               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL155 - Forecast Group Limits                               *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADER1
     A                                      SKIPB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      'Reference SD009562P1'
     A                                      SPACEB(001)
     A            RRPTDS        53A  O    40
     A                                   +26
     A                                      'Date'
     A            BJMRDT    R    7   O    +1
     A                                      REFFLD(MRDT MFRFL)
     A                                   119
     A                                      'Page'
     A                                      SPACEB(001)
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A            FTITLE        43A  O    46
     A                                      SPACEB(001)
     A            FUNDRS        43A  O    46
     A                                      SPACEB(001)
      *
      *-----------------*
      ***  Header 2     *
      *-----------------*
      *
     A          R HEADER2
     A                                      SPACEB(001)
     A                                      SPACEA(001)
     A                                     1
     A                                      'Nostro Group'
     A                                      SPACEB(002)
     A            PRGRPC        10A  O    +2
     A                                   +37
     A                                      'Description'
     A            PRGDES        20A  O    +3
     A                                     1
     A                                      'Daily Limit'
     A                                      SPACEB(001)
     A            PRDLIM        21A  O    +3
     A                                   +26
     A                                      'Currency'
     A            PRLCCY         3A  O    +6
      *
      *-----------------*
      ***  Header 3     *
      *-----------------*
      *
     A          R HEADER3
     A                                     1
     A                                      'Last Change User'
     A                                      SPACEB(002)
     A                                    19
     A                                      '/'
     A                                    22
     A                                      'Date'
     A                                    31
     A                                      '/'
     A                                    36
     A                                      'Time'
     A                                    46
     A                                      '/'
     A                                    51
     A                                      'Action'
     A            PUSER1        10A  O     1
     A                                      SPACEB(001)
     A            PDATE1         7A  O    22
     A            PTIME1         8A  O    36
     A            PACT1         10A  O    51
     A          R HEADER4
     A                                      SPACEA(001)
     A                                     5'Nostro Code'
     A                                      SPACEB(001)
     A                                    18'Account Name'
     A                                    40'Report Name'
     A                                    56'Report Town'
      *
      *-----------------*
      ***  Details      *
      *-----------------*
      *
     A          R DETAIL
     A                                      SPACEB(001)
     A            PRNOST         5A  O     7
     A            PRACNM        20A  O    18
     A            PRCRNM        10A  O    41
     A            PRCRTN        10A  O    56
      *
      *-----------------*
      ***  Footer       *
      *-----------------*
      *
     A          R FOOTER
     A                                    46
     A                                      '*** END OF REPORT ***'
     A                                      SPACEB(003)
