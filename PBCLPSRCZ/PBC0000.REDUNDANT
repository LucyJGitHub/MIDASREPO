/*********************************************************************/
/*S*D****CLPBASE******************************************************/
/*********************************************************************/
/*                                                                   */
/*       Midas - Private Banking Module                              */
/*                                                                   */
/*       PBC0000 - Midas PB Module Setup                             */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2001           */
/*                                                                   */
/*       Last Amend No. CUP004  *REDUNDANT Date 13Oct10              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CSC017             Date 14Feb03              */
/* Midas Release 4 --------------- Base -----------------------------*/
/* Midas DBA 3.05 ---------------------------------------------------*/
/*       Prev Amend No. 183604             Date 12Sep00              */
/* Midas DBA 3.02 ---------------------------------------------------*/
/*                      CPB001  *CREATE    Date 02Jun99              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUP004 - Replacement for SCBEGIN.                          */
/*       CSC017 - Change to 250 library list.                        */
/*       183604 - Ensure TABLETX journalled for call of TABLETN pgm  */
/*       CPB001 - Private Banking Module                             */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&JOBTYPE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&RTCD) TYPE(*CHAR) LEN(7) VALUE(' ')
             DCL        VAR(&FMT) TYPE(*CHAR) LEN(200)
             DCL        VAR(&NOREPLY) TYPE(*CHAR) LEN(1)
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)
/************DCL        VAR(&INLLIBL)  TYPE(*CHAR) LEN(275)                 */ /*183604*/ /*CSC017*/
/************DCL        VAR(&CMD)      TYPE(*CHAR) LEN(289)                 */ /*183604*/ /*CSC017*/
             DCL        VAR(&INLLIBL)  TYPE(*CHAR) LEN(2750)                              /*CSC017*/
             DCL        VAR(&CMD)      TYPE(*CHAR) LEN(2764)                              /*CSC017*/
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2001')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             RTVJOBA    TYPE(&JOBTYPE)
             OVRMSGF    MSGF(PBUSRMSG) TOMSGF(GBPBUSRMSG)
 
/*  Access SDMMODPD for whether PB module is on             */
 
             CALL       PGM(AOMMODR0) PARM(&RTCD '*FIRST' &FMT)
 
/*  Database error handling  */
 
             IF         COND(&RTCD *NE '       ') THEN(DO)
                SNDPGMMSG  MSG('Database error on Midas modules +
                             file') TOMSGQ(MOPERQ MRUNQ)
 
/* Call SCC0010 to inform user that an error has occurred */
 
                RTVMSG     MSGID(PB00010) MSGF(PBUSRMSG) +
                             MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (251 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(PB00011) MSGF(PBUSRMSG) +
                             MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (301 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('PBC0000' 'ENTER' ' ')
                GOTO       CMDLBL(ABNOR)
 
             ENDDO
 
/*  TI must be switched on before PB File Setup can be run  */
 
             IF         COND(%SST(&FMT 104 1) *NE 'Y') THEN(DO)
 
                SNDPGMMSG  MSG('Switch on PB via Mode 1 Modules +
                          Maintenance option before running +
                          Private Banking Setup program') +
                          TOMSGQ(MOPERQ MRUNQ)
 
/*  Call SCC0010 to inform user that program cannot be run now */
 
                RTVMSG     MSGID(PB00012) MSGF(PBUSRMSG) +
                             MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (251 50)) VALUE(&MESSAGE)
                RTVMSG     MSGID(PB00013) MSGF(PBUSRMSG) +
                             MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (351 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('PBC0000' 'ENTER' +
                             &NOREPLY)
                GOTO       CMDLBL(END)
 
             ENDDO
 
             CHGJOB     SWS(XXXXXX00)
             SNDPGMMSG  MSG('Initial PB file setup')  TOMSGQ(MOPERQ)
                                                                        /*183604*/
             STRJRNPF   FILE(TABLETX TABLETA) JRN(ICJRN) +
                          IMAGES(*BOTH) OMTJRNE(*OPNCLO)                /*183604*/
             MONMSG     MSGID(CPF0000)                                  /*183604*/
                                                                        /*183604*/
             RTVJOBD    JOBD(TIJOBD) INLLIBL(&INLLIBL)                  /*183604*/
                                                                        /*183604*/
             CHGVAR     VAR(&CMD) VALUE('CHGLIBL LIBL(' *CAT +
                             &INLLIBL *CAT ')')                         /*183604*/
                                                                        /*183604*/
/************CALL       PGM(QCMDEXC) PARM(&CMD 289)                         */ /*183604*/ /*CSC017*/
             CALL       PGM(QCMDEXC) PARM(&CMD 2764)                                      /*CSC017*/
 
/* Call PB0000 */
 
             CALL       PGM(PB0000)
 
/* Database error processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                RTVMSG     MSGID(PB00014) MSGF(PBUSRMSG) +
                             MSG(&MESSAGE)
                CHGDTAARA  DTAARA(MIDASMSG (251 50)) VALUE(&MESSAGE)
                CALL       PGM(SCC0010) PARM('PBC0000' 'ENTER' +
                               &NOREPLY)
                GOTO       CMDLBL(ABNOR)
             ENDDO
                                                                        /*183604*/
             ENDJRNPF   FILE(TABLETX TABLETA) JRN(ICJRN)                /*183604*/
             MONMSG     MSGID(CPF0000)                                  /*183604*/
 
             SNDPGMMSG  MSG('PB Customer Flag setup in file +
                          SDCUSTPD') TOMSGQ(MOPERQ)
 
/* Call SD0420 */
 
             CALL       PGM(SD0420) PARM('1')
 
/* Database error processing */
 
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                CHGVAR     VAR(&RTCD) VALUE('*ERROR')
                SNDPGMMSG  MSG('Program SD0420 ended abnormally') +
                          TOMSGQ(MOPERQ)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             SNDPGMMSG  MSG('PB Files set up') TOMSGQ(MRUNQ)
             RTVMSG     MSGID(PB00015) MSGF(PBUSRMSG) MSG(&MESSAGE)
             CHGDTAARA  DTAARA(MIDASMSG (251 50)) VALUE(&MESSAGE)
             CALL       PGM(SCC0010) PARM('PBC0000' 'ENTER' &NOREPLY)
             GOTO       CMDLBL(END)
 
 ABNOR:
 
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          PBC0000 ended abnormally - see job log') +
                          TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
             RTVMSG     MSGID(PB00016) MSGF(PBUSRMSG) MSG(&MESSAGE)
             CHGDTAARA  DTAARA(MIDASMSG (251 50)) VALUE(&MESSAGE)
             CALL       PGM(SCC0010) PARM('PBC0000' 'ENTER' &NOREPLY)
 
END:         CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Misys International Banking Systems Ltd.')
                                                                        /*183604*/
             RTVJOBD    JOBD(MIDASIC) INLLIBL(&INLLIBL)                 /*183604*/
                                                                        /*183604*/
             CHGVAR     VAR(&CMD) VALUE('CHGLIBL LIBL(' *CAT +
                             &INLLIBL *CAT ')')                         /*183604*/
                                                                        /*183604*/
/************CALL       PGM(QCMDEXC) PARM(&CMD 289)                         */ /*183604*/ /*CSC017*/
             CALL       PGM(QCMDEXC) PARM(&CMD 2764)                                      /*CSC017*/
 
             ENDPGM
