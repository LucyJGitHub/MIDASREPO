      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PB Supplementary Customer Fields for Take-on')
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PB0011DF - Supplementary Customer fields for Take-On         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. 198158             Date 27Sep01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CPB002  *CREATE    Date 08Nov00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  198158 - Add validation on Account Manager: must be defined  *
      *           and have code 1 or 3.                               *
      *  CPB002 - 'Private Banking' Module                            *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03
     A  36                                  CA10
     A  44                                  CA12
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      ***  Screen 1 Format.
      *
     A          R PB0011F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      BLINK
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'PB0011F1'
     A                                  1 24'Supplementary Customer fields +
     A                                       for Take-on'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 03 03'Action'
     A            SACT           1A  B 03 11
     A N61                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                 04 03'Customer Number'
     A            S1CUST         6A  B 04 20
     A N62                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A                                 04 52'Review from'
     A            S1RCRG         6A  B 04 65
     A N63                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A            SLINE         79A  O 05 02DSPATR(UL)
     A                                 23 03'F3=Exit  Review from'
      *
      ***  Screen 2 Format Amend/Enquiry
      *
     A          R PB0011F2
     A                                      BLINK
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'PB0011F2'
     A                                  1 24'Supplementary Customer Fields +
     A                                       Take-on'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 03 03'Action'
     A            SACT           1A  O 03 11
     A                                      DSPATR(HI)
     A  35                             03 72'DELETED'
     A  35                                  DSPATR(BL)
     A                                 04 03'Customer number'
     A            S1CUST         6A  O 04 20
     A                                      DSPATR(HI)
      *
     A            SLINE         79A  O 05 02DSPATR(UL)
      *
     A                                  7  3'Internal type'
     A            SCSTP          4   B  7 23
     A N73                                  DSPATR(HI)
     A N73N51                               DSPATR(UL)
     A  73                                  DSPATR(RI PC)
     A  51                                  DSPATR(PR)
      *
     A                                  8  3'Usual city'
     A            SUSCI         40   B  8 23
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A                                  9  3'Zip city code'
     A            SZIPC         10   B  9 23
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A                                 10  3'Usual language'
     A            SLGID          2   B 10 23
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A                                 11  3'Birth place'
     A            SBTHP         20   B 11 23
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A                                 12  3'Birth date'
     A            SBTHD          8   B 12 23
     A N67                                  DSPATR(HI)
     A N67N51                               DSPATR(UL)
     A  67                                  DSPATR(RI PC)
     A  51                                  DSPATR(PR)
      *
     A                                 13  3'Document Id.'
     A            SDOCI          4   B 13 23
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A                                 14  3'Document number'
     A            SDOCN         15   B 14 23
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A                                 15  3'Document date'
     A            SDOCD          8   B 15 23
     A N68                                  DSPATR(HI)
     A N68N51                               DSPATR(UL)
     A  68                                  DSPATR(RI PC)
     A  51                                  DSPATR(PR)
      *
     A                                 16  3'Document location'
     A            SDOCL         15   B 16 23
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A                                 17  3'Risk Country'
     A            SRISC          3   B 17 23
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A                                 18  3'Civil status'
     A            SCIVS          4   B 18 23
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A                                 18 46'Office phone number'
     A            SCTNB         15   B 18 66
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A                                 20  3'Nature code'
     A            SNATU          4   B 20 23
     A N69                                  DSPATR(HI)
     A N69N51                               DSPATR(UL)
     A  69                                  DSPATR(RI PC)
     A  51                                  DSPATR(PR)
      *
     A                                 21  3'Acc manager code'
     A**Start of change CGL029
     A**********  SACMG          4   B 21 23                                                  CGL029
     A            SACMG         10   B 21 23
     A**End of change CGL029
     A*                                              Begin /198158/
     A**********                            DSPATR(HI)                    198158
     A*N51******                            DSPATR(UL)                    198158
     A N70                                  DSPATR(HI)
     A N70N51                               DSPATR(UL)
     A  70                                  DSPATR(RI PC)
     A*                                              End   /198158/
     A  51                                  DSPATR(PR)
      *
     A                                  7 46'Default currency'
     A            SDFCY          3   B  7 66
     A N71                                  DSPATR(HI)
     A N71N51                               DSPATR(UL)
     A  71                                  DSPATR(RI PC)
     A  51                                  DSPATR(PR)
      *
     A                                  9 46'Working currency'
     A            SWRCY          3   B  9 66
     A N72                                  DSPATR(HI)
     A N72N51                               DSPATR(UL)
     A  72                                  DSPATR(RI PC)
     A  51                                  DSPATR(PR)
      *
     A                                 10 46'Telex number'
     A            STLNB         15   B 10 66
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A                                 11 46'Fax number'
     A            SFXNB         15   B 11 66
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A                                 12 46'Management type'
     A            SMGTP          4   B 12 66
     A N74                                  DSPATR(HI)
     A N74N51                               DSPATR(UL)
     A  74                                  DSPATR(RI PC)
     A  51                                  DSPATR(PR)
      *
     A                                 13 46'Investment profile'
     A            SINTP          4   B 13 66
     A N75                                  DSPATR(HI)
     A N75N51                               DSPATR(UL)
     A  75                                  DSPATR(RI PC)
     A  51                                  DSPATR(PR)
      *
     A                                 14 46'Phone discharge'
     A            SPHDS          4   B 14 66
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A                                 15 46'Secret code'
     A            SSECR         12   B 15 66
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A                                 16 46'GSM Number'
     A            SGSMN         15   B 16 66
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A                                 17 46'IBLC code'
     A            SIBLC          9   B 17 66
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A                                 19  3'Entitled code'
     A            SENTC          3   B 19 23
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A**Start of change CGL029
     A**********                       19 30'Multi-Port'                                      CGL029
     A**********  SMULT          1   B 19 41                                                  CGL029
     A                                 19 36'Multi-Port'
     A            SMULT          1   B 19 47
     A**End of change CGL029
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A**Start of change CGL029
     A**********                       19 46'Department'                                      CGL029
     A**********  SDEPT          3   B 19 66                                                  CGL029
     A                                 19 52'Department'
     A            SDEPT          3   B 19 72
     A**End of change CGL029
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A**Start of change CGL029
     A**********                       20 30'Ownership'                                       CGL029
     A**********  SOWNS          1   B 20 41                                                  CGL029
     A                                 20 36'Ownership'
     A            SOWNS          1   B 20 47
     A**End of change CGL029
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A**Start of change CGL029
     A**********                       20 46'Account access ind'                              CGL029
     A**********  SACSS          1   B 20 66                                                  CGL029
     A                                 20 52'Account access ind'
     A            SACSS          1   B 20 72
     A**End of change CGL029
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A**Start of change CGL029
     A**********                       21 30'Sign.Power'                                      CGL029
     A**********  SCSSG          1   B 21 41                                                  CGL029
     A                                 21 36'Sign.Power'
     A            SCSSG          1   B 21 47
     A**End of change CGL029
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
      *
     A**Start of change CGL029
     A**********                       21 46'Joint/Linked Acc'                                CGL029
     A**********  SJOIN          1   B 21 66                                                  CGL029
     A                                 21 52'Joint/Linked Acc'
     A            SJOIN          1   B 21 72
     A**End of change CGL029
     A                                      DSPATR(HI)
     A N51                                  DSPATR(UL)
     A  51                                  DSPATR(PR)
     A            S2RRN          4  0H
      *
     A N36                             23 03'F3=Exit  F12=Previous Screen'
     A  36                             23 03'F3=Exit  F10=Delete'
     A  36                             23 24'F12=Previous Screen'
      *
      ***  Screen 3 Subfile Record Format.
      *
     A          R PB0011S1                  SFL
      *
     A  33                                  SFLNXTCHG
     A            S2SEL          1A  B  8  8CHECK(ER)
     A N66                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI PC)
     A            S2TYP          1A  O  8 10
     A                                      DSPATR(BL)
     A                                      DSPATR(HI)
     A            S2CUST         6A  O  8 12DSPATR(HI)
     A            S2CSSN        10A  O  8 24DSPATR(HI)
     A            S2USCI        20A  O  8 40DSPATR(HI)
     A            S2RRN          4S 0H
      *
      ***  Subfile Control Record Format.
      *
     A          R PB0011C1                  SFLCTL(PB0011S1)
      *
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      BLINK
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A  47                                  ROLLUP(27)
      *
     A            SFLRR2         4S 0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'PB0011C1'
     A                                  1 24'Supplementary Fields for +
     A                                       Take-On'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  3 52'Review from'
     A            S1RCRG         6A  B  3 65DSPATR(HI)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A            SLINE         79A  O  4  2DSPATR(UL)
     A                                  5 12'Customer'
     A                                  6 12'Number'
     A                                  5 24'Shortname'
     A                                  5 40'Report Name'
     A            S2RCRG         6A  H
      *
      ***  Bottom format
      *
     A          R PB0011F3                  OVERLAY
     A                                 23 02'F3=Exit  F12=Previous Screen'
      *
      ***  Message Subfile Record Format.
      *
     A          R PB0011S2                  TEXT('Message Subfile Format')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            ##SFKY                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
      ***  Message Subfile Control Format.
      *
     A          R PB0011C2
     A                                      SFLCTL(PB0011S2)
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            ##PGMQ                    SFLPGMQ
