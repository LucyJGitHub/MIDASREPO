      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PB Template portfolio Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PB0013DF - Template Portfolio maintenance                    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Last Amend No. CPB001  *CREATE    Date  2Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPB001 - 'Private Banking' Module                            *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      VLDCMDKEY(60 'ANY VALID KEY')
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03
     A*======================================================================
     A** FORMAT PB0013F1 **
     A*======================================================================
     A*
     A          R PB0013F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      BLINK
     A                                      OVERLAY
     A            USID          10A  O  1  2
     A                                  1 13'PB0013'
     A            BJMRDT         7A  O  1 72
     A            WSIDN         10A  O  2  2
     A                                  2 72TIME
     A                                  4  2'Action'
     A            DDACTN         1A  B  4 12TEXT('ACTION')
     A                                      DSPATR(HI)
     A N25N26                               DSPATR(UL)
     A  25                                  DSPATR(PR)
     A  26                                  DSPATR(PC)
     A  26                                  DSPATR(RI)
     A                                  5  2'Portfolio'
     A            DDPREF         4A  B  5 15TEXT('PORTFOLIO REFERENCE')
     A                                      DSPATR(HI)
     A N25N28                               DSPATR(UL)
     A  25                                  DSPATR(PR)
     A  28                                  DSPATR(PC)
     A  28                                  DSPATR(RI)
     A                                  5 65'Portfolio'
     A  25                                  DSPATR(ND)
     A            DDPORT         4A  B  5 75DSPATR(HI)
     A  25                                  DSPATR(PR)
     A  25                                  DSPATR(ND)
     A                                  6  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                  1 23'TEMPLATE PORTFOLIO DEFINITION MAIN-
     A                                      TENANCE'
     A                                      DSPATR(HI)
     A*======================================================================
     A** FORMAT PB0013F2
     A*======================================================================
     A          R PB0013F2
     A                                      TEXT('HEADER FORMAT')
     A                                      OVERLAY
     A                                      CF12
     A                                  7  2'Portfolio Narrative'
     A            DDPTFN        20   B  7 23
     A                                      DSPATR(HI)
     A N31N59                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  59                                  DSPATR(PC)
     A  59                                  DSPATR(RI)
     A                                  9  2'Valuation Frequency'
     A            DDVLFQ         1A  B  9 23TEXT('VALUATION FREQUENCY')
     A                                      DSPATR(HI)
     A N31N40                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A                                  9 27'Day in Month'
     A            DDVLDM         2A  B  9 41TEXT('DAY IN MONTH')
     A                                      DSPATR(HI)
     A N31N41                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A                                  9 46'Next Statement Date'
     A            DDVLND         6A  B  9 67TEXT('NEXT STATEMENT DATE')
     A                                      DSPATR(HI)
     A N31N42                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A                                 11  2'Select (Y/N) Sections of Statement-
     A                                       to be Printed:'
     A                                 12  5'Inst.Summary'
     A            DDS9VR         1A  B 12 18TEXT('INSTRUMENT SUMMARY')
     A                                      DSPATR(HI)
     A N31N44                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A                                 12 20'Curr.Summ.'
     A            DDS0VR         1A  B 12 31TEXT('CURRENCY SUMMARY')
     A                                      DSPATR(HI)
     A N31N45                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A                                 12 34'Position Details'
     A            DDS1VR         1A  B 12 51TEXT('RESUME')
     A                                      DSPATR(HI)
     A N31N43                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A                                 12 54'Sector Breakdowns'
     A            DDS2VR         1A  B 12 72TEXT('SECTOR BREAKDOWNS')
     A                                      DSPATR(HI)
     A N31N46                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A                                 13  5'Performance For Past Year'
     A            DDS3VR         1A  B 13 31TEXT('Performance for Past Year')
     A                                      DSPATR(HI)
     A N31N47                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A                                 13 34'Tax Year To Date'
     A            DDS4VR         1A  B 13 51TEXT('Tax Year to Date')
     A                                      DSPATR(HI)
     A N31N48                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A                                 13 54'Additional Period'
     A            DDS5VR         1A  B 13 72TEXT('Monthly Cashflo')
     A                                      DSPATR(HI)
     A N31N49                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  49                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A                                 14  5'Daily Cashflows'
     A            DDS6VR         1A  B 14 31TEXT('Monthly Cashflo')
     A                                      DSPATR(HI)
     A N31N50                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A                                 14 34'Monthly'
     A            DDS7VR         1A  B 14 51TEXT('Monthly Cashflows')
     A                                      DSPATR(HI)
     A N31N51                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A                                 14 54'Yearly'
     A            DDS8VR         1A  B 14 72TEXT('Yearly Cashflows')
     A                                      DSPATR(HI)
     A N31N52                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A                                 17  2'Performance Periods:'
     A                                 17 23'Report Past Year (Y/N)'
     A            DDRPYP         1A  B 17 46TEXT('Report Past Year Performance')
     A                                      DSPATR(HI)
     A N31N53                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A                                 17 49'Tax Year Start Date'
     A            DDTYSM         2A  B 17 69TEXT('Cust. Tax Year Start Month')
     A                                      DSPATR(HI)
     A N31N54                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A                                 18 14'Additional Period (H/Q)'
     A            DDADPP         1A  B 18 38TEXT('Additional Perform. Period')
     A                                      DSPATR(HI)
     A N31N55                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A                                 18 42'Performance Start Date'
     A            DDPSDT         6A  B 18 69TEXT('Performance Extracted From')
     A                                      DSPATR(HI)
     A N31N56                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A                                 19 14'Performance Net/Gross of Tax (N/G)'
     A            DDPMNG         1A  B 19 50TEXT('Net or Gross of Tax')
     A                                      DSPATR(HI)
     A N31N57                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A*======================================================================
     A** FORMAT PB0013E3
     A*======================================================================
     A          R PB0013E3                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*======================================================================
     A** FORMAT PB0013F3
     A*======================================================================
     A          R PB0013F3                  SFLCTL(PB0013E3)
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A N65                                  SFLEND
     A                                      SFLINZ
     A            DDPGMQ                    SFLPGMQ
     A*
     A            DDINFO        76A  O 23  2
     A**=====================================================================
     A** FORMAT PM063300 **
     A**=====================================================================
     A          R PB0013S0                  SFL
     A            DDACTN         1A  B  9  9DSPATR(HI)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                      CHECK(ER)
     A            DDPTFR         4A  O  9 17TEXT('PORTFOLIO REFERENCE')
     A                                      DSPATR(HI)
     A            DDPTFN        20A  O  9 31TEXT('PORTFOLIO NARRATIVE')
     A                                      DSPATR(HI)
     A**=====================================================================
     A** FORMAT PB0013C0 **
     A**=====================================================================
     A          R PB0013C0                  SFLCTL(PB0013S0)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A                                      BLINK
     A  61                                  SFLDSP
     A  62                                  SFLDSPCTL
     A N61N62                               SFLCLR
     A  63                                  SFLEND(*MORE)
     A                                      CF12
     A N63                                  ROLLUP(21)
     A N64                                  ROLLDOWN(22)
     A            USID          10A  O  1  2
     A                                  1 13'PB0013'
     A                                  1 23'TEMPLATE PORTFOLIO DEFINITION MAIN-
     A                                      TENANCE'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            BJMRDT         7A  O  1 72
     A            WSIDN         10A  O  2  2
     A                                  2 72TIME
     A                                  3 65'Portfolio'
     A            DDPORT         4A  B  3 75DSPATR(HI)
     A                                  4  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                  6  7'Action'
     A                                  6 17'Portfolio'
     A                                  6 31'Portfolio'
     A                                  7 17'Reference'
     A                                  7 31'Narrative'
     A                                  3 53'Review from'
     A**=====================================================================
     A** FORMAT PB0013F4 **
     A**=====================================================================
     A          R PB0013F4                  TEXT('CLEAR SCREEN')
     A                                  1  2' '
