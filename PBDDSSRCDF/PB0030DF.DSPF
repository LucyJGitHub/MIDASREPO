      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PB Instrument Type Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PB0030DF - Intrument Type Maintenance                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CPB001  *CREATE    Date 29Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPB001 - 'Private Banking' Module                            *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03
     A  58                                  CA10
     A  50                                  CA12
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      ***  Screen 1 Format.
      *
     A          R PB0030F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      BLINK
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'PB0030F1'
     A                                  1 28'INSTRUMENT TYPE MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 03 03'Action'
     A            SACT           1A  B 03 11
     A N61                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                 04 03'Instrument Type'
     A            S1INNR         3A  B 04 20
     A N62                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A                                 04 52'Review from'
     A            S1RCRG         3A  B 04 65
     A N63                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A            SLINE         79A  O 05 02DSPATR(UL)
     A                                 23 03'F3=Exit  Review from'
      *
      ***  Screen 2 Format Insert/Amend/Enquiry/Delete
      *
     A          R PB0030F2
     A                                      BLINK
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'PB0030F2'
     A                                  1 28'INSTRUMENT TYPE MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 03 03'Action'
     A            SACT           1A  O 03 11
     A                                      DSPATR(HI)
     A  57                             03 72'DELETED'
     A  57                                  DSPATR(BL)
     A                                 04 03'Instrument Type'
     A            S1INNR         3A  O 04 20
     A                                      DSPATR(HI)
      *
     A            SLINE         79A  O 05 02DSPATR(UL)
      *
     A                                  9  3'Instrument Narrative'
     A            SISTN         20   B  9 32
     A N64                                  DSPATR(HI)
     A N64N51                               DSPATR(UL)
     A  64                                  DSPATR(RI PC)
     A  51                                  DSPATR(PR)
     A            S2RRN          4  0H
      *
     A N58                             23 03'F3=Exit  F12=Previous Screen'
     A  58                             23 03'F3=Exit  F10=Delete'
     A  58                             23 24'F12=Previous Screen'
      *
      ***  Screen 3 Subfile Record Format.
      *
     A          R PB0030S1                  SFL
      *
     A  33                                  SFLNXTCHG
     A            S2SEL          1A  B  8  8CHECK(ER)
     A N66                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI PC)
     A            S2TYP          1A  O  8 10
     A                                      DSPATR(BL)
     A                                      DSPATR(HI)
     A            S2INNR         3A  O  8 12DSPATR(HI)
     A            S2ISTN        20A  O  8 25DSPATR(HI)
     A            S2RRN          4S 0H
      *
      ***  Subfile Control Record Format.
      *
     A          R PB0030C1                  SFLCTL(PB0030S1)
      *
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      BLINK
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A  47                                  ROLLUP(27)
      *
      *
     A            SFLRR2         4S 0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'PB0030C1'
     A                                  1 28'INSTRUMENT TYPE MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  3 52'Review from'
     A            S1RCRG         3A  B  3 65DSPATR(HI)
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A            SLINE         79A  O  4  2DSPATR(UL)
     A                                  5 11'Instrument'
     A                                  6 11'Type'
     A                                  5 25'Narrative'
     A            S2RCRG         3A  H
      *
      ***  Bottom format
      *
     A          R PB0030F3                  OVERLAY
     A                                 23 02'F3=Exit  F12=Previous Screen'
      *
      ***  Message Subfile Record Format.
      *
     A          R PB0030S2                  TEXT('Message Subfile Format')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            ##SFKY                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
      ***  Message Subfile Control Format.
      *
     A          R PB0030C2
     A                                      SFLCTL(PB0030S2)
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            ##PGMQ                    SFLPGMQ
