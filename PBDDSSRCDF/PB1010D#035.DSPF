      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PB Display Currency Group')
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  DSPF/PB1010D# - Midas PB Currency Group subfile              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CPB001  *CREATE    Date 06Oct99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPB001 - 'Private Banking' Module                            *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1010D#99
     A          R #SFLRCD                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,PB1010D#98
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Subfile next change')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A            #1CODE         3      9  8TEXT('Currency Gr')
     A                                      DSPATR(HI)
     A            #1DESC        30      9 17TEXT('Currency Description')
     A                                      DSPATR(HI)
     A            #1TYPE         1      9 53TEXT('Group Type')
     A                                      DSPATR(HI)
     A            #1RECY         3      9 59TEXT('Reference Ccy')
     A                                      DSPATR(HI)
     A            #1DSCY         1      9 68TEXT('Reference Ccy')
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1010D#97
     A          R #SFLCTL                   TEXT('Subfile control.')
     A*/COPY WNCPYSRC,PB1010D#96
     A                                      SFLCTL(#SFLRCD)
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      BLINK OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Clear subfile')
     A                                      INDTXT(81 'Display subfile')
     A                                      INDTXT(82 'End of subfile')
     A                                      INDTXT(86 'Enable PUTOVR')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A                                      SETOF(31 'Invalid: #2CODE')
     A                                      SETOF(32 'Invalid: #2DESC')
     A                                      SETOF(33 'Invalid: #2TYPE')
     A                                      SETOF(34 'Invalid: #2RECY')
     A                                      SETOF(35 'Invalid: #2DSCY')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'PB1010D1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 28'Currency Group Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2TYLC         1   H      TEXT('Type of Last Change')
     A            #2TYPE         1   H      TEXT('Group type')
     A            #2RECY         3   H      TEXT('Reference Ccy')
     A            #2DSCY         1   H      TEXT('Amount display in ccy')
     A                                  4  8'Group'
     A                                  4 17'Description'
     A                                  4 51'Group'
     A                                  4 59'Ref.'
     A                                  4 65'Amount'
     A                                  5  8'Code '
     A                                  5 51'Type '
     A                                  5 59'Ccy'
     A                                  5 65'display'
     A                                  6 65'in Ccy'
     A            #2CODE         3   B  7  8TEXT('Currency Gr')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2DESC        30   B  7 17TEXT('Currency Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1010D#95
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,PB1010D#94
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main Menu   F5=Refresh'
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1010D#93
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,PB1010D#92
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1010D#91
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,PB1010D#90
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1010D#89
