      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PB Currency Group code Details Edit record')
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  DSPF/PB1011R# - Midas PB Currency Group Code Maint           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CPB001  *CREATE    Date 05Nov99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPB001 - 'Private Banking' Module                            *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1011R#99
     A          R #RCDKEY                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,PB1011R#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '89')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A                                      CF09(09 'Change mode.')
     A* SETOFFS.................................................................
     A                                      SETOF(85 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CODE')
     A                                      SETOF(32 'Invalid: #1TYPE')
     A                                      SETOF(33 'Invalid: #1RECY')
     A                                      SETOF(34 'Invalid: #1DSCY')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'PB1011R1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 26'New Currency Group Code Details'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  5  2'Currency Group code'
     A            #1CODE         3   B  5 25TEXT('Ccy gr Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1011R#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,PB1011R#96
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1011R#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,PB1011R#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(89 '''ADD'' mode')
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A N89                                  CF10(10 'Delete.')
     A* SETOFFS.................................................................
     A                                      SETOF(85 'Global error flag')
     A                                      SETOF(31 'Invalid: #1CODE')
     A                                      SETOF(32 'Invalid: #1TYPE')
     A                                      SETOF(33 'Invalid: #1RECY')
     A                                      SETOF(34 'Invalid: #1DSCY')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'PB1011R2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 20'Currency Group Code Maintenance'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A                                  5  2'Currency Group Code.......:'
     A            #1CODE         3      5 30TEXT('Ccy gr Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Currency Group Description:'
     A            #1DESC        30   B  6 30TEXT('Ccy gr Description')
     A                                      CHECK(LC)
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  7  2'Group Type ...............:'
     A            #1TYPE         1   B  7 30TEXT('Group Type')
     A N25                                  OVRDTA
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  8  2'Reference currency........:'
     A            #1RECY         3   B  8 30TEXT('Reference Ccy')
     A N25                                  OVRDTA
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A                                  9  2'Amount display in currency:'
     A            #1DSCY         1   B  9 30TEXT('Amount displa')
     A N25                                  OVRDTA
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1011R#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,PB1011R#92
     A                                      OVERLAY PUTOVR
     A            ##CTX1        78     23  2TEXT('Command Text 1')
     A N25                                  OVRDTA
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1011R#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,PB1011R#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1011R#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,PB1011R#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1011R#87
