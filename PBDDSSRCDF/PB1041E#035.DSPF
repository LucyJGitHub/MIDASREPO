      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PB Base Rate code Display record(1 scr)')
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  DSPF/PB1041E# - Midas PB Base Rate Code Enq                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CPB001  *CREATE    Date 05Nov99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPB001 - 'Private Banking' Module                            *
      *                                                               *
      *****************************************************************
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1041E#99
     A          R #RCDKEY                   TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A*/COPY WNCPYSRC,PB1041E#98
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'PB1041E1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 32'Base Rate Code Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A                                  5  2'Base Rate Code'
     A            #1BACD         2      5 21TEXT('Base rate Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1041E#97
     A          R #CMDTXT1                  TEXT('Command keys')
     A*/COPY WNCPYSRC,PB1041E#96
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Exit'
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1041E#95
     A          R #RCDDTL1                  TEXT('Details')
     A*/COPY WNCPYSRC,PB1041E#94
     A                                      SETOF(98 'Force input format')
     A                                      BLINK OVERLAY
     A                                      INDTXT(86 'Enable PUTOVR')
     A  86                                  PUTOVR
     A                                      INDTXT(88 'Protect key fields')
     A                                      INDTXT(87 'Enable key screen')
     A* Command keys............................................................
     A                                      VLDCMDKEY(29 'Valid command key')
     A                                      CA03(03 'Exit.')
     A  87                                  CA12(12 'Key screen.')
     A* SETOFFS.................................................................
     A                                      SETOF(99 'Global error flag')
     A*.........................................................................
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'PB1041E2'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 34'Base Rate Code Enquiry'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1TYLC         1   H      TEXT('Type of Last Change')
     A                                  5  2'Base Rate Code............:'
     A            #1BACD         2      5 30TEXT('Base rate Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  6  2'Base Rate Name............:'
     A            #1BANA        30      6 30TEXT('Weight Description')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A                                  7  2'Attach code...............:'
     A            #1ATTA         1      7 30TEXT('Attach Code')
     A N25                                  OVRDTA
     A                                      DSPATR(HI)
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1041E#93
     A          R #CMDTXT2                  TEXT('Command keys')
     A*/COPY WNCPYSRC,PB1041E#92
     A                                      OVERLAY PUTOVR
     A                                 23  2'F3=Main menu   F12=Previous'
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1041E#91
     A          R #MSGRCD                   TEXT('Program messages')
     A*/COPY WNCPYSRC,PB1041E#90
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1041E#89
     A          R #MSGCTL                   TEXT('Program messages')
     A*/COPY WNCPYSRC,PB1041E#88
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A*/COPY WNCPYSRC,PB1041E#87
