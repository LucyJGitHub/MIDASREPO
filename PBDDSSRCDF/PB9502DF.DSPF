      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PB TOF Retail Accruals Take-on Selection')
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PB9502DF - Retail Accruals Take-On                           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CPB002  *CREATE    Date 25Aug00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CA03
     A  58                                  CA10
     A  50                                  CA12
     A  70                                  CA21
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
      ***  Screen 1 Format.
      *
     A          R PB9502F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      BLINK
      *
      *
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'PB9502F1'
     A                                  1 26'RETAIL ACCRUALS TAKE-ON'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                 07 15'Customer'
     A            S1CUST        10A  B 07 40
     A N62                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  62                                  DSPATR(RI PC)
     A                                 09 15'Branch Code'
     A            S1BRCA         3A  B 09 40
     A N61                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  61                                  DSPATR(RI PC)
     A                                 11 15'Currency'
     A            S1ACCY         3A  B 11 40
     A N63                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  63                                  DSPATR(RI PC)
     A                                 13 15'Account Code'
     A**Start of change CGL029
     A**********  S1ACOD         4A  B 13 40                                                  CGL029
     A            S1ACOD        10A  B 13 40
     A**End of change CGL029
     A N64                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  64                                  DSPATR(RI PC)
     A                                 15 15'Account Sequence'
     A            S1ASEQ         2A  B 15 40
     A N65                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  65                                  DSPATR(RI PC)
     A                                 23 03'F3=Exit'
      *
      ***  Screen 2 Subfile Record Format.
      *
     A          R PB9502S1                  SFL
      *
     A  33                                  SFLNXTCHG
     A            S2SEL          1A  B  8 13
     A N66                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI PC)
     A                                      VALUES('X' ' ')
     A**Start of change CGL029
     A**********  S2ACCN        18A  O  8 30DSPATR(HI)                                        CGL029
     A            S2ACCN        24A  O  8 30DSPATR(HI)
     A**End of change CGL029
     A            S2RRN          4S 0H
      *
      ***  Subfile Control Record Format.
      *
     A          R PB9502C1                  SFLCTL(PB9502S1)
      *
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      BLINK
     A  28                                  SFLDSP
     A  29                                  SFLDSPCTL
     A  30                                  SFLCLR
     A  31                                  SFLEND
     A  47                                  ROLLUP(27)
      *
      *
     A            SFLRR2         4S 0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'PB9502C1'
     A                                  1 26'RETAIL ACCRUALS TAKE-ON'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A  65                                  DSPATR(RI)
     A  65                                  DSPATR(PC)
     A            SLINE         79A  O  4  2DSPATR(UL)
     A                                  5 11'Select'
     A                                  5 30'Account Number'
     A**Start of change CGL029
     A**********  S2RCRG        18A  H                                                        CGL029
     A            S2RCRG        24A  H
     A**End of change CGL029
      *
      ***  Bottom format
      *
     A          R PB9502F3                  OVERLAY
     A                                 23 02'F3=Exit  F12=Previous Screen'
     A  70                                +2'F21=Select all'
      *
      ***  Message Subfile Record Format.
      *
     A          R PB9502S2                  TEXT('Message Subfile Format')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            ##SFKY                    SFLMSGKEY
     A            ##PGMQ                    SFLPGMQ
      *
      ***  Message Subfile Control Format.
      *
     A          R PB9502C2
     A                                      SFLCTL(PB9502S2)
     A                                      OVERLAY
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  26                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A            ##PGMQ                    SFLPGMQ
