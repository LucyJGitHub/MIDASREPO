      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas PB File for Reconciliation')                     *
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PF/PBRECOPD - Midas PB file for Reconciliation               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 PB4501             Date 04Aug99               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001  *CREATE    Date 16Jul99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  PB4501 - F&O INTERFACE WITH TOF - PHASE 1                    *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CPB001 - 'Private Banking' Module                            *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R PBRECOD0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            TRMODI         2          TEXT('Module Id.')
     A                                      COLHDG('Module Id.')
      *
     A            TRTOCA         2S 0       TEXT('TOF category')
     A                                      COLHDG('TOF category')
      *
     A            TRPORN         2S 0       TEXT('Portfolio number')
     A                                      COLHDG('Portf.' 'Number')
      *
     A            TRBRCA         3          TEXT('Branch Code - Alpha')
     A                                      COLHDG('Branch' 'Code')
      *
     A            TRCNUM         6          TEXT('Customer number')
     A                                      COLHDG('Customer' 'Number')
      *
     A            TRCCY          3          TEXT('Currency code')
     A                                      COLHDG('Currency' 'Code')
      *
     A**Start of change CGL029
     A**********  TRACOD         4          TEXT('Account code')                              CGL029
     A**********                            COLHDG('Account' 'Code')                          CGL029
      *
     A            QQACOD         4A
     A**End of change CGL029
     A            TRACSQ         2          TEXT('Account sequence number')
     A                                      COLHDG('A/c' 'Sequence' 'Number')
      *
      **Start of change CLE148
     A**********  TRDLNO         6S 0       TEXT('Deal number')                               CLE148
     A            TRDLNO         6A         TEXT('Deal number')
      **End of change CLE148
     A                                      COLHDG('Deal' 'Number')
      *
     A            TRDTYP         4          TEXT('Deal type/subtype')
     A                                      COLHDG('Deal' 'type/subtype')
      *
     A            TRDDAT         5  0       TEXT('Deal Date')
     A                                      COLHDG('Deal Date')
      *
     A            TRVDAT         5  0       TEXT('Value Date')
     A                                      COLHDG('Value Date')
      *
     A            TRMDAT         5  0       TEXT('Maturity Date')
     A                                      COLHDG('Maturity' 'Date')
      *
     A            TRSESN        10          TEXT('Security Shortname')
     A                                      COLHDG('Security' 'Shortname')
      *
     A            TRAMTD        18S 4       TEXT('Amount - Trade Date')
     A                                      COLHDG('Amount' '- Trade Date')
      *
     A            TRAMVD        18S 4       TEXT('Amount - Value Date')
     A                                      COLHDG('Amount' '- Value Date')
      *
     A            TRRADR        18S 8       TEXT('Debit Rate')
     A                                      COLHDG('Debit' 'Rate')
      *
     A            TRRACR        18S 8       TEXT('Credit Date')
     A                                      COLHDG('Credit' 'Rate')
      *
     A            TROCCY         3          TEXT('Other currency')
     A                                      COLHDG('Other currency')
      *
     A            TREXCH        14S 8       TEXT('Exchange Rate')
     A                                      COLHDG('Exchange' 'Rate')
      *
     A            TRRUND         5  0       TEXT('Run Date')
     A                                      COLHDG('Run' 'Date')
      *
     A            TRAMEX        18S 4       TEXT('Amount - Ex Date')
     A                                      COLHDG('Amount' '- Ex Date')
      *
     A**Start of change CGL029
     A            TRACOD        10A         TEXT('Account code')
     A**End of change CGL029
      *                                              Begin /PB4501/
     A            TRISTT         5A         TEXT('Instrument Type')
     A                                      COLHDG('Instrument' 'Type')
      *
     A            TRYRNO         2  0       TEXT('Year Number')
     A                                      COLHDG('Year' 'Number')
      *
     A            TRMTHN         2  0       TEXT('Month Number')
     A                                      COLHDG('Month' 'Number')
      *
     A            TRPCAL         1A         TEXT('Put Call')
     A                                      COLHDG('Put' 'Call')
      *
     A            TRSTRP        15  8       TEXT('Strike Price')
     A                                      COLHDG('Strike' 'Price')
      *                                              End   /PB4501/
