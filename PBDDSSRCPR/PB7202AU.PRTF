      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas PB TOF RE account - take on')
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PRTF/PB7202AU - TOF GL Account - Take on - Audit             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CPB004 *CREATE     Date 11Jul02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPB004 - Set replication indicator for all Retail accounts.  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE PB7202AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'Take-On Retail Account to be -
     A                                      transfer to TOF'
     A                                      SPACEA(1)
      *
     A                                    46'------------------------------
     A                                      ---------------'
     A                                      SPACEA(3)
      *
     A                                     7'Acc.'
     A                                    20'Customer'
     A                                    40'Curr'
     A                                    55'A/C'
     a                                    65'Acc'
     A                                    73'Ledger'
     A                                    93'Cleared'
     A                                      SPACEA(1)
     A                                     7'Code'
     A                                    20'Number'
     A                                    40'Code'
     A                                    55'Seq'
     a                                    65'Typ'
     A                                    73'Balance'
     A                                    93'Balance'
     A                                      SPACEA(2)
      *
      *-------------------------------------*
      ***  Report no details record format  *
      *-------------------------------------*
      *
     A          R DETAP1
     A            RACOD          4         7
     A            RCNUM          6        20
     A            RCCY           3        40
     A            RACSQ          2        56
     A            RATYP          1        65
     A            RLDBL         15        70
     A            RCLBL         15        93
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Record Counter *
      *-------------------*
      *
     A          R COUNTER                   SPACEB(3)
     A  55                                15'Number of records transferred : '
     A N55                                50'*** NO DATA TO TRANSFER ***'
     A            NREC           5  0     48EDTWRD('    0')
