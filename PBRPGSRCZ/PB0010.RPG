     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PB Duplicate Accounts Report')
      *****************************************************************
      *                                                               *
      *  Private Banking Module                                       *
      *                                                               *
      *  PB0010 - Midas TOF Duplicate Account Report                  *
      *                                                               *
      *  Function:  This program print the list of duplicate accounts *
      *             LF/ACCNT .                                        *
      *                                                               *
      *  Called By: PBC0010 - Duplicate accounts report               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CPB002  *CREATE    Date 01Jun00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
     FACCNT   IF  E           K        DISK         KINFSR *PSSR
     F            ACCNTAAF                          KIGNORE
     F            ACCNTACF                          KIGNORE
      ** Accounts Details
      *
     FPB0010P1O   E                    PRINTER      KINFDS SPOOL1     UC
      ** Duplicate account report
      *
     FPB0010AUO   E                    PRINTER      KINFDS SPOOLU     UC
      ** Audit report
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *                                                               *
      *   80  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
     E                    CPY@    1   1 80
      ** Array for Copyright
      *
      *****************************************************************
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
     IPGMDS     ESDSY2PGDSP
      *
     ISPOOL1      DS
      ** File Information Data Structure for PB0010P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ** File Information Data Structure for PB0012AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     ISDBANK    E DSSDBANKPD
      ** External data structures for Bank Details
      *
     ISDCUST    E DSSDCUSTPD
      ** External data structures for Customer Details
      *
     IDSLDY     E DSDSLDY
      ** First DS for access programs, very long data structure
      *
     IDSSDY     E DSDSSDY
      ** First DS for access programs, short data structure
      *
     IDSFDY     E DSDSFDY
      ** First DS for access programs, long data structure
      *
      /EJECT
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Read first record from File.
      *
     C                     READ ACCNT                    80
      *
      ***  If End of Records, (*IN80), Perform: Audit Reporting (No
      ***  Records).
      *
     C           *IN80     IFEQ *ON
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If record found
      ***  Do while  End of File.
      *
     C           *IN80     DOWEQ*OFF
      *
      ***  If the customer is retail
     C           ATYP      IFEQ 'R'
      *
     C           PBNOPB    IFEQ 'Y'
     C                     MOVELCNUM      @KEY1     P
     C                     CALL 'AOCUSTR1'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           @KEY1  10
     C                     PARM           @KYST   7
     C           SDCUST    PARM SDCUST    DSLDY
     C                     ENDIF
      *
     C           @RTCD     IFEQ *BLANKS
     C           BBPRBA    ANDEQ'Y'
     C           PBNOPB    ANDEQ'Y'
      *
     C           PBNOPB    OREQ 'N'
      *
      ***  Position the reading in the file
      *
     C**********           Z-ADDCNUM      CNUMP                                               CSD027
     C                     MOVE CNUM      CNUMP                                               CSD027
     C                     MOVE CCY       CCYP
     C                     Z-ADDACOD      ACODP
     C                     Z-ADDACSQ      ACSQP
     C                     MOVE BRCA      BRCAP
      *
     C                     EXSR REPORT
     C                     ENDIF
     C                     ENDIF
      *
     C                     READ ACCNT                    80
     C                     ENDDO
      *
      ***  Write End of Report.
      *
     C           RCOUNT    IFNE 0
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Total Format.
      *
     C                     WRITETRAILP1
     C                     ENDIF
      *
      ***  End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
     C           REPORT    BEGSR                           *** REPORT ***
      *
      *** Open printer P1 the first time
      *
     C           WWP1      IFEQ 'N'
     C                     EXSR RCFP1
     C                     MOVE 'Y'       WWP1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      *** Clear detail of printer file before setup
      *
     C                     CLEARDETAIL
      *
      *** Setup detail report
      *
     C                     MOVE CNUM      R1CNUM
     C                     MOVE CCY       R1CCY
     C                     MOVE ACOD      R1ACOD
     C                     MOVE ACSQ      R1ACSQ
     C                     MOVE BRCA      R1BRCA
     C                     MOVE PTFR      R1PTFR
      *
      *** First account find
      *
     C                     Z-ADD1         CPT     10
      *
     C           KEYDUB    READEACCNT                    60
     C           *IN60     DOWEQ*OFF
      *
      ***  If there more than 1 record duplicate
      ***  No more print the first account
      *
     C           CPT       IFGT 1
     C                     MOVE *BLANK    R1CNUM
     C                     MOVE *BLANK    R1CCY
     C                     MOVE *BLANK    R1ACOD
     C                     MOVE *BLANK    R1ACSQ
     C                     MOVE *BLANK    R1BRCA
     C                     MOVE *BLANK    R1PTFR
     C                     ENDIF
      *
     C                     MOVE CNUM      R2CNUM    P
     C                     MOVE CCY       R2CCY     P
     C                     MOVE ACOD      R2ACOD    P
     C                     MOVE ACSQ      R2ACSQ    P
     C                     MOVE BRCA      R2BRCA    P
     C                     MOVE PTFR      R2PTFR    P
      *
      ***  Write out Detail Record.
      *
     C                     WRITEDETAIL
     C                     ADD  1         CPT     10
     C                     ADD  1         RCOUNT
     C           KEYDUB    READEACCNT                    60
     C                     END
      *
      ***  Read next record
      *
     C           KEYPOS    SETLLACCNT
     C                     READ ACCNT                    80
     C                     MOVE PTFR      PTFRSV  4
      *
      *
      *** First account find
      *
     C                     Z-ADD1         CPT     10
      *
     C           KEYDUP    READEACCNT                    60
     C           *IN60     DOWEQ*OFF
     C           PTFR      ANDEQPTFRSV
      *
      ***  If there more than 1 record duplicate
      ***  No more print the first account
      *
     C                     MOVE PTFR      PTFRSV
      *
     C           CPT       IFGT 1
     C                     MOVE *BLANK    R1CNUM
     C                     MOVE *BLANK    R1CCY
     C                     MOVE *BLANK    R1ACOD
     C                     MOVE *BLANK    R1ACSQ
     C                     MOVE *BLANK    R1BRCA
     C                     MOVE *BLANK    R1PTFR
     C                     ENDIF
      *
     C                     MOVE CNUM      R2CNUM    P
     C                     MOVE CCY       R2CCY     P
     C                     MOVE ACOD      R2ACOD    P
     C                     MOVE ACSQ      R2ACSQ    P
     C                     MOVE BRCA      R2BRCA    P
     C                     MOVE PTFR      R2PTFR    P
      *
      ***  Write out Detail Record.
      *
     C                     WRITEDETAIL
     C                     ADD  1         CPT     10
     C                     ADD  1         RCOUNT
     C           KEYDUP    READEACCNT                    60
     C                     END
      *
      ***  Read next record
      *
     C           KEYPOS    SETLLACCNT
     C                     READ ACCNT                    80
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     OPEN PB0010P1
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           RSEQ    5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM *BLANKS   ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVE 'PB0010P1'DBFILE           * DB ERROR 01 *
     C           'ZSFILE R'CAT  'OUTINE'  DBKEY     P      ***************
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     OPEN PB0010AU
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           RSEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANKS   ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            ***************
     C                     MOVE 'PB0010AU'DBFILE           * DB ERROR 02 *
     C           'ZSFILE R'CAT  'OUTINE'  DBKEY     P      ***************
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, PROCES, *PSSR                    *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ***  RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ***  Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *  *INZSR - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  AOBANKR0 Access Program                                      *
      *                                                               *
      *****************************************************************
     C           *INZSR    BEGSR                           ***  INIT  ***
      *
      ** Define parameters list
      *
     C           *ENTRY    PLIST
     C                     PARM           RSEQ    5
     C                     PARM           PBNOPB  1
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      * Define key lists for Accounts Master File by Branch
      *
     C           KEYDUB    KLIST
     C                     KFLD           CNUM
     C                     KFLD           CCY
     C                     KFLD           ACOD
     C                     KFLD           ACSQ
     C                     KFLD           BRCA
      *
      * Define key lists for Accounts Master File by Portfolio
      *
     C           KEYDUP    KLIST
     C                     KFLD           CNUM
     C                     KFLD           CCY
     C                     KFLD           ACOD
     C                     KFLD           ACSQ
      *
      * Define key Position for Accounts Master File
      *
     C           KEYPOS    KLIST
     C**********           KFLD           CNUMP   60                                          CSD027
     C                     KFLD           CNUMP   6                                           CSD027
     C                     KFLD           CCYP    3
     C**********           KFLD           ACODP   40                                          CGL029
     C                     KFLD           ACODP  100                                          CGL029
     C                     KFLD           ACSQP   20
     C                     KFLD           BRCAP   3
      *
      ** Initialise LDA field.
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Call Access Program for Bank Details - Title, Run Date and
      ** Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE            ***************
     C                     MOVE 'SDBANKPD'DBFILE           * DB ERROR 03 *
     C                     MOVEL'*FIRST'  DBKEY     P      ***************
     C                     MOVEL##PGM     DBPGM
     C                     OUT  LDA
     C                     SETON                     U7U8
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Report Work fields.
      *
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
     C                     Z-ADD0         RCOUNT  50
     C                     MOVE 'N'       WWP1    1
     C                     MOVE 'N'       WWTITL  1
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           *** PSSR **
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C           DBASE     IFEQ *ZERO
     C           *LOCK     IN   LDA
     C                     MOVEL##PGM     DBPGM
     C                     Z-ADD999       DBASE
     C                     MOVE ##ERFL    DBFILE
     C                     OUT  LDA
     C                     SETON                     U7U8
     C                     ENDIF
      *
     C                     EXSR AUDIT
     C                     DUMP
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2001
