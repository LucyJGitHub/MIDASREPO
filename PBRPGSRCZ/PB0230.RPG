     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PB Portfolio Instrument Types list')
      *****************************************************************
      *                                                               *
      *  Midas - Private Banking Module                               *
      *                                                               *
      *  PB0230 - Midas PB Portfolio Instrument Types list            *
      *                                                               *
      *  Function:  This program print all records contained in       *
      *             LF/SDPLINL0 .                                     *
      *                                                               *
      *  Called By: PBC0230 - Instrument Types List                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001   *CREATE   Date 30Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CPB001 -  'Private Banking' Module                           *
      *                                                               *
      *****************************************************************
     FSDPLINL0IF  E           K        DISK         KINFSR *PSSR
      ** Instrument Type Details
      *
     FPB0230P1O   E                    PRINTER      KINFDS SPOOL1     UC
      ** Instrument Types List
      *
     FPB0230AUO   E                    PRINTER      KINFDS SPOOLU     UC
      ** Instrument Types Audit List
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *                                                               *
      *                                                               *
      *   80  - End of File                                           *
      *                                                               *
      *   98  - Date Format: DDMMYY (off); MMDDYY (on)                *
      * 90-99 - Used by Standard Subroutines                          *
      *                                                               *
      * U7+U8 - Database Error                                        *
      *                                                               *
      *****************************************************************
     E                    CPY@    1   1 80
      ** Array for Copyright
      *
      *****************************************************************
     ILDA       E DSLDA                         256
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     IRUNDAT    E DSRUNDAT
     IPGMDS     ESDSY2PGDSP
      *
     ISPOOL1      DS
      ** File Information Data Structure for PB0230P1.
     I                                       83  92 SFILE1
     I                                    B 123 1240SFNUM1
     I                                    B 188 1890OFLLN1
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
      ** File Information Data Structure for PB0230AU.
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     ISDBANK    E DSSDBANKPD
      ** External data structures for Bank Details
      *
     ISDCUST    E DSSDCUSTPD
      ** External data structures for Customer Details
      *
     IDSSDY     E DSDSSDY
      ** First DS for access programs, short data structure
      *
     IDSFDY     E DSDSFDY
      ** First DS for access programs, long data structure
      *
      /EJECT
      *================================================================
      *  P R O G R A M   S T A R T                                    *
      *================================================================
      *
      ***  Read first record from File.
      *
     C                     READ SDPLINL0                 80
      *
      ***  If End of Records, (*IN80), Perform: Audit Reporting (No
      ***  Records).
      *
     C           *IN80     IFEQ *ON
     C                     EXSR AUDIT
     C                     ENDIF
      *
      ***  If record found
      ***  Do while  End of File.
      *
     C           *IN80     DOWEQ*OFF
     C                     EXSR REPORT
     C                     READ SDPLINL0                 80
     C                     ENDDO
      *
     C           RCOUNT    IFNE 0
      *
      ***  If the number of record is uneven the last record must be writed.
      *
     C           CPT       IFGE 2
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
     C                     WRITEDETAIL
     C                     ENDIF
      *
      ***  Write End of Report.
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD4         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Write out Total Format.
      *
     C                     WRITETRAILP1
     C                     ENDIF
      *
      ***  End Program and Return.
      *
     C                     SETON                     LR
     C                     RETRN
      *================================================================
      *  P R O G R A M   E N D                                        *
      *================================================================
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  REPORT - Process Report Lines.                               *
      *                                                               *
      *  Called By: PROCES                                            *
      *  Calls:                                                       *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
     C           REPORT    BEGSR                           *** REPORT ***
      *
      *** Open printer P1 the first time
      *
     C           WWP1      IFEQ 'N'
     C                     EXSR RCFP1
     C                     MOVE 'Y'       WWP1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      ***  Check that sufficient lines remain for the Format. If not,
      ***  write out the Main Headings on a new page.
      *
     C                     Z-ADD1         RQDLN1
     C           OFLLN1    SUB  PRTLN1    DIFLN1
     C           DIFLN1    IFLE RQDLN1
     C                     WRITEHEADP1
     C                     ENDIF
      *
      *** Setup detail report
      *
     C           CPT       IFEQ 1
     C                     MOVELPDINNR    R1INNR
     C                     MOVELPDISTN    R1ISTN    P
      *
     C                     CALL 'ZDATE2'
     C                     PARM PDLCD     ZDAYNO  50
     C                     PARM           ZDATFM  1
     C                     PARM           ZDATE   60
     C                     PARM           ZADATE  7
      *
     C                     MOVE ZADATE    R1LCD            DDMMMYY
     C                     MOVE *BLANKS   R1LCTY           Last change type
     C                     MOVEL'INSERT'  R1LCTY
     C           PDCHTP    IFEQ 'A'
     C                     MOVEL'AMEND '  R1LCTY
     C                     ENDIF
     C           PDCHTP    IFEQ 'D'
     C                     MOVEL'DELETE'  R1LCTY
     C                     ENDIF
      *
     C                     MOVEL*BLANKS   R2INNR
     C                     MOVEL*BLANKS   R2ISTN
     C                     MOVEL*BLANKS   R2LCD
     C                     MOVEL*BLANKS   R2LCTY
      *
     C                     ELSE
      *
     C                     MOVELPDINNR    R2INNR
     C                     MOVELPDISTN    R2ISTN    P
      *
     C                     CALL 'ZDATE2'
     C                     PARM PDLCD     ZDAYNO  50
     C                     PARM           ZDATFM  1
     C                     PARM           ZDATE   60
     C                     PARM           ZADATE  7
      *
     C                     MOVE ZADATE    R2LCD            DDMMMYY
     C                     MOVE *BLANKS   R2LCTY           Last change type
     C                     MOVEL'INSERT'  R2LCTY
     C           PDCHTP    IFEQ 'A'
     C                     MOVEL'AMEND '  R2LCTY
     C                     ENDIF
     C           PDCHTP    IFEQ 'D'
     C                     MOVEL'DELETE'  R2LCTY
     C                     ENDIF
     C                     ENDIF
      *
     C                     ADD  1         CPT
     C                     ADD  1         RCOUNT
      *
      ***  Write out a line complet of Detail Record.
      *
     C           CPT       IFGT 2
     C                     WRITEDETAIL
     C                     Z-ADD1         CPT
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *
      *                                                               *
      *****************************************************************
     C           RCFP1     BEGSR                            *** RCFP1  ***
      *
      ***  Ensure Report Spool File recorded by RCF.
      *
     C                     OPEN PB0230P1
     C                     Z-ADDSFNUM1    ZSNUM   60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           RSEQ    5
     C                     PARM           SENTY   3
     C                     PARM           SFILE1
     C                     PARM           ZSNUM
     C                     PARM *BLANKS   ZSERR   1
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVE 'PB0230P1'DBFILE           * DB ERROR 01 *
     C           'ZSFILE R'CAT  'OUTINE'  DBKEY     P      ***************
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *
      *                                                               *
      *****************************************************************
     C           RCFAU     BEGSR                            *** RCFAU  ***
      *
      ***  Ensure Audit Spool File recorded by RCF.
      *
     C                     OPEN PB0230AU
     C                     Z-ADDSFNUMU    ZSNUMU  60
      *
     C                     CALL 'ZSFILE'
     C                     PARM           RSEQ
     C                     PARM *BLANKS   SENTY
     C                     PARM           SFILEU
     C                     PARM           ZSNUMU
     C                     PARM *BLANKS   ZSERR
      *
      ***  If Error occurs during ZSFILE processing, then return to the
      ***  Calling Program.
      *
     C           ZSERR     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            ***************
     C                     MOVE 'PB0230AU'DBFILE           * DB ERROR 02 *
     C           'ZSFILE R'CAT  'OUTINE'  DBKEY     P      ***************
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *
      *                                                               *
      *  Called By: Main Processing, PROCES, *PSSR                    *
      *  Calls: None.                                                 *
      *                                                               *
      *****************************************************************
     C           AUDIT     BEGSR                           *** AUDIT  ***
      *
      ***  RCF Processing for Audit File.
      *
     C                     EXSR RCFAU
      *
      ***  Output Report Header and File Controls.
      *
     C                     WRITEHEADAU
      *
      ***  If there is a DB Error, Output the DB Error Information.
      *
     C           *INU7     IFEQ *ON
     C                     WRITEDBERROR
     C                     ELSE
      *
      ***  Or, if no records read, Output "No Details" message.
      *
     C           RCOUNT    IFEQ 0
     C                     WRITENODTLS
     C                     ENDIF
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *  *INZSR - Subroutine to do Program Initialisation.            *
      *                                                               *
      *  Called By: Main Processing                                   *
      *  Calls:                                                       *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *  AOBANKR0 Access Program                                      *
      *                                                               *
      *****************************************************************
     C           *INZSR    BEGSR                           ***  INIT  ***
      *
      ** Define parameters list
      *
     C           *ENTRY    PLIST
     C                     PARM           RSEQ    5
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ** Initialise LDA field.
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Call Access Program for Bank Details - Title, Run Date and
      ** Run Day Number.
      *
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   WRTCD   7
     C                     PARM '*FIRST ' WOPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
      ** Handle Database Error.
      *
     C           WRTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD3         DBASE            ***************
     C                     MOVE 'SDBANKPD'DBFILE           * DB ERROR 03 *
     C                     MOVEL'*FIRST'  DBKEY     P      ***************
     C                     MOVEL##PGM     DBPGM
     C                     OUT  LDA
     C                     SETON                     U7U8
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Check System Date Format, DDMMYY (*IN98 off)
      **                           MMDDYY (*IN98 on).
      *
     C                     MOVE BJDFIN    ZDATFM
      *
      ** Report Work fields.
      *
     C                     Z-ADD1         CPT     10
     C                     Z-ADD0         RQDLN1  30
     C                     Z-ADD0         DIFLN1  30
     C                     Z-ADD0         RCOUNT  50
     C                     MOVE 'N'       WWP1    1
     C                     MOVE 'N'       WWTITL  1
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR  - Error control subroutine                            *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           *** PSSR **
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
      *
     C           DBASE     IFEQ *ZERO
     C           *LOCK     IN   LDA
     C                     MOVEL##PGM     DBPGM
     C                     Z-ADD999       DBASE
     C                     MOVE ##ERFL    DBFILE
     C                     OUT  LDA
     C                     SETON                     U7U8
     C                     ENDIF
      *
     C                     EXSR AUDIT
     C                     DUMP
     C                     ENDIF
      *
      ***  If *PSSR already run, then just end the program here.
      *
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
**  CPY@
(c) Finastra International Limited 2001
