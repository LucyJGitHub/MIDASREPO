     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PB Forward FX Revaluation          - take on')
      *****************************************************************
      *                                                               *
      *  Private Banking Module                                       *
      *                                                               *
      *  PB6600 - Forward FX Revaluation - Take On Program            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Prev Amend No. CSD006             Date 11Oct00               *
      *                 CPB002  *CREATE    Date 01Jun99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD006 - Recompiled over changed FWDRFTE.                    *
      *  CPB002 - Meridian DBA Middleware Replication Customization.  *
      *                                                               *
      *****************************************************************
     FFWDRMIR UF  E           K        DISK         KINFSR *PSSR A
      *** TOF Forward Rates Details
      *
     FFWDRTFE UF  E           K        DISK         KINFSR *PSSR
      *****************************************************************
     E                    NDAY       25  5 0
      **  Array contains Number of days forward
      *
     E                    NRAT       25 13 8
      **  Array contains Forward rates
      *
     E                    NSIG       25  1
      **  Array contains Sign
      *
     E                    NPNT       25 13 8
      **  Array contains Forward points
      *
     E                    CPY@    1   1 80
      **  Copyright Array
      *
      *****************************************************************
      *
     IPSDS       SDS
      **  Program Status DS
      *
     I                                     *PROGRAM PGM
     I                                      244 253 WSID
     I                                      254 263 USER
      *
     ILDA       E DSLDA                         256
      **  Local Data Area for DB Errors
      *
      **                                    134 141 DBFILE
      **                                    142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     INEWREC    E DSFWDRTFE
      **  Record to be inserted or new values
     I                                    P   5  79 NDAY
     I                                    P  80 254 NRAT
     I                                      412 436 NSIG
     I                                    P 437 611 NPNT
      *
     IDSFDY     E DSDSFDY
      **  First DS for access programs, short DS
      *
     IDSSDY     E DSDSSDY
      **  Second DS for access programs, Long DS
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ** +--- Start of Main processing -----------------------------------+
      ** ¦                                                                ¦
      ** ¦ Initial processing is performed automatically: the *INZSR is   ¦
      ** ¦ executed at program activation.                                ¦
      ** ¦                                                                ¦
      ** +----------------------------------------------------------------+
      *
      ***  Read all record to write a miror in FWDRMIR
      *
     C                     READ FWDRTFE                  88
     C           *IN88     DOWEQ*OFF
      *
     C           RECI      IFEQ '*'
     C                     DELETFWDRTFEF
     C                     ELSE
      *
      ***  Do While not 25 process or Number of days Forward not equal to zero
      *
     C                     Z-ADD1         X       20
     C           X         DOWLE25
     C           NDAY,X    ANDNE0
      *
      ***  Check if the Currency already exist in the file
      *
     C                     MOVE 'N'       WWFND   1
     C                     MOVE CCY       WWCCY   3
     C           WWCCY     SETLLFWDRMIR
     C           WWCCY     READEFWDRMIR                  87
      *
     C           *IN87     DOWEQ*OFF
     C           WWFND     ANDEQ'N'
      *
      ***  Found if same currency and same day forward
      *
     C           NDAY,X    IFEQ FWDFW
     C                     MOVE 'Y'       WWFND
     C                     ENDIF
     C           WWFND     IFNE 'Y'
     C           WWCCY     READEFWDRMIR                  87
     C                     ENDIF
     C                     ENDDO
      *
      ***  If Feature S01432 is active (It means forward points are present)
      *
     C           S01432    IFEQ 'Y'
     C           NSIG,X    IFEQ '+'
     C                     Z-ADDNPNT,X    FWPNF            Forward Points
     C                     ELSE
     C                     Z-SUBNPNT,X    FWPNF            Forward Points
     C                     ENDIF
     C                     ENDIF
      *
     C                     MOVE CCY       FWCCY            Currency Code
     C                     Z-ADDNDAY,X    FWDFW            Days Forward
     C                     Z-ADDNRAT,X    FWRFW            Forward Rates
     C                     Z-ADDLCD       FWLCD            Last Change Date
     C                     MOVE CHTP      FWCHTP           Last Change Type
     C           RECI      IFEQ '*'
     C                     MOVE '*'       FWRECI           Record Id
     C                     ELSE
     C                     MOVE 'D'       FWRECI           Record Id
     C                     ENDIF
      *
      ***  Write a record in FWDRMIR
      *
     C           WWFND     IFEQ 'N'
     C                     WRITEFWDRMIR0
     C                     ELSE
     C                     UPDATFWDRMIR0
     C                     ENDIF
     C                     ADD  1         X
     C                     ENDDO
     C                     ENDIF
      *
      ***  Read Next Forward Rates
      *
     C                     READ FWDRTFE                  88
     C                     ENDDO
      *
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *  *INZSR - Program Initialisation subroutine                   *
      *****************************************************************
      *
     C           *INZSR    BEGSR
      *
      ***  Set up copyright parameter.
      *
     C                     MOVEACPY@      MKI@   80
      *
      ***  Define the LDA.
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVELPGM       DBPGM
     C                     MOVE *BLANKS   DBKEY
     C                     MOVE *BLANKS   DBFILE
     C                     Z-ADD0         DBASE
     C                     OUT  LDA
      *
      ***  Access SAR details file to determine if FX Interface
      ***  is switched on
      *
     C                     MOVEL'N'       S01432  1
     C                     CALL 'AOSARDR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*VERIFY' @OPTN   7
     C                     PARM 'S01432'  @SARD   6
      *
     C           @RTCD     IFEQ *BLANKS
     C                     MOVEL'Y'       S01432
     C                     ENDIF
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
      ***  Handle Database Error.
      *
     C           *LOCK     IN   LDA
     C                     MOVELPGM       DBPGM
     C                     OUT  LDA
      *
      ***  Check to see that *PSSR has not already been called.
      *
     C                     DUMP
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVE *ON       *INLR
      *
     C                     RETRN
      *
     C                     ENDSR
      *
      *****************************************************************
      /EJECT
      *****************************************************************
**   CPY@
(c) Finastra International Limited 2001
