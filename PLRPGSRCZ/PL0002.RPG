     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PL Initialise new fields on PF/DEALSDG')
      *****************************************************************
      *                                                               *
      *  Midas Plato Interface Enhancement.                           *
      *                                                               *
      *  PL0002  -  Initialise New Field on PF/DEALSDG;               *
      *                                     PF/DLGHISPD;              *
      *                                     PF/DLPCSCPD.              *
      *                                                               *
      *  Function:  This program should only be run as part of the    *
      *             installation procedure for the Midas-Plato        *
      *             Interface - Phase 2.                              *
      *                                                               *
      *  Input   :  Action Code (7)                                   *
      *                                                               *
      *  Output  :  Return Code (7)                                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. CDL038             Date 10May05               *
      *                 BUG7411            Date 01Jun05               *
      *                 CDL028             Date 07Feb05               *
      *                 CAS008             Date 16Jun04               *
      *                 217684             Date 12May03               *
      *                 CAS006             Date 21Jan03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CAP056             Date 13Mar02               *
      *                 CIR008             Date 13Mar02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CIR003             Date 12Sep00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CIR005             Date 21Jan00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CPL002  *CREATE    Date 08MAR99               *
      *                                                               *
      * ------------------------------------------------------------  *
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  BUG7411- CDL028 fields missing from DLGHISPD (Recompile)     *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  CAS008 - IAS 39 Enhancements (Recompile)                     *
      *  217684 - Authorisation User Malfunction (Recompile)          *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CAP056 - Automatic Authorisation of Interface deals          *
      *           (Recompile)                                         *
      *  CIR008 - FRA/IRS Deals Authorisation (Recompile)             *
      *  CIR003 - Principal Exchange on Cross Currencies IRS          *
      *  CIR005 - FRA/IRS Business Day Conventions (Recompile)        *
      *  CPL002 - Midas-Plato Interface - Phase 2.                    *
      *           The Original Principal Amount fields (UOPAM and     *
      *           TOPAM) may not have been set up. They should be     *
      *           initialised here.                                   *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F   I N D I C A T O R S                  *
      *  -------------------------------------------                  *
      *                                                               *
      *   01  - Record processed from PF/DEALSDG.                     *
      *   02  - Record processed from PF/DLGHISPD.                    *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *  -------------------------------                              *
      *                                                               *
      *   #P01   - Initialisation Subroutine                          *
      *   #P02   - Update PF/DEALSDG and PF/DLGHISPD.                 *
      *   #P03   - Termination Subroutine                             *
      *                                                               *
      *                                                               *
      *   *PSSR  - Database Error Processing Subroutine.              *
      *                                                               *
      *****************************************************************
      *
     FDEALSDG UP  E                    DISK
     F                                              KCOMIT
     F                                              KINFSR *PSSR
      * UPD : Midas FRA/IRS Deals.
      *
     FDLGHISPDUS  E                    DISK
     F                                              KCOMIT
     F                                              KINFSR *PSSR
      *
     FDLPCSCPDUF  E           K        DISK
     F                                              KCOMIT
     F                                              KINFSR *PSSR
      * UPD : Midas FRA/IRS Principal Changes.
      *
      *****************************************************************
      *
     E                    STK        99  8               Subr stack
     E                    CPY@    1   1 80
      *
      *****************************************************************
     IDEALSDGF    01
     IDLGHISD0    02
     IDBERR       DS                            256
      **  Local data area for database error details
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
      *****************************************************************
     C/EJECT
      *****************************************************************
      **  M A I N   P R O C E S S I N G
      *****************************************************************
      *
      **  Perform Initialisation Processing.
      *
      *
     C                     Z-ADD1         @Q      30       increment stack
     C                     MOVEL'MAINLINE'STK,@Q           load subr name
      *
     C           @WKF01    IFNE 'Y'
     C                     EXSR #P01
     C                     MOVEL'Y'       @WKF01  1
     C                     ENDIF
      *
      **  Update Processing.
      *
     C           P@ACTN    IFEQ '*UPDATE'
     C                     EXSR #P02
     C                     ENDIF
      *
      **  Perform Termination Processing.
      *
     C                     MOVE *BLANKS   STK,@Q           remove subr name
     C                     SUB  1         @Q               decrement stack
      *
      *
     CLR                   EXSR #P03
     C/EJECT
      *
      *****************************************************************
      *
      **  #P01 - Initialisation Subroutine.
      *
      *****************************************************************
      *
     C           #P01      BEGSR                           ** #P01 SR   **
      *
     C                     ADD  1         @Q               increment stack
     C                     MOVEL'#P01    'STK,@Q           load subr name
      *
      **  Entry List.
      *
     C           *ENTRY    PLIST
     C                     PARM           P@ACTN  7        I: Action Code
      *
     C                     PARM           P@RTCD  7        O: Return Code
      *
      **  Initialise Database Error Details.
      *
     C                     MOVEL'PL0002'  DBPGM
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     Z-ADD*ZEROS    DBASE
      *
      **  Key list for PF/DLPCSCPD.
      *
     C           #KLST1    KLIST
     C                     KFLD           DLNO
     C                     KFLD           WKOTIN  1
      *
      **  Set up Copyright parameter. Initialise output fields.
      *
     C                     MOVEACPY@      MKI@   80
      *
     C                     MOVE *BLANKS   STK,@Q           remove subr name
     C                     SUB  1         @Q               decrement stack
      *
     C                     ENDSR
      *
      *****************************************************************
      *
      *   #P02   - Update Processing subroutine
      *
      *****************************************************************
      *
     C           #P02      BEGSR                           ** #P02 SR   **
      *
     C                     ADD  1         @Q               increment stack
     C                     MOVEL'#P02    'STK,@Q           load subr name
      *
      ** Processing for IRS deals only.
      *
     C           DTYP      IFEQ 'RS'
     C           DTYP      OREQ 'RX'
      *
      ** Check new fields for Last Rate Change Date.
      *
     C           PLULRC    IFEQ 0
     C                     Z-ADDULPCD     PLULRC
     C                     ENDIF
      *
     C           PLTLRC    IFEQ 0
     C                     Z-ADDTLPCD     PLTLRC
     C                     ENDIF
      *
      ** Update the records on PF/DLPCSCPD.
      *
     C                     EXSR #P21
      *
      ** Update record on either PF/DEALSDG or PF/DLGHISPD.
      *
     C           *IN01     IFEQ '1'
     C                     UPDATDEALSDGF
     C                     ENDIF
      *
     C           *IN02     IFEQ '1'
     C                     UPDATDLGHISD0
     C                     ENDIF
      **********
      ***Update*the records on PF/DLPCSCPD.
      **********
     C**********           EXSR #P21
      *
     C                     ENDIF
      *
     C                     MOVE *BLANKS   STK,@Q           remove subr name
     C                     SUB  1         @Q               decrement stack
      *
     C                     ENDSR
      *
      *****************************************************************
      *
      *   #P21   - Update PF/DLPCSCPD.
      *
      *****************************************************************
      *
     C           #P21      BEGSR                           ** #P02 SR   **
      *
     C                     ADD  1         @Q               increment stack
     C                     MOVEL'#P21    'STK,@Q           load subr name
      *
      ** Set-up our/their indicator.
      *
     C           DTYP      IFEQ 'RS'
     C                     MOVEL' '       WKOTIN
     C                     ELSE
     C                     MOVEL'U'       WKOTIN
     C                     ENDIF
      **********
      ***Set-up*"previous" principal amount.
      **********
     C**********           Z-ADDUOPAM     WKPRVA 150
      *
      ** Read all records for deal on PF/DLPCSCPD.
      *
     C           #KLST1    SETLLDLPCSCD0
     C           #KLST1    READEDLPCSCD0                 03
      *
      ** If Original Principal Amount is zero, use the first new
      **   Principal Amount, if a Principl Change schedule exists,
      **   or, the deal principal amount if not.
      *
     C           UOPAM     IFEQ 0
     C           *IN03     IFEQ '1'
     C           *IN03     OREQ '0'
     C           DTPR      ANDEQ' '
     C                     Z-ADDUPAMT     UOPAM
     C           DTYP      IFEQ 'RS'
     C                     Z-ADDTPAMT     TOPAM
     C                     ENDIF
     C                     ELSE
     C                     Z-ADDPAMT      UOPAM
     C           DTYP      IFEQ 'RS'
     C                     Z-ADDPAMT      TOPAM
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ** Set-up "previous" principal amount.
      *
     C                     Z-ADDUOPAM     WKPRVA 150
      *
     C           *IN03     DOWEQ'0'
      *
      ** If chamge amount is zero, calculate amount and update record.
      *
     C           CAMT      IFEQ 0
      *
     C           PAMT      SUB  WKPRVA    CAMT
      *
      ** Set-up "previous" principal amount.
      *
     C                     Z-ADDPAMT      WKPRVA
      *
      ** Update record.
      *
     C                     UPDATDLPCSCD0
      *
     C                     ENDIF
      *
      ** Read next record for deal on PF/DLPCSCPD.
      *
     C           #KLST1    READEDLPCSCD0                 03
     C                     ENDDO
      *
      ** Repeat the process for "RX" deals.
      *
     C           DTYP      IFEQ 'RX'
      *
     C                     MOVEL'T'       WKOTIN
      **********
      ***Set-up*"previous" principal amount.
      **********
     C**********           Z-ADDTOPAM     WKPRVA 150
      *
      ** Read all records for deal on PF/DLPCSCPD.
      *
     C           #KLST1    SETLLDLPCSCD0
     C           #KLST1    READEDLPCSCD0                 03
      *
      ** If Original Principal Amount is zero, use the first new
      **   Principal Amount, if a Principl Change schedule exists,
      **   or, the deal principal amount if not.
      *
     C           TOPAM     IFEQ 0
     C           *IN03     IFEQ '1'
     C           *IN03     OREQ '0'
     C           DTPR      ANDEQ' '
     C                     Z-ADDTPAMT     TOPAM
     C                     ELSE
     C                     Z-ADDPAMT      TOPAM
     C                     ENDIF
     C                     ENDIF
      *
      ** Set-up "previous" principal amount.
      *
     C                     Z-ADDTOPAM     WKPRVA
      *
     C           *IN03     DOWEQ'0'
      *
      ** If chamge amount is zero, calculate amount and update record.
      *
     C           CAMT      IFEQ 0
      *
     C           PAMT      SUB  WKPRVA    CAMT
      *
      ** Set-up "previous" principal amount.
      *
     C                     Z-ADDPAMT      WKPRVA
      *
      ** Update record.
      *
     C                     UPDATDLPCSCD0
      *
     C                     ENDIF
      *
      ** Read next record for deal on PF/DLPCSCPD.
      *
     C           #KLST1    READEDLPCSCD0                 03
     C                     ENDDO
      *
     C                     ENDIF
      *
     C                     MOVE *BLANKS   STK,@Q           remove subr name
     C                     SUB  1         @Q               decrement stack
      *
     C                     ENDSR
      *
      *****************************************************************
      *
      **  #P03 - Termination Subroutine.
      *
      *****************************************************************
      *
     C           #P03      BEGSR                           ** #P03 SR   **
      *
     C                     ADD  1         @Q               increment stack
     C                     MOVEL'#P03    'STK,@Q           load subr name
      *
     C                     COMIT
     C                     MOVEL*BLANKS   P@RTCD
     C                     MOVE *ON       *INLR
     C                     RETRN
      *
     C                     MOVE *BLANKS   STK,@Q           remove subr name
     C                     SUB  1         @Q               decrement stack
      *
     C                     ENDSR
      *
      *****************************************************************
      *
      **  *PSSR - Database Error Subroutine.
      *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR  **
      *
      **  If an error has occurred within this program, update LDA, set
      **  error switches, dump program and return to calling program.
      *
     C           WWDUMP    IFEQ *BLANK
     C                     DUMP
     C                     ROLBK
     C                     SETON                     U7U8LR
     C                     MOVE 'Y'       WWDUMP  1
     C           *NAMVAR   DEFN LDA       @LDA  256
     C           *LOCK     IN   @LDA
     C                     MOVE DBERR     @LDA
     C                     OUT  @LDA
     C                     ENDIF
      *
     C                     MOVEL'*ERROR'  P@RTCD
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
