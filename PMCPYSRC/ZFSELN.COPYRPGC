      /EJECT
      *****************************************************************
      *                                                               *
      *  Midas - Copy Source                                          *
      *                                                               *
      *  ZFSELN - Formats the 20A Portfolio Mgmt Line Description     *
      *           for Securities Trading                              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CPM005 *CREATE     DATE 26JUN96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPM005 - Private Banking Phase 2                             *
      *           Focus Group Changes upgraded to DBA                 *
      *                                                               *
      *****************************************************************
      *
     C           ZFSELN    BEGSR
      *****************************************************************
      *
      *  Format: XXX 999.9999 DDMMMYY
      *          XXX =  Nominal currency
      *          999.9999 = Coupon date
      *          DDMMMYY = Maturity date
      *
      *  Input Fields
      *       ZNMCY   3A     Nominal currency
      *       ZCPNR  11N7    Coupon rate
      *       ZMATY   5N0    Maturity date
      *       ZPROT   1A     Processing type
      *
      *  Output Fields
      *       ZLIN   20A     Line description
      *
      *  Array Used
      *       ZLI    20 elements, each 1 character long
      *
      *****************************************************************
      *
      **  Clear line description
      *
     C                     MOVEA*BLANKS   ZLI
      *
      **  Move nominal currency to line description
      *
     C                     MOVEAZNMCY     ZLI,01
      *
      **  If processing type     not commodities ZPROT = 2
      **                         not equities    ZPROT = 4
      **                         not unit trusts ZPROT = 7
      *
     C           ZPROT     IFNE '2'
     C           ZPROT     ANDNE'4'
     C           ZPROT     ANDNE'7'
      *
      **  Move coupon rate to line description
      *
     C                     Z-ADDZCPNR     ZWRKN   74
     C                     MOVELZWRKN     ZWRKA3  3
     C                     MOVE ZWRKN     ZWRKA4  4
     C                     MOVELZWRKA3    ZWRKA2  2
      *
      **  Supress leading zeroes
      *
     C           ZWRKA2    IFEQ '00'
     C                     MOVEL'  '      ZWRKA3
     C                     END
     C                     MOVELZWRKA3    ZWRKA1  1
     C           ZWRKA1    IFEQ '0'
     C                     MOVEL' '       ZWRKA3
     C                     END
     C                     MOVEAZWRKA3    ZLI,05
     C                     MOVEA'.'       ZLI,08
     C                     MOVEAZWRKA4    ZLI,09
      *
      **  Move maturity date to line description
      *
     C                     Z-ADDZMATY     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    ZWRKA1  1
     C           ZADATE    IFNE *BLANK
     C           ZWRKA1    IFEQ ' '
     C                     MOVEL'0'       ZADATE
     C                     END
     C                     MOVEAZADATE    ZLI,14
     C                     END
     C                     END
      *
      **  Set all fields to blanks or zeroes
      *
     C                     MOVE *BLANKS   ZNMCY   3
     C                     MOVE *BLANKS   ZPROT   1
     C                     Z-ADD*ZEROS    ZCPNR  117
     C                     Z-ADD*ZEROS    ZMATY   50
      *
     C                     MOVEAZLI       ZLIN   20
      *
     C                     ENDSR
     C*******************************************************************
