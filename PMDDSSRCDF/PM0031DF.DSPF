      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Select Instrument Types Record')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  DSPF/PM0031DF - Display File for RPG PM0031                  *
     A*            Select Instrument Type record                      *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
     A*  Last Amend No. CPB001             Date  2Jul99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. S01486             DATE 11AUG94               *
     A*                 PM0011             DATE 05JUL94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CPB001 - 'Private Banking' Module                            *
     A*  S01486 - Portfolio Management Upgrade to R10.                *
     A*  PM0011 - Rename PM0030s# to PM0031DF                         *
     A*  S01486 - Created for Private Banking Access Object AOPLINR0  *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
     A*
     A*
     A***********                                    Begin /PM0011/
     A**********R PM0030S0                  TEXT('(c)ACT +
     A          R PM0031S0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A***********                                    End   /PM0011/
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(33 'Invalid #1SEL     ')
     A  84                                  SFLNXTCHG
     A*
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1CHTP         1   H      TEXT('Type of Last Change')
     A            #1SEL          1   B  8  5
     A                                      VALUES('1')
     A                                      CHECK(AB)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            #1INNR         3      8 13TEXT('Inst Type')
     A                                      DSPATR(HI)
     A            #1ISTN        20      8 25TEXT('Inst Name')
     A                                      DSPATR(HI)
     A            #1VALG         3      8 52TEXT('Val Group')
     A                                      DSPATR(HI)
     A            #1DSPG         3      8 67TEXT('Disp Group')
     A                                      DSPATR(HI)
     A*
     A*=========================================================================
     A*
     A***********                                    Begin /PM0011/
     A**********R PM0030C0                  TEXT('Subfile control.')
     A***********                           SFLCTL(PM0030S0)
     A          R PM0031C0                  TEXT('Subfile control.')
     A                                      SFLCTL(PM0031S0)
     A***********                                    End   /PM0011/
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2AGCD')
     A                                      SETOF(32 'Invalid: #2AITX')
     A                                      SETOF(33 'Invalid: #2PRTL')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'PM0031F1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 28'Select Instrument Types'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2CHTP         1   H      TEXT('Type of Last Change')
     A*
     A                                  4 10'Instrument'
     A                                      DSPATR(HI)
     A                                  4 27'Instrument Name'
     A                                      DSPATR(HI)
     A*                                              Begin /CPB001/
     A******                            4 49'Valuation'
     A  85                              4 49'Valuation'
     A*                                              End   /CPB001/
     A                                      DSPATR(HI)
     A*                                              Begin /CPB001/
     A******                            4 65'Display'
     A  85                              4 65'Display'
     A*                                              End   /CPB001/
     A                                      DSPATR(HI)
     A*
     A                                  5 13'Type'
     A                                      DSPATR(HI)
     A*                                              Begin /CPB001/
     A******                            5 51'Group'
     A  85                              5 51'Group'
     A*                                              End   /CPB001/
     A                                      DSPATR(HI)
     A*                                              Begin /CPB001/
     A******                            5 66'Group'
     A  85                              5 66'Group'
     A*                                              End   /CPB001/
     A                                      DSPATR(HI)
     A*
     A            #2INNR         3   B  6 13TEXT('Inst Type')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2ISTN        20   B  6 25TEXT('Inst Name')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2VALG         3   B  6 52TEXT('Val Group')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A*                                              Begin /CPB001/
     A N85                                  DSPATR(ND)
     A N85                                  DSPATR(PR)
     A*                                              End   /CPB001/
     A N25                                  OVRATR
     A            #2DSPG         3   B  6 67TEXT('Disp Group')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A*                                              Begin /CPB001/
     A N85                                  DSPATR(ND)
     A N85                                  DSPATR(PR)
     A*                                              End   /CPB001/
     A N25                                  OVRATR
     A*=========================================================================
     A***********                                    Begin /PM0011/
     A**********R PM0030F0                  TEXT('Command keys.')
     A          R PM0031F0                  TEXT('Command keys.')
     A***********                                    End   /PM0011/
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select request    F3=Exit       -
     A                                                                         -
     A                                               '
     A*=========================================================================
     A***********                                    Begin /PM0011/
     A**********R PM0030S1                  TEXT('Program messages')
     A          R PM0031S1                  TEXT('Program messages')
     A***********                                    End   /PM0011/
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A***********                                    Begin /PM0011/
     A**********R PM0030C1                  TEXT('Program messages')
     A***********                           SFLCTL(PM0030S1)
     A          R PM0031C1                  TEXT('Program messages')
     A                                      SFLCTL(PM0031S1)
     A***********                                    End   /PM0011/
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
