      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Instrument Groups Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  DSPF/PM0050DF - INSTRUMENT GROUPS MAINTENANCE                *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01486             DATE 11AUG94               *
     A*  PREV AMEND NO. S01355             DATE 08JUN92               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01486 - Portfolio Management Upgrade to R10.                *
     A*---------------------------------------------------------------*
     A*      RSTDSP(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*           C R E A T I O N     P A R A M E T E R S             *
     A*
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      VLDCMDKEY(49 'ANY VALID KEY')
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
     A                                      CA03
     A*================================================================
     A**
     A*                                              BEGIN /S01486/
     A**********R PM0050F1                  TEXT('INSTRUMENT GROUPS MAINT.')
     A          R PM0050F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                              END   /S01486/
     A                                      OVERLAY
     A                                      BLINK
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
     A*                                              BEGIN /S01486/
     A************                      1 10'INSTRUMENT GROUPS MAINTENANCE'
     A************                          DSPATR(HI)
     A************WSID          10A  O  1 52TEXT('WORK STATION ID')
     A************DDRUNA         7A  O  1 63
     A************                      1 72TIME
     A            USID          10A  O  1  2
     A                                  1 13'PM0050'
     A                                  1 26'INSTRUMENT GROUP MAINTENANCE'
     A                                      DSPATR(HI UL)
     A            BJMRDT         7A  O  1 72
     A            WSID          10A  O  2  2
     A                                  2 72TIME
     A*                                              END   /S01486/
     A                                  3  2'Action'
     A            DDACTN         1A  B  3 10TEXT('ACTION')
     A                                      DSPATR(HI)
     A N45N47                               DSPATR(UL)
     A  45                                  DSPATR(PR)
     A  47                                  DSPATR(PC RI)
     A            DDDELT         7      3 72TEXT('RECORD DELETED?')
     A                                      DSPATR(HI BL)
     A                                  4  2'Instrument group'
     A            DDINTG         2A  B  4 20TEXT('INSTRUMENT GROUP')
     A                                      DSPATR(HI)
     A N45N48                               DSPATR(UL)
     A  45                                  DSPATR(PR)
     A  48                                  DSPATR(PC RI)
     A                                  4 38'Review from'
     A  45                                  DSPATR(ND)
     A            DDPKEY         2A  B  4 51TEXT('REVIEW FROM KEY')
     A                                      DSPATR(HI)
     A N45N46                               DSPATR(UL)
     A  45                                  DSPATR(PR ND)
     A  46                                  DSPATR(PC RI)
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A*================================================================
     A**
     A          R PM0050S0                  SFL
     A            DDACTN         1A  I  9  2TEXT('ACTION')
     A N40                                  DSPATR(UL HI)
     A  40                                  DSPATR(PC RI)
     A                                      AUTO(RA)
     A            DDSTAT         1A  O  9  7TEXT('STATUS')
     A                                      DSPATR(BL HI)
     A            DDINTG         2A  O  9 10
     A                                      DSPATR(HI)
     A            DDINGN        20A  O  9 24
     A                                      DSPATR(HI)
     A*================================================================
     A**
     A          R PM0050C0                  SFLCTL(PM0050S0)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      BLINK
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A N41N42                               SFLCLR
     A  43                                  SFLEND
     A                                      CA12
     A N43                                  ROLLUP(25)
     A N44                                  ROLLDOWN(26)
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
     A*                                              BEGIN /S01486/
     A************                      1 10'INSTRUMENT GROUPS MAINTENANCE'
     A***********                           DSPATR(HI)
     A************WSID          10A  O  1 52TEXT('WORK STATION ID')
     A************DDRUNA         7A  O  1 63
     A            USID          10A  O  1  2
     A                                  1 13'PM0050'
     A                                  1 26'INSTRUMENT GROUP MAINTENANCE'
     A                                      DSPATR(HI UL)
     A            BJMRDT         7A  O  1 72
     A            WSID          10A  O  2  2
     A                                  2 72TIME
     A*                                              END   /S01486/
     A                                  1 72TIME
     A                                  4 31'Review from'
     A            DDPKEY         2A  B  4 51TEXT('REVIEW FROM KEY')
     A                                      DSPATR(HI UL)
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                  7  9'Group          Narrative'
     A**
     A          R PM0050F3                  TEXT('CLEAR SCREEN')
     A                                  1  2' '
     A*================================================================
     A**
     A          R PM0050S1                  SFL
     A  35                                  SFLNXTCHG
     A            DDFLAG         1A  O 10 10DSPATR(BL HI)
     A            DDINNR         3A  B 10 15
     A                                      DSPATR(HI)
     A N37N36                               DSPATR(UL)
     A  37                                  DSPATR(PR)
     A  36                                  DSPATR(PC RI)
     A            DDISTN        20A  O 10 26DSPATR(HI)
     A            DDDTTK         3A  H
     A            DDDTTL        20A  H
     A*================================================================
     A**
     A          R PM0050C1                  SFLCTL(PM0050S1)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A                                      BLINK
     A N33                                  SFLDSP
     A N33                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND
     A                                      CF10
     A                                      CA12
     A N34                                  ROLLUP(25)
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
     A            SFLRRN         4S 0H      SFLRCDNBR
     A                                  7  2'Instrument Group Narrative'
     A            DDGNAR        20A  B  7 30
     A                                      DSPATR(HI)
     A N32N31                               DSPATR(UL)
     A  32                                  DSPATR(PR)
     A  30                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                 09 12'Instrument'
     A                                 09 30'Narrative'
     A*================================================================
     A**
     A          R PM0050E2                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*================================================================
     A**
     A          R PM0050F2                  SFLCTL(PM0050E2)
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A N38                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
     A            DDINFO        78A  O 23  1
