      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Select Bank Investment Policies Record')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  DSPF/PM0091DF - Display File for RPG PM0091 (Select Rec)     *
     A*            Select Record for Bank Investment Policies         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01486             DATE 11AUG94               *
     A*  PREV AMEND NO. PM0011             DATE 05JUL94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01486 - Portfolio Management Upgrade to R10.                *
     A*  PM0011 - Rename from PM0090S# to PM0091DF                    *
     A*                                                               *
     A*****************************************************************
     A*           C R E A T I O N     P A R A M E T E R S             *
     A*
     A*                    RSTDSP(*YES)                               *
     A*****************************************************************
     A*                                                               *
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
     A*
     A*
     A***********                                    Begin /PM0011/
     A**********R PM0090S0                  TEXT('(c)ACT +
     A          R PM0091S0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A***********                                    End   /PM0011/
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(33 'Invalid #1SEL     ')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1CHTP         1   H      TEXT('Type of Last Change')
     A            #1SEL          1   B  8  3
     A                                      VALUES('1')
     A                                      CHECK(AB)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            #1POLC         2      8  7TEXT('Invst Pol')
     A                                      DSPATR(HI)
     A            #1POLN        20      8 16TEXT('Invst Narr')
     A                                      DSPATR(HI)
     A            #1IPIN         1      8 41TEXT('Inst Pol')
     A                                      DSPATR(HI)
     A            #1CYIN         1      8 53TEXT('Curr Pol')
     A                                      DSPATR(HI)
     A            #1NPIN         1      8 64TEXT('Indst Pol')
     A                                      DSPATR(HI)
     A            #1YPIN         1      8 75TEXT('Count Pol')
     A                                      DSPATR(HI)
     A*
     A*=========================================================================
     A*
     A***********                                    Begin /PM0011/
     A**********R PM0090C0                  TEXT('Subfile control.')
     A***********                           SFLCTL(PM0090S0)
     A          R PM0091C0                  TEXT('Subfile control.')
     A                                      SFLCTL(PM0091S0)
     A***********                                    End   /PM0011/
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2AGCD')
     A                                      SETOF(32 'Invalid: #2AITX')
     A                                      SETOF(33 'Invalid: #2PRTL')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'PM0091F1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 26'Select Bank Investment +
     A                                      Policies'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A*
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2CHTP         1   H      TEXT('Type of Last Change')
     A*
     A                                  4  5'Investment'
     A                                      DSPATR(HI)
     A                                  4 18'Policy Narrative'
     A                                      DSPATR(HI)
     A                                  4 37'Instrument'
     A                                      DSPATR(HI)
     A                                  4 50'Currency'
     A                                      DSPATR(HI)
     A                                  4 61'Industry'
     A                                      DSPATR(HI)
     A                                  4 72'Country'
     A                                      DSPATR(HI)
     A*
     A                                  5  7'Policy'
     A                                      DSPATR(HI)
     A                                  5 39'Policy'
     A                                      DSPATR(HI)
     A                                  5 51'Policy'
     A                                      DSPATR(HI)
     A                                  5 62'Policy'
     A                                      DSPATR(HI)
     A                                  5 73'Policy'
     A                                      DSPATR(HI)
     A*
     A            #2POLC         2   B  6  7TEXT('Invst Pol')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2POLN        20   B  6 16TEXT('Pol Narr')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2IPIN         1   B  6 41TEXT('Inst Pol')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2CYIN         1   B  6 53TEXT('Curr Pol')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2NPIN         1   B  6 64TEXT('Indst Pol')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2YPIN         1   B  6 75TEXT('Count Pol')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A***********                                    Begin /PM0011/
     A**********R PM0090F0                  TEXT('Command keys.')
     A          R PM0091F0                  TEXT('Command keys.')
     A***********                                    End   /PM0011/
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select request    F3=Exit       -
     A                                                                         -
     A                                               '
     A*=========================================================================
     A***********                                    Begin /PM0011/
     A**********R PM0090S1                  TEXT('Program messages')
     A          R PM0091S1                  TEXT('Program messages')
     A***********                                    End   /PM0011/
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A***********                                    Begin /PM0011/
     A**********R PM0090C1                  TEXT('Program messages')
     A***********                           SFLCTL(PM0090S1)
     A          R PM0091C1                  TEXT('Program messages')
     A                                      SFLCTL(PM0091S1)
     A***********                                    End   /PM0011/
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
