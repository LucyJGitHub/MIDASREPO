      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Select Document Comment Codes Record')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  DSPF/PM0131DF - Display File for RPG PM0131  (Select Rec)    *
     A*            Select Record for Document Comment Codes           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01486             Date 11Aug94               *
      *                 PM0011             Date 07Jul94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01486 - Portfolio Management Upgrade to R10.                *
     A*  PM0011 - Rename from PM0130S# to PM0131DF                    *
     A*                                                               *
     A*****************************************************************
     A*           C R E A T I O N     P A R A M E T E R S             *
     A*
     A*                    RSTDSP(*YES)                               *
     A*****************************************************************
     A*
     A                                      INDARA
     A                                      PRINT(QSYSPRT)
     A                                      CHGINPDFT
     A*
     A*
     A***********                                    Begin /PM0011/
     A**********R PM0130S0                  TEXT('(c)ACT +
     A          R PM0131S0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A***********                                    End   /PM0011/
     A                                      SFL
     A                                      SETOF(98 'Force input format')
     A                                      INDTXT(84 'Condition SFLNXTCHG.')
     A                                      INDTXT(33 'Invalid #1SEL     ')
     A  84                                  SFLNXTCHG
     A            #1LCD          5  0H      TEXT('Last Change Date')
     A            #1CHTP         1   H      TEXT('Type of Last Change')
     A            #1SEL          1   B  8 18
     A                                      VALUES('1')
     A                                      CHECK(AB)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL HI)
     A            #1DCCC         3      8 25TEXT('Doc Com Code')
     A                                      DSPATR(HI)
     A            #1DCMN        20      8 38TEXT('Document Comment')
     A                                      DSPATR(HI)
     A*
     A*=========================================================================
     A*
     A***********                                    Begin /PM0011/
     A**********R PM0130C0                  TEXT('Subfile control.')
     A***********                           SFLCTL(PM0130S0)
     A          R PM0131C0                  TEXT('Subfile control.')
     A                                      SFLCTL(PM0131S0)
     A***********                                    End   /PM0011/
     A                                      SETOF(98 'Force input format')
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      OVERLAY
     A  86                                  PUTOVR
     A                                      INDTXT(80 'Condition SFLCLR.')
     A                                      INDTXT(81 'Condition SFLDSP.')
     A                                      INDTXT(82 'Condition SFLEND.')
     A                                      INDTXT(86 'Condition PUTOVR.')
     A  80                                  SFLCLR
     A N80                                  SFLDSPCTL
     A N25
     AAN80
     AA 81                                  SFLDSP
     A N80 81 82                            SFLEND
     A*.COMMAND.KEYS............................................................
     A                                      VLDCMDKEY(29 'Valid CMD key.')
     A                                      CA03(03 'Exit.')
     A                                      CF05(05 'Reset.')
     A N82                                  ROLLUP(27 'Next page.')
     A*.SETOFFS.................................................................
     A                                      SETOF(99 'Global error ind.')
     A                                      SETOF(31 'Invalid: #2AGCD')
     A                                      SETOF(32 'Invalid: #2AITX')
     A                                      SETOF(33 'Invalid: #2PRTL')
     A*.........................................................................
     A            ##SFRC         4  0H      SFLRCDNBR
     A            ##USR         10      1  2TEXT('*USER')
     A N25                                  OVRDTA
     A            ##PGM         10   H      TEXT('*PROGRAM')
     A                                  1 13'PM0131F1'
     A            ##MRDT         7      1 72TEXT('Midas Run Date')
     A N25                                  OVRDTA
     A            ##JOB         10      2  2TEXT('*JOB')
     A N25                                  OVRDTA
     A                                  2 27'Select Document Comment +
     A                                      Codes'
     A                                      DSPATR(HI)
     A            ##TME          6  0   2 71TEXT('*TIME')
     A                                      EDTWRD('  :  :  ')
     A N25                                  OVRDTA
     A*
     A            #2LCD          5  0H      TEXT('Last Change Date')
     A            #2CHTP         1   H      TEXT('Type of Last Change')
     A*
     A                                  4 22'Document'
     A                                      DSPATR(HI)
     A                                  4 40'Document Comment'
     A                                      DSPATR(HI)
     A*
     A                                  5 20'Comment Code'
     A                                      DSPATR(HI)
     A*
     A            #2DCCC         3   B  6 25TEXT('Doc Com Code')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A            #2DCMN        20   B  6 38TEXT('Doc Comm')
     A N25                                  OVRDTA
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL HI)
     A N25                                  OVRATR
     A*=========================================================================
     A***********                                    Begin /PM0011/
     A**********R PM0130F0                  TEXT('Command keys.')
     A          R PM0131F0                  TEXT('Command keys.')
     A***********                                    End   /PM0011/
     A                                      OVERLAY PUTOVR
     A                                 23  2'1=Select request    F3=Exit       -
     A                                                                         -
     A                                               '
     A*=========================================================================
     A***********                                    Begin /PM0011/
     A**********R PM0130S1                  TEXT('Program messages')
     A          R PM0131S1                  TEXT('Program messages')
     A***********                                    End   /PM0011/
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A***********                                    Begin /PM0011/
     A**********R PM0130C1                  TEXT('Program messages')
     A***********                           SFLCTL(PM0130S1)
     A          R PM0131C1                  TEXT('Program messages')
     A                                      SFLCTL(PM0131S1)
     A***********                                    End   /PM0011/
     A                                      SFLPAG(01) SFLSIZ(03)
     A  86                                  OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
