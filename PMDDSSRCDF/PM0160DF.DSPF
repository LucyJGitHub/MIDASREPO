      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Customer Documentation Maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  DSPF/PM0160DF - CUSTOMER DOCUMENTATION MAINTENANCE           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01486             DATE 11AUG94               *
     A*  PREV AMEND NO. S01355             DATE 08JUN92               *
     A*                 B9662              DATE 04SEP91               *
     A*                 R0982_             DATE 20NOV90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01486 - Portfolio Management Upgrade to R10.                *
     A*  B9662  - REMOVE FIX R0982 (THIS FIELD CAN NOT BE ALWAYS      *
     A*           IN PROTECT).                                        *
     A*  R0982  - ENHANCEMENT FOR CUST. SCREENS HANDLING              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*      RSTDSP(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*           C R E A T I O N     P A R A M E T E R S             *
     A*
     A*****************************************************************
     A                                      VLDCMDKEY(45 'ANY VALID KEY')
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
     A                                      CF03
     A                                      CF12
     A**=====================================================================
     A**
     A** FORMAT PM0160F1 **
     A*                                              BEGIN /S01486/
     A**********R PM0160F1                  TEXT('HEADER FORMAT')
     A          R PM0160F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                              END   /S01486/
     A                                      OVERLAY
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
     A*                                              BEGIN /S01486/
     A************                      1 10'CUSTOMER DOCUMENTATION +
     A************                           MAINTENANCE'
     A************                          DSPATR(HI)
     A************WSID          10A  O  1 52TEXT('WORK STATION ID')
     A************DDRUNA         7A  O  1 63
     A************                      1 72TIME
     A            USID          10A  O  1  2
     A                                  1 13'PM0160'
     A                                  1 23'CUSTOMER DOCUMENTATION +
     A                                      MAINTENANCE'
     A                                      DSPATR(HI UL)
     A            BJMRDT         7A  O  1 72
     A            WSID          10A  O  2  2
     A                                  2 72TIME
     A*                                              END   /S01486/
     A                                  3  3'Customer Number'
     A            DDCNUM         6A  O  3 20TEXT('CUSTOMER NUMBER')
     A                                      DSPATR(HI)
     A                                  3 29'Name'
     A            DDNAME        20A  O  3 34TEXT('CUSTOMER NAME')
     A                                      DSPATR(HI)
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A*======================================================================
     A**
     A** FORMAT PM0160E2
     A**
     A          R PM0160E2                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A**
     A**
     A** FORMAT PM0160F2
     A**
     A          R PM0160F2                  SFLCTL(PM0160E2)
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A N52                                  SFLEND
     A                                      SFLINZ
     A            DDPGMQ                    SFLPGMQ
     A*
     A            DDINFO        76A  O 23  1
     A**=====================================================================
     A** FORMAT PM0160F3 **
     A          R PM0160F3                  TEXT('CLEAR SCREEN')
     A                                  1  2' '
     A**=====================================================================
     A** FORMAT PM0160S0 **
     A          R PM0160S0                  SFL
     A**53****                              SFLNXTCHG
     A*
     A            DDDCCO         3A  O 10 06TEXT('DOCUMENT CODE')
     A                                      DSPATR(HI)
     A*
     A            DDDNAR        20A  O 10 13TEXT('DOCUMENT CODE TEXT')
     A                                      DSPATR(HI)
     A  51                                  DSPATR(RI)
     A*
     A            DDDCCC         3A  B 10 37TEXT('DOCUMENT COMMENT CODE')
     A                                      DSPATR(HI)
     A N35N36                               DSPATR(UL)
     A  35                                  DSPATR(PR)
     A  36                                  DSPATR(PC RI)
     A*
     A            DDDCCM        20A  B 10 44TEXT('DOCUMENT COMMENT TEXT')
     A                                      DSPATR(HI)
     A N35N37                               DSPATR(UL)
     A**35***R0982                          DSPATR(PR)
     A*******R0982*B9662                    DSPATR(PR)
     A** B9662 START
     A  35                                  DSPATR(PR)
     A** B9662 END
     A  37                                  DSPATR(PC RI)
     A            DDDCMS         1A  B 10 72TEXT('DOCUMENTATION STATUS')
     A                                      DSPATR(HI)
     A N35N38                               DSPATR(UL)
     A  35                                  DSPATR(PR)
     A  38                                  DSPATR(PC RI)
     A*
     A*
     A** FORMAT PM0160C0 **
     A          R PM0160C0                  SFLCTL(PM0160S0)
     A                                      SFLSIZ(0250)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      BLINK
     A N54                                  SFLDSP
     A N54                                  SFLDSPCTL
     A  54                                  SFLCLR
     A  55                                  SFLEND
     A*****                                 ROLLUP(56)
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
      *>>S01355*<< Release 3 incorporation
     A*                                              BEGIN /S01486/
     A*                                              END   /S01486/
     A            SFLRRN         4S 0H      SFLRCDNBR
     A*                                              BEGIN /S01486/
     A************                      1 10'CUSTOMER DOCUMENTATION +
     A************                           MAINTENANCE'
     A************                          DSPATR(HI)
     A************WSID          10A  O  1 52TEXT('WORK STATION ID')
     A************DDRUNA         7A  O  1 63
     A************                      1 72TIME
     A            USID          10A  O  1  2
     A                                  1 13'PM0160'
     A                                  1 23'CUSTOMER DOCUMENTATION +
     A                                      MAINTENANCE'
     A                                      DSPATR(HI UL)
     A            BJMRDT         7A  O  1 72
     A            WSID          10A  O  2  2
     A                                  2 72TIME
     A*                                              END   /S01486/
     A                                  3  3'Customer Number'
     A            DDCNUM         6A  O  3 20TEXT('CUSTOMER NUMBER')
     A                                      DSPATR(HI)
     A                                  3 29'Name'
     A            DDNAME        20A  O  3 34TEXT('CUSTOMER NAME')
     A                                      DSPATR(HI)
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                 07 03'Document'
     A                                 07 13'Document Code Text'
     A                                 07 35'Comment'
     A                                 07 44'Document Comment Text'
     A                                 07 67'Documentation'
     A                                 08 05'Code'
     A                                 08 37'Code'
     A                                 08 70'Status'
