      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Select Cashflow Horizonts')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  DSPF/PM0811DF - Select cashflow Horizonts to be reported.    *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01486             Date 11Aug94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01486 - Portfolio Managemenmt Upgrade to R10.               *
     A*           File created from PM0810DF with Customer and        *
     A*           Portfoloi omitted                                   *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*      RSTDSP(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*           C R E A T I O N     P A R A M E T E R S             *
     A*
     A*****************************************************************
     A                                      VLDCMDKEY(50 'ANY VALID KEY')
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CA03
     A**
     A**=====================================================================
     A**  FORMAT PM0811F1  **
     A**
     A          R PM0811F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      BLINK
     A**
     A            USER          10A  O  1  2
     A                                  1 13'PM0811'
     A                                  1 27'Select Cashflow Details to be Rep+
     A                                      orted'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE'
     A            BJMRDT         7A  O  1 73
     A            WKSTN         10A  O  2  2
     A                                  2 67'TIME'
     A                                  2 73TIME
     A**
     A                                  8  9'Cashflow horizons required:'
     A**
     A                                  9  9'60 day detailed (Y/N)'
     A            DDDAYD         1A  B  9 38TEXT('60 DAY DETAILED')
     A                                      DSPATR(HI)
     A N08N25                               DSPATR(UL)
     A N08 25                               DSPATR(PC RI)
     A**
     A                                 10  9'Monthly total (Y/N)'
     A            DDMONT         1A  B 10 38TEXT('MONTH TOTAL')
     A                                      DSPATR(HI)
     A N08N26                               DSPATR(UL)
     A N08 26                               DSPATR(PC RI)
     A**
     A                                 11  9'Yearly total (Y/N)'
     A            DDYEAR         1A  B 11 38TEXT('YEAR TOTAL')
     A                                      DSPATR(HI)
     A N08N27                               DSPATR(UL)
     A N08 27                               DSPATR(PC RI)
     A**
     A**
     A                                 23  3'F3=Exit'
     A**
     A**=====================================================================
     A**  FORMAT PM0811E2  **
     A**
     A          R PM0811E2                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A**
     A**  FORMAT PM0811F2  **
     A**
     A          R PM0811F2                  SFLCTL(PM0811E2)
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A N52                                  SFLEND
     A                                      SFLINZ
     A            DDPGMQ                    SFLPGMQ
     A**
