      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Portfolio Movements Enquiry')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                          *
     A*                                                               *
     A*  DSPF/PM2055DF - PORTFOLIO MOVEMENTS (SE) ENQUIRY             *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. CPM005             DATE 11JUL96               *
     A*  PREV AMEND NO. CPM004             DATE 07FEB96               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CPM005 - Private Banking Phase II                            *
     A*           Focus Group Changes Upgraded to DBA                 *
     A*  CPM004 - Bank Portfolios (NEW PROGRAM)                       *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CF03
     A                                      CF09
     A                                      CF12
     A***********                                    Begin /CPM005/
     A**********                            CF22
     A***********                                    End   /CPM005/
     A          R PM2055S1                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            DDACTN         1A  B  8  2
     A N74                                  DSPATR(PR ND)
     A  70                                  DSPATR(HI)
     A                                      AUTO(RA)
     A            DDACDT         7A  O  8  4
     A  70                                  DSPATR(HI)
     A            DDBCTR         6A  O  8 12
     A  70                                  DSPATR(HI)
     A            DDNML         15A  O  8 19
     A  70                                  DSPATR(HI)
     A  71                                  DSPATR(BL)
     A            DDPRC         12A  O  8 35
     A  70                                  DSPATR(HI)
     A            DDAPN         16A  O  8 48
     A  70                                  DSPATR(HI)
     A  72                                  DSPATR(BL)
     A            DDFAP         16A  O  8 65
     A  70                                  DSPATR(HI)
     A  73                                  DSPATR(BL)
     A            BCTR           6A  H
     A            RRN            4S 0H
     A          R PM2055C1                  SFLCTL(PM2055S1)
     A                                      OVERLAY
     A                                      RTNDTA
     A                                      PUTOVR
     A                                      SFLSIZ(1600)
     A                                      SFLPAG(0016)
     A                                      BLINK
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A            USID          10A  O  1  2
     A                                  1 13'PM2055'
     A            DDHED1        32A     1 25
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A            DDRUNA         7A  O  1 73TEXT('RUN DATE')
     A            WSID          10A  O  2  2
     A                                  2 73TIME
     A                                  3  2'Portfolio Customer'
     A            DDCNUM         6A  O  3 22TEXT('PORTFOLIO CUSTOMER NUMBER')
     A                                      DSPATR(HI)
     A            DDCRNM        20A  O  3 30TEXT('CUSTOMER NAME')
     A                                      DSPATR(HI)
     A                                  4  2'Portfolio Reference'
     A            DDPTFR         4A  O  4 22TEXT('PORTFOLIO REFERENCE')
     A                                      DSPATR(HI)
     A            DDPTFN        20A  O  4 30TEXT('PORTFOLIO NARRATIVE')
     A                                      DSPATR(HI)
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                  6  2'Security'
     A            DDSESN        10A  O  6 11TEXT('SECURITY SHORTNAME')
     A                                      DSPATR(HI)
     A            DDSFN1        30A  O  6 23TEXT('SECU FULL NAME-1')
     A                                      DSPATR(HI)
     A                                  6 59'Currency'
     A            DDNMCY         3A  O  6 68TEXT('NOMINAL CURRENCY')
     A                                      DSPATR(HI)
     A                                  7  2'D'
     A                                  7  4'  Date '
     A                                  7 12' Ref. '
     A                                  7 19'   Nominal   '
     A                                  7 33'    Price      '
     A                                  7 49'   Book Cost   '
     A                                  7 65'Portfolio Cost'
     A            CRRN           4  0H      SFLRCDNBR(CURSOR)
     A          R PM2055F1
     A                                      OVERLAY
     A                                      PUTOVR
     A            DDHED2        79A    24  2
     A                                      OVRDTA
