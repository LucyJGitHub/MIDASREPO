      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Related customer Numbers enquiry')
     A********************************************************************
     A*                                                                  *
     A*  MIDAS - PORTFOLIO MANAGEMENT MODULE                             *
     A*                                                                  *
     A*  DSPF/PM3100DF - RELATED CUSTOMER NUMBERS ENQUIRY                *
     A*                                                                  *
     A*     (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. CPM005             DATE 26JUL96                  *
     A*+                S01124     MLD     DATE 03JUN94                  *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
     A*  MD046248 - Finastra Rebranding                                  *
     A*  CPM005  -  Private Banking Phase 2                              *
     A*             Focus Group Changes Upgraded to DBA                  *
     A*  S01124  -  PBFG/6 - ADDITIONAL CUSTOMER DETAILS                 *
     A*                                                                  *
     A********************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A********************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(45 'ANY VALID KEY')
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03
     A                                      CA12
     A*======================================================================
     A** FORMAT PM3100S1
     A*======================================================================
     A*                                              Begin /CPM005/
     A**********R PM3100S1                  SFL
     A          R PM3100S1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A*                                              End /CPM005/
     A            DDCUST        10A  O  8  7TEXT('CUSTOMER NUMBER/SHORTNAME')
     A            DDCSSN        10A  O  8 26TEXT('CUSTOMER SHORTNAME')
     A            DDCRNM        20A  O  8 42TEXT('CUSTOMER REPORT NAME')
     A**=====================================================================
     A** FORMAT PM3100C1 **
     A**=====================================================================
     A          R PM3100C1                  SFLCTL(PM3100S1)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A N39                                  ROLLUP(40)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  80                                  SFLDSPCTL
     A N80N81                               SFLCLR
     A  80 39                               SFLEND
     A*                                              Begin /CPM005/
     A*                                              End /CPM005/
     A*
     A            RRN            4S 0H      SFLRCDNBR
     A**
     A*                                              Begin /CPM005/
     A***********                       1  6'RELATED CUSTOMER NUMBERS ENQUIRY'
     A***********                           DSPATR(HI)
     A*********** WSID          10A  O  1 49TEXT('WORK STATION ID')
     A*********** DDRUNA         7A  O  1 62
     A***********                       1 72TIME
     A*
     A            USER          10A  O  1  2
     A                                  1 13'PM3100'
     A                                  1 25'RELATED CUSTOMER NUMBERS ENQUIRY'
     A                                       DSPATR(HI UL)
     A                                  1 67'DATE '
     A            BJMRDT         7A  O  1 73
     A            WSID          10A  O  2  2TEXT('WORK STATION ID')
     A                                  2 67'TIME'
     A                                  2 73TIME EDTWRD('  :  :  ')
     A*                                              End /CPM005/
     A*
     A N41                              3  4'Customer Id'
     A  41                              3  4'Customer Nb'
     A            DDUSID        20A  O  3 16TEXT('CUSTOMER IDENTIFIER')
     A                                      DSPATR(HI)
     A                                  5  2'                                  -
     A                                                                         -
     A                                                '
     A                                      DSPATR(UL)
     A*                                              Begin /CPM005/
     A************                      6 28'Customer'
     A************                          DSPATR(HI)
     A************                      7  7'Customer Number '
     A************                          DSPATR(HI)
     A************                      7 27'Shortname'
     A************                          DSPATR(HI)
     A************                      7 42'Customer Report Name'
     A************                          DSPATR(HI)
     A*
     A                                  6 28'Customer'
     A                                  7  7'Customer Number '
     A                                  7 27'Shortname'
     A                                  7 42'Customer Report Name'
     A*                                              End /CPM005/
     A*
     A*======================================================================
     A** FORMAT PM3100S0
     A*======================================================================
     A          R PM3100S0                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*======================================================================
     A** FORMAT PM3100C0
     A*======================================================================
     A          R PM3100C0                  SFLCTL(PM3100S0)
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      OVERLAY
     A  40
     AON40                                  SFLEND
     A                                      SFLINZ
     A            DDPGMQ                    SFLPGMQ
     A  39                             18 17'***  NO DETAILS FOR THIS CUSTOMER -
     A                                       ***'
     A  39                                  DSPATR(HI BL)
     A*======================================================================
     A** FORMAT PM3100F1
     A*======================================================================
     A          R PM3100F1
     A                                      OVERLAY
     A*                                              Begin /CPM005/
     A**********                       23  3'F3=Main Menu'
     A**********                            DSPATR(HI)
     A**********                       23 18'F12=Previous Screen'
     A**********                            DSPATR(HI)
     A*
     A                                 23  3'F3=Main Menu'
     A                                 23 18'F12=Previous Screen'
     A*                                              End /CPM005/
