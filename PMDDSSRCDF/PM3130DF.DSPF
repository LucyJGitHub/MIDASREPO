      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Customer Credit Card enquiry')
     A*
     A********************************************************************
     A*                                                                  *
     A*  MIDAS -  PORTFOLIO MANAGEMENT MODULE                            *
     A*                                                                  *
     A*  DSPF/PM3130DF - CUSTOMER CREDIT CARD ENQUIRY                    *
     A*                                                                  *
     A*     (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. CPM005             DATE 26JUL96                  *
     A*+                S01124     MLD     DATE 26APR94                  *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
     A*  MD046248 - Finastra Rebranding                                  *
     A*  CPM005  -  Private Banking Phase 2                              *
     A*             Focus Group Changes Upgraded to DBA                  *
     A*  S01124  -  PBFG/6 - ADDITIONAL CUSTOMER DETAILS                 *
     A*                                                                  *
     A********************************************************************
     A*     CREATION REQUIREMENTS                                     *
     A*                                                               *
     A*      RSTDSP(*YES)                                             *
     A*      SHARE(*YES)                                              *
     A*                                                               *
     A*****************************************************************
     A*
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      VLDCMDKEY(45 'ANY VALID KEY')
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03
     A                                      CA12
      *-------------------------------------------------------------------------
     A**
     A** SUBFILE 1 - NARRATIVE DETAILS
     A**
     A*                                              Begin /CPM005/
     A********* R PM3130S1                  SFL
     A          R PM3130S1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
     A*                                              End /CPM005/
     A**
     A            DDCRCN        15A  O  7  2TEXT('CREDIT CARD NAME')
     A            DDCANB        25A  O  7 20TEXT('CREDIT CARD NUMBER')
     A            DDLCCY         3A  O  7 47TEXT('LIMIT CURRENCY')
     A            DDLAMT        18A  O  7 51TEXT('LIMIT AMOUNT')
     A            DDEXDA         6A  O  7 71TEXT('EXPIRY DATE')
     A            DDTEXT        75A  O  8  3TEXT('CREDIT CARD TEXT')
      *-------------------------------------------------------------------------
     A          R PM3130C1                  SFLCTL(PM3130S1)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A*                                              Begin /CPM005/
     A N39                                  ROLLUP(40)
     A*                                              End /CPM005/
     A                                      OVERLAY
     A                                      PUTOVR
     A  81                                  SFLDSP
     A  80                                  SFLDSPCTL
     A N80N81                               SFLCLR
     A*                                              Begin /CPM005/
     A  80 39                               SFLEND
     A*                                              End /CPM005/
     A**
     A            RRN            4S 0H      SFLRCDNBR
     A**
     A*                                              Begin /CPM005/
     A***********                       1 11'CUSTOMER CREDIT CARD ENQUIRY'
     A***********                           DSPATR(HI)
     A*********** WSID          10A  O  1 49TEXT('WORKSTATION ID')
     A*********** DDRUNA         7A  O  1 62TEXT('RUN DATE')
     A***********                       1 72TIME
     A**
     A            USER          10A  O  1  2
     A                                  1 13'PM3130'
     A                                  1 25'CUSTOMER CREDIT CARD ENQUIRY'
     A                                      DSPATR(HI UL)
     A                                  1 67'DATE '
     A            BJMRDT         7A  O  1 73
     A            WSID          10A  O  2  2TEXT('WORK STATION ID')
     A                                  2 67'TIME'
     A                                  2 73TIME EDTWRD('  :  :  ')
     A*                                              End /CPM005/
     A                                  3  4'Customer No/Shortname'
     A            DDCNUM        10A  O  3 26TEXT('CUSTOMER NUMBER')
     A                                      DSPATR(HI)
     A                                  3 37'/'
     A            DDCSSN        10A  O  3 39TEXT('CUSTOMER SHORTNAME')
     A                                      DSPATR(HI)
     A                                  3 51'Name'
     A            DDCRNM        20A  O  3 56TEXT('CUSTOMER REPORT NAME')
     A                                      DSPATR(HI)
     A**
     A                                  4  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A*                                              Begin /CPM005/
     A**********                        5  2'Credit Card Name'
     A**********                            DSPATR(HI)
     A**********                        5 23'Credit Card Number'
     A**********                            DSPATR(HI)
     A**********                        5 49'Card Limit'
     A**********                            DSPATR(HI)
     A**********                        5 68'Expires'
     A**********                            DSPATR(HI)
     A**********                        6  3'<--------------------------- Credi-
     A**********                            t Card Text ------------------------
     A**********                            ----->'
     A**********                            DSPATR(HI)
     A*
     A                                  5  2'Credit Card Name'
     A                                  5 23'Credit Card Number'
     A                                  5 49'Card Limit'
     A                                  5 68'Expires'
     A                                  6  3'<--------------------------- Credi-
     A                                      t Card Text ------------------------
     A                                      ----->'
     A*                                              End /CPM005/
      *-------------------------------------------------------------------------
     A          R PM3130S0                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *-------------------------------------------------------------------------
     A          R PM3130C0                  SFLCTL(PM3130S0)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0001)
     A                                      SFLINZ
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      OVERLAY
     A  40
     AON40                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
     A***********                                    Begin /CPM005/
     A**39*******                      18 17'***  NO DETAILS FOR THIS CUSTOMER -
     A***********                            ***'
     A**39*******                           DSPATR(HI BL)
     A  38                             18 17'***  NO DETAILS FOR THIS CUSTOMER -
     A                                       ***'
     A  38                                  DSPATR(HI BL)
     A***********                                    End /CPM005/
      *-------------------------------------------------------------------------
     A          R PM3130F1
     A                                      OVERLAY
     A*                                              Begin /CPM005/
     A**********                       23  3'F3=Main Menu'
     A**********                            DSPATR(HI)
     A**********                       23 18'F12=Previous Screen'
     A**********                            DSPATR(HI)
     A*
     A                                 23  3'F3=Main Menu'
     A                                 23 18'F12=Previous Screen'
     A*                                              End /CPM005/
