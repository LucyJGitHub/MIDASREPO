      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Performance Event Details Mtnc.')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                          *
     A*                                                               *
     A*  DSPF/PM4110DF - PERFORMANCE EVENT DETAILS MAINTENANCE        *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG2627            Date 12May04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 078956             Date 31Jan96               *
      *                 085862             DATE 16Mar95               *
     A*                 S01486             DATE 11AUG94               *
     A*                 73957              DATE 29JUL94               *
     A*                 72605              DATE 08SEP94               *
     A*                 72242              DATE 15JUN94               *
     A*                 53103              DATE 19OCT93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  BUG2627- Re-format TIME value and edit word.                 *
     A*  078956 - DISPLAY FX RATE & ALLOW AMENDMENT FOR DEPOT MOVEMENT*
     A*  085862 - INCLUDE BRANCH CODE IN ACCOUNT IDENTIFIER           *
     A*  S01486 - Portfolio Management Upgrade to R10.                *
     A*  73957  - PM INSTRUCTION MANUAL CHANGES                       *
     A*  72605  - ALLOW MARKET PRICE TO BE CHANGED FOR DEPOT MOVEMEN  *
     A*  72242  - ALLOW PERFORMANCE EVENTS TO BE DELETED              *
     A*  53103  - FILE CREATED FOR PERFORMANCE RECONCILIATION CHANGE  *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*           C R E A T I O N     P A R A M E T E R S             *
     A*
     A*****************************************************************
     A                                      REF(MFRFL)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CA03(03 'END OF PGM')
     A                                      INDTXT(42 'Duplicate mode')
     A*                                              Begin /S01486/
     A*                                              End   /S01486/
     A*
     A*****************************************************************
     A**   SCREEN HEADER + EVENT REFERENCE
     A**---------------------------------------------------------------
     A*
     A***********                                    Begin /S01486/
     A**********R*PM4110F0                  TEXT('Initial screen')
     A          R PM4110F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A***********                                    End   /S01486/
     A                                      INDTXT(48 'PROTECT REFERENCE')
     A*                                              Begin /S01486/
     A*                                              End   /S01486/
     A*                                              Begin /S01486/
     A***********                       1  7'PERFORMANCE EVENT DETAILS -
     A***********                           MAINTENANCE'
     A***********                           DSPATR(HI)
     A*********** DDJOB         10A  O  1 49TEXT('JOB NAME')
     A*********** DDDATE         7A  O  1 62TEXT('MIDAS RUN DATE')
     A***********                    O  1 72TIME
     A***********                           EDTWRD('  :  :  ')
     A*
     A            ##USR     R        O  1  2REFFLD(USER)
     A                                  1 13'PM4110'
     A                                  1 22'PERFORMANCE EVENT DETAILS +
     A                                      MAINTENANCE'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A            DDDATE    R        O  1 73REFFLD(RUNA)
     A*
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
      **Start of change BUG2627
     A**********  ##TIME         6  0   2 73EDTWRD('  :  :  ')                               BUG2627
     A                                  2 73TIME
     A                                      EDTWRD('  :  :  ')
      **End of change BUG2627
     A*                                              End   /S01486/
     A*
     A                                  3  3'Event Reference'
     A            DDREFE         7A  B  3 19TEXT('EVENT REFERENCE')
     A                                      DSPATR(HI)
     A N71N48                               DSPATR(UL)
     A  71                                  DSPATR(RI)
     A  71                                  DSPATR(PC)
     A  48                                  DSPATR(PR)
     A*
     A***START 72242***
     A**42**                            3 30'Duplicate of performance -
     A****                                  event reference'
     A****                                  DSPATR(HI)
     A**42**      DDDPRF         7A  O    +1TEXT('Duplicated reference')
     A****                                  DSPATR(HI)
     A*
     A  42N60
     AO 11                              3 30'Duplicate of performance -
     A                                      event reference'
     A                                      DSPATR(HI)
     A  42N60
     AO 11        DDDPRF         7A  O    +1TEXT('Duplicated reference')
     A                                      DSPATR(HI)
     A  48 60                           3 65'DELETED'
     A                                      DSPATR(BL HI RI)
     A***END 72242***
     A                                  4  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A*
     A*****************************************************************
     A**   SCREEN TRAILER - KEYS USED
     A**---------------------------------------------------------------
     A*
     A          R PM4110F1                  TEXT('Trailer')
     A                                      INDTXT(41 'ENABLE F9')
     A                                      INDTXT(46 'ENABLE F12')
     A***START 72242***
     A**41****                              CF09(09 'ADD LINKS')
     A  41N60                               CF09(09 'ADD LINKS')
     A  60                                  CF05(05 'RESET')
     A  61                                  CF10(10 'DELETE')
     A  56                                  CF11(11 'INSERT')
     A**END 72242***
     A  46                                  CA12(12 'PREVIOUS LEVEL')
     A                                      OVERLAY
     A                                 23  2'F3=Main menu'
     A***START 72242***
     A*******                          23  2'F3=Main menu'
     A**41***                          23 16'F9=Duplicate Event'
     A**46***                          23 36'F12=Previous screen'
     A                                 23  2'F3=Exit'
     A  60                             23 11'F5=Restore'
     A  41                             23 23'F9=Duplicate'
     A  61                             23 37'F10=Delete'
     A  56                             23 50'F11="XT" Event'
     A  46                             23 66'F12=Previous'
     A***END 72242***
     A*
     A*****************************************************************
     A**   MAIN SCREEN
     A**---------------------------------------------------------------
     A*
     A          R PM4110F2                  TEXT('Main screen')
     A                                      OVERLAY
     A                                      INDTXT(43 'Protect categ./subcat.')
     A                                      INDTXT(44 'Display Applies to ret.')
     A                                      INDTXT(45 'Protect Applies to ret.')
     A                                      INDTXT(47 'Protect Capital change')
     A*                                              Begin /S01486/
     A*                                              End   /S01486/
     A***START 73957***
     A***START 72605***
     A***END 72605***
     A*                                              Begin /S01486/
     A*
     A***********                                    Begin /078956/
     A***********                                    End   /078956/
     A*                                              End   /S01486/
     A***END 73957***
     A*
     A                                  6  5'Customer'
     A            DDCUST         6A  O  6 15TEXT('Customer no.')
     A                                    +1'/'
     A            DDCUSH        10A  O    +1TEXT('Customer shortname')
     A                                  7  5'Portfolio'
     A            DDPORT         4A  O  7 25TEXT('Portfolio')
     A*
     A                                  8  5'Module id'
     A            DDMODU         2A  O  8 25TEXT('Module id')
     A*
     A                                  9  5'Position Reference'
     A            DDPORE        20A  O  9 25TEXT('Posit. Reference')
     A***********                                    Begin /085862/
     A*
     A            DDBRCD         3A  O  9 46TEXT('BRANCH CODE')
     A***********                                    End   /085862/
     A*
     A                                 10  5'Event Date'
     A            DDEVDT         7A  O 10 25TEXT('Event date')
     A*
     A                                 11  5'Transaction Type'
     A            DDTRTP         5A  O 11 25TEXT('Transact. type')
     A***********                                    Begin /078956/
     A***********                      13  5'Performance Category'
     A*********** DDPECA         2A  B 13 34TEXT('Perf. Category')
     A                                 12  5'Performance Category'
     A            DDPECA         2A  B 12 34TEXT('Perf. Category')
     A***********                                    End   /078956/
     A                                      DSPATR(HI)
     A N72N43                               DSPATR(UL)
     A  72                                  DSPATR(RI)
     A  72                                  DSPATR(PC)
     A  43                                  DSPATR(PR)
     A***********                                    Begin /078956/
     A*********** DDPECT        17A  O 13 38TEXT('Category text')
     A            DDPECT        17A  O 12 38TEXT('Category text')
     A  50                             13  5'FX Rate (Nominal to Base)'
     A  50        DDMFXR        19A  B 13 34TEXT('Perf. Category')
     A  50                                  DSPATR(HI)
     A N55 50                               DSPATR(UL)
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A N50                                  DSPATR(PR)
     A***********                                    End   /078956/
     A***START 72605***
     A  49                             14  5'Market Price'
     A  49        DDMKTP        16A  B 14 34TEXT('Perf. Category')
     A  49                                  DSPATR(HI)
     A N76 49                               DSPATR(UL)
     A  76                                  DSPATR(RI)
     A  76                                  DSPATR(PC)
     A N49                                  DSPATR(PR)
     A***END 72605***
     A*
     A                                 15  5'Performance Event Amount'
     A            DDPEAC         3A  O 15 34TEXT('Perf. Event Amt Ccy')
     A            DDPEAM        15A  B 15 38TEXT('Perf. Event Amount')
     A                                      DSPATR(HI)
     A N73N42                               DSPATR(UL)
     A  73                                  DSPATR(RI)
     A  73                                  DSPATR(PC)
     A  42                                  DSPATR(PR)
     A*
     A                                 17  5'Change to Portfolio Capital'
     A            DDCHPC         1A  B 17 34TEXT('Change to portf. capital')
     A                                      DSPATR(HI)
     A N74N42N47                            DSPATR(UL)
     A  74                                  DSPATR(RI)
     A  74                                  DSPATR(PC)
     A  42
     AO 47                                  DSPATR(PR)
     A*
     A  44                             19  5'Applies to retail account'
     A  44        DDAPRA         1A  B 19 34TEXT('Applies to retail a/c')
     A                                      DSPATR(HI)
     A N75N45                               DSPATR(UL)
     A  75                                  DSPATR(RI)
     A  75                                  DSPATR(PC)
     A  45                                  DSPATR(PR)
     A*
     A*****************************************************************
     A**   ERROR MESSAGE SUBFILE
     A**---------------------------------------------------------------
     A*
     A          R #MSGRCD
     A                                      SFL SFLMSGRCD(24)
     A            DDMSGK                    SFLMSGKEY
     A            DDPGM                     SFLPGMQ
     A*
     A*****************************************************************
     A**   ERROR MESSAGE SUBFILE CONTROL
     A**---------------------------------------------------------------
     A*
     A          R #MSGCTL
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(10)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  99
     AON99                                  SFLEND
     A            DDPGM                     SFLPGMQ
