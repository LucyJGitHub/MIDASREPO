      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Pool Portfolio Definition Maintenance')
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  DSPF/PM5010DF - POOL PORTFOLIO DEFINITION MAINTENANCE        *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01486             Date 11Aug94               *
      *                 073957             Date 30Nov94               *
     A*                 059472             DATE 04AUG93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01486 - Portfolio Management Upgrade to R10.                *
     A*  073957 - Changes to PM instruction manual                    *
     A*  059472  - RECOMPILE DUE TO HELP AREA CHANGES                 *
     A*                                                               *
     A*  WRITTEN FOR SAR 91/08 - POOL SECURITIES TRANSACTIONS         *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*           C R E A T I O N     P A R A M E T E R S             *
     A*
     A*****************************************************************
     A                                      VLDCMDKEY(45 'ANY VALID KEY')
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CA03
     A*
     A** FORMAT PM5010F1 **
     A***********                                    Begin /S01486/
     A*********** PM5010F1                  TEXT('POOL PORTFOLIO MAINTENANCE')
     A          R PM5010F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A***********                                    End   /S01486/
     A                                      OVERLAY
     A***********                                    Begin /S01486/
     A***********                           BLINK
     A***********                       1  9'POOL PORTFOLIO DEFINITION'
     A            USID          10A  O  1  2
     A                                  1 13'PM5010'
     A                                  1 25'POOL PORTFOLIO DEFINITION'
     A***********                                    End   /S01486/
     A                                      DSPATR(HI)
     A***********                                    Begin /S01486/
     A                                      DSPATR(UL)
     A***********                       1 35'MAINTENANCE'
     A                                  1 51'MAINTENANCE'
     A***********                                    End   /S01486/
     A                                      DSPATR(HI)
     A***********                                    Begin /S01486/
     A                                      DSPATR(UL)
     A*********** WSID          10A  O  1 52TEXT('WORK STATION ID')
     A*********** DDRUNA         7A  O  1 63
     A***********                       1 72TIME
     A                                  1 67'DATE '
     A            DDRUNA         7A  O  1 73
     A            WSID          10A  O  2  2TEXT('WORK STATION ID')
     A                                  2 67'TIME '
     A                                  2 73TIME EDTWRD('  :  :  ')
     A***********                                    End   /S01486/
     A                                  3  2'Action'
     A            DDACTN         1A  B  3 10TEXT('ACTION')
     A                                      DSPATR(HI)
     A N33N30                               DSPATR(UL)
     A  33                                  DSPATR(PR)
     A  30                                  DSPATR(PC RI)
     A            DDDELT         7      3 72TEXT('RECORD DELETED?')
     A                                      DSPATR(HI BL)
     A                                  4  2'Pool Customer'
     A            DDPCNU         6A  B  4 17TEXT('Pool Customer')
     A                                      DSPATR(HI)
     A N33N31                               DSPATR(UL)
     A  33                                  DSPATR(PR)
     A  31                                  DSPATR(PC RI)
     A                                  4 26'Portfolio Reference'
     A            DDPPTF         4A  B  4 46TEXT('Portfolio Reference')
     A                                      DSPATR(HI)
     A N33N32                               DSPATR(UL)
     A  33                                  DSPATR(PR)
     A  32                                  DSPATR(PC RI)
     A                                  4 57'Review from'
     A  33                                  DSPATR(ND)
     A            DDKEY1         6A  B  4 69TEXT('REVIEW FROM KEY')
     A                                      DSPATR(HI)
     A N33N34                               DSPATR(UL)
     A  33                                  DSPATR(PR ND)
     A  34                                  DSPATR(PC RI)
     A            DDKEY2         4A  B  4 77TEXT('REVIEW FROM KEY')
     A                                      DSPATR(HI)
     A N33N34                               DSPATR(UL)
     A  33                                  DSPATR(PR ND)
     A  34                                  DSPATR(PC RI)
     A                                  5  2'                                  -
     A                                                                         -
     A                                                '
     A                                      DSPATR(UL)
     A*
     A** FORMAT PM5010S0 **
     A          R PM5010S0                  SFL
     A            DDACTN         1A  B 10  2TEXT('ACTION')
     A                                      DSPATR(HI)
     A N47                                  DSPATR(UL)
     A  47                                  DSPATR(PC RI)
     A                                      AUTO(RA)
     A            DDSTAT         1A  O 10  7TEXT('STATUS')
     A                                      DSPATR(BL HI)
     A            DDPCNU         6A  O 10 17DSPATR(HI)
     A            DDPPTF         4A  O 10 29DSPATR(HI)
     A*
     A** FORMAT PM5010C0 **
     A          R PM5010C0                  SFLCTL(PM5010S0)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      BLINK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A N50N51                               SFLCLR
     A  52                                  SFLEND
     A                                      CA12
     A N52                                  ROLLUP(25)
     A N53                                  ROLLDOWN(26)
     A*059472
     A***********                                    Begin /S01486/
     A***********                       1  9'POOL PORTFOLIO DEFINITION'
     A            USID          10A  O  1  2
     A                                  1 13'PM5010'
     A                                  1 25'POOL PORTFOLIO DEFINITION'
     A***********                                    End   /S01486/
     A                                      DSPATR(HI)
     A***********                                    Begin /S01486/
     A                                      DSPATR(UL)
     A***********                       1 35'MAINTENANCE'
     A                                  1 51'MAINTENANCE'
     A***********                                    End   /S01486/
     A                                      DSPATR(HI)
     A***********                                    Begin /S01486/
     A                                      DSPATR(UL)
     A*********** WSID          10A  O  1 52TEXT('WORK STATION ID')
     A*********** DDRUNA         7A  O  1 63
     A***********                       1 72TIME
     A                                  1 67'DATE '
     A            DDRUNA         7A  O  1 73
     A            WSID          10A  O  2  2TEXT('WORK STATION ID')
     A                                  2 67'TIME '
     A                                  2 73TIME EDTWRD('  :  :  ')
     A***********                                    End   /S01486/
     A                                  4 57'Review from'
     A            DDKEY1         6A  B  4 69TEXT('REVIEW FROM KEY')
     A                                      DSPATR(HI UL)
     A            DDKEY2         4A  B  4 77TEXT('REVIEW FROM KEY')
     A                                      DSPATR(HI UL)
     A                                  5  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                  8 17'Customer'
     A                                  8 28'Portfolio'
     A*
     A** FORMAT PM5010F3 **
     A          R PM5010F3                  TEXT('CLEAR SCREEN')
     A                                  1  2' '
     A*
     A** FORMAT PM5010S1 **
     A          R PM5010S1                  SFL
     A  54                                  SFLNXTCHG
     A            DDFLAG         1A  O 10 10DSPATR(BL HI)
     A*
     A            DDCNUM         6A  B 10 17TEXT('DETAILS 1')
     A                                      DSPATR(HI)
     A N37N35                               DSPATR(UL)
     A  37                                  DSPATR(PR)
     A  35                                  DSPATR(PC RI)
     A*
     A            DDPTFR         4A  B 10 29TEXT('DETAILS 2')
     A                                      DSPATR(HI)
     A N37N36                               DSPATR(UL)
     A  37                                  DSPATR(PR)
     A  36                                  DSPATR(PC RI)
     A            DDNOUN         7A  B 10 44TEXT('DETAILS 3')
     A                                      DSPATR(HI)
     A N37N38                               DSPATR(UL)
     A  37                                  DSPATR(PR)
     A  38                                  DSPATR(PC RI)
     A            DDDTTK         6A  H
     A            DDDTTL         4A  H
     A            DDDTTM         7A  H
     A*
     A** FORMAT PM5010C1 **
     A          R PM5010C1                  SFLCTL(PM5010S1)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A                                      BLINK
     A N55                                  SFLDSP
     A N55                                  SFLDSPCTL
     A  55                                  SFLCLR
     A  56                                  SFLEND
     A                                      CF10
     A                                      CA12
     A N56                                  ROLLUP(25)
     A*059472
     A***********                                    Begin /S01486/
     A*                                     BEGIN /073957/
     A*                                     END /073957/
     A***********                                    End   /S01486/
     A            SFLRRN         4S 0H      SFLRCDNBR
     A                                  6  2'Start Date'
     A            DDPSTD         6A  B  6 15TEXT('Start Date')
     A                                      DSPATR(HI)
     A N60N61                               DSPATR(UL)
     A  61                                  DSPATR(PR)
     A  60                                  DSPATR(PC RI)
     A                                  6 29'Interim Date'
     A            DDITDT         6A  B  6 42TEXT('Interim Date')
     A                                      DSPATR(HI)
     A N62N63                               DSPATR(UL)
     A  63                                  DSPATR(PR)
     A  62                                  DSPATR(PC RI)
     A                                  6 57'Open/Closed Indicator'
     A            DDOPCP         1A  B  6 79TEXT('Open/Closed Indicator')
     A                                      DSPATR(HI)
     A N64N65                               DSPATR(UL)
     A  65                                  DSPATR(PR)
     A  64                                  DSPATR(PC RI)
     A                                  7  2'Initial Unit Value'
     A            DDINUS        16A  B  7 29TEXT('Initial Unit Value')
     A                                      DSPATR(HI)
     A N66N67                               DSPATR(UL)
     A  67                                  DSPATR(PR)
     A  66                                  DSPATR(PC RI)
     A                                  7 54'Trade Number Range Code'
     A            DDRANG         2A  B  7 78TEXT('Trade Number Range Code')
     A                                      DSPATR(HI)
     A N68N69                               DSPATR(UL)
     A  69                                  DSPATR(PR)
     A  68                                  DSPATR(PC RI)
     A                                 09 17'Customer'
     A                                 09 29'Portfolio'
     A                                 09 40'Number of Units'
     A**
     A          R PM5010E2                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A**
     A          R PM5010F2                  SFLCTL(PM5010E2)
     A                                      SFLSIZ(0025)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A N57                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
     A*
     A            DDINFO        76A  O 23  1
