      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Net Commitment by Currency Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Portfolio Management Module                          *
      *                                                               *
      *  DSPF/PM6020DF - PM Net Commitment by Currency Enquiry        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  LAST AMEND NO. CPM005             DATE 08JUL96               *
      *  PREV AMEND NO. S01124             DATE 28JUN94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPM005 - Private Banking Phase II                            *
      *           Focus Group Changes Upgraded to DBA                 *
      *  S01124 - PBFG/7 - NET COMMITMENT BY CURRENCY                 *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      VLDCMDKEY(60 'ANY VALID KEY')
     A                                      CA03
     A                                      CA12
     A***********                                    Begin /CPM005/
     A                                      CA22
     A***********                                    End   /CPM005/
     A*----------------------------------------------------------------
     A** HEADER FORMAT
     A*----------------------------------------------------------------
     A***********                                    Begin /CPM005/
     A**********R PM6020F1
     A          R PM6020F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A***********                                    End   /CPM005/
     A                                      OVERLAY
     A***********                                    Begin /CPM005/
     A***********                                    End   /CPM005/
     A***********                                    Begin /CPM005/
     A**********  WSID          10A  O  1 52TEXT('WORKSTATION ID')
     A**********  DDRUNA         7A  O  1 63TEXT('RUN DATE')
     A**********                        1 72TIME
     A            USER          10A  O  1  2
     A                                  1 13'PM6020'
     A                                  1 22'NET COMMITMENT BY CURRENCY ENQUIRY'
     A                                      DSPATR(HI)
     A            DDRUNA         7A  O  1 72
     A            WKSTN         10A  O  2  2
     A                                  2 72TIME
     A***********                                    End   /CPM005/
     A            DDCNUM         6A  O  3 11DSPATR(HI)
     A            DDCRNM        20A  O  3 18DSPATR(HI)
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      --------'
     A                                  3  2'Customer'
     A            DDPTFR         4A  O  4 13DSPATR(HI)
     A***********                                    Begin /CPM005/
     A**********                        1 10'NET COMMITMENT BY CURRENCY ENQUIRY'
     A**********                            DSPATR(HI)
     A***********                                    End   /CPM005/
     A                                  4  2'Portfolio'
     A            DDPTFN        20A  O  4 18DSPATR(HI)
     A                                  3 39'Total Current Value'
     A            DDCCY1         3A  O  3 60DSPATR(HI)
     A            DDCUVL        16A  O  3 64DSPATR(HI)
     A                                  4 39'Total Net Commitment'
     A            DDCCY2         3A  O  4 60DSPATR(HI)
     A            DDNTCO        16A  O  4 64DSPATR(HI)
     A*----------------------------------------------------------------
     A**   SUBFILE
     A*----------------------------------------------------------------
     A          R PM6020S1                  SFL
     A            DSACTN         1A  I  9  2DSPATR(HI)
     A***********                                    Begin /CPM005/
     A**********                            VALUES('D' ' ')
     A***********                                    End   /CPM005/
     A                                      CHECK(ER)
     A                                      DSPATR(PC)
     A            DSPERC         6A  O  9 74
     A            DSNTCO        15A  O  9 58
     A            DSFFOR        15A  O  9 42
     A            DSCVAL        15A  O  9 10
     A            DSCURR         3A  O  9  6
     A            DSFORX        15A  O 10 42
     A            DSUNAM        15A  O 10 58
     A            DSCAPC        15A  O  9 26
     A*----------------------------------------------------------------
     A**   SUBFILE CONTROL
     A*----------------------------------------------------------------
     A          R PM6020C1                  SFLCTL(PM6020S1)
     A                                      SFLSIZ(0500)
     A                                      SFLPAG(0007)
     A                                      OVERLAY
     A N61                                  SFLDSP
     A N61                                  SFLDSPCTL
     A  61                                  SFLCLR
     A  62                                  SFLINZ
     A N62                                  SFLEND
     A            SFLRRN         4S 0H      SFLRCDNBR
     A                                  7  2'Sel'
     A                                  7  6'Ccy'
     A                                  7 12'Current Value'
     A                                  7 27'Capital Change'
     A                                  7 42'Fwd Commitments'
     A                                  7 58'Net Commitment'
     A                                  7 75'%'
     A                                  8  2'(D)'
     A                                  8 42'FX F&O Amounts'
     A                                  8 58'Unhedged Amount'
     A                                  6 10'<----------- Figures in Portfolio -
     A                                      Currency'
     A                                  6 53'('
     A            DDPMCU         3   O  6 55DSPATR(HI)
     A                                  6 59')'
     A                                  6 61'---------->'
     A*----------------------------------------------------------------
     A** FORMAT PM6020F2
     A*----------------------------------------------------------------
     A          R PM6020F2
     A                                      OVERLAY
     A                                 24  2'F3=Exit'
     A                                 24 12'F12=Previous Screen'
     A***********                                    Begin /CPM005/
     A**********                       24 34'Rollup/Rolldown'
     A                                 24 34'F22=Enquiry Selection'
     A*----------------------------------------------------------------
     A** FORMAT PM6020F3
     A*----------------------------------------------------------------
     A          R PM6020F3
     A                                      OVERLAY
     A  82                             23  2'109-Invalid entry'
     A                                      DSPATR(HI)
     A***********                                    End   /CPM005/
