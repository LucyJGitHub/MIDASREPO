      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Total by Instrument Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Portfolio Management Module                          *
      *                                                               *
      *  DSPF/PM6050DF - PM Totals by Instrument Enquiry              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CPM005             Date 08Jul96               *
      *                 S01124             Date 26May94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CPM005 - Private Banking Phase II                            *
      *           Focus Group Changes Upgraded to DBA                 *
      *  S01124 - PBFG/5 - INSTRUMENT ASSET LIABILITY SPLIT           *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      VLDCMDKEY(60 'ANY VALID KEY')
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03
     A                                      CA12
     A***********                                    Begin /CPM005/
     A                                      CA22
     A***********                                    End   /CPM005/
     A                                      CA23
     A                                      CA24
     A*----------------------------------------------------------------
     A** HEADER FORMAT
     A*----------------------------------------------------------------
     A***********                                    Begin /CPM005/
     A**********R PM6050F1
     A          R PM6050F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A***********                                    End   /CPM005/
     A                                      OVERLAY
     A                                      PUTOVR
     A***********                                    Begin /CPM005/
     A**********  WSID          10A  O  1 52TEXT('WORKSTATION ID')
     A**********  DDRUNA         7A  O  1 63TEXT('RUN DATE')
     A**********                            OVRDTA
     A**********                        1 72TIME
     A            USER          10A  O  1  2
     A                                  1 13'PM6050'
     A                                  1 22'TOTALS BY INSTRUMENT ENQUIRY'
     A                                      DSPATR(HI)
     A            DDRUNA         7A  O  1 72
     A            WKSTN         10A  O  2  2
     A                                  2 72TIME
     A***********                                    End   /CPM005/
     A            DDCNUM         6A  O  3 12DSPATR(HI)
     A                                      OVRDTA
     A            DDCRNM        20A  O  3 19DSPATR(HI)
     A                                      OVRDTA
     A                                  5  2'-----------------------------------
     A                                      ------------------------------------
     A                                      --------'
     A***********                                    Begin /CPM005/
     A**********                        1 15'TOTALS BY INSTRUMENT ENQUIRY'
     A**********                            DSPATR(HI)
     A***********                                    End   /CPM005/
     A                                  3  2'Customer'
     A                                  3 42'Portfolio'
     A            DDPTFR         4A  O  3 53DSPATR(HI)
     A                                      OVRDTA
     A            DDPTFN        20A  O  3 58DSPATR(HI)
     A                                      OVRDTA
     A                                  4  2'Portfolio Ccy'
     A            DDPTCY         3A  O  4 17DSPATR(HI)
     A                                      OVRDTA
     A                                  4 23'Total AS'
     A            DDASSV        16A  O  4 32DSPATR(HI)
     A                                      OVRDTA
     A                                  4 54'LI'
     A            DDLIAV        16A  O  4 57DSPATR(HI)
     A                                      OVRDTA
     A*----------------------------------------------------------------
     A**   SUBFILE
     A*----------------------------------------------------------------
     A          R PM6050S1                  SFL
     A            DDACTN         1A  B  8  2DSPATR(HI)
     A***********                                    Begin /CPM005/
     A**********                            VALUES('D' ' ')
     A***********                                    End   /CPM005/
     A                                      CHECK(ER)
     A                                      DSPATR(PC)
     A            DDCUPO        25A  O  8  5
     A  78                                  COLOR(RED)
     A  79                                  COLOR(BLU)
     A  80                                  COLOR(GRN)
     A            DDASVL        16A  O  8 31
     A  78                                  COLOR(RED)
     A  79                                  COLOR(BLU)
     A  80                                  COLOR(GRN)
     A            DDLIVL        16A  O  8 55
     A  78                                  COLOR(RED)
     A  79                                  COLOR(BLU)
     A  80                                  COLOR(GRN)
     A            DDASPC         6A  O  8 48
     A  78                                  COLOR(RED)
     A  79                                  COLOR(BLU)
     A  80                                  COLOR(GRN)
     A            DDLIPC         6A  O  8 72
     A  78                                  COLOR(RED)
     A  79                                  COLOR(BLU)
     A  80                                  COLOR(GRN)
     A            DDTOTT         1A  H
     A            DDPOTY         2A  H
     A            DDDGRC         3A  H
     A            DDDGRS         2S 0H
     A            DDINSC         3A  H
     A            DDINSS         2S 0H
     A            DDTOAS        15S 0H
     A            DDTOLI        15S 0H
     A*----------------------------------------------------------------
     A**   SUBFILE CONTROL
     A*----------------------------------------------------------------
     A          R PM6050C1                  SFLCTL(PM6050S1)
     A                                      SFLSIZ(0500)
     A                                      SFLPAG(0015)
     A                                      OVERLAY
     A                                      PUTOVR
     A N61N02                               SFLDSP
     A N61                                  SFLDSPCTL
     A  61                                  SFLCLR
     A N62                                  SFLEND
     A            SFLRRN         4S 0H      SFLRCDNBR
     A                                  7  1'(D)'
     A                                  7  5'Position Text'
     A                                  7 36'Value -'
     A            DDASCY         3A  O  7 44DSPATR(HI)
     A***********                                    Begin /CPM005/
     A                                      OVRDTA
     A***********                                    End   /CPM005/
     A                                  7 60'Value -'
     A            DDLICY         3A  O  7 68DSPATR(HI)
     A***********                                    Begin /CPM005/
     A                                      OVRDTA
     A***********                                    End   /CPM005/
     A                                  7 50'%'
     A                                  7 74'%'
     A                                  6 42'Asset'
     A                                  6 62'Liability'
     A*----------------------------------------------------------------
     A** FORMAT PM6050F2
     A*----------------------------------------------------------------
     A          R PM6050F2
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 24  3'F3=Exit'
     A                                 24 12'F12=Previous'
     A                                 24 26'F23=Cashflow'
     A                                 24 40'F24=Customer Details'
     A***********                                    Begin /CPM005/
     A**********                       24 62'ATTN=Enquiry Sel'
     A                                 24 62'F22=Enquiry Sel'
     A*----------------------------------------------------------------
     A** FORMAT PM6050F3
     A*----------------------------------------------------------------
     A          R PM6050F3
     A                                      OVERLAY
     A  82                             23  2'109-Invalid entry'
     A                                      DSPATR(HI)
     A***********                                    End   /CPM005/
