      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Consolidated Portfolio Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Portfolio Management Module                          *
      *                                                               *
      *  DSPF/PM6330DF - Consolidated Portfolio Maintenance           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CPM005             DATE 18JUN96               *
      *  Prev Amend No. S01124             DATE 09MAY94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPM005 - Private Banking Phase 2                             *
      *           Focus Group Changes upgraded to DBA                 *
      *  S01124 - PBFG/2 - CALCULATE & REPORT PERF. FOR ALL CUST.PORT.*
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      VLDCMDKEY(60 'ANY VALID KEY')
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CF03
     A*======================================================================
     A** FORMAT PM6330F1 **
     A*======================================================================
     A*                                              Begin /CPM005/
     A**********R PM6330F1                  TEXT('HEADER FORMAT')
     A          R PM6330F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                              End   /CPM005/
     A                                      OVERLAY
     A                                      BLINK
     A*                                              Begin /CPM005/
     A***********                       1  3'CONSOLIDATED PORTFOLIO DEFINITION +
     A***********                            MAINTENANCE'
     A***********                           DSPATR(HI)
     A*********** WSIDN         10A  O  1 52TEXT('WORK STATION ID')
     A*********** DDRUNA         7A  O  1 63
     A***********                       1 72TIME
     A***********
     A***********                       2  2'Action'
     A*********** DDACTN         1A  B  2 15
     A            USID          10A  O  1  2
     A                                  1 13'PM6330'
     A                                  1 22'CONSOLIDATED PORTFOLIO DEFINITION +
     A                                      MAINTENANCE'
     A                                      DSPATR(HI UL)
     A            BJMRDT         7A  O  1 72
     A            WSIDN         10A  O  2  2
     A                                  2 72TIME
     A                                  3  2'Action'
     A            DDACTN         1A  B  3 12TEXT('ACTION')
     A*                                              End   /CPM005/
     A                                      DSPATR(HI)
     A N25N26                               DSPATR(UL)
     A  25                                  DSPATR(PR)
     A  26                                  DSPATR(PC RI)
     A*
     A*                                              Begin /CPM005/
     A***********                       3  2'Customer'
     A*********** DDCSNM        10A  B  3 15
     A                                  4  2'Customer'
     A            DDCSNM        10A  B  4 15
     A*                                              End   /CPM005/
     A                                      DSPATR(HI)
     A N25N27                               DSPATR(UL)
     A  25                                  DSPATR(PR)
     A  27                                  DSPATR(PC RI)
     A*
     A*                                              Begin /CPM005/
     A***********                       4  2'Portfolio'
     A*********** DDPREF         4A  B  4 15TEXT('PORTFOLIO REFERENCE')
     A                                  5  2'Portfolio'
     A            DDPREF         4A  B  5 15TEXT('PORTFOLIO REFERENCE')
     A*                                              End   /CPM005/
     A                                      DSPATR(HI)
     A N25N28                               DSPATR(UL)
     A  25                                  DSPATR(PR)
     A  28                                  DSPATR(PC RI)
     A*
     A*                                              Begin /CPM005/
     A***********                       4 36'Review from:Customer'
     A                                  5 36'Review from:Customer'
     A*                                              End   /CPM005/
     A  25                                  DSPATR(ND)
     A*                                              Begin /CPM005/
     A*********** DDPNP8         6A  B  4 57
     A            DDPNP8         6A  B  5 57
     A*                                              End   /CPM005/
     A                                      DSPATR(HI)
     A N25N29                               DSPATR(UL)
     A  25                                  DSPATR(PR ND)
     A  29                                  DSPATR(PC RI)
     A*
     A*                                              Begin /CPM005/
     A***********                       4 65'Portfolio'
     A                                  5 65'Portfolio'
     A*                                              End   /CPM005/
     A  25                                  DSPATR(ND)
     A*                                              Begin /CPM005/
     A*********** DDPORT         4A  B  4 75
     A            DDPORT         4A  B  5 75
     A*                                              End   /CPM005/
     A                                      DSPATR(HI)
     A  25                                  DSPATR(PR ND)
     A*                                              Begin /CPM005/
     A***********                       5  2'                                  -
     A***********                                                              -
     A***********                                   '
     A                                  6  2'                                  -
     A                                                                         -
     A                                              '
     A*                                              End   /CPM005/
     A                                      DSPATR(UL)
     A*======================================================================
     A** FORMAT PM6330F2
     A*======================================================================
     A          R PM6330F2
     A                                      TEXT('HEADER FORMAT')
     A                                      OVERLAY
     A                                      CF12
     A*
     A                                  7  2'Valuation Frequency'
     A            DDVLFQ         1A  B  7 23TEXT('VALUATION FREQUENCY')
     A                                      DSPATR(HI)
     A N31N40                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  40                                  DSPATR(PC)
     A  40                                  DSPATR(RI)
     A*
     A                                  7 27'Day in Month'
     A            DDVLDM         2A  B  7 41TEXT('DAY IN MONTH')
     A                                      DSPATR(HI)
     A N31N41                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  41                                  DSPATR(PC)
     A  41                                  DSPATR(RI)
     A*
     A                                  7 46'Next Statement Date'
     A            DDVLND         6A  B  7 67TEXT('NEXT STATEMENT DATE')
     A                                      DSPATR(HI)
     A N31N42                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  42                                  DSPATR(PC)
     A  42                                  DSPATR(RI)
     A*
     A                                 10  2'Select (Y/N) Sections of Statement-
     A                                       to be Printed:'
     A*
     A                                 12  5'Inst.Summary'
     A            DDS9VR         1A  B 12 18TEXT('INSTRUMENT SUMMARY')
     A                                      DSPATR(HI)
     A N31N44                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  44                                  DSPATR(PC)
     A  44                                  DSPATR(RI)
     A*
     A                                 12 20'Curr.Summ.'
     A            DDS0VR         1A  B 12 31TEXT('CURRENCY SUMMARY')
     A                                      DSPATR(HI)
     A N31N45                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  45                                  DSPATR(PC)
     A  45                                  DSPATR(RI)
     A*
     A                                 12 34'Position Details'
     A            DDS1VR         1A  B 12 51TEXT('RESUME')
     A                                      DSPATR(HI)
     A N31N43                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  43                                  DSPATR(PC)
     A  43                                  DSPATR(RI)
     A*
     A                                 12 54'Sector Breakdowns'
     A            DDS2VR         1A  B 12 72TEXT('SECTOR BREAKDOWNS')
     A                                      DSPATR(HI)
     A N31N46                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  46                                  DSPATR(PC)
     A  46                                  DSPATR(RI)
     A*
     A                                 13  5'Performance For Past Year'
     A            DDS3VR         1A  B 13 31TEXT('Performance for Past Year')
     A                                      DSPATR(HI)
     A N31N47                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  47                                  DSPATR(PC)
     A  47                                  DSPATR(RI)
     A*
     A                                 13 34'Tax Year To Date'
     A            DDS4VR         1A  B 13 51TEXT('Tax Year to Date')
     A                                      DSPATR(HI)
     A N31N48                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  48                                  DSPATR(PC)
     A  48                                  DSPATR(RI)
     A*
     A                                 13 54'Additional Period'
     A            DDS5VR         1A  B 13 72TEXT('Monthly Cashflo')
     A                                      DSPATR(HI)
     A N31N49                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  49                                  DSPATR(PC)
     A  49                                  DSPATR(RI)
     A*
     A                                 14  5'Daily Cashflows'
     A            DDS6VR         1A  B 14 31TEXT('Monthly Cashflo')
     A                                      DSPATR(HI)
     A N31N50                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  50                                  DSPATR(PC)
     A  50                                  DSPATR(RI)
     A*
     A                                 14 34'Monthly'
     A            DDS7VR         1A  B 14 51TEXT('Monthly Cashflows')
     A                                      DSPATR(HI)
     A N31N51                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  51                                  DSPATR(PC)
     A  51                                  DSPATR(RI)
     A*
     A                                 14 54'Yearly'
     A            DDS8VR         1A  B 14 72TEXT('Yearly Cashflows')
     A                                      DSPATR(HI)
     A N31N52                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  52                                  DSPATR(PC)
     A  52                                  DSPATR(RI)
     A*
     A                                 17  2'Performance Periods:'
     A*
     A                                 17 23'Report Past Year (Y/N)'
     A            DDRPYP         1A  B 17 46TEXT('Report Past Year Performance')
     A                                      DSPATR(HI)
     A N31N53                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  53                                  DSPATR(PC)
     A  53                                  DSPATR(RI)
     A*
     A                                 17 49'Tax Year Start Date'
     A            DDTYSM         2A  B 17 69TEXT('Cust. Tax Year Start Month')
     A                                      DSPATR(HI)
     A N31N54                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  54                                  DSPATR(PC)
     A  54                                  DSPATR(RI)
     A*
     A                                 18 14'Additional Period (H/Q)'
     A            DDADPP         1A  B 18 38TEXT('Additional Perform. Period')
     A                                      DSPATR(HI)
     A N31N55                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  55                                  DSPATR(PC)
     A  55                                  DSPATR(RI)
     A*
     A                                 18 42'Performance Start Date'
     A            DDPSDT         6A  B 18 69TEXT('Performance Extracted From')
     A                                      DSPATR(HI)
     A N31N56                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  56                                  DSPATR(PC)
     A  56                                  DSPATR(RI)
     A*
     A                                 19 14'Performance Net/Gross of Tax (N/G)'
     A            DDPMNG         1A  B 19 50TEXT('Net or Gross of Tax')
     A                                      DSPATR(HI)
     A N31N57                               DSPATR(UL)
     A  31                                  DSPATR(PR)
     A  57                                  DSPATR(PC)
     A  57                                  DSPATR(RI)
     A*
     A*======================================================================
     A** FORMAT PM6330E3
     A*======================================================================
     A          R PM6330E3                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A*======================================================================
     A** FORMAT PM6330F3
     A*======================================================================
     A          R PM6330F3                  SFLCTL(PM6330E3)
     A                                      SFLSIZ(0024)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A N65                                  SFLEND
     A                                      SFLINZ
     A            DDPGMQ                    SFLPGMQ
     A*
     A            DDINFO        76A  O 23  2
     A**=====================================================================
     A** FORMAT PM063300 **
     A**=====================================================================
     A          R PM6330S0                  SFL
     A            DDACTN         1A  B  9  9DSPATR(HI)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                      CHECK(ER)
     A            DDCNUM         6A  O  9 22TEXT('CUSTOMER NUMBER')
     A                                      DSPATR(HI)
     A            DDPTFR         4A  O  9 38TEXT('PORTFOLIO REFERENCE')
     A                                      DSPATR(HI)
     A            DDPTFN        20A  O  9 52TEXT('PORTFOLIO NARRATIVE')
     A                                      DSPATR(HI)
     A**=====================================================================
     A** FORMAT PM6330C0 **
     A**=====================================================================
     A          R PM6330C0                  SFLCTL(PM6330S0)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A                                      BLINK
     A  61                                  SFLDSP
     A  62                                  SFLDSPCTL
     A N61N62                               SFLCLR
     A  63                                  SFLEND(*MORE)
     A                                      CF12
     A N63                                  ROLLUP(21)
     A N64                                  ROLLDOWN(22)
     A*                                              Begin /CPM005/
     A***********                       1  3'CONSOLIDATED PORTFOLIO DEFINITION +
     A***********                            MAINTENANCE'
     A***********                           DSPATR(HI)
     A*********** WSIDN         10A  O  1 52TEXT('WORK STATION ID')
     A*********** DDRUNA         7A  O  1 63
     A***********                       1 72TIME
     A            USID          10A  O  1  2
     A                                  1 13'PM6330'
     A                                  1 22'CONSOLIDATED PORTFOLIO +
     A                                      DEFINITION MAINTENANCE'
     A                                      DSPATR(HI UL)
     A            BJMRDT         7A  O  1 72
     A            WSIDN         10A  O  2  2
     A                                  2 72TIME
     A*                                              End   /CPM005/
     A                                  3 36'Review from:Customer'
     A            DDPNP8         6A  B  3 57DSPATR(HI)
     A                                  3 65'Portfolio'
     A            DDPORT         4A  B  3 75DSPATR(HI)
     A                                  4  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                  6  7'Action'
     A                                  6 22'Customer'
     A                                  6 36'Portfolio'
     A                                  6 52'Portfolio  Narrative'
     A                                  7 22'Number'
     A                                  7 36'Reference'
     A**=====================================================================
     A** FORMAT PM6330F4 **
     A**=====================================================================
     A          R PM6330F4                  TEXT('CLEAR SCREEN')
     A                                  1  2' '
