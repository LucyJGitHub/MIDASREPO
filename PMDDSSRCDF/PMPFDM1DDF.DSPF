      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Portfolio charges details display')
      *****************************************************************
      *                                                               *
      *  Midas - Portfolio Management Module                          *
      *                                                               *
      *  DSPF/PMPFDM1DDF- Portfolio Charges Details Display           *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP033  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CAP033 - Conversion of PM inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      REF(MFRFL)
     A                                      INDARA
      *
     A          R PMPFDMC1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
      *
     A            DDCGCO    R        B 12  2TEXT('Charge Code')
     A                                      REFFLD(CGCO)
     A N44                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  44                                  DSPATR(PC RI)
      *
     A            DDCGSP        14A  B 12 06TEXT('Charge Spread')
     A N45                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  45                                  DSPATR(PC RI)
      *
     A            DDCGSI    R        B 12 21TEXT('Charge Spread Indicator')
     A                                      REFFLD(CGSI)
     A N46                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  46                                  DSPATR(PC RI)
      *
     A            DDCGEI    R        B 12 26TEXT('Effect')
     A                                      REFFLD(CGEI)
     A N47                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  47                                  DSPATR(PC RI)
      *
     A            DDCGFQ    R        B 12 32TEXT('Accrual Frequency')
     A                                      REFFLD(CGFQ)
     A N48                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  48                                  DSPATR(PC RI)
      *
     A            DDCGDY         2A  B 12 35TEXT('Accrual Day')
     A N49                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  49                                  DSPATR(PC RI)
      *
     A            DDCGND         6A  B 12 38TEXT('Accrual Next')
     A N50                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  50                                  DSPATR(PC RI)
      *
     A            DDCGST    R        B 12 49TEXT('Charge Stopped')
     A                                      REFFLD(CGST)
     A N51                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  51                                  DSPATR(PC RI)
      *
     A            DDCSFQ    R        B 12 54TEXT('Charge Frequency')
     A                                      REFFLD(CSFQ)
     A N52                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  52                                  DSPATR(PC RI)
      *
     A            DDCSDY         2A  B 12 57TEXT('Settle Day')
     A N53                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  53                                  DSPATR(PC RI)
      *
     A            DDCSND         6A  B 12 60TEXT('Settle Next')
     A N54                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  54                                  DSPATR(PC RI)
      *
     A            DDPRFC    R        B 12 73TEXT('Profit Centre')
     A                                      REFFLD(PCCU)
     A N55                                  DSPATR(HI)
     A N83                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A N83
     AO 70                                  DSPATR(PR)
     A  55                                  DSPATR(PC RI)
      *
     A            DDVALD         1   H
     A            DDCGCH         2   H
     A            DDCGSH        13  0H
     A            DDCGEH         1   H
     A            DDCGLH         5  0H
     A            DDCGDH         2   H
     A            DDCGNH         5  0H
     A            DDCGFH         1   H
     A            DDCSFH         1   H
     A            DDCSDH         2   H
     A            DDCSNH         5  0H
     A            DDCGTH         1   H
     A            DDPRFH         4   H
      *
     A          R PMPFDMC0                  TEXT('Charges Subfile control')
     A                                      CF03
     A                                      CF12
     A                                      CF01
     A                                      CF24
     A                                      SFLCTL(PMPFDMC1)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  95                                  SFLCLR
     A                                      SFLSIZ(10)
     A                                      SFLPAG(10)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      BLINK
      *
     A            DDSFRN         4  0H      SFLRCDNBR
      *
     A            PSUSER        10A  O  1  2TEXT('User Name')
     A                                  1 13'PMPFDMC'
     A                                  1 28'Portfolio Definition Maintenance'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  3  2'Action'
     A            DDACTN         1A  O  3 12TEXT('Action code')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A            DDDELT        16   O  3 62TEXT('Closed?')
     A                                      DSPATR(HI BL)
      *
     A                                  4 02'Customer'
     A            DDCSNM        10A  O  4 12TEXT('Customer Number')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  4 25'Portfolio'
     A            DDPREF         4A  O  4 36TEXT('Portfolio Reference')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
      *
     A**Start of change CGL029
     A**********                        6  4'Bank Charging Ccy'                               CGL029
     A**********  DDBCGY    R        B  6 23TEXT('Bank Currency')                             CGL029
     A                                  6  2'Bank Charging Ccy'
     A            DDBCGY    R        B  6 21TEXT('Bank Currency')
     A**End of change CGL029
     A                                      REFFLD(BCGY)
     A N40                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  40                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A**Start of change CGL029
     A**********                        7  4'Customer Settlement Ccy'                         CGL029
     A**********  DDCSCY    R        B  7 29TEXT('Cust Set Currency')                         CGL029
     A                                  7  2'Customer Settlement Ccy'
     A            DDCSCY    R        B  7 26TEXT('Cust Set Currency')
     A**End of change CGL029
     A                                      REFFLD(CSCY)
     A N41                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  41                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A**Start of change CGL029
     A**********                        7 33'Customer Settlement Account'                     CGL029
     A**********  DDCUSA    R        B  7 61TEXT('Account')                                   CGL029
     A**********                            REFFLD(CUSA)                                      CGL029
     A                                  7 30'Customer Settlement Account'
     A            DDCUSA        18A  B  7 58TEXT('Account')
     A**End of change CGL029
     A N42                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  42                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A**Start of change CGL029
     A**********  DDBRCA    R        B  7 74TEXT('Branch')                                    CGL029
     A            DDBRCA    R        B  7 77TEXT('Branch')
     A**End of change CGL029
     A                                      REFFLD(BRCA)
     A N43                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  43                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  9  2'Charge'
     A                                  9  9'Adjustment'
     A                                  9 24'Effect'
     A                                  9 32'Accrual date'
     A                                  9 46'Charge'
     A                                  9 53'Charge Settle date'
     A  83                              9 73'Profit'
     A                                 10  2'Code'
     A                                 10  8'Amount / %'
     A                                 10 20'+/-'
     A                                 10 25'C/N'
     A                                 10 31'Freq Day Next'
     A                                 10 46'Stopped'
     A                                 10 54'Freq  Day   Next'
     A  83                             10 73'Centre'
      *
     A          R PMPFDMS1                  TEXT('Message Subfile')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R PMPFDMS0                  TEXT('MSG Subfile Control')
     A                                      CF03
     A                                      CF12
     A                                      CF01
     A                                      CF24
     A                                      SFLCTL(PMPFDMS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
     A          R PMPFDMF1                  TEXT('Screen Footer')
     A                                      CF03
     A                                      CF12
     A                                      CF01
     A                                      CF24
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 23  1'F1=Portfolio Details'
     A                                 23 23'F3=Exit F12=Cancel'
     A                                 23 43'F24=Notepad'
      *
