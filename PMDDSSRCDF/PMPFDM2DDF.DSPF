      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Portfolio definition key fields display')
      *****************************************************************
      *                                                               *
      *  Midas - Portfolio Management Module                          *
      *                                                               *
      *  DSPF/PMPFDM2DDF- Portfolio Definition Key Fields Display     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Last Amend No. CAP033  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP033 - Conversion of PM inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      REF(MFRFL)
     A                                      INDARA
      *
     A          R PMPFDMI1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CF03
     A                                      CF07
     A                                      CF08
     A                                      CF12
     A                                      CF15
      *
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
      *
     A            PSUSER        10A  O  1  2TEXT('User Name')
     A                                  1 13'PMPFDMD'
     A                                  1 28'Portfolio Definition Maintenance'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  3  2'Action'
     A            DDACTN         1A  B  3 12TEXT('Action code')
     A N10                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  10                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  4 02'Customer'
     A            DDCSNM        10A  B  4 12TEXT('Customer Number')
     A N11                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  11                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  4 25'Portfolio'
     A            DDPREF    R        B  4 36TEXT('Portfolio Reference')
     A                                      REFFLD(PTFR)
     A N12                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  12                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  4 50'Review from'
     A            DDPNP8         6A  B  4 63TEXT('Review from Customer')
     A N13                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  13                                  DSPATR(PC RI)
     A                                      OVRDTA
     A            DDPORT    R        B  4 70TEXT('Review from Reference')
     A                                      REFFLD(PTFR)
     A N13                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A  13                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
      *
     A          R PMPFDMS1                  TEXT('Message Subfile')
     A                                      SFL
     A                                      SFLMSGRCD(23)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R PMPFDMS0                  TEXT('MSG Subfile Control')
     A                                      CF03
     A                                      CF07
     A                                      CF08
     A                                      CF12
     A                                      CF15
     A                                      SFLCTL(PMPFDMS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
     A          R PMPFDMF1                  TEXT('Screen Footer')
     A                                      CF03
     A                                      CF07
     A                                      CF08
     A                                      CF12
     A                                      CF15
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 24  2'F3=Exit'
     A                                 24 11'F7=Prev'
     A                                 24 20'F8=Next'
     A                                 24 29'F12=Cancel'
     A                                 24 41'F15=Select'
      *
