      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas PM Portfolio definition details display')
      *****************************************************************
      *                                                               *
      *  Midas - Portfolio Management Module                          *
      *                                                               *
      *  DSPF/PMPFDM3DDF- Portfolio Definition Details Display        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. HUT113             Date 01APR20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *                 198173             Date 24Jan02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CPB002             Date 25Oct00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CAP033  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  HUT113 - Full Private Banking Functionality in FM            *
      *           (Apply MD-54322)                                    *
      *           Addition of hook HUT113_042                         *
      *   MD054322 - Portfolio Management - SE - Incorrect holdings   *
      *              for custody fee calculation report.              *
      *              Allow portfolio definition for PB customer.      *
      *  MD046248 - Finastra Rebranding                               *
      *  198173 - Review CPB002 - For a PB customer, portfolio ccy,   *
      *           narrative and date opened/closed must be protected  *
      *  CPB002 - 'Private Banking' Module                            *
      *  CAP033 - Conversion of PM inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CHGINPDFT
     A                                      MSGLOC(22)
     A                                      REF(MFRFL)
     A                                      INDARA
      *
      ** Details screen
      *
     A          R PMPFDMD1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      CF03
     A                                      CF12
     A  71                                  CF01
     A  72                                  CF07
     A  73                                  CF08
     A  74                                  CF15
     A  75                                  CF24
      *
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
      *
      *
     A            PSUSER        10A  O  1  2TEXT('User Name')
     A                                  1 13'PMPFDMD'
     A                                  1 28'Portfolio Definition Maintenance'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  3  2'Action'
     A            DDACTN         1A  O  3 12TEXT('Action code')
     A N10                                  DSPATR(HI)
     A  10                                  DSPATR(RI)
     A  68
     AO 10                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            DDDELT        16   O  3 62TEXT('Closed?')
     A                                      DSPATR(HI BL)
     A                                      OVRDTA
      *
     A                                  4 02'Customer'
     A            DDCSNM        10A  O  4 12
     A N11                                  DSPATR(HI)
     A  11                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  4 25'Portfolio'
     A            DDPREF         4A  O  4 36TEXT('Portfolio Reference')
     A N12                                  DSPATR(HI)
     A  12                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
      *
     A**Start of change 198173
     A**********                        6  2'Portfolio Ccy'                                   198173
     A                                  6  2'PM Port. Ccy'
     A**End of change 198173
     A            DDPTCY    R        B  6 17TEXT('Portfolio Currency')
     A                                      REFFLD(PTCY)
     A N13                                  DSPATR(HI)
     A**Start of change 198173
     A******N70*                                 DSPATR(UL)                                   198173
     A N70N50                               DSPATR(UL)
     A*******70*                                 DSPATR(PR)                                   198173
     A  70
     AO 50                                  DSPATR(PR)
     A**End of change 198173
     A  13                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  6 24'Narrative'
     A            DDPTFN    R        B  6 35TEXT('Portfolio Narrative')
     A                                      REFFLD(PTFN)
     A*                                                         /*Start CPB002*/
     A**Start of change 198173
     A**********                            CHECK(LC)                                         198173
     A N51                                  CHECK(LC)
     A**End of change 198173
     A*                                                         /*End   CPB002*/
     A N14                                  DSPATR(HI)
     A**Start of change 198173
     A******N70*                                 DSPATR(UL)                                   198173
     A N70N51                               DSPATR(UL)
     A*******70*                            DSPATR(PR)                                        198173
     A  51
     AO 70                                  DSPATR(PR)
     A**End of change 198173
     A  14                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  6 60'Type'
     A            DDPTFT    R        B  6 66TEXT('Portfolio Type')
     A                                      REFFLD(PTFT)
     A N15                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  15                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A  81                              7 02'Custody Group Code'
     A            DDCFGC    R        B  7 22TEXT('CF Group Code')
     A                                      REFFLD(CFGC)
     A N16                                  DSPATR(HI)
     A N81                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A N81
     AO 70                                  DSPATR(PR)
     A  16                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  7 39'Managed Indicator (M/P/U)'
     A            DDMNGI    R        B  7 67TEXT('Managed Indicator')
     A                                      REFFLD(MNGI)
     A N17                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  17                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  8  2'Date Opened'
     A            DDDPOP         6A  O  8 15TEXT('Date Opened')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A                                  8 24'Date Closed'
     A            DDDPCL         6A  B  8 37TEXT('Date Closed')
     A N18                                  DSPATR(HI)
     A  76
     A**Start of change 198173
     A*****ON70*                                 DSPATR(UL)                                   198173
     AON70N50                               DSPATR(UL)
     A N76
     A*****A*70*                                 DSPATR(PR)                                   198173
     AA 70
     AON76
     AA 50                                  DSPATR(PR)
     A**End of change 198173
     A  18                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A  82                              8 45'FF Margin A/C Seq. :'
     A            DDACSQ         2A  B  8 66TEXT('FF Margin A/C seq.')
     A N19                                  DSPATR(HI)
     A N82                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A N82
     AO 70                                  DSPATR(PR)
     A  19                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  9  2'Pledge Start Date'
     A            DDPDGS         6A  B  9 21TEXT('Pledge Start Date')
     A N20                                  DSPATR(HI)
     A N70N84                               DSPATR(UL)
     A  84
     AO 70                                  DSPATR(PR)
     A  20                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  9 29'Pledge Finish Date'
     A            DDPDGF         6A  B  9 49TEXT('Pledge Finish Date')
     A N21                                  DSPATR(HI)
     A N70N84                               DSPATR(UL)
     A  84
     AO 70                                  DSPATR(PR)
     A  21                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A  83                              9 57'Profit Centre'
     A            DDPRF2    R        B  9 71TEXT('Profit Centre')
     A                                      REFFLD(PRFC)
     A N22                                  DSPATR(HI)
     A N83                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A N83
     AO 70                                  DSPATR(PR)
     A  22                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 10  2'Valuation Frequency'
     A            DDVLFQ    R        B 10 23TEXT('Valuation Frequency')
     A                                      REFFLD(VLFQ)
     A N23                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  23                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 10 27'Day in Month'
     A            DDVLDM         2A  B 10 41TEXT('Day in Month')
     A N24                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  24                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 10 46'Next Valuation Date'
     A            DDVLND         6A  B 10 67TEXT('Next Valuation Date')
     A N25                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  25                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 11  5'Select (Y/N) Sections of +
     A                                      Valuation Statement to be Printed:'
      *
     A                                 12  5'Instr.Summary'
     A            DDS9VR    R        B 12 19TEXT('Instrument Summary Section')
     A                                      REFFLD(S11VR)
     A N26                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  26                                  DSPATR(PC RI)
     A                                      OVRDTA
     A                                 12 21'Curr.Sum.'
     A            DDS0VR    R        B 12 31TEXT('Currency Summary Section')
     A                                      REFFLD(S11VR)
     A N27                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  27                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 12 34'Position Details'
     A            DDS1VR    R        B 12 51TEXT('Resume')
     A                                      REFFLD(S11VR)
     A N28                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  28                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 12 54'Sector Breakdown'
     A            DDS2VR    R        B 12 72TEXT('Sector Breakdown')
     A                                      REFFLD(S12VR)
     A N29                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  29                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 13  5'Performance For Past Year'
     A            DDS3VR    R        B 13 31TEXT('Performance for Past Year')
     A                                      REFFLD(S13VR)
     A N30                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  30                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 13 34'Tax Year To Date'
     A            DDS4VR    R        B 13 51TEXT('Tax Year to Date')
     A                                      REFFLD(S14VR)
     A N31                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  31                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 13 54'Additional Period'
     A            DDS5VR    R        B 13 72TEXT('Monthly Cashflows')
     A                                      REFFLD(S15VR)
     A N32                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  32                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 14 05'Daily Cashflows'
     A            DDS6VR    R        B 14 31TEXT('Monthly Cashflows')
     A                                      REFFLD(S16VR)
     A N33                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  33                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 14 34'Monthly'
     A            DDS7VR    R        B 14 51TEXT('Monthly Cashflows')
     A                                      REFFLD(S17VR)
     A N34                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  34                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 14 54'Yearly'
     A            DDS8VR    R        B 14 72TEXT('Yearly Cashflows')
     A                                      REFFLD(S18VR)
     A N35                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  35                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 15  2'Performance Periods:'
     A                                 15 23'Report Past Year (Y/N)'
     A            DDRPYP    R        B 15 46TEXT('Report Past Year Performance')
     A                                      REFFLD(RPYP)
     A N36                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  36                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 15 49'Tax Year Start Date'
     A            DDTYSM         2A  B 15 69TEXT('Cust. Tax Year Start Month')
     A N37                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  37                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 16 14'Additional Period (H/Q)'
     A            DDADPP    R        B 16 38TEXT('Additional Perform. Period')
     A                                      REFFLD(ADPP)
     A N38                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  38                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 16 42'Performance Start Date'
     A            DDPSDT         6A  B 16 69TEXT('Performance Extracted From')
     A N39                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  39                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 17 23'Performance Net/Gross of Tax (N/G)'
     A            DDPMNG    R        B 17 60TEXT('Net or Gross of Tax')
     A                                      REFFLD(PMNG)
     A N40                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  40                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 18  2'Cashflow Report Frequency'
     A            DDCASF    R        B 18 29TEXT('Cashflow frequency')
     A                                      REFFLD(CASF)
     A N41                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  41                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 18 33'Day in Month'
     A            DDCASD         2A  B 18 47TEXT('Day in Month')
     A N42                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  42                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 18 52'Next Report Date'
     A            DDCASN         6A  B 18 70TEXT('Next Report Date')
     A N43                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  43                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A N80                             19  9'Enter Bank Investment'
     A  80                             19  2'Either Enter Bank Investment'
     A                                 19 31'Policy Code'
     A            DDPOLC    R        B 19 44TEXT('Bank Investment Policy Code')
     A                                      REFFLD(POLC)
     A N44                                  DSPATR(HI)
     A N70                                  DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  44                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A  80                             19 48'Or Define Portfolio :'
     A  80                             20  2'Instrument Investment'
     A            DDIPIN    R        B 20 33TEXT('Instrument Inv. Policy')
     A                                      REFFLD(IPIN)
     A N45                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A N80
     AO 70                                  DSPATR(PR)
     A  45                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A  80                             20 40'Currency Investment'
     A  80                             20 60'Policy (Y/N/A)'
     A            DDCYIN    R        B 20 75TEXT('Currnecy Investment Pol.')
     A                                      REFFLD(CYIN)
     A N46                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A N80
     AO 70                                  DSPATR(PR)
     A  46                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A  80                             21  2'Country of Risk'
     A  80                             21 18'Policy (Y/N/A)'
     A            DDYPIN    R        B 21 33TEXT('Country of Risk Inv. Pol.')
     A                                      REFFLD(YPIN)
     A N47                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A N80
     AO 70                                  DSPATR(PR)
     A  47                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A  80                             21 40'Industry Investment'
     A  80                             21 60'Policy (Y/N/A)'
     A            DDNPIN    R        B 21 75TEXT('Industry Investment Pol.')
     A                                      REFFLD(NPIN)
     A N48                                  DSPATR(HI)
     A N80                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A N80
     AO 70                                  DSPATR(PR)
     A  48                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A            DDIPIH         1   H
     A            DDCYIH         1   H
     A            DDYPIH         1   H
     A            DDNPIH         1   H
     A*                                                         /*Start CPB002*/
     A            DDMGTP         3A  H      TEXT('Management Type')
     A            DDINTP         3A  H      TEXT('Investment Profile Code')
     A*                                                         /*End   CPB002*/
      *
      ** 2nd Details screen
      *
     A          R PMPFDMD2                  TEXT('Record format used when +
     A                                      customer is not a PM Customer')
      *
     A                                      CF03
     A                                      CF12
     A  72                                  CF07
     A  73                                  CF08
     A  74                                  CF15
      *
     A                                      FRCDTA
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
      *
      *
     A            PSUSER        10A  O  1  2TEXT('User Name')
     A                                  1 13'PMPFDMD'
     A                                  1 28'Portfolio Definition Maintenance'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  3  2'Action'
     A            DDACTN         1A  O  3 12TEXT('Action code')
     A N10                                  DSPATR(HI)
     A  10                                  DSPATR(RI)
     A  68
     AO 10                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A            DDDELT        16   O  3 62TEXT('Closed?')
     A                                      DSPATR(HI BL)
     A                                      OVRDTA
      *
     A                                  4 02'Customer'
     A            DDCSNM        10A  O  4 12
     A N11                                  DSPATR(HI)
     A  11                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  4 25'Portfolio'
     A            DDPREF         4A  O  4 36TEXT('Portfolio Reference')
     A N12                                  DSPATR(HI)
     A  12                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                  5  2'                                  -
     A                                                                         -
     A                                                '
     A                                      DSPATR(UL)
      *
     A**Start of change 198173
     A**********                        7  5'Portfolio Ccy'                                   198173
     A**********  DDPTCY    R        B  7 25TEXT('Portfolio Currency')                        198173
     A                                  7  5'PM Port. Ccy'
     A            DDPTCY    R        O  7 25TEXT('Portfolio Currency')
     A**End of change 198173
     A                                      REFFLD(PTCY)
     A***Start of change 198173
     A******N13*                                 DSPATR(HI)                                   198173
     A******N70*                                 DSPATR(UL)                                   198173
     A*******70*                                 DSPATR(PR)                                   198173
     A*******13*                                 DSPATR(PC RI)                                198173
     A                                      DSPATR(HI)
     A**End of change 198173
     A                                      OVRDTA
      *
     A                                  7 40'Narrative'
     A**Start of change 198173
     A**********  DDPTFN    R        B  7 60TEXT('Portfolio Narrative')                       198173
     A            DDPTFN    R        O  7 60TEXT('Portfolio Narrative')
     A**End of change 198173
     A                                      REFFLD(PTFN)
     A*                                                         /*Start CPB002*/
     A**Start of change 198173
     A**********                            CHECK(LC)                                         198173
     A*                                                         /*End   CPB002*/
     A******N14*                                 DSPATR(HI)                                   198173
     A******N70*                                 DSPATR(UL)                                   198173
     A*******70*                            DSPATR(PR)                                        198173
     A*******14*                                 DSPATR(PC RI)                                198173
     A                                      DSPATR(HI)
     A**End of change 198173
     A                                      OVRDTA
      *
     A                                  9  5'Date Opened'
     A            DDDPOP         6A  O  9 25TEXT('Date Opened')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A  81                              9 40'Custody Group Code'
     A            DDCFGC    R        B  9 60TEXT('CF Group Code')
     A                                      REFFLD(CFGC)
     A N16                                  DSPATR(HI)
     A N81                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A N81
     AO 70                                  DSPATR(PR)
     A  16                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A                                 11  5'Date Closed'
     A**Start of change 198173
     A**********  DDDPCL         6A  B 11 25TEXT('Date Closed')
     A            DDDPCL         6A  O 11 25TEXT('Date Closed')
     A******N18*                                 DSPATR(HI)                                   198173
     A*******76*                                                                              198173
     A*****ON70*                                 DSPATR(UL)                                   198173
     A******N76*                                                                              198173
     A*****A*70*                                 DSPATR(PR)                                   198173
     A*******18*                                 DSPATR(PC RI)                                198173
     A**End of change 198173
     A                                      OVRDTA
      *
     A  82                             11 40'FF Margin A/C Seq.'
     A            DDACSQ         2A  B 11 60TEXT('FF Margin A/C seq.')
     A N19                                  DSPATR(HI)
     A N82                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A  70
     AON82                                  DSPATR(PR)
     A  19                                  DSPATR(PC RI)
     A                                      OVRDTA
      *
     A  83                             13  5'Profit Centre'
     A            DDPRF2    R        B 13 25TEXT('Profit Centre')
     A                                      REFFLD(PRFC)
     A N22                                  DSPATR(HI)
     A N83                                  DSPATR(ND)
     A N70                                  DSPATR(UL)
     A N83
     AO 70                                  DSPATR(PR)
     A  22                                  DSPATR(PC RI)
     A                                      OVRDTA
     A*                                                         /*Start CPB002*/
     A            DDMGTP         3A  H      TEXT('Management Type')
     A            DDINTP         3A  H      TEXT('Investment Profile Code')
     A*                                                         /*End   CPB002*/
      */COPY WNCPYSRC,HUT113_042                                                         HUT113
      *
     A          R PMPFDMS1                  TEXT('Message Subfile')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
     A          R PMPFDMS0                  TEXT('MSG Subfile Control')
     A                                      CF03
     A                                      CF12
     A  71                                  CF01
     A  72                                  CF07
     A  73                                  CF08
     A  74                                  CF15
     A  75                                  CF24
      *
     A                                      SFLCTL(PMPFDMS1)
     A N96                                  SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  97                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
     A          R PMPFDMF1                  TEXT('Screen Footer')
     A                                      CF03
     A                                      CF12
     A  71                                  CF01
     A  72                                  CF07
     A  73                                  CF08
     A  74                                  CF15
     A  75                                  CF24
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                 23  2'F1=Portfolio Charges'
     A N71                                  DSPATR(ND)
     A                                      OVRATR
      *
     A                                 23 24'F7=Prev'
     A N71
     AON72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 23 33'F8=Next'
     A N71
     AON73                                  DSPATR(ND)
     A                                      OVRATR
      *
     A                                 23 42'F24=Notepad'
     A N75                                  DSPATR(ND)
     A                                      OVRATR
      *
     A                                 24  2'F3=Exit  F12=Cancel'
      *
     A                                 24 23'F15=Select'
     A N74                                  DSPATR(ND)
     A                                      OVRATR
      *
     A                                 24 64'F7=Prev'
     A  71
     AON72                                  DSPATR(ND)
     A                                      OVRATR
     A                                 24 73'F8=Next'
     A  71
     AON73                                  DSPATR(ND)
     A                                      OVRATR
      *
