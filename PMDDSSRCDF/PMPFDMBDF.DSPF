      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas PM Portfolio definitions browse')
      *****************************************************************
      *                                                               *
      *  Midas - Portfolio Management Module                          *
      *                                                               *
      *  DSPF/PMPFDMBDF - Portfolio Definitions Browse                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CPB002             Date 25Oct00               *
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. CAP033  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPB002 - 'Private Banking' Module                            *
      *  CAP033 - Conversion of PM inputs into modular structure to   *
      *           use as APIs.                                        *
      *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
     A          R PMPFDMCH                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            PSUSER        10A  O  1  2TEXT('User Name')
     A                                  1 13'PMPFDMBRW'
     A                                  1 24'Portfolio Definition Maintenance'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                  2 34'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
      *
     A          R PMPFDMS0                  TEXT('Select Screen Subfile - BO')
     A                                      SFL
     A  93                                  SFLNXTCHG
     A            DDOPTN         1A  B 09  4TEXT('Action code')
     A                                      DSPATR(UL)
     A            DDSTAT         2A  O 09 11TEXT('Status')
     A                                      DSPATR(BL HI)
     A            DDCNUM         6A  O 09 17TEXT('Customer Number')
     A                                      DSPATR(HI)
     A            DDPTFR    R        O 09 28TEXT('Portfolio Reference')
     A                                      REFFLD(PTFR)
     A                                      DSPATR(HI)
     A            DDPCCY    R        O 09 40TEXT('Portfolio Currency')
     A                                      REFFLD(PTCY)
     A                                      DSPATR(HI)
     A            DDPTFN    R        O 09 48TEXT('Portfolio Narrative')
     A                                      REFFLD(PTFN)
     A                                      DSPATR(HI)
     A            DDDATE         7A  O 09 72TEXT('Portfolio Date Closed')
     A                                      DSPATR(HI)
     A*                                                         /*Start CPB002*/
     A            DDMGTP         3A  H      TEXT('Management Type')
     A            DDINTP         3A  H      TEXT('Investment Profile Code')
     A*                                                         /*End   CPB002*/
      *
     A          R PMPFDMC0                  TEXT('Select Screen Control')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(PMPFDMS0)
     A N92                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  97                                  SFLCLR
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A  96                                  SFLEND
     A N96                                  ROLLUP(98)
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
      *
     A            DDSFRN         4  0H      SFLRCDNBR
     A            PSUSER        10A  O  1  2TEXT('User Name')
     A                                  1 13'PMPFDMBRW'
     A                                  1 24'Portfolio Definition Maintenance'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                  2 34'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  4 50'Review from'
     A            DDPNP8         6A  B  4 63TEXT('Review from Customer')
     A N10                                  DSPATR(HI UL)
     A  10                                  DSPATR(PC RI)
     A                                      OVRDTA
     A            DDPORT         4A  B  4 70TEXT('Review from Reference')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
      *
     A                                  6  2'Action'
     A                                  6 09'Status'
     A                                  6 16'Customer'
     A                                  6 26'Portfolio'
     A                                  6 37'Portfolio'
     A                                  6 48'Portfolio Narrative'
     A                                  6 70'Date Closed'
      *
     A                                  7  3'Code'
     A                                  7 17'Number'
     A                                  7 26'Reference'
     A                                  7 37'Currency'
      *
     A          R PMPFDMS1                  TEXT('Select Screen Subfile - FO')
     A                                      SFL
     A            DDOPTN         1A  B 09  4TEXT('Action code')
     A                                      DSPATR(UL)
     A            DDFRNT    R     A    09  9TEXT('Front OFC Transaction ID')
     A                                      REFFLD(FOTRNID)
     A                                      DSPATR(HI)
     A            DDCNUM         6A  O 09 32TEXT('Customer Number')
     A                                      DSPATR(HI)
     A            DDPTFR    R        O 09 43TEXT('Portfolio Reference')
     A                                      REFFLD(PTFR)
     A                                      DSPATR(HI)
     A            DDPCCY    R        O 09 53TEXT('Portfolio Currency')
     A                                      REFFLD(PTCY)
     A                                      DSPATR(HI)
     A            DDPTFN    R        O 09 60TEXT('Portfolio Narrative')
     A                                      REFFLD(PTFN)
     A                                      DSPATR(HI)
     A*                                                         /*Start CPB002*/
     A            DDMGTP         3A  H      TEXT('Management Type')
     A            DDINTP         3A  H      TEXT('Investment Profile Code')
     A*                                                         /*End   CPB002*/
      *
     A          R PMPFDMC1                  TEXT('Select Screen Control')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      SFLCTL(PMPFDMS1)
     A N92                                  SFLDSP
     A N97                                  SFLDSPCTL
     A  97                                  SFLCLR
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A  96                                  SFLEND
     A N96                                  ROLLUP(98)
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
      *
     A            DDSFRN         4  0H      SFLRCDNBR
     A            PSUSER        10A  O  1  2TEXT('User Name')
     A                                  1 13'PMPFDMBRW'
     A                                  1 24'Portfolio Definition Maintenance'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                  2 34'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  5  2'                                  -
     A                                                                         -
     A                                              '
     A                                      DSPATR(UL)
     A                                  6  2'Action'
     A                                  6  9'Front Office ID'
     A                                  6 31'Customer'
     A                                  6 40'Portfolio'
     A                                  6 50'Portfolio'
     A                                  6 60'Portfolio Narrative'
      *
     A                                  7  3'Code'
     A                                  7 32'Number'
     A                                  7 40'Reference'
     A                                  7 50'Currency'
      *
     A          R PMPFDMF1                  TEXT('Select Screen Footer')
     A                                      CF03
     A                                      CF11
     A                                      CF12
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            ERRORMSG      75     22  2TEXT('Error message')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A
      *
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit'
     A                                 24 11'F11=Toggle Front Office ID'
     A                                 24 39'F12=Cancel'
      *
