      *****************************************************************
/*STD *  DSPFDFRWN                                                    *
/*EXI *  TEXT('Midas PM Invalid portfolio definitions browse')
      *****************************************************************
      *                                                               *
      *  Midas - Portfolio Management Module                          *
      *                                                               *
      *  DSPF/PMPFDMRDF - Invalid Portfolio Definitions Browse        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *  Last Amend No. CPB002             Date 25Oct00               *
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Prev Amend No. 176287             Date 10Mar00               *
      *                 CAP033  *CREATE    Date 26Apr99               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CPB002 - 'Private Banking' Module                            *
      *  176287 - Timestamp field not compatible with release level.  *
      *  CAP033 - Conversion of PM inputs into modular structure to   *
      *           use as APIs.                                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
      ** Select Screen subfile
      *
     A          R PMIPFDMS0                 TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SFL
      *
     A            DDOPTN         1   B 09  2TEXT('Select Option')
     A                                      DSPATR(UL)
     A            DDACTN    R        O 09  6TEXT('Action code')
     A                                      REFFLD(ACTO)
     A                                      DSPATR(HI)
     A            DDFRNT    R        O 09 10TEXT('Front Office Transaction ID')
     A                                      REFFLD(FOTRNID)
     A                                      DSPATR(HI)
     A            DDCSNM        10A  O 09 34TEXT('Customer Number')
     A                                      DSPATR(HI)
     A            DDPREF         4A  O 09 48TEXT('Portfolio Reference')
     A                                      DSPATR(HI)
     A            DDPTCY    R        O 09 54TEXT('Portfolio Currency')
     A                                      REFFLD(PTCY)
     A                                      DSPATR(HI)
     A            DDPTFN    R        O 09 60TEXT('Portfolio Narrative')
     A                                      REFFLD(PTFN)
     A                                      DSPATR(HI)
      *
     A            DDDPCL         6A  H      TEXT('Date Closed')
     A            DDPTFT    R        H      TEXT('Portfolio Type')
     A                                      REFFLD(PTFT)
     A            DDMNGI    R        H      TEXT('Managed Indicator')
     A                                      REFFLD(MNGI)
     A            DDPRF2    R        H      TEXT('Profit Centre')
     A                                      REFFLD(PRFC)
     A            DDCFGC    R        H      TEXT('CF Group Code')
     A                                      REFFLD(CFGC)
     A            DDACSQ         2A  H      TEXT('FF Margin A/C seq.')
     A            DDAFRNT   R        H      TEXT('Associated Front Office +
     A                                      Transaction ID')
     A                                      REFFLD(FOTRNID)
      *                                     Start of change /176287/
     A************DDTMESTMP R        H      TEXT('Time Stamp')
     A            D@TMESTMP R   26A  H      TEXT('Time Stamp')
      *                                       End of change /176287/
     A                                      REFFLD(TIMESTMP)
     A*                                                         /*Start CPB002*/
     A            DDMGTP         3A  H      TEXT('Management Type')
     A            DDINTP         3A  H      TEXT('Investment Profile Code')
     A*                                                         /*End   CPB002*/
      *
     A          R PMIPFDMC0                 TEXT('Select Screen Control')
     A                                      CF03
      *
     A                                      SFLCTL(PMIPFDMS0)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A  96                                  SFLEND
     A  97                                  SFLCLR
     A                                      SFLSIZ(13)
     A                                      SFLPAG(12)
     A N96                                  ROLLUP(98)
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            DDSFRN         4  0H      SFLRCDNBR
     A            PSUSER        10A  O  1  2TEXT('User Name')
     A                                  1 13'PMPFDMRPB'
     A                                  1 24'Invalid Portfolio Definition +
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                  2 41'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  6  1'Sel'
     A                                  6  5'Act'
     A                                  6 10'Front Office ID'
     A                                  6 34'Customer'
     A                                  6 48'Portfolio'
     A                                  6 60'Portfolio  Narrative'
      *
     A                                  7 48'Ref.'
     A                                  7 54'Ccy'
      *
     A          R PMIPFDMF1                 TEXT('Select Screen Footer')
     A                                      CF03
      *
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A                                 23  2'Rollup/Rolldown for further record+
     A                                      s'
     A                                 24  2'F3=Exit'
      *
     A            ERRORMSG      56     24 24TEXT('Error message')
     A                                      OVRDTA
     A                                      DSPATR(HI)
      *
     A          R PMIPFDMEND                TEXT('End Message')
     A                                      CF03
      *
     A                                      BLINK
     A                                      OVERLAY
     A                                      PUTOVR
      *
     A            PSUSER        10A  O  1  2TEXT('User Name')
     A                                  1 13'PMPFDMRPB'
     A                                  1 24'Invalid Portfolio Definition +
     A                                      Maintenance'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A            BJMRDT         7A  O  1 73TEXT('Midas rundate')
     A            PSJOBNAME     10A  O  2  2TEXT('Workstation ID')
     A                                  2 41'Browse'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A            DDTIME         6Y 0O  2 72TEXT('Current Time')
     A                                      EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A N10                             11 26'NO INVALID DEFINITIONS +
     A                                      PRESENT'
     A N10                                  DSPATR(HI)
     A  10                             11 26'USER IS NOT AUTHORISED TO +
     A                                      BROWSE'
     A  10                                  DSPATR(HI)
     A                                 23  2'Press enter to end this function'
      *
