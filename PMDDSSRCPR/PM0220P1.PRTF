      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas PM Valuation Groups List')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  PRTF/PM0220P1 - PRINT A VALUATION GROUPS LIST                *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01486             Date 11Aug94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01486 - Portfolio Management Upgrade to R10.                *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*           C R E A T I O N     P A R A M E T E R S             *
     A*
     A*****************************************************************
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DOCUMENT HEADER
     A**---------------------------------------------------------------
     A*
     A***********                                    Begin /S01486/
     A**********R HEADLI
     A          R HEADLI                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A***********                                    End   /S01486/
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'PM0220P1'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    43'PORTFOLIO MANAGEMENT VALUATION '
     A                                    74'GROUPS - '
     A  41                                83'AUDIT'
     A                                      SPACEA(1)
     A  42                                83'LIST'
     A                                      SPACEA(1)
     A                                    43'-------------------------------'
     A                                    74'------'
     A                                      SPACEA(1)
     A                                   107'L A S T  A C T I V I T Y'
     A                                      SPACEA(1)
     A                                     2'VALUATION GROUP CODE'
     A                                    31'TITLE'
     A                                    48'SEQUENCE'
     A  43                                64'LANGUAGE'
     A  43                                83'NARRATIVE'
     A                                   107'ACTION    DATE    TIME'
     A                                      SPACEA(1)
     A                                     2'--------------------'
     A                                    31'-----'
     A                                    48'--------'
     A  43                                64'--------'
     A  43                                83'---------'
     A                                   107'------------------------'
     A                                      SPACEA(1)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR VALUATION GROUPS - HEADER DETAILS
     A**---------------------------------------------------------------
     A*
     A          R LIST
     A  44        RRVALG         3         8
     A  44        RRVLGT        20        25
     A  44        RRVLGS         2  0     51
     A  44        RRCHTP         6       107
     A  44        RRDLUP         2  0    115
     A  44        RRMLUP         3       117
     A  44        RRYLUP         2  0    120
     A  44        RRTLUP         6  0    123EDTWRD('  :  :  ')
     A N45                                  SPACEA(1)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR VALUATION GROUPS - LANGUAGE DETAILS
     A**---------------------------------------------------------------
     A*
     A          R LANG
     A  43        RRVLGI         2        67
     A  43        RRVLGN        20        79
     A  45                                  SPACEA(1)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR END OF REPORT
     A**---------------------------------------------------------------
     A*
     A          R ENDLI
     A                                      SPACEB(3)
     A                                    56'** END OF REPORT **'
