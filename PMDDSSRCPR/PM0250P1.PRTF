      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas PM Instrument Types Groups List')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  PRTF/PM0250P1 - PRINT A CURRENCY GROUPS LIST                 *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01486             DATE 11AUG94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01486 - Portfolio Management Upgrade to R10.                *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*           C R E A T I O N     P A R A M E T E R S             *
     A*
     A*****************************************************************
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DOCUMENT HEADER
     A**---------------------------------------------------------------
     A*
     A***********                                    Begin /S01486/
     A**********R HEADLI
     A          R HEADLI                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A***********                                    End   /S01486/
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'PM0250P1'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    43'PORTFOLIO MANAGEMENT CURRENCY '
     A                                    73'GROUPS - '
     A  41                                82'AUDIT'
     A                                      SPACEA(1)
     A  42                                82'LIST'
     A                                      SPACEA(1)
     A                                    43'------------------------------'
     A                                    73'------'
     A                                      SPACEA(1)
     A                                     6'CURRENCY'
     A                                   107'L A S T  A C T I V I T Y'
     A                                      SPACEA(1)
     A                                     5'GROUP CODE'
     A                                    31'NARRATIVE'
     A                                    53'CURRENCY CODE'
     A                                    73'CURRENCY NAME'
     A  44                               107'ACTION    DATE    TIME'
     A                                      SPACEA(1)
     A  46                               110'ACTION        DATE'
     A                                      SPACEA(1)
     A                                     5'----------'
     A                                    31'---------'
     A                                    53'-------------'
     A                                    73'-------------'
     A                                   107'------------------------'
     A                                      SPACEA(1)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR CURRENCY GROUPS - HEADER DETAILS/MODE '1'
     A**---------------------------------------------------------------
     A*
     A          R LISTA
     A
     A  44        RRCCYG         2         9SPACEB(1)
     A  44        RRCCGN        20        26
     A  44        RRCHTP         6       107
     A  44        RRDLUP         2  0    115
     A  44        RRMLUP         3       117
     A  44        RRYLUP         2  0    120
     A  44        RRTLUP         6  0    123EDTWRD('  :  :  ')
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR CURRENCY GROUPS - HEADER DETAILS/MODE '2'
     A**---------------------------------------------------------------
     A*
     A          R LISTB
     A  46        RRCCYG         2         9SPACEB(1)
     A  46        RRCCGN        20        26
     A  46        RRCHTP         6       110
     A  46        RRDLUP         2  0    122
     A  46        RRMLUP         3       124
     A  46        RRYLUP         2  0    127
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR CURRENCY GROUPS - CURRENCY GROUPS DETAILS
     A**---------------------------------------------------------------
     A*
     A          R CURR
     A            RRCCY          3        58
     A            RRCYNM        14        73
     A                                      SPACEA(1)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR NO CURRENCIES TYPES ASSIGNED TO GROUP
     A**---------------------------------------------------------------
     A*
     A          R NOCCY
     A                                    58'*** NO CURRENCIES IN GROUP ***'
     A                                      SPACEA(1)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR END OF REPORT
     A**---------------------------------------------------------------
     A*
     A          R ENDLI
     A                                      SPACEB(3)
     A                                    56'** END OF REPORT **'
