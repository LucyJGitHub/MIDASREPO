      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas PM Portfolio Transfer List')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  PRTF/PM0340P1 - PORTFOLIO TRANSFER REPORT                    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. S01486             Date 11Aug94               *
     A*                 58162              Date 01Jul90               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  S01486 - Portfolio Management Upgrade to R10.                *
     A*  58162  - INCORRECT AMOUNT2 OUTPUT                            *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*           C R E A T I O N     P A R A M E T E R S             *
     A*
     A*****************************************************************
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR REPORT HEADER - FIRST & SECOND PAGE
     A**---------------------------------------------------------------
     A*
     A***********                                    Begin /S01486/
     A**********R HEADER
     A          R HEADER                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A***********                                    End   /S01486/
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'PM0340P1'
     A            RRTITL        53        37
     A                                   101'DATE'
     A            RRRUNA         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    43'PORTFOLIO TRANSFERS EFFECTIVE +
     A                                       TODAY REPORT'
     A                                      SPACEA(1)
     A                                    43'------------------------------+
     A                                       ------------'
     A                                      SPACEA(2)
     A          R HEADR1
     A                                     1'ACCOUNT OFFICER'
     A                                      HIGHLIGHT
     A            RRACOC         2        19
     A                                      HIGHLIGHT
     A            RRACON        30        22
     A                                      HIGHLIGHT
     A**
     A          R HEADR2
     A                                     3'CUSTOMER'
     A                                      HIGHLIGHT
     A                                      SPACEB(2)
      * Begin /CSD027/
     A**********  RRCNUM         6  0     16                                                  CSD027
     A            RRCNUM         6A       16
      * End /CSD027/
     A                                      HIGHLIGHT
     A            RRCRNM        30        23
     A                                      HIGHLIGHT
     A**
     A          R HEADR3
     A                                    14'TRANSFERRED'
     A                                      SPACEB(2)
     A                                    31'MODULE'
     A                                    98'TRANSACTION'
     A*
     A                                    13'FROM       TO'
     A                                      SPACEB(1)
     A                                    33'ID'
     A                                    42'NAME & DESCRIPTION'
     A                                    73'NOMINAL AMOUNT'
     A                                    97'/ SECURITY REF'
     A*
     A                                    13'-------------'
     A                                      SPACEB(1)
     A                                    31'------'
     A                                    42'------------------'
     A                                    73'--------------'
     A                                    97'--------------'
     A** *******************
     A** DETAIL     FORMATS*
     A** *******************
     A**
     A          R RRDET1
     A**
     A            RRFPTF         4        13TEXT('FROM PORTFOLIO')
     A                                      SPACEB(2)
     A**
     A            RRTPTF         4        22TEXT('TO PORTFOLIO')
     A**
     A            RRMODI         2        33TEXT('MODULE ID')
     A**
     A            RRDES1        20        42TEXT('NAME & DESCRIPTION')
     A**
     A            RRCCY1         3        67TEXT('CURRENCY')
     A            RRAMO1        22        71TEXT('AMOUNT')
     A            RRIND1         2        94TEXT('INDICATOR')
     A**
     A N65        RRTRNB         6       101TEXT('TRANSACTION NUMBER')
     A  65        RRSESN        10        99TEXT('SECURITY REFERENCE')
     A**
     A  66                               111'*** DUE TO CLOSURE ***'
     A  67                               111'***  NOT TO BE  ***'
     A**
     A** SECOND DETAIL FORMAT
     A**
     A          R RRDET2
     A**
     A            RRPART         4        32
     A                                      SPACEB(1)
     A**
     A            RRDES2        20        42TEXT('NAME AND DESCRIPTION')
     A**
     A            RRCCY2         3        67TEXT('CURRENCY')
     A***START 58162***
     A****        RRAMO2        16        71TEXT('AMOUNT')
     A            RRAMO2        22        71TEXT('AMOUNT')
     A***END 58162***
     A            RRIND2         2        94TEXT('INDICATOR')
     A  65        RRTRN2         6       101TEXT('TRANSACTION NUMBER')
     A**
     A  67                               111'***  PROCESSED   **'
     A**
     A** **********************
     A** * NO DETAILS FORMAT  *
     A** **********************
     A          R RRNDET
     A                                     1'ACCOUNT OFFICER'
     A                                      HIGHLIGHT
     A                                    19'*ALL'
     A                                      HIGHLIGHT
     A                                    26'ALL ACCOUNT OFFICERS'
     A                                      HIGHLIGHT
     A                                      SPACEA(2)
     A**
     A
     A                                     3'CUSTOMER'
     A                                      HIGHLIGHT
     A                                    16'*ALL'
     A                                      HIGHLIGHT
     A                                    25'ALL CUSTOMERS'
     A                                      HIGHLIGHT
     A                                      SPACEA(3)
     A**
     A                                    52'** NO DETAILS TO REPORT **'
     A** **********************
     A** *END OF REPORT FORMAT*
     A** **********************
     A**
     A          R ENDRE
     A                                    56'** END OF REPORT **'
     A                                      SPACEB(3)
     A**
