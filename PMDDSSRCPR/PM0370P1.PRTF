      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas PM Portfolio Charges Accrued Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                          *
     A*                                                               *
     A*  PRTF/PM0370P1 - MANAGEMENT FEES ACCRUED / SETTLED TODAY      *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 085811             Date 13Mar95               *
      *                 S01486             Date 11Aug94               *
     A*                 B07866             Date 25May91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  085811 - No need on underline anymore                        *
     A*  S01486 - Portfolio Management Upgrade to R10.                *
     A*  B07866 - FILE REWRITTEN FOR CHANGES TO CHARGING PROCESSING   *
     A*                                                               *
     A*****************************************************************
     A*           C R E A T I O N     P A R A M E T E R S             *
     A*
     A*****************************************************************
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DOCUMENT HEADER
     A**---------------------------------------------------------------
     A*
     A***********                                    Begin /S01486/
     A**********R PM0370F2
     A          R PM0370F2                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A***********                                    End   /S01486/
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'PM0370P1'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    29'MANAGEMENT FEES ACCRUED / SETTLED'
     A                                    62' ON PORTFOLIOS TODAY'
     A*                                              BEGIN /S01486/
     A************                       101'LOCATION - '
     A************RRPRTL         3       112
     A*                                              END   /S01486/
     A                                      SPACEA(1)
     A                                    29'---------------------------------'
     A                                    62'--------------------'
     A***********                                    Begin /085811/
     A***********                        101'--------------'
     A***********                                    End   /085811/
     A                                      SPACEA(2)
     A                                     2'ACCOUNT OFFICER'
     A                                      HIGHLIGHT
     A            RRACOC         2        19
     A                                      HIGHLIGHT
     A            RRACON        30        22
     A                                      HIGHLIGHT
     A                                      SPACEA(3)
     A                                     4'CUSTOMER'
     A                                      HIGHLIGHT
     A            RRCUST         6        16
     A                                      HIGHLIGHT
     A            RRCRNM        20        24
     A                                      HIGHLIGHT
     A                                      SPACEA(2)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR
     A**---------------------------------------------------------------
     A*
     A          R PM0370F3                  SPACEB(1)
     A                                     6'PORTFOLIO REFERENCE '
     A                                      HIGHLIGHT
     A            RRPTFR         4        26
     A                                      HIGHLIGHT
     A            RRPTFN        20        31
     A                                      HIGHLIGHT
     A                                    54'BANK CHARGING CCY'
     A                                      HIGHLIGHT
     A            RRBCGY         3        73
     A                                      HIGHLIGHT
     A                                    80'SETTLEMENT ACCOUNT'
     A                                      HIGHLIGHT
     A            RRCUST         6       100
     A                                      HIGHLIGHT
     A                                   106'-'
     A                                      HIGHLIGHT
     A            RRCSCY         3       107
     A                                      HIGHLIGHT
     A                                   110'-'
     A                                      HIGHLIGHT
     A**Start of change CGL029
     A**********  RRACOD         4       111                                                  CGL029
     A            RRACOD        10       111
     A**End of change CGL029
     A                                      HIGHLIGHT
     A**Start of change CGL029
     A**********                         115'-'                                               CGL029
     A                                   121'-'
     A**End of change CGL029
     A                                      HIGHLIGHT
     A**Start of change CGL029
     A**********  RRACSQ         2       116                                                  CGL029
     A            RRACSQ         2       122
     A**End of change CGL029
     A                                      HIGHLIGHT
     A                                      SPACEA(1)
     A                                     6'(PORTFOLIO NET ASSET VALUE IN'
     A            RRPTCY         3        37
     A            RRPNAV        15        41
     A                                    57'CONVERTED TO'
     A            RRBCG1         3        71
     A            RRPNAC        15        75
     A                                    92'FOR MANAGEMENT FEE CALCULATIONS)'
     A                                      SPACEA(1)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR
     A**---------------------------------------------------------------
     A*
     A          R PM0370F4                  SPACEB(1)
     A                                     8'CHARGE'
     A                                    21'CHARGE'
     A                                    35'LAST'
     A                                    43'CHARGE'
     A                                    53'NEXT'
     A                                    64'AMOUNT IN'
     A                                    80'AMOUNT IN'
     A                                      SPACEA(1)
     A                                     9'CODE'
     A                                    19'NARRATIVE'
     A                                    34'ACCRUAL'
     A                                    43'STOPPED'
     A                                    52'SETTLE'
     A                                    61'BANK CHARGE CCY'
     A                                    77'CUST SETTLE CCY'
     A*                                              BEGIN /S01486/
     A                                    93'PROFIT CENTRE'
     A                                   107'CHARGES TEXT'
     A**********                          98'CHARGES TEXT'
     A*                                              END   /S01486/
     A                                      SPACEA(1)
     A                                     8'------'
     A                                    19'---------'
     A                                    34'-------'
     A                                    43'-------'
     A                                    52'------'
     A                                    61'---------------'
     A                                    77'---------------'
     A*                                              BEGIN /S01486/
     A**********                          98'------------'
     A                                    93'-------------'
     A                                   107'------------'
     A*                                              END   /S01486/
     A                                      SPACEA(2)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR
     A**---------------------------------------------------------------
     A*
     A          R PM0370F7
     A                                      SPACEB(1)
     A N61N62     RRCGCY         3         8
     A N61N62     RRCGCO         2        12
     A N61N62     RRCNAR        15        16
     A  61N62     RRCTEX        26         7
     A    N62     RRCGAD         7        34
     A    N62     RRCGST         1        46
     A    N62     RRCSND         7        51
     A            RRAMBC        15        62
     A            RRAMSC        15        79
     A*                                              BEGIN /S01486/
     A************RRCTXT        23        96
     A            RRPRFC         4        97
     A            RRCTXT        23       107
     A*                                              END   /S01486/
     A                                      SPACEA(1)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR
     A**---------------------------------------------------------------
     A*
     A          R PM0370F1
     A                                     7'REFERENCE'
     A                                    17'PM0370P1'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    29'MANAGEMENT FEES ACCRUED / SETTLED'
     A                                    62' ON PORTFOLIOS TODAY'
     A                                      SPACEA(1)
     A                                    29'---------------------------------'
     A                                    62'--------------------'
     A                                      SPACEA(5)
     A                                    53'*** NO DETAILS TO REPORT ***'
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR
     A**---------------------------------------------------------------
     A*
     A          R PM0370F0
     A                                      SPACEB(1)
     A                                    47'*** NO CHARGES DEFINED FOR POR-
     A                                      TFOLIO ***'
     A                                      SPACEA(1)
     A*
     A          R PM0370F5
     A                                      SPACEB(3)
     A*                                              BEGIN /S01486/
     A**********                          56'*** END OF REPORT ***'
     A                                    46'** END OF REPORT FOR ACCOUNT +
     A                                      OFFICER'
     A            RRACOC         2        +1
     A                                    +1'**'
     A*                                              END   /S01486/
