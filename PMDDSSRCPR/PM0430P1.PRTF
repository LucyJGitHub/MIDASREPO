      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD56J                                                    *
/*EXI *  TEXT('Midas PM Customer / Portfolio Cashflow')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  PRTF/PM0430P1 - PORTFOLIO CASHFLOW EVENTS REPORT             *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01486             Date 26Sep94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01486 - Portfolio Management Upgrade to Release 10          *
     A*                                                               *
     A*****************************************************************
     A*                                              Begin /S01486/
     A                                      REF(MFRFL)
     A*                                              End   /S01486/
     A*
     A**  FORMAT FOR DOCUMENT HEADER:
     A          R HEADRE                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A*                                              Begin /S01486/
     A***********                          7'REFERENCE PM0430P1'
     A                                     6'REFERENCE PM0430P1'
     A*                                              End   /S01486/
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A*                                              Begin /S01486/
     A**********                         124PAGNBR
     A            PAGE           3  0    124
     A*                                              End   /S01486/
     A                                      EDTCDE(Z)
     A*                                              Begin /S01486/
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A                                    39'PORTFOLIO MANAGEMENT CUSTOMER / '
     A                                    71'PORTFOLIO CASHFLOW'
     A                                      SPACEA(1)
     A                                    39'--------------------------------'
     A                                    71'------------------'
     A*                                              Begin /S01486/
     A                                      SPACEA(1)
     A                                    89'A/C OFR -'
     A            RRACOF         2        99
     A            RRACNM    R            102REFFLD(BRNM)
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A*
     A**  FORMAT FOR ACCOUNT OFFICER:
     A          R ACOFRE
     A                                     2'ACCOUNT OFFICER'
     A            RRACOC         4        19
     A            RRACON        30        24
     A                                      SPACEA(2)
     A                                     4'CUSTOMER'
     A            RRCUST         6        17
     A            RRCRNM        20        24
     A                                      SPACEA(2)
     A                                     7'PORTFOLIO REFERENCE'
     A            RRPORT         4        27
     A            RRNARR        20        32
     A                                    55'PORTFOLIO CURRENCY'
     A            RRCURR         3        75
     A                                      SPACEA(2)
     A*
     A**  FORMAT FOR DAILY REPORT:
     A          R DAYRE
     A                                     4'EVENT'
     A                                    13'EVENT'
     A                                    26'NARRATIVE'
     A                                    46'TRANS REF'
     A                                    86'EVENT AMOUNT'
     A                                   115'EVENT AMOUNT IN'
     A                                      SPACEA(1)
     A                                     4'DATE'
     A                                    14'CCY'
     A                                    27'DETAILS'
     A                                    46'/SECURITY'
     A                                    64'EVENT TYPE'
     A                                    86'IN EVENT CCY'
     A                                   116'PORTFOLIO CCY'
     A                                      SPACEA(1)
     A                                     4'-----'
     A                                    13'-----'
     A                                    26'---------'
     A                                    46'---------'
     A                                    64'----------'
     A                                    86'------------'
     A                                   115'---------------'
     A                                      SPACEA(1)
     A*
     A**  FORMAT FOR TOTALS DAILY REPORT:
     A          R DTOTRE
     A  41        RREVTD         7         3
     A  42        RRECCY         3        14
     A            RRT1XT        20        22
     A            RRTTRF         6        46
     A            RRENAR        20        60
     A            RRETAM        16        82
     A            RREAPC        16       116
     A                                      SPACEA(1)
     A            RRT2XT        20        22
     A            RRSEPR        15        46
     A                                      SPACEA(1)
     A*
     A**  FORMAT FOR CASH (DAILY TOTALS):
     A*                                              Begin /S01486/
     A**********R DCATRE                    HIGHLIGHT
     A          R DCATRE
     A*                                              End   /S01486/
     A            RREVTD         7       108
     A            RRCOBD        16       116
     A                                      SPACEA(1)
     A                                   101'CUM TO'
     A            RREVTT         7       108
     A            RRCUBD        16       116
     A                                      SPACEA(2)
     A*
     A**  FORMAT FOR END OF MONTH REPORT:
     A          R MONRE
     A                                    29'CASHFLOW'
     A                                    47'AMOUNT IN CASHFLOW CURRENCY'
     A                                    82'NET AMOUNT IN'
     A                                   112'NET AMOUNT IN'
     A                                      SPACEA(1)
     A                                    17'MONTH'
     A                                    29'CURRENCY'
     A                                    45'INCOMING'
     A                                    64'OUTGOING'
     A                                    83'CASHFLOW CCY'
     A                                   112'PORTFOLIO CCY'
     A                                      SPACEA(1)
     A                                    17'------'
     A                                    29'--------'
     A                                    45'--------           --------'
     A                                    82'-------------'
     A                                   112'-------------'
     A                                      SPACEA(2)
     A*
     A**  FORMAT FOR CASH (MONTHLY TOTALS):
     A          R MCATRE
     A  30        RRMYNO         5        17
     A            RRCFCY         3        31
     A            RRICAS        16        39
     A            RROCAS        16        58
     A            RRNECS        16        79
     A            RRNEPT        16       111
     A                                      SPACEA(1)
     A*
     A**  FORMAT FOR END OF YEAR REPORT:
     A          R YEARE
     A                                    15'YEAR END'
     A                                    29'CASHFLOW'
     A                                    47'AMOUNT IN CASHFLOW CURRENCY'
     A                                    82'NET AMOUNT IN'
     A                                   112'NET AMOUNT IN'
     A                                      SPACEA(1)
     A                                    17'DATE'
     A                                    29'CURRENCY'
     A                                    45'INCOMING'
     A                                    64'OUTGOING'
     A                                    82'CASHFLOW CCY'
     A                                   112'PORTFOLIO CCY'
     A                                      SPACEA(1)
     A                                    15'--------'
     A                                    29'--------'
     A                                    45'--------           --------'
     A                                    82'-------------'
     A                                   112'-------------'
     A                                      SPACEA(2)
     A*
     A**  FORMAT FOR CASH (YEARLY TOTALS):
     A          R YCATRE
     A  31        RRMYNY         7        16
     A            RRCFYR         3        31
     A            RRICAY        16        39
     A            RROCAY        16        58
     A            RRNECY        16        79
     A            RRNEPY        16       111
     A                                      SPACEA(1)
     A*
     A**  FORMAT FOR CCY AND CUMULATIVE TOTALS
     A*                                              Begin /S01486/
     A**********R MYCMTO                    HIGHLIGHT
     A          R MYCMTO
     A*                                              End   /S01486/
     A            RRMNY1         7       103
     A            RRNEMT        16       111
     A                                      SPACEA(1)
     A            RRMNY2        14        96
     A            RRCMOT        16       111
     A                                      SPACEA(2)
     A*
     A**  FORMAT FOR FX RATES HEADER:
     A          R RAHERE
     A                                    30'RATES SHOWN BELOW ARE THE FX RATES'
     A                                    64' APPLYING WHEN THE REPORT WAS '
     A                                    94'PRODUCED.'
     A                                      SPACEA(2)
     A                                    62'EXCHANGE'
     A                                    81'MULTIPLY /'
     A                                      SPACEA(1)
     A                                    41'CURRENCY'
     A                                    64'RATE'
     A                                    80'DIVIDE (M/D)'
     A                                      SPACEA(1)
     A                                    41'--------'
     A                                    62'--------'
     A                                    80'------------'
     A                                      SPACEA(1)
     A*
     A**  FORMAT FOR FX RATES:
     A          R RATERE
     A            RRCCY          3        43
     A            RRSPOT        13S 8     57EDTWRD('   0 .        ')
     A            RRMDIN         1        85
     A                                      SPACEA(1)
     A*
     A**  FORMAT FOR NO CASHFLOW EVENTS TO REPORT:
     A          R NOCERE
     A                                      SPACEB(3)
     A                                    45'*** NO CASHFLOW EVENTS FOR '
                                          72'PORTFOLIO ***'
     A                                      SPACEA(3)
     A*
     A**  FORMAT FOR NO MONTHLY CASHFLOW TO REPORT:
     A          R NOMCRE
     A                                      SPACEB(3)
     A                                    45'*** NO MONTHLY CASHFLOWS FOR'
                                          73' PORTFOLIO ***'
     A                                      SPACEA(3)
     A*
     A**  FORMAT FOR NO YEARLY CASHFLOW TO REPORT:
     A          R NOYCRE
     A                                      SPACEB(3)
     A                                    45'*** NO YEARLY CASHFLOWS FOR'
                                          72' PORTFOLIO ***'
     A                                      SPACEA(3)
     A*
     A*                                              Begin /S01486/
     A****FORMAT FOR NO PORTFOLIOS TO REPORT:
     A**********R NOPORE
     A**********                            SPACEB(3)
     A**********                          47'*** NO PORTFOLIOS TO BE REP'
     A**********                          74'ORTED ***'
     A**********                            SPACEA(3)
     A**********
     A****FORMAT FOR NO CASHFLOWS TO REPORT:
     A**********R NOCARE
     A**********                            SPACEB(3)
     A**********                          47'*** NO CASHFLOWS TO BE REP'
     A**********                          73'ORTED ***'
     A**********                            SPACEA(3)
     A*                                              End   /S01486/
     A*
     A**  FORMAT FOR END OF REPORT:
     A          R ENDRE
     A                                      SPACEB(3)
     A*                                              Begin /S01486/
     A**********                          55'*** END OF REPORT ***'
     A                                    46'*** END OF REPORT FOR A/C OFR ***'
     A*
     A**  FORMAT FOR DATABASE ERROR REFERENCE
     A*
     A          R DBERROR
     A                                    32'*** REFERENCE PM0430'
     A                                      SPACEB(3)
     A                                    53'DATABASE ERROR ***'
     A*                                              End   /S01486/
