      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas PM T/O by Instrument Type per Department')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  PRTF/PM0460P1 - TURNOVER BY INSTR. TYPE PER DEPARTMENT       *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01486             Date 10Oct94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01486 - Portfolio Management Upgrade to Release 10          *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*                                              Begin /S01486/
     A                                      REF(MFRFL)
     A*                                              End   /S01486/
     A*
     A**   PAGE HEADER
     A**---------------------------------------------------------------
     A*
     A          R HEADRE                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A*                                              Begin /S01486/
     A***********                          7'REFERENCE'
     A                                     6'REFERENCE'
     A*                                              Begin /S01486/
     A            BJURPT        53        37
     A                                    17'PM0460P1'
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A*                                              Begin /S01486/
     A************                       124PAGNBR
     A            PAGE           3  0    124
     A*                                              End   /S01486/
     A                                      EDTCDE(Z)
     A*                                              Begin /S01486/
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A                                    34'TURNOVER BY INSTRUMENT TYPE -
     A                                      PER ACCOUNT OFFICER DEPARTMENT'
     A                                      SPACEA(1)
     A                                    34'-----------------------------
     A                                      ------------------------------'
     A*                                              Begin /S01486/
     A                                      SPACEA(1)
     A                                    89'DEPT -'
     A            RRRDPM         3        96
     A            RRRDPN    R            102REFFLD(BRNM)
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A*
     A*****************************************************************
     A**   DEPARTMENT CODE AND NARRATIVE
     A**---------------------------------------------------------------
     A*
     A          R DEPMT
     A*                                              Begin /S01486/
     A**********                            HIGHLIGHT
     A*                                              End   /S01486/
     A                                     2'DEPARTMENT'
     A            RRDPCD         4        14
     A            RRDPNM        30        19
     A                                      SPACEA(2)
     A*
     A*****************************************************************
     A**   TITLES FOR TURNOVER AMOUNTS
     A**---------------------------------------------------------------
     A*
     A          R TRNVR
     A                                    53'T U R N O V E R   -
     A                                      C A L C U L A T E D   I N   -
     A                                      0 0 0 '' S   O F'
     A            RRCYCD         3       117
     A                                      SPACEA(1)
     A                                     4'INSTRUMENT'
     A                                    39'ACCOUNT'
     A                                    54'PREVIOUS'
     A                                    71'PREVIOUS'
     A                                    87'RUNNING'
     A                                   100'RUNNING MONTH'
     A                                   117'RUNNING MONTH'
     A                                      SPACEA(1)
     A                                     7'TYPE'
     A                                    16'INSTRUMENT NARRATIVE'
     A                                    39'OFFICER'
     A                                    56'YEAR'
     A                                    73'MONTH'
     A                                    84'YEAR TO DATE'
     A                                   102'LAST YEAR'
     A                                   120'TO DATE'
     A                                      SPACEA(1)
     A                                     4'----------'
     A                                    16'--------------------'
     A                                    39'-------'
     A                                    54'--------'
     A                                    71'--------'
     A                                    84'------------'
     A                                   100'-------------'
     A                                   117'-------------'
     A                                      SPACEA(1)
     A*
     A*****************************************************************
     A**   INSTRUMENT TYPE AND NARRATIVE
     A**---------------------------------------------------------------
     A*
     A          R INSTR
     A            RRINNR         3         7
     A            RRISTN        20        16
     A*
     A*****************************************************************
     A**   TURNOVER AMOUNTS
     A**---------------------------------------------------------------
     A*
     A          R AMNTS                     SPACEA(1)
     A            RRACOC         2        41
     A            RRAMT1        15        47
     A            RRAMT2        15        64
     A            RRAMT3        15        81
     A            RRAMT4        15        98
     A            RRAMT5        15       115
     A*
     A*****************************************************************
     A**   NUMBER OF CONTRACTS
     A**---------------------------------------------------------------
     A*
     A          R CONTS                     SPACEA(1)
     A  21                                54'('
     A  21        ZCONT1         6S 0     55EDTWRD('     0)')
     A  22                                71'('
     A  22        ZCONT2         6S 0     72EDTWRD('     0)')
     A  23                                88'('
     A  23        ZCONT3         6S 0     89EDTWRD('     0)')
     A  24                               105'('
     A  24        ZCONT4         6S 0    106EDTWRD('     0)')
     A  25                               122'('
     A  25        ZCONT5         6S 0    123EDTWRD('     0)')
     A                                   132' '
     A*
     A*****************************************************************
     A**   TOTALS FOR INSTRUMENT
     A**---------------------------------------------------------------
     A*
     A          R TOTIN                     SPACEA(1)
     A                                    13'TOTAL TURNOVER FOR INSTRUMENT'
     A            RRINS1        15        47
     A            RRINS2        15        64
     A            RRINS3        15        81
     A            RRINS4        15        98
     A            RRINS5        15       115
     A*
     A*****************************************************************
     A**   TOTALS FOR CONTRACTS - INSTRUMENT LEVEL
     A**---------------------------------------------------------------
     A*
     A          R TOTIC                     SPACEA(1)
     A  21                                54'('
     A  21        RRINC1         6S 0     55EDTWRD('     0)')
     A  22                                71'('
     A  22        RRINC2         6S 0     72EDTWRD('     0)')
     A  23                                88'('
     A  23        RRINC3         6S 0     89EDTWRD('     0)')
     A  24                               105'('
     A  24        RRINC4         6S 0    106EDTWRD('     0)')
     A  25                               122'('
     A  25        RRINC5         6S 0    123EDTWRD('     0)')
     A                                   132' '
     A                                      SPACEA(1)
     A*
     A*****************************************************************
     A**   DISPLAY GROUP HEADER
     A**---------------------------------------------------------------
     A*
     A*                                              Begin /S01486/
     A**********R DGHEAD                    HIGHLIGHT
     A          R DGHEAD
     A*                                              End   /S01486/
     A                                      SPACEB(1)
     A            RRDSPG         3         4
     A            RRDSPT        20        13SPACEA(2)
     A*
     A*****************************************************************
     A**   TOTALS TURNOVER IN DISPLAY GROUP
     A**---------------------------------------------------------------
     A*
     A*                                              Begin /S01486/
     A**********R TOTDG                     HIGHLIGHT
     A          R TOTDG
     A*                                              End   /S01486/
     A                                     7'TOTAL TURNOVER IN'
     A            RRDSPT        20        25
     A            RRDST1        15        47
     A            RRDST2        15        64
     A            RRDST3        15        81
     A            RRDST4        15        98
     A            RRDST5        15       115
     A                                      SPACEA(1)
     A*
     A*****************************************************************
     A**   TOTALS FOR CONTRACTS - DEPARTMENT LEVEL
     A**---------------------------------------------------------------
     A*
     A*                                              Begin /S01486/
     A**********R TOTGC                     HIGHLIGHT
     A          R TOTGC
     A*                                              End   /S01486/
     A  61                                54'('
     A  61        RRDGC1         6S 0     55EDTWRD('     0)')
     A  62                                71'('
     A  62        RRDGC2         6S 0     72EDTWRD('     0)')
     A  63                                88'('
     A  63        RRDGC3         6S 0     89EDTWRD('     0)')
     A  64                               105'('
     A  64        RRDGC4         6S 0    106EDTWRD('     0)')
     A  65                               122'('
     A  65        RRDGC5         6S 0    123EDTWRD('     0)')
     A                                   132' '
     A                                      SPACEA(1)
     A*
     A*****************************************************************
     A**   TOTALS FOR DEPARTMENT
     A**---------------------------------------------------------------
     A*
     A*                                              Begin /S01486/
     A**********R TOTDE                      HIGHLIGHT
     A          R TOTDE
     A*                                              End   /S01486/
     A                                    13'TOTAL TURNOVER FOR DEPARTMENT'
     A            RRDPT1        15        47
     A            RRDPT2        15        64
     A            RRDPT3        15        81
     A            RRDPT4        15        98
     A            RRDPT5        15       115
     A                                      SPACEA(1)
     A*
     A*****************************************************************
     A**   TOTALS FOR CONTRACTS - DEPARTMENT LEVEL
     A**---------------------------------------------------------------
     A*
     A*                                              Begin /S01486/
     A**********R TOTDC                     HIGHLIGHT
     A          R TOTDC
     A*                                              End   /S01486/
     A  21                                54'('
     A  21        RRDPC1         6S 0     55EDTWRD('     0)')
     A  22                                71'('
     A  22        RRDPC2         6S 0     72EDTWRD('     0)')
     A  23                                88'('
     A  23        RRDPC3         6S 0     89EDTWRD('     0)')
     A  24                               105'('
     A  24        RRDPC4         6S 0    106EDTWRD('     0)')
     A  25                               122'('
     A  25        RRDPC5         6S 0    123EDTWRD('     0)')
     A                                   132' '
     A                                      SPACEA(1)
     A*
     A*****************************************************************
     A**   SEPARATOR LINES
     A**---------------------------------------------------------------
     A*
     A          R LINES
     A  21
     AO 31                                47'---------------'
     A  22
     AO 32                                64'---------------'
     A  23
     AO 33                                81'---------------'
     A  24
     AO 34                                98'---------------'
     A  25
     AO 35                               115'---------------'
     A                                   132' '
     A                                      SPACEA(1)
     A*
     A          R LINE2
     A  21
     AO 31                                47'---------------'
     A  22
     AO 32                                64'---------------'
     A  23
     AO 33                                81'---------------'
     A  24
     AO 34                                98'---------------'
     A  25
     AO 35                               115'---------------'
     A                                   132' '
     A                                      SPACEA(2)
     A*
     A*                                              Begin /S01486/
     A*****************************************************************
     A*****NO*TURNOVER TO REPORT
     A**---------------------------------------------------------------
     A**********
     A**********R NOTRN
     A**********                            SPACEB(1)
     A**********                          43'** NO TURNOVER TO BE REPORTED -
     A**********                            FOR DEPARTMENT **'
     A**********                            SPACEA(3)
     A*****NO*DETAILS*TO*REPORT****************************************
     A**********-------------------------------------------------------
     A**********
     A**********R NODET
     A**********                            SPACEB(3)
     A**********                          55'NO DETAILS TO REPORT'
     A**********                            SPACEA(3)
     A*****************************************************************
     A*                                              End   /S01486/
     A**   END OF REPORT
     A**---------------------------------------------------------------
     A*
     A          R ENDRE
     A                                      SPACEB(3)
     A*                                              Begin /S01486/
     A**********                          56'** END OF REPORT **'
     A                                    46'*** END OF REPORT FOR DEPT ***'
     A*
     A**  FORMAT FOR DATABASE ERROR REFERENCE
     A*
     A          R DBERROR
     A                                    32'*** REFERENCE PM0460'
     A                                      SPACEB(3)
     A                                    53'DATABASE ERROR ***'
     A*                                              End   /S01486/
