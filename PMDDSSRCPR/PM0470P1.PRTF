      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas PM T/O by Instr. Type per A/C Officer')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  PM0470P1 - TURNOVER BY INSTR. TYPE PER A/C OFFICER           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01486             DATE 23SEP94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01486 - Portfolio Management Upgrade to Release 10          *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*                                              Begin /S01486/
     A                                      REF(MFRFL)
     A*                                              End   /S01486/
     A*
     A**   PAGE HEADER
     A**---------------------------------------------------------------
     A*
     A          R HEADRE                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A*                                              Begin /S01486/
     A***********                          7'REFERENCE'
     A                                     6'REFERENCE'
     A*                                              End   /S01486/
     A            BJURPT        53        37
     A                                    17'PM0470P1'
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A*                                              Begin /S01486/
     A************                       124PAGNBR
     A            PAGE           3  0    124
     A*                                              End   /S01486/
     A                                      EDTCDE(Z)
     A*                                              Begin /S01486/
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A                                    40'TURNOVER BY INSTRUMENT TYPE -
     A                                      PER ACCOUNT OFFICER'
     A                                      SPACEA(1)
     A                                    40'-----------------------------
     A                                      -------------------'
     A*                                              Begin /S01486/
     A                                      SPACEA(1)
     A                                    89'A/C OFR -'
     A            RRACOF         2        99
     A            RRACNM    R            102REFFLD(BRNM)
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A*
     A*****************************************************************
     A**   DEPARTMENT AND NARRATIVE, ACCOUNT OFFICER AND NARRATIVE
     A**---------------------------------------------------------------
     A*
     A          R DEPMT
     A*                                              Begin /S01486/
     A**********                            HIGHLIGHT
     A*                                              End   /S01486/
     A                                     2'DEPARTMENT'
     A            RRDPCD         4        14
     A            RRDPNM        30        19
     A                                      SPACEA(2)
     A*
     A                                     2'ACCOUNT OFFICER'
     A            RRACOC         4        19
     A            RRACON        30        24
     A                                      SPACEA(2)
     A*
     A*****************************************************************
     A**   TITLES FOR TURNOVER AMOUNTS
     A**---------------------------------------------------------------
     A*
     A          R TRNVR
     A                                    53'T U R N O V E R   -
     A                                      C A L C U L A T E D   I N   -
     A                                      0 0 0 '' S   O F'
     A            RRCYCD         3       117
     A                                      SPACEA(1)
     A                                     4'DISPLAY'
     A                                    13'INSTR'
     A*                                              Begin /S01486/
     A**********                          51'PREVIOUS'
     A**********                          69'PREVIOUS'
     A**********                          86'RUNNING'
     A**********                         100'RUNNING MONTH'
     A**********                         118'RUNNING MONTH'
     A                                    52'PREVIOUS'
     A                                    70'PREVIOUS'
     A                                    87'RUNNING'
     A                                   101'RUNNING MONTH'
     A                                   119'RUNNING MONTH'
     A*                                              End   /S01486/
     A                                      SPACEA(1)
     A                                     5'GROUP'
     A                                    14'TYPE'
     A*                                              Begin /S01486/
     A**********                          25'INSTRUMENT NARRATIVE'
     A**********                          53'YEAR'
     A**********                          70'MONTH'
     A**********                          83'YEAR TO DATE'
     A**********                         102'LAST YEAR'
     A**********                         120'TO DATE'
     A                                    21'INSTRUMENT NARRATIVE'
     A                                    54'YEAR'
     A                                    72'MONTH'
     A                                    84'YEAR TO DATE'
     A                                   103'LAST YEAR'
     A                                   122'TO DATE'
     A*                                              End   /S01486/
     A                                      SPACEA(1)
     A                                     4'-------'
     A                                    13'-----'
     A*                                              Begin /S01486/
     A**********                          25'--------------------'
     A**********                          51'--------'
     A**********                          69'--------'
     A**********                          83'------------'
     A**********                         100'-------------'
     A**********                         118'-------------'
     A                                    21'--------------------'
     A                                    52'--------'
     A                                    70'--------'
     A                                    84'------------'
     A                                   101'-------------'
     A                                   119'-------------'
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A*
     A*****************************************************************
     A**   ACCOUNT OFFICER CODE AND NARRATIVE ONLY OUTPUT
     A**---------------------------------------------------------------
     A*
     A          R ACOFF
     A*                                              Begin /S01486/
     A**********                            HIGHLIGHT
     A*                                              End   /S01486/
     A                                     2'ACCOUNT OFFICER'
     A            RRACOC         4        19
     A            RRACON        30        24
     A                                      SPACEA(2)
     A*
     A*****************************************************************
     A**   INSTRUMENT TYPE AND NARRATIVE
     A**---------------------------------------------------------------
     A*
     A          R INSTR
     A  69        RRDSPG         3         5
     A            RRINNR         3        14
     A*                                              Begin /S01486/
     A**********  RRISTN        20        20
     A            RRISTN        20        21
     A*                                              End   /S01486/
     A*
     A*****************************************************************
     A**   TURNOVER AMOUNTS
     A**---------------------------------------------------------------
     A*
     A          R AMNTS
     A            RRAMT1        15        45
     A            RRAMT2        15        63
     A            RRAMT3        15        81
     A            RRAMT4        15        99
     A            RRAMT5        15       117
     A                                      SPACEA(1)
     A*
     A*****************************************************************
     A**   NUMBER OF CONTRACTS
     A**---------------------------------------------------------------
     A*
     A          R CONTS
     A*                                              Begin /S01486/
     A                                      SPACEA(1)
     A*                                              End   /S01486/
     A  21                                52'('
     A  21        ZCONT1         6S 0     53EDTWRD('     0)')
     A  22                                70'('
     A  22        ZCONT2         6S 0     71EDTWRD('     0)')
     A  23                                88'('
     A  23        ZCONT3         6S 0     89EDTWRD('     0)')
     A  24                               106'('
     A  24        ZCONT4         6S 0    107EDTWRD('     0)')
     A*                                              Begin /S01486/
     A**25********                       125'('
     A**25********ZCONT5         6S 0    126EDTWRD('     0)')
     A  25                               124'('
     A  25        ZCONT5         6S 0    125EDTWRD('     0)')
     A************                          SPACEA(1)
     A*                                              End   /S01486/
     A*
     A*****************************************************************
     A**   TOTALS FOR ACCOUNT OFFICER
     A**---------------------------------------------------------------
     A*
     A*                                              Begin /S01486/
     A**********R TOTOF                     HIGHLIGHT
     A          R TOTOF
     A*                                              End   /S01486/
     A                                     7'TOTAL TURNOVER FOR ACCOUNT -
     A                                      OFFICER'
     A            RROFF1        15        45
     A            RROFF2        15        63
     A            RROFF3        15        81
     A            RROFF4        15        99
     A            RROFF5        15       117
     A                                      SPACEA(1)
     A*
     A*****************************************************************
     A**   TOTALS FOR CONTRACTS - OFFICER LEVEL
     A**---------------------------------------------------------------
     A*
     A*                                              Begin /S01486/
     A**********R TOTOC                     HIGHLIGHT
     A          R TOTOC
     A                                      SPACEA(1)
     A*                                              End   /S01486/
     A  21                                52'('
     A  21        RROFC1         6S 0     53EDTWRD('     0)')
     A  22                                70'('
     A  22        RROFC2         6S 0     71EDTWRD('     0)')
     A  23                                88'('
     A  23        RROFC3         6S 0     89EDTWRD('     0)')
     A  24                               106'('
     A  24        RROFC4         6S 0    107EDTWRD('     0)')
     A*                                              Begin /S01486/
     A**25********                       125'('
     A**25********RROFC5         6S 0    126EDTWRD('     0)')
     A  25                               124'('
     A  25        RROFC5         6S 0    125EDTWRD('     0)')
     A***********                        132' '
     A***********                           SPACEA(1)
     A*                                              End   /S01486/
     A*
     A*****************************************************************
     A**   TOTALS FOR DEPARTMENT
     A**---------------------------------------------------------------
     A*
     A*                                              Begin /S01486/
     A**********R TOTDE                     HIGHLIGHT
     A          R TOTDE
     A*                                              End   /S01486/
     A                                    12'TOTAL TURNOVER FOR DEPARTMENT'
     A            RRDPT1        15        45
     A            RRDPT2        15        63
     A            RRDPT3        15        81
     A            RRDPT4        15        99
     A            RRDPT5        15       117
     A                                      SPACEA(1)
     A*
     A*****************************************************************
     A**   TOTALS FOR CONTRACTS - DEPARTMENT LEVEL
     A**---------------------------------------------------------------
     A*
     A*                                              Begin /S01486/
     A**********R TOTDC                     HIGHLIGHT
     A          R TOTDC
     A                                      SPACEA(1)
     A*                                              End   /S01486/
     A  21                                52'('
     A  21        RRDPC1         6S 0     53EDTWRD('     0)')
     A  22                                70'('
     A  22        RRDPC2         6S 0     71EDTWRD('     0)')
     A  23                                88'('
     A  23        RRDPC3         6S 0     89EDTWRD('     0)')
     A  24                               106'('
     A  24        RRDPC4         6S 0    107EDTWRD('     0)')
     A*                                              Begin /S01486/
     A**25********                       125'('
     A**25********RRDPC5         6S 0    126EDTWRD('     0)')
     A  25                               124'('
     A  25        RRDPC5         6S 0    125EDTWRD('     0)')
     A***********                        132' '
     A***********                           SPACEA(1)
     A*                                              End   /S01486/
     A*
     A*****************************************************************
     A**   TOTALS TURNOVER IN DISPLAY GROUP
     A**---------------------------------------------------------------
     A*
     A*                                              Begin /S01486/
     A**********R TOTDG                     HIGHLIGHT
     A          R TOTDG
     A*                                              End   /S01486/
     A                                     7'TOTAL TURNOVER IN'
     A            RRDSPT        20        25
     A            RRDST1        15        45
     A            RRDST2        15        63
     A            RRDST3        15        81
     A            RRDST4        15        99
     A            RRDST5        15       117
     A                                      SPACEA(1)
     A*
     A*****************************************************************
     A**   TOTALS FOR CONTRACTS - DEPARTMENT LEVEL
     A**---------------------------------------------------------------
     A*
     A*                                              Begin /S01486/
     A**********R TOTGC                     HIGHLIGHT
     A          R TOTGC
     A                                      SPACEA(1)
     A*                                              End   /S01486/
     A  61                                52'('
     A  61        RRDGC1         6S 0     53EDTWRD('     0)')
     A*                                              Begin /S01486/
     A**62********                        69'('
     A**62********RRDGC2         6S 0     70EDTWRD('     0)')
     A**63********                        86'('
     A**63********RRDGC3         6S 0     87EDTWRD('     0)')
     A**64********                       103'('
     A**64********RRDGC4         6S 0    104EDTWRD('     0)')
     A**65********                       120'('
     A**65********RRDGC5         6S 0    121EDTWRD('     0)')
     A************                       132' '
     A************                          SPACEA(1)
     A  62                                70'('
     A  62        RRDGC2         6S 0     71EDTWRD('     0)')
     A  63                                88'('
     A  63        RRDGC3         6S 0     89EDTWRD('     0)')
     A  64                               106'('
     A  64        RRDGC4         6S 0    107EDTWRD('     0)')
     A  65                               124'('
     A  65        RRDGC5         6S 0    125EDTWRD('     0)')
     A*                                              End   /S01486/
     A*
     A*****************************************************************
     A**   SEPARATOR LINES
     A**---------------------------------------------------------------
     A*
     A          R LINES
     A  21
     AO 31                                45'---------------'
     A  22
     AO 32                                63'---------------'
     A  23
     AO 33                                81'---------------'
     A  24
     AO 34                                99'---------------'
     A  25
     AO 35                               117'---------------'
     A                                   132' '
     A                                      SPACEA(1)
     A*
     A          R LINE2
     A  21
     AO 31                                45'---------------'
     A  22
     AO 32                                63'---------------'
     A  23
     AO 33                                81'---------------'
     A  24
     AO 34                                99'---------------'
     A  25
     AO 35                               117'---------------'
     A                                   132' '
     A                                      SPACEA(2)
     A*
     A*                                              Begin /S01486/
     A*****************************************************************
     A*****NO*TURNOVER TO REPORT
     A**---------------------------------------------------------------
     A**********
     A**********R NOTRN
     A**********                            SPACEB(1)
     A**********                          43'** NO TURNOVER TO BE REPORTED -
     A**********                            FOR DEPARTMENT **'
     A**********                            SPACEA(3)
     A*****************************************************************
     A*****NO*DETAILS*TO*REPORT****************************************
     A**********-------------------------------------------------------
     A**********
     A**********R NODET
     A**********                            SPACEB(3)
     A**********                          55'NO DETAILS TO REPORT'
     A**********                            SPACEA(3)
     A*****************************************************************
     A*                                              End   /S01486/
     A**   END OF REPORT
     A**---------------------------------------------------------------
     A*
     A          R ENDRE
     A                                      SPACEB(3)
     A*                                              Begin /S01486/
     A**********                          56'** END OF REPORT **'
     A                                    46'*** END OF REPORT FOR A/C OFR ***'
     A*
     A**  FORMAT FOR DATABASE ERROR REFERENCE
     A*
     A          R DBERROR
     A                                    32'*** REFERENCE PM0470'
     A                                      SPACEB(3)
     A                                    53'DATABASE ERROR ***'
     A*                                              End   /S01486/
