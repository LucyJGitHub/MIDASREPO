      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD54J                                                    *
/*EXI *  TEXT('Midas PM T/O by Instrument Type per Customer')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  PRTF/PM0480P1 - TURNOVER BY INSTR. TYPE PER CUST/PORT        *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01486             DATE 10OCT94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01486 - Portfolio Management Upgrade to Release 10          *
     A*                                                               *
     A*****************************************************************
     A*                                              Begin /S01486/
     A                                      REF(MFRFL)
     A*                                              End   /S01486/
     A*
     A**   PAGE HEADER
     A**---------------------------------------------------------------
     A*
     A          R HEADRE                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A*                                              Begin /S01486/
     A***********                          7'REFERENCE'
     A                                     6'REFERENCE'
     A*                                              End   /S01486/
     A            BJURPT        53        37
     A                                    17'PM0480P1'
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A*                                              Begin /S01486/
     A************                       124PAGNBR
     A            PAGE           3  0    124
     A*                                              End   /S01486/
     A                                      EDTCDE(Z)
     A*                                              Begin /S01486/
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A                                    40'TURNOVER BY INSTRUMENT TYPE -
     A                                      PER CUSTOMER / PORTFOLIO'
     A                                      SPACEA(1)
     A                                    40'-----------------------------
     A                                      ------------------------'
     A*                                              Begin /S01486/
     A                                      SPACEA(1)
     A                                    89'A/C OFR -'
     A            RRACOF         2        99
     A            RRACNM    R            102REFFLD(BRNM)
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A*
     A**   A/C OFFICER, CUSTOMER AND PORTFOLIO DETAILS
     A*
     A          R ACOFF
     A*                                              Begin /S01486/
     A**********                            HIGHLIGHT
     A*                                              End   /S01486/
     A                                     2'ACCOUNT OFFICER CODE'
     A            RRACOC         4        24
     A            RRACON        30        29
     A                                      SPACEA(2)
     A                                     4'CUSTOMER'
     A            RRCUST         6        16
     A            RRCRNM        20        24
     A                                      SPACEA(2)
     A                                     4'PORTFOLIO REFERENCE'
     A            RRPTFR         4        24
     A            RRPTFN        20        29
     A                                    51'REPORTING CURRENCY'
     A            RRCCY          3        70
     A            RRTEXT        20        75
     A                                      SPACEA(2)
     A                                    50'T U R N O V E R   I N   -
     A                                      0 0 0 '' S   O F   -
     A                                      R E P O R T I N G   C C Y'
     A                                      SPACEA(1)
     A                                    60'A N D   I N   0 0 0 '' S   O F   -
     A                                      B A S E   C C Y -'
     A            BJCYCD         3       112
     A                                      SPACEA(1)
     A*
     A**  CUSTOMER AND PORTFOLIO DETAILS
     A*
     A          R CUSTD
     A*                                              Begin /S01486/
     A**********                            HIGHLIGHT
     A*                                              End   /S01486/
     A                                     4'CUSTOMER'
     A            RRCUST         6        16
     A            RRCRNM        20        24
     A                                      SPACEA(2)
     A                                     4'PORTFOLIO REFERENCE'
     A            RRPTFR         4        24
     A            RRPTFN        20        29
     A                                    51'REPORTING CURRENCY'
     A            RRCCY          3        70
     A            RRTEXT        20        75
     A                                      SPACEA(2)
     A*
     A**  PORTFOLIO DETAILS
     A*
     A          R PTFRD
     A*                                              Begin /S01486/
     A**********                            HIGHLIGHT
     A*                                              End   /S01486/
     A                                     4'PORTFOLIO REFERENCE'
     A            RRPTFR         4        24
     A            RRPTFN        20        29
     A                                    51'REPORTING CURRENCY'
     A            RRCCY          3        70
     A            RRTEXT        20        75
     A*                                              Begin /S01486/
     A**********  RRTEX2        20        97HIGHLIGHT
     A            RRTEX2        20        97
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A*
     A*****************************************************************
     A**   TITLES FOR TURNOVER AMOUNTS
     A**---------------------------------------------------------------
     A*
     A          R TRNVR
     A                                      SPACEA(1)
     A                                     4'DISPLAY'
     A                                    15'INSTR'
     A                                    52'PREVIOUS'
     A                                    70'PREVIOUS'
     A                                    87'RUNNING'
     A                                   101'RUNNING MONTH'
     A                                   119'RUNNING MONTH'
     A                                      SPACEA(1)
     A                                     5'GROUP'
     A                                    16'TYPE'
     A                                    25'INSTRUMENT NARRATIVE'
     A                                    53'YEAR'
     A                                    70'MONTH'
     A                                    83'YEAR TO DATE'
     A                                   102'LAST YEAR'
     A                                   120'TO DATE'
     A                                      SPACEA(1)
     A                                     4'-------'
     A                                    15'-----'
     A                                    25'--------------------'
     A                                    52'--------'
     A                                    70'--------'
     A                                    84'------------'
     A                                   101'-------------'
     A                                   119'-------------'
     A                                      SPACEA(2)
     A*
     A*****************************************************************
     A**   AMOUNTS AND CONTRACTS
     A**---------------------------------------------------------------
     A*
     A          R AMNTS
     A            RRDSPG         3         6
     A            RRINNR         3        16
     A            RRISTN        20        25
     A            RRAM11        15        45
     A            RRAM12        15        63
     A            RRAM13        15        81
     A            RRAM14        15        99
     A            RRAM15        15       117
     A                                      SPACEA(1)
     A            RRAM21        15        45
     A            RRAM22        15        63
     A            RRAM23        15        81
     A            RRAM24        15        99
     A            RRAM25        15       117
     A                                      SPACEA(1)
     A  21                                52'('
     A  21        ZCONT1         6S 0     53EDTWRD('     0)')
     A  22                                70'('
     A  22        ZCONT2         6S 0     71EDTWRD('     0)')
     A  23                                88'('
     A  23        ZCONT3         6S 0     89EDTWRD('     0)')
     A  24                               106'('
     A  24        ZCONT4         6S 0    107EDTWRD('     0)')
     A  25                               125'('
     A  25        ZCONT5         6S 0    126EDTWRD('     0)')
     A  26                               132' '
     A  26                                  SPACEA(1)
     A*
     A*****************************************************************
     A**   TOTAL AMOUNTS AND CONTRACTS
     A**---------------------------------------------------------------
     A*
     A          R TOTLS
     A  40                                45'---------------'
     A  41                                63'---------------'
     A  42                                81'---------------'
     A  43                                99'---------------'
     A  44                               117'---------------'
     A                                   132' '
     A                                      SPACEA(1)
     A            RRNARR        38         2
     A            RRTO11        15        45
     A            RRTO12        15        63
     A            RRTO13        15        81
     A            RRTO14        15        99
     A            RRTO15        15       117
     A                                      SPACEA(1)
     A            RRTO21        15        45
     A            RRTO22        15        63
     A            RRTO23        15        81
     A            RRTO24        15        99
     A            RRTO25        15       117
     A                                      SPACEA(1)
     A  21                                52'('
     A  21        RRCON1         6S 0     53EDTWRD('     0)')
     A  22                                70'('
     A  22        RRCON2         6S 0     71EDTWRD('     0)')
     A  23                                88'('
     A  23        RRCON3         6S 0     89EDTWRD('     0)')
     A  24                               106'('
     A  24        RRCON4         6S 0    107EDTWRD('     0)')
     A  25                               125'('
     A  25        RRCON5         6S 0    126EDTWRD('     0)')
     A  26                               132' '
     A  26                                  SPACEA(1)
     A  40                                45'---------------'
     A  41                                63'---------------'
     A  42                                81'---------------'
     A  43                                99'---------------'
     A  44                               117'---------------'
     A                                   132' '
     A                                      SPACEA(3)
     A*
     A*****************************************************************
     A**   NO TURNOVER TO REPORT
     A**---------------------------------------------------------------
     A*
     A          R NOTRN
     A                                      SPACEB(1)
     A                                    43'** NO TURNOVER TO BE REPORTED -
     A                                      FOR PORTFOLIO **'
     A                                      SPACEA(3)
     A*                                              Begin /S01486/
     A*****************************************************************
     A*****NO*DETAILS*TO*REPORT****************************************
     A**********-------------------------------------------------------
     A**********
     A**********R NODET
     A**********                            SPACEB(3)
     A**********                          52'** NO DETAILS TO REPORT **'
     A*                                              End   /S01486/
     A*****************************************************************
     A**   END OF REPORT
     A**---------------------------------------------------------------
     A*
     A          R ENDRE
     A                                      SPACEB(3)
     A*                                              Begin /S01486/
     A**********                          56'** END OF REPORT **'
     A                                    46'*** END OF REPORT FOR A/C OFR ***'
     A*
     A**  FORMAT FOR DATABASE ERROR REFERENCE
     A*
     A          R DBERROR
     A                                    32'*** REFERENCE PM0480'
     A                                      SPACEB(3)
     A                                    53'DATABASE ERROR ***'
     A*                                              End   /S01486/
