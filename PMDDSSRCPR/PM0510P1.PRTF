      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD54J                                                    *
/*EXI *  TEXT('Midas PM T/O by Currency per Customer/Portf.')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  PRTF/PM0510P1 - TURNOVER BY CURRENCY PER CUSTOMER/PORTFOLIO  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01486             DATE 10OCT94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01486 - Portfolio Management Upgrade to Release 10          *
     A*                                                               *
     A*****************************************************************
     A*                                              Begin /S01486/
     A                                      REF(MFRFL)
     A*                                              End   /S01486/
     A*
     A**   PAGE HEADER
     A**---------------------------------------------------------------
     A*
     A          R HEADRE                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A*                                              Begin /S01486/
     A***********                          7'REFERENCE'
     A                                     6'REFERENCE'
     A*                                              End   /S01486/
     A            BJURPT        53        37
     A                                    17'PM0510P1'
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A*                                              Begin /S01486/
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A                                    40'TURNOVER BY CURRENCY -
     A                                      PER CUSTOMER'
     A                                      SPACEA(1)
     A                                    40'----------------------
     A                                      -------------'
     A*                                              Begin /S01486/
     A                                      SPACEA(1)
     A                                    89'A/C OFR -'
     A            RRACOF         2        99
     A            RRACNM    R            102REFFLD(BRNM)
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A*
     A**   A/C OFFICER, CUSTOMER AND PORTFOLIO DETAILS
     A*
     A          R ACOFF
     A                                     2'ACCOUNT OFFICER CODE'
     A*                                              Begin /S01486/
     A***********                           HIGHLIGHT
     A*********** RRACOC         4        24HIGHLIGHT
     A*********** RRACON        30        29HIGHLIGHT
     A            RRACOC         4        24
     A            RRACON        30        29
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A                                     4'CUSTOMER'
     A*                                              Begin /S01486/
     A***********                           HIGHLIGHT
     A*********** RRCUST         6        16HIGHLIGHT
     A*********** RRCRNM        20        24HIGHLIGHT
     A            RRCUST         6        16
     A            RRCRNM        20        24
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A                                     4'PORTFOLIO REFERENCE'
     A*                                              Begin /S01486/
     A***********                           HIGHLIGHT
     A*********** RRPTFR         4        24HIGHLIGHT
     A*********** RRPTFN        20        29HIGHLIGHT
     A            RRPTFR         4        24
     A            RRPTFN        20        29
     A*                                              End   /S01486/
     A                                      SPACEA(1)
     A                                    50'T U R N O V E R   I N   -
     A                                      0 0 0 '' S   O F   -
     A                                      R E P O R T I N G   C C Y'
     A                                      SPACEA(1)
     A                                    60'A N D   I N   0 0 0 '' S   O F   -
     A                                      B A S E   C C Y -'
     A            BJCYCD         3       112
     A                                      SPACEA(1)
     A*
     A**  CUSTOMER AND PORTFOLIO DETAILS
     A*
     A          R CUSTD
     A*                                              Begin /S01486/
     A***********                           HIGHLIGHT
     A*                                              End   /S01486/
     A                                     4'CUSTOMER'
     A            RRCUST         6        16
     A            RRCRNM        20        24
     A                                      SPACEA(2)
     A                                     4'PORTFOLIO REFERENCE'
     A            RRPTFR         4        24
     A            RRPTFN        20        29
     A                                      SPACEA(2)
     A*
     A**  PORTFOLIO DETAILS
     A*
     A          R PTFRD
     A*                                              Begin /S01486/
     A***********                           HIGHLIGHT
     A*                                              End   /S01486/
     A                                     4'PORTFOLIO REFERENCE'
     A            RRPTFR         4        24
     A            RRPTFN        20        29
     A*                                              Begin /S01486/
     A*********** RRTEX2        20        59HIGHLIGHT
     A            RRTEX2        20        59
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A*
     A*****************************************************************
     A**   TITLES FOR TURNOVER AMOUNTS
     A**---------------------------------------------------------------
     A*
     A          R TRNVR
     A                                      SPACEA(1)
     A                                     4'CURRENCY'
     A                                    47'PREVIOUS'
     A                                    65'PREVIOUS'
     A                                    82'RUNNING'
     A                                    96'RUNNING MONTH'
     A                                   114'RUNNING MONTH'
     A                                      SPACEA(1)
     A                                     6'CODE'
     A                                    14'CURRENCY NAME'
     A                                    49'YEAR'
     A                                    66'MONTH'
     A                                    79'YEAR TO DATE'
     A                                    98'LAST YEAR'
     A                                   116'TO DATE'
     A                                      SPACEA(1)
     A                                     4'--------'
     A                                    14'-------------'
     A                                    47'--------'
     A                                    65'--------'
     A                                    79'------------'
     A                                    96'-------------'
     A                                   114'-------------'
     A                                      SPACEA(1)
     A*
     A*****************************************************************
     A**   AMOUNTS AND CONTRACTS
     A**---------------------------------------------------------------
     A*
     A          R AMNTS
     A            RRICCY         3         7
     A            RRCYNM        14        14
     A            RRAM11        15        40
     A            RRAM12        15        58
     A            RRAM13        15        76
     A            RRAM14        15        94
     A            RRAM15        15       112
     A                                      SPACEA(1)
     A            RRAM21        15        40
     A            RRAM22        15        58
     A            RRAM23        15        76
     A            RRAM24        15        94
     A            RRAM25        15       112
     A                                      SPACEA(1)
     A*
     A*****************************************************************
     A**   TOTAL AMOUNTS AND CONTRACTS
     A**---------------------------------------------------------------
     A*
     A          R TOTLS
     A  40                                40'---------------'
     A  41                                58'---------------'
     A  42                                76'---------------'
     A  43                                94'---------------'
     A  44                               112'---------------'
     A                                   132' '
     A                                      SPACEA(1)
     A            RRNARR        38         2
     A            RRTO21        15        40
     A            RRTO22        15        58
     A            RRTO23        15        76
     A            RRTO24        15        94
     A            RRTO25        15       112
     A                                      SPACEA(1)
     A  40                                40'---------------'
     A  41                                58'---------------'
     A  42                                76'---------------'
     A  43                                94'---------------'
     A  44                               112'---------------'
     A                                   132' '
     A                                      SPACEA(3)
     A*
     A*****************************************************************
     A*                                              Begin /S01486/
     A*****NO*TURNOVER TO REPORT
     A**---------------------------------------------------------------
     A**********
     A**********R NOTRN
     A**********                            SPACEB(1)
     A**********                          43'** NO TURNOVER TO BE REPORTED -
     A**********                            FOR PORTFOLIO **'
     A**********                            SPACEA(3)
     A*****************************************************************
     A*****NO*DETAILS TO REPORT
     A**---------------------------------------------------------------
     A**********
     A**********R NODET
     A**********                            SPACEB(3)
     A**********                          52'** NO DETAILS TO REPORT **'
     A*                                              End   /S01486/
     A*****************************************************************
     A**   END OF REPORT
     A**---------------------------------------------------------------
     A*
     A          R ENDRE
     A                                      SPACEB(3)
     A*                                              Begin /S01486/
     A**********                          56'** END OF REPORT **'
     A                                    46'*** END OF REPORT FOR A/C OFR ***'
     A*
     A**  FORMAT FOR DATABASE ERROR REFERENCE
     A*
     A          R DBERROR
     A                                    32'*** REFERENCE PM0510'
     A                                      SPACEB(3)
     A                                    53'DATABASE ERROR ***'
     A*                                              End   /S01486/
