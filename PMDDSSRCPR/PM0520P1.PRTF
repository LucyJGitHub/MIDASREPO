      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas PM T/O by Country per Department')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  PRTF/PM0520P1 - TURNOVER BY COUNTRY PER DEPARTMENT           *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01486             DATE 14OCT94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01486 - Portfolio Management Upgrade to Release 10          *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*                                              Begin /S01486/
     A                                      REF(MFRFL)
     A*                                              End   /S01486/
     A*
     A**   PAGE HEADER
     A**---------------------------------------------------------------
     A*
     A          R HEADRE                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A*                                              Begin /S01486/
     A***********                          7'REFERENCE'
     A                                     6'REFERENCE'
     A*                                              End   /S01486/
     A            BJURPT        53        37
     A                                    17'PM0520P1'
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A*                                              Begin /S01486/
     A************                       124PAGNBR
     A            PAGE           3  0    124
     A*                                              End   /S01486/
     A                                      EDTCDE(Z)
     A*                                              Begin /S01486/
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A                                    31'TURNOVER BY DOMICILE OF CUSTOMER -
     A                                      PER ACCOUNT OFFICER DEPARTMENT'
     A                                      SPACEA(1)
     A                                    31'---------------------------------
     A                                      ------------------------------'
     A*                                              Begin /S01486/
     A                                      SPACEA(1)
     A                                    89'DEPT -'
     A            RRDEPT         3        96
     A            RRDEPN    R            102REFFLD(BRNM)
     A*                                              End   /S01486/
     A                                      SPACEA(2)
     A*
     A*****************************************************************
     A**   DEPARTMENT CODE AND NARRATIVE
     A**---------------------------------------------------------------
     A*
     A          R DEPMT
     A*                                              Begin /S01486/
     A**********                            HIGHLIGHT
     A*                                              End   /S01486/
     A                                     2'DEPARTMENT'
     A            RRDPCD         4        14
     A            RRDPNM        30        19
     A                                      SPACEA(2)
     A*
     A*****************************************************************
     A**   TITLES FOR TURNOVER AMOUNTS
     A**---------------------------------------------------------------
     A*
     A          R TRNVR
     A                                    53'T U R N O V E R   -
     A                                      C A L C U L A T E D   I N   -
     A                                      0 0 0 '' S   O F'
     A            RRCYCD         3       117
     A                                      SPACEA(2)
     A                                     2'COUNTRY'
     A                                    33'ACCOUNT'
     A                                    47'PREVIOUS'
     A                                    65'PREVIOUS'
     A                                    81'RUNNING'
     A                                    96'RUNNING MONTH'
     A                                   114'RUNNING MONTH'
     A                                      SPACEA(1)
     A                                     4'CODE'
     A                                    12'COUNTRY NAME'
     A                                    33'OFFICER'
     A                                    49'YEAR'
     A                                    66'MONTH'
     A                                    79'YEAR TO DATE'
     A                                    98'LAST YEAR'
     A                                   117'TO DATE'
     A                                      SPACEA(1)
     A                                     2'-------'
     A                                    12'-------------'
     A                                    33'-------'
     A                                    47'--------'
     A                                    65'--------'
     A                                    79'------------'
     A                                    96'-------------'
     A                                   114'-------------'
     A                                      SPACEA(1)
     A*
     A*****************************************************************
     A**   COUNTRY CODE AND NAME
     A**---------------------------------------------------------------
     A*
     A          R CNTRY
     A            RRCOLC         2         4
     A            RRCNNM        30        12
     A*
     A*****************************************************************
     A**   AMOUNTS AND CONTRACTS
     A**---------------------------------------------------------------
     A*
     A          R AMNTS
     A            RRACOC         2        35
     A            RRAM21        15        40
     A            RRAM22        15        58
     A            RRAM23        15        76
     A            RRAM24        15        94
     A            RRAM25        15       112
     A                                      SPACEA(1)
     A*
     A*****************************************************************
     A**   TOTALS PER COUNTRY
     A**---------------------------------------------------------------
     A*
     A          R TOTCY
     A  31                                40'---------------'
     A  32                                58'---------------'
     A  33                                76'---------------'
     A  34                                94'---------------'
     A  35                               112'---------------'
     A                                   132' '
     A                                      SPACEA(1)
     A                                     9'TOTAL TURNOVER FOR COUNTRY'
     A            RRTO21        15        40
     A            RRTO22        15        58
     A            RRTO23        15        76
     A            RRTO24        15        94
     A            RRTO25        15       112
     A                                      SPACEA(1)
     A  31                                40'---------------'
     A  32                                58'---------------'
     A  33                                76'---------------'
     A  34                                94'---------------'
     A  35                               112'---------------'
     A                                   132' '
     A                                      SPACEA(2)
     A*
     A*****************************************************************
     A**   TOTALS PER DEPARTMENT
     A**---------------------------------------------------------------
     A*
     A          R TOTDE
     A                                     6'TOTAL TURNOVER FOR DEPARTMENT'
     A            RRTO41        15        40
     A            RRTO42        15        58
     A            RRTO43        15        76
     A            RRTO44        15        94
     A            RRTO45        15       112
     A                                      SPACEA(1)
     A  31                                40'---------------'
     A  32                                58'---------------'
     A  33                                76'---------------'
     A  34                                94'---------------'
     A  35                               112'---------------'
     A                                   132' '
     A                                      SPACEA(3)
     A*
     A*****************************************************************
     A**   SEPARATOR LINES FOR DEPARTMENT TOTALS
     A**---------------------------------------------------------------
     A*
     A          R LINES
     A  31                                40'---------------'
     A  32                                58'---------------'
     A  33                                76'---------------'
     A  34                                94'---------------'
     A  35                               112'---------------'
     A                                   132' '
     A                                      SPACEA(1)
     A*
     A*****************************************************************
     A**   NO TURNOVER TO REPORT
     A**---------------------------------------------------------------
     A*
     A          R NOTRN
     A                                      SPACEB(1)
     A                                    43'** NO TURNOVER TO BE REPORTED -
     A                                      FOR DEPARTMENT **'
     A                                      SPACEA(3)
     A*                                              Begin /S01486/
     A*****************************************************************
     A*****NO*DETAILS*TO*REPORT****************************************
     A**---------------------------------------------------------------
     A**********
     A**********R NODET
     A**********                            SPACEB(3)
     A**********                          55'NO DETAILS TO REPORT'
     A**********                            SPACEA(3)
     A*                                              End   /S01486/
     A*****************************************************************
     A**   END OF REPORT
     A**---------------------------------------------------------------
     A*
     A          R ENDRE
     A                                      SPACEB(3)
     A*                                              Begin /S01486/
     A**********                          56'** END OF REPORT **'
     A                                    46'*** END OF REPORT FOR DEPT ***'
     A*
     A**  FORMAT FOR DATABASE ERROR REFERENCE
     A*
     A          R DBERROR
     A                                    32'*** REFERENCE PM0520'
     A                                      SPACEB(3)
     A                                    53'DATABASE ERROR ***'
     A*                                              End   /S01486/
