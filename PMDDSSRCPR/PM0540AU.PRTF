      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas PM T/O by Industry per Department Audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  PM0540AU - TURNOVER BY INDUSTRY PER DEPARTMENT - AUDIT       *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01486             Date 17Oct94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01486 - Portfolio Management Upgrade to Release 10          *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DOCUMENT HEADER
     A**---------------------------------------------------------------
     A*
     A          R PM0540Z1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                      SKIPB(3)
     A                                     6'REFERENCE'
     A                                    16'PM0540AU'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A            PAGE1          3  0    124EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      SPACEA(2)
     A*
     A                                    36'TURNOVER BY INDUSTRY SECTOR -
     A                                      PER DEPARTMENT AUDIT REPORT'
     A                                      SPACEA(1)
     A                                    36'-----------------------------
     A                                      ---------------------------'
     A*
     A                                      SPACEA(3)
     A**---------------------------------------------------------------
     A**   FORMAT FOR DATABASE ERROR MESSAGE.
     A**---------------------------------------------------------------
     A*
     A          R PM0540ER                  TEXT('DATABASE ERROR MSG')
     A                                 15 25'**** DATABASE ERROR ****'
     A                                 27 11'***************************+
     A                                      ***************************'
     A                                 28 11'*'
     A                                 28 64'*'
     A                                 29 11'*    FILE NAME    '''
     A            RRFILE         8     29 30
     A                                 29 38''''
     A                                 29 64'*'
     A                                 30 11'*    KEY          '''
     A            RRKEY         29     30 30
     A                                 30 59'''    *'
     A                                 31 11'*    PROGRAM NAME '''
     A            RRPGM         10     31 30
     A                                 31 40''''
     A                                 31 64'*'
     A                                 32 11'*    AT           '''
     A            RRBASE         3     32 30
     A                                 32 33''''
     A                                 32 64'*'
     A                                 33 11'*'
     A                                 33 64'*'
     A                                 34 11'***************************+
     A                                      ***************************'
     A**---------------------------------------------------------------
     A**   FORMAT FOR NO DETAILS MESSAGE.
     A**---------------------------------------------------------------
     A          R PM0540ND
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(4)
