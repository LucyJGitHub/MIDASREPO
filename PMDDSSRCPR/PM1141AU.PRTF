      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas PM SE Portfolio Holdings Update Audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                          *
     A*                                                               *
     A*  PRTF/PM1141AU - Portfolio Holdings Update Audit Report       *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CPM004             Date 08Feb96               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CPM004 - Bank Portfolios (NEW PROGRAM)                       *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DOCUMENT
     A**---------------------------------------------------------------
     A*
     A          R PM1141PP                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  1  6'REFERENCE PM1141AU'
     A            BJURPT        53      1 37
     A                                  1101'DATE'
     A            BJMRDT         7      1106
     A                                  1119'PAGE'
     A                                  1124PAGNBR
     A                                      EDTCDE(Z)
     A                                  3 40'PORTFOLIO HOLDINGS UPDATE (SE) +
     A                                      AUDIT REPORT'
     A                                  4 40'-------------------------------+
     A                                      ------------'
     A*
     A                                 12  7'PORTFOLIO HOLDINGS CREATED:'
     A            PPHCRE         5  0  12 52EDTCDE(3)
     A*
     A                                 14  7'MATURED PORTFOLIO HOLDINGS -
     A                                      CREATED:'
     A            PMHCRE         5  0  14 52EDTCDE(3)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DATABASE ERROR MESSAGE.
     A**---------------------------------------------------------------
     A*
     A          R ERRORAU                   TEXT('DATABASE ERROR MSG')
     A                                      SKIPB(3)
     A                                     6'REFERENCE PM1141AU'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    40'PORTFOLIO HOLDINGS UPDATE (SE) +
     A                                      AUDIT REPORT'
     A                                      SPACEA(1)
     A                                    40'-------------------------------+
     A                                      ------------'
     A                                      SPACEA(5)
     A*
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE        10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29A       30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
     A*
     A**---------------------------------------------------------------
     A**  Format for No Details To Report message
     A**---------------------------------------------------------------
     A*
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
