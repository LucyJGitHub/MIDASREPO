      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas PM Determine Portfolio Due Statement')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  PRTF/PM1390AU - Portfolio Due Statement Report Audit         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01486             DATE 11AUG94               *
     A*  PREV AMEND NO. B08268             DATE 22OCT91               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01486 - Portfolio Management Upgrade to Release 10.         *
     A*  B08268 - RESET NEXT POSITION DATE, PMCPOSPP TO ZERO          *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*                                              Begin /S01486/
     A                                      REF(MFRFL)
     A*                                              End   /S01486/
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DOCUMENT
     A**---------------------------------------------------------------
     A*
     A          R PM1390PP                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*                                              Begin /S01486/
     A***********                       1  7'REFERENCE'
     A***********                       1 17'PM1390AU'
     A                                  1  6'REFERENCE PM1390AU'
     A*                                              End   /S01486/
     A            BJURPT        53      1 37
     A                                  1101'DATE'
     A            BJMRDT         7      1106
     A                                  1119'PAGE'
     A                                  1124PAGNBR
     A                                      EDTCDE(Z)
     A                                  3 40'RESET NEXT POSITION DATE TO ZERO'
     A                                  4 40'--------------------------------'
     A*
     A                                 10 20'NUMBER OF POSITIONS UPDATED:'
     A            RRNORE         5  0  10 55EDTCDE(3)
     A*
     A*                                              Begin /S01486/
     A***********                      15 56'** END OF REPORT **'
     A*
     A**---------------------------------------------------------------
     A**   Format for Database Error message.
     A**---------------------------------------------------------------
     A*
     A          R DBERROR                   TEXT('DATABASE ERROR MSG')
     A                                      SKIPB(3)
     A                                     6'REFERENCE PM1390AU'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    40'RESET NEXT POSITION DATE TO ZERO'
     A                                      SPACEA(1)
     A                                    40'--------------------------------'
     A                                      SPACEA(3)
     A*
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE        10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 ''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
     A*
     A**---------------------------------------------------------------
     A**  Format for No Details To Report message
     A**---------------------------------------------------------------
     A*
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A*                                              End   /S01486/
