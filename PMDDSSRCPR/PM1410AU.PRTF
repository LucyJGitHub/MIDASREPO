      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas PM Gen. Fwd. Event for Portfolio Audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  PRTF/PM1410AU - Generate Forward Event for Portfolio Audit   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01486             DATE 11AUG94               *
     A*                                                               *
     A*****************************************************************
     A*                                              Begin /S01486/
     A                                      REF(MFRFL)
     A*                                              End   /S01486/
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR CONTROL REPORT
     A**---------------------------------------------------------------
     A*
     A          R PM1410AR
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A*                                              Begin /S01486/
     A***********                          7'REFERENCE'
     A***********                         17'PM1410AU'
     A                                     6'REFERENCE PM1410AU'
     A*                                              End   /S01486/
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    29'PORTFOLIO MANAGEMENT - GENERATE FO-
     A                                      RWARD EVENTS FOR PORTFOLIOS'
     A                                      SPACEA(1)
     A                                    29'-----------------------------------
     A                                      ---------------------------'
     A                                      SPACEA(5)
     A*
     A                                    10'NUMBER OF CASHFLOW EVENTS GENER-
     A                                      ATED'
     A            PPCFEV         5  0     52EDTCDE(3) SPACEA(7)
     A*                                              Begin /S01486/
     A***********                         56'** END OF REPORT **'
     A*
     A**---------------------------------------------------------------
     A**   Format for Database Error message.
     A**---------------------------------------------------------------
     A*
     A          R ERRORAU
     A                                      SKIPB(3)
     A                                     6'REFERENCE PM1410AU'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    29'PORTFOLIO MANAGEMENT - GENERATE FO-
     A                                      RWARD EVENTS FOR PORTFOLIOS'
     A                                      SPACEA(1)
     A                                    29'-----------------------------------
     A                                      ---------------------------'
     A                                      SPACEA(3)
     A*
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE        10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 ''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
     A*
     A**---------------------------------------------------------------
     A**  Format for No Details To Report message
     A**---------------------------------------------------------------
     A*
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A*                                              End   /S01486/
