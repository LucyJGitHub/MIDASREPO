      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Portfolio Perf. Reconciliation Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  PRTF/PM1661P1 - Portfolio Perfromance Reconcilation Report   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. S01486             DATE 01NOV94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  S01486 - Portfolio Management Upgrade to Release 10          *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                               REF(MFRFL)
     A*                                                                  *
     A*   HEADER
     A***
     A          R PM1661H                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A***
     A                                      SKIPB(003)
     A                                     6'REFERENCE PM1661P1'
     A            RRURPT        53A  O    37
     A                                      TEXT('User report title')
     A                                   101
     A                                      'DATE'
     A            RRMRDT         7A  O   106
     A                                      TEXT('Midas Run Day Date')
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A                                    42
     A                                      'PORTFOLIO PERFORMANCE RECONCILIATI-
     A                                      ON REPORT'
     A  45                                89'CONSOLIDATED'
     A                                   132' '
     A                                      SPACEA(1)
     A                                    42
     A                                      '-----------------------------------
     A                                      ---------'
     A                                      SPACEA(2)
     A*
     A                                     4'DEPARTMENT'
     A            RRDPCD         3A       15
     A            RRDPNM    R             19REFFLD(BRNM)
     A                                    51'ACCOUNT OFFICER'
     A            RRACOC         2A       67
     A            RRACON    R             70REFFLD(BRNM)
     A                                   132' '
     A                                      SPACEA(1)
     A                                     1
     A                                      '    CUSTOMER                    -
     A                                                        PORTFOLIO   <----
     A                                      ------- FIGURES IN PORTFOLIO CUR-
     A                                      RENCY ---------->'
     A                                      SPACEB(002)
     A                                     1
     A                                      'NUMBER/SHORTNAME  PORTFOLIO   PE-
     A                                      RFORMANCE PERIOD   CURRENCY       -
     A                                        RETURN      TOTAL COMPONENTS -
     A                                              DIFFERENCE '
     A                                      SPACEB(001)
     A                                     1
     A                                      ' '
     A                                      SPACEB(001)
     A*****************************************************************
     A*   DETAIL
     A***
     A          R PM1661D
     A***
     A                                      SPACEB(001)
     A                                      TEXT('Detail')
     A            RRCUST         6A  O     1
     A                                      TEXT('Customer Number')
     A  70                                  HIGHLIGHT
     A                                     7
     A                                      '/'
     A            RRCSSN        10A  O     8
     A                                      TEXT('Customer Shortname')
     A  70                                  HIGHLIGHT
     A            RRPTFR         4A  O    21
     A                                      TEXT('Portfolio Reference')
     A  70                                  HIGHLIGHT
     A            RRPFPN        18A  O    31
     A                                      TEXT('Performance Period Text')
     A  70                                  HIGHLIGHT
     A            RRPTCY         3A  O    54
     A                                      TEXT('Portfolio Currency')
     A  70                                  HIGHLIGHT
     A            RRRETR        16A  O    61
     A                                      TEXT('Return')
     A  70                                  HIGHLIGHT
     A            RRSUMC        16A  O    80
     A                                      TEXT('Sum of Components')
     A  70                                  HIGHLIGHT
     A            RRDIFF        16A  O    99
     A                                      TEXT('Difference')
     A  70                                  HIGHLIGHT
     A  70                               120'***ERROR***'
     A  70                                  HIGHLIGHT
     A*****************************************************************
     A*
     A* Database Error
     A* --------------
     A          R PM1661DB
     A                                    32'*** REFERENCE PM1661'
     A                                      SPACEB(3)
     A                                    53'DATABASE ERROR ***'
     A*****************************************************************
     A*   END OF REPORT
     A***
     A          R PM1661E
     A***
     A                                      SPACEB(003)
     A                                      TEXT('End of Report')
     A
     A                                    51'*** END OF REPORT ***'
     A**************************************************************************
