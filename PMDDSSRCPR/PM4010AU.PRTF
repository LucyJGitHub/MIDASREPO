      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas PM Reconcile CAPRHD Agnst TRADSD&DPTMVD1')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  PRTF/PM4010AU - Reconcile CAPRHD Actions Against TRADSD      *
     A*                  and DPTMVD - Run Controls                    *
     A*                                                               *
     A*  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  PREV AMEND NO. S01486 *CREATE     DATE 11AUG94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  MD046248 - Finastra Rebranding                               *
     A*  S01486 - Portfolio Management Upgrade to Release 10.         *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A*
     A** Report header
     A*
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE PM4010AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A*
     A                                    43'INCORRECT NUMBER ACTIONS ON CA+
     A                                      PRHD- RUN CONTROLS'
     A                                      SPACEA(1)
     A                                    43'------------------------------+
     A                                      ------------------'
     A                                      SPACEA(3)
     A*
     A** File control
     A*
     A          R CONTROL
     A                                    54'---------- PROGRAM ----------'
     A                                      SPACEB(3)
     A                                     7'NUMBER OF RECORDS +
     A                                      - LF/TRADHS       '
     A                                      SPACEB(3)
     A            SNORE          5  0     65EDTWRD('  , 0 ')
     A                                     7'NUMBER OF RECORDS +
     A                                      - PF/DPTMVD       '
     A                                      SPACEB(2)
     A            SNORE1         5  0     65EDTWRD('  , 0 ')
     A*
     A** Database Error
     A*
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'**********************************+
     A                                      ********************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE        10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29A       30
     A                                    59''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'**********************************+
     A                                      ********************'
     A                                      SPACEB(1)
     A*
     A** No Details
     A*
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
