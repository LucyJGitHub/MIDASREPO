      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Portfolio Position Reconciliation Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  PRTF/PM4201P1 - PORTFOLIO POSITION RECONCILIATION REPORT     *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01486             Date 07Dec94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01486 - Portfolio Management Upgrade to Release 10          *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A*
     A**  Header Format
     A*
     A          R HEAD
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  3  6'REFERENCE PM4201P1'
     A            BJURPT        53      3 37
     A                                  3101'DATE'
     A            BJMRDT         7      3106
     A                                  3119'PAGE'
     A            PAGE           4  0   3124EDTCDE(Z)
     A                                  4101'TIME'
     A                                  4106TIME
     A*
     A                                  5 34'PORTFOLIO POSITION RECONCILIATION +
     A                                      REPORT'
     A                                  6 34'----------------------------------+
     A                                      ------'
     A  50                              6 89'CONSOLIDATED'
     A*
     A                                  8  4'ACCOUNT OFFICER'
     A            RRACOF         2      8 20TEXT('Account officer')
     A            RRACON        30      8 23TEXT('Account officer name')
     A                                  9  4'CUSTOMER'
     A            RRCUST         6      9 18TEXT('Customer number')
     A            RRCSSN        10      9 26TEXT('Customer short name')
     A            RRCRNM        20      9 38TEXT('Customer report name')
     A                                 10  4'PORTFOLIO'
     A            RRPTFR         4     10 20TEXT('Portfolio reference')
     A            RRPTFN        20     10 26TEXT('Portfolio narrative')
     A                                 10 48'PERIOD'
     A            RRPERI        17     10 55TEXT('Performance period')
     A                                 10 74'PORTFOLIO CURRENCY'
     A            RRPTCY         3     10 93TEXT('Portfolio currency')
     A                                 10 98'DIFFERENCE'
     A            RRDIFF        18     10109TEXT('Difference')
     A                                 11  1' '
     A                                 12 46'MODULE'
     A                                 12 60'<---------------- ALL FIGURES IN P+
     A                                      ORTFOLIO CURRENCY ---------------->'
     A                                 13  7'INSTRUMENT'
     A                                 13 20'REFERENCE/SHORTNAME'
     A                                 13 48'ID'
     A                                 13 69'RETURN'
     A                                 13 87'SUM OF COMPONENTS'
     A                                 13117'DIFFERENCE'
     A                                 14  1' '
     A*
     A**  Detail Format
     A*
     A          R DETAIL                    TEXT('Detail')
     A*
     A                                      SPACEB(1)
     A            RRINNR         3        10TEXT('Instrument')
     A            RRRESH        20        20TEXT('Ref./Shortname')
     A            RRMODI         2        48TEXT('Module Id')
     A            RRRETU        18        60TEXT('Return')
     A            RRSUCO        18        86TEXT('Sum of Components')
     A            RRDIFA        18       111TEXT('Difference')
     A*
     A**  End of Report Format
     A*
     A          R ENDREP                    TEXT('End of report')
     A*
     A                                      SPACEB(3)
     A                                    51'*** END OF REPORT ***'
     A*
     A**  Format for database error reference
     A*
     A          R DBERROR
     A                                    32'*** REFERENCE PM4201'
     A                                      SPACEB(3)
     A                                    53'DATABASE ERROR ***'
