/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas PM  Portf. Capital Flow Recon Rep Match Itms')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Portfolio Management Module                             *
     A*                                                                  *
     A*  PRTF/PM4210P4 - PORTFOLIO CAPITAL FLOW RECONCILIATION REPORT    *
     A*                  PROBABLE MATCHED ITEMS                          *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. 097389 *CREATE     DATE 15MAR96                  *
     A*                                                                  *
     A********************************************************************
     A*                                                                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  097389 - Matching is not done between transactions on reports   *
     A*           Output new report showing 'PROBABLE MATCHES'           *
     A*                                                                  *
     A********************************************************************
     A                                      REF(MFRFL)
     A*
     A*   HEADER 1
     A*
     A*****************************************************************
     A          R HEAD41                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                      SKIPB(3)
     A                                     6'REFERENCE PM4210P4'
     A            BJURPT        53        37
     A                                   101'DATE'
     A            BJMRDT         7       106
     A                                   119'PAGE'
     A            PAGE4          4  0    124EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      SPACEA(2)
     A*
     A N61                                31'PORTFOLIO CAPITAL FLOW RECONCILIAT+
     A                                      ION REPORT - PROBABLE MATCHES TODAY'
     A                                      SPACEA(1)
     A  61                                34'PORTFOLIO CAPITAL FLOW RECONCILIAT+
     A                                      ION REPORT - PROBABLE MATCHES'
     A                                      SPACEA(1)
     A N61                                31'----------------------------------+
     A                                      ----------------------------------'
     A                                      SPACEA(1)
     A  61                                34'----------------------------------+
     A                                      ----------------------------'
     A                                      SPACEA(1)
     A*
     A  50 55                             89'CONSOLIDATED'
     A                                      SPACEA(1)
     A*
     A  56                                89'DEPT -'
     A  56        RRDPC4    R             96REFFLD(DPCD)
     A  56        RRDPN4    R            102REFFLD(BRNM)
     A                                      SPACEA(1)
     A*
     A  57                                89'A/C OFR -'
     A  57        RRACC4    R             99REFFLD(ACOF)
     A  57        RRACN4    R            102REFFLD(BRNM)
     A                                      SPACEA(1)
     A*
     A                                     2'ACCOUNT OFFICER'
     A                                      SPACEB(1)
     A            RRACOF         2        20TEXT('Account officer')
     A            RRACON        30        23TEXT('Account officer name')
     A                                      SPACEA(2)
     A                                     2'<---------------------'
     A                                    25'CAPITAL OUTFLOWS'
     A                                    42'----------------------->'
     A                                    67'<-----------------------'
     A                                    92'CAPITAL INFLOWS'
     A                                   108'------------------------>'
     A                                      SPACEA(1)
     A                                     3'DATE    INSTR. REFERENCE/SHORTNAME+
     A                                          MODULE  TOTAL AMOUNT'
     A            RRCCY1         3        62TEXT('Amount currency')
     A            RRCCY2         3        68TEXT('Amount currency')
     A                                    72'TOTAL AMOUNT    INSTR.  REFERENCE/+
     A                                      SHORTNAME  MODULE   DATE'
     A                                      SPACEA(1)
     A                                     3'TRANSACTION   CAP.CHG.  EVENT REF -
     A                                       EVENT CAT. EVENT AMOUNT'
     A            RRCCY3         3        62TEXT('Amount currency')
     A            RRCCY4         3        68TEXT('Amount currency')
     A                                    72'EVENT AMOUNT  EVENT CAT  EVENT REF+
     A                                      .   CAP.CHG.  TRANSACTION'
     A*
     A*****************************************************************
     A*
     A*   HEADER 2
     A*
     A*****************************************************************
     A          R HEAD42                    TEXT('Header 2')
     A*
     A                                     2'CUSTOMER'
     A                                      SPACEB(2)
     A            RRCUST         6        16TEXT('Customer number')
     A            RRCSSN        10        23TEXT('Customer short name')
     A            RRCRNM        20        34TEXT('Customer report name')
     A                                     2'PORTFOLIO'
     A                                      SPACEB(1)
     A            RRPTFR         4        18TEXT('Portfolio reference')
     A            RRPTFN        20        23TEXT('Portfolio narrative')
     A  61                                45'PERIOD'
     A  61        RRPERI        17        52TEXT('Performance period')
     A  61                                71'PORTFOLIO CURRENCY'
     A  61        RRPTCY         3        90TEXT('Portfolio currency')
     A  61                                95'DIFFERENCE'
     A  61        RRDIFF        18       106TEXT('Difference')
     A N61                                47'PORTFOLIO CURRENCY'
     A N61        RRPTCX         3        66TEXT('Portfolio currency')
     A                                     1' '
     A                                      SPACEB(1)
     A**************************************************************************
     A*
     A*   DETAIL 1
     A*
     A**************************************************************************
     A          R DET41                     TEXT('Detail 1')
     A*
     A N66        RRDATO         7         2TEXT('Date out')
     A                                      SPACEB(2)
     A  66        RRDATX         7         2TEXT('Date out')
     A                                      SPACEB(1)
     A            RRINTO         3        12TEXT('Instr. out')
     A**Start of change CGL029
     A**********  RRREFO        20        18TEXT('Ref./Shortn. out')                          CGL029
     A**********  RRFRIO         2        41TEXT('Module out')                                CGL029
     A            RRREFO        26        16TEXT('Ref./Shortn. out')
     A            RRFRIO         2        43TEXT('Module out')
     A**End of change CGL029
     A            RRTOAO        16        49TEXT('Total amount out')
     A            RRTOAI        16        68TEXT('Total amount in')
     A            RRINTI         3        89TEXT('Instr. in')
     A**Start of change CGL029
     A**********  RRREFI        20        96TEXT('Ref./Shortn. in')                           CGL029
     A**********  RRFRII         2       119TEXT('Module in')                                 CGL029
     A            RRREFI        26        93TEXT('Ref./Shortn. in')
     A            RRFRII         2       120TEXT('Module in')
     A**End of change CGL029
     A            RRDATI         7       125TEXT('Date in')
     A*
     A**************************************************************************
     A*
     A*   DETAIL 2
     A*
     A**************************************************************************
     A          R DET42                     TEXT('Detail 2')
     A*
     A            RRTRAO         6         6TEXT('Transaction out')
     A                                      SPACEB(1)
     A            RRCAPO         1        20TEXT('Cap.Chg. out')
     A            RREVNO         7        28TEXT('Event ref. out')
     A            RRCATO         2        41TEXT('Event cat. out')
     A            RREVAO        16        49TEXT('Event amount out')
     A            RREVAI        16        68TEXT('Event amount in')
     A            RRCATI         2        90TEXT('Event cat. in')
     A            RREVNI         7        98TEXT('Event ref. in')
     A            RRCAPI         1       113TEXT('Cap.Chg. in')
     A            RRTRAI         6       125TEXT('Transaction in')
     A*
     A**************************************************************************
     A*
     A*   END OF REPORT
     A*
     A**************************************************************************
     A          R ENDREP4                   TEXT('End of report')
     A*
     A                                      SPACEB(3)
     A N56N57                             51'*** END OF REPORT ***'
     A  56                                46'*** END OF REPORT FOR DEPT ***'
     A  57                                46'*** END OF REPORT FOR A/C OFR ***'
     A**************************************************************************
     A*
     A**  FORMAT FOR DATABASE ERROR REFERENCE
     A*
     A**************************************************************************
     A          R DBERR4
     A                                    32'*** REFERENCE PM4210'
     A                                      SPACEB(3)
     A                                    53'DATABASE ERROR ***'
     A**************************************************************************
