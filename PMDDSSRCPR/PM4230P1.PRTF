      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Depot Movements Link Mtnc Audit Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Portfolio Management Module                      *
     A*                                                               *
     A*  PRTF/PM4230P1 - DEPOT MOVEMENTS LINK MAINTENANCE             *
     A*                  DETAIL REPORT                                *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01486             Date 11Aug94               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  S01486 - Portfolio Management Upgrade to R10.                *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*           C R E A T I O N     P A R A M E T E R S             *
     A*
     A*****************************************************************
     A*   HEADER
     A***
     A***********                                    Begin /S01486/
     A**********R PM4230H1
     A          R PM4230H1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A***********                                    End   /S01486/
     A***
     A                                      SKIPB(003)
     A                                     7
     A                                      'REFERENCE PM4230P1'
     A            RRURPT        53A  O    37
     A                                      TEXT('User report title')
     A                                   101
     A                                      'DATE'
     A            RRMRDT         7A  O   106
     A                                      TEXT('Midas Run Day Date')
     A                                   119
     A                                      'PAGE'
     A            PAGE2          3  0O   124
     A                                      EDTCDE(Z)
     A                                    40
     A                                      'DEPOT MOVEMENTS LINK MAINTENANCE A-
     A                                      UDIT REPORT'
     A                                      SPACEB(002)
     A                                    40
     A                                      '-----------------------------------
     A                                      -----------'
     A                                      SPACEB(001)
     A                                     3
     A                                      'CUSTOMER  FOLIO   <------- WALK OU-
     A                                      T REFERENCES ------->   <------- WA-
     A                                      LK IN REFERENCES -------->      L A-
     A                                       S T    A C T I V I T Y   '
     A                                      SPACEB(002)
     A                                     3
     A                                      '                      MOVEMENT 1  -
     A                                              MOVEMENT 2          MOVEMEN-
     A                                      T 1          MOVEMENT 2          TY-
     A                                      PE    DATE      USER      '
     A                                      SPACEB(001)
     A*****************************************************************
     A*   DETAIL
     A***
     A          R PM4230D1
     A***
     A                                      SPACEB(001)
     A                                      TEXT('Detail')
     A  70                                 1
     A                                      ' '
     A                                      SPACEB(001)
     A  70        RRCNUM         6A  O     3
     A                                      TEXT('Customer number')
     A  70        RRPTFR         4A  O    13
     A                                      TEXT('Portfolio reference')
     A            RRWOS1        10A  O    21
     A                                      TEXT('Out Security Shortname 1')
     A            RRWOR1         6A  O    32
     A                                      TEXT('Out Movement Reference 1')
     A            RRWOS2        10A  O    41
     A                                      TEXT('Out Security Shortname 2')
     A            RRWOR2         6A  O    52
     A                                      TEXT('Out Movement Reference 2')
     A            RRWIS1        10A  O    61
     A                                      TEXT('In Security Shortname 1')
     A            RRWIR1         6A  O    72
     A                                      TEXT('In Movement Reference 1')
     A            RRWIS2        10A  O    81
     A                                      TEXT('In Security Shortname 2')
     A            RRWIR2         6A  O    92
     A                                      TEXT('In Movement Reference 2')
     A            RRCHTX         6A  O   104
     A                                      TEXT('Type of Last Change')
     A            RRLCD          7A  O   112
     A                                      TEXT('Last Change Date')
     A            RRUSER        10A  O   122
     A                                      TEXT('User ID')
     A*****************************************************************
     A*   END OF REPORT
     A***
     A          R PM4230E1
     A***
     A                                      SPACEB(003)
     A                                      TEXT('End of Report')
     A                                    52
     A                                      '*** END OF REPORT ***'
     A**************************************************************************
