     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Pool Portfolio File Setup')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management Module                      *
     F*                                                               *
     F*  PM0002 - PM Pool Portfolio Files Setup                       *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01486  *CREATE    Date 25Nov94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     F*  S01486 - Portfolio Management Enhancements (New program)     *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
     FPMPPTDPAUF  E                    DISK                      A
     F*
     E**  For compilation - Copyright Insertion
     E*
     E                    CPY@    1   1 80
     E*
     I*
     ILDA         DS                            256
     I                                      132 183 DBLOT
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     ISDMMOD    E DSSDMMODPD
     I**  Data structure for module details
     I*
     ISDPORT    E DSSDPORTPD
     I**  Data structure for portfolio management details
     I*
     IDSFDY     E DSDSFDY
     I**  First data structure for access objects, short data structure
     I*
     C/EJECT
     C*
     C**  Copyright statement
     C*
     C                     MOVEACPY@      ACT@   80
     C*
     C**  Entry parameter
     C*
     C           *ENTRY    PLIST
     C                     PARM           WLRTRN  1
     C*
     C           *NAMVAR   DEFN           LDA
     C*
     C**  Initialise return code
     C*
     C                     MOVE *BLANK    WLRTRN
     C*
     C**  Get module details using access object
     C*
     C                     CALL 'AOMMODR0'
     C                     PARM *BLANKS   WLRTCD  7
     C                     PARM '*FIRST ' WLOPTN  7
     C           SDMMOD    PARM SDMMOD    DSFDY
     C*
     C           WLRTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE            ***************
     C                     MOVE 'SDMMODPD'DBFILE           * DB ERROR 01 *
     C                     MOVEL'PM0002'  DBPGM            ***************
     C                     MOVELWLOPTN    DBKEY
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
     C*
     C**  If Portfolio Management is present, get PM details
     C**  using access object
     C*
     C           BGPFMG    IFEQ 'Y'
     C*
     C                     CALL 'AOPORTR0'
     C                     PARM *BLANKS   WLRTCD
     C                     PARM '*FIRST ' WLOPTN
     C           SDPORT    PARM SDPORT    DSFDY
     C*
     C           WLRTCD    IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE            ***************
     C                     MOVE 'SDPORTPD'DBFILE           * DB ERROR 02 *
     C                     MOVEL'PM0002'  DBPGM            ***************
     C                     MOVELWLOPTN    DBKEY
     C                     OUT  LDA
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
     C*
     C**  If Pool Portfolio is installed, read audit file PMPPTDPA
     C*
     C           FCPPPR    IFEQ 'Y'
     C                     READ PMPPTDPA                 50
     C*
     C**  If no record found, write a record to it
     C*
     C           *IN50     IFEQ '1'
     C                     MOVE 'Z'       ORRECI
     C                     Z-ADD*ZEROS    ORNORE
     C                     WRITEPMPPTDZ0
     C                     ELSE
     C*
     C**  Else, record already exists so move '3' to return code
     C                     MOVE '3'       WLRTRN
     C                     END
     C*
     C                     ELSE
     C*
     C**  If Pool Portfolio not installed, move '2' to return code
     C                     MOVE '2'       WLRTRN
     C                     END
     C*
     C                     ELSE
     C*
     C**  If Portfolio Management not present, move '1' to return code
     C                     MOVE '1'       WLRTRN
     C                     END
     C*
     C**  Return to calling program
     C*
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
**  CPY@
(c) Finastra International Limited 2001
