     H
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Display Group Maintenance')
/*OVRF*: OVRDBF FILE(PMDSPGXX) TOFILE(PMDSPGPD) SHARE(*NO)          : *   S01486
     F********************************************************************
     F*                                                                  *
     F*  Midas PORTFOLIO MANAGEMENT                                      *S01486
     F*                                                                  *
     F*  PM0010  - DISPLAY GROUP MAINTENANCE                             *S01486
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *S01486
     F*   Function : This program maintains the Display Groups. Display  *S01486
     F*   Groups are used to arrange the transactions according to       *S01486
     F*   specific categories.                                           *S01486
     F*   Called by : PMC0010                                            *S01486
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01486             Date 27Jun94               *
     F*                 B9996      ADIF    Date 01OCT91               *S01486
     F*                 R00300             DATE 10AUG90                  *S01486
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  S01486 - Portfolio Management Upgrade to R10.                   *S01486
     F*  B9996  - PGM ACCEPTS TO DELETE DISPLAY GROUP ATTACHED TO        *S01486
     F*            LIVE INSTRUMENTS                                      *S01486
     F*  R00300 - Message file changes.                                  *S01486
     F*                                                                  *
     F********************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*:*OVRDBF FILE(PMDSPGXX) TOFILE(PMDSPGPP) SHARE(*NO)            *   S01486
     F*                                                               *
     F*****************************************************************
     F***PM0010DDCF**E                    WORKSTN                         S01486
     FPM0010DFCF  E                    WORKSTN                            S01486
     F                                        RRN   KSFILE PM0010S0
     F***PMDSPGPPUF**E           K        DISK         KCOMIT       A     S01486
     FPMDSPGPDUF  E           K        DISK         KCOMIT       A        S01486
     F                                              KINFDS INFDS
     F                                              KINFSR *PSSR
     FPMDSPGXXIF  E           K        DISK
     F                                              KINFSR *PSSR
     F            PMDSPGP0                          KRENAMEPMDSPGPX
     F*
     F***RECORD*ID*OF FILE SDBANKPD = 'BJ'                                S01486
     F***SDBANKL1IF**E           K        DISK                            S01486
     F***************                               KINFSR *PSSR          S01486
     F***PMDSPGZZUF**E                    DISK         KCOMIT             S01486
     FPMDSPGPAUF  E                    DISK         KCOMIT                S01486
     F                                              KINFSR *PSSR
     F***SDPLINL2IF**E           K        DISK                      B9996 S01486
     F***************                               KINFSR *PSSR    B9996 S01486
     FSDPLINL2IF  E           K        DISK                           UC  S01486
     F                                              KINFSR *PSSR          S01486
     F*
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*       07         START  SCREEN
     F*       08         REVIEW SCREEN
     F*       09         UPDATE SCREEN
     F*
     F*       15         INDICATOR USED ON WRITE/UPDATE OPS TO CONDITION
     F*                                    CALL OF *PSSR
     F*       30         S/PM0000F1  PROTECT/NON-DISPLAY KEY FIELDS
     F*       31         S/PM0000F1  ERROR-DDACTN
     F*       32         S/PM0000F1  ERROR-DDDSPG
     F*       35         S/PM0000F0  PROTECT DETAIL FIELDS
     F*       50         S/PM0000C0  SFLDSP
     F*       51         S/PM0000C0  SFLDSPCTL
     F*       52         S/PM0000C0  SFLEND
     F*       53         S/PM0000C0  ROLLDOWN NOT ALLOWED
     F*
     F*
     F*       45         VLDCMDKY INDICATOR FOR DISPLAY FILE
     F*
     F*       71         LOKUP OPERATION
     F*       72         FILE OPERATION
     F*       73         FILE OPERATION
     F*       74         FILE OPERATION
     F*       75         ERROR ON SCREEN
     F*       82         REVIEW FROM REQUESTED
     F*       88         ERROR ON NUMERIC TEST
     F**      89         ERROR ON NUMERIC TEST
     F*
     F*       U7         DATABASE ERROR
     F*       U8         PROGRAM  ERROR
     F*
     F*
     F*       03         CA3
     F*       10         CA10
     F*       12         CA12
     F*       25         ROLLUP
     F*       26         ROLLDOWN
     F*
     F**             *************************
     F**             ** INDICATORS NOT USED **
     F**             ** ------------------- **
     F**             *************************
     F**
     F**      ***************************************************
     F**      * 01   02   ..   04   05   06   ..   ..   ..   .. *
     F**      * 11   ..   13   14   ..   16   17   18   19   20 *
     F**      * 21   22   23   24   ..   ..   27   28   29   .. *
     F**      * ..   ..   33   34   ..   ..   ..   38   39   40 *
     F**      * 41   42   43   44   ..   46   47   48   49   .. *
     F**      * ..   ..   ..   54   55   56   57   58   59   60 *
     F**      * 61   62   63   64   65   66   67   68   69   70 *
     F**      * ..   ..   ..   ..   ..   76   77   78   79   80 *
     F**      * 81   ..   83   84   85   86   87   88   89   90 *
     F**      * 91   92   93   94   95   96   97   98   99      *
     F**      ***************************************************
     F*********************************************************************
     E                    CPY@    1   1 80
     E*
     E** ARRAY TO TEST DISPLAY GROUP INPUT
     E*
     E                    DSP         3  1
     E*
     E** ARRAY FOR NUMERIC TEST
     E*
     E                    NUMARA 10  10  1
     I*
     I** KEY FIELDS (TAKEN FROM 'KEY' PORTION OF SCREEN)
     I*
     ISCRCTL      DS
     I                                        1   1 WWREVW
     I                                        2   2 DDACTN
     I                                        3   5 DDDSPG
     I                                        6   8 DDPKEY
     I                                        9  11 WWPAG1
     I                                       12  14 WWPAG2
     I*
     I*    MIDAS 'Next Available Transaction No.' Data Area  MNATN .
     IDNATN       DS                              5
     I                                        1   50FNATN
     I*
     I*    NUMERIC ARRAY DS DEFINITION
     I            DS
     I                                        1   2 WWWORK
     I                                        1   1 WWORK1
     I                                        2   2 WWORK2
     I*
     I*
     I** DATA STRUCTURE FOR RPG 01021 ERROR HANDLING
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I*
     I*
     I** LOCAL DATA AREA FOR DATABASE ERROR DETAILS
     I*
     I****LDA********UDS                            256                   S01486
     I***************                       132 183 DBLOT                 S01486
     I***************                       132 141 DBFILE                S01486
     IWWLDA       DS                            256                       S01486
     I                                      134 183 DBLOT                 S01486
     I                                      134 141 DBFILE                S01486
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I** DATA STRUCTURE FOR SETUP OF COMMIT TEXT
     I*
     I*
     ICMTTXT      DS
     I                                        1   50NATN
     I                                        6   7 MDID
     I                                        8  15 PGMN
     I                                       16  18 WSIDX
     I                                       19  240MTIME
     I                                       25  25 ACTNX
     I                                       26  35 USIDX
     I**
     I            DS
     I                                        1   20WWDLUP
     I                                        3   5 WWMLUP
     I                                        6   70WWYLUP
     I                                        1   7 WWHLUP
     I*
     I** PROGRAM STATUS DATA STRUCTURE FOR WS/USER ID'S
     I*
     I*
     IPSDS       SDS
     I                                      244 253 WSID
     I                                      254 263 USID
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     ISDBANK    E DSSDBANKPD                                              S01486
     I** EXTERNAL DS FOR BANK DETAILS                                     S01486
     IDSFDY     E DSDSFDY                                                 S01486
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01486
     IRUNDT       DS                                                      S01486
      **  DATA AREA RUNDAT                                                S01486
     I                                        1   7 RUNA                  S01486
     I                                    P   8  100RUND                  S01486
     I                                       11  11 SSF                   S01486
     I                                       12  12 DATF                  S01486
     I                                       13  13 MBIN                  S01486
     IZMUSER      DS                             17                       S01486
     I** DATA AREA OF MENU USER                                           S01486
     I                                       11  13 USRBCH                S01486
     I**
      /EJECT
     C*
     C*****************************************************************
     C*  P R O G R A M     S T A R T
     C*****************************************************************
     C*
     C** PERFORM SETUP OF STANDARD FIELDS
     C*
     C                     EXSR #A
     C*
     C** D O    U N T I L   C K 3   O R   E R R O R
     C*
     C           AID       DOUEQ'03'
     C*
     C           *IN07     CASEQ'1'       #BA
     C*
     C           *IN08     CASEQ'1'       #BB
     C*
     C           *IN09     CASEQ'1'       #C
     C*
     C                     END
     C                     END
     C*
     C** TERMINATE PROGRAM
     C*
     C                     SETON                         LR
     C*****************************************************************
     C*  P R O G R A M     E N D
     C*****************************************************************
      /EJECT
     C*****************************************************************
      ** INDEX OF SUBROUTINES
      **
      ** #A                INITIALISATION OF STANDARD FIELDS,
      **                   ACCESS OF STANDING DATA
      **
      ** #BA               OUTPUT AND PROCESS KEY FIELDS SCREEN
      **
      ** #BB               PROCESS SUBFILE SELECTION SCREEN
      **
      ** #C                PROCESS DETAIL SCREEN AND UPDATE FILE
      **
      ** #CA               UPDATE FILE RECORDS
      **
      ** #CAA              PROCESS UPDATE BY ANOTHER WORKSTATION
      **
      ** #CB               FORMAT RECORD FIELDS FOR SCREEN
      **
      ** #CC               VALIDATE DETAIL SCREEN
      **
      ** #ZBAA             VALIDATE KEY FIELDS
      **
      ** #ZBAB             VALIDATE COMMAND KEYS
      **
      ** ZTNLU1            ACCESS DATAAREA FOR NEXT TRANSACTION NO.
      **
      ** *PSSR             FILE ERROR HANDLING S/R
      **
      ** ZASNMS            SENDS MESSAGE TO PROGRAM MESSAGE QUEUE.
      **
     C*****************************************************************
      /EJECT
     C*****************************************************************
      **
      ** #A   S/R - TO INITIALISE STATIC FIELDS AND ACCESS STANDING DATA
      ** CALLED ONCE AT START OF PROGRAM FROM MAIN LINE
      ** CALLS NO OTHER ROUTINES
      **
     C*****************************************************************
     C*
     C           #A        BEGSR                           ** #A    BSR **
     C*
     C                     MOVEL'*'       DDPGMQ
     C*                                                                   R00300
     C**   Select the program MSGQ for error msg.                         R00300
     C                     MOVEL'*'       TOPQ                            R00300
     C                     MOVEL'*PRV'    TOPRQ                           R00300
     C*                                                                   R00300
     C**   Fill in fields for subroutine ZASNMS                           R00300
     C                     MOVEL*BLANK    ZAPGM  10        Message queue  R00300
     C                     MOVEL'PMUSRMSG'ZAMSGF 10        Message file   R00300
     C                     MOVEL*BLANK    ZAPGRL  5        Rel queue      R00300
     C                     MOVEL*BLANK    ZAMSID  7        Message Id.    R00300
     C                     MOVEL*BLANK    ZAMSDA132        Message data.  R00300
     C                     MOVEL*BLANK    ZAMSTP  7        Message type.  R00300
      ***  Copyright Statement.                                           S01486
     C                     MOVEACPY@      ACT@   80                       S01486
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           S01486
     C                     IN   RUNDT                                     S01486
     C           *NAMVAR   DEFN           ZMUSER                          S01486
     C                     IN   ZMUSER                                    S01486
     C*
     C** PREPARE LDA
     C*
     C           *NAMVAR   DEFN LDA       WWLDA                           S01486
     C           *LOCK     IN   WWLDA                                     S01486
     C                     MOVE *BLANK    DBFILE                          S01486
     C                     MOVE *BLANK    DBKEY                           S01486
     C                     MOVE *BLANK    DBPGM                           S01486
     C                     Z-ADD*ZERO     DBASE                           S01486
     C                     MOVEL'PM0010'  DBPGM                           S01486
     C                     OUT  WWLDA                                     S01486
     C*************        MOVEL*BLANK    DBLOT                           S01486
     C*
     C** PREPARE COMIT TEXT
     C*
     C                     MOVEL'PM'      MDID
     C                     MOVEL'PM0010'  PGMN
     C                     MOVELWSID      WSIDX
     C                     MOVELUSID      USIDX
     C**
     C**  SET UP PARAMETER TO RETRIEVE MESSAGE FROM MESSAGE FILE
     C           PLLANG    PLIST
     C                     PARM           MSMBR4  7
     C                     PARM           MSGER  10
     C                     PARM           MSGTXT 80
     C***********          MOVEL'MLGBMSGF'MSGER                           S01486
     C                     MOVEL'PMUSRMSG'MSGER                           S01486
     C*
     C** GET INSTALLATION CONTROL DATA RECORD 1
     C*
     C                     CALL 'AOBANKR0'                                S01486
     C                     PARM *BLANKS   WWRTCD  7                       S01486
     C                     PARM '*FIRST ' WWOPTN  7                       S01486
     C           SDBANK    PARM SDBANK    DSFDY                           S01486
     C           WWRTCD    IFNE *BLANKS                                   S01486
     C           *LOCK     IN   WWLDA                                     S01486
     C                     MOVE 'SDBANKPD'DBFILE           ***************S01486
     C                     Z-ADD1         DBASE            * DB ERROR 01 *S01486
     C                     MOVELWWOPTN    DBKEY            ***************S01486
     C                     MOVELWWOPTN    DBOPTN  7                       S01486
     C                     OUT  WWLDA                                     S01486
     C***********          READ SDBANKL1                 74               S01486
     C************IN74     IFEQ '1'                        ***************S01486
     C************         MOVEL'01'      DBASE            **DB ERROR 01**S01486
     C************         MOVEL'SDBANKL1'DBFILE           ***************S01486
     C************         MOVE *BLANKS   DBKEY                           S01486
     C                     EXSR *PSSR
     C                     END
     C************         MOVELBJMRDT    DDRUNA                          S01486
     C*
     C** PROCEED TO START SCREEN
     C*
     C                     MOVE '1'       *IN07
     C*
     C                     ENDSR                           ** #A  ENDSR **
     C*****************************************************************
      /EJECT
     C*****************************************************************
      **
      ** #BA   S/R TO OUTPUT AND PROCESS KEY FIELDS SCREEN
      ** CALLED FROM MAIN PROCESSING
      ** CALLS: #ZBAA  TO VALIDATE KEY FIELDS IF ENTERED
      **
     C*****************************************************************
     C*
     C           #BA       BEGSR                           ** #BA   BSR **
     C*
     C** INITIALISE SCREEN DATA
     C*
     C                     MOVEA'0'       *IN,07
     C                     SETOF                     303132
     C                     SETOF                     82
     C                     MOVEL*BLANK    SCRCTL
     C                     MOVEL'PM90001' MSMBR4
     C                     CALL 'SDRTVTXT'PLLANG
     C                     MOVELMSGTXT    DDINFO
     C*
     C** CLEAR WHOLE SCREEN
     C*
     C                     WRITEPM0010F3
     C*
     C** D O    U N T I L   C H A N G E  O F  S C R E E N
     C*
     C           *IN07     DOWEQ'0'
     C           *IN08     ANDEQ'0'
     C           *IN09     ANDEQ'0'
     C*
     C** S T A R T   S C R E E N     O U T P U T
     C**
     C** INITIALIZE SCREEN FIELDS
     C**
     C           *IN75     IFEQ '0'
     C                     MOVE *BLANK    DDACTN
     C                     MOVE *BLANKS   DDDSPG
     C                     END
     C                     MOVE *BLANKS   DDDELT
     C                     MOVE *BLANKS   DDDSPT
     C                     MOVE *BLANKS   DDDSPS
     C                     WRITEPM0010F2
     C                     EXFMTPM0010F1
     C                     MOVE '0'       *IN75
     C*********************CALL 'ZA0250'                                  R00300
     C*                                                                   R00300
     C**  Clear program message queue.                                    R00300
     C                     CALL 'Y2CLMSC'                                 R00300
     C                     PARM           TOPQ   10                       R00300
     C                     PARM           TOPRQ   5                       R00300
     C*
     C                     EXSR #ZBAB
     C*
     C** CK3 - TERMINATE
     C*
     C           AID       IFEQ '03'
     C                     MOVEA'1'       *IN,07
     C                     END
     C*
     C** ENTER TAKEN VALIDATE KEY INPUT
     C*
     C           AID       IFEQ 'RA'
     C                     EXSR #ZBAA
     C*
     C** IF NO ERRORS, DETERMINE NEXT SCREEN
     C*
     C           *IN75     IFEQ '0'
     C*
     C** IF ACTION CODE IS BLANK, NEXT SCREEN IS REVIEW SCREEN,
     C** OTHERWISE, DISPLAY REQUESTED DETAIL SCREEN AS PER KEY INPUT
     C*
     C           DDACTN    IFEQ *BLANK
     C                     MOVEA'1'       *IN,08
     C                     ELSE
     C                     MOVEA'1'       *IN,09
     C                     END
     C                     END
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR                           ** #BA  ENDSR**
     C*****************************************************************
      /EJECT
     C*****************************************************************
      **
      ** #BB    S/R TO PROCESS SUBFILE SELECTION SCREEN
      ** CALLED FROM MAIN PROCESSING
      ** CALLS: #ZBAA  TO VALIDATE KEY FIELDS IF SUBFILE RECORD SELECTED
      **
     C*****************************************************************
     C*
     C           #BB       BEGSR                           ** #BB    BSR**
     C*
     C                     MOVE '0'       *IN08
     C                     MOVE '1'       *IN82
     C*
     C** SET PROCESSING FLAG TO FORCE SUBFILE REBUILD
     C*
     C                     MOVEL'RA'      AID
     C*
     C** D O    U N T I L   C H A N G E  O F  S C R E E N
     C*
     C           *IN07     DOUEQ'1'
     C           *IN08     OREQ '1'
     C           *IN09     OREQ '1'
     C*
     C** R E B U I L D   S U B F I L E
     C*
     C           AID       IFEQ 'UP'
     C           AID       OREQ 'DN'
     C           AID       OREQ 'RA'
     C*
     C** WRITE CONTROL RECORD
     C*
     C                     SETOF                     505152
     C                     Z-ADD*ZERO     RRN
     C                     WRITEPM0010C0
     C                     MOVE '1'       *IN53
     C*
     C** SET FILE POINTER
     C*
     C** SET FILE POINTER - PAGE BACK
     C*
     C           AID       IFEQ 'DN'
     C                     MOVEL*BLANK    DDDSPG
     C                     MOVEL*BLANK    WWPAG2
     C           DDDSPG    SETLLPMDSPGXX
     C*
     C**                  - PAGE FORWARD
     C                     ELSE
     C*
     C           *IN75     IFEQ '0'
     C                     MOVELDDPKEY    DDDSPG
     C                     MOVELDDPKEY    WWPAG2
     C                     ELSE
     C                     MOVELWWPAG2    DDDSPG
     C                     END
     C**
     C           DDDSPG    SETLLPMDSPGXX
     C                     END
     C*
     C** READ IN DETAILS-FILL UP SUBFILE
     C*
     C*
     C           RRN       DOUEQ13
     C           *IN03     OREQ '1'
     C**
     C                     MOVE '0'       *IN34
     C                     MOVEL*BLANK    DDACTN
     C*
     C** READ DETAILS UNTIL SUBFILE PAGE IS FULL
     C*
     C                     READ PMDSPGXX                 03
     C           *IN03     IFEQ '0'
     C*
     C** IF RECORD IS DELETED, FLAG IT AS SO ON SCREEN
     C*
     C           PARECI    IFEQ '*'
     C                     MOVEL'D'       DDSTAT  1
     C                     ELSE
     C                     MOVEL*BLANK    DDSTAT
     C                     END
     C*
     C                     MOVE PADSPG    DDDSPG
     C                     MOVE PADSPT    DDDSPT
     C                     MOVE PADSPS    DDDSPS
     C                     ADD  1         RRN     50     53
     C*
     C           RRN       IFEQ WWSAVE
     C                     MOVELWWACTN    DDACTN
     C                     MOVE '1'       *IN34
     C                     END
     C**
     C                     WRITEPM0010S0
     C*
     C** RECORD NOW WRITTEN TO SUBFILE
     C*
     C                     END
     C*
     C** PAGE NOW FULL (OR EOF)
     C*
     C                     END
     C*
     C** READ ONE MORE RECORD FOR NEXT SCREEN
     C*
     C           *IN03     IFEQ '0'
     C                     READ PMDSPGXX                 03
     C                     END
     C*
     C** IF NOT EOF SET NEXT 'REVIEW FROM' KEY ELSE SET IT TO BLANK
     C*
     C           *IN03     IFEQ '0'
     C                     MOVELPADSPG    DDPKEY
     C                     MOVE '0'       *IN52
     C                     ELSE
     C                     MOVEL*BLANK    DDPKEY
     C                     MOVE '1'       *IN52
     C                     END
     C*
     C** FOR SUBFILE CONTROL
     C*
     C           *IN53     IFEQ '0'
     C                     SETON                     5051
     C                     ELSE
     C                     SETON                     51
     C                     SETOF                     50
     C                     END
     C                     END
     C*
     C** R E V I E W   S C R E E N     O U T P U T
     C*
     C                     MOVEL'PM90002' MSMBR4
     C                     CALL 'SDRTVTXT'PLLANG
     C                     MOVELMSGTXT    DDINFO
     C*
     C                     WRITEPM0010F2
     C                     EXFMTPM0010C0
     C                     MOVE '0'       *IN75
     C                     Z-ADD0         WWSAVE  40
     C*********************CALL 'ZA0250'                                  R00300
     C*                                                                   R00300
     C**   Initialise screen input MSGQ                                   R00300
     C                     CALL 'Y2CLMSC'                                 R00300
     C                     PARM           TOPQ                            R00300
     C                     PARM           TOPRQ                           R00300
     C*
     C                     EXSR #ZBAB
     C*
     C** CK3 - TERMINATE
     C*
     C           AID       IFEQ '03'
     C                     MOVE '1'       *IN08
     C                     END
     C*
     C** CK12- INITIAL SCREEN
     C*
     C           AID       IFEQ '12'
     C                     MOVE '1'       *IN07
     C                     END
     C*
     C** ENTER PRESSED, PROCESS RECORD SELECTION
     C*
     C           AID       IFEQ 'RA'
     C                     MOVE '1'       *IN72
     C           *IN,53    IFEQ '0'
     C                     READCPM0010S0                 72
     C                     END
     C*
     C** R E C O R D   S E L E C T   V A L I D A T I O N
     C*
     C           *IN72     IFEQ '0'
     C                     EXSR #ZBAA
     C*
     C** NO ERRORS, UPDATE SCREEN CAN BE PROCESSED NEXT
     C*
     C           *IN75     IFEQ '0'
     C                     MOVE '1'       *IN09
     C                     MOVEL'Y'       WWREVW
     C                     MOVE DDPKEY    WWPAG1
     C                     MOVE *BLANKS   DDPKEY
     C                     ELSE
     C                     MOVELDDACTN    WWACTN  1
     C                     Z-ADDRRN       WWSAVE
     C                     END
     C                     END
     C                     END
     C**
     C                     END
     C*
     C                     ENDSR                           **        ESR**
     C*
     C*****************************************************************
      /EJECT
     C*****************************************************************
      **
      ** #C     S/R TO PROCESS DETAIL SCREEN AND UPDATE FILE
      ** CALLED FROM MAIN PROCESSING
      ** CALLS: #CC   , #CA   , #CA   , #CB  ,
      **
     C*****************************************************************
     C*
     C           #C        BEGSR                           ** #C     BSR**
     C*
     C** SET SCREEN INDICATORS - PROTECT KEY, SET OFF ERROR INDS,
     C** POSITION CURSOR
     C*
     C                     SETOF                     3132
     C                     MOVE '1'       *IN30
     C                     MOVE '0'       *IN09
     C*
     C** ACCESS FILE RECORD FOR OLD TNLU TO CHECK AT UPDATE TIME
     C** IF NO RECORD PRESENTLY ON FILE, ZEROISE STORED TNLU
     C*
     C                     Z-ADD*ZERO     WWTNLU  50
     C           DDDSPG    CHAINPMDSPGXX             72
     C           *IN72     IFEQ '0'
     C                     Z-ADDPATNLU    WWTNLU
     C                     END
     C*
     C** IF DELETED RECORD-ENQUIRY ONLY (RECI IS SET TO BLANK FOR INSERTS)
     C*
     C           PARECI    IFEQ '*'
     C           DDACTN    ANDNE'I'
     C                     MOVEL'E'       DDACTN
     C                     MOVEL'DELETED' DDDELT  7
     C                     ELSE
     C                     MOVEL*BLANK    DDDELT
     C                     END
     C*
     C** TRANSLATE RECORD DETAILS FROM FILE LAYOUT TO SCREEN LAYOUT
     C*
     C           DDACTN    IFNE 'I'
     C                     EXSR #CB
     C                     ELSE
     C                     MOVEL*BLANKS   DDDSPT
     C                     MOVEL*BLANKS   DDDSPS
     C                     END
     C*
     C** SET UP DEFAULTS (DONE ON BLANK FIELDS)
     C*
     C*
     C** IF DELETE/ENQUIRY PROTECT FIELD INPUT ON S/PM0010F0
     C*
     C           DDACTN    IFEQ 'D'
     C           DDACTN    OREQ 'E'
     C                     MOVEA'1'       *IN,35
     C                     END
     C*
     C** D O    U N T I L   C H A N G E  O F  S C R E E N
     C*
     C           *IN07     DOWEQ'0'
     C           *IN08     ANDEQ'0'
     C           *IN09     ANDEQ'0'
     C*
     C** L O A D    U  P    I N F O M E S S A G E S
     C*
     C           DDACTN    IFNE 'D'
     C                     MOVEL'PM90003' MSMBR4
     C                     CALL 'SDRTVTXT'PLLANG
     C                     MOVELMSGTXT    DDINFO
     C                     ELSE
     C                     MOVEL'PM90004' MSMBR4
     C                     CALL 'SDRTVTXT'PLLANG
     C                     MOVELMSGTXT    DDINFO
     C                     END
     C*
     C** D E T A I L   S C R E E N     O U T P U T
     C*
     C**
     C                     WRITEPM0010F2
     C                     WRITEPM0010F1
     C**
     C                     EXFMTPM0010F4
     C                     MOVE '0'       *IN75
     C*********************CALL 'ZA0250'                                  R00300
     C*                                                                   R00300
     C**   Initialise screen input MSGQ                                   R00300
     C                     CALL 'Y2CLMSC'                                 R00300
     C                     PARM           TOPQ                            R00300
     C                     PARM           TOPRQ                           R00300
     C*
     C                     EXSR #ZBAB
     C*
     C** CK3 - TERMINATE
     C*
     C           AID       IFEQ '03'
     C                     MOVEA'1'       *IN,09
     C                     END
     C*
     C** CK12- INITIAL SCREEN
     C*
     C           AID       IFEQ '12'
     C                     MOVEA'10'      *IN,07
     C                     END
     C*
     C** V A L I D A T E   D E T A I L   S C R E E N   I N P U T
     C*
     C           AID       IFNE '03'
     C           AID       ANDNE'12'
     C                     EXSR #CC
     C*
     C** N O    D E T A I L   S C R E E N    E R R O R S
     C*
     C           *IN75     IFEQ '0'
     C           AID       ANDEQ'RA'
     C*
     C** DETERMINE WHAT SCREEN TO RETURN TO
     C*
     C           WWREVW    IFEQ 'Y'
     C                     MOVELWWPAG2    DDPKEY
     C                     MOVEA'1'       *IN,08
     C                     ELSE
     C                     MOVEA'10'      *IN,07
     C                     END
     C*
     C**     U  P  D  A  T  E      F  I  L  E  S
     C*
     C           DDACTN    IFEQ 'I'
     C           DDACTN    OREQ 'A'
     C           DDACTN    OREQ 'D'
     C                     EXSR #CA
     C                     END
     C*
     C                     END
     C                     END
     C*
     C                     END
     C*
     C** RESET PROTECT/NON-DISPLAY INDS
     C*
     C                     MOVE '0'       *IN30
     C                     MOVE '0'       *IN35
     C*
     C           XTUPDT    ENDSR                           ** XTUPDA ESR**
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C           #CA       BEGSR                           ** #CA    BSR**
     C*
     C** U P D A T E   F I L E S
     C*
     C** RETRIEVE NEXT TRANSACTION NUMBER
     C*
     C                     EXSR ZTNLU1
     C*
     C**               I  N  S  E  R  T  S
     C*
     C           DDACTN    IFEQ 'I'
     C***********DDDSPG    CHAINPMDSPGPP             72                   S01486
     C           DDDSPG    CHAINPMDSPGPD             72                   S01486
     C*
     C** RECORD UPDATED BY ANOTHER WORKSTATION
     C*
     C           *IN72     IFEQ '0'
     C           PATNLU    IFNE WWTNLU
     C                     EXSR #CAA
     C                     GOTO XTUPDF
     C                     END
     C                     END
     C*
     C** TRANSLATE RECORD DETAILS FROM SCREEN LAYOUT TO FILE LAYOUT
     C*
     C                     MOVEL'D'       PARECI
     C                     MOVELDDDSPG    PADSPG
     C                     MOVELDDDSPT    PADSPT
     C                     MOVELDDDSPS    PADSPS
     C                     Z-ADDBJRDNB    PALCD
     C                     MOVE BJMRDT    WWHLUP
     C                     Z-ADDWWDLUP    PADLUP
     C                     MOVE WWMLUP    PAMLUP
     C                     Z-ADDWWYLUP    PAYLUP
     C                     TIME           PATLUP
     C                     MOVEL'I'       PACHTP
     C                     MOVELNATN      PATNLU
     C           *IN72     IFEQ '1'
     C                     WRITEPMDSPGP0               15
     C*
     C** TEST FOR UPDATE BY ANOTHER W/S, OR A TERMINAL ERROR
     C*
     C           *IN15     IFEQ '1'
     C           STATUS    ANDEQ01021
     C***********DDDSPG    CHAINPMDSPGPP             15                   S01486
     C           DDDSPG    CHAINPMDSPGPD             15                   S01486
     C           *IN15     IFEQ '0'
     C                     EXSR #CAA
     C                     GOTO XTUPDF
     C                     END
     C                     END
     C           *IN15     IFEQ '1'
     C                     EXSR *PSSR
     C                     END
     C                     ELSE
     C                     UPDATPMDSPGP0
     C                     END
     C*
     C                     END
     C*
     C**               A  M  E  N  D  S
     C*
     C           DDACTN    IFEQ 'A'
     C***********DDDSPG    CHAINPMDSPGPP             72                   S01486
     C           DDDSPG    CHAINPMDSPGPD             72                   S01486
     C           *IN72     IFEQ '0'
     C*
     C** RECORD UPDATED BY ANOTHER WORKSTATION
     C*
     C           PATNLU    IFNE WWTNLU
     C                     EXSR #CAA
     C                     GOTO XTUPDF
     C                     END
     C*
     C** TRANSLATE RECORD DETAILS FROM SCREEN LAYOUT TO FILE LAYOUT
     C*
     C                     EXSR #CC
     C                     MOVE DDDSPT    PADSPT
     C                     MOVE DDDSPS    PADSPS
     C           BJRDNB    IFNE PALCD
     C                     MOVEL'A'       PACHTP
     C                     END
     C                     Z-ADDBJRDNB    PALCD
     C                     MOVE BJMRDT    WWHLUP
     C                     Z-ADDWWDLUP    PADLUP
     C                     MOVE WWMLUP    PAMLUP
     C                     Z-ADDWWYLUP    PAYLUP
     C                     TIME           PATLUP
     C                     MOVELNATN      PATNLU
     C                     UPDATPMDSPGP0
     C                     END
     C*
     C                     END
     C*
     C**               D  E  L  E  T  E  S
     C*
     C           DDACTN    IFEQ 'D'
     C***********DDDSPG    CHAINPMDSPGPP             72                   S01486
     C           DDDSPG    CHAINPMDSPGPD             72                   S01486
     C           *IN72     IFEQ '0'
     C*
     C** RECORD UPDATED BY ANOTHER WORKSTATION
     C*
     C           PATNLU    IFNE WWTNLU
     C                     EXSR #CAA
     C                     GOTO XTUPDF
     C                     END
     C*
     C                     MOVEL'*'       PARECI
     C                     Z-ADDBJRDNB    PALCD
     C                     MOVE BJMRDT    WWHLUP
     C                     Z-ADDWWDLUP    PADLUP
     C                     MOVE WWMLUP    PAMLUP
     C                     Z-ADDWWYLUP    PAYLUP
     C                     TIME           PATLUP
     C                     MOVELNATN      PATNLU
     C                     MOVEL'D'       PACHTP
     C                     UPDATPMDSPGP0
     C                     END
     C*
     C                     END
     C*
     C** GET AUDIT RECORD
     C*
     C***********1         CHAINPMDSPGZZ             73                   S01486
     C           1         CHAINPMDSPGPA             73                   S01486
     C*
     C** UPDATE AUDIT RECORD IF FOUND
     C*
     C           *IN73     IFEQ '0'
     C*
     C** FOR INSERT, UPDATE NO. OF LIVE RECORDS
     C*
     C           DDACTN    IFEQ 'I'
     C                     ADD  1         PANORE
     C                     END
     C*
     C** FOR DELETIONS, UPDATE NO. OF LIVE RECS
     C*
     C           DDACTN    IFEQ 'D'
     C                     SUB  1         PANORE
     C                     END
     C*
     C** FORMAT COMIT TEXT
     C*
     C                     TIME           MTIME
     C                     MOVELDDACTN    ACTNX
     C*
     C** UPDATE AUDIT RECORD
     C*
     C                     UPDATPMDSPGZ0
     C*
     C** C  O  M  I  T
     C*
     C           CMTTXT    COMIT
     C*
     C** AUDIT RECORD NOT FOUND: DATABASE ERROR
     C*
     C                     ELSE
     C           *LOCK     IN   WWLDA                                     S01486
     C*                                                    ***************
     C                     MOVEL'02'      DBASE            **DB ERROR 02**
     C************         MOVEL'PMDSP'   DBFILE           ***************S01486
     C                     MOVEL'PMDSPGPA'DBFILE           ***************S01486
     C                     MOVEL'AUDIT'   DBKEY
     C                     OUT  WWLDA                                     S01486
     C                     EXSR *PSSR
     C                     END
     C*
     C           XTUPDF    ENDSR
     C*****************************************************************
      /EJECT
     C*****************************************************************
     C           #CAA      BEGSR                           ** #CAA   BSR**
     C*
     C                     ROLBK
     C*
     C**   SET UP NEXT SCREEN TO BE DISPLAYED:
     C**   IF RECORD IS LIVE, ALLOW AMEND PROCESSING;
     C**
     C           PARECI    IFEQ 'D'
     C                     MOVEL'A'       DDACTN
     C                     ELSE
     C**
     C**   IF RECORD HAS BEEN DELETED, ALLOW ENQUIRY
     C**
     C                     MOVEL'E'       DDACTN
     C                     END
     C**
     C**   SET SCREEN PROCESSOR TO UPDATE AND MOVE UPDATE MESSAGE TO ERROR
     C**
     C                     MOVE '1'       *IN75
     C*********************MOVEL'PM01303' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM01303' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C**
     C                     ENDSR
     C**
     C*****************************************************************
      /EJECT
     C*****************************************************************
      **
      ** #CB   S/R TO FORMAT RECORD FIELDS FOR SCREEN
      ** CALLED FROM UPDATE
      ** CALLS NO OTHER S/R'S
      **
     C*****************************************************************
     C*
     C           #CB       BEGSR                           ** #CB   BSR **
     C*
     C** TRANSLATE RECORD DETAILS FROM FILE LAYOUT TO SCREEN LAYOUT
     C*
     C                     MOVELPADSPT    DDDSPT
     C*
     C                     MOVELPADSPS    DDDSPS
     C*
     C                     ENDSR
     C*****************************************************************
      /EJECT
     C*****************************************************************
      **
      ** #CC    S/R - TO VALIDATE DETAIL SCREEN
      ** CALLED FROM #C     S/R
      ** CALLS:
      **
     C*****************************************************************
     C*
     C           #CC       BEGSR                           ** #CC    BSR**
     C*
     C** RESET SCREEN ERROR INDICATORS
     C*
     C                     SETOF                     3637
     C*
     C** ENTER KEY PRESSED
     C*
     C           AID       IFEQ 'RA'
     C*
     C** IF ACTION = D WITH ENTER PRESSED THEN ERROR
     C*
     C           DDACTN    IFEQ 'D'
     C*********************MOVEL'PM01301' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM01301' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN75
     C                     END
     C*
     C** ACTION = I OR A
     C*
     C           DDACTN    IFEQ 'I'
     C           DDACTN    OREQ 'A'
     C*
     C** VALIDATION OF FIELDS FOR INSERT/AMEND
     C*
     C** CHECK THAT DDDSPT IS VALID
     C*
     C           DDDSPT    IFEQ *BLANK
     C                     MOVE '1'       *IN36
     C*********************MOVEL'PM01200' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM01200' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN75
     C                     END
     C*
     C** CHECK THAT DDDSPS IS VALID
     C*
     C                     MOVE DDDSPS    WWWORK
     C           WWORK1    LOKUPNUMARA                   88
     C           WWORK2    LOKUPNUMARA                   89
     C           *IN88     IFEQ '0'
     C           *IN89     OREQ '0'
     C                     MOVE '1'       *IN37
     C*********************MOVEL'PM01201' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM01201' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN75
     C                     END
     C                     END
     C*
     C** ENTER KEY NOT PRESSED
     C*
     C                     ELSE
     C*
     C** COMMAND 10 TAKEN CORRECTLY WHEN ACTION IS D-FORCE 'RECORD ADVANCE'
     C** OTHERWISE,COMMAND 10 TAKEN INCORRECTLY WHEN ACTION IS NOT D - ERROR
     C*
     C           DDACTN    IFEQ 'D'
     C           AID       IFEQ '10'
     C                     MOVEL'RA'      AID     2
     C                     END
     C                     ELSE
     C           AID       IFEQ '10'
     C*********************MOVEL'PM01302' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM01302' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN75
     C                     END
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
     C*****************************************************************
      /EJECT
     C*****************************************************************
      ** #ZBAA  S/R TO VALIDATE KEY FIELDS
      ** CALLED FROM S/R #BA  , #BB
      ** CALLS NO OTHER S/R'S
      **
     C*****************************************************************
     C*
     C           #ZBAA     BEGSR                           ** #ZBAA  BSR**
     C*
     C** RESET ERROR INDICATORS
     C*
     C                     MOVEA'0'       *IN,75
     C*
     C** A C T I O N    C O D E    I N P U T - MUST BE BLANK,I,A,E,D
     C*
     C           DDACTN    IFNE *BLANK
     C           DDACTN    ANDNE'I'
     C           DDACTN    ANDNE'A'
     C           DDACTN    ANDNE'E'
     C           DDACTN    ANDNE'D'
     C*********************MOVEL'PM01100' MSGID  10                       R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM01100' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN31
     C                     MOVE '1'       *IN75
     C                     END
     C** USER SHOULD BE AUTHORISED TO ACTION CODE                         S01486
     C           DDACTN    IFNE *BLANK                                    S01486
     C*                                                                   S01486
     C** WHEN SINGLE BRANCH                                               S01486
     C           MBIN      IFNE 'Y'                                       S01486
     C*                                                                   S01486
     C                     CALL 'ZVACTU'                                  S01486
     C                     PARM DDACTN    @ZACTN  1                       S01486
     C                     PARM           @ERR    10                      S01486
     C           @ERR      IFNE *ZERO                                     S01486
     C                     MOVEL'PM02000' ZAMSID                          S01486
     C                     EXSR ZASNMS                                    S01486
     C                     MOVE '1'       *IN31                           S01486
     C                     MOVE '1'       *IN75                           S01486
     C                     END                                            S01486
     C*                                                                   S01486
     C                     ELSE                                           S01486
     C*                                                                   S01486
     C** WHEN MULTI-BRANCH                                                S01486
     C                     CALL 'ZVACTBU'                                 S01486
     C                     PARM DDACTN    @ZACTN  1                       S01486
     C                     PARM USRBCH    @ZBR    3                       S01486
     C                     PARM           @ERR    10                      S01486
     C           @ERR      IFNE *ZERO                                     S01486
     C                     MOVEL'PM02000' ZAMSID                          S01486
     C                     EXSR ZASNMS                                    S01486
     C                     MOVE '1'       *IN31                           S01486
     C                     MOVE '1'       *IN75                           S01486
     C                     END                                            S01486
     C*                                                                   S01486
     C                     END                                            S01486
     C                     END                                            S01486
     C*                                                                   S01486
     C*
     C** C H E C K   K E Y  F I E L D S
     C*
     C*
     C** ACTION CODE BLANK BUT KEY INPUT
     C*
     C           DDACTN    IFEQ *BLANK
     C           DDDSPG    ANDNE*BLANK
     C*********************MOVEL'PM01101' MSGID  10                       R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM01101' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN31
     C                     MOVE '1'       *IN75
     C                     END
     C**
     C** IF ACTION CODE ENTERED REVIEW FROM MUST BE BLANK
     C**
     C           DDACTN    IFNE *BLANK
     C           DDDSPG    ORNE *BLANKS
     C           *IN82     IFNE '1'
     C           DDPKEY    ANDNE*BLANKS
     C*********************MOVEL'PM01305' MSGID  10                       R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM01305' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN75
     C                     SETON                     3132
     C                     END
     C                     END
     C*
     C** KEY CANNOT CONTAIN BLANKS EXCEPT IF REVIEW FROM REQUESTED
     C**  (ACTION CODE MUST BE BLANK)
     C*
     C                     MOVEADDDSPG    DSP,1
     C           ' '       LOKUPDSP                      71
     C           *IN71     IFEQ '1'
     C           DDACTN    ANDNE' '
     C*********************MOVEL'PM01106' MSGID  10                       R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM01106' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN32
     C                     MOVE '1'       *IN75
     C                     END
     C*
     C** N O   E R R O R  O N  K E Y  F I E L D S
     C**
     C*
     C** A C T I O N    C O D E    I
     C*
     C           DDACTN    IFEQ 'I'
     C           DDDSPG    CHAINPMDSPGXX             72
     C           *IN72     IFEQ '0'
     C           PARECI    IFEQ 'D'
     C*********************MOVEL'PM01103' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM01103' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN32
     C                     MOVE '1'       *IN75
     C                     END
     C                     END
     C                     END
     C*
     C** A C T I O N    C O D E    A , D
     C*
     C           DDACTN    IFEQ 'A'
     C           DDACTN    OREQ 'D'
     C           DDDSPG    CHAINPMDSPGXX             72
     C           *IN72     IFEQ '1'
     C           PARECI    ORNE 'D'
     C*********************MOVEL'PM01104' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM01104' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN32
     C                     MOVE '1'       *IN75
     C                     END
     C                     END
     C**
     C** IF ACTION CODE = D SYSTEM SETUP FLAG MUST BE OFF
     C**
     C           DDACTN    IFEQ 'D'
     C           BJSUC     ANDEQ'Y'
     C*********************MOVEL'PM01102' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM01102' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN75
     C                     END
     C*                                                                   B9996
     C* BEFORE DELETE CHECK IF DISPLAY GROUP NOT ATTACHED TO A LIVE       B9996
     C* INSTRUMENT                                                        B9996
     C*                                                                   B9996
     C           DDACTN    IFEQ 'D'                                       B9996
     C*                                                                   B9996
     C                     OPEN SDPLINL2               72                 S01486
     C           *IN72     IFEQ '1'                                       S01486
     C           *LOCK     IN   WWLDA                      ***************S01486
     C                     Z-ADD3         DBASE            * DB ERROR 03 *S01486
     C                     MOVEL'SDPLINL2'DBFILE           ***************S01486
     C                     MOVE *BLANKS   DBKEY                           S01486
     C                     OUT  WWLDA                                     S01486
     C                     EXSR *PSSR                                     S01486
     C                     ELSE                                           S01486
     C           DDDSPG    CHAINSDPLINL2             72                   S01486
     C           *IN72     IFEQ '0'                                       S01486
     C                     MOVEL'PM01110' ZAMSID                          B9996
     C                     EXSR ZASNMS                                    B9996
     C                     MOVE '1'       *IN32                           B9996
     C                     MOVE '1'       *IN75                           B9996
     C                     END                                            B9996
     C                     CLOSESDPLINL2                                  S01486
     C                     END                                            S01486
     C                     END                                            B9996
     C*
     C** A C T I O N    C O D E    E
     C*
     C           DDACTN    IFEQ 'E'
     C           DDDSPG    CHAINPMDSPGXX             72
     C           *IN72     IFEQ '1'
     C*********************MOVEL'PM01105' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM01105' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN32
     C                     MOVE '1'       *IN75
     C                     END
     C                     END
     C*
     C                     ENDSR
     C*****************************************************************
      /EJECT
     C*****************************************************************
      **
      ** ASCHGS S/R TO MOVE THE APPROPRIATE VALUES INTO FIELD AID,
      ** DEPENDING ON WHICH FUNCTION KEY HAS BEEN PRESSED.
      ** CALLED FROM UPDATE
      **
     C*****************************************************************
     C*
     C           #ZBAB     BEGSR
     C*
     C* If ENTER pressed, the VLDCMDKEY indicator will be off
     C*
     C           *IN45     IFEQ '0'
     C                     MOVE 'RA'      AID
     C                     END
     C*
     C* If F3
     C*
     C           *INKC     IFEQ '1'
     C                     MOVE '03'      AID
     C                     MOVE '0'       *INKC
     C                     END
     C*
     C* If F10
     C*
     C           *INKJ     IFEQ '1'
     C                     MOVE '10'      AID
     C                     MOVE '0'       *INKJ
     C                     END
     C*
     C* If F12
     C*
     C           *INKL     IFEQ '1'
     C                     MOVE '12'      AID
     C                     MOVE '0'       *INKL
     C                     END
     C*
     C* If ROLLUP pressed
     C*
     C           *IN25     IFEQ '1'
     C                     MOVE 'UP'      AID
     C                     MOVE '0'       *IN25
     C                     END
     C*
     C* If ROLLDOWN pressed
     C*
     C           *IN26     IFEQ '1'
     C                     MOVE 'DN'      AID
     C                     MOVE '0'       *IN26
     C                     END
     C*
     C                     ENDSR
     C*****************************************************************
      /EJECT
     C** ZTNLU1 SUBROUTINE
     C*****************************************************************
     C**                                                              *
     C**  ZTNLU1 SR. - TO LOCK THE TRANSACTION NUMBER DATA AREA,      *
     C**  ~~~~~~~~~~   ACCESS THE NEXT AVAILABLE TRANSACTION NUMBER,  *
     C**               THEN UPDATE AND RELEASE THE DATA AREA.         *
     C**                                                              *
     C*****************************************************************
     C**
     C           ZTNLU1    BEGSR
     C**
     C           *NAMVAR   DEFN MNATN     DNATN
     C           *LOCK     IN   DNATN
     C                     MOVE FNATN     NATN    50
     C                     MOVE FNATN     ZZWK05  50
     C                     ADD  1         ZZWK05
     C                     MOVE ZZWK05    FNATN
     C                     OUT  DNATN
     C**
     C                     ENDSR
     C*****************************************************************
      /EJECT
     C*****************************************************************   R00300
     C*                                                               *   R00300
     C* ZASNMS   - Send message to program message queue              *   R00300
     C*                                                               *   R00300
     C* CALLS    Y2SNMSGC                                             *   R00300
     C*                                                               *   R00300
     C* CALLED BY  #CAA   - Processes update by another workstation.  *   R00300
     C*            #CC    - Validate detail screen.                   *   R00300
     C*            #ZBAA  - Validate key screen.                      *   R00300
     C*                                                               *   R00300
     C*****************************************************************   R00300
     C           ZASNMS    BEGSR                                          R00300
     C*                                                                   R00300
     C                     CALL 'Y2SNMGC'                                 R00300
     C                     PARM           ZAPGM            Program queue  R00300
     C                     PARM           ZAPGRL           Rel queue      R00300
     C                     PARM           ZAMSID           Message Id.    R00300
     C                     PARM           ZAMSGF           Message file.  R00300
     C                     PARM           ZAMSDA           Message data.  R00300
     C                     PARM           ZAMSTP           Message type.  R00300
     C*                                                                   R00300
     C** Clear all fields for default mechanism next time.                R00300
     C                     MOVEL*BLANK    ZAPGM            Message queue  R00300
     C                     MOVEL*BLANK    ZAPGRL           Rel queue      R00300
     C                     MOVEL*BLANK    ZAMSDA           Message data.  R00300
     C                     MOVEL*BLANK    ZAMSTP           Message type.  R00300
     C*                                                                   R00300
     C           ZASNM9    ENDSR                           ZASNM9 TAG     R00300
     C*                                                                   R00300
     C*****************************************************************   R00300
      /EJECT                                                              R00300
     C*****************************************************************
     C**
     C**   FILE EXEPTION/ERROR HANDLING SUBROUTINE
     C**
     C*****************************************************************
     C           *PSSR     BEGSR
     C*
     C                     SETON                     U7U8LR
     C**
     C** ROLL BACK CHANGES, PRINT DUMP AND CANCEL PROGRAM
     C**
     C                     ROLBK
     C                     DUMP
     C                     CALL 'DBERRCTL'                                S01486
     C                     RETRN
     C**
     C                     ENDSR
     C*****************************************************************
      /EJECT
**  CPY@
(c) Finastra International Limited 2001
** NUMARA
0123456789
