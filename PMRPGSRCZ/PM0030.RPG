     H
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Instrument type maintenance')
/*OVRF*: OVRDBF FILE(SDPLINXX) TOFILE(SDPLINPD) SHARE(*NO)          : *   S01486
/*OVRF*: OVRDBF FILE(LBGROUXX) TOFILE(LBGROUL1) SHARE(*NO)          : *   S01486
/*OVRF*: OVRDBF FILE(PMDSPGXX) TOFILE(PMDSPGPD) SHARE(*NO)          : *   S01486
/*OVRF*: OVRDBF FILE(PMVLGHXX) TOFILE(PMVLGHPD) SHARE(*NO)          : *   S01486
     F********************************************************************
     F*                                                                  *
     F*    Midas PORTFOLIO MANAGEMENT                                *
     F*                                                                  *
     F*    PM0030  - INSTRUMENT TYPE MAINTENANCE                         *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. BUG4009            Date 17Aug04               *
      *                 CFX001             Date 20Oct03               *
      *                 CGL014             Date 20Oct03               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 103079             Date 19Apr96               *S01486
      *                 080087             Date 12Dec94               *S01486
     F*                 S01486             DATE 30JUN94                  *S01486
     F*                 36086      ADIF    DATE 18FEB92                  *S01486
     F*                 37772              DATE 04DEC91                  *S01486
     F*                 B9257      ADIF    DATE 13NOV91                  *S01486
     F*                 R0971      DC      DATE 23NOV90                  *S01486
     F*                 R00300             DATE 10AUG90                  *S01486
     F*                                                                  *
     F********************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*: OVRDBF FILE(PMDSPGXX) TOFILE(PMDSPGPP) SHARE(*NO)          : *   S01486
     F*: OVRDBF FILE(PMVLGHXX) TOFILE(PMVLGHPP) SHARE(*NO)          : *   S01486
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG4009 - Maintenance does not show 0 Weighing Code          *
      *  CFX001 - FX Margin Trading                                   *
      *  CGL014 - Collaterals Processing                              *
      *  CLE025 - Credit Lines                                        *
     F*  103079  -  If Portfolio Lending not installed do not open    *
     F*             LB* files                                         *
     F*  080087  -  Recompiled due to changes in the display file     *
     F*             (Replaced 'Lombard' by 'Portfolio Lending')       *
     F*  S01486  -  Portfolio Management Upgrade to R10               *   S01486
     F*  36086   -  LOMBARD GROUP CODE MUST BE MANDATORY              *   S01486
     F*  37772   -  ICD Maintenance amendments.                       *   S01486
     F*  B9257   -  DISPLAY GROUP CODE AND VALUATION GROUP CODE       *   S01486
     F*             HAVE TO BE MANDATORY                              *   S01486
     F*  R0971   -  Display Groups should be optionnal.               *   S01486
     F*  R00300  -  Message file changes                              *   S01486
     F*                                                               *
     F*****************************************************************
     F***PM0030DDCF**E                    WORKSTN                         S01486
     FPM0030DFCF  E                    WORKSTN                            S01486
     F                                        RRN   KSFILE PM0030S0
     F**  PREFIX OF SDPLINPD IS "PD"
     FSDPLINPDUF  E           K        DISK         KCOMIT       A
     F                                              KINFDS INFDS
     F                                              KINFSR *PSSR
     FSDPLINXXIF  E           K        DISK
     F                                              KINFSR *PSSR
     F            SDPLINP0                          KRENAMESDPLINPX
     F**  PREFIX OF SDPLINPD IS "PD"
     F***SDPLINZZUF**E                    DISK         KCOMIT             S01486
     FSDPLINPAUF  E                    DISK         KCOMIT                S01486
     F                                              KINFSR *PSSR
     F***PMVLGHPPUF**E           K        DISK         KCOMIT       A37772S01486
     FPMVLGHPDUF  E           K        DISK         KCOMIT       A        S01486
     F                                              KINFSR *PSSR          37772
     FPMVLGHXXIF  E           K        DISK                               37772
     F                                              KINFSR *PSSR          37772
     F            PMVLGHP0                          KRENAMEPMVLGHPX       37772
     F**  PREFIX OF SDPLINPD IS "PD"                                      37772
     F***PMVLGHZZUF**E                    DISK         KCOMIT        37772S01486
     FPMVLGHPAUF  E                    DISK         KCOMIT                S01486
     F                                              KINFSR *PSSR          37772
     F***PMDSPGPPUF**E           K        DISK         KCOMIT       A37772S01486
     FPMDSPGPDUF  E           K        DISK         KCOMIT       A        S01486
     F                                              KINFSR *PSSR          37772
     FPMDSPGXXIF  E           K        DISK                               37772
     F                                              KINFSR *PSSR          37772
     F            PMDSPGP0                          KRENAMEPMDSPGPX       37772
     F**  PREFIX OF SDPLINPD IS "PD"                                      37772
     F***PMDSPGZZUF**E                    DISK         KCOMIT        37772S01486
     FPMDSPGPAUF  E                    DISK         KCOMIT                S01486
     F                                              KINFSR *PSSR          37772
     F*LBGROUXXIF**E***********K********DISK                         37772103079
     FLBGROUXXIF  E           K        DISK                           UC  103079
     F                                              KINFSR *PSSR          37772
     F            LBGROUD0                          KRENAMELBGROUPX       37772
     F****PREFIX*OF*SDBANKL1*IS*"BJ"                                      S01486
     F***SDBANKL1IF**E           K        DISK                            S01486
     F***************                               KINFSR *PSSR          S01486
     F****PREFIX*OF*PMDSPGPP*IS*"PA"                                      S01486
     F*******PMDSPGPPIF  E           K        DISK                        37772
     F*******                                              KINFSR *PSSR   37772
     F****PREFIX*OF PMVLGHPP IS "PB"                                      37772
     F*******PMVLGHPPIF  E           K        DISK                        37772
     F*******                                              KINFSR *PSSR   37772
     F**  PREFIX OF PMPGDIL1 IS "PG"
     FPMPGDIL1IF  E           K        DISK
     F                                              KINFSR *PSSR
     F**  PREFIX OF SDMMODPD IS "BG"
     F***SDMMODL1IF**E           K        DISK                            S01486
     F***************                               KINFSR *PSSR          S01486
     F**  PREFIX OF LBGROUL1 IS "GP"
     F*LBGROUL1UF**E***********K        DISK         KCOMIT       A  37772103079
     FLBGROUL1UF  E           K        DISK         KCOMIT       A    UC  103079
     F                                              KINFSR *PSSR          37772
     F***LBGROUL1IF**E***********K********DISK
     F*LBTRAIPDUF**E***********K        DISK         KCOMIT          37772103079
     FLBTRAIPDUF  E           K        DISK         KCOMIT            UC  103079
     F                                              KINFSR *PSSR          37772
     F*************************************************KINFSR *PSSR
     F*
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*       03         END OF FILE PF/SDPLINXX
     F*       07         START  SCREEN
     F*       08         REVIEW SCREEN
     F*       09         UPDATE SCREEN
     F*
     F*       15         INDICATOR USED ON WRITE/UPDATE OPS TO CONDITION
     F*                                    CALL OF *PSSR
     F*       28         PROTECT LB FIELDS IF LB NOT ON SYSTEM
     F*       29         PROTECT PM FIELDS IF PM NOT ON SYSTEM
     F*       30         S/PM0030F1  PROTECT/NON-DISPLAY KEY FIELDS
     F*       31         S/PM0030F1  ERROR-DDACTN
     F*       32         S/PM0030F1  ERROR-DDINNR
     F*       34         S/PM0030C0  ERROR-DDACTN (REVIEW SCREEN)
     F*       35         S/PM0030F0  PROTECT DETAIL FIELDS
     F*       36         S/PM0030F4  ERROR ON FIELD DDISTN
     F*       37         S/PM0030F4  ERROR ON FIELD DDDSPG
     F*       38         S/PM0030F4  ERROR ON FIELD DDITDS
     F*       39         S/PM0030F4  ERROR ON FIELD DDVALG
     F*       40         S/PM0030F4  ERROR ON FIELD DDVLIS
     F*       41         S/PM0030F4  ERROR ON FIELD DDLOMG
     F*       42         S/PM0030F4  ERROR ON FIELD DDCSHI
     F*       45         VALID COMMAND KEY ENTERED
     F*       50         S/PM0030C0  SFLDSP
     F*       51         S/PM0030C0  SFLDSPCTL
     F*       52         S/PM0030C0  SFLEND
     F*       53         S/PM0030C0  ROLLDOWN NOT ALLOWED
     F*       55         CONDITION SFLEND KEYWORD
     F*       57         ICD PROCESSING                                   37772
     F*
     F*       71         LOKUP OPERATION
     F*       72         FILE OPERATION
     F*       73         FILE OPERATION
     F*       74         FILE OPERATION
     F*       75         ERROR ON SCREEN
     F*       82         REVIEW FROM SCREEN
     F*
     F**             *************************
     F**             ** INDICATORS NOT USED **
     F**             ** ------------------- **
     F**             *************************
     F**
     F**      ***************************************************
     F**      * 01   02   ..   04   05   06   ..   ..   ..   10 *
     F**      * 11   12   13   14   ..   16   17   18   19   20 *
     F**      * 21   22   23   24   ..   ..   ..   ..   ..   .. *
     F**      * ..   ..   33   ..   ..   ..   ..   ..   ..   .. *
     F**      * ..   ..   43   44   ..   46   47   48   49   .. *
     F**      * ..   ..   ..   54   ..   56   57   58   59   60 *
     F**      * 61   62   63   64   65   66   67   68   69   70 *
     F**      * ..   ..   ..   ..   ..   76   77   78   79   80 *
     F**      * 81   ..   83   84   85   86   87   88   89   90 *
     F**      * 91   92   93   94   95   96   97   98   99      *
     F**      ***************************************************
     F*********************************************************************
     E                    CPY@    1   1 80
     E                    DEFD    1   3 20                                37772
     E*
     E** ARRAY TO TEST DISPLAY GROUP INPUT
     E*
     E                    DSP         3  1
     E*
     I*
     I** KEY FIELDS (TAKEN FROM 'KEY' PORTION OF SCREEN)
     I*
     ISCRCTL      DS
     I                                        1   1 WWREVW
     I                                        2   2 DDACTN
     I                                        3   5 DDINNR
     I                                        6   8 DDPKEY
     I                                        9  11 WWPAG1
     I                                       12  14 WWPAG2
     I*
     I*    MIDAS 'Next Available Transaction No.' Data Area  MNATN .
     IDNATN       DS                              5
     I                                        1   50FNATN
     I*
     I*    NUMERIC ARRAY DS DEFINITION
     I            DS
     I                                        1   2 WWWORK
     I                                        1   1 WWORK1
     I                                        2   2 WWORK2
     I*
     I*
     I** DATA STRUCTURE FOR RPG 01021 ERROR HANDLING
     I*
     IINFDS       DS
     I                                     *STATUS  STATUS
     I*
     I*
     I** LOCAL DATA AREA FOR DATABASE ERROR DETAILS
     I*
     I**LDA        UDS                            256                     S01486
     I***********                           132 183 DBLOT                 S01486
     I***********                           132 141 DBFILE                S01486
     ILDA         DS                            256                       S01486
     I                                      134 141 DBFILE                S01486
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I** DATA STRUCTURE FOR SETUP OF COMMIT TEXT
     I*
     I*
     ICMTTXT      DS
     I                                        1   50NATN
     I                                        6   7 MDID
     I                                        8  15 PGMN
     I                                       16  18 WSIDX
     I                                       19  240MTIME
     I                                       25  25 ACTNX
     I                                       26  35 USIDX
     I**
     I            DS
     I                                        1   20WWDLUP
     I                                        3   5 WWMLUP
     I                                        6   70WWYLUP
     I                                        1   7 WWHLUP
     I*
     I** PROGRAM STATUS DATA STRUCTURE FOR WS/USER ID'S
     I*
     I*
     IPSDS       SDS
     I                                      244 253 WSID
     I                                      254 263 USID                  S01486
     ISDMMOD    E DSSDMMODPD                                              S01486
     I** EXTERNAL DS FOR LANGUAGE ID'S                                    S01486
     ISDBANK    E DSSDBANKPD                                              S01486
     I** EXTERNAL DS FOR BANK DETAILS                                     S01486
      *                                                                                       CLE025
     ISCSARD    E DSSCSARDPD                                                                  CLE025
      ** External DS for Switchable Features file                                             CLE025
      *                                                                                       CLE025
     IGPWEIG    E DSGPWEIGPD                                                                  CLE025
      ** Weighting Code Details                                                               CLE025
      *                                                                                       CLE025
     IDSFDY     E DSDSFDY                                                 S01486
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01486
     IRUNDT       DS                                                      S01486
      **  DATA AREA RUNDAT                                                S01486
     I                                        1   7 RUNA                  S01486
     I                                    P   8  100RUND                  S01486
     I                                       11  11 SSF                   S01486
     I                                       12  12 DATF                  S01486
     I                                       13  13 MBIN                  S01486
     IZMUSER      DS                             17                       S01486
     I** DATA AREA OF MENU USER                                           S01486
     I                                       11  13 USRBCH                S01486
     I**
      /EJECT
     C**************************************************************************
      /EJECT
      **************************************************************************
      *                                                                        *
      * INDEX OF SUBROUTINES                                                   *
      * MAIN     - MAIN CONTROLLING ROUTINE                                    *
      * #INIT    - INITIAL PROCESSING                                          *
      * #START   - FORMAT AND OUTPUT INITIAL SCREEN                            *
      * #RVIEW   - FORMAT AND OUTPUT REVIEW SCREEN                             *
      * #UPDTE   - FORMAT AND OUTPUT DETAIL SCREEN                             *
      * #CA      - UPDATE FILE DETAILS                                         *
      * #CAA     - RECORD UPDATED BY ANOTHER WORKSTATION PROCESSING            *
      * #CB      - PREPARE DETAILS FOR OUTPUT                                  *
      * #CC      - VALIDATE INSTRUMENT DETAILS INPUT                           *
      * #ZBAA    - VALIDATE INITIAL SCREEN DETAILS INPUT                       *
      * #ZBAB    - SET UP 'AID' WITH COMMAND KEY INFORMATION                   *
      * ZTNLU1   - CALCULATE NEXT AVAILABLE TRANSACTION NUMBER                 *
      * *PSSR    - STANDARD ERROR PROCESSING                                   *
      * ZASNMS   - SEND MESSAGE                                           R00300
      *                                                                        *
      * EXTERNAL ROUTINES CALLED                                               *
      **ZA0250*****CLEAR PROGRAM MESSAGE QUEUE                                 *
      **ZA0240*****ACCESS MESSAGE FILE AND SEND MESSAGE                   R00300
      * Y2CLMSC  - CLEAR MESSAGE QUEUE                                    R00300
      * Y2SNMGC  - SEND MESSAGE                                           R00300
      *                                                                        *
      **************************************************************************
     C*
     C*================================================================
     C*  P R O G R A M     S T A R T
     C*================================================================
     C*
     C** PERFORM SETUP OF STANDARD FIELDS
     C                     EXSR #INIT
     C*
     C** D O    U N T I L   C K 3   O R   E R R O R
     C           AID       DOUEQ'03'
     C*
     C           *IN07     CASEQ'1'       #START
     C*
     C           *IN08     CASEQ'1'       #RVIEW
     C*
     C           *IN09     CASEQ'1'       #UPDTE
     C*
     C                     END
     C                     END
     C*
     C** TERMINATE PROGRAM
     C                     SETON                         LR
     C**************************************************************************
      /EJECT
      **************************************************************************
      *                                                                        *
      * #INIT    - INITIAL PROCESSING                                          *
      *                                                                        *
      * SUBROUTINE CALLS:                                                      *
      * *PSSR    - STANDARD ERROR PROCESSING                                   *
      *                                                                        *
      * SUBROUTINE CALLED FROM:                                                *
      * MAIN     - MAIN CONTROLLING ROUTINE                                    *
      *                                                                        *
      * ROUTINE USES VARIABLES:                                                *
      *                                                                        *
      **************************************************************************
     C           #INIT     BEGSR                           ** INIT  BSR **
     C*
     C           *ENTRY    PLIST                                          37772
     C                     PARM           WWSTAT  3                       37772
     C                     PARM           WWRTCD  3                       37772
     C*                                                                   37772
     C                     MOVEL'*'       DDPGMQ
     C*
     C** SET UP PROGRAM NAME
     C                     MOVEL'PM0030'  DBPGM
     C**                                                                  S01486
      ***  Copyright Statement.                                           S01486
     C                     MOVEACPY@      CPY2@  80                       S01486
     C           *NAMVAR   DEFN RUNDAT    RUNDT                           S01486
     C                     IN   RUNDT                                     S01486
     C           *NAMVAR   DEFN           ZMUSER                          S01486
     C                     IN   ZMUSER                                    S01486
     C           *NAMVAR   DEFN           LDA                             S01486
     C*
     C**   Select the program MSGQ for error msg.                         R00300
     C                     MOVEL'*'       TOPQ                            R00300
     C                     MOVEL'*PRV'    TOPRQ                           R00300
     C*                                                                   R00300
     C**   Fill in fields for subroutine ZASNMS                           R00300
     C                     MOVEL*BLANK    ZAPGM  10        Message queue  R00300
     C                     MOVEL'PMUSRMSG'ZAMSGF 10        Message file   R00300
     C                     MOVEL*BLANK    ZAPGRL  5        Rel queue      R00300
     C                     MOVEL*BLANK    ZAMSID  7        Message Id.    R00300
     C                     MOVEL*BLANK    ZAMSDA132        Message data.  R00300
     C                     MOVEL*BLANK    ZAMSTP  7        Message type.  R00300
     C*                                                                   R00300
     C** PREPARE COMIT TEXT
     C                     MOVEL'PM'      MDID
     C                     MOVEL'PM0030'  PGMN
     C                     MOVELWSID      WSIDX
     C                     MOVELUSID      USIDX
     C**
     C**
     C**  SET UP PARAMETER TO RETRIEVE MESSAGE FROM MESSAGE FILE
     C           PLLANG    PLIST
     C                     PARM           MSMBR4  7
     C                     PARM           MSGER  10
     C                     PARM           MSGTXT 80
     C************         MOVEL'MLGBMSGF'MSGER                           S01486
     C                     MOVEL'PMUSRMSG'MSGER                           S01486
     C*
     C***ACCESS*BANK*DETAILS*LF/SDBANKL1                                  S01486
     C** ACCESS BANK DETAILS                                              S01486
     C                     CALL 'AOBANKR0'                                S01486
     C                     PARM *BLANKS   XXRTCD  7                       S01486
     C                     PARM '*FIRST ' WWOPTN  7                       S01486
     C           SDBANK    PARM SDBANK    DSFDY                           S01486
     C           XXRTCD    IFNE *BLANKS                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     MOVE 'SDBANKPD'DBFILE           ***************S01486
     C                     Z-ADD1         DBASE            * DBERROR  01 *S01486
     C                     MOVELWWOPTN    DBKEY            ***************S01486
     C                     MOVELWWOPTN    DBOPTN  7                       S01486
     C                     OUT  LDA                                       S01486
     C***********          READ SDBANKL1                 74               S01486
     C************IN74     IFEQ '1'                        ***************S01486
     C************         Z-ADD1         DBASE            * DB ERROR 01 *S01486
     C************         MOVEL'SDBANKL1'DBFILE           ***************S01486
     C************         MOVE *BLANKS   DBKEY                           S01486
     C                     EXSR *PSSR
     C                     END
     C************         MOVELBJMRDT    DDRUNA                          S01486
     C*
     C** ACCESS MODULES FILE LF/SDMMODL1
      * Define Keylist.                                                   37772
     C                     CALL 'AOMMODR0'                                S01486
     C                     PARM *BLANKS   XXRTCD  7                       S01486
     C                     PARM '*FIRST ' WWOPTN  7                       S01486
     C           SDMMOD    PARM SDMMOD    DSFDY                           S01486
     C           XXRTCD    IFNE *BLANKS                    ************** S01486
     C           *LOCK     IN   LDA                        ***************S01486
     C                     Z-ADD2         DBASE            **DB ERROR 02**S01486
     C                     MOVE 'SDMMODPD'DBFILE           ***************S01486
     C                     MOVELWWOPTN    DBKEY                           S01486
     C                     MOVELWWOPTN    DBOPTN  7                       S01486
     C                     OUT  LDA                                       S01486
     C***********KPSSP     KLIST                                     37772S01486
     C***********          KFLD           BGMODS           Modules   37772S01486
      **Move*fields to key list.                                     37772S01486
     C***********          MOVEL'MMOD'    BGMODS           Modules   37772S01486
     C***********KPSSP     SETLL@BGREC8                    *         37772S01486
     C***********          READ @BGREC8                  90*         37772S01486
     C*********************READ SDMMODL1                 74          37772S01486
     C************IN74     IFEQ '1'                        ***************S01486
     C************         Z-ADD2         DBASE            * DB ERROR 02 *S01486
     C************         MOVEL'SDMMODL1'DBFILE           ***************S01486
     C************         MOVE *BLANKS   DBKEY                           S01486
     C                     EXSR *PSSR
     C                     END
     C*
     C** SET UP INDICATORS CONDITIONING DISPLAY OF LOMBARD CREDIT AND PORTFOLIO
     C** MANAGEMENT FIELDS
     C           BGLMCR    IFEQ 'N'
     C                     SETON                     28
     C                     ELSE                                           103079
     C                     OPEN LBGROUXX                                  103079
     C                     OPEN LBGROUL1                                  103079
     C                     OPEN LBTRAIPD                                  103079
     C                     END
     C*
     C           BGPFMG    IFEQ 'N'
     C                     SETON                     29
     C                     END
      *                                                                                       CLE025
      ** Check if enhancement CLE025 (Multi-Option Facilities) is on                          CLE025
      *                                                                                       CLE025
     C                     CALL 'AOSARDR0'                                                    CLE025
     C                     PARM *BLANKS   PRTCD   7                                           CLE025
     C                     PARM '*VERIFY' POPTN   7                                           CLE025
     C                     PARM 'CLE025'  PSARD   6                                           CLE025
     C           SCSARD    PARM SCSARD    DSFDY                                               CLE025
      *                                                                                       CLE025
     C           PRTCD     IFNE *BLANKS                                                       CLE025
     C           PRTCD     ANDNE'*NRF   '                                                     CLE025
     C           *LOCK     IN   LDA                                                           CLE025
     C                     MOVEL'SCSARDPD'DBFILE                                              CLE025
     C                     MOVEL'CLE025'  DBKEY                                               CLE025
     C                     Z-ADD7         DBASE                                               CLE025
     C                     OUT  LDA                                                           CLE025
     C                     EXSR *PSSR                                                         CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
     C           PRTCD     IFEQ *BLANKS                                                       CLE025
     C                     MOVE *ON       *IN76                                               CLE025
     C                     MOVE 'Y'       CLE025  1                                           CLE025
     C                     ELSE                                                               CLE025
     C                     MOVE 'N'       CLE025                                              CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CGL014
      ** Check if enhancement CGL014 (Collaterals Processing) is on                           CGL014
      *                                                                                       CGL014
     C                     CALL 'AOSARDR0'                                                    CGL014
     C                     PARM *BLANKS   PRTCD                                               CGL014
     C                     PARM '*VERIFY' POPTN                                               CGL014
     C                     PARM 'CGL014'  PSARD                                               CGL014
     C           SCSARD    PARM SCSARD    DSFDY                                               CGL014
      *                                                                                       CGL014
     C           PRTCD     IFNE *BLANKS                                                       CGL014
     C           PRTCD     ANDNE'*NRF   '                                                     CGL014
     C           *LOCK     IN   LDA                                                           CGL014
     C                     MOVEL'SCSARDPD'DBFILE                                              CGL014
     C                     MOVEL'CGL014'  DBKEY                                               CGL014
     C                     Z-ADD8         DBASE                                               CGL014
     C                     OUT  LDA                                                           CGL014
     C                     EXSR *PSSR                                                         CGL014
     C                     ENDIF                                                              CGL014
      *                                                                                       CGL014
     C           PRTCD     IFEQ *BLANKS                                                       CGL014
     C                     MOVE *ON       *IN77                                               CGL014
     C                     MOVE 'Y'       CGL014  1                                           CGL014
     C                     ELSE                                                               CGL014
     C                     MOVE 'N'       CGL014                                              CGL014
     C                     ENDIF                                                              CGL014
      *                                                                                       CFX001
      ** Check if enhancement CFX001 (FX Margin Trading) is on                                CFX001
      *                                                                                       CFX001
     C                     CALL 'AOSARDR0'                                                    CFX001
     C                     PARM *BLANKS   PRTCD                                               CFX001
     C                     PARM '*VERIFY' POPTN                                               CFX001
     C                     PARM 'CFX001'  PSARD                                               CFX001
     C           SCSARD    PARM SCSARD    DSFDY                                               CFX001
      *                                                                                       CFX001
     C           PRTCD     IFNE *BLANKS                                                       CFX001
     C           PRTCD     ANDNE'*NRF   '                                                     CFX001
     C           *LOCK     IN   LDA                                                           CFX001
     C                     MOVEL'SCSARDPD'DBFILE                                              CFX001
     C                     MOVEL'CFX001'  DBKEY                                               CFX001
     C                     Z-ADD9         DBASE                                               CFX001
     C                     OUT  LDA                                                           CFX001
     C                     EXSR *PSSR                                                         CFX001
     C                     ENDIF                                                              CFX001
      *                                                                                       CFX001
     C           PRTCD     IFEQ *BLANKS                                                       CFX001
     C                     MOVE *ON       *IN76                                               CFX001
     C                     MOVE 'Y'       CFX001  1                                           CFX001
     C                     ELSE                                                               CFX001
     C                     MOVE 'N'       CFX001                                              CFX001
     C                     ENDIF                                                              CFX001
     C*
     C** PROCEED TO START SCREEN
     C                     MOVE '1'       *IN07
     C*                                                                   37772
     C           WWSTAT    IFEQ 'ICD'                                     37772
     C                     MOVE '0'       *IN07                           37772
     C                     MOVE '1'       *IN08                           37772
     C                     MOVE '1'       *IN57                           37772
     C                     END                                            37772
     C*
     C                     ENDSR                           ** INIT ENDSR **
     C**************************************************************************
      /EJECT
      **************************************************************************
      *                                                                        *
      * #START   - FORMAT AND OUTPUT INITIAL SCREEN                            *
      *                                                                        *
      * SUBROUTINE CALLS:                                                      *
      **ZA0250***-*CLEAR*PROGRAM*MESSAGE*QUEUE                            R00300
      * Y2CLMSC  - CLEAR PROGRAM MESSAGE QUEUE                            R00300
      * #ZBAB    - SET UP 'AID' WITH COMMAND KEY INFORMATION                   *
      * #ZBAA    - VALIDATE INITIAL SCREEN DETAILS INPUT                       *
      *                                                                        *
      * SUBROUTINE CALLED FROM:                                                *
      * MAIN     - MAIN CONTROLLING ROUTINE                                    *
      *                                                                        *
      * ROUTINE USES VARIABLES:                                                *
      *                                                                        *
      **************************************************************************
     C           #START    BEGSR                           ** START BSR **
     C*
     C** INITIALISE SCREEN DATA
     C                     MOVE '0'       *IN07
     C                     SETOF                     303132
     C                     SETOF                     82
     C                     MOVEL*BLANK    SCRCTL
     C                     MOVEL'PM90001' MSMBR4
     C                     CALL 'SDRTVTXT'PLLANG
     C                     MOVELMSGTXT    DDINFO
     C*
     C** RESET SCREEN ERROR INDICATORS
     C                     SETOF                     363738
     C                     SETOF                     394041
     C                     SETOF                     42
     C                     MOVEA'0000'    *IN,46                                              CLE025
     C                     MOVE *OFF      *IN54                                               CLE025
     C*
     C** CLEAR WHOLE SCREEN
     C                     WRITEPM0030F3
     C*
     C** D O    U N T I L   C H A N G E  O F  S C R E E N
     C           *IN07     DOWEQ'0'
     C           *IN08     ANDEQ'0'
     C           *IN09     ANDEQ'0'
     C*
     C** S T A R T   S C R E E N     O U T P U T
     C**
     C** INITIALIZE SCREEN FIELDS
     C           *IN75     IFEQ '0'
     C                     MOVE *BLANK    DDACTN
     C                     MOVE *BLANKS   DDINNR
     C                     END
     C           WWSTAT    IFEQ 'ICD'                                     37772
     C                     MOVE 'I'       DDACTN                          37772
     C                     MOVE '1'       *IN33                           37772
     C                     END                                            37772
     C                     MOVE *BLANKS   DDDELT
     C                     MOVE *BLANKS   DDISTN
     C                     WRITEPM0030F2
     C                     EXFMTPM0030F1
     C                     MOVE '0'       *IN75
     C*********************CALL 'ZA0250'                                  R00300
     C                     CALL 'Y2CLMSC'                                 R00300
     C                     PARM           TOPQ   10                       R00300
     C                     PARM           TOPRQ   5                       R00300
     C*
     C                     EXSR #ZBAB
     C*
     C** CK3 - TERMINATE
     C           AID       IFEQ '03'
     C                     MOVE '1'       *IN07
     C                     END
     C*
     C** ENTER TAKEN VALIDATE KEY INPUT
     C           AID       IFEQ 'RA'
     C                     EXSR #ZBAA
     C*
     C** IF NO ERRORS, DETERMINE NEXT SCREEN
     C           *IN75     IFEQ '0'
     C*
     C** IF ACTION CODE IS BLANK, NEXT SCREEN IS REVIEW SCREEN,
     C** OTHERWISE, DISPLAY REQUESTED DETAIL SCREEN AS PER KEY INPUT
     C           DDACTN    IFEQ *BLANK
     C                     MOVE '1'       *IN08
     C                     ELSE
     C                     MOVE '1'       *IN09
     C                     END
     C                     END
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR                           ** START ENDSR**
     C*
     C**************************************************************************
      /EJECT
      **************************************************************************
      *                                                                        *
      * #RVIEW   - FORMAT AND OUTPUT REVIEW SCREEN                             *
      *                                                                        *
      * SUBROUTINE CALLS:                                                      *
      **ZA0250***-*CLEAR*PROGRAM MESSAGE QUEUE                            R00300
      * Y2CLMSC  - CLEAR PROGRAM MESSAGE QUEUE                            R00300
      * #ZBAB    - SET UP 'AID' WITH COMMAND KEY INFORMATION                   *
      * #ZBAA    - VALIDATE INITIAL SCREEN DETAILS INPUT                       *
      *                                                                        *
      * SUBROUTINE CALLED FROM:                                                *
      * MAIN     - MAIN CONTROLLING ROUTINE                                    *
      *                                                                        *
      * ROUTINE USES VARIABLES:                                                *
      * WWPAG2   - FIRST RECORD ON NEXT PAGE OF REVIEW SCREEN                  *
      * WWSAVE   - SAVED SUBFILE RELATIVE RECORD NUMBER                        *
      * WWACTN   - SAVED ACTION CODE                                           *
      * WWREVW   - REVIEW SCREEN SELECTED                                      *
      * WWPAG1   - PREVIOUS START OF REVIEW SCREEN                             *
      *                                                                        *
      **************************************************************************
     C           #RVIEW    BEGSR                           ** REVIEW BSR**
     C*
     C                     MOVE '0'       *IN08
     C                     MOVE '1'       *IN82
     C*
     C** SET PROCESSING FLAG TO FORCE SUBFILE REBUILD
     C                     MOVEL'RA'      AID
     C*
     C** D O    U N T I L   C H A N G E  O F  S C R E E N
     C           *IN07     DOUEQ'1'
     C           *IN08     OREQ '1'
     C           *IN09     OREQ '1'
     C*
     C** R E B U I L D   S U B F I L E
     C*
     C           AID       IFEQ 'UP'
     C           AID       OREQ 'DN'
     C           AID       OREQ 'RA'
     C*
     C** WRITE CONTROL RECORD
     C                     SETOF                     505152
     C                     Z-ADD*ZERO     RRN
     C                     WRITEPM0030C0
     C                     MOVE '1'       *IN53
     C*
     C** SET FILE POINTER
     C** SET FILE POINTER - PAGE BACK
     C           AID       IFEQ 'DN'
     C                     MOVEL*BLANK    DDINNR
     C                     MOVEL*BLANK    WWPAG2
     C           DDINNR    SETLLSDPLINXX
     C*
     C**                  - PAGE FORWARD
     C                     ELSE
     C*
     C           *IN75     IFEQ '0'
     C                     MOVELDDPKEY    DDINNR
     C                     MOVELDDPKEY    WWPAG2
     C                     ELSE
     C                     MOVELWWPAG2    DDINNR
     C                     END
     C*
     C           DDINNR    SETLLSDPLINXX
     C                     END
     C*
     C** READ IN DETAILS-FILL UP SUBFILE
     C           RRN       DOUEQ13
     C           *IN03     OREQ '1'
     C*
     C                     MOVE '0'       *IN34
     C                     MOVEL*BLANK    DDACTN
     C*
     C** READ DETAILS UNTIL SUBFILE PAGE IS FULL
     C                     READ SDPLINXX                 03
     C           *IN03     IFEQ '0'
     C*
     C** IF RECORD IS DELETED, FLAG IT AS SO ON SCREEN
     C           PDRECI    IFEQ '*'
     C                     MOVEL'D'       DDSTAT  1
     C                     ELSE
     C                     MOVEL*BLANK    DDSTAT
     C                     END
     C*
     C                     MOVE PDINNR    DDINNR
     C                     MOVE PDISTN    DDISTN
     C                     ADD  1         RRN     50     53
     C*
     C           RRN       IFEQ WWSAVE
     C                     MOVELWWACTN    DDACTN
     C                     MOVE '1'       *IN34
     C                     END
     C*
     C                     WRITEPM0030S0
     C*
     C** RECORD NOW WRITTEN TO SUBFILE
     C                     END
     C*
     C** PAGE NOW FULL (OR EOF)
     C                     END
     C*
     C** READ ONE MORE RECORD FOR NEXT SCREEN
     C           *IN03     IFEQ '0'
     C                     READ SDPLINXX                 03
     C                     END
     C*
     C** IF NOT EOF SET NEXT 'REVIEW FROM' KEY ELSE SET IT TO BLANK
     C           *IN03     IFEQ '0'
     C                     MOVELPDINNR    DDPKEY
     C                     MOVE '0'       *IN52
     C                     ELSE
     C                     MOVEL*BLANK    DDPKEY
     C                     MOVE '1'       *IN52
     C                     END
     C*
     C** FOR SUBFILE CONTROL
     C           *IN53     IFEQ '0'
     C                     SETON                     5051
     C                     ELSE
     C                     SETON                     51
     C                     SETOF                     50
     C                     END
     C                     END
     C*
     C** R E V I E W   S C R E E N     O U T P U T
     C                     MOVEL'PM90002' MSMBR4
     C                     CALL 'SDRTVTXT'PLLANG
     C                     MOVELMSGTXT    DDINFO
     C*
     C                     WRITEPM0030F2
     C                     EXFMTPM0030C0
     C                     MOVE '0'       *IN75
     C                     Z-ADD0         WWSAVE  40
     C*********************CALL 'ZA0250'                                  R00300
     C                     CALL 'Y2CLMSC'                                 R00300
     C                     PARM           TOPQ                            R00300
     C                     PARM           TOPRQ                           R00300
     C*
     C                     EXSR #ZBAB
     C*
     C** CK3 - TERMINATE
     C           AID       IFEQ '03'
     C                     MOVE '1'       *IN08
     C                     END
     C*
     C** CK12- INITIAL SCREEN
     C           AID       IFEQ '12'
     C                     MOVE '1'       *IN07
     C                     END
     C*
     C** ENTER PRESSED, PROCESS RECORD SELECTION
     C           AID       IFEQ 'RA'
     C                     MOVE '1'       *IN72
     C           *IN,53    IFEQ '0'
     C                     READCPM0030S0                 72
     C                     END
     C*
     C** R E C O R D   S E L E C T   V A L I D A T I O N
     C*
     C           *IN72     IFEQ '0'
     C           WWSTAT    IFEQ 'ICD'                                     37772
     C           DDACTN    ANDNE*BLANK                                    37772
     C           DDSTAT    IFNE 'D'                                       37772
     C                     MOVELDDINNR    WWRTCD                          37772
     C                     MOVE '1'       *INLR                           37772
     C                     RETRN                                          37772
     C                     ELSE                                           37772
     C                     MOVEL'PM03100' ZAMSID                          37772
     C                     EXSR ZASNMS                                    37772
     C                     MOVE '1'       *IN31                           37772
     C                     MOVE '1'       *IN75                           37772
     C                     END                                            37772
     C                     ELSE                                           37772
     C                     EXSR #ZBAA
     C                     END                                            37772
     C*
     C** NO ERRORS, UPDATE SCREEN CAN BE PROCESSED NEXT
     C           *IN75     IFEQ '0'
     C                     MOVE '1'       *IN09
     C                     MOVEL'Y'       WWREVW
     C                     MOVE DDPKEY    WWPAG1
     C                     MOVE *BLANKS   DDPKEY
     C                     ELSE
     C                     MOVELDDACTN    WWACTN  1
     C                     Z-ADDRRN       WWSAVE
     C                     END
     C                     END
     C                     END
     C**
     C                     END
     C*
     C                     ENDSR                           **        ESR**
     C**************************************************************************
      /EJECT
      **************************************************************************
      *                                                                        *
      * #UPDTE   - FORMAT AND OUTPUT DETAIL SCREEN                             *
      *                                                                        *
      * SUBROUTINE CALLS:                                                      *
      * #CB      - PREPARE DETAILS FOR OUTPUT                                  *
      **ZA0250***-*CLEAR*PROGRAM MESSAGE QUEUE                            R00300
      * Y2CLMSC  - CLEAR PROGRAM MESSAGE QUEUE                            R00300
      * #ZBAB    - SET UP 'AID' WITH COMMAND KEY INFORMATION                   *
      * #CC      - VALIDATE INSTRUMENT DETAILS INPUT                           *
      * #CA      - UPDATE FILE DETAILS                                         *
      *                                                                        *
      * SUBROUTINE CALLED FROM:                                                *
      * MAIN     - MAIN CONTROLLING ROUTINE                                    *
      *                                                                        *
      * ROUTINE USES VARIABLES:                                                *
      * WWTNLU   - SAVED TRANSACTION NUMBER OF LAST UPDATE                     *
      * WWREVW   - REVIEW SCREEN SELECTED                                      *
      * WWPAG2   - FIRST RECORD ON NEXT PAGE OF REVIEW SCREEN                  *
      *                                                                        *
      **************************************************************************
     C           #UPDTE    BEGSR                           ** UPDATE BSR**
     C*
     C** SET SCREEN INDICATORS - PROTECT KEY, SET OFF ERROR INDS,
     C** POSITION CURSOR
     C                     SETOF                     3132
     C                     MOVE '1'       *IN30
     C                     MOVE '0'       *IN09
     C*
     C** ACCESS FILE RECORD FOR OLD TNLU TO CHECK AT UPDATE TIME
     C** IF NO RECORD PRESENTLY ON FILE, ZEROISE STORED TNLU
     C                     Z-ADD*ZERO     WWTNLU  50
     C           DDINNR    CHAINSDPLINXX             72
     C           *IN72     IFEQ '0'
     C                     Z-ADDPDTNLU    WWTNLU
     C                     END
     C*
     C** IF DELETED RECORD-ENQUIRY ONLY (RECI IS SET TO BLANK FOR INSERTS)
     C           PDRECI    IFEQ '*'
     C           DDACTN    ANDNE'I'
     C                     MOVEL'E'       DDACTN
     C                     MOVEL'DELETED' DDDELT  7
     C                     ELSE
     C                     MOVEL*BLANK    DDDELT
     C                     END
     C*
     C** TRANSLATE RECORD DETAILS FROM FILE LAYOUT TO SCREEN LAYOUT
     C           DDACTN    IFNE 'I'
     C                     EXSR #CB
     C                     ELSE
     C                     MOVE *BLANKS   DDISTN
     C                     MOVE *BLANKS   DDDSPG
     C                     MOVE *BLANKS   DDITDS
     C                     MOVE *BLANKS   DDVALG
     C                     MOVE *BLANKS   DDVLIS
     C                     MOVE *BLANKS   DDLOMG
     C                     MOVE *BLANKS   DDCSHI
     C                     MOVE *BLANKS   DDWEIG                                              CLE025
     C                     MOVE *BLANKS   DDFCMF                                              CLE025
     C                     MOVE *BLANKS   DDINOF                                              CLE025
     C                     MOVE *BLANKS   DDIOPC                                              CLE025
     C                     MOVE *BLANKS   DDMARG                                              CLE025
     C                     END
     C*
     C** SET UP DEFAULTS (DONE ON BLANK FIELDS)
     C*                                                                   S01486
     C** IF DELETE/ENQUIRY PROTECT FIELD INPUT ON S/PM0030F0
     C           DDACTN    IFEQ 'D'
     C           DDACTN    OREQ 'E'
     C                     MOVE '1'       *IN35
     C                     END
     C*
     C** D O    U N T I L   C H A N G E  O F  S C R E E N
     C           *IN07     DOWEQ'0'
     C           *IN08     ANDEQ'0'
     C           *IN09     ANDEQ'0'
     C*
     C** L O A D    U  P    I N F O M E S S A G E S
     C           DDACTN    IFNE 'D'
     C                     MOVEL'PM90003' MSMBR4
     C                     CALL 'SDRTVTXT'PLLANG
     C                     MOVELMSGTXT    DDINFO
     C                     ELSE
     C                     MOVEL'PM90004' MSMBR4
     C                     CALL 'SDRTVTXT'PLLANG
     C                     MOVELMSGTXT    DDINFO
     C                     END
     C*                                                                   37772
     C** LOAD DEFAULT VALUES WHEN INSERTING FROM ICD                      37772
     C*                                                                   37772
     C           WWSTAT    IFEQ 'ICD'                                     37772
     C                     EXSR #SPEC                                     37772
     C                     END                                            37772
     C*
     C** D E T A I L   S C R E E N     O U T P U T
     C                     WRITEPM0030F2
     C                     WRITEPM0030F1
     C**
     C                     EXFMTPM0030F4
     C                     MOVE '0'       *IN75
     C*********************CALL 'ZA0250'                                  R00300
     C                     CALL 'Y2CLMSC'                                 R00300
     C                     PARM           TOPQ                            R00300
     C                     PARM           TOPRQ                           R00300
     C*
     C                     EXSR #ZBAB
     C*
     C** CK3 - TERMINATE
     C           AID       IFEQ '03'
     C                     MOVE '1'       *IN09
     C                     END
     C*
     C** CK12- INITIAL SCREEN
     C           AID       IFEQ '12'
     C                     MOVE '1'       *IN07
     C                     MOVE '0'       *IN08
     C                     END
     C*
     C** V A L I D A T E   D E T A I L   S C R E E N   I N P U T
     C           AID       IFNE '03'
     C           AID       ANDNE'12'
     C                     EXSR #CC
     C*
     C** N O    D E T A I L   S C R E E N    E R R O R S
     C           *IN75     IFEQ '0'
     C           AID       ANDEQ'RA'
     C*
     C** DETERMINE WHAT SCREEN TO RETURN TO
     C           WWREVW    IFEQ 'Y'
     C                     MOVELWWPAG2    DDPKEY
     C                     MOVE '1'       *IN08
     C                     ELSE
     C                     MOVE '1'       *IN07
     C                     MOVE '0'       *IN08
     C                     END
     C*
     C**     U  P  D  A  T  E      F  I  L  E  S
     C*
     C           DDACTN    IFEQ 'I'
     C           DDACTN    OREQ 'A'
     C           DDACTN    OREQ 'D'
     C                     EXSR #CA
     C                     END
     C*
     C                     END
     C                     END
     C*
     C                     END
     C*
     C** RESET PROTECT/NON-DISPLAY INDS
     C                     MOVE '0'       *IN30
     C                     MOVE '0'       *IN35
     C*                                                                   37772
     C** IF ICD ORIGIN GO BACK TO SUBFILE                                 37772
     C*                                                                   37772
     C           WWSTAT    IFEQ 'ICD'                                     37772
     C                     MOVEA'010'     *IN,07                          37772
     C                     END                                            37772
     C*                                                                   37772
     C           XTUPDT    ENDSR                           ** XTUPDA ESR**
     C*================================================================
      /EJECT
     C*================================================================
     C**************************************************************************
      /EJECT
      **************************************************************************
      *                                                                        *
      * #CA      - UPDATE FILE DETAILS                                         *
      *                                                                        *
      * SUBROUTINE CALLS:                                                      *
      * ZTNLU1   - CALCULATE NEXT AVAILABLE TRANSACTION NUMBER                 *
      * #CAA     - RECORD UPDATED BY ANOTHER WORKSTATION PROCESSING            *
      * *PSSR    - STANDARD ERROR PROCESSING                                   *
      *                                                                        *
      * SUBROUTINE CALLED FROM:                                                *
      * #UPDTE   - FORMAT AND OUTPUT DETAIL SCREEN                             *
      *                                                                        *
      * ROUTINE USES VARIABLES:                                                *
      * WWTNLU   - SAVED TRANSACTION NUMBER OF LAST UPDATE                     *
      * WWHLUP   - TIME OF LAST UPDATE                                         *
      * WWDLUP   - DAY OF LAST UPDATE                                          *
      * WWMLUP   - MONTH OF LAST UPDATE                                        *
      * WWYLUP   - YEAR OF RECORD LAST UPDATED                                 *
      *                                                                        *
      **************************************************************************
     C           #CA       BEGSR                           ** #CA    BSR**
     C*
     C** U P D A T E   F I L E S
     C** RETRIEVE NEXT TRANSACTION NUMBER
     C                     EXSR ZTNLU1
     C*
     C**               I  N  S  E  R  T  S
     C           DDACTN    IFEQ 'I'
     C           DDINNR    CHAINSDPLINPD             72
     C*
     C** RECORD UPDATED BY ANOTHER WORKSTATION
     C           *IN72     IFEQ '0'
     C           PDTNLU    IFNE WWTNLU
     C                     EXSR #CAA
     C                     GOTO XTUPDF
     C                     END
     C                     END
     C*
     C** TRANSLATE RECORD DETAILS FROM SCREEN LAYOUT TO FILE LAYOUT
     C*
     C                     MOVEL'D'       PDRECI
     C                     MOVELDDINNR    PDINNR
     C                     MOVELDDISTN    PDISTN
     C                     MOVELDDDSPG    PDDSPG
     C                     MOVELDDITDS    PDITDS
     C                     MOVELDDVALG    PDVALG
     C                     MOVELDDVLIS    PDVLIS
     C                     MOVELDDLOMG    PDLOMG
     C                     MOVELDDCSHI    PDCSHI
      *                                                                                       CLE025
     C           CLE025    IFEQ 'Y'                                                           CLE025
     C           CFX001    OREQ 'Y'                                                           CFX001
     C                     MOVE DDWEIG    PDWEIG                                              CLE025
     C                     MOVE DDFCMF    PDFCMF                                              CLE025
     C                     MOVE DDINOF    PDINOF                                              CLE025
     C                     MOVE DDIOPC    PDIOPC                                              CLE025
     C                     MOVE DDMARG    PDMARG                                              CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CGL014
     C           CGL014    IFEQ 'Y'                                                           CGL014
     C                     MOVE DDWEIG    PDWEIG                                              CGL014
     C                     ENDIF                                                              CGL014
      *                                                                                       CGL014
     C                     Z-ADDBJRDNB    PDLCD
     C                     MOVE BJMRDT    WWHLUP
     C                     Z-ADDWWDLUP    PDDLUP
     C                     MOVE WWMLUP    PDMLUP
     C                     Z-ADDWWYLUP    PDYLUP
     C                     TIME           PDTLUP
     C                     MOVEL'I'       PDCHTP
     C                     MOVELNATN      PDTNLU
     C           *IN72     IFEQ '1'
     C                     WRITESDPLINP0               15
     C*
     C** TEST FOR UPDATE BY ANOTHER W/S, OR A TERMINAL ERROR
     C           *IN15     IFEQ '1'
     C           STATUS    ANDEQ01021
     C           DDINNR    CHAINSDPLINPD             15
     C           *IN15     IFEQ '0'
     C                     EXSR #CAA
     C                     GOTO XTUPDF
     C                     END
     C                     END
     C           *IN15     IFEQ '1'
     C                     EXSR *PSSR
     C                     END
     C                     ELSE
     C                     UPDATSDPLINP0
     C                     END
     C*
     C                     END
     C*
     C**               A  M  E  N  D  S
     C*
     C           DDACTN    IFEQ 'A'
     C           DDINNR    CHAINSDPLINPD             72
     C           *IN72     IFEQ '0'
     C*
     C** RECORD UPDATED BY ANOTHER WORKSTATION
     C           PDTNLU    IFNE WWTNLU
     C                     EXSR #CAA
     C                     GOTO XTUPDF
     C                     END
     C*
     C** TRANSLATE RECORD DETAILS FROM SCREEN LAYOUT TO FILE LAYOUT
     C                     MOVE DDISTN    PDISTN
     C                     MOVE DDINNR    PDINNR
     C                     MOVE DDDSPG    PDDSPG
     C                     MOVE DDITDS    PDITDS
     C                     MOVE DDVALG    PDVALG
     C                     MOVE DDVLIS    PDVLIS
     C                     MOVE DDLOMG    PDLOMG
     C                     MOVE DDCSHI    PDCSHI
      *                                                                                       CLE025
     C           CLE025    IFEQ 'Y'                                                           CLE025
     C           CFX001    OREQ 'Y'                                                           CFX001
     C                     MOVE DDWEIG    PDWEIG                                              CLE025
     C                     MOVE DDFCMF    PDFCMF                                              CLE025
     C                     MOVE DDINOF    PDINOF                                              CLE025
     C                     MOVE DDIOPC    PDIOPC                                              CLE025
     C                     MOVE DDMARG    PDMARG                                              CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
     C           CGL014    IFEQ 'Y'                                                           CGL014
     C                     MOVE DDWEIG    PDWEIG                                              CGL014
     C                     ENDIF                                                              CGL014
      *                                                                                       CGL014
     C           BJRDNB    IFNE PDLCD
     C                     MOVEL'A'       PDCHTP
     C                     END
     C                     Z-ADDBJRDNB    PDLCD
     C                     MOVE BJMRDT    WWHLUP
     C                     Z-ADDWWDLUP    PDDLUP
     C                     MOVE WWMLUP    PDMLUP
     C                     Z-ADDWWYLUP    PDYLUP
     C                     TIME           PDTLUP
     C                     MOVELNATN      PDTNLU
     C                     UPDATSDPLINP0
     C                     END
     C*
     C                     END
     C*
     C**               D  E  L  E  T  E  S
     C           DDACTN    IFEQ 'D'
     C           DDINNR    CHAINSDPLINPD             72
     C           *IN72     IFEQ '0'
     C*
     C** RECORD UPDATED BY ANOTHER WORKSTATION
     C           PDTNLU    IFNE WWTNLU
     C                     EXSR #CAA
     C                     GOTO XTUPDF
     C                     END
     C*
     C                     MOVEL'*'       PDRECI
     C                     Z-ADDBJRDNB    PDLCD
     C                     MOVE BJMRDT    WWHLUP
     C                     Z-ADDWWDLUP    PDDLUP
     C                     MOVE WWMLUP    PDMLUP
     C                     Z-ADDWWYLUP    PDYLUP
     C                     TIME           PDTLUP
     C                     MOVELNATN      PDTNLU
     C                     MOVEL'D'       PDCHTP
     C                     UPDATSDPLINP0
     C                     END
     C*
     C                     END
     C*
     C** GET AUDIT RECORD
     C***********1         CHAINSDPLINZZ             73                   S01486
     C           1         CHAINSDPLINPA             73                   S01486
     C*
     C** UPDATE AUDIT RECORD IF FOUND
     C           *IN73     IFEQ '0'
     C*
     C** FOR INSERT, UPDATE NO. OF LIVE RECORDS
     C           DDACTN    IFEQ 'I'
     C                     ADD  1         PDNORE
     C                     END
     C*
     C** FOR DELETIONS, UPDATE NO. OF LIVE RECS
     C           DDACTN    IFEQ 'D'
     C                     SUB  1         PDNORE
     C                     END
     C*
     C** FORMAT COMIT TEXT
     C*
     C                     TIME           MTIME
     C                     MOVELDDACTN    ACTNX
     C*
     C** UPDATE AUDIT RECORD
     C                     UPDATSDPLINZ0
     C*
     C** C  O  M  I  T
     C           CMTTXT    COMIT
     C*
     C** AUDIT RECORD NOT FOUND: DATABASE ERROR
     C                     ELSE
     C*                                                    ***************
     C           *LOCK     IN   LDA                                       S01486
     C                     Z-ADD3         DBASE            * DB ERROR 03 *
     C***********          MOVEL'SDPLINZZ'DBFILE           ***************S01486
     C                     MOVEL'SDPLINPA'DBFILE           ***************S01486
     C                     MOVE *BLANKS   DBKEY
     C                     OUT  LDA                                       S01486
     C                     EXSR *PSSR
     C                     END
     C*
     C           XTUPDF    ENDSR
     C**************************************************************************
      /EJECT
      **************************************************************************
      *                                                                        *
      * #CAA     - RECORD UPDATED BY ANOTHER WORKSTATION PROCESSING            *
      *                                                                        *
      * SUBROUTINE CALLS:                                                      *
      **ZA0240***-*ACCESS MESSAGE FILE AND SEND MESSAGE                   R00300
      * ZASNMS   - SEND MESSAGE                                           R00300
      *                                                                        *
      * SUBROUTINE CALLED FROM:                                                *
      * #CA      - UPDATE FILE DETAILS                                         *
      *                                                                        *
      * ROUTINE USES VARIABLES:                                                *
      *                                                                        *
      **************************************************************************
     C           #CAA      BEGSR                           ** #CAA   BSR**
     C**
     C                     ROLBK
     C**   SET UP NEXT SCREEN TO BE DISPLAYED:
     C**   IF RECORD IS LIVE, ALLOW AMEND PROCESSING;
     C           PDRECI    IFEQ 'D'
     C                     MOVEL'A'       DDACTN
     C                     ELSE
     C**
     C**   IF RECORD HAS BEEN DELETED, ALLOW ENQUIRY
     C                     MOVEL'E'       DDACTN
     C                     END
     C**
     C**   SET SCREEN PROCESSOR TO UPDATE AND MOVE UPDATE MESSAGE TO ERROR
     C                     MOVE '1'       *IN07
     C                     MOVE '0'       *IN08
     C                     MOVE '0'       *IN09
     C                     MOVE '1'       *IN75
     C*********************MOVEL'PM03303' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03303' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C**
     C                     ENDSR
     C**************************************************************************
      /EJECT
      **************************************************************************
      *                                                                        *
      * #CB      - PREPARE DETAILS FOR OUTPUT                                  *
      *                                                                        *
      * SUBROUTINE CALLS:                                                      *
      *                                                                        *
      * SUBROUTINE CALLED FROM:                                                *
      * #UPDTE   - FORMAT AND OUTPUT DETAIL SCREEN                             *
      *                                                                        *
      * ROUTINE USES VARIABLES:                                                *
      *                                                                        *
      **************************************************************************
     C           #CB       BEGSR                           ** #CB   BSR **
     C*
     C** TRANSLATE RECORD DETAILS FROM FILE LAYOUT TO SCREEN LAYOUT
     C                     MOVELPDISTN    DDISTN
     C                     MOVE PDDSPG    DDDSPG
     C           PDITDS    IFNE 0                                         R0971
     C                     MOVE PDITDS    DDITDS
     C                     ELSE                                           R0971
     C                     MOVE *BLANKS   DDITDS                          R0971
     C                     END                                            R0971
     C                     MOVE PDVALG    DDVALG
     C           PDVLIS    IFNE 0                                         R0971
     C                     MOVE PDVLIS    DDVLIS
     C                     ELSE                                           R0971
     C                     MOVE *BLANKS   DDVLIS                          R0971
     C                     END                                            R0971
     C                     MOVE PDLOMG    DDLOMG
     C                     MOVE PDCSHI    DDCSHI
      *                                                                                       CLE025
     C           CLE025    IFEQ 'Y'                                                           CLE025
     C           CFX001    OREQ 'Y'                                                           CFX001
     C********** PDWEIG    IFNE 0                                                     CLE025 BUG4009
     C                     MOVE PDWEIG    DDWEIG                                              CLE025
     C**********           ELSE                                                       CLE025 BUG4009
     C**********           MOVE *BLANKS   DDWEIG                                      CLE025 BUG4009
     C**********           ENDIF                                                      CLE025 BUG4009
     C                     MOVE PDFCMF    DDFCMF                                              CLE025
     C                     MOVE PDINOF    DDINOF                                              CLE025
     C                     MOVE PDIOPC    DDIOPC                                              CLE025
     C                     MOVE PDMARG    DDMARG                                              CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CGL014
     C           CGL014    IFEQ 'Y'                                                           CGL014
     C********** PDWEIG    IFNE 0                                                     CGL014 BUG4009
     C                     MOVE PDWEIG    DDWEIG                                              CGL014
     C**********           ELSE                                                       CGL014 BUG4009
     C**********           MOVE *BLANKS   DDWEIG                                      CGL014 BUG4009
     C**********           ENDIF                                                      CGL014 BUG4009
     C                     ENDIF                                                              CGL014
     C*
     C                     ENDSR
     C**************************************************************************
      /EJECT
      **************************************************************************
      *                                                                        *
      * #CC      - VALIDATE INSTRUMENT DETAILS INPUT                           *
      *                                                                        *
      * SUBROUTINE CALLS:                                                      *
      **ZA0240***- ACCESS MESSAGE FILE AND SEND MESSAGE                   R00300
      * ZASNMS   - SEND MESSAGE                                           R00300
      *                                                                        *
      * SUBROUTINE CALLED FROM:                                                *
      * #UPDTE   - FORMAT AND OUTPUT DETAIL SCREEN                             *
      *                                                                        *
      * ROUTINE USES VARIABLES:                                                *
      *                                                                        *
      **************************************************************************
     C           #CC       BEGSR                           ** #CC    BSR**
     C*
     C** RESET SCREEN ERROR INDICATORS
     C                     SETOF                     363738
     C                     SETOF                     394041
     C                     SETOF                     42
     C                     MOVEA'0000'    *IN,46                                              CLE025
     C                     MOVE *OFF      *IN54                                               CLE025
     C*
     C** ENTER KEY PRESSED
     C           AID       IFEQ 'RA'                           ------------
     C*
     C** IF ACTION = D WITH ENTER PRESSED THEN ERROR
     C           DDACTN    IFEQ 'D'
     C*********************MOVEL'PM03301' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVE '1'       *IN75
     C                     MOVEL'PM03301' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     END
     C*
     C** ACTION = I OR A
     C*
     C           DDACTN    IFEQ 'I'
     C           DDACTN    OREQ 'A'
     C*
     C** VALIDATION OF FIELDS FOR INSERT/AMEND
     C** CHECK THAT DDISTN IS VALID
     C           DDISTN    IFEQ *BLANK
     C                     MOVE '1'       *IN36
     C*********************MOVEL'PM03200' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03200' ZAMSID                          R00300
     C                     MOVE '1'       *IN75                           R00300
     C                     EXSR ZASNMS                                    R00300
     C                     END
     C*
     C** FOLLOWING FIELDS ONLY VALIDATED IF PM PRESENT. CHECK DISPLAY GROUP
     C           BGPFMG    IFEQ 'Y'
     C***********DDDSPG    IFNE *BLANKS                              R0971B9257
     C***********DDITDS    ORNE *BLANKS                              R0971B9257
     C***********DDDSPG*****HAINPMDSPGPP             72                   37772
     C           DDDSPG    CHAINPMDSPGXX             72                   37772
     C           *IN72     IFEQ '1'
     C           PARECI    ORNE 'D'
     C                     MOVE '1'       *IN37
     C*********************MOVEL'PM03201' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03201' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN75
     C                     END
     C*
     C****DEFAULT*VALUATION GROUP DETAILS TO DISPLAY GROUP INFORMATION    R0971
     C*****      DDVALG    IFEQ *BLANKS                                   R0971
     C*****      DDVLIS    ANDEQ*BLANKS                                   R0971
     C*****                MOVE DDDSPG    DDVALG                          R0971
     C*****                MOVE DDITDS    DDVLIS                          R0971
     C*****                END                                            R0971
     C*
     C** CHECK INSTR. DISPLAY SEQUENCE
     C                     MOVELDDITDS    WWVLS1  1
     C                     MOVE DDITDS    WWVLS2  1
     C           WWVLS1    IFLT '0'
     C           WWVLS1    ORGT '9'
     C           WWVLS2    ORLT '0'
     C           WWVLS2    ORGT '9'
     C                     MOVE '1'       *IN38
     C*********************MOVEL'PM03202' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03202' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN75
     C                     END
     C*********************END                                       R0971B9257
     C*
     C** CHECK VALUATION GROUP
     C***********DDVALG    IFNE *BLANKS                              R0971B9257
     C***********DDVLIS    ORNE *BLANKS                              R0971B9257
     C***********DDVALG    CHAINPMVLGHPP             72                   37772
     C           DDVALG    CHAINPMVLGHXX             72                   37772
     C           *IN72     IFEQ '1'
     C           PBRECI    ORNE 'D'
     C                     MOVE '1'       *IN39
     C*********************MOVEL'PM03203' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03203' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN75
     C                     END
     C*
     C** CHECK VALUATION GROUP SEQUENCE
     C                     MOVELDDVLIS    WWVLS1  1
     C                     MOVE DDVLIS    WWVLS2  1
     C           WWVLS1    IFLT '0'
     C           WWVLS1    ORGT '9'
     C           WWVLS2    ORLT '0'
     C           WWVLS2    ORGT '9'
     C                     MOVE '1'       *IN40
     C*********************MOVEL'PM03204' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03204' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN75
     C                     END
     C*********************END                                       R0971B9257
     C                     END
     C*
     C** FOLLOWING FIELDS ONLY VALIDATED IF LOMBARD CREDITS PRESENT
     C** CHECK LOMBARD DISPLAY GROUP
     C           BGLMCR    IFEQ 'Y'
     C***********DDLOMG    IFNE *BLANKS                             R0971 36086
     C***********DDCSHI    ORNE *BLANKS                             R0971 36086
     C***********DDLOMG****CHAINLBGROUL1             72                   37772
     C           DDLOMG    CHAINLBGROUXX             72                   37772
     C           *IN72     IFEQ '1'
     C           GPRECI    ORNE 'D'
     C                     MOVE '1'       *IN41
     C*********************MOVEL'PM03206' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03206' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN75
     C                     END
     C           DDCSHI    IFNE 'Y'
     C           DDCSHI    ANDNE'N'
     C                     MOVE '1'       *IN42
     C*********************MOVEL'PM03205' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03205' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN75
     C                     END
     C*********************END                                      R0971 36086
     C                     END
      *                                                                                       CLE025
     C           CLE025    IFEQ 'Y'                                                           CLE025
     C           CGL014    OREQ 'Y'                                                           CGL014
     C           CFX001    OREQ 'Y'                                                           CFX001
      *                                                                                       CLE025
     C           DDWEIG    IFNE *BLANKS                                                       CLE025
     C           *IN76     ANDEQ*ON                                                           CGL014
     C           DDWEIG    ORNE *BLANKS                                                       CLE025
     C           *IN77     ANDEQ*ON                                                           CGL014
      *                                                                                       CLE025
     C                     MOVEL'GP'      AOPGM  10                                           CLE025
     C                     MOVE 'AOWEIGR0'AOPGM                                               CLE025
      *                                                                                       CLE025
     C                     CALL AOPGM                                                         CLE025
     C                     PARM *BLANKS   PRTCD                                               CLE025
     C                     PARM '*VERIFY' POPTN                                               CLE025
     C                     PARM DDWEIG    PWEIG   2                                           CLE025
     C           GPWEIG    PARM GPWEIG    DSFDY                                               CLE025
      *                                                                                       CLE025
     C           PRTCD     IFNE *BLANKS                                                       CLE025
     C           PRTCD     ANDNE'*NRF   '                                                     CLE025
     C           PRTCD     ANDNE'*NOSEL '                                                     CLE025
     C           *LOCK     IN   LDA                                                           CLE025
     C                     MOVEL'SDWEIGPD'DBFILE                                              CLE025
     C                     MOVELPWEIG     DBKEY                                               CLE025
     C                     Z-ADD10        DBASE                                               CLE025
     C                     OUT  LDA                                                           CLE025
     C                     EXSR *PSSR                                                         CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
     C           PRTCD     IFEQ '*NRF   '                                                     CLE025
     C                     MOVE *ON       *IN46                                               CLE025
     C                     MOVEL'PM03207' ZAMSID                                              CLE025
     C                     EXSR ZASNMS                                                        CLE025
     C                     MOVE *ON       *IN75                                               CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
     C           PRTCD     IFEQ *BLANKS                                                       CLE025
     C                     MOVE PWEIG     DDWEIG                                              CLE025
     C                     ENDIF                                                              CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
      ** If left blank, default Facility Mandatory to 'N'                                     CLE025
      *                                                                                       CLE025
     C           DDFCMF    IFEQ *BLANK                                                        CLE025
     C           *IN76     ANDEQ*ON                                                           CGL014
     C                     MOVE 'N'       DDFCMF                                              CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
      ** If entered, it must be 'Y', 'N', or 'C'                                              CLE025
      *                                                                                       CLE025
     C           DDFCMF    IFNE *BLANK                                                        CLE025
     C           DDFCMF    ANDNE'Y'                                                           CLE025
     C           DDFCMF    ANDNE'N'                                                           CLE025
     C           DDFCMF    ANDNE'C'                                                           CLE025
     C           *IN76     ANDEQ*ON                                                           CGL014
     C                     MOVE *ON       *IN47                                               CLE025
     C                     MOVEL'PM03208' ZAMSID                                              CLE025
     C                     EXSR ZASNMS                                                        CLE025
     C                     MOVE *ON       *IN75                                               CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
      ** If left blank and Facility Mandatory is valid,                                       CLE025
      ** set Include Offsets to value of Facility Mandatory                                   CLE025
      *                                                                                       CLE025
     C           DDINOF    IFEQ *BLANK                                                        CLE025
     C           *IN76     ANDEQ*ON                                                           CGL014
     C           *IN47     ANDEQ*OFF                                                          CLE025
     C                     MOVE DDFCMF    DDINOF                                              CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
      ** If entered, Include Offsets must be 'Y', 'N', or 'C'                                 CLE025
      *                                                                                       CLE025
     C           DDINOF    IFNE *BLANK                                                        CLE025
     C           DDINOF    ANDNE'Y'                                                           CLE025
     C           DDINOF    ANDNE'N'                                                           CLE025
     C           DDINOF    ANDNE'C'                                                           CLE025
     C           *IN76     ANDEQ*ON                                                           CGL014
     C                     MOVE *ON       *IN48                                               CLE025
     C                     MOVEL'PM03209' ZAMSID                                              CLE025
     C                     EXSR ZASNMS                                                        CLE025
     C                     MOVE *ON       *IN75                                               CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
      ** If left blank, set Include Offsets in Pre-deal Check to 'N'                          CLE025
      *                                                                                       CLE025
     C           DDIOPC    IFEQ *BLANK                                                        CLE025
     C           *IN76     ANDEQ*ON                                                           CGL014
     C                     MOVE 'N'       DDIOPC                                              CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
      ** If entered, it must be 'Y', 'N', or 'C'                                              CLE025
      *                                                                                       CLE025
     C           DDIOPC    IFNE *BLANK                                                        CLE025
     C           DDIOPC    ANDNE'Y'                                                           CLE025
     C           DDIOPC    ANDNE'N'                                                           CLE025
     C           DDIOPC    ANDNE'C'                                                           CLE025
     C           *IN76     ANDEQ*ON                                                           CGL014
     C                     MOVE *ON       *IN49                                               CLE025
     C                     MOVEL'PM03211' ZAMSID                                              CLE025
     C                     EXSR ZASNMS                                                        CLE025
     C                     MOVE *ON       *IN75                                               CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
      ** If left blank, default Marginable flag to 'N'                                        CLE025
      *                                                                                       CLE025
     C           DDMARG    IFEQ *BLANK                                                        CLE025
     C           *IN76     ANDEQ*ON                                                           CLE025
     C                     MOVE 'N'       DDMARG                                              CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
      ** If entered, it must be 'Y' or 'N'                                                    CLE025
      *                                                                                       CLE025
     C           DDMARG    IFNE *BLANK                                                        CLE025
     C           DDMARG    ANDNE'Y'                                                           CLE025
     C           DDMARG    ANDNE'N'                                                           CLE025
     C           *IN76     ANDEQ*ON                                                           CLE025
     C                     MOVE *ON       *IN54                                               CLE025
     C                     MOVEL'PM03210' ZAMSID                                              CLE025
     C                     EXSR ZASNMS                                                        CLE025
     C                     MOVE *ON       *IN75                                               CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
     C                     END
     C*
     C** ENTER KEY NOT PRESSED
     C                     ELSE
     C*
     C** COMMAND 10 TAKEN CORRECTLY WHEN ACTION IS D-FORCE 'RECORD ADVANCE'
     C** OTHERWISE,COMMAND 10 TAKEN INCORRECTLY WHEN ACTION IS NOT D - ERROR
     C           DDACTN    IFEQ 'D'
     C           AID       IFEQ '10'
     C                     MOVEL'RA'      AID     2
     C                     END
     C                     ELSE
     C           AID       IFEQ '10'
     C*********************MOVEL'PM03302' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03302' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN75
     C                     END
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
     C**************************************************************************
      /EJECT
     C*****************************************************************   R00300
     C*                                                               *   R00300
     C* ZASNMS     - Send message to program message queue            *   R00300
     C*                                                               *   R00300
     C* CALLS      - Y2SNMSGC                                         *   R00300
     C*                                                               *   R00300
     C* CALLED BY  - #CAA - Record updated by another workstation     *   R00300
     C*                     processing                                *   R00300
     C*            - #CC  - Validate instrument details input         *   R00300
     C*            - #ZBAA- Validate initial screen details input     *   R00300
     C*                                                               *   R00300
     C*****************************************************************   R00300
     C           ZASNMS    BEGSR                                          R00300
     C*                                                                   R00300
     C                     CALL 'Y2SNMGC'                                 R00300
     C                     PARM           ZAPGM            Program queue  R00300
     C                     PARM           ZAPGRL           Rel queue      R00300
     C                     PARM           ZAMSID           Message Id.    R00300
     C                     PARM           ZAMSGF           Message file.  R00300
     C                     PARM           ZAMSDA           Message data.  R00300
     C                     PARM           ZAMSTP           Message type.  R00300
     C*                                                                   R00300
     C** Clear all fields for default mechanism next time.                R00300
     C                     MOVEL*BLANK    ZAPGM            Message queue  R00300
     C                     MOVEL*BLANK    ZAPGRL           Rel queue      R00300
     C                     MOVEL*BLANK    ZAMSDA           Message data.  R00300
     C                     MOVEL*BLANK    ZAMSTP           Message type.  R00300
     C*                                                                   R00300
     C           ZASNM9    ENDSR                           ZASNM9 TAG     R00300
     C*                                                                   R00300
      ********************************************************************R00300
      /EJECT                                                              R00300
      **************************************************************************
      *                                                                        *
      * #ZBAA    - VALIDATE INITIAL SCREEN DETAILS INPUT                       *
      *                                                                        *
      * SUBROUTINE CALLS:                                                      *
      **ZA0240***- ACCESS MESSAGE FILE AND SEND MESSAGE                   R00300
      * ZASNMS   - SEND MESSAGE                                           R00300
      *                                                                        *
      * SUBROUTINE CALLED FROM:                                                *
      * #START   - FORMAT AND OUTPUT INITIAL SCREEN                            *
      * #RVIEW   - FORMAT AND OUTPUT REVIEW SCREEN                             *
      *                                                                        *
      * ROUTINE USES VARIABLES:                                                *
      *                                                                        *
      **************************************************************************
     C           #ZBAA     BEGSR                           ** #ZBAA  BSR**
     C*
     C** RESET ERROR INDICATORS
     C                     MOVE '0'       *IN75
     C*
     C** A C T I O N    C O D E    I N P U T - MUST BE BLANK,I,A,E,D
     C*
     C           DDACTN    IFNE *BLANK
     C           DDACTN    ANDNE'I'
     C           DDACTN    ANDNE'A'
     C           DDACTN    ANDNE'E'
     C           DDACTN    ANDNE'D'
     C*********************MOVEL'PM03100' MSGID  10                       R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03100' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN31
     C                     MOVE '1'       *IN75
     C                     END
     C** USER SHOULD BE AUTHORISED TO ACTION CODE                         S01486
     C           DDACTN    IFNE *BLANK                                    S01486
     C*                                                                   S01486
     C** WHEN SINGLE BRANCH                                               S01486
     C           MBIN      IFNE 'Y'                                       S01486
     C*                                                                   S01486
     C                     CALL 'ZVACTU'                                  S01486
     C                     PARM DDACTN    @ZACTN  1                       S01486
     C                     PARM           @ERR    10                      S01486
     C           @ERR      IFNE *ZERO                                     S01486
     C                     MOVEL'PM02000' ZAMSID                          S01486
     C                     EXSR ZASNMS                                    S01486
     C                     MOVE '1'       *IN31                           S01486
     C                     MOVE '1'       *IN75                           S01486
     C                     END                                            S01486
     C*                                                                   S01486
     C                     ELSE                                           S01486
     C*                                                                   S01486
     C** WHEN MULTI-BRANCH                                                S01486
     C                     CALL 'ZVACTBU'                                 S01486
     C                     PARM DDACTN    @ZACTN  1                       S01486
     C                     PARM USRBCH    @ZBR    3                       S01486
     C                     PARM           @ERR    10                      S01486
     C           @ERR      IFNE *ZERO                                     S01486
     C                     MOVEL'PM02000' ZAMSID                          S01486
     C                     EXSR ZASNMS                                    S01486
     C                     MOVE '1'       *IN31                           S01486
     C                     MOVE '1'       *IN75                           S01486
     C                     END                                            S01486
     C*                                                                   S01486
     C                     END                                            S01486
     C                     END                                            S01486
     C*                                                                   S01486
     C*
     C** C H E C K   K E Y  F I E L D S
     C*
     C** ACTION CODE BLANK BUT KEY INPUT
     C           DDACTN    IFEQ *BLANK
     C           DDINNR    ANDNE*BLANK
     C*********************MOVEL'PM03101' MSGID  10                       R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03101' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN31
     C                     MOVE '1'       *IN75
     C                     END
     C**
     C** IF ACTION CODE ENTERED REVIEW FROM MUST BE BLANK
     C**
     C           DDACTN    IFNE *BLANK
     C           DDINNR    ORNE *BLANKS
     C           *IN82     IFNE '1'
     C           DDPKEY    ANDNE*BLANKS
     C*********************MOVEL'PM03305' MSGID  10                      R00300
     C*********************CALL 'ZA0240'                                 R00300
     C*********************PARM           MSGID                          R00300
     C                     MOVEL'PM03305' ZAMSID                         R00300
     C                     EXSR ZASNMS                                   R00300
     C                     MOVE '1'       *IN75
     C                     SETON                     3132
     C                     END
     C                     END
     C*
     C** KEY CANNOT CONTAIN BLANKS EXCEPT IF REVIEW FROM REQUESTED
     C**  (ACTION CODE MUST BE BLANK)
     C                     MOVEADDINNR    DSP,1
     C           ' '       LOKUPDSP                      71
     C           *IN71     IFEQ '1'
     C           DDACTN    ANDNE' '
     C*********************MOVEL'PM03107' MSGID  10                       R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03107' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN32
     C                     MOVE '1'       *IN75
     C                     END
     C*
     C** N O   E R R O R  O N  K E Y  F I E L D S
     C**
     C** A C T I O N    C O D E    I
     C           DDACTN    IFEQ 'I'
     C           DDINNR    CHAINSDPLINXX             72
     C           *IN72     IFEQ '0'
     C           PDRECI    IFEQ 'D'
     C*********************MOVEL'PM03103' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03103' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN32
     C                     MOVE '1'       *IN75
     C                     END
     C                     END
     C                     END
     C*
     C** A C T I O N    C O D E    A , D
     C           DDACTN    IFEQ 'A'
     C           DDACTN    OREQ 'D'
     C           DDINNR    CHAINSDPLINXX             72
     C           *IN72     IFEQ '1'
     C           PDRECI    ORNE 'D'
     C*********************MOVEL'PM03104' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03104' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN32
     C                     MOVE '1'       *IN75
     C                     END
     C                     END
     C**
     C** IF ACTION CODE = D SYSTEM SETUP FLAG MUST BE OFF
     C           DDACTN    IFEQ 'D'
     C           BJSUC     ANDEQ'Y'
     C*********************MOVEL'PM03102' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03102' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN31
     C                     MOVE '1'       *IN75
     C                     END
     C*
     C** IF ACTION 'D', INST. TYPE MUST NOT EXIST ON LF/PMPGDIL1
     C           DDACTN    IFEQ 'D'
     C           DDINNR    CHAINPMPGDIL1             72
     C           *IN72     IFEQ '0'
     C*********************MOVEL'PM03106' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03106' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN31
     C                     MOVE '1'       *IN75
     C                     END
     C                     END
     C*
     C** A C T I O N    C O D E    E
     C           DDACTN    IFEQ 'E'
     C           DDINNR    CHAINSDPLINXX             72
     C           *IN72     IFEQ '1'
     C*********************MOVEL'PM03105' MSGID                           R00300
     C*********************CALL 'ZA0240'                                  R00300
     C*********************PARM           MSGID                           R00300
     C                     MOVEL'PM03105' ZAMSID                          R00300
     C                     EXSR ZASNMS                                    R00300
     C                     MOVE '1'       *IN32
     C                     MOVE '1'       *IN75
     C                     END
     C                     END
     C*
     C                     ENDSR
     C**************************************************************************
      /EJECT
      **************************************************************************
      *                                                                        *
      * #ZBAB    - SET UP 'AID' WITH COMMAND KEY INFORMATION                   *
      *                                                                        *
      * SUBROUTINE CALLS:                                                      *
      *                                                                        *
      * SUBROUTINE CALLED FROM:                                                *
      * #START   - FORMAT AND OUTPUT INITIAL SCREEN                            *
      * #RVIEW   - FORMAT AND OUTPUT REVIEW SCREEN                             *
      * #UPDTE   - FORMAT AND OUTPUT DETAIL SCREEN                             *
      *                                                                        *
      * ROUTINE USES VARIABLES:                                                *
      *                                                                        *
      **************************************************************************
     C           #ZBAB     BEGSR
     C*
     C* If ENTER pressed, the VLDCMDKEY indicator will be off
     C           *IN45     IFEQ '0'
     C                     MOVE 'RA'      AID
     C                     END
     C*
     C* If F3
     C           *INKC     IFEQ '1'
     C                     MOVE '03'      AID
     C                     MOVE '0'       *INKC
     C                     END
     C*
     C* If F10
     C           *INKJ     IFEQ '1'
     C                     MOVE '10'      AID
     C                     MOVE '0'       *INKJ
     C                     END
     C*
     C* If F12
     C           *INKL     IFEQ '1'
     C                     MOVE '12'      AID
     C                     MOVE '0'       *INKL
     C                     END
     C*
     C* If ROLLUP pressed
     C           *IN25     IFEQ '1'
     C                     MOVE 'UP'      AID
     C                     MOVE '0'       *IN25
     C                     END
     C*
     C* If ROLLDOWN pressed
     C*
     C           *IN26     IFEQ '1'
     C                     MOVE 'DN'      AID
     C                     MOVE '0'       *IN26
     C                     END
     C*
     C                     ENDSR
     C**************************************************************************
      /EJECT
      **************************************************************************
      *                                                                        *
      * ZTNLU1   - CALCULATE NEXT AVAILABLE TRANSACTION NUMBER                 *
      *                                                                        *
      * SUBROUTINE CALLS:                                                      *
      *                                                                        *
      * SUBROUTINE CALLED FROM:                                                *
      * #CA      - UPDATE FILE DETAILS                                         *
      *                                                                        *
      * ROUTINE USES VARIABLES:                                                *
      *                                                                        *
      **************************************************************************
     C           ZTNLU1    BEGSR
     C**
     C           *NAMVAR   DEFN MNATN     DNATN
     C           *LOCK     IN   DNATN
     C                     MOVE FNATN     NATN    50
     C                     MOVE FNATN     ZZWK05  50
     C                     ADD  1         ZZWK05
     C                     MOVE ZZWK05    FNATN
     C                     OUT  DNATN
     C**
     C                     ENDSR
     C**************************************************************************
      /EJECT                                                              37772
      ********************************************************************37772*
      *                                                                   37772*
      * #SPEC    - ICD PROCESSING SUBROUTINE                              37772*
      *                                                                   37772*
      * SUBROUTINE CALLS:                                                 37772*
      *                                                                   37772*
      * SUBROUTINE CALLED FROM:                                           37772*
      *                                                                   37772*
      * ROUTINE USES VARIABLES:                                           37772*
      *                                                                   37772*
      ********************************************************************37772*
     C           #SPEC     BEGSR                                          37772
     C*                                                                   37772
     C** SET UP DEFAULTS FOR ICD PROCESSING                               37772
     C** IF NO DISPLAY GROUP, VALUATION GROUP OR LOMBARD CREDIT GROUP EXIS37772
     C** CREATE  DEFAULT DISPLAY GROUP, VALUATION GROUP AND LOMBARD CREDIT37772
     C** GROUP TO ATTACH INSTRUMENT TYPE                                  37772
     C*                                                                   37772
     C** PORTFOLIO DISPLAY GROUP AND VALUATION GROUP                      37772
     C*                                                                   37772
     C           BGPFMG    IFEQ 'Y'                                       37772
     C**                                                                  37772
     C                     MOVE *LOVAL    WWDDPG  3                       37772
     C           WWDDPG    SETLLPMDSPGXX                                  37772
     C                     READ PMDSPGXX                 89               37772
     C           *IN89     IFEQ '1'                                       37772
     C**                                                                  37772
     C** CREATE DEFAULT                                                   37772
     C**                                                                  37772
     C                     EXSR #DSPG                                     37772
     C                     MOVELPADSPG    DDDSPG                          37772
     C**                                                                  37772
     C                     END                                            37772
     C**                                                                  37772
     C                     MOVE *LOVAL    WWDDVG  3                       37772
     C           WWDDVG    SETLLPMVLGHXX                                  37772
     C                     READ PMVLGHXX                 89               37772
     C           *IN89     IFEQ '1'                                       37772
     C**                                                                  37772
     C** CREATE DEFAULT                                                   37772
     C**                                                                  37772
     C                     EXSR #VLGH                                     37772
     C**                                                                  37772
     C                     MOVELPBVALG    DDVALG                          37772
     C                     END                                            37772
     C**                                                                  37772
     C                     END                                            37772
     C*                                                                   37772
     C** LOMBARD CREDIT GROUP                                             37772
     C*                                                                   37772
     C           BGLMCR    IFEQ 'Y'                                       37772
     C**                                                                  37772
     C                     MOVE *LOVAL    WWDDLG  3                       37772
     C           WWDDLG    SETLLLBGROUXX                                  37772
     C                     READ LBGROUXX                 89               37772
     C           *IN89     IFEQ '1'                                       37772
     C**                                                                  37772
     C** CREATE DEFAULT                                                   37772
     C**                                                                  37772
     C                     EXSR #LBGP                                     37772
     C                     MOVELGPLCGR    DDLOMG                          37772
     C**                                                                  37772
     C                     END                                            37772
     C                     END                                            37772
     C**                                                                  37772
     C                     ENDSR                                          37772
      ********************************************************************37772*
      /EJECT                                                              37772
      ********************************************************************37772*
      *                                                                   37772*
      * #DSPG    - DISPLAY GROUP DEFAULT CODE                             37772*
      *                                                                   37772*
      * SUBROUTINE CALLS:                                                 37772*
      *                                                                   37772*
      * SUBROUTINE CALLED FROM:                                           37772*
      *                                                                   37772*
      * ROUTINE USES VARIABLES:                                           37772*
      *                                                                   37772*
      ********************************************************************37772*
     C           #DSPG     BEGSR                                          37772
     C*                                                                   37772
     C** INITIALIZE FIELDS                                                37772
     C*                                                                   37772
     C                     MOVE 'D'       PARECI                          37772
     C                     MOVE 'I'       PACHTP                          37772
     C                     Z-ADDBJRDNB    PALCD                           37772
     C                     MOVE BJMRDT    WWHLUP                          37772
     C                     Z-ADDWWDLUP    PADLUP                          37772
     C                     MOVE WWMLUP    PAMLUP                          37772
     C                     Z-ADDWWYLUP    PAYLUP                          37772
     C                     Z-ADD0         PATNLU                          37772
     C                     MOVEL'001'     PADSPG                          37772
     C                     MOVELDEFD,1    PADSPT                          37772
     C                     Z-ADD1         PADSPS                          37772
     C**                                                                  37772
     C                     WRITEPMDSPGP0               15                 37772
     C*                                                                   37772
     C** TEST FOR UPDATE BY ANOTHER W/S, OR A TERMINAL ERROR              37772
     C           *IN15     IFEQ '1'                                       37772
     C           STATUS    ANDEQ01021                                     37772
     C***********PADSPG    CHAINPMDSPGPP             15              37772S01486
     C           PADSPG    CHAINPMDSPGPD             15                   S01486
     C           *IN15     IFEQ '0'                                       37772
     C                     EXSR #CAA                                      37772
     C                     GOTO XTDSPG                                    37772
     C                     END                                            37772
     C                     END                                            37772
     C           *IN15     IFEQ '1'                                       37772
     C                     EXSR *PSSR                                     37772
     C                     END                                            37772
     C** GET AUDIT RECORD                                                 37772
     C***********1         CHAINPMDSPGZZ             73              37772S01486
     C           1         CHAINPMDSPGPA             73                   S01486
     C*                                                                   37772
     C** UPDATE AUDIT RECORD IF FOUND                                     37772
     C           *IN73     IFEQ '0'                                       37772
     C*                                                                   37772
     C** FOR INSERT, UPDATE NO. OF LIVE RECORDS                           37772
     C                     ADD  1         PANORE                          37772
     C*                                                                   37772
     C** FORMAT COMIT TEXT                                                37772
     C*                                                                   37772
     C                     TIME           MTIME                           37772
     C                     MOVELDDACTN    ACTNX                           37772
     C*                                                                   37772
     C** UPDATE AUDIT RECORD                                              37772
     C                     UPDATPMDSPGZ0                                  37772
     C*                                                                   37772
     C** C  O  M  I  T                                                    37772
     C           CMTTXT    COMIT                                          37772
     C*                                                                   37772
     C** AUDIT RECORD NOT FOUND: DATABASE ERROR                           37772
     C                     ELSE                                           37772
     C           *LOCK     IN   LDA                                       S01486
     C*                                                    ***************37772
     C***********          Z-ADD3         DBASE     * DB ERROR 03 *37772  S01486
     C                     Z-ADD4         DBASE            * DB ERROR 04 *S01486
     C***********          MOVEL'PMDSPGZZ'DBFILE           **********37772S01486
     C                     MOVEL'PMDSPGPA'DBFILE           ***************S01486
     C                     MOVE *BLANKS   DBKEY                           37772
     C                     OUT  LDA                                       S01486
     C                     EXSR *PSSR                                     37772
     C                     END                                            37772
     C**                                                                  37772
     C           XTDSPG    ENDSR                                          37772
      /EJECT                                                              37772
      ********************************************************************37772*
      *                                                                   37772*
      * #VLGH    - VALUATION GROUP DEFAULT CODE                           37772*
      *                                                                   37772*
      * SUBROUTINE CALLS:                                                 37772*
      *                                                                   37772*
      * SUBROUTINE CALLED FROM:                                           37772*
      *                                                                   37772*
      * ROUTINE USES VARIABLES:                                           37772*
      *                                                                   37772*
      ********************************************************************37772*
     C           #VLGH     BEGSR                                          37772
     C*                                                                   37772
     C** INITIALIZE FIELDS                                                37772
     C*                                                                   37772
     C                     MOVE 'D'       PBRECI                          37772
     C                     MOVE 'I'       PBCHTP                          37772
     C                     Z-ADDBJRDNB    PBLCD                           37772
     C                     MOVE BJMRDT    WWHLUP                          37772
     C                     Z-ADDWWDLUP    PBDLUP                          37772
     C                     MOVE WWMLUP    PBMLUP                          37772
     C                     Z-ADDWWYLUP    PBYLUP                          37772
     C                     Z-ADD0         PBTNLU                          37772
     C                     MOVEL'001'     PBVALG                          37772
     C                     MOVELDEFD,2    PBVLGT                          37772
     C                     Z-ADD1         PBVLGS                          37772
     C**                                                                  37772
     C                     WRITEPMVLGHP0               15                 37772
     C*                                                                   37772
     C** TEST FOR UPDATE BY ANOTHER W/S, OR A TERMINAL ERROR              37772
     C           *IN15     IFEQ '1'                                       37772
     C           STATUS    ANDEQ01021                                     37772
     C***********PBVALG    CHAINPMVLGHPP             15              37772S01486
     C           PBVALG    CHAINPMVLGHPD             15                   S01486
     C           *IN15     IFEQ '0'                                       37772
     C                     EXSR #CAA                                      37772
     C                     GOTO XTVLGH                                    37772
     C                     END                                            37772
     C                     END                                            37772
     C           *IN15     IFEQ '1'                                       37772
     C                     EXSR *PSSR                                     37772
     C                     END                                            37772
     C** GET AUDIT RECORD                                                 37772
     C***********1         CHAINPMVLGHZZ             73              37772S01486
     C           1         CHAINPMVLGHPA             73                   S01486
     C*                                                                   37772
     C** UPDATE AUDIT RECORD IF FOUND                                     37772
     C           *IN73     IFEQ '0'                                       37772
     C*                                                                   37772
     C** FOR INSERT, UPDATE NO. OF LIVE RECORDS                           37772
     C                     ADD  1         PBNORE                          37772
     C*                                                                   37772
     C** FORMAT COMIT TEXT                                                37772
     C*                                                                   37772
     C                     TIME           MTIME                           37772
     C                     MOVELDDACTN    ACTNX                           37772
     C*                                                                   37772
     C** UPDATE AUDIT RECORD                                              37772
     C                     UPDATPMVLGHZ0                                  37772
     C*                                                                   37772
     C** C  O  M  I  T                                                    37772
     C           CMTTXT    COMIT                                          37772
     C*                                                                   37772
     C** AUDIT RECORD NOT FOUND: DATABASE ERROR                           37772
     C                     ELSE                                           37772
     C           *LOCK     IN   LDA                                       S01486
     C*                                                    ***************37772
     C***********          Z-ADD3         DBASE      * DB ERROR 03 *37772 S01486
     C                     Z-ADD5         DBASE            * DB ERROR 05 *S01486
     C***********          MOVEL'PMVLGHZZ'DBFILE           **********37772S01486
     C                     MOVEL'PMVLGHPA'DBFILE           ***************S01486
     C                     MOVE *BLANKS   DBKEY                           37772
     C                     OUT  LDA                                       S01486
     C                     EXSR *PSSR                                     37772
     C                     END                                            37772
     C**                                                                  37772
     C           XTVLGH    ENDSR                                          37772
      /EJECT                                                              37772
      ********************************************************************37772*
      *                                                                   37772*
      * #LBGP    - LOMBARD CREDIT GROUP DEFAULT CODE                      37772*
      *                                                                   37772*
      * SUBROUTINE CALLS:                                                 37772*
      *                                                                   37772*
      * SUBROUTINE CALLED FROM:                                           37772*
      *                                                                   37772*
      * ROUTINE USES VARIABLES:                                           37772*
      *                                                                   37772*
      ********************************************************************37772*
     C           #LBGP     BEGSR                                          37772
     C*                                                                   37772
     C** INITIALIZE FIELDS                                                37772
     C*                                                                   37772
     C                     MOVE 'D'       GPRECI                          37772
     C                     MOVE 'I'       GPCHTP                          37772
     C                     Z-ADDBJRDNB    GPLCD                           37772
     C                     MOVE BJMRDT    WWHLUP                          37772
     C                     Z-ADDWWDLUP    GPDLUP                          37772
     C                     MOVE WWMLUP    GPMLUP                          37772
     C                     Z-ADDWWYLUP    GPYLUP                          37772
     C                     Z-ADD0         GPTNLU                          37772
     C                     MOVEL'001'     GPLCGR                          37772
     C                     MOVELDEFD,3    GPLCGN                          37772
     C                     Z-ADD1         GPLCGS                          37772
     C                     MOVE *BLANKS   GPATTI                          37772
     C**                                                                  37772
     C                     WRITELBGROUD0               15                 37772
     C*                                                                   37772
     C** TEST FOR UPDATE BY ANOTHER W/S, OR A TERMINAL ERROR              37772
     C           *IN15     IFEQ '1'                                       37772
     C           STATUS    ANDEQ01021                                     37772
     C           GPLCGR    CHAINLBGROUD0             15                   37772
     C           *IN15     IFEQ '0'                                       37772
     C                     EXSR #CAA                                      37772
     C                     GOTO XTLBGP                                    37772
     C                     END                                            37772
     C                     END                                            37772
     C           *IN15     IFEQ '1'                                       37772
     C                     EXSR *PSSR                                     37772
     C                     END                                            37772
     C** GET AUDIT RECORD                                                 37772
     C                     MOVEL'LBGROUPD'WWLBKY  8
     C           WWLBKY    CHAINLBTRAIPD             73                   37772
     C*                                                                   37772
     C** UPDATE AUDIT RECORD IF FOUND                                     37772
     C           *IN73     IFEQ '0'                                       37772
     C*                                                                   37772
     C** FOR INSERT, UPDATE NO. OF LIVE RECORDS                           37772
     C                     MOVE 'A'       TRCHTP                          37772
     C                     Z-ADDBJRDNB    TRLCD                           37772
     C                     ADD  1         TRINS                           37772
     C                     ADD  1         TRLIVE                          37772
     C*                                                                   37772
     C** FORMAT COMIT TEXT                                                37772
     C*                                                                   37772
     C                     TIME           MTIME                           37772
     C                     MOVELDDACTN    ACTNX                           37772
     C*                                                                   37772
     C** UPDATE AUDIT RECORD                                              37772
     C                     UPDATLBTRAID0                                  37772
     C*                                                                   37772
     C** C  O  M  I  T                                                    37772
     C           CMTTXT    COMIT                                          37772
     C*                                                                   37772
     C** AUDIT RECORD NOT FOUND: DATABASE ERROR                           37772
     C                     ELSE                                           37772
     C           *LOCK     IN   LDA                                       S01486
     C*                                                    ***************37772
     C***********          Z-ADD3         DBASE      * DB ERROR 03 *37772 S01486
     C                     Z-ADD6         DBASE            * DB ERROR 06 *S01486
     C                     MOVEL'LBTRAIPD'DBFILE           ***************37772
     C                     MOVE *BLANKS   DBKEY                           37772
     C                     OUT  LDA                                       S01486
     C                     EXSR *PSSR                                     37772
     C                     END                                            37772
     C**                                                                  37772
     C           XTLBGP    ENDSR                                          37772
      /EJECT
      **************************************************************************
      *                                                                        *
      * *PSSR    - STANDARD ERROR PROCESSING                                   *
      *                                                                        *
      * SUBROUTINE CALLS:                                                      *
      *                                                                        *
      * SUBROUTINE CALLED FROM:                                                *
      * #INIT    - INITIAL PROCESSING                                          *
      * #CA      - UPDATE FILE DETAILS                                         *
      *                                                                        *
      * ROUTINE USES VARIABLES:                                                *
      *                                                                        *
      **************************************************************************
     C           *PSSR     BEGSR
     C*
     C           WRUN      IFNE 'Y'                                       S01486
     C                     MOVEL'Y'       WRUN    1                       S01486
     C*                                                                   S01486
     C                     SETON                     U7U8LR
     C**
     C** ROLL BACK CHANGES, PRINT DUMP AND CANCEL PROGRAM
     C**
     C                     ROLBK
     C                     DUMP
     C                     CALL 'DBERRCTL'                                S01486
     C                     ENDIF                                          S01486
     C                     SETON                     U7U8LR               S01486
     C                     RETRN
     C**
     C                     ENDSR
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**  DEFD                                                                  37772
DEF DISPLAY GROUP                                                         37772
DEF VALUATION GROUP                                                       37772
DEF LOMB.CREDIT GR.                                                       37772
