     H        1                                                           S01486
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Display Group List')
     H***********                                                         S01486
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management Module                      *
     F*                                                               *
     F*  PM0210 - DISPLAY GROUP LIST                                  *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 082662             Date 14SEP95               *
     F*                 S01486             Date 25AUG94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  082662 - Recompiled over printer file                        *
     F*  S01486 - Portfolio Management Upgrade to R10                 *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F***PMDSPGPPIF**E           K        DISK         KINFSR *PSSR       S01486
     FPMDSPGPDIF  E           K        DISK         KINFSR *PSSR          S01486
     F            PMDSPGP0                          KRENAMEPMDSPGPX
     FPMDSPGLCIF  E           K        DISK         KINFSR *PSSR
     F***PMDSPGZZIF**E                    DISK         KINFSR *PSSR       S01486
     FPMDSPGPAIF  E                    DISK         KINFSR *PSSR          S01486
     F***TABSE***IF**E           K        DISK         KINFSR *PSSR       S01486
     F**********  TABLETAF                          KIGNORE               S01486
     F**ICD*1***  TABTB10F                          KIGNORE               S01486
     F**********  TABTB11F                          KIGNORE               S01486
     F**********  TABTB36F                          KIGNORE               S01486
     F**********  TABTB40F                          KIGNORE               S01486
     F**********  TABTE10F                          KIGNORE               S01486
     F**********  TABTE20F                          KIGNORE               S01486
     F**********  TABTG10F                          KIGNORE               S01486
     F**********  TABTG20F                          KIGNORE               S01486
     F**********  TABLETHF                          KIGNORE               S01486
     F**********  TABLETJF                          KIGNORE               S01486
     F**********  TABLETKF                          KIGNORE               S01486
     F**********  TABLETLF                          KIGNORE               S01486
     F**********  TABLETNF                          KIGNORE               S01486
     F**********  TABLETPF                          KIGNORE               S01486
     F**********  TABLETRF                          KIGNORE               S01486
     F**********  TABLETXF                          KIGNORE               S01486
     F**********  TABLET2F                          KIGNORE               S01486
     F**********  TABLET3F                          KIGNORE               S01486
     F**********  TABLET5F                          KIGNORE               S01486
     F***PM0210P1O***E             70     PRINTER                         S01486
     F***PM0210AUO***E                    PRINTER                         S01486
     FPM0210P1O   E             70     PRINTER      KINFDS SPOOL      UC  S01486
     FPM0210AUO   E                    PRINTER      KINFDS SPOOLU         S01486
     F/EJECT
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F**  41      - INPUT PARAMETER 'MODE' IS '1'
     F**  42      - INPUT PARAMETER 'MODE' IS '2'
     F*       69         TO ACCESS FILES
     F*       70         OVERFLOW INDICATOR
     F*       88         NO DETAILS TO REPORT                             S01486
     F*       98         DATF = MMDDYY
     F*       99         FILE OPERATIONS INDICATOR
     F*
     F*       U7         DATABASE ERROR
     F*       U8         PROGRAM  ERROR
     F*
     F**             *************************
     F**             ** INDICATORS NOT USED **
     F**             ** ------------------- **
     F**             *************************
     F**
     F**      ***************************************************
     F**      * 01   02   03   04   05   06   07   08   09   10 *
     F**      * 11   12   13   14   15   16   17   18   19   20 *
     F**      * 21   22   23   24   25   26   27   28   29   30 *
     F**      * 31   32   33   34   35   36   37   38   39   40 *
     F**      * 41   42   43   44   45   46   47   48   49   50 *
     F**      * 51   52   53   ..   55   56   57   58   59   60 *
     F**      * 61   62   63   64   65   66   67   68   ..   .. *
     F**      * 71   72   73   74   75   76   77   78   79   80 *
     F**      * 81   82   83   84   85   86   87   ..   89   90 *         S01486
     F**      * 91   92   93   94   95   96   97   ..   ..      *
     F**      ***************************************************
     E                    CPY@    1   1 80
     E*
     E/EJECT
     I*
     I**LDA******  UDS                            256                     S01486
     I***********                           132 183 DBLOT                 S01486
     I***********                           132 141 DBFILE                S01486
     ILDA         DS                            256                       S01486
     I                                      134 183 DBLOT                 S01486
     I                                      134 141 DBFILE                S01486
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE                 S01486
     ISDBANK    E DSSDBANKPD                                              S01486
     I** EXTERNAL DS FOR BANK DETAILS                                     S01486
     IDSFDY     E DSDSFDY                                                 S01486
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01486
     ISPOOL       DS                                                      S01496
     I                                       83  92 SFILE                 S01496
     I                                    B 123 1240SFNUM                 S01496
     ISPOOLU      DS                                                      S01496
     I                                       83  92 SFILEU                S01496
     I                                    B 123 1240SFNUMU                S01496
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I/EJECT
     C**================================================================
     C**           PROGRAM  START
     C**================================================================
     C**
     C** PARAMETER ENTRY
     C**
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
     C                     PARM           RSEQ    5                       S01486
     C**
     C** INITIALIZATION
     C**
     C                     EXSR #A
     C**
     C** CHECK MODE IF NO ERRORS
     C**
     C**
     C           MODE      CASEQ'1'       #BA
     C**
     C           MODE      CASEQ'2'       #BB
     C**
     C                     CAS            #CC
     C**
     C                     END
     C**
     C                     EXSR #C
     C**
     C**================================================================
     C**           PROGRAM  END
     C**================================================================
     C**
     C                     MOVE '1'       *INLR
     C**
     C*================================================================
     C/EJECT
     C*================================================================
      ** INDEX OF SUBROUTINES
      **
      ** #A                INITIALISATION OF STANDARD FIELDS,
      **                   ACCESS OF STANDING DATA
      **
      ** #BA               PROCESS 1 IF MODE =1
      **
      ** #BB               PROCESS 2 IF MODE = 2
      **
      ** #C                PROCESS AUDIT
      **
      ** #ZBA1             CHECK CHANGE TYPE AND COMPUTE
      **
      ** #ZBA2             TRANSFER FILE FIELDS TO PRINTER FIELDS
      **
      ** #CA               WRITE AUDIT
      **
      ** *PSSR             ERROR SUBROUTINE
      **
     C*================================================================
     C/EJECT
     C*================================================================
      **
      ** #A   S/R - TO INITIALISE STATIC FIELDS AND ACCESS STANDING DATA
      ** CALLED ONCE AT START OF PROGRAM FROM MAIN LINE
      ** CALLS NO OTHER ROUTINES
      **
     C*================================================================
     C*
     C           #A        BEGSR                           ** #A    BSR **
     C*
     C*
     C** PREPARE LDA
     C*
     C           *NAMVAR   DEFN           LDA                             S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'PM0210'  DBPGM
     C                     OUT  LDA                                       S01486
     C*
     C** GET INSTALLATION CONTROL DATA RECORD 1
     C*
     C************         READ TABTB10F                 99               S01486
     C************IN99     IFEQ '1'                                       S01486
     C************IN99     OREQ '0'                                       S01486
     C***********RECI      ANDNE'D'                                       S01486
     C                     CALL 'AOBANKR0'                                S01486
     C                     PARM '*MSG   ' WWRTCD  7                       S01486
     C                     PARM '*FIRST ' WWOPTN  7                       S01486
     C           SDBANK    PARM SDBANK    DSFDY                           S01486
     C**                                                                  S01486
     C**  IF RECORD FOUND:                                                S01486
     C**                                                                  S01486
     C           WWRTCD    IFNE *BLANKS                                   S01486
     C*
     C** OTHERWISE DB ERROR
     C*                                                    ***************
     C           *LOCK     IN   LDA                        ***************S01486
     C                     MOVEL'01'      DBASE            **DB ERROR 01**
     C                     MOVE 'SDBANKPD'DBFILE           ***************S01486
     C***********          MOVEL'TABSE'   DBFILE           ***************S01486
     C***********          MOVEL'01'      ZTABKY 12                       S01486
     C***********          MOVE '10'      ZTABKY                          S01486
     C***********          MOVELZTABKY    DBKEY                           S01486
     C                     OUT  LDA                                       S01486
     C                     WRITEPM0210F1                                  S01486
     C                     EXSR *PSSR
     C                     END
     C*                                                                   S01486
     C**  ENSURE AUDIT SPOOL FILE RECORDED BY RCF                         S01486
     C*                                                                   S01486
     C                     MOVE RSEQ      SEQ                             S01486
     C                     Z-ADDSFNUMU    ZSNUMU  60                      S01486
     C*                                                                   S01486
     C                     CALL 'ZSFILE'                                  S01486
     C                     PARM           SEQ                             S01486
     C                     PARM *BLANKS   SENTY                           S01486
     C                     PARM           SFILEU                          S01486
     C                     PARM           ZSNUMU                          S01486
     C                     PARM *BLANK    ZSERR                           S01486
     C*                                                                   S01486
     C           ZSERR     IFEQ 'Y'                                       S01486
     C*                                                                   S01486
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       S01486
     C*                                                                   S01486
     C                     SETON                     U7U8LR               S01486
     C                     RETRN                                          S01486
     C                     END                                            S01486
     C*
     C                     ENDSR                           ** #A     ESR**
     C*================================================================
     C/EJECT
     C**=============================================================
     **
     ** #BA   SUBROUTINE TO PRINT DISPLAY GROUP INSERTED, AMENDED
     **       OR DELETED DURING DAY
     **
     C**=============================================================
     C*
     C           #BA       BEGSR                           **#BA   SR**
     C**
     C                     MOVE '1'       *IN41
     C***********          WRITEHEADER                                    S01486
     C**
     C***********RUND      SETLLPMDSPGLC                                  S01486
     C***********RUND      READEPMDSPGLC                 69               S01486
     C           BJRDNB    SETLLPMDSPGLC                                  S01486
     C           BJRDNB    READEPMDSPGLC                 69               S01486
     C           *IN69     IFEQ '0'                                       S01486
     C                     OPEN PM0210P1                                  S01486
     C*                                                                   S01486
     C**  ENSURE REPORT SPOOL FILE RECORDED BY RCF                        S01486
     C*                                                                   S01486
     C                     Z-ADDSFNUM     ZSNUM   60                      S01486
     C*                                                                   S01486
     C                     CALL 'ZSFILE'                                  S01486
     C                     PARM           SEQ     5                       S01486
     C                     PARM *BLANKS   SENTY   3                       S01486
     C                     PARM           SFILE                           S01486
     C                     PARM           ZSNUM                           S01486
     C                     PARM           ZSERR   1                       S01486
     C*                                                                   S01486
     C           ZSERR     IFEQ 'Y'                                       S01486
     C*                                                                   S01486
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       S01486
     C*                                                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     SETON                     U7U8LR               S01486
     C                     RETRN                                          S01486
     C                     END                                            S01486
     C*                                                                   S01486
     C                     WRITEHEADER                                    S01486
     C                     ELSE                                           S01486
     C                     MOVE '1'       *IN88                           S01486
     C                     GOTO ENDBA                                     S01486
     C                     END                                            S01486
     C**
     C           *IN69     DOWEQ'0'
     C**
     C           *IN70     IFEQ '1'
     C                     WRITEHEADER
     C                     MOVE '0'       *IN70
     C                     END
     C**
     C** COMPARE LAST CHANGE DATE AND RUN DATE
     C**
     C***********PALCD     IFEQ RUND                                      S01486
     C           PALCD     IFEQ BJRDNB                                    S01486
     C**
     C** CHECK CHANGE TYPE AND COMPUTE
     C**
     C                     EXSR #ZBA1
     C**
     C                     EXSR #ZBA2
     C**
     C                     WRITELIST
     C                     END
     C**
     C***********RUND      READEPMDSPGLC                 69               S01486
     C           BJRDNB    READEPMDSPGLC                 69               S01486
     C**
     C                     END
     C**
     C                     WRITEENDLI
     C                     CLOSEPM0210P1                                  S01486
     C**
     C***********          ENDSR                                          S01486
     C           ENDBA     ENDSR                                          S01486
     C**=============================================================
     C/EJECT
     C**=============================================================
     **
     ** #BB   SUBROUTINE TO PRINT ALL DISPLAY GROUPS
     **
     **
     C**=============================================================
     C**
     C           #BB       BEGSR                           **#BB    SR**
     C**
     C**
     C                     MOVE '1'       *IN42
     C***********          WRITEHEADER                                    S01486
     C**
     C                     MOVE *LOVAL    WWDSPG  3
     C***********WWDSPG    SETLLPMDSPGPP                                  S01486
     C           WWDSPG    SETLLPMDSPGPD                                  S01486
     C**
     C***********          READ PMDSPGPP                 69               S01486
     C                     READ PMDSPGPD                 69               S01486
     C           *IN69     IFEQ '0'                                       S01486
     C                     OPEN PM0210P1                                  S01486
     C*                                                                   S01486
     C**  ENSURE REPORT SPOOL FILE RECORDED BY RCF                        S01486
     C*                                                                   S01486
     C                     Z-ADDSFNUM     ZSNUM   60                      S01486
     C*                                                                   S01486
     C                     CALL 'ZSFILE'                                  S01486
     C                     PARM           SEQ     5                       S01486
     C                     PARM *BLANKS   SENTY   3                       S01486
     C                     PARM           SFILE                           S01486
     C                     PARM           ZSNUM                           S01486
     C                     PARM           ZSERR   1                       S01486
     C*                                                                   S01486
     C           ZSERR     IFEQ 'Y'                                       S01486
     C*                                                                   S01486
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       S01486
     C*                                                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     SETON                     U7U8LR               S01486
     C                     RETRN                                          S01486
     C                     END                                            S01486
     C                     WRITEHEADER                                    S01486
     C                     ELSE                                           S01486
     C                     MOVE '1'       *IN88                           S01486
     C                     GOTO ENDBB                                     S01486
     C                     END                                            S01486
     C**
     C           *IN69     DOWEQ'0'
     C**
     C           *IN70     IFEQ '1'
     C                     WRITEHEADER
     C                     MOVE '0'       *IN70
     C                     END
     C**
     C           PARECI    IFEQ 'D'
     C                     ADD  1         WWNORE  50
     C                     EXSR #ZBA1
     C                     EXSR #ZBA2
     C                     WRITELIST
     C                     END
     C**
     C***********          READ PMDSPGPP                 69               S01486
     C                     READ PMDSPGPD                 69               S01486
     C**
     C                     END
     C**
     C                     WRITEENDLI
     C                     CLOSEPM0210P1                                  S01486
     C**
     C***********          ENDSR                                          S01486
     C           ENDBB     ENDSR                                          S01486
     C**=============================================================
     C/EJECT
     C**=============================================================
     **
     ** #ZBA1 SUBROUTINE TO DETERMINE CHANGE TYPE AND COMPUTE
     **       RECORDS
     **
     C**=============================================================
     C**
     C           #ZBA1     BEGSR                           **#ZBA1  SR**
     C**
     C           PACHTP    IFEQ 'I'
     C                     ADD  1         WWINST  50
     C                     MOVEL'INSERT ' WWCHGT  7
     C                     END
     C**
     C           PACHTP    IFEQ 'A'
     C                     ADD  1         WWAMEN  50
     C                     MOVEL'AMEND  ' WWCHGT
     C                     END
     C**
     C           PACHTP    IFEQ 'D'
     C                     ADD  1         WWDELT  50
     C                     MOVEL'DELETE ' WWCHGT
     C                     END
     C**
     C                     ENDSR
     C**=============================================================
     C/EJECT
     C**=============================================================
     **
     ** #ZBA2 SUBROUTINE TO TRANSFER FILE AND WORK FIELDS TO
     **       PRINTER FIELDS
     **
     C**=============================================================
     C**
     C           #ZBA2     BEGSR                           **#ZBA2   SR**
     C**
     C                     Z-ADDPADLUP    RRDLUP
     C                     MOVE PAMLUP    RRMLUP
     C                     Z-ADDPAYLUP    RRYLUP
     C                     MOVE PADSPG    RRDSPG
     C                     MOVE PADSPT    RRDSPT
     C                     MOVE PADSPS    RRDSPS
     C                     MOVE WWCHGT    RRCHGT
     C                     Z-ADDPATLUP    RRTLUP
     C                     Z-ADDPANORE    P1NORE
     C**
     C                     ENDSR
     C**=============================================================
     C/EJECT
     C**=============================================================
     **
     ** #C    SUBROUTINE TO CHAIN ON TRAILER FILE
     **
     **
     C**=============================================================
     C           #C        BEGSR                           ** #C   SR**
     C**
     C** GET AUDIT RECORD
     C*
     C************         READ PMDSPGZZ                 99               S01486
     C                     READ PMDSPGPA                 99               S01486
     C**
     C           *IN99     IFEQ '0'
     C**
     C                     EXSR #CA
     C*
     C** AUDIT RECORD NOT FOUND: DATABASE ERROR
     C*
     C                     ELSE
     C*
     C           *LOCK     IN   LDA                                       S01486
     C                     MOVEL'02'      DBASE            **DB ERROR 02**
     C************         MOVEL'PMDSP'   DBFILE           ***************S01486
     C                     MOVEL'PMDSPGPA'DBFILE           ***************S01486
     C                     MOVEL'AUDIT'   DBKEY
     C                     OUT  LDA                                       S01486
     C                     WRITEPM0210F1                                  S01486
     C                     EXSR *PSSR
     C                     END
     C**
     C                     ENDSR
     C**=============================================================
     C/EJECT
     C**=============================================================
     **
     ** #CA   SUBROUTINE TO PRINT AUDIT REPORT
     **
     **
     C**=============================================================
     C**
     C           #CA       BEGSR
     C**
     C           MODE      IFEQ '1'
     C                     MOVE '1'       *IN41
     C                     Z-ADDWWINST    RRNOIN
     C                     Z-ADDWWAMEN    RRNOAM
     C                     Z-ADDWWDELT    RRNODE
     C                     Z-ADDPANORE    P1NORE
     C                     WRITEHEADAU
     C                     WRITECNTRL1
     C   88                WRITENODETAIL                                  S01486
     C***********          WRITEENDAU                                     S01486
     C                     END
     C**
     C           MODE      IFEQ '2'
     C                     MOVE '1'       *IN42
     C           PANORE    IFNE WWNORE
     C                     SETON                     U854
     C                     END
     C                     Z-ADDPANORE    P1NORE
     C                     Z-ADDWWNORE    P2NORE
     C                     WRITEHEADAU
     C                     WRITECNTRL2
     C   88                WRITENODETAIL                                  S01486
     C***********          WRITEENDAU                                     S01486
     C                     END
     C**
     C                     ENDSR
     C**=============================================================
     C/EJECT
     C**==============================================================
     C**
     C**  #CC  SUBROUTINE TO OUTPUT MODE     ERROR
     C**
     C**==============================================================
     C**
     C           #CC       BEGSR                           **#CC   SR**
     C**
     C           *LOCK     IN   LDA                                       S01486
     C                     MOVEL'03'      DBASE            **DB  03 **
     C                     MOVEL'PM0210'  DBPGM
     C                     MOVEL'MODE  '  DBFILE
     C                     MOVELMODE      DBKEY
     C                     OUT  LDA                                       S01486
     C                     WRITEPM0210F1                                  S01486
     C                     EXSR *PSSR
     C**
     C                     ENDSR                           **#CC  ESR**
     C**=============================================================
     C/EJECT
     C*================================================================
     C**
     C**   FILE EXEPTION/ERROR HANDLING SUBROUTINE
     C**
     C           *PSSR     BEGSR
     C*
     C                     SETON                     U7U8LR
     C**
     C** PRINT DUMP AND CANCEL PROGRAM
     C**
     C                     DUMP
     C                     RETRN
     C**
     C                     ENDSR
     C*================================================================
     C/EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
