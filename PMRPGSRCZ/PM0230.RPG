     H        1                                                           S01486
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Instrument Types list')
     H***********                                                         S01486
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management Module                          *
     F*                                                               *
     F*  PM0230 - INSTRUMENT TYPES LIST                               *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CFX001             Date 20Oct03               *
      *                 CGL014             Date 20Oct03               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 085806             Date 10Mar95               *
      *                 S01486             Date 26Aug94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CFX001 - FX Margin Trading                                   *
      *  CGL014 - Collaterals Processing                              *
      *  CLE025 - Credit Lines                                        *
     F*  085806 - OUTPUT 'CASH INSTRUMENT' INDICATOR                  *
     F*  S01486 - Portfolio Management Upgrade to R10                 *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F***SDPLINPDIF**E           K        DISK         KINFSR *PSSR       S01486
     F************SDPLINP0                          KRENAMESDPLINPX       S01486
     F***SDPLINLCIF**E           K        DISK         KINFSR *PSSR       S01486
     F***SDPLINZZIF**E                    DISK         KINFSR *PSSR       S01486
     FSDPLINPAIF  E                    DISK         KINFSR *PSSR          S01486
     F***SDBANKPDIF  E                    DISK         KINFSR *PSSR       S01486
     F***PM0230P1O***E             70     PRINTER                         S01486
     F***PM0230AUO***E                    PRINTER                         S01486
     FPM0230P1O   E             70     PRINTER      KINFDS SPOOL      UC  S01486
     FPM0230AUO   E                    PRINTER      KINFDS SPOOLU         S01486
     F**
     F*==================================================================
     F**                FUNCTION OF INDICATORS
     F**
     F**  41      - INPUT PARAMETER 'MODE' IS '1'
     F**  42      - INPUT PARAMETER 'MODE' IS '2'
     F**  54      - PRINT '*** DIFFERENCE ***' LEGEND
     F**  70      - PAGE OVERFLOW
     F**
     F****80      - ACCESS SDBANKPD                                       S01486
     F****81      - ACCESS SDPLINLC                                       S01486
     F****82      - ACCESS SDPLINPD                                       S01486
     F****83      - ACCESS SDPLINZZ                                       S01486
     F**  83      - ACCESS SDPLINPA                                       S01486
     F**
     F**  98      - SYSTEM DATE FORMAT
     F**
     F**  U8      - FILE OUT OF BALANCE
     F**  U7 U8   - DATABASE ERROR / DATABASE CONTROL ERROR
     F**
     F**  LR      - END OF FILE
     F**
     F**                 *************************
     F**                 ** INDICATORS NOT USED **
     F**                 *************************
     F**
     F**      ***************************************************
     F**      * 01   02   03   04   05   06   07   08   09   10 *
     F**      * 11   12   13   14   15   16   17   18   19   20 *
     F**      * 21   22   23   24   25   26   27   28   29   30 *
     F**      * 31   32   33   34   35   36   37   38   39   40 *
     F**      * __   __   43   44   45   46   47   48   49   50 *
     F**      * 51   52   53   __   55   56   57   58   59   60 *
     F**      * 61   62   63   64   65   66   67   68   69   __ *
     F**      * 71   72   73   74   75   76   77   78   79   __ *
     F**      * __   __   __   84   85   86   87   ..   89   90 *         S01486
     F**      * 91   92   93   94   95   96   97   __   99      *
     F**      ***************************************************
     F**
     F*==================================================================
     F/EJECT
     E**
     E                    CPY@    1   1 80
     E/EJECT
     I**
     I**  LOCAL DATA AREA FOR DATA BASE ERRORS:
     I**
     I**LDA******  UDS                            256                     S01486
     I***********                           132 183 DBLOT                 S01486
     I***********                           132 141 DBFILE                S01486
     ILDA         DS                            256                       S01486
     I                                      134 183 DBLOT                 S01486
     I                                      134 141 DBFILE                S01486
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 183 DBASE
     ISDBANK    E DSSDBANKPD                                              S01486
     I** EXTERNAL DS FOR BANK DETAILS                                     S01486
     ISDPLIN    E DSSDPLINPD                                              S01486
     I** EXTERNAL DS FOR INSTRUNMENT DETAILS                              S01486
     ISCSARD    E DSSCSARDPD                                                                  CLE025
      ** External DS for Switchable Features file                                             CLE025
      *                                                                                       CLE025
     IDSFDY     E DSDSFDY                                                 S01486
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01486
     IDSSDY     E DSDSSDY                                                 S01486
     I* FIRST DS FOR ACCESS PROGRAMS, LONG  DATA STRUCTURE                S01486
     ISPOOL       DS                                                      S01486
     I                                       83  92 SFILE                 S01486
     I                                    B 123 1240SFNUM                 S01486
     ISPOOLU      DS                                                      S01486
     I                                       83  92 SFILEU                S01486
     I                                    B 123 1240SFNUMU                S01486
     I**
     I**  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION:
     I**
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I/EJECT
     C**
     C**================================================================
     C**           PROGRAM  START
     C**================================================================
     C**
     C**  DEFINE INPUT PARAMETERS:
     C**
     C           *ENTRY    PLIST
     C                     PARM           MODE    1
     C                     PARM           RSEQ    5                       S01486
     C**
     C**  INITIALIZATION:
     C**
      ***  Copyright Statement.                                           S01486
     C                     MOVEACPY@      ACT@   80                       S01486
     C                     EXSR #INIT
     C**
     C**  PROCESS AUDIT REPORT:
     C**
     C           MODE      IFEQ '1'                        - B1
     C                     EXSR #PR01
     C                     ELSE                            - X1
     C**
     C**  PROCESS FULL INSTRUMENT TYPES LIST:
     C**
     C           MODE      IFEQ '2'                        - B2
     C                     EXSR #PR02
     C                     ELSE                            - X2
     C**
     C**  PROCESS DATABASE ERROR:
     C**
     C           *LOCK     IN   LDA                                       S01486
     C                     MOVEL'02'      DBASE            *****************
     C                     MOVEL'PM0230'  DBPGM            ** DB ERROR 02 **
     C                     MOVEL'MODE  '  DBFILE           *****************
     C                     MOVELMODE      DBKEY
     C                     OUT  LDA                                       S01486
     C**
     C                     EXSR *PSSR
     C                     END                             - E2
     C**
     C                     END                             - E1
     C**
     C**  PROCESS AUDIT:
     C**
     C                     EXSR #AU01
     C**================================================================
     C**           PROGRAM  END
     C**================================================================
     C**
     C                     MOVE '1'       *INLR
     C**
     C**================================================================
     C/EJECT
     C**================================================================
      **                INDEX TO SUBROUTINES
      **
      ** #INIT    -        PROCESS TO INITIALIZE STANDARD FIELDS AND ACCESS
      **                           OF STANDING DATA
      ** #PR01    -        PROCESS AUDIT REPORT (MODE = '1')
      ** #PR02    -        PROCESS INSTRUMENT TYPES FULL LIST (MODE = '2')
      ** *PSSR    -        PROCESS DATABASE ERROR
      ** #AU01    -        PROCESS AUDIT
      **
      ** #TYPE    -        PROCESS TO DETERMINE CHANGE TYPE AND COMPUTE RECORDS
      ** #FIELD   -        PROCESS TO TRANSFER FILE & WORK FIELDS TO PRINTER
      **                           FIELDS
      **
      ** #REPOR   -        PROCESS TO PRINT AUDIT REPORT
      **
     C**================================================================
     C/EJECT
     C**=================================================================
      **
      **  #INIT       SUBROUTINE TO INITIALIZE STATIC FIELDS AND ACCESS STANDING
      **              DATA CALLED ONCE AT START OF PROGRAM FROM MAIN LINE.
     C**
      **     CALLED BY PROGRAM
      **     CALLS     *PSSR
      **
     C**=================================================================
     C**                                                   *****************
     C           #INIT     BEGSR                           ** #INIT BEGSR **
     C**                                                   *****************
     C**  PREPARE LDA:
     C**
     C           *NAMVAR   DEFN           LDA                             S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'PM0230'  DBPGM
     C                     OUT  LDA                                       S01486
     C**
     C**  INITIALIZE INDICATORS:
     C**
     C                     MOVE '0'       *IN
     C**
     C**  GET INSTALLATION CONTROL DATA RECORD 1:
     C**
     C************         READ SDBANKPD                 80               S01486
     C                     CALL 'AOBANKR0'                                S01486
     C                     PARM '*MSG   ' WWRTCD  7                       S01486
     C                     PARM '*FIRST ' WWOPTN  7                       S01486
     C           SDBANK    PARM SDBANK    DSFDY                           S01486
     C**
     C**  IF RECORD FOUND:
     C**
     C************IN80     IFEQ '0'                        - B1           S01486
     C           WWRTCD    IFEQ *BLANKS                                   S01486
     C**
     C**  CHECK SYSTEM DATE FORMAT (DDMMYY OR MMDDYY):
     C**
     C           BJDFIN    IFEQ 'M'                        - B2
     C                     MOVE '1'       *IN98            ON => MMDDYY
     C                     END                             - E2
     C                     ELSE                            - X1
     C**
     C**  OTHERWISE DB ERROR:
     C**
     C           *LOCK     IN   LDA                                       S01486
     C                     MOVEL'01'      DBASE            *****************
     C***********          MOVEL'SDBANKPD'DBFILE           ** DB ERROR 01 S01486
     C                     MOVE 'SDBANKPD'DBFILE           ** DB ERROR 01 S01486
     C                     MOVEL' NONE '  DBKEY            *****************
     C                     OUT  LDA                                       S01486
     C**
     C                     EXSR *PSSR
     C                     END                             - E1
      *                                                                                       CLE025
      ** Check if enhancement CLE025 (Multi-Option Facilities) is on                          CLE025
      *                                                                                       CLE025
     C                     CALL 'AOSARDR0'                                                    CLE025
     C                     PARM *BLANKS   PRTCD   7                                           CLE025
     C                     PARM '*VERIFY' POPTN   7                                           CLE025
     C                     PARM 'CLE025'  PSARD   6                                           CLE025
     C           SCSARD    PARM SCSARD    DSSDY                                               CLE025
      *                                                                                       CLE025
     C           PRTCD     IFNE *BLANKS                                                       CLE025
     C           PRTCD     ANDNE'*NRF   '                                                     CLE025
     C           *LOCK     IN   LDA                                                           CLE025
     C                     MOVEL'SCSARDPD'DBFILE                                              CLE025
     C                     MOVEL'CLE025'  DBKEY                                               CLE025
     C                     MOVEL'04'      DBASE                                               CLE025
     C                     OUT  LDA                                                           CLE025
     C                     EXSR *PSSR                                                         CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CLE025
     C           PRTCD     IFEQ *BLANKS                                                       CLE025
     C                     MOVEA'11111'   *IN,25                                              CLE025
     C                     MOVE 'Y'       CLE025  1                                           CLE025
     C                     ELSE                                                               CLE025
     C                     MOVEA'00000'   *IN,25                                              CLE025
     C                     MOVE 'N'       CLE025                                              CLE025
     C                     ENDIF                                                              CLE025
      *                                                                                       CGL014
      ** Check if enhancement CGL014 (Collaterals Processing) is on                           CGL014
      *                                                                                       CGL014
     C                     CALL 'AOSARDR0'                                                    CGL014
     C                     PARM *BLANKS   PRTCD                                               CGL014
     C                     PARM '*VERIFY' POPTN                                               CGL014
     C                     PARM 'CGL014'  PSARD                                               CGL014
     C           SCSARD    PARM SCSARD    DSFDY                                               CGL014
      *                                                                                       CGL014
     C           PRTCD     IFNE *BLANKS                                                       CGL014
     C           PRTCD     ANDNE'*NRF   '                                                     CGL014
     C           *LOCK     IN   LDA                                                           CGL014
     C                     MOVEL'SCSARDPD'DBFILE                                              CGL014
     C                     MOVEL'CGL014'  DBKEY                                               CGL014
     C                     MOVE '005'     DBASE                                               CGL014
     C                     OUT  LDA                                                           CGL014
     C                     EXSR *PSSR                                                         CGL014
     C                     ENDIF                                                              CGL014
      *                                                                                       CGL014
     C           PRTCD     IFEQ *BLANKS                                                       CGL014
     C                     MOVE *ON       *IN25                                               CGL014
     C                     MOVE 'Y'       CGL014  1                                           CGL014
     C                     ELSE                                                               CGL014
     C                     MOVE 'N'       CGL014                                              CGL014
     C                     ENDIF                                                              CGL014
      *                                                                                       CFX001
      ** Check if enhancement CFX001 (FX Margin Trading) is on                                CFX001
      *                                                                                       CFX001
     C                     CALL 'AOSARDR0'                                                    CFX001
     C                     PARM *BLANKS   PRTCD                                               CFX001
     C                     PARM '*VERIFY' POPTN                                               CFX001
     C                     PARM 'CFX001'  PSARD                                               CFX001
     C           SCSARD    PARM SCSARD    DSFDY                                               CFX001
      *                                                                                       CFX001
     C           PRTCD     IFNE *BLANKS                                                       CFX001
     C           PRTCD     ANDNE'*NRF   '                                                     CFX001
     C           *LOCK     IN   LDA                                                           CFX001
     C                     MOVEL'SCSARDPD'DBFILE                                              CFX001
     C                     MOVEL'CFX001'  DBKEY                                               CFX001
     C                     MOVE '006'     DBASE                                               CFX001
     C                     OUT  LDA                                                           CFX001
     C                     EXSR *PSSR                                                         CFX001
     C                     ENDIF                                                              CFX001
      *                                                                                       CFX001
     C           PRTCD     IFEQ *BLANKS                                                       CFX001
     C                     MOVEA'11111'   *IN,25                                              CFX001
     C                     MOVE 'Y'       CFX001  1                                           CFX001
     C                     ELSE                                                               CFX001
     C                     MOVE 'N'       CFX001                                              CFX001
     C                     ENDIF                                                              CFX001
     C*                                                                   S01486
     C**  ENSURE AUDIT SPOOL FILE RECORDED BY RCF                         S01486
     C*                                                                   S01486
     C                     MOVE RSEQ      SEQ                             S01486
     C                     Z-ADDSFNUMU    ZSNUMU  60                      S01486
     C*                                                                   S01486
     C                     CALL 'ZSFILE'                                  S01486
     C                     PARM           SEQ                             S01486
     C                     PARM *BLANKS   SENTY                           S01486
     C                     PARM           SFILEU                          S01486
     C                     PARM           ZSNUMU                          S01486
     C                     PARM *BLANK    ZSERR                           S01486
     C*                                                                   S01486
     C           ZSERR     IFEQ 'Y'                                       S01486
     C*                                                                   S01486
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       S01486
     C*                                                                   S01486
     C                     SETON                     U7U8LR               S01486
     C                     RETRN                                          S01486
     C                     END                                            S01486
     C**                                                   *****************
     C                     ENDSR                           ** #INIT ENDSR **
     C**                                                   *****************
     C**================================================================
     C/EJECT
     C**=================================================================
     **
     **  #PR01       SUBROUTINE TO PRINT INSTRUMENT TYPES INSERTED, AMENDED
     **              OR DELETED DURING DAY.
     **
     C**     CALLED BY PROGRAM
     C**     CALLS     #TYPE, #FIELD
     **
     C**=================================================================
     C**                                                   *****************
     C           #PR01     BEGSR                           ** #PR01 BEGSR **
     C**                                                   *****************
     C**  PRINT LIST HEADER:
     C**
     C                     MOVE '1'       *IN41
     C***********          WRITEHEADLI                                    S01486
     C**
     C**  ACCESS SDPLINLC:
     C**
     C***********BJRDNB    SETLLSDPLINLC                                  S01486
     C***********BJRDNB    READESDPLINLC                 81               S01486
     C                     CALL 'AOPLINR0'                                S01486
     C                     PARM '*MSG    'WWRTCD  7                       S01486
     C                     PARM '*FIRST  'WWOPTN  7                       S01486
     C                     PARM           WWINNR  3                       S01486
     C           SDPLIN    PARM SDPLIN    DSSDY                           S01486
     C           WWRTCD    IFEQ *BLANKS                                   S01486
     C                     OPEN PM0230P1                                  S01486
     C*                                                                   S01486
     C**  ENSURE REPORT SPOOL FILE RECORDED BY RCF                        S01486
     C*                                                                   S01486
     C                     Z-ADDSFNUM     ZSNUM   60                      S01486
     C*                                                                   S01486
     C                     CALL 'ZSFILE'                                  S01486
     C                     PARM           SEQ     5                       S01486
     C                     PARM *BLANKS   SENTY   3                       S01486
     C                     PARM           SFILE                           S01486
     C                     PARM           ZSNUM                           S01486
     C                     PARM           ZSERR   1                       S01486
     C*                                                                   S01486
     C           ZSERR     IFEQ 'Y'                                       S01486
     C*                                                                   S01486
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       S01486
     C*                                                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     SETON                     U7U8LR               S01486
     C                     RETRN                                          S01486
     C                     END                                            S01486
     C                     WRITEHEADLI                                    S01486
     C                     ELSE                                           S01486
     C                     MOVE '1'       *IN88                           S01486
     C                     GOTO END01                                     S01486
     C                     END                                            S01486
     C**
     C**  SO LONG AS LAST CHANGE DATE = RUN DATE:
     C**
     C************IN81     DOWEQ'0'                        - B1           S01486
     C           WWRTCD    DOWEQ*BLANKS                                   S01486
     C**
     C**  EXECUTE PAGE OVERFLOW:
     C**
     C           *IN70     IFEQ '1'                        - B2
     C                     WRITEHEADLI
     C                     MOVE '0'       *IN70
     C                     END                             - E2
     C**
     C**  DETERMINE CHANGE TYPE AND COMPUTE RECORDS:
     C**
     C                     EXSR #TYPE
     C**
     C**  TRANSFER FILE AND WORK FIELDS TO PRINTER FIELDS:
     C**
     C                     EXSR #FIELD
     C**
     C**  PRINT LIST:
     C**
     C                     WRITELIST
     C**
     C**  ACCESS SDPLINLC AGAIN:
     C**
     C***********          READ SDPLINLC                 81               S01486
     C                     CALL 'AOPLINR0'                                S01486
     C                     PARM '*MSG    'WWRTCD  7                       S01486
     C                     PARM '*NEXT   'WWOPTN  7                       S01486
     C                     PARM           WWINNR  3                       S01486
     C           SDPLIN    PARM SDPLIN    DSSDY                           S01486
     C                     END                             - E1
     C**
     C**  PRINT LIST END:
     C**
     C                     WRITEENDLI
     C                     CLOSEPM0230P1                                  S01486
     C**                                                   *****************
     C***********          ENDSR                           ** #PR01 ENDSR S01486
     C           END01     ENDSR                           ** #PR01 ENDSR S01486
     C**                                                   *****************
     C**=================================================================
     C/EJECT
     C**=================================================================
     **
     **  #PR02       SUBROUTINE TO PRINT ALL INSTRUMENT TYPES.
     **
     **     CALLED BY PROGRAM
     **     CALLS     #TYPE, #FIELD
     **
     C**=================================================================
     C**                                                   *****************
     C           #PR02     BEGSR                           ** #PR02 BEGSR **
     C**                                                   *****************
     **  PRINT LIST HEADER:
     **
     C                     MOVE '1'       *IN42
     C***********          WRITEHEADLI                                    S01486
     C**
     **  ACCESS SDPLINPD:
     **
     C                     MOVE *LOVAL    WWINNR  3
     C***********WWINNR    SETLLSDPLINPD                                  S01486
     C***********                                                         S01486
     C***********          READ SDPLINPD                 82               S01486
     C                     CALL 'AOPLINR0'                                S01486
     C                     PARM '*MSG    'WWRTCD  7                       S01486
     C                     PARM '*FIRST  'WWOPTN  7                       S01486
     C                     PARM           WWINNR  3                       S01486
     C           SDPLIN    PARM SDPLIN    DSSDY                           S01486
     C           WWRTCD    IFEQ *BLANKS                                   S01486
     C                     OPEN PM0230P1                                  S01486
     C*                                                                   S01486
     C**  ENSURE REPORT SPOOL FILE RECORDED BY RCF                        S01486
     C*                                                                   S01486
     C                     Z-ADDSFNUM     ZSNUM   60                      S01486
     C*                                                                   S01486
     C                     CALL 'ZSFILE'                                  S01486
     C                     PARM           SEQ     5                       S01486
     C                     PARM *BLANKS   SENTY   3                       S01486
     C                     PARM           SFILE                           S01486
     C                     PARM           ZSNUM                           S01486
     C                     PARM           ZSERR   1                       S01486
     C*                                                                   S01486
     C           ZSERR     IFEQ 'Y'                                       S01486
     C*                                                                   S01486
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM       S01486
     C*                                                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     SETON                     U7U8LR               S01486
     C                     RETRN                                          S01486
     C                     END                                            S01486
     C                     WRITEHEADLI                                    S01486
     C                     ELSE                                           S01486
     C                     MOVE '1'       *IN88                           S01486
     C                     GOTO END02                                     S01486
     C                     END                                            S01486
     C**
     **  SO LONG AS END OF FILE NOT REACHED:
     **
     C************IN82     DOWEQ'0'                        - B1           S01486
     C           WWRTCD    DOWEQ*BLANKS                    - B1           S01486
     C**
     **  EXECUTE PAGE OVERFLOW:
     **
     C           *IN70     IFEQ '1'                        - B2
     C                     WRITEHEADLI
     C                     MOVE '0'       *IN70
     C                     END                             - E2
     C**
     **  FOR EACH LIVE RECORD READ:
     **
     C           PDRECI    IFEQ 'D'                        - B2
     C**
     **  ADD 1 TO NUMBER OF RECORDS READ:
     **
     C                     ADD  1         WWNORE  50
     C**
     **  DETERMINE CHANGE TYPE AND COMPUTE RECORDS:
     **
     C                     EXSR #TYPE
     C**
     **  TRANSFER FILE AND WORK FIELDS TO PRINTER FIELDS:
     **
     C                     EXSR #FIELD
     C**
     **  PRINT LIST:
     **
     C                     WRITELIST
     C                     END                             - E2
     C**
     **  ACCESS SDPLINPD AGAIN:
     **
     C***********          READ SDPLINPD                 82               S01486
     C                     CALL 'AOPLINR0'                                S01486
     C                     PARM '*MSG    'WWRTCD  7                       S01486
     C                     PARM '*NEXT   'WWOPTN  7                       S01486
     C                     PARM           WWINNR  3                       S01486
     C           SDPLIN    PARM SDPLIN    DSSDY                           S01486
     C                     END                             - E1
     C**
     **  PRINT LIST END:
     **
     C                     WRITEENDLI
     C                     CLOSEPM0230P1                                  S01486
     C**                                                   *****************
     C***********          ENDSR                           ** #PR02 ENDSR S01486
     C           END02     ENDSR                           ** #PR02 ENDSR S01486
     C**                                                   *****************
     C**================================================================
     C/EJECT
     C**=================================================================
     C**
     C**  *PSSR       SUBROUTINE TO OUTPUT DATABASE ERROR.
     C**
     C**     CALLED BY PROGRAM, #INIT, #AU01
     C**     CALLS     -
     C**
     C**=================================================================
     C**                                                   *****************
     C           *PSSR     BEGSR                           ** *PSSR BEGSR **
     C**                                                   *****************
     C           WWERRO    IFEQ ' '                        - B1
     C                     MOVE 'Y'       WWERRO  1
     C**
     C           DBASE     IFNE '000'                      - B2
     C**
     C**  PRINT ERROR MESSAGE:
     C**
     C                     MOVE '1'       *IN41
     C                     WRITEHEADAU
     C                     WRITEERRORAU
     C***********          WRITEENDAU                                     S01486
     C                     ELSE                            - X2
     C**
     C                     MOVE '999'     DBASE
     C                     END                             - E2
     C**
     C                     DUMP
     C                     END                             - E1
     C**
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C**
     C                     RETRN
     C**                                                   *****************
     C                     ENDSR                           ** *PSSR ENDSR **
     C**                                                   *****************
     C**================================================================
     C/EJECT
     C**=================================================================
     **
     **  #AU01       SUBROUTINE TO CHAIN ON TRAILER FILE.
     **
     **     CALLED BY PROGRAM
     **     CALLS     #REPOR, *PSSR
     **
     C**=================================================================
     **                                                    *****************
     C           #AU01     BEGSR                           ** #AU01 BEGSR **
     C**                                                   *****************
     C**  ACCESS SDPLINZZ:
     C**
     C***********          READ SDPLINZZ                 83               S01486
     C                     READ SDPLINPA                 83               S01486
     C**
     C           *IN83     IFEQ '0'                        - B1
     C**
     C**  PRINT AUDIT REPORT:
     C**
     C                     EXSR #REPOR
     C                     ELSE                            - X1
     C**
     C**  AUDIT RECORD NOT FOUND THEN DATABASE ERROR:
     C**
     C           *LOCK     IN   LDA                                       S01486
     C                     MOVEL'03'      DBASE            *****************
     C***********          MOVEL'SDPLINZZ'DBFILE           ** DB ERROR 03 S01486
     C                     MOVE 'SDPLINPA'DBFILE           ** DB ERROR 03 S01486
     C                     MOVEL'AUDIT'   DBKEY            *****************
     C                     OUT  LDA                                       S01486
     C**
     C                     EXSR *PSSR
     C                     END                             - E1
     C**                                                   *****************
     C                     ENDSR                           ** #AU01 ENDSR **
     C**                                                   *****************
     C**================================================================
     C/EJECT
     C**=================================================================
     **
     **  #TYPE       SUBROUTINE TO DETERMINE CHANGE TYPE AND COMPUTE
     **              RECORDS.
     **
     **     CALLED BY #PR01, #PR02
     **     CALLS     -
     **
     C**=================================================================
     C**                                                   *****************
     C           #TYPE     BEGSR                           ** #TYPE BEGSR **
     C**                                                   *****************
     C**  IF THE LAST CHANGE TYPE IS 'I', ADD 1 TO NUMBER OF INSERTS:
     C**
     C           PDCHTP    IFEQ 'I'                        - B1
     C                     ADD  1         WWNOIN  50
     C                     MOVEL'INSERT'  WWCHTP  6
     C                     END                             - E1
     C**
     C**  IF THE LAST CHANGE TYPE IS 'A', ADD 1 TO NUMBER OF AMENDS:
     C**
     C           PDCHTP    IFEQ 'A'                        - B1
     C                     ADD  1         WWNOAM  50
     C                     MOVEL'AMEND '  WWCHTP
     C                     END                             - E1
     C**
     C**  IF THE LAST CHANGE TYPE IS 'D', ADD 1 TO NUMBER OF DELETES:
     C**
     C           PDCHTP    IFEQ 'D'                        - B1
     C                     ADD  1         WWNODE  50
     C                     MOVEL'DELETE'  WWCHTP
     C                     END                             - E1
     C**                                                   *****************
     C                     ENDSR                           ** #TYPE ENDSR **
     C**                                                   *****************
     C**================================================================
     C/EJECT
     C**=================================================================
     **
     **  #FIELD      SUBROUTINE TO TRANSFER FILE AND WORK FIELDS TO
     **              PRINTER FIELDS.
     **
     **     CALLED BY #PRO1, #PR02
     **     CALLS     -
     **
     C**=================================================================
     C**                                                   ******************
     C           #FIELD    BEGSR                           ** #FIELD BEGSR **
     C**                                                   ******************
     C                     MOVE PDINNR    RRINNR
     C                     MOVE PDISTN    RRISTN
     C                     MOVE PDDSPG    RRDSPG
     C                     MOVE PDITDS    RRITDS
     C                     MOVE PDVALG    RRVALG
     C                     MOVE PDVLIS    RRVLIS
     C                     MOVE PDLOMG    RRLOMG
     C                     MOVE PDCSHI    RRCSHI                          085806
     C                     MOVE WWCHTP    RRCHTP
     C                     Z-ADDPDDLUP    RRDLUP
     C                     MOVE PDMLUP    RRMLUP
     C                     Z-ADDPDYLUP    RRYLUP
     C                     Z-ADDPDTLUP    RRTLUP
      *                                                                                       CLE025
     C           CLE025    IFEQ 'Y'                                                           CLE025
     C                     Z-ADDPDWEIG    RRWEIG                                              CLE025
     C                     MOVE PDFCMF    RRFCMF                                              CLE025
     C                     MOVE PDINOF    RRINOF                                              CLE025
     C******               MOVE PDIOPC    RRIOPC                                    CLE025    QQQQQQ
     C                     MOVE PDIOPC    RRIOPC  1                                 CLE025    QQQQQQ
     C                     ENDIF                                                              CLE025
      *                                                                                       CGL014
     C           CGL014    IFEQ 'Y'                                                           CGL014
     C                     Z-ADDPDWEIG    RRWEIG                                              CGL014
     C                     ENDIF                                                              CGL014
      *                                                                                       CFX001
     C           CFX001    IFEQ 'Y'                                                           CFX001
     C                     MOVE PDFCMF    RRFCMF                                              CFX001
     C                     MOVE PDMARG    RRMARG                                              CFX001
     C                     ENDIF                                                              CFX001
     C**                                                   ******************
     C                     ENDSR                           ** #FIELD ENDSR **
     C**                                                   ******************
     C**================================================================
     C/EJECT
     C**=================================================================
     **
     **  #REPOR      SUBROUTINE TO PRINT AUDIT REPORT.
     **
     **     CALLED BY #AU01
     **     CALLS     -
     **
     C**=================================================================
     C**                                                   ******************
     C           #REPOR    BEGSR                           ** #REPOR BEGSR **
     C**                                                   ******************
     C**  WHEN MODE IS '1':
     C**
     C           MODE      IFEQ '1'                        - B1
     C                     Z-ADDWWNOIN    RRNOIN
     C                     Z-ADDWWNOAM    RRNOAM
     C                     Z-ADDWWNODE    RRNODE
     C                     Z-ADDPDNORE    R1NORE
     C**
     C**  PRINT AUDIT REPORT:
     C**
     C                     MOVE '1'       *IN41
     C                     WRITEHEADAU
     C                     WRITECONTROL1
     C   88                WRITENODETAIL                                  S01486
     C***********          WRITEENDAU                                     S01486
     C                     ELSE                            - X1
     C**
     C**  WHEN MODE IS '2':
     C**
     C           MODE      IFEQ '2'                        - B2
     C**
     C**  IF COUNT OF LIVE RECORDS READ NOT EQUAL TO NUMBER TOTAL OF RECORDS:
     C**
     C           WWNORE    IFNE PDNORE                     - B3
     C**
     C**  PRINT '*** DIFFERENCE ***' LEGEND:
     C**
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *IN54
     C                     END                             - E3
     C**
     C                     Z-ADDPDNORE    R1NORE
     C                     Z-ADDWWNORE    R2NORE
     C**
     C**  PRINT AUDIT REPORT:
     C**
     C                     MOVE '1'       *IN42
     C                     WRITEHEADAU
     C                     WRITECONTROL2
     C   88                WRITENODETAIL                                  S01486
     C***********          WRITEENDAU                                     S01486
     C                     END                             - E2
     C**
     C                     END                             - E1
     C**                                                   ******************
     C                     ENDSR                           ** #REPOR ENDSR **
     C**                                                   ******************
     C**=================================================================
     C/EJECT
     C**================================================================
**  CPY@
(c) Finastra International Limited 2001
