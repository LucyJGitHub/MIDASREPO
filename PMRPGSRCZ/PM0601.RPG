     H
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Notepad Maintenance')
/*OVRF*: OVRDBF FILE(PMPORXXX) TOFILE(PMPORTPD) SHARE(*NO)          : *   S01486
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management                             *
     F*                                                               *
     F*  PM0601 - NOTEPAD MAINTENANCE                                 *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 BUG6198            Date 04Apr05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CPM005             Date 20Jun96               *
      *                 092515             Date 11Aug95               *
     F*                 080080             DATE 22DEC94               *
     F*                 080202             DATE 20DEC94               *
     F*                 S01486             DATE 08JUL94               *
     F*                 S01313             DATE 06MAY93               *
     F*                 SA9108             DATE 06NOV91               *
     F*                 049467             DATE 28JAN93               *
     F*                 R10042             DATE 20SEP90               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  BUG6198- Cannot save new Details for CUSD (Recompile)        *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
     F*  CPM005 - Private Banking Phase II                            *
     F*           Focus Group Changes Upgraded to DBA                 *
     F*           Just Recompiled                                     *
     F*  092515 - Also called from Customer Enquiry                   *
     F*  080080 - Accessing of Customer file changed to cope with     *
     F*           being called from Customer maintenance              *
     F*  080202 - Portfolio Notepad fields not updated                *
     F*  S01486 - Portfolio Management Upgrade                        *
     F*  S01313 - RECOMPILE DUE TO PORTFOLIO PERFORMANCE              *
     F*  SA9108 - POOL SECURITIES TRANSACTIONS (ONLY RECOMPILED       *
     F*           FOR DTAARA/LDA)                                     *
     F*  049467 - RECOMPILED FOR RELEASE 3 PTF 02                     *
     F*  R10042 - Display Customer subfile on whole screen if         *
     F*           Portfolio subfile is not to be displayed.           *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*: OVRDBF FILE(PMPORXXX) TOFILE(PMPORTPP) SHARE(*NO)          : *   S01486
     F*                                                               *
     F*****************************************************************
     F****SDBANKL1IF  E           K        DISK                           S01486
     F** PREFIX : BJ                                                      S01486
     F***********                                   KINFSR *PSSR          S01486
     F****SDPLCSL2IF  E           K        DISK                           S01486
     F** PREFIX : BB + QB                                                 S01486
     F**********                                    KINFSR *PSSR          S01486
     FSDCUSTL0IF  E           K        DISK         KINFSR *PSSR      UC  080080
     FPMPORXXXIF  E           K        DISK
     F** PREFIX : PN
     F                                              KINFSR *PSSR
     F****PMCSNPPPUF  E           K        DISK                      A    S01486
     FPMCSNPPDUF  E           K        DISK                      A        S01486
     F** PREFIX : PR
     F                                              KINFSR *PSSR
     FPMCSNPL1IF  E           K        DISK
     F                                              KINFSR *PSSR
     F            PMCSNPP0                          KRENAMEPMCSNPXX
     F****PMPTNPPPUF  E           K        DISK                      A    S01486
     FPMPTNPPDUF  E           K        DISK                      A        S01486
     F** PREFIX : PR
     F                                              KINFSR *PSSR
     FPMPTNPL1IF  E           K        DISK
     F                                              KINFSR *PSSR
     F            PMPTNPP0                          KRENAMEPMPTNPXX
     F****PM0600DDCF  E                    WORKSTN                        S01486
     FPM0600DFCF  E                    WORKSTN                            S01486
     F** PREFIX : DD
     F                                              KINFDS INFDS
     F                                        DDRRN KSFILE PM0600S0
     F                                        DDRRN KSFILE PM0600S1
     F                                        DDRRN KSFILE PM0600S2       R10042
     F*
     F*
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*       01         CUSTOMER MASTER DETAILS (PF1)                 N
     F*       02         CUSTOMER DOCUMENTATION  (PF2)                 N
     F*       03         EXIT                    (PF3)                 N
     F*       05         PORTFOLIO DEFINITION    (PF5)                 N
     F*       12         PREVIOUS SCREEN        (PF12)                 N
     F*
     F*       41         SFLDSP - CUSTOMER SFL
     F*       42         SFLDSP - PORTFOLIO SFL
     F*       43         CLEAR SUBFILE DEATAILS
     F*       49         VLDCMDKY
     F*
     F******* 80         FILE OPERATIONS INDICATOR - SDBANKL1             S01486
     F*       81         FILE OPERATIONS INDICATOR - SDPLCSL2
     F*       82         FILE OPERATIONS INDICATOR - PMPORXXX
     F******* 83         FILE OPERATIONS INDICATOR - PMCSNPPP             S01486
     F*       83         FILE OPERATIONS INDICATOR - PMCSNPPD             S01486
     F*       84         FILE OPERATIONS INDICATOR - PMCSNPXX
     F******* 85         FILE OPERATIONS INDICATOR - PMPTNPPP             S01486
     F*       85         FILE OPERATIONS INDICATOR - PMPTNPPD             S01486
     F*       86         FILE OPERATIONS INDICATOR - PMPTNPXX
     F******* 87         FILE OPERATIONS INDICATOR - PM0600DD             S01486
     F*       87         FILE OPERATIONS INDICATOR - PM0600DF             S01486
     F*
     F*       98         DATE FORMAT = MMDDYY
     F*       99         WORK
     F*
     F*                Unused indicators
     F*                -----------------
     F*       ..  ..  ..  04  ..  06  07  08  09  10
     F*       11  ..  13  14  15  16  17  18  19  20
     F*       21  22  23  24  25  26  27  28  29  30
     F*       31  32  33  34  35  36  37  38  39  40
     F*       ..  ..  ..  44  45  46  47  48  ..  50
     F*       51  52  53  54  55  56  57  58  59  60
     F*       61  62  63  64  65  66  67  68  69  70
     F*       71  72  73  74  75  76  77  78  79  ..
     F*       ..  ..  ..  ..  ..  ..  ..  88  89  90
     F*       91  92  93  94  95  96  97  ..  ..
     F*
     F*
     F*       U7         DATABASE ERROR
     F*       U8         PROGRAM  ERROR
     F*
     F*       'WWAID' VALUES USED
     F*
     F*       01         F1
     F*       02         F2
     F*       03         F3
     F*       05         F5
     F*       12         F12
     F*       RA         ENTER PRESSED
     F*       UP         ROLLUP
     F*       DN         ROLLDOWN
     E*                                                                   080080
     E                    OVR     1   1 32                                080080
     E*
     E                    CPY@    1   1 80
     I*
     I** DATA STRUCTURES FOR CURSOR POSITION ON SCREEN
     I*
     IINFDS       DS
     I                                      370 371 WWCPOS
     IWWCREF      DS
     I                                        1   1 WWPOS1
     I                                        2   2 WWPOS2
     I*
     I** LOCAL DATA AREA FOR DATABASE ERROR DETAILS
     I*
     ILDA         DS                            256
     I**********                            132 141 DBFILE                S01486
     I                                      134 141 DBFILE                S01486
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I** DATA STRUCTURE FOR ZTNLU1
     I*
     I*    MIDAS 'Next Available Transaction No.' Data Area DNATN .
     IDNATN       DS                              5
     I                                        1   50FNATN
     I*
     I** PROGRAM STATUS DATA STRUCTURE FOR WS/USER ID'S
     I*
     IPSDS       SDS
     I************************************* 244 253 WSID                  S01486
     I************************************* 254 263 USID                  S01486
     I                                      244 253 WKSTN                 S01486
     I                                      254 263 USER                  S01486
     I*
     I** DATA STRUCTURE FOR LAST UPDATE
     I*
     IWWHLUP      DS
     I                                        1   20WWDLUP
     I                                        3   5 WWMLUP
     I                                        6   70WWYLUP
     I*                                                                   S01486
     ISDBANK    E DSSDBANKPD                                              S01486
     I** EXTERNAL DS FOR BANK DETAILS                                     S01486
     I***SDPLCS****E DSSDPLCSPD                                     S01486080080
     I**Portfolio*Customer*Details*Accessed*Via*Access*Program******S01486080080
     I***********                                                   S01486080080
     I*                                                                   S01486
     ISDCUST    E DSSDCUSTPD                                              S01486
     I** DUMMY RECORD FORMAT FOR CUSTOMER DETAILS                         S01486
     I*                                                                   S01486
     IDSFDY     E DSDSFDY                                                 S01486
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01486
     I*                                                                   S01486
     IDSSDY     E DSDSSDY                                                 S01486
     I* SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE                S01486
     I*
     I** DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     I*
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I**
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INDEX OF SUBROUTINES                                             *
      * MAIN     -                                                       *
      * #INIT    - INITIAL PROCESSING                                    *
      * #P01     - FORMAT CUSTOMER NOTEPAD DETAILS                       *
      * #P02     - FORMAT PORTFOLIO NOTEPAD DETAILS                      *
      * #P03     - PROCESS PM NOTEPAD SCREEN                             *
      * #P04     - UPDATE PM CUSTOMER NOTEPAD                            *
      * #P05     - UPDATE PM PORTFOLIO NOTEPAD                           *
      * #SFLC    - FILL CUSTOMER SUBFILE                                 *
      * #SFLP    - FILL PORTFOLIO SUBFILE                                *
      * #UPWS    - FILE UPDATED BY ANOTHER WORKSTATION                   *
      * #VCMD    - VALIDATE COMMAND KEYS                                 *
      * ZTNLU1   - RETRIEVE NEXT TRANSACTION NUMBER                      *
      * *PSSR    - FILE ERROR HANDLING                                   *
      *                                                                  *
      * EXTERNAL ROUTINES CALLED                                         *
      * PMC1000  - WRITE DETAILS TO *GDA                                 *
      * PMC1020  - END GROUP JOBS                                        *
      * PMC1010  - RETRIEVE DATA FROM *GDA                               *
      * PM0710   - CUSTOMER MASTER DETAILS ENQUIRY                       *
      * PM0160   - CUSTOMER DOCUMENTATION MAINTENANCE                    *
      * PM0720   - PORTFOLIO DETAILS ENQUIRY                             *
      * ZA0240   -                                                       *
      *                                                                  *
      ********************************************************************
     C*
     C**  DEFINE WORK FIELDS
     C*
     C           *LIKE     DEFN PRCNUM    WWCNUM
     C           *LIKE     DEFN PRPTFR    WWPTFR
     C           *LIKE     DEFN PRNOTS    WWSAV0
     C           *LIKE     DEFN PRNOTS    WWSAV1
     C           *LIKE     DEFN PRNOTS    WWRRN
     C           *LIKE     DEFN WWSFLA    WWRTRN
     C*
     C**  DEFINE KEYS
     C*
     C           WWKEYC    KLIST
     C                     KFLD           WWCNUM
     C                     KFLD           DDRRN
     C*
     C           WWKEYP    KLIST
     C                     KFLD           WWCNUM
     C                     KFLD           WWPTFR
     C*
     C           WWKEY3    KLIST
     C                     KFLD           WWCNUM
     C                     KFLD           WWPTFR
     C                     KFLD           DDRRN
      ********************************************************************
      /EJECT
      ********************************************************************
      *  P R O G R A M     S T A R T
      ********************************************************************
     C*
     C           WWRTRN    IFNE '9'
     C*
     C** PERFORM SETUP OF STANDARD FIELDS
     C*
     C                     EXSR #INIT
     C*
     C** FORMAT CUSTOMER NOTEPAD DETAILS
     C*
     C                     EXSR #P01
     C*
     C** FORMAT PORTFOLIO NOTEPAD DETAILS IF PGM NOT CALLED FROM SD0010
     C*
     C           WWRTRN    IFNE '3'
     C           WWRTRN    ANDNE'4'                                       092515
     C                     EXSR #P02
     C                     END
     C*
     C** PROCESS NOTEPAD SCREEN
     C*
     C                     EXSR #P03
     C*
     C           WWAID     IFEQ '03'
     C                     MOVE '1'       WWRTRN
     C                     END
     C*
     C                     CLOSESDCUSTL0                                  092515
     C                     RETRN
     C                     ELSE
     C*
     C** TERMINATE PROGRAM
     C*
     C                     SETON                         LR
     C                     END
      ********************************************************************
      *  P R O G R A M     E N D
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #INIT    - INITIAL PROCESSING                                    *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * *PSSR    - FILE ERROR HANDLING                                   *
      * PMC1010  - RETRIEVE DATA FROM *GDA                               *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWRTRN   - RETURN CODE (PARAMETER)                               *
      * WWCNUM   - CUSTOMER NUMBER (PARAMETER)                           *
      * WWPTFR   - PORTFOLIO REFERENCE (PARAMETER)                       *
      *                                                                  *
      ********************************************************************
     C           #INIT     BEGSR                           ** #INIT BSR **
     C*
     C** RECEIVE INPUT PARAMETERS
     C*
     C           *ENTRY    PLIST
     C                     PARM           WWRTRN
     C                     PARM           DDCNUM
     C                     PARM           DDPTFR
     C*
     C** PGM CALLED FROM PM0110
     C*
     C           WWRTRN    IFEQ '0'
     C                     MOVE DDCNUM    WWCNUM
     C                     MOVE DDPTFR    WWPTFR
     C                     END
     C*
     C** PGM CALLED FROM SD0010
     C*
     C           WWRTRN    IFEQ '3'
     C           WWRTRN    OREQ '4'                                       092515
     C                     MOVE DDCNUM    WWCNUM
     C                     END
     C*
     C** PREPARE LDA
     C*
     C                     MOVEL'PM0601'  DBPGM
     C**
     C**
     C**  SET UP PARAMETER TO RETRIEVE MESSAGE FROM MESSAGE FILE
     C           PLLANG    PLIST
     C                     PARM           MSMBR4  7
     C                     PARM           MSGER  10
     C                     PARM           MSGTXT 80
     C**********           MOVEL'MLGBMSGF'MSGER                           S01486
     C                     MOVEL'PMUSRMSG'MSGER                           S01486
     C*
     C** GET INSTALLATION CONTROL DATA RECORD 1
     C*
     C********** *LOVAL    SETLLSDBANKL1                                  S01486
     C**********           READ SDBANKL1                 80               S01486
     C                     CALL 'AOBANKR0'                                S01486
     C                     PARM '*DBERR ' @RTCD   7                       S01486
     C                     PARM '*FIRST ' @OPTN   7                       S01486
     C           SDBANK    PARM SDBANK    DSFDY                           S01486
     C           @RTCD     IFNE *BLANKS                                   S01486
     C*
     C********** *IN80     IFEQ '1'                                       S01486
     C                     Z-ADD1         DBASE            **DB ERROR 01**
     C                     MOVEL'SDBANKL1'DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     EXSR *PSSR
     C                     END
     C*
     C** IF FOUND CHECK SYSTEM DATE FORMAT, DDMMYY OR MMDDYY.
     C*
     C           BJDFIN    IFEQ 'M'                        MMDDYY, 98 ON
     C                     MOVE '1'       *IN98
     C                     END
     C                     MOVE BJMRDT    DDRUNA
     C*
     C** INITIALIZE REFERENCE FIELD FOR CURSOR POSITION
     C*
     C                     BITON'467'     WWPOS1
     C                     BITOF'01235'   WWPOS1
     C                     BITON'13'      WWPOS2
     C                     BITOF'024567'  WWPOS2
     C*                                                                   080080
     C** Open customer file when access changed to share *NO              080080
     C*                                                                   080080
     C                     Z-ADD32        MSGLEN 155                      080080
     C                     CALL 'QCMDEXC'                                 080080
     C                     PARM           OVR                             080080
     C                     PARM           MSGLEN                          080080
     C                     OPEN SDCUSTL0                                  080080
     C*
     C           E#INIT    ENDSR                           ** #INIT ESR **
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P01     - FORMAT CUSTOMER NOTEPAD DETAILS                       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * *PSSR    - FILE ERROR HANDLING                                   *
      * #SFLC    - FILL CUSTOMER SUBFILE                                 *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWRTRN   - RETURN CODE (PARAMETER)                               *
      * WWCNUM   - CUSTOMER NUMBER (PARAMETER)                           *
      * WWSAV0   - KEEPS NBR OF RECORDS PRESENT IN CUST. SUBFILE         *
      * WWSFLA   - INDICATOR FOR WRITE/EXFMT                             *
      *                                                                  *
      ********************************************************************
     C           #P01      BEGSR                           ** #P01  BSR **
     C*
     C********** DDCNUM    CHAINSDPLCSL2             81                   S01486
     C*
     C********** *IN81     IFEQ '1'                                       S01486
     C***********          MOVELDDCNUM    PLCUS                     S01486080080
     C***********          CALL 'AOPLCSR0'                          S01486080080
     C***********          PARM *BLANKS   RTCD    7                 S01486080080
     C***********          PARM '*KEY'    POPTN   7                 S01486080080
     C***********          PARM           PLCUS  10                 S01486080080
     C***********SDPLCS    PARM SDPLCS    DSFDY                     S01486080080
     C***********RTCD      IFEQ *BLANKS                             S01486080080
     C***********          CALL 'AOCUSTR0'                          S01486080080
     C***********          PARM *BLANKS   RTCD                      S01486080080
     C***********          PARM '*KEY'    POPTN                     S01486080080
     C***********          PARM           PLCUS                     S01486080080
     C***********          PARM           KEYST   7                 S01486080080
     C***********SDCUST    PARM SDCUST    DSSDY                     S01486080080
     C***********          END                                      S01486080080
     C           DDCNUM    CHAINSDCUSTL0             81                   080080
     C           *IN81     IFEQ '1'                                       080080
     C***********RTCD      IFNE *BLANKS                             S01486080080
     C                     Z-ADD2         DBASE            **DB ERROR 02**
     C***********          MOVEL'SDPLCSL2'DBFILE                          080080
     C                     MOVE 'SDCUSTL0'DBFILE                          080080
     C                     MOVE *BLANKS   DBKEY
     C                     EXSR *PSSR
     C                     END
     C*
     C** INITIALIZE SUBFILE
     C*
     C                     Z-ADD1         DDRRN
     C                     MOVE '1'       *IN43
     C*                                                                   R10042
     C           WWRTRN    IFEQ '3'                                       R10042
     C           WWRTRN    OREQ '4'                                       092515
     C                     WRITEPM0600C2                                  R10042
     C                     ELSE                                           R10042
     C                     WRITEPM0600C0
     C                     END                                            R10042
     C*                                                                   R10042
     C                     MOVE '0'       *IN43
     C                     MOVE '1'       *IN41
     C                     MOVELBBCSSN    DDCRNM
     C*
     C** ACCESS FILE RECORD
     C*
     C           WWCNUM    CHAINPMCSNPXX             84
     C*
     C** FILL SUBFILE
     C*
     C                     Z-ADD1         WWSAV0
     C                     EXSR #SFLC
     C                     MOVE ' '       WWSFLA
     C*
     C           E#P01     ENDSR                           ** #P01   ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P02     - FORMAT PORTFOLIO NOTEPAD DETAILS                      *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * *PSSR    - FILE ERROR HANDLING                                   *
      * #SFLP    - FILL PORTFOLIO SUBFILE                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWSAV1   - KEEPS NBR OF RECORDS PRESENT IN PORTF. SUBFILE        *
      * WWRTRN   - RETURN CODE (PARAMETER)                               *
      * WWKEYP   - KEY FOR CHAINING TO PORTFOLIO NOTEPAD FILE            *
      * WWSFLA   - INDICATOR FOR WRITE/EXFMT                             *
      *                                                                  *
      ********************************************************************
     C           #P02      BEGSR                           ** #P02  BSR **
     C*
     C           WWKEYP    CHAINPMPORXXX             82
     C*
     C           *IN82     IFEQ '1'
     C                     Z-ADD3         DBASE            **DB ERROR 03**
     C                     MOVEL'PMPORTPP'DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     EXSR *PSSR
     C                     END
     C*
     C** INITIALIZE SUBFILE
     C*
     C                     Z-ADD1         DDRRN
     C                     MOVE '1'       *IN43
     C                     WRITEPM0600C1
     C                     MOVE '0'       *IN43
     C                     MOVE '1'       *IN42
     C                     MOVELPNPTFN    DDPTFN
     C*
     C** ACCESS FILE RECORD
     C*
     C           WWKEYP    CHAINPMPTNPXX             86
     C*
     C** FILL SUBFILE
     C*
     C                     Z-ADD1         WWSAV1
     C                     EXSR #SFLP
     C*                                                                   R10042
     C           WWRTRN    IFNE '3'                                       R10042
     C           WWRTRN    ANDNE'4'                                       092515
     C                     MOVE 'C'       WWSFLA
     C                     END                                            R10042
     C*
     C           E#P02     ENDSR                           ** #P02   ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P03     - PROCESS PM NOTEPAD SCREEN                             *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * #VCMD    - VALIDATE COMMAND KEYS                                 *
      * #SFLC    - FILL CUSTOMER SUBFILE                                 *
      * #SFLP    - FILL PORTFOLIO SUBFILE                                *
      * PM0710   - CUSTOMER MASTER DETAILS ENQUIRY                       *
      * PMC1000  - WRITE DETAILS TO *GDA                                 *
      * PMC1020  - END GROUP JOBS                                        *
      * PM0160   - CUSTOMER DOCUMENTATION MAINTENANCE                    *
      * PM0720   - PORTFOLIO DETAILS ENQUIRY                             *
      * #P04     - UPDATE PM CUSTOMER NOTEPAD                            *
      * #P05     - UPDATE PM PORTFOLIO NOTEPAD                           *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWRTRN   - RETURN CODE (PARAMETER)                               *
      * WWAID    -                                                       *
      * WWCNUM   - CUSTOMER NUMBER (PARAMETER)                           *
      * WWKEYP   - KEY FOR CHAINING TO PORTFOLIO NOTEPAD FILE            *
      * WWDCRI   - DOCUMENTATION RETURN INDICATOR (PARAMETER)            *
      * WWSFLA   - INDICATOR FOR WRITE/EXFMT                             *
      *                                                                  *
      ********************************************************************
     C           #P03      BEGSR                           ** #P03   BSR**
     C*
     C** SELECT INFO MESSAGES TO BE DISPLAYED
     C*
     C           WWRTRN    IFEQ '0'
     C           WWRTRN    OREQ '3'
     C           WWRTRN    OREQ '4'                                       092515
     C                     MOVE *BLANKS   DDINF1
     C                     MOVEL'PM90039' MSMBR4
     C                     CALL 'SDRTVTXT'PLLANG
     C                     MOVELMSGTXT    DDINF2
     C                     END
     C*
     C                     WRITEPM0600F1
     C                     WRITEPM0600F3
     C*
     C** DISPLAY PORTFOLIO NOTEPAD HEADER
     C*
     C           WWRTRN    IFNE '3'
     C           WWRTRN    ANDNE'4'                                       092515
     C                     WRITEPM0600F2
     C                     END
     C*
     C                     MOVE 'RA'      WWAID
     C*
     C** D O   U N T I L   F 3   O R   F 1 2   T A K E N
     C*
     C           WWAID     DOUEQ'03'
     C           WWAID     OREQ '12'
     C*
     C** PORTFOLIO SFL IS NOT DISPLAYED
     C*
     C           WWSFLA    IFEQ ' '
     C*                                                                   R10042
     C** DISPLAY CUSTOMER SFL ON FULL SCREEN IF PORTFOLIO SFL             R10042
     C** IS NOT TO BE USED                                                R10042
     C           WWRTRN    IFEQ '3'                                       R10042
     C           WWRTRN    OREQ '4'                                       092515
     C                     EXFMTPM0600C2                                  R10042
     C                     ELSE                                           R10042
     C                     EXFMTPM0600C0
     C                     END                                            R10042
     C*                                                                   R10042
     C                     END
     C*
     C** CURSOR IS PLACED ON CUSTOMER SFL
     C*
     C           WWSFLA    IFEQ 'C'
     C*
     C           WWAID     IFEQ 'RA'
     C                     WRITEPM0600C1
     C                     END
     C*
     C                     EXFMTPM0600C0
     C                     END
     C*
     C** CURSOR IS PLACED ON PORTFOLIO SFL
     C*
     C           WWSFLA    IFEQ 'P'
     C*
     C           WWAID     IFEQ 'RA'
     C                     WRITEPM0600C0
     C                     END
     C*
     C                     EXFMTPM0600C1
     C                     END
     C*
     C** UPDATE CURSOR POSITION IN INFDS
     C*
     C                     POST           INFDS
     C                     EXSR #VCMD
     C*
     C** 'ENTER'
     C*
     C           WWAID     IFEQ 'RA'
     C                     EXSR #P04
     C                     Z-ADD1         SFLNR0
     C*
     C           DDPTFR    IFNE *BLANKS
     C                     EXSR #P05
     C                     Z-ADD1         SFLNR1
     C*
     C           WWSFLA    IFEQ 'C'
     C                     MOVE 'P'       WWSFLA  1
     C                     ELSE
     C                     MOVE 'C'       WWSFLA
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C** ROLL UP WITH CURSOR ON CUSTOMER SUBFILE
     C*
     C           WWAID     IFEQ 'UC'
     C*
     C           *IN44     IFEQ '0'
     C           WWCNUM    READEPMCSNPXX                 84
     C                     EXSR #SFLC
     C                     END
     C*
     C           WWSFLA    IFNE ' '
     C                     MOVE 'C'       WWSFLA
     C                     END
     C*
     C                     END
     C*
     C** ROLL UP WITH CURSOR ON PORTFOLIO SUBFILE
     C*
     C           WWAID     IFEQ 'UF'
     C*
     C           *IN45     IFEQ '0'
     C           WWKEYP    READEPMPTNPXX                 86
     C                     EXSR #SFLP
     C                     END
     C*
     C                     MOVE 'P'       WWSFLA
     C                     END
     C*
     C                     END
     C*
     C           E#P03     ENDSR                           ** #P03   ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P04     - UPDATE PM CUSTOMER NOTEPAD                            *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * #UPWS    - FILE UPDATED BY ANOTHER WORKSTATION                   *
      * ZTNLU1   - RETRIEVE NEXT TRANSACTION NUMBER                      *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P03     - PROCESS PM NOTEPAD SCREEN                             *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWCNUM   - CUSTOMER NUMBER (PARAMETER)                           *
      * WWDLUP   - DAY OF LAST UPDATE                                    *
      * WWMLUP   - MONTH OF LAST UPDATE                                  *
      * WWYLUP   - YEAR OF LAST UPDATE                                   *
      * WWTLUP   - TIME OF LAST UPDATE                                   *
      * WWKEYC   - KEY FOR CHAINING TO CUSTOMER NOTEPAD FILE             *
      *                                                                  *
      ********************************************************************
     C           #P04      BEGSR                           ** #P04   BSR**
     C*
     C** RUN DATE
     C*
     C                     MOVE BJMRDT    WWHLUP
     C                     Z-ADDWWDLUP    PRDLUP
     C                     MOVE WWMLUP    PRMLUP
     C                     Z-ADDWWYLUP    PRYLUP
     C*
     C** RETRIEVE NEXT TRANSACTION NUMBER
     C*
     C                     EXSR ZTNLU1
     C                     MOVELNATN      PRTNLU
     C*
     C** S C A N   S U B F I L E   A N D   U P D A T E   F I L E
     C*
     C                     Z-ADD1         DDRRN
     C                     MOVELDDCNUM    PRCNUM
     C*                                                                   R10042
     C           WWRTRN    IFEQ '3'                                       R10042
     C           WWRTRN    OREQ '4'                                       092515
     C           DDRRN     CHAINPM0600S2             87                   R10042
     C                     ELSE                                           R10042
     C           DDRRN     CHAINPM0600S0             87
     C                     END                                            R10042
     C*
     C           *IN87     DOWEQ'0'
     C********** WWKEYC    CHAINPMCSNPPP             83                   S01486
     C           WWKEYC    CHAINPMCSNPPD             83                   S01486
     C                     MOVE DDRRN     PRNOTS
     C                     TIME           PRTLUP
     C*
     C           DDNTPD    IFEQ *BLANKS
     C                     MOVE '*'       PRRECI
     C                     ELSE
     C                     MOVE 'D'       PRRECI
     C                     MOVE DDNTPD    PRNTPD
     C                     END
     C*
     C** UPDATE RECORD ON FILE IF RECORD EXISTS ON BOTH FILE AND SUBFILE
     C*
     C           *IN83     IFEQ '0'
     C                     Z-ADDWWDLUP    PRDLUP
     C                     MOVE WWMLUP    PRMLUP
     C                     Z-ADDWWYLUP    PRYLUP
     C                     UPDATPMCSNPP0
     C                     ELSE
     C*
     C** ADD RECORD ON FILE IF RECORD EXISTS ON SUBFILE ONLY
     C*
     C           PRRECI    IFEQ 'D'
     C                     WRITEPMCSNPP0
     C                     END
     C*
     C                     END
     C*
     C                     ADD  1         DDRRN
     C*                                                                   R10042
     C           WWRTRN    IFEQ '3'                                       R10042
     C           WWRTRN    OREQ '4'                                       092515
     C           DDRRN     CHAINPM0600S2             87                   R10042
     C                     ELSE                                           R10042
     C           DDRRN     CHAINPM0600S0             87
     C                     END                                            R10042
     C*                                                                   R10042
     C                     END
     C*
     C           E#P04     ENDSR                           ** #P04   ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P05     - UPDATE PM PORTFOLIO NOTEPAD                           *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P03     - PROCESS PM NOTEPAD SCREEN                             *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWTLUP   - TIME OF LAST UPDATE                                   *
      * WWKEY3   - FULL KEY FOR CHAINING TO PORTFOLIO NOTEPAD FILE       *
      *                                                                  *
      ********************************************************************
     C           #P05      BEGSR                           ** #P05   BSR**
     C*                                                                   080202
     C                     MOVELNATN      GATNLU                          080202
     C                     Z-ADDWWDLUP    GADLUP                          080202
     C                     MOVE WWMLUP    GAMLUP                          080202
     C                     Z-ADDWWYLUP    GAYLUP                          080202
     C*
     C** S C A N   S U B F I L E   A N D   U P D A T E   F I L E
     C*
     C***********          MOVELDDPTFR    PRPTFR                          080202
     C                     MOVELDDCNUM    GACNUM                          080202
     C                     MOVELDDPTFR    GAPTFR                          080202
     C                     Z-ADD1         DDRRN
     C           DDRRN     CHAINPM0600S1             87
     C*
     C           *IN87     DOWEQ'0'
     C********** WWKEY3    CHAINPMPTNPPP             85                   S01486
     C           WWKEY3    CHAINPMPTNPPD             85                   S01486
     C***********          MOVE DDRRN     PRNOTS                          080202
     C***********          TIME           PRTLUP                          080202
     C                     MOVE DDRRN     GANOTS                          080202
     C                     TIME           GATLUP                          080202
     C*
     C           DDNTPD    IFEQ *BLANKS
     C***********          MOVE '*'       PRRECI                          080202
     C                     MOVE '*'       GARECI                          080202
     C                     ELSE
     C***********          MOVE 'D'       PRRECI                          080202
     C***********          MOVE DDNTPD    PRNTPD                          080202
     C                     MOVE 'D'       GARECI                          080202
     C                     MOVE DDNTPD    GANTPD                          080202
     C                     END
     C*
     C** UPDATE RECORD ON FILE IF RECORD EXISTS ON BOTH FILE AND SUBFILE
     C*
     C           *IN85     IFEQ '0'
     C***********          Z-ADDWWDLUP    PRDLUP                          080202
     C***********          MOVE WWMLUP    PRMLUP                          080202
     C***********          Z-ADDWWYLUP    PRYLUP                          080202
     C                     Z-ADDWWDLUP    GADLUP                          080202
     C                     MOVE WWMLUP    GAMLUP                          080202
     C                     Z-ADDWWYLUP    GAYLUP                          080202
     C                     UPDATPMPTNPP0
     C                     ELSE
     C*
     C** ADD RECORD ON FILE IF RECORD EXISTS ON SUBFILE ONLY
     C*
     C***********PRRECI    IFEQ 'D'                                       080202
     C           GARECI    IFEQ 'D'                                       080202
     C                     WRITEPMPTNPP0
     C                     END
     C*
     C                     END
     C*
     C                     ADD  1         DDRRN
     C           DDRRN     CHAINPM0600S1             87
     C                     END
     C*
     C           E#P05     ENDSR                           ** #P05   ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #SFLC    - FILL CUSTOMER SUBFILE                                 *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P01     - FORMAT CUSTOMER NOTEPAD DETAILS                       *
      * #P03     - PROCESS PM NOTEPAD SCREEN                             *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWSAV0   - KEEPS NBR OF RECORDS PRESENT IN CUST. SUBFILE         *
      * WWCNUM   - CUSTOMER NUMBER (PARAMETER)                           *
      *                                                                  *
      ********************************************************************
     C           #SFLC     BEGSR                           ** #SFLC  BSR**
     C*
     C** INITIALIZE RECORD NUMBER
     C*
     C                     Z-ADDWWSAV0    DDRRN
     C                     Z-ADDWWSAV0    SFLNR0
     C                     Z-ADD0         WWRRN
     C*
     C** FILL SUBFILE
     C*
     C           *IN84     DOWEQ'0'
     C                     MOVE *BLANKS   DDNTPD
     C*
     C           DDRRN     IFEQ PRNOTS
     C           PRRECI    ANDEQ'D'
     C                     MOVELPRNTPD    DDNTPD
     C                     END
     C*
     C           WWRTRN    IFEQ '3'                                       R10042
     C           WWRTRN    OREQ '4'                                       092515
     C                     WRITEPM0600S2                                  R10042
     C                     ELSE                                           R10042
     C                     WRITEPM0600S0
     C                     END                                            R10042
     C*                                                                   R10042
     C                     ADD  1         WWRRN
     C                     ADD  1         DDRRN
     C*
     C           DDRRN     IFGT PRNOTS
     C           WWCNUM    READEPMCSNPXX                 84
     C                     END
     C*
     C                     END
     C*
     C** CHECK IF LAST PAGE OF SUBFILE IS COMPLETE
     C*
     C           WWRRN     DOULE0
     C*                                                                   R10042
     C           WWRTRN    IFEQ '3'                                       R10042
     C           WWRTRN    OREQ '4'                                       092515
     C                     SUB  18        WWRRN                           R10042
     C                     ELSE                                           R10042
     C                     SUB  7         WWRRN
     C                     END                                            R10042
     C*                                                                   R10042
     C                     END
     C*
     C** COMPLETE SUBFILE PAGE IF NECESSARY
     C*
     C           WWRRN     IFLT 0
     C                     MOVE *BLANKS   DDNTPD
     C                     Z-SUBWWRRN     WWRRN
     C*
     C                     DO   WWRRN
     C*                                                                   R10042
     C** AVOID WRITING MORE THAN 98 RECORDS                               R10042
     C           DDRRN     IFLT 99                                        R10042
     C*                                                                   R10042
     C           WWRTRN    IFEQ '3'                                       R10042
     C           WWRTRN    OREQ '4'                                       092515
     C                     WRITEPM0600S2                                  R10042
     C                     ELSE                                           R10042
     C                     WRITEPM0600S0
     C                     END                                            R10042
     C*                                                                   R10042
     C                     ADD  1         DDRRN
     C                     END                                            R10042
     C*                                                                   R10042
     C                     END
     C*
     C                     END
     C*
     C** SAVE RRN
     C*
     C                     Z-ADDDDRRN     WWSAV0
     C*
     C** ACTIVATE SFLEND
     C*
     C           WWSAV0    IFGE 98
     C                     MOVE '1'       *IN44
     C                     END
     C*
     C           E#SFLC    ENDSR                           ** #SFLC  ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #SFLP    - FILL PORTFOLIO SUBFILE                                *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P02     - FORMAT PORTFOLIO NOTEPAD DETAILS                      *
      * #P03     - PROCESS PM NOTEPAD SCREEN                             *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWKEYP   - KEY FOR CHAINING TO PORTFOLIO NOTEPAD FILE            *
      *                                                                  *
      ********************************************************************
     C           #SFLP     BEGSR                           ** #SFLP  BSR**
     C*
     C*
     C** INITIALIZE RECORD NUMBER
     C*
     C                     Z-ADDWWSAV1    DDRRN
     C                     Z-ADDWWSAV1    SFLNR1
     C                     Z-ADD0         WWRRN
     C*
     C** FILL SUBFILE
     C*
     C           *IN86     DOWEQ'0'
     C                     MOVE *BLANKS   DDNTPD
     C*
     C***********DDRRN     IFEQ PRNOTS                                    080202
     C***********PRRECI    ANDEQ'D'                                       080202
     C***********          MOVELPRNTPD    DDNTPD                          080202
     C           DDRRN     IFEQ GANOTS                                    080202
     C           GARECI    ANDEQ'D'                                       080202
     C                     MOVELGANTPD    DDNTPD                          080202
     C                     END
     C*
     C                     WRITEPM0600S1
     C                     ADD  1         DDRRN
     C                     ADD  1         WWRRN
     C*
     C***********DDRRN     IFGT PRNOTS                                    080202
     C           DDRRN     IFGT GANOTS                                    080202
     C           WWKEYP    READEPMPTNPXX                 86
     C                     END
     C*
     C                     END
     C*
     C** CHECK IF LAST PAGE OF SUBFILE IS COMPLETE
     C*
     C           WWRRN     DOULE0
     C                     SUB  7         WWRRN
     C                     END
     C*
     C** COMPLETE SUBFILE PAGE IF NECESSARY
     C*
     C           WWRRN     IFLT 0
     C                     MOVE *BLANKS   DDNTPD
     C                     Z-SUBWWRRN     WWRRN
     C*
     C                     DO   WWRRN
     C                     WRITEPM0600S1
     C                     ADD  1         DDRRN
     C                     END
     C*
     C                     END
     C*
     C** SAVE RRN
     C*
     C                     Z-ADDDDRRN     WWSAV1
     C*
     C** ACTIVATE SFLEND
     C*
     C           WWSAV1    IFGE 98
     C                     MOVE '1'       *IN45
     C                     END
     C*
     C           E#SFLP    ENDSR                           ** #SFLP  ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #VCMD    - VALIDATE COMMAND KEYS                                 *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P03     - PROCESS PM NOTEPAD SCREEN                             *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWAID    -                                                       *
      * WWCPOS   - CURSOR POSITION                                       *
      * WWCREF   - REFERENCE POSITION SEPARATING THE TWO SUBFILES        *
      *                                                                  *
      ********************************************************************
     C           #VCMD     BEGSR                           ** #VCMD  BSR **
     C*
     C** If ENTER pressed, the VLDCMDKEY indicator will be off
     C*
     C           *IN49     IFEQ '0'
     C                     MOVE 'RA'      WWAID   2
     C                     END
     C*
     C** If F1
     C*
     C           *INKA     IFEQ '1'
     C                     MOVE '01'      WWAID
     C                     MOVE '0'       *INKA
     C                     END
     C*
     C** If F2
     C*
     C           *INKB     IFEQ '1'
     C                     MOVE '02'      WWAID
     C                     MOVE '0'       *INKB
     C                     END
     C*
     C** If F3
     C*
     C           *INKC     IFEQ '1'
     C                     MOVE '03'      WWAID
     C                     MOVE '0'       *INKC
     C                     END
     C*
     C** If F5
     C*
     C           *INKE     IFEQ '1'
     C                     MOVE '05'      WWAID
     C                     MOVE '0'       *INKE
     C                     END
     C*
     C** If F12
     C*
     C           *INKL     IFEQ '1'
     C                     MOVE '12'      WWAID
     C                     MOVE '0'       *INKL
     C                     END
     C*
     C** If ROLLUP pressed
     C*
     C           *IN25     IFEQ '1'
     C*
     C           WWCPOS    IFGT WWCREF
     C           DDPTFR    ANDNE*BLANKS
     C                     MOVE 'UF'      WWAID
     C                     ELSE
     C                     MOVE 'UC'      WWAID
     C                     END
     C*
     C                     MOVE '0'       *IN25
     C                     END
     C*
     C                     ENDSR                           ** #VCMD  ESR *
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * ZTNLU1   - RETRIEVE NEXT TRANSACTION NUMBER                      *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P04     - UPDATE PM CUSTOMER NOTEPAD                            *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           ZTNLU1    BEGSR                            ** ZTNLU1 BSR**
     C*
     C           *NAMVAR   DEFN MNATN     DNATN
     C           *LOCK     IN   DNATN
     C                     MOVE FNATN     NATN    50
     C                     MOVE FNATN     ZZWK05  50
     C                     ADD  1         ZZWK05
     C                     MOVE ZZWK05    FNATN
     C                     OUT  DNATN
     C*
     C                     ENDSR                            ** ZTNLU1 ESR**
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * *PSSR    - FILE ERROR HANDLING                                   *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * PMC1000  - WRITE DETAILS TO *GDA                                 *
      * PMC1020  - END GROUP JOBS                                        *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #INIT    - INITIAL PROCESSING                                    *
      * #P01     - FORMAT CUSTOMER NOTEPAD DETAILS                       *
      * #P02     - FORMAT PORTFOLIO NOTEPAD DETAILS                      *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           *PSSR     BEGSR                            ** *PSRR BSR**
     C*
     C                     SETON                     LRU7U8
     C*
     C           *NAMVAR   DEFN *LDA      LDA
     C                     IN   LDA
     C                     DUMP
     C                     OUT  LDA
     C*
     C                     RETRN
     C*
     C                     ENDSR                            ** *PSSR ESR**
      ********************************************************************
**  OVR
OVRDBF FILE(SDCUSTL0) SHARE(*NO)                                          S01486
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
