     H        1                                                           S01486
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Port.Valn Statement:Port Summary-Indy')
     H***********                                                         S01486
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management                             *
     F*                                                               *
     F*  PM0949 - PORTFOLIO VALUATION STATEMENT:                      *
     F*           PORTFOLIO SUMMARY - COUNTR                          *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CRE026             Date 24May06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 BUG3644            Date 12Jul04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01486             Date 15Sep94               *
      *                 066271             Date 27Jan93               *
     F*                 48007              DATE 01DEC92               *
     F*                 47719              DATE 25NOV92               *
     F*                 S01313             DATE 25JUL92               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CRE026 - Consumer Banking                                    *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG3644 - Serious Error in Collateralised Lending (Recompile)*
     F*  S01486 - Private Banking upgrade to Release 10               *
     F*  066271 - Recompile program due to changes in PRTF            *
     F*  48007  - SUNDRY FIXES TO VALUATION STATEMENT                 *
     F*  47719  - CORRECT CONVERSION OF AMOUNTS BETWEEN CURRENCIES    *
     F*  S01313 - REWRITTEN FOR PORTFOLIO PERFORMANCE                 *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F**
     F**      FILE DEFINITIONS
     F**     ------------------
     F*
     F**   PM COUNTRY TOTALS. PREFIX - QH
     F***PMCNTTPPIF**E           K        DISK         KINFSR *PSSR       S01486
     FPMCNTTPDIF  E           K        DISK         KINFSR *PSSR          S01486
     F*
     F*****COUNTRY*CODES.*PREFIX*-*A4*************************************S01486
     F***SDCTRYL1IF**E           K        DISK         KINFSR *PSSR       S01486
     F***********                                                         S01486
     F**   PM PORTFOLIO TOTALS. PREFIX - QC
     F***PMPOTTPPIF**E           K        DISK         KINFSR *PSSR       S01486
     FPMPOTTPDIF  E           K        DISK         KINFSR *PSSR          S01486
     F**   Audit Control                                                  S01486
     FPM0949AUO   E                    PRINTER      KINFSR *PSSR      UC  S01486
     F                                              KINFDS SPOOLU         S01486
     F*
     F**   PORTFOLIO VALUATION STATEMENT PORTFOLIO SUMMARY
     FPM0950P1O   E             60     PRINTER      KINFDS OVRFL1     UC
     F                                              KINFSR *PSSR
     F*
     F********************************************************************
     F/EJECT
     F********************************************************************
     F**                                                                 *
     F** ID F  C  H  L    FUNCTION OF INDICATORS                         *
     F**                 ------------------------                        *
     F** 30-33 NUMBER DECIMAL PLACES: PORTFOLIO CURRENCY                 *
     F***50*-*FILE*INDICATOR*ON*PMINDTPP**********************************S01486
     F***51*-*FILE*INDICATOR*ON*SDINDSL1**********************************S01486
     F***52*-*FILE*INDICATOR*ON*PMPOTTPP**********************************S01486
     F** 50 - FILE INDICATOR ON PMINDTPD                                 *S01486
     F** 52 - FILE INDICATOR ON PMPOTTPD                                 *S01486
     F**    60 - OVERFLOW INDICATOR ON PM0950P1                          *
     F**       98 - USED BY ZDATE2                                       *
     F**          U7, U8 - DATABASE ERROR HANDLING                       *
     F********************************************************************
     F/EJECT
     E********************************************************************
     E*
     E** ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
     E*
     E**   FOR USE WITH ZDATE2
      /COPY ZSRSRC,ZDATE2Z1
     E*
     E** ARRAY USED IN CURRENCY AMOUNT CONVERSION
      /COPY ZSRSRC,ZPOWERZ1
     E*
     E**   FOR USE WITH ZSEDIT
      /COPY ZSRSRC,ZSEDITZ1
     E*
     E**   ARRAY TO FACILITATE OUTPUT OF UNDERLINES FOR TOTALS
     E                    UL1        21  1
     E                    UL2        21  1
     E********************************************************************
     E/EJECT
     I********************************************************************
     I*
     I** DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     ICPYR@$      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
     IPSDS       SDS
     I*
     I**   PROGRAM STATUS DATA STRUCTURE
     I                                      244 253 JOB
     I                                      254 263 USER
     I*
     I**   FILE INFORMATION DATA STRUCTURE
     IOVRFL1      DS
     I*
     I**   FOR OVERFLOW
     I                                    B 188 1890WWOFL1
     I                                    B 367 3680WWPTL1
     I*                                                                   S01486
     ISPOOLU      DS                                                      S01486
     I**    SPOOL FILE INFORMATION DATA STRUCTURE                         S01486
     I                                       83  92 SFILEU                S01486
     I                                    B 123 1240SFNUMU                S01486
     ILDA         DS                            256
     I*
     I**   LOCAL DATA AREA
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I** DS USED TO PASS PARAMETERS TO CALLED PROGRAMS
     I***WWPARM****E*DSPM095XPP                                           S01486
     IWWPARM    E DSPM095XPD                                              S01486
     I*                                                                   S01486
     ISDCTRY    E DSSDCTRYPD                                              S01486
     I** DUMMY RECORD FORMAT FOR COUNTRY DETAILS                          S01486
     I*                                                                   S01486
     IDSFDY     E DSDSFDY                                                 S01486
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01486
     I*                                                                   S01486
     I*
     I**   ZSEDIT DATA STRUCTURE
      /COPY ZSRSRC,ZSEDITZ2
     I*
     I**   DATA STRUCTURE TO SET LOCAL INDUSTRY CODE
     I            DS
     I                                        1   2 QHCNTR
     I                                        1   2 WWCNTR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INDEX OF SUBROUTINES                                             *
      * MAIN     - MAINLINE                                              *
      * #A       - INITIALISATION                                        *
      * #B       - PROCESS COUNTRY DETAILS                               *
      * #BA      - PRODUCE VALUATION STATEMENT                           *
      * #C       - PRODUCE PORTFOLIO TOTALS                              *
      * #P06     - FORMAT TOTALS UNDERLINE                               *
      * *PSSR    - PSSR SUBROUTINE                                       *
      * ZSEDIT   - EDIT NUMERICS FOR OUTPUT TO REPORT                    *
      * ZDATE2   - CONVERT 5 FIGURE DAY NUMBER TO DDMMMYY FORMAT         *
      * ZCCYCN   - CURRENCY CONVERSION SUBROUTINE 1                      *
      * ZCCYXR   - CURRENCY CONVERSION SUBROUTINE 2                      *
      *                                                                  *
      * EXTERNAL ROUTINES CALLED                                         *
      * SUBR23R3 - MESSAGE RETRIEVAL SUBROUTINE                          *
      *                                                                  *
      ********************************************************************
     C/EJECT
     C********************************************************************
     C           *ENTRY    PLIST
     C                     PARM           WWPARM
     C*
     C**   PLIST FOR USE WITH MESSAGE TEXT HANDLING
     C           SUBR23    PLIST
     C                     PARM           VARMSG  7
     C                     PARM           TEXT   40
     C                     PARM           LEVEL   10
     C                     PARM           RETCD   10
     C                     PARM           TXTL    40
     C*
     C*****KEY*LIST*TO*ACCESS*FILES*PMPOTTPP*&*PMCNTTPP*******************S01486
     C**   KEY LIST TO ACCESS FILES PMPOTTPD & PMCNTTPD                   S01486
     C           WWKEY1    KLIST
     C                     KFLD           WWCNUM
     C                     KFLD           WWPTFF
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C**              START MAINLINE
     C**             ----------------
     C*
     C**   INITIALISATION
     C                     EXSR #A
     C*
     C**   PROCESS INDUSTRY DETAILS
     C                     EXSR #B
     C*
     C**   PRODUCE PORTFOLIO TOTALS
     C                     EXSR #C
     C*
     C**   END PROGRAM
     C                     CLOSEPM0950P1
     C                     RETRN
     C                     MOVE '1'       *INLR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #A       - INITIALISATION                                        *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZDATE2   - CONVERT 5 FIGURE DAY NUMBER TO DDMMMYY FORMAT         *
      * SUBR23R3 - MESSAGE RETRIEVAL SUBROUTINE                          *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAINLINE                                              *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWHDWT   - HEADER WRITTEN FLAG                                   *
      * WWDEC0   - WORKFIELD DECIMAL PLACES: 0                           *
      * WWDEC1   - WORKFIELD DECIMAL PLACES: 1                           *
      * WWDEC2   - WORKFIELD DECIMAL PLACES: 2                           *
      * WWDEC3   - WORKFIELD DECIMAL PLACES: 3                           *
      * WWECD    - EVENT CONTROL DATE                                    *
      * WWCRNM   - CUSTOMER NAME                                         *
      * WWPTFR   - PORTFOLIO REFERENCE                                   *
      * WWPTFN   - PORTFOLIO NARRATIVE                                   *
      * WWPTCY   - PORTFOLIO CURRENCY                                    *
      * WWPCDP   - PORTFOLIO CURRENCY NUMBER OF DECIMAL PLACES           *
      * WWKEY1   - KEY LIST                                              *
      * WWTCVL   - TOTAL CURRENT VALUE                                   *
      * WWTINT   - TOTAL INTEREST                                        *
      * WWTVPS   - TRADE/VALUE DATE INDICATOR                            *
      * WWTOTL   - PORTFOLIO TOTAL                                       *
      * WWCVAL   - INDUSTRY CURRENT VALUE                                *
      * WWTVAL   - INDUSTRY TOTAL                                        *
      *                                                                  *
      ********************************************************************
     C           #A        BEGSR
     C*
     C**   INITIALISE DB ERROR FIELDS AND STORE PROGRAM NAME FOR
     C**   DB ERROR FIELD.
     C                     MOVE *BLANKS   WBFILE 10
     C                     MOVE *BLANKS   WBKEY  29
     C                     MOVE *BLANKS   WBASE   30
     C                     MOVEL'PM0949'  WBPGM  10
     C                     MOVE '0'       *IN98
     C                     Z-ADD3         LEVEL
     C                     Z-ADD40        TXTL
     C                     MOVE 'N'       WWHDWT  1
     C                     MOVE 'J'       ZECODE
     C                     Z-ADD0         X       20
     C                     Z-ADD0         Y       20
     C                     Z-ADD0         WWDEC0 150
     C                     Z-ADD0         WWDEC1 151
     C                     Z-ADD0         WWDEC2 152
     C                     Z-ADD0         WWDEC3 153
     C*
     C**   CONVERT EVENT CONTROL DATE
     C                     Z-ADDWWECD     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    R9DATE
     C*
     C                     MOVE WWCNUM    R9CUST
     C                     MOVELWWCRNM    R9CNAM
     C                     MOVELWWPTFR    R9PREF
     C                     MOVELWWPTFN    R9PNAR
     C                     MOVELWWPTCY    R9CCY
     C                     MOVELWWLXDT    R9LXTD
     C*
     C**   ACCESS LANGUAGE DEPENDENT MESSAGE FILE FOR TEXT LITERALS
     C           WWLANG    IFNE WWLNGS
     C                     MOVE WWLANG    WWLNGS  2
     C***********          MOVEL'PM94901' VARMSG                          S01486
     C                     MOVEL'PM95486' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R9LT01
     C***********          MOVEL'PM94902' VARMSG                          S01486
     C                     MOVEL'PM95487' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R9LT02
     C***********          MOVEL'PM94903' VARMSG                          S01486
     C                     MOVEL'PM95420' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R9LT03
     C***********          MOVEL'PM94904' VARMSG                          S01486
     C                     MOVEL'PM95489' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R9LT04
     C***********          MOVEL'PM94905' VARMSG                          S01486
     C                     MOVEL'PM95490' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R9LT05
     C***********          MOVEL'PM94906' VARMSG                          S01486
     C                     MOVEL'PM95491' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R9LT06
     C***********          MOVEL'PM94907' VARMSG                          S01486
     C                     MOVEL'PM95492' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R9LT07
     C***********          MOVEL'PM94908' VARMSG                          S01486
     C                     MOVEL'PM95493' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R9LT08
     C***********          MOVEL'PM94909' VARMSG                          S01486
     C                     MOVEL'PM95488' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R9LT09
     C***********          MOVEL'PM95900' VARMSG                          S01486
     C                     MOVEL'PM95483' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R9LT25
     C***********          MOVEL'PM95901' VARMSG                          S01486
     C                     MOVEL'PM95484' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R9LT26
     C***********          MOVEL'PM95902' VARMSG                          S01486
     C                     MOVEL'PM95401' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R9LT27
     C***********          MOVEL'PM95903' VARMSG                          S01486
     C                     MOVEL'PM95485' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R9LT28
     C                     END
     C*
     C**   SET DECIMAL PLACES AS NECESSARY
     C                     MOVEA'0000'    *IN,30
     C           30        ADD  WWPCDP    X
     C                     MOVEA'1'       *IN,X
     C*
     C*****ACCESS*PMPOTTPP************************************************S01486
     C**   ACCESS PMPOTTPD                                                S01486
     C***********WWKEY1    CHAINPMPOTTPP             52                   S01486
     C           WWKEY1    CHAINPMPOTTPD             52                   S01486
     C*
     C**   IF RECORD NOT FOUND THEN ZEROISE TOTAL CURRENT VALUE AND
     C**   TOTAL INTEREST
     C           *IN52     IFEQ '1'
     C*
     C                     Z-ADD*ZERO     WWTCVL 150
     C                     Z-ADD*ZERO     WWTINT 150
     C                     ELSE
     C*
     C           WWTVPS    IFEQ 'T'
     C*
     C           QCPTPA    SUB  QCPTNA    WWTCVL
     C                     SUB  QCPTMV    WWTCVL
     C                     ELSE
     C*
     C           WWTVPS    IFEQ 'V'
     C*
     C           QCPVPA    SUB  QCPVNA    WWTCVL
     C                     SUB  QCPVMV    WWTCVL
     C                     END
     C*
     C                     END
     C*
     C                     Z-ADDQCPTAI    WWTINT
     C*
     C                     END
     C*
     C           WWTCVL    ADD  WWTINT    WWTOTL 150
     C*
     C                     Z-ADD*ZERO     WWCVAL 150
     C                     Z-ADD*ZERO     WWTVAL 150
     C*
     C** OPEN PRINTER FILE
     C                     OPEN PM0950P1
     C*
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #B       - PROCESS COUNTRY DETAILS                               *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * #BA      - PRODUCE VALUATION STATEMENT                           *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAINLINE                                              *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWKEY1   - KEY LIST                                              *
      * WWHDWT   - HEADER WRITTEN FLAG                                   *
      * WWLINE   - LINES LEFT ON PRINTER PAGE                            *
      *                                                                  *
      ********************************************************************
     C           #B        BEGSR
     C*
     C*****SET*LOWER*LIMITS*AND*READ*PMINDTPP*FOR*THE*PASSED*CUSTOMER*****S01486
     C**   SET LOWER LIMITS AND READ PMCNTTPD FOR THE PASSED CUSTOMER     S01486
     C**   & PORTFOLIO REFERENCE
     C***********WWKEY1    SETLLPMCNTTPP                                  S01486
     C           WWKEY1    SETLLPMCNTTPD                                  S01486
     C*
     C           *IN50     DOUEQ'1'
     C*
     C           WWHDWT    IFEQ 'N'
     C*
     C                     WRITEP949HDR
     C                     MOVE 'Y'       WWHDWT
     C*
     C                     ELSE
     C*
     C                     EXSR #BA
     C*
     C           WWOFL1    SUB  WWPTL1    WWLINE  20
     C*
     C           WWLINE    IFLT 3
     C                     WRITEP949HDR
     C                     END
     C*
     C                     END
     C*
     C***********WWKEY1    READEPMCNTTPP                 50               S01486
     C           WWKEY1    READEPMCNTTPD                 50               S01486
     C*
     C                     END
     C*
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #BA      - PRODUCE VALUATION STATEMENT                           *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZSEDIT   - EDIT NUMERICS FOR OUTPUT TO REPORT                    *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #B       - PROCESS COUNTRY DETAILS                               *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWCCOD   - LOCAL COUNTRY CODE                                    *
      * WWRTCD   - RETURN CODE                                           *
      * WWTVPS   - TRADE/VALUE DATE INDICATOR                            *
      * WWCVAL   - INDUSTRY CURRENT VALUE                                *
      * WWDEC0   - WORKFIELD DECIMAL PLACES: 0                           *
      * WWDEC1   - WORKFIELD DECIMAL PLACES: 1                           *
      * WWDEC2   - WORKFIELD DECIMAL PLACES: 2                           *
      * WWDEC3   - WORKFIELD DECIMAL PLACES: 3                           *
      * WWTVAL   - INDUSTRY TOTAL                                        *
      * WWTOTL   - PORTFOLIO TOTAL                                       *
      * WWPERC   - WORKFIELD TO STORE PERCENTAGE                         *
      * WWPPER   - PORTFOLIO PERCENTAGE                                  *
      *                                                                  *
      ********************************************************************
     C           #BA       BEGSR
     C*
     C**   ACCESS SDCTRYL1
     C***********WWCNTR    CHAINSDCTRYL1             51                   S01486
     C                     CALL 'AOCTRYR0'                                S01486
     C                     PARM *BLANKS   RTCD    7                       S01486
     C                     PARM '*KEY'    OPTN    7                       S01486
     C                     PARM WWCNTR    KEYC    2                       S01486
     C           SDCTRY    PARM SDCTRY    DSFDY                           S01486
     C*
     C**   IF RECORD NOT FOUND THEN DB ERROR
     C************IN51     IFEQ '1'                                       S01486
     C           RTCD      IFNE *BLANKS                                   S01486
     C                     MOVE '001'     WBASE
     C                     MOVE *BLANKS   WBFILE           ****************
     C                     MOVEL'SDCTRYL1'WBFILE           * DBERROR  001 *
     C                     MOVELWWCNTR    WBKEY            ***************
     C                     MOVE '9'       WWRTCD
     C                     EXSR *PSSR
     C                     ELSE
     C*
     C           *IN51     IFEQ '0'
     C*
     C                     MOVELA4CNNM    R9INNM
     C*
     C           WWTVPS    IFEQ 'T'
     C*
     C           QHPTPA    SUB  QHPTNA    WWCVAL
     C                     SUB  QHPTMV    WWCVAL                          48007
     C                     ELSE
     C*
     C           WWTVPS    IFEQ 'V'
     C*
     C           QHPVPA    SUB  QHPVNA    WWCVAL
     C                     SUB  QHPVMV    WWCVAL                          48007
     C                     END
     C*
     C                     END
     C*
     C**   MOVE COUNTRY CURRENT VALUE TO FIELD WITH CORRECT NUMBER OF
     C**   DECIMAL PLACES
     C           *IN30     IFEQ '1'
     C                     MOVE WWCVAL    WWDEC0
     C                     Z-ADDWWDEC0    ZFLD15
     C                     ELSE
     C*
     C           *IN31     IFEQ '1'
     C                     MOVE WWCVAL    WWDEC1
     C                     Z-ADDWWDEC1    ZFLD15    H
     C                     ELSE
     C*
     C           *IN32     IFEQ '1'
     C                     MOVE WWCVAL    WWDEC2
     C                     Z-ADDWWDEC2    ZFLD15    H
     C                     ELSE
     C*
     C           *IN33     IFEQ '1'
     C                     MOVE WWCVAL    WWDEC3
     C                     Z-ADDWWDEC3    ZFLD15    H
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     Z-ADD0         ZDECS
     C*
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R9CVAL
     C*
     C** SET UP FIELD CONTAINING TRADE OR VALUE DATE ACCRUED
     C** INTEREST AS APPROPRIATE
     C                     Z-ADDQHPTAI    WWACIN 150
     C*
     C           WWACIN    IFNE *ZERO
     C*
     C**   MOVE ACCRUED INTEREST TO A FIELD WITH CORRECT NUMBER OF
     C**   DECIMAL PLACES
     C           *IN30     IFEQ '1'
     C                     MOVE WWACIN    WWDEC0
     C                     Z-ADDWWDEC0    ZFLD15
     C                     ELSE
     C*
     C           *IN31     IFEQ '1'
     C                     MOVE WWACIN    WWDEC1
     C                     Z-ADDWWDEC1    ZFLD15    H
     C                     ELSE
     C*
     C           *IN32     IFEQ '1'
     C                     MOVE WWACIN    WWDEC2
     C                     Z-ADDWWDEC2    ZFLD15    H
     C                     ELSE
     C*
     C           *IN33     IFEQ '1'
     C                     MOVE WWACIN    WWDEC3
     C                     Z-ADDWWDEC3    ZFLD15    H
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     Z-ADD0         ZDECS
     C*
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R9INTR
     C*
     C           WWCVAL    ADD  WWACIN    WWTVAL
     C*
     C**   MOVE COUNTRY TOTAL TO A FIELD WITH CORRECT NUMBER OF
     C**   DECIMAL PLACES
     C           *IN30     IFEQ '1'
     C                     MOVE WWTVAL    WWDEC0
     C                     Z-ADDWWDEC0    ZFLD15
     C                     ELSE
     C*
     C           *IN31     IFEQ '1'
     C                     MOVE WWTVAL    WWDEC1
     C                     Z-ADDWWDEC1    ZFLD15    H
     C                     ELSE
     C*
     C           *IN32     IFEQ '1'
     C                     MOVE WWTVAL    WWDEC2
     C                     Z-ADDWWDEC2    ZFLD15    H
     C                     ELSE
     C*
     C           *IN33     IFEQ '1'
     C                     MOVE WWTVAL    WWDEC3
     C                     Z-ADDWWDEC3    ZFLD15    H
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     Z-ADD0         ZDECS
     C*
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R9TVAL
     C*
     C                     ELSE
     C*
     C                     MOVE *BLANKS   R9INTR
     C                     MOVE R9CVAL    R9TVAL
     C                     Z-ADDWWCVAL    WWTVAL
     C*
     C                     END
     C*
     C**   CALCULATE PORTFOLIO PERCENTAGE AS NECESSARY
     C           WWTOTL    IFNE *ZERO
     C*
     C           WWTVAL    DIV  WWTOTL    WWPERC  73H
     C*
     C           WWPERC    MULT 100       WWPPER  41
     C*
     C           WWPERC    IFLE 9.999
     C           WWPERC    ANDGE-9.999
     C                     Z-ADD0         ZFLD15
     C                     MOVE WWPPER    ZFLD15
     C                     Z-ADD1         ZDECS
     C                     EXSR ZSEDIT
     C*
     C                     MOVE ZOUT21    R9PPER
     C*
     C                     ELSE
     C                     MOVEL'  N/A '  R9PPER
     C                     MOVEA*BLANKS   UL2
     C                     MOVEA'  --- '  UL2,16
     C                     Z-ADD3         WWCT
     C                     END
     C*
     C                     ELSE
     C*
     C                     MOVE *ZERO     R9PPER
     C*
     C                     END
     C*
     C**   WRITE INDUSTRY DETAIL LINE
     C           WWCVAL    IFNE 0
     C           WWACIN    ORNE 0
     C                     WRITEP949DET
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #C       - PRODUCE PORTFOLIO TOTALS                              *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZSEDIT   - EDIT NUMERICS FOR OUTPUT TO REPORT                    *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAINLINE                                              *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWDEC0   - WORKFIELD DECIMAL PLACES: 0                           *
      * WWDEC1   - WORKFIELD DECIMAL PLACES: 1                           *
      * WWDEC2   - WORKFIELD DECIMAL PLACES: 2                           *
      * WWDEC3   - WORKFIELD DECIMAL PLACES: 3                           *
      * WWTINT   - TOTAL INTEREST                                        *
      *                                                                  *
      ********************************************************************
     C           #C        BEGSR
     C*
     C**   MOVE TOTAL CURRENT VALUE TO FIELD WITH CORRECT NUMBER OF
     C**   DECIMAL PLACES
     C           *IN30     IFEQ '1'
     C                     MOVE WWTCVL    WWDEC0
     C                     Z-ADDWWDEC0    ZFLD15
     C                     ELSE
     C*
     C           *IN31     IFEQ '1'
     C                     MOVE WWTCVL    WWDEC1
     C                     Z-ADDWWDEC1    ZFLD15    H
     C                     ELSE
     C*
     C           *IN32     IFEQ '1'
     C                     MOVE WWTCVL    WWDEC2
     C                     Z-ADDWWDEC2    ZFLD15    H
     C                     ELSE
     C*
     C           *IN33     IFEQ '1'
     C                     MOVE WWTCVL    WWDEC3
     C                     Z-ADDWWDEC3    ZFLD15    H
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     Z-ADD0         ZDECS
     C*
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R9TCVL
     C*
     C** Count number of non blank positions in ZOUT21 field
     C                     EXSR #P06
     C*
     C** Checks for output of totals underline
     C                     MOVEAUL2       WWF21A 21
     C                     MOVE WWF21A    R9UN1
     C*
     C           WWTINT    IFNE *ZERO
     C*
     C**   MOVE TOTAL INTEREST TO A FIELD WITH CORRECT NUMBER OF
     C**   DECIMAL PLACES
     C           *IN30     IFEQ '1'
     C                     MOVE WWTINT    WWDEC0
     C                     Z-ADDWWDEC0    ZFLD15
     C                     ELSE
     C*
     C           *IN31     IFEQ '1'
     C                     MOVE WWTINT    WWDEC1
     C                     Z-ADDWWDEC1    ZFLD15    H
     C                     ELSE
     C*
     C           *IN32     IFEQ '1'
     C                     MOVE WWTINT    WWDEC2
     C                     Z-ADDWWDEC2    ZFLD15    H
     C                     ELSE
     C*
     C           *IN33     IFEQ '1'
     C                     MOVE WWTINT    WWDEC3
     C                     Z-ADDWWDEC3    ZFLD15    H
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     Z-ADD0         ZDECS
     C*
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R9TINT
     C*
     C** Count number of non blank positions in ZOUT21 field
     C                     EXSR #P06
     C*
     C** Checks for output of totals underline
     C                     MOVEAUL2       WWF21A 21
     C                     MOVE WWF21A    R9UN2
     C*
     C**   MOVE TOTAL PORTFOLIO CURRENCY TO FIELD WITH CORRECT NUMBER
     C**   OF DECIMAL PLACES
     C           *IN30     IFEQ '1'
     C                     MOVE WWTOTL    WWDEC0
     C                     Z-ADDWWDEC0    ZFLD15
     C                     ELSE
     C*
     C           *IN31     IFEQ '1'
     C                     MOVE WWTOTL    WWDEC1
     C                     Z-ADDWWDEC1    ZFLD15    H
     C                     ELSE
     C*
     C           *IN32     IFEQ '1'
     C                     MOVE WWTOTL    WWDEC2
     C                     Z-ADDWWDEC2    ZFLD15    H
     C                     ELSE
     C*
     C           *IN33     IFEQ '1'
     C                     MOVE WWTOTL    WWDEC3
     C                     Z-ADDWWDEC3    ZFLD15    H
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     Z-ADD0         ZDECS
     C*
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R9TOTL
     C*
     C** Count number of non blank positions in ZOUT21 field
     C                     EXSR #P06
     C*
     C** Checks for output of totals underline
     C                     MOVEAUL2       WWF21A 21
     C                     MOVE WWF21A    R9UN3
     C*
     C                     ELSE
     C*
     C****                 MOVE *BLANKS   R9UN3                           48007
     C                     MOVE R9UN1     R9UN3                           48007
     C                     MOVE *BLANKS   R9TINT
     C                     MOVE R9TCVL    R9TOTL
     C*
     C                     END
     C*
     C**   WRITE PORTFOLIO TOTALS
     C           WWTCVL    IFNE 0
     C           WWTINT    ORNE 0
     C                     WRITEP949TOT
     C                     END
     C*
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P06     - THIS SUBROUTINE FORMATS UNDERLINES FOR TOTALS         *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWCT     - DASH COUNT FOR TOTALS UNDERLINE                       *
      *                                                                  *
      ********************************************************************
     C           #P06      BEGSR
     C*
     C** Initialise arrays
     C                     MOVEAZOUT21    UL1
     C                     MOVE *BLANKS   UL2
     C                     Z-ADD1         C       30
     C*
     C** Initialise field to count number of dashes
     C                     Z-ADD0         WWCT    30
     C           C         DOWLE21
     C           UL1,C     IFNE ' '
     C                     MOVE '-'       UL2,C
     C                     ADD  1         WWCT
     C                     END
     C                     ADD  1         C
     C                     END
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * *PSSR    - PSSR SUBROUTINE                                       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           *PSSR     BEGSR
     C*
     C** UPDATE DTAARA/LDA WITH DETAILS OF ERROR
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE WBFILE    DBFILE
     C                     MOVE WBKEY     DBKEY
     C                     Z-ADDWBASE     DBASE
     C                     MOVE WBPGM     DBPGM
     C                     OUT  LDA
     C                     OPEN PM0949AU                                  S01486
     C                     WRITEPM0949F0                                  S01486
     C                     WRITEPM0949F1                                  S01486
     C*                                                                   S01486
     C** Report printer file to RCF via ZSFILE                            S01486
     C*                                                                   S01486
     C                     Z-ADDSFNUMU    ZSNUMU  60                      S01486
     C                     CALL 'ZSFILE'                                  S01486
     C                     PARM           SEQ     5                       S01486
     C                     PARM           @ENT    3                       S01486
     C                     PARM           SFILEU                          S01486
     C                     PARM           ZSNUMU                          S01486
     C                     PARM *BLANKS   ZSERR   1                       S01486
     C*                                                                   S01486
     C** Error during ZSFILE processing, return to calling program        S01486
     C*                                                                   S01486
     C           ZSERR     IFEQ 'Y'                                       S01486
     C                     SETON                     U7U8LR               S01486
     C                     END                                            S01486
     C*
     C                     DUMP
     C*
     C                     MOVE '9'       WWRTCD
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C*
     C                     RETRN
     C*
     C           #PSSR0    ENDSR
      ********************************************************************
      /EJECT
      /COPY ZSRSRC,ZDATE2Z2
      /EJECT
      /COPY ZSRSRC,ZSEDITZ3
      /EJECT
      /COPY ZSRSRC,ZCCYCN
      /EJECT                                                              S01486
      /COPY ZSRSRC,ZCCYXR                                                 S01486
      ****/EJECT                                                          47719
      ****/COPY ZSRSRC,ZCCYXR                                             47719
     C/EJECT
** CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**   POWER - POWERS OF 10 FOR CURRENCY CONVERSION
0000001
0000010
0000100
0001000
0010000
0100000
1000000
