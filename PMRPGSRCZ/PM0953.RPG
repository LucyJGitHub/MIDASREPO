     H        1                                                           S01486
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Port.Valn Statement:Port Summary-Inst')
     H***********                                                         S01486
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management                             *
     F*                                                               *
     F*  PM0953 - PORTFOLIO VALUATION STATEMENT:                      *
     F*           PORTFOLIO SUMMARY - INSTRUMENT                      *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01486             Date 16Sep94               *
      *                 066271             Date 27Jan93               *
     F*                 S01313             DATE 25JUL92               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE025 - Credit Lines (Recompile)                            *
     F*  S01486 - Private Banking upgrade to Release 10               *
     F*  066271 - Recompile program due to changes in PRTF            *
     F*  S01313 - PORTFOLIO PERFORMANCE CHANGES                       *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F**
     F**      File definitions
     F**     ------------------
     F*
     F**   Instrument type totals      - PrefiX - QE
     FPMISTTLLIF  E           K        DISK         KINFSR *PSSR
     F*
     F**   PM PORTFOLIO TOTALS         - Prefix - QC
     F***PMPOTTPPIF**E           K        DISK         KINFSR *PSSR       S01486
     FPMPOTTPDIF  E           K        DISK         KINFSR *PSSR          S01486
     F*
     F**   PM VALUATION GROUP DETAILS  - Prefix - PC
     F***PMVLGDPPIF**E           K        DISK         KINFSR *PSSR       S01486
     FPMVLGDPDIF  E           K        DISK         KINFSR *PSSR          S01486
     F***********                                                         S01486
     F*****INSTRUMENT*TYPES************-*Prefix*-*PD**********************S01486
     F***SDPLINPDIF**E           K        DISK         KINFSR *PSSR       S01486
     F**   Audit Control                                                  S01486
     FPM0953AUO   E                    PRINTER      KINFSR *PSSR      UC  S01486
     F                                              KINFDS SPOOLU         S01486
     F*
     F**   PORTFOLIO VALUATION STATEMENT PORTFOLIO SUMMARY
     F*                                - No Prefix
     FPM0950P1O   E             10     PRINTER      KINFDS ERF1DS     UC
     F                                              KINFSR *PSSR
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F**                                                              *
     F** ID F  C  H  L    FUNCTION OF INDICATORS                      *
     F**                 ------------------------                     *
     F**                                                              *
     F**    10         - OVERFLOW INDICATOR ON PM0950P1               *
     F**                                                              *
     F**       30      - USE FIELD WWFLD0 15,0 WITH ZSEDIT            *
     F**       31      - USE FIELD WWFLD1 15,1 WITH ZSEDIT            *
     F**       32      - USE FIELD WWFLD2 15,2 WITH ZSEDIT            *
     F**       33      - USE FIELD WWFLD3 15,3 WITH ZSEDIT            *
     F**                                                              *
     F*********70******-*CHAIN*FAIL*ON*PMPOTTPP***************************S01486
     F**       70      - CHAIN FAIL ON PMPOTTPD                       *   S01486
     F**       71      - CHAIN FAIL ON PMISTTLL                       *
     F**       73      - CHAIN FAIL ON PMVLGDPD                       *   S01486
     F*********72******-*CHAIN*FAIL*ON*SDPLINPD***************************S01486
     F*********73******-*CHAIN*FAIL*ON*PMVLGDPP***************************S01486
     F**       98      - Used by ZDATE2                               *
     F**                                                              *
     F*****************************************************************
     F/EJECT
     E********************************************************************
     E*
     E**   ARRAY FOR COPYRIGHT
     E*
     E                    CPY@    1   1 80
     E********************************************************************
     E/EJECT
     E*****************************************************************
      /COPY ZSRSRC,ZDATE2Z1
     E********************************************************************
     E/EJECT
     E********************************************************************
     E*
     E**   ARRAY FOR ZSEDIT SUBROUTINE
     E*
      /COPY ZSRSRC,ZSEDITZ1
     I*****************************************************************
     E/EJECT
     E********************************************************************
     E*
     E**   ARRAY TO FACILITATE OUTPUT OF UNDERLINES FOR TOTALS
     E*
     E                    UL1        21  1
     E                    UL2        21  1
     E*
     E**   ARRAY TO FACILITATE OUTPUT OF UNDERLINES FOR NARRATIVES
     E*
     E                    UL3        20  1
     E                    UL4        20  1
     I*****************************************************************
     I/EJECT
     I*****************************************************************
     I*
     I**   DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     ICPYR@$      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
     I*
     I**   PROGRAM STATUS DATA STRUCTURE
     IPSDS       SDS
     I                                      244 253 JOB
     I                                      254 263 USER
     I*
     I**   FILE INFORMATION DATA STRUCTURE
     IERF1DS      DS
     I*
     I**   FOR OVERFLOW - PRINTER FILE
     I                                    B 367 3680LINE
     I*                                                                   S01486
     ISPOOLU      DS                                                      S01486
     I**    SPOOL FILE INFORMATION DATA STRUCTURE                         S01486
     I                                       83  92 SFILEU                S01486
     I                                    B 123 1240SFNUMU                S01486
     I*
     I**   LOCAL DATA AREA
     ILDA         DS                            256
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I** DS USED TO PASS PARAMETERS TO CALLED PROGRAMS
     I***WWPARM****E*DSPM095XPP                                           S01486
     IWWPARM    E DSPM095XPD                                              S01486
     I*                                                                   S01486
     ISDPLIN    E DSSDPLINPD                                              S01486
     I** DUMMY RECORD FORMAT FOR INSTRUMENT TYPE DETAILS                  S01486
     I*                                                                   S01486
     IDSFDY     E DSDSFDY                                                 S01486
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01486
     I*
     I**   ZSEDIT DATA STRUCTURE
      /COPY ZSRSRC,ZSEDITZ2
     I*
     I*****DATA*STRUCTURE*TO*HOLD*KEY*FOR*ACCESSING*PMVLGDPP*FILE*********S01486
     I**   DATA STRUCTURE TO HOLD KEY FOR ACCESSING PMVLGDPD FILE         S01486
     IKEY2        DS
     I                                        1   3 WWPA1
     I                                        4   5 WWPA2
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INDEX OF SUBROUTINES                                             *
      * ZSEDIT   - EDITS NUMERICS FOR OUTPUT TO REPORT                   *
      * ZDATE2   - CONVERTS DATE FROM 5 NUMERIC TO DDMMMYY FORMAT        *
      * #INIT    - INITIALISATION SUBROUTINE                             *
      * #P01     - THIS SUBROUTINE PRODUCES INSTRUMENT TYPES             *
      * #P02     - THIS SUBROUTINE PRODUCES PORTFOLIO TOTALS             *
      * #P03     - THIS SUBROUTINE PRODUCES VALUATION GROUP TOTALS       *
      * #P04     - THIS SUBROUTINE PRODUCES VALUATION GROUP HEADING      *
      * #P05     - THIS SUBROUTINE PRODUCES VALUATION STATEMENT          *
      * #P06     - THIS SUBROUTINE FORMATS UNDERLINES FOR TOTALS         *
      * *PSSR    - DATABASE ERROR HANDLING SUBROUTINE                    *
      *                                                                  *
      * EXTERNAL ROUTINES CALLED                                         *
      * SUBR23R3 - ACCESSES MESSAGE FILE FOR LITERALS                    *
      *                                                                  *
     C********************************************************************
     C/EJECT
     C*****************************************************************
     C*
     C**              START MAINLINE
     C**             ----------------
     C*
     C**   INITIAL PROCESSING
     C*
     C           *ENTRY    PLIST
     C                     PARM           WWPARM
     C**
     C**  SET UP PARAMETER TO RETRIEVE MESSAGE FROM MESSAGE FILE
     C           SUBR23    PLIST
     C                     PARM           VARMSG  7
     C                     PARM           TEXT   40
     C                     PARM           LEVEL   10
     C                     PARM           RETCD   10
     C                     PARM           TXTL    40
     C*
     C*****KEY*LIST*TO*ACCESS*FILE*PMPOTTPP*&*PMISTTLL********************S01486
     C**   KEY LIST TO ACCESS FILE PMPOTTPD & PMISTTLL                    S01486
     C*
     C           KFLD1     KLIST
     C                     KFLD           WWCNUM
     C                     KFLD           WWPTFF
     C*
     C*****KEY*LIST*TO*ACCESS*FILE*PMVLGDPP*******************************S01486
     C**   KEY LIST TO ACCESS FILE PMVLGDPD                               S01486
     C*
     C           KFLD2     KLIST
     C                     KFLD           QEVALG
     C                     KFLD           WWLANG
     C*
     C**   INITIALISATION PROCESSING
     C*
     C                     EXSR #INIT
     C*
     C**   PROCESS INSTRUMENT TYPES
     C*
     C                     EXSR #P01
     C*
     C**   PRODUCE PORTFOLIO TOTALS
     C*
     C                     EXSR #P02
     C*
     C**   END PROGRAM
     C*
     C                     CLOSEPM0950P1
     C                     RETRN
     C                     MOVE '1'       *INLR
      ********************************************************************
      /EJECT
      /COPY ZSRSRC,ZDATE2Z2
      /EJECT
      /COPY ZSRSRC,ZSEDITZ3
      /EJECT
      ********************************************************************
      *                                                                  *
      * #INIT    - INITIALISATION SUBROUTINE                             *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZDATE2   - CONVERTS DATE FROM 5 NUMERIC TO DDMMMYY FORMAT        *
      * SUBR23R3 - ACCESSES MESSAGE FILE FOR LITERALS                    *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN LINE PROCESSING                                  *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWTVAL   - TOTAL CURRENT VALUE                                   *
      * WWTINT   - TOTAL INTEREST                                        *
      * WWTVPS   - TRADE/VALUE DATE                                      *
      * WWTTOT   - FINAL TOTAL                                           *
      * WWIVAL   - INSTRUMENT CURRENT VALUE                              *
      * WWITOT   - INSTRUMENT TOTAL                                      *
      * WWCVAL   - VALUATION GROUP CURRENT VALUE                         *
      * WWCINT   - VALUATION GROUP INTEREST                              *
      * WWCTOT   - VALUATION GROUP TOTAL                                 *
      * WWFLD0   - HOLDS 15,0 FIELD                                      *
      * WWFLD1   - HOLDS 15,1 FIELD                                      *
      * WWFLD2   - HOLDS 15,2 FIELD                                      *
      * WWFLD3   - HOLDS 15,3 FIELD                                      *
      * WWVALG   - VALUATION GROUP WORK FIELD                            *
      * WWF21A   - HOLDS MAX UNDERLINE DASHES FOR CURRENT VALUE          *
      * WWF21B   - HOLDS MAX UNDERLINE DASHES FOR INTEREST               *
      * WWF21C   - HOLDS MAX UNDERLINE DASHES FOR TOTAL                  *
      * WWF21D   - HOLDS MAX UNDERLINE DASHED FOR PERCENTAGE             *
      * WWFREC   - FIRST TIME THROUGH INDICATOR                          *
      * WWPCDP   - INPUT PARAMETER DECIMAL PLACES                        *
      * WWEVCD   - INPUT PARAMETER EVENT CONTROL DATE                    *
      * WWCRNM   - INPUT PARAMETER CUSTOMER NAME                         *
      * WWPTFR   - INPUT PARAMETER PORTFOLIO REFERENCE                   *
      * WWPTFN   - INPUT PARAMETER PORTFOLIO NARRATIVE                   *
      * WWPTCY   - INPUT PARAMETER PORTFOLIO CURRENCY                    *
      *                                                                  *
      ********************************************************************
     C           #INIT     BEGSR
     C*
     C** Initialise standard pgm variable to 3
     C*
     C                     Z-ADD3         LEVEL
     C*
     C** Define length of text to be returned  from message file as 40
     C*
     C                     Z-ADD40        TXTL
     C*
     C** Initialise database error fields
     C*
     C                     MOVEL'PM0953'  WBPGM  10
     C                     Z-ADD0         WBASE   30
     C                     MOVE *BLANKS   WBKEY  29
     C                     MOVE *BLANKS   WBFILE 10
     C*
     C** Access portfolio totals file
     C*
     C***********KFLD1     CHAINPMPOTTPP             70                   S01486
     C           KFLD1     CHAINPMPOTTPD             70                   S01486
     C           *IN70     IFEQ '1'
     C                     MOVE *ZEROS    WWTVAL
     C                     MOVE *ZEROS    WWTINT
     C                     ELSE
     C           *IN70     IFEQ '0'
     C*
     C** Trade/event date equals T
     C           WWTVPS    IFEQ 'T'
     C*
     C** Positive asset - negative assets - mkt value liabilities
     C           QCPTPA    SUB  QCPTNA    WWTVAL
     C                     SUB  QCPTMV    WWTVAL
     C                     ELSE
     C*
     C** Trade/event date equals V
     C           WWTVPS    IFEQ 'V'
     C*
     C** Positive asset - negative assets - mkt value liabilities
     C           QCPVPA    SUB  QCPVNA    WWTVAL
     C                     SUB  QCPVMV    WWTVAL
     C                     END
     C                     END
     C                     MOVE QCPTAI    WWTINT
     C                     END
     C                     END
     C           WWTVAL    ADD  WWTINT    WWTTOT
     C*
     C** Initialise work fields
     C                     Z-ADD0         WWIVAL
     C                     Z-ADD0         WWITOT
     C                     Z-ADD0         WWCVAL
     C                     Z-ADD0         WWCINT
     C                     Z-ADD0         WWCTOT
     C                     Z-ADD0         WWFLD0 150
     C                     Z-ADD0         WWFLD1 151
     C                     Z-ADD0         WWFLD2 152
     C                     Z-ADD0         WWFLD3 153
     C*
     C**  DEFINE WORK FIELDS
     C*
     C           *LIKE     DEFN QCPTPA    WWTVAL
     C           *LIKE     DEFN QCPTPA    WWTINT
     C           *LIKE     DEFN QCPTPA    WWTTOT
     C           *LIKE     DEFN QCPTPA    WWCVAL
     C           *LIKE     DEFN QCPTPA    WWCINT
     C           *LIKE     DEFN QCPTPA    WWCTOT
     C           *LIKE     DEFN QCPTPA    WWIVAL
     C           *LIKE     DEFN QCPTPA    WWITOT
     C           *LIKE     DEFN QEVALG    WWVALG
     C           *LIKE     DEFN ZOUT21    WWF21A
     C           *LIKE     DEFN ZOUT21    WWF21B
     C           *LIKE     DEFN ZOUT21    WWF21C
     C           *LIKE     DEFN ZOUT21    WWF21D
     C*
     C** Initialise first time through flag
     C                     MOVE '0'       WWFREC  1
     C*
     C                     MOVE *BLANKS   WWVALG
     C*
     C** Set up indicator to select correct length field for ZSEDIT processing
     C*
     C                     MOVEA'0000'    *IN,30
     C           30        ADD  WWPCDP    V       20
     C                     MOVEA'1'       *IN,V
     C** Convert a 5 figure day number into date format - last change date
     C                     Z-ADDWWECD     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    R3DATE
     C*
     C** Access language dependent message file for heading text
     C           WWLANG    IFNE WWLNGS
     C                     MOVE WWLANG    WWLNGS  2
     C***********          MOVEL'PM95301' VARMSG                          S01486
     C                     MOVEL'PM95563' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R3FLD1
     C***********          MOVEL'PM95302' VARMSG                          S01486
     C                     MOVEL'PM95487' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R3FLD2
     C***********          MOVEL'PM95303' VARMSG                          S01486
     C                     MOVEL'PM95420' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R3FLD3
     C***********          MOVEL'PM95304' VARMSG                          S01486
     C                     MOVEL'PM95489' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R3FLD4
     C***********          MOVEL'PM95305' VARMSG                          S01486
     C                     MOVEL'PM95490' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R3FLD5
     C***********          MOVEL'PM95306' VARMSG                          S01486
     C                     MOVEL'PM95491' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R3FLD6
     C***********          MOVEL'PM95307' VARMSG                          S01486
     C                     MOVEL'PM95492' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R3FLD7
     C***********          MOVEL'PM95308' VARMSG                          S01486
     C                     MOVEL'PM95564' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R3FLD8
     C***********          MOVEL'PM95309' VARMSG                          S01486
     C***********          CALL 'SUBR23R3'SUBR23                          S01486
     C***********          MOVELTEXT      R3FLD9                          S01486
     C***********          MOVEL'PM95310' VARMSG                          S01486
     C                     MOVEL'PM95488' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R3FL10
     C***********          MOVEL'PM95900' VARMSG                          S01486
     C                     MOVEL'PM95483' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R3LT25
     C***********          MOVEL'PM95901' VARMSG                          S01486
     C                     MOVEL'PM95484' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R3LT26
     C***********          MOVEL'PM95902' VARMSG                          S01486
     C                     MOVEL'PM95401' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R3LT27
     C***********          MOVEL'PM95903' VARMSG                          S01486
     C                     MOVEL'PM95485' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      R3LT28
     C                     END
     C*
     C** Initialise printer file fields which use input parameters
     C                     MOVE WWCNUM    R3CUST
     C                     MOVE WWCRNM    R3CNAM
     C                     MOVE WWPTFR    R3PREF
     C                     MOVE WWPTFN    R3PNAR
     C                     MOVE WWPTCY    R3PCCY
     C                     MOVELWWLXDT    R3LXTD
     C*
     C                     OPEN PM0950P1
     C*
     C                     ENDSR
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P01     - THIS SUBROUTINE PRODUCES INSTRUMENT TYPES             *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * #P03     - THIS SUBROUTINE PRODUCES VALUATION GROUP TOTALS       *
      * #P04     - THIS SUBROUTINE PRODUCES VALUATION GROUP HEADING      *
      * #P05     - THIS SUBROUTINE PRODUCES VALUATION STATEMENT          *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN LINE PROCESSING                                  *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWFREC   - FIRST TIME THROUGH INDICATOR                          *
      * WWVALG   - VALUATION GROUP WORK FIELD                            *
      *                                                                  *
      ********************************************************************
     C           #P01      BEGSR
     C*
     C** Access instruments file
     C           KFLD1     SETLLPMISTTLL
     C           KFLD1     READEPMISTTLL                 71
     C*
     C           *IN71     DOWEQ'0'
     C           LINE      IFGT 55
     C           WWFREC    OREQ '0'
     C                     WRITEP953HDR
     C                     END
     C*
     C** On change of valuation group
     C           QEVALG    IFNE WWVALG
     C           WWFREC    ANDEQ'1'
     C*
     C** Produce valuation group totals
     C           WWCVAL    IFNE 0
     C           WWCINT    ORNE 0
     C           WWCTOT    ORNE 0
     C                     EXSR #P03
     C                     END
     C                     END
     C           WWFREC    IFEQ '0'
     C           QEVALG    ORNE WWVALG
     C           LINE      ORGT 55
     C                     MOVE QEVALG    WWVALG
     C                     MOVE 'Y'       WWVLGH  1
     C                     END
     C                     MOVE '1'       WWFREC
     C*
     C** Produce valuation statement
     C                     EXSR #P05
     C           KFLD1     READEPMISTTLL                 71
     C                     END
     C           WWFREC    IFEQ '1'
     C           WWCVAL    IFNE 0
     C           WWCINT    ORNE 0
     C           WWCTOT    ORNE 0
     C                     EXSR #P03
     C                     END
     C                     END
     C                     ENDSR
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P02     - THIS SUBROUTINE PRODUCES PORTFOLIO TOTALS             *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZSEDIT   - EDITS NUMERICS FOR OUTPUT TO REPORT                   *
      * #P06     - THIS SUBROUTINE FORMATS UNDERLINES FOR TOTALS         *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN LINE PROCESSING                                  *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWFLD0   - HOLDS 15,0 FIELD                                      *
      * WWFLD1   - HOLDS 15,1 FIELD                                      *
      * WWFLD2   - HOLDS 15,2 FIELD                                      *
      * WWFLD3   - HOLDS 15,3 FIELD                                      *
      * WWFREC   - FIRST TIME THROUGH INDICATOR                          *
      * WWDCT1   - HOLDS MAX DASH LENGTH FOR CURRENT VALUE UNDERLINE     *
      * WWF21A   - HOLDS MAX UNDERLINE DASHES FOR CURRENT VALUE          *
      * WWTINT   - TOTAL INTEREST                                        *
      * WWDCT2   - HOLDS MAX DASH LENGTH FOR INTEREST UNDERLINE          *
      * WWF21B   - HOLDS MAX UNDERLINE DASHES FOR INTEREST               *
      * WWDCT3   - HOLDS MAX DASH LENGTH FOR TOTALS UNDERLINE            *
      * WWF21C   - HOLDS MAX UNDERLINE DASHES FOR TOTAL                  *
      *                                                                  *
      ********************************************************************
     C           #P02      BEGSR
     C           WWFREC    IFEQ '0'
     C                     WRITEP953HDR
     C                     END
     C*
     C** Processing for final portfolio totals
     C   30                MOVE WWTVAL    WWFLD0
     C   30                Z-ADDWWFLD0    ZFLD15
     C   31                MOVE WWTVAL    WWFLD1
     C   31                Z-ADDWWFLD1    ZFLD15    H
     C   32                MOVE WWTVAL    WWFLD2
     C   32                Z-ADDWWFLD2    ZFLD15    H
     C   33                MOVE WWTVAL    WWFLD3
     C   33                Z-ADDWWFLD3    ZFLD15    H
     C                     MOVE *ZEROS    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R3TVAL
     C*
     C** Count number of non blank positions in ZOUT21 field
     C                     EXSR #P06
     C*
     C** Checks for output of totals underline
     C           WWCT      IFGT WWDCT1
     C                     MOVEAUL2       WWF21A
     C                     END
     C                     MOVE WWF21A    R3UN1
     C*
     C** Total interest is not equal to zero
     C           WWTINT    IFNE *ZEROS
     C   30                MOVE WWTINT    WWFLD0
     C   30                Z-ADDWWFLD0    ZFLD15
     C   31                MOVE WWTINT    WWFLD1
     C   31                Z-ADDWWFLD1    ZFLD15    H
     C   32                MOVE WWTINT    WWFLD2
     C   32                Z-ADDWWFLD2    ZFLD15    H
     C   33                MOVE WWTINT    WWFLD3
     C   33                Z-ADDWWFLD3    ZFLD15    H
     C                     MOVE *ZEROS    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R3TINT
     C                     EXSR #P06
     C           WWCT      IFGT WWDCT2
     C                     MOVEAUL2       WWF21B
     C                     END
     C                     MOVE WWF21B    R3UN2
     C   30                MOVE WWTTOT    WWFLD0
     C   30                Z-ADDWWFLD0    ZFLD15
     C   31                MOVE WWTTOT    WWFLD1
     C   31                Z-ADDWWFLD1    ZFLD15    H
     C   32                MOVE WWTTOT    WWFLD2
     C   32                Z-ADDWWFLD2    ZFLD15    H
     C   33                MOVE WWTTOT    WWFLD3
     C   33                Z-ADDWWFLD3    ZFLD15    H
     C                     MOVE *ZEROS    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R3TTOT
     C                     EXSR #P06
     C           WWCT      IFGT WWDCT3
     C                     MOVEAUL2       WWF21C
     C                     END
     C                     MOVE WWF21C    R3UN3
     C                     ELSE
     C*
     C** Total interest is equal to zero
     C           WWTINT    IFEQ *ZEROS
     C                     MOVE *BLANKS   R3TINT
     C                     MOVE R3TVAL    R3TTOT
     C           WWDCT3    IFGT WWDCT1
     C                     MOVE WWF21C    R3UN3
     C                     ELSE
     C                     MOVE R3UN1     R3UN3
     C                     END
     C                     END
     C                     END
     C*
     C** Only output totals if any details not set to zero
     C           WWTVAL    IFNE 0
     C           WWTINT    ORNE 0
     C           WWTTOT    ORNE 0
     C                     WRITEP953TUL
     C                     WRITEP953TOT
     C                     END
     C*
     C                     ENDSR
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P03     - THIS SUBROUTINE PRODUCES VALUATION GROUP TOTALS       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZSEDIT   - EDITS NUMERICS FOR OUTPUT TO REPORT                   *
      * #P06     - THIS SUBROUTINE FORMATS UNDERLINES FOR TOTALS         *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P01     - THIS SUBROUTINE PRODUCES INSTRUMENT TYPES             *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWFLD0   - HOLDS 15,0 FIELD                                      *
      * WWFLD1   - HOLDS 15,1 FIELD                                      *
      * WWFLD2   - HOLDS 15,2 FIELD                                      *
      * WWFLD3   - HOLDS 15,3 FIELD                                      *
      * WWDCT1   - HOLDS MAX DASH LENGTH FOR CURRENT VALUE UNDERLINE     *
      * WWF21A   - HOLDS MAX UNDERLINE DASHES FOR CURRENT VALUE          *
      * WWCINT   - VALUATION GROUP INTEREST                              *
      * WWDCT2   - HOLDS MAX DASH LENGTH FOR INTEREST UNDERLINE          *
      * WWF21B   - HOLDS MAX UNDERLINE DASHES FOR INTEREST               *
      * WWDCT3   - HOLDS MAX DASH LENGTH FOR TOTALS UNDERLINE            *
      * WWF21C   - HOLDS MAX UNDERLINE DASHES FOR TOTAL                  *
      * WWCPER   - VALUATION GROUP PERCENTAGE                            *
      * WWDCT4   - HOLDS MAX DASH LENGTH FOR PERCENTAGE UNDERLINE        *
      * WWF21D   - HOLDS MAX UNDERLINE DASHED FOR PERCENTAGE             *
      * WWCVAL   - VALUATION GROUP CURRENT VALUE                         *
      * WWCTOT   - VALUATION GROUP TOTAL                                 *
      *                                                                  *
      ********************************************************************
     C           #P03      BEGSR
     C*
     C   30                MOVE WWCVAL    WWFLD0
     C   30                Z-ADDWWFLD0    ZFLD15
     C   31                MOVE WWCVAL    WWFLD1
     C   31                Z-ADDWWFLD1    ZFLD15    H
     C   32                MOVE WWCVAL    WWFLD2
     C   32                Z-ADDWWFLD2    ZFLD15    H
     C   33                MOVE WWCVAL    WWFLD3
     C   33                Z-ADDWWFLD3    ZFLD15    H
     C                     MOVE *ZEROS    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R3CVAL
     C                     EXSR #P06
     C           WWCT      IFGT WWDCT1
     C                     MOVEAUL2       WWF21A
     C                     Z-ADDWWCT      WWDCT1
     C                     END
     C                     MOVE WWF21A    R3UN5
     C           WWCINT    IFNE *ZEROS
     C   30                MOVE WWCINT    WWFLD0
     C   30                Z-ADDWWFLD0    ZFLD15
     C   31                MOVE WWCINT    WWFLD1
     C   31                Z-ADDWWFLD1    ZFLD15    H
     C   32                MOVE WWCINT    WWFLD2
     C   32                Z-ADDWWFLD2    ZFLD15    H
     C   33                MOVE WWCINT    WWFLD3
     C   33                Z-ADDWWFLD3    ZFLD15    H
     C                     MOVE *ZEROS    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R3CINT
     C                     EXSR #P06
     C           WWCT      IFGT WWDCT2
     C                     Z-ADDWWCT      WWDCT2
     C                     MOVEAUL2       WWF21B
     C                     END
     C                     MOVE WWF21B    R3UN6
     C   30                MOVE WWCTOT    WWFLD0
     C   30                Z-ADDWWFLD0    ZFLD15
     C   31                MOVE WWCTOT    WWFLD1
     C   31                Z-ADDWWFLD1    ZFLD15    H
     C   32                MOVE WWCTOT    WWFLD2
     C   32                Z-ADDWWFLD2    ZFLD15    H
     C   33                MOVE WWCTOT    WWFLD3
     C   33                Z-ADDWWFLD3    ZFLD15    H
     C                     MOVE *ZEROS    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R3CTOT
     C                     EXSR #P06
     C           WWCT      IFGT WWDCT3
     C                     MOVEAUL2       WWF21C
     C                     Z-ADDWWCT      WWDCT3
     C                     END
     C                     MOVE WWF21C    R3UN7
     C                     ELSE
     C           WWCINT    IFEQ *ZEROS
     C                     MOVE *BLANKS   R3CINT
     C                     MOVE R3CVAL    R3CTOT
     C                     MOVE *BLANKS   R3UN6
     C           WWDCT3    IFGT WWDCT1
     C                     MOVE WWF21C    R3UN7
     C                     ELSE
     C                     MOVE R3UN5     R3UN7
     C                     END
     C                     END
     C                     END
     C           WWPER1    IFLE 9.999
     C           WWPER1    ANDGE-9.999
     C                     MOVE *ZEROS    ZFLD15
     C           WWPER1    MULT 100       WWCPER  41
     C                     MOVE WWCPER    ZFLD15
     C                     MOVE 1         ZDECS
     C                     MOVE ' '       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R3CPER
     C                     EXSR #P06
     C*
     C                     ELSE
     C                     MOVEL'  N/A '  R3CPER
     C                     MOVEA*BLANKS   UL2
     C                     MOVEA'  --- '  UL2,16
     C                     Z-ADD3         WWCT
     C                     END
     C           WWCT      IFGT WWDCT4
     C                     MOVEAUL2       WWF21D
     C                     Z-ADDWWCT      WWDCT4
     C                     END
     C                     MOVE WWF21D    R3UN8
     C                     WRITEP953VUL
     C                     WRITEP953VDL
     C                     MOVE *ZEROS    WWPER1
     C                     MOVE *ZEROS    WWCVAL
     C                     MOVE *ZEROS    WWCINT
     C                     MOVE *ZEROS    WWCTOT
     C                     MOVE *ZEROS    WWCPER
     C*
     C                     ENDSR
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P04     - THIS SUBROUTINE PRODUCES VALUATION GROUP HEADING      *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * *PSSR    - DATABASE ERROR HANDLING SUBROUTINE                    *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P01     - THIS SUBROUTINE PRODUCES INSTRUMENT TYPES             *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWDCT1   - HOLDS MAX DASH LENGTH FOR CURRENT VALUE UNDERLINE     *
      * WWDCT2   - HOLDS MAX DASH LENGTH FOR INTEREST UNDERLINE          *
      * WWDCT3   - HOLDS MAX DASH LENGTH FOR TOTALS UNDERLINE            *
      * WWDCT4   - HOLDS MAX DASH LENGTH FOR PERCENTAGE UNDERLINE        *
      **WWPA1****-*VALUATION*GROUP*-*1ST*KEY*FIELD*FOR*PMVLGDPP*FILE******S01486
      **WWPA2****-*LANG.INDICATOR**-*2ND*KEY*FIELD*FOR*PMVLGDPP*FILE******S01486
      * WWPA1    - VALUATION GROUP - 1ST KEY FIELD FOR PMVLGDPD FILE     *S01486
      * WWPA2    - LANG.INDICATOR  - 2ND KEY FIELD FOR PMVLGDPD FILE     *S01486
      *                                                                  *
      ********************************************************************
     C           #P04      BEGSR
     C*
     C** Initialise counter field for the number of non blank
     C** positions in each column of the report
     C*
     C                     MOVE *ZEROS    WWDCT1  20
     C                     MOVE *ZEROS    WWDCT2  20
     C                     MOVE *ZEROS    WWDCT3  20
     C                     MOVE *ZEROS    WWDCT4  20
     C                     MOVE QEVALG    WWPA1
     C                     MOVE WWLANG    WWPA2
     C*
     C** Access valuation group details file
     C***********KFLD2     CHAINPMVLGDPP             73                   S01486
     C           KFLD2     CHAINPMVLGDPD             73                   S01486
     C                     MOVE PCVLGN    R3VNAR
     C                     MOVEAR3VNAR    UL3
     C                     MOVE *BLANKS   UL4
     C                     Z-ADD20        C
     C*
     C** SEARCH FOR LAST CHARACTER OF NARRATIVE STRING
     C           C         DOWNE0
     C           UL3,C     ANDEQ' '
     C                     SUB  1         C
     C                     END
     C                     Z-ADD1         D       20
     C*
     C** INSERT DASHES FOR UNDERLINE
     C           D         DOWLEC
     C                     MOVE '-'       UL4,D
     C                     ADD  1         D
     C                     END
     C                     MOVEAUL4       R3VUL
     C                     WRITEP953VHD
     C                     ENDSR
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P05     - THIS SUBROUTINE PRODUCES VALUATION STATEMENT          *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * *PSSR    - DATABASE ERROR HANDLING SUBROUTINE                    *
      * ZSEDIT   - EDITS NUMERICS FOR OUTPUT TO REPORT                   *
      * #P06     - THIS SUBROUTINE FORMATS UNDERLINES FOR TOTALS         *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P01     - THIS SUBROUTINE PRODUCES INSTRUMENT TYPES             *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWTVPS   - TRADE/VALUE DATE                                      *
      * WWIVAL   - INSTRUMENT CURRENT VALUE                              *
      * WWFLD0   - HOLDS 15,0 FIELD                                      *
      * WWFLD1   - HOLDS 15,1 FIELD                                      *
      * WWFLD2   - HOLDS 15,2 FIELD                                      *
      * WWFLD3   - HOLDS 15,3 FIELD                                      *
      * WWCVAL   - VALUATION GROUP CURRENT VALUE                         *
      * WWDCT1   - HOLDS MAX DASH LENGTH FOR CURRENT VALUE UNDERLINE     *
      * WWF21A   - HOLDS MAX UNDERLINE DASHES FOR CURRENT VALUE          *
      * WWCINT   - VALUATION GROUP INTEREST                              *
      * WWITOT   - INSTRUMENT TOTAL                                      *
      * WWDCT2   - HOLDS MAX DASH LENGTH FOR INTEREST UNDERLINE          *
      * WWF21B   - HOLDS MAX UNDERLINE DASHES FOR INTEREST               *
      * WWDCT3   - HOLDS MAX DASH LENGTH FOR TOTALS UNDERLINE            *
      * WWF21C   - HOLDS MAX UNDERLINE DASHES FOR TOTAL                  *
      * WWCTOT   - VALUATION GROUP TOTAL                                 *
      * WWTTOT   - FINAL TOTAL                                           *
      * WWPERC   - 4,3 FIELD TO HOLD INSTRUMENT PERCENTAGE               *
      * WWIPER   - INSTRUMENT PERCENTAGE                                 *
      * WWDCT4   - HOLDS MAX DASH LENGTH FOR PERCENTAGE UNDERLINE        *
      * WWF21D   - HOLDS MAX UNDERLINE DASHED FOR PERCENTAGE             *
      * WWCPER   - VALUATION GROUP PERCENTAGE                            *
      *                                                                  *
      ********************************************************************
     C           #P05      BEGSR
     C*
     C** Access instruments types file
     C***********QEINNR    CHAINSDPLINPD             72                   S01486
     C                     CALL 'AOPLINR0'                                S01486
     C                     PARM *BLANKS   RTCD    7                       S01486
     C                     PARM '*KEY'    OPTN    7                       S01486
     C                     PARM QEINNR    KEYIN   3                       S01486
     C           SDPLIN    PARM SDPLIN    DSFDY                           S01486
     C************IN72     IFEQ '1'                                       S01486
     C           RTCD      IFNE *BLANKS                                   S01486
     C           PDRECI    ORNE 'D'
     C                     Z-ADD1         WBASE            ***************
     C                     MOVEL'SDPLINPD'WBFILE           * DBERROR 001 *
     C                     MOVE *BLANKS   WBKEY            ***************
     C                     MOVELQEINNR    WBKEY
     C                     EXSR *PSSR
     C                     END
     C                     MOVE PDISTN    R3INAR
     C*
     C** Determine instrument value and interest amount
     C           WWTVPS    IFEQ 'T'
     C           QEPTPA    SUB  QEPTNA    WWIVAL
     C                     SUB  QEPTMV    WWIVAL
     C                     Z-ADDQEPTAI    WWACIN 150
     C                     ELSE
     C           WWTVPS    IFEQ 'V'
     C           QEPVPA    SUB  QEPVNA    WWIVAL
     C                     SUB  QEPVMV    WWIVAL
     C                     Z-ADDQEPTAI    WWACIN 150
     C                     END
     C                     END
     C*
     C   30                MOVE WWIVAL    WWFLD0
     C   30                Z-ADDWWFLD0    ZFLD15
     C   31                MOVE WWIVAL    WWFLD1
     C   31                Z-ADDWWFLD1    ZFLD15    H
     C   32                MOVE WWIVAL    WWFLD2
     C   32                Z-ADDWWFLD2    ZFLD15    H
     C   33                MOVE WWIVAL    WWFLD3
     C   33                Z-ADDWWFLD3    ZFLD15    H
     C                     MOVE *ZEROS    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R3IVAL
     C                     ADD  WWIVAL    WWCVAL
     C                     EXSR #P06
     C           WWCT      IFGT WWDCT1
     C                     Z-ADDWWCT      WWDCT1
     C                     MOVEAUL2       WWF21A
     C                     END
     C*
     C** Accrued interest is not equal to zero
     C           WWACIN    IFNE *ZEROS
     C*
     C   30                MOVE WWACIN    WWFLD0
     C   30                Z-ADDWWFLD0    ZFLD15
     C   31                MOVE WWACIN    WWFLD1
     C   31                Z-ADDWWFLD1    ZFLD15    H
     C   32                MOVE WWACIN    WWFLD2
     C   32                Z-ADDWWFLD2    ZFLD15    H
     C   33                MOVE WWACIN    WWFLD3
     C   33                Z-ADDWWFLD3    ZFLD15    H
     C                     MOVE *ZEROS    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R3IINT
     C                     ADD  WWACIN    WWCINT
     C           WWIVAL    ADD  WWACIN    WWITOT
     C                     EXSR #P06
     C           WWCT      IFGT WWDCT2
     C                     MOVEAUL2       WWF21B
     C                     Z-ADDWWCT      WWDCT2
     C                     END
     C*
     C   30                MOVE WWITOT    WWFLD0
     C   30                Z-ADDWWFLD0    ZFLD15
     C   31                MOVE WWITOT    WWFLD1
     C   31                Z-ADDWWFLD1    ZFLD15    H
     C   32                MOVE WWITOT    WWFLD2
     C   32                Z-ADDWWFLD2    ZFLD15    H
     C   33                MOVE WWITOT    WWFLD3
     C   33                Z-ADDWWFLD3    ZFLD15    H
     C                     MOVE *ZEROS    ZDECS
     C                     MOVE 'J'       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R3ITOT
     C                     EXSR #P06
     C           WWCT      IFGT WWDCT3
     C                     Z-ADDWWCT      WWDCT3
     C                     MOVEAUL2       WWF21C
     C                     END
     C                     ELSE
     C*
     C** Accrued interest is equal to zero
     C           QEPTAI    IFEQ 0
     C                     MOVE *BLANKS   R3IINT
     C                     MOVE R3IVAL    R3ITOT
     C                     MOVE WWIVAL    WWITOT
     C                     END
     C                     END
     C                     ADD  WWITOT    WWCTOT
     C*
     C** Division by zero not allowed
     C           WWTTOT    IFNE 0
     C           WWITOT    DIV  WWTTOT    WWPERC  73H
     C           WWPERC    MULT 100       WWIPER  41
     C                     ELSE
     C                     Z-ADD0         WWIPER
     C                     END
     C                     MOVE 1         ZDECS
     C                     MOVE *ZEROS    ZFLD15
     C           WWPERC    IFLE 9.999
     C           WWPERC    ANDGE-9.999
     C                     MOVE WWIPER    ZFLD15
     C                     MOVE ' '       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R3IPER
     C                     EXSR #P06
     C*
     C                     ELSE
     C                     MOVEL'  N/A '  R3IPER
     C                     MOVEA*BLANKS   UL2
     C                     MOVEA'  --- '  UL2,16
     C                     Z-ADD3         WWCT
     C                     END
     C*
     C           WWCT      IFGT WWDCT4
     C                     Z-ADDWWCT      WWDCT4
     C                     MOVEAUL2       WWF21D
     C                     END
     C                     ADD  WWPERC    WWPER1  73
     C*
     C** Only output line of report if either maket value or accrued
     C** interest non zero
     C           WWACIN    IFNE 0
     C           WWIVAL    ORNE 0
     C*
     C** If new valuation group title required perform routine #P04
     C           WWVLGH    IFEQ 'Y'
     C                     EXSR #P04
     C                     MOVE 'N'       WWVLGH
     C                     END
     C*
     C                     WRITEP953IDL
     C                     END
     C*
     C                     ENDSR
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P06     - THIS SUBROUTINE FORMATS UNDERLINES FOR TOTALS         *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P02     - THIS SUBROUTINE PRODUCES PORTFOLIO TOTALS             *
      * #P03     - THIS SUBROUTINE PRODUCES VALUATION GROUP TOTALS       *
      * #P05     - THIS SUBROUTINE PRODUCES VALUATION STATEMENT          *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWCT     - DASH COUNT FOR TOTALS UNDERLINE                       *
      *                                                                  *
      ********************************************************************
     C           #P06      BEGSR
     C*
     C** Initialise arrays
     C                     MOVEAZOUT21    UL1
     C                     MOVE *BLANKS   UL2
     C                     Z-ADD1         C       20
     C*
     C** Initialise field to count number of dashes
     C                     Z-ADD0         WWCT    20
     C           C         DOWLE21
     C           UL1,C     IFNE ' '
     C                     MOVE '-'       UL2,C
     C                     ADD  1         WWCT
     C                     END
     C                     ADD  1         C
     C                     END
     C                     ENDSR
      *****************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * *PSSR    - DATABASE ERROR HANDLING SUBROUTINE                    *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P04     - THIS SUBROUTINE PRODUCES VALUATION GROUP HEADING      *
      * #P05     - THIS SUBROUTINE PRODUCES VALUATION STATEMENT          *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWRTCD   - INPUT PARAMETER RETURN CODE                           *
      *                                                                  *
      ********************************************************************
     C           *PSSR     BEGSR
     C*
     C** UPDATE DTAARA/LDA WITH DETAILS OF ERROR
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE WBFILE    DBFILE
     C                     MOVE WBKEY     DBKEY
     C                     Z-ADDWBASE     DBASE
     C                     MOVE WBPGM     DBPGM
     C                     OUT  LDA
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     OPEN PM0953AU                                  S01486
     C                     WRITEPM0953F0                                  S01486
     C                     WRITEPM0953F1                                  S01486
     C*                                                                   S01486
     C** Report printer file to RCF via ZSFILE                            S01486
     C*                                                                   S01486
     C                     Z-ADDSFNUMU    ZSNUMU  60                      S01486
     C                     CALL 'ZSFILE'                                  S01486
     C                     PARM           SEQ     5                       S01486
     C                     PARM           @ENT    3                       S01486
     C                     PARM           SFILEU                          S01486
     C                     PARM           ZSNUMU                          S01486
     C                     PARM *BLANKS   ZSERR   1                       S01486
     C*                                                                   S01486
     C** Error during ZSFILE processing, return to calling program        S01486
     C*                                                                   S01486
     C           ZSERR     IFEQ 'Y'                                       S01486
     C                     SETON                     U7U8LR               S01486
     C                     END                                            S01486
     C*
     C** Set return code for a database error
     C                     MOVE '9'       WWRTCD
     C                     DUMP
     C                     RETRN
     C*
     C                     ENDSR
      *****************************************************************
      /EJECT
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
