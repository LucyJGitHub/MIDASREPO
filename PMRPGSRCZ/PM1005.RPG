     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Drop deleted customer details')                  *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management                                 *
     F*                                                               *
     F*  PM1005 - DROP DELETED CUSTOMERS DETAILS PAST RETENTION DATE  *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. AR1075401          Date 01Jun20               *     
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CLE148             Date 23Jul12               *
      *                 CER059             Date 19Jul10               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CAC003             Date 25Feb97               *
     F*                 CFF004             Date 02OCT96               *
     F*                 S01486             DATE 03JUL94               *
     F*                 038430             DATE 03AUG93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  AR1075401 - Increase QFOAMT length in PMHOLDPD. Recompiled.  *
      *              (Child: AR1075402)                               *     
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus (Recompile)                              *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  CAC003 - Profit Centre Accounting Development Phase 3.       *
     F*         - Recompiled due to changes in PF/PMPORTPD.           *
     F*  CFF004 - Increase in size of Price Fields,(Recompiled Only)  *
     F*  S01486 - Private Banking upgrade to Release 10               *
     F*  038430 - Created for 038430 enhancements to customer         *
     F*           insertion / deletion processing                     *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F***SDPCSDPDUF**E           K        DISK                            S01486
     FPMPCSDPDUF  E           K        DISK                               S01486
     F                                              KINFSR *PSSR
     F***PMPORDPPUF**E           K        DISK                            S01486
     FPMPORDPDUF  E           K        DISK                               S01486
     F                                              KINFSR *PSSR
     F***PMCLOCPPUF**E           K        DISK                            S01486
     FPMCLOCPDUF  E           K        DISK                               S01486
     F                                              KINFSR *PSSR
     F*****PM*ICD*-*PREFIX*P9*********************************************S01486
     F***SDPORTPDIF**E           K        DISK         KINFSR *PSSR       S01486
     F***BANK*ICD*-*PREFIX*BJ*********************************************S01486
     F***SDBANKL0IF**E           K        DISK         KINFSR *PSSR       S01486
     F*
     F**   Audit Control
     FPM1005AUO   E                    PRINTER      KINFSR *PSSR
     F*************************************************************
     E** TABLES AND ARRAYS
     E*********************************************************************
     E***ARRAY*FOR*COPYRIGHT******************************************    S01486
     E** ARRAY FOR COPYRIGHT                                              S01486
      *
     E                    CPY@    1   1 80
     ISDBANK    E DSSDBANKPD                                              S01486
     I** EXTERNAL DS FOR BANK DETAILS                                     S01486
     ISDPORT    E DSSDPORTPD                                              S01486
      ** PM MODULE ACCESSED VIA ACCESS PROGRAM                            S01486
     IDSFDY     E DSDSFDY                                                 S01486
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01486
     I*                                                                   S01486
     IDSSDY     E DSDSSDY                                                 S01486
     I* SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE                S01486
     I**   Local data area for database error details
     I***LDA********UDS                            256                    S01486
     I************                          132 183 DBLOT                 S01486
     I************                          132 141 DBFILE                S01486
     ILDA         DS                            256                       S01486
     I                                      134 183 DBLOT                 S01486
     I                                      134 141 DBFILE                S01486
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I**
     I*  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     C*************************************************************
     C** P R O G R A M    S T A R T
     C*************************************************************
     C** INTIIALIZE DATABASE ERROR FIELDS
     C           *NAMVAR   DEFN           LDA                             S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     MOVE *BLANKS   DBLOT
     C                     MOVEL'PM1005'  DBPGM
     C                     OUT  LDA                                       S01486
     C*
     C** READ BANK DETAILS
     C                     CALL 'AOBANKR0'                                S01486
     C                     PARM '*MSG   ' @RTCD   7                       S01486
     C                     PARM '*FIRST ' @OPTN   7                       S01486
     C           SDBANK    PARM SDBANK    DSFDY                           S01486
     C***********          READ SDBANKL0                 70               S01486
     C*
     C************IN70     IFEQ '1'                                       S01486
     C           @RTCD     IFNE *BLANKS                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     Z-ADD01        DBASE            ***************S01486
     C                     MOVEL'SDBANKL0'DBFILE           * DB ERROR 01 *S01486
     C                     MOVEL@OPTN     DBKEY                           S01486
     C                     OUT  LDA                                       S01486
     C                     EXSR *PSSR                      ***************
     C                     END
     C*
     C** READ PORTFOLIO ICD
     C***********          READ SDPORTPD                 70               S01486
     C*
     C                     CALL 'AOPORTR0'                                S01486
     C                     PARM '*MSG   ' @RTCD   7                       S01486
     C                     PARM '*FIRST ' @OPTN   7                       S01486
     C           SDPORT    PARM SDPORT    DSFDY                           S01486
     C************IN70     IFEQ '1'                                       S01486
     C***********          Z-ADD01        DBASE            ***************S01486
     C           @RTCD     IFNE *BLANKS                                   S01486
     C                     Z-ADD2         DBASE            ***************S01486
     C                     MOVEL'SDPORTPD'DBFILE           * DB ERROR 01 *
     C                     MOVEL@OPTN     DBKEY                           S01486
     C                     EXSR *PSSR                      ***************
     C                     END
     C*
     C** CALCULATE RETENTION DATE
     C**********           MOVE P9RDCI    WWRDCI  20                      S01486
     C                     MOVE FCRDCI    WWRDCI  20                      S01486
     C           WWRDCI    MULT 31        WWNDYS  50
     C           BJRDNB    SUB  WWNDYS    WWRTND  50
     C*
     C** PROCESSING LOOP
     C***********          READ SDPCSDPD                 70               S01486
     C                     READ PMPCSDPD                 70               S01486
     C           *IN70     DOWEQ'0'                        B1
     C*
     C** PERFORM PROCESSING FOR RECORDS SAVED BEFORE RETENTION DATE
     C           QBLCD     IFLE WWRTND                     B2
     C                     ADD  1         RRTOTV
     C*
     C** PERFORM PROCESSING TO DROP CUSTOMER RECORDS
     C                     EXSR #DROP
     C*
     C** DELETE CUSTOMER RECORD
     C***********          DELETSDPCSDP0                                  S01486
     C                     DELETPMPCSDP0                                  S01486
     C                     END                             E2
     C*
     C***********          READ SDPCSDPD                 70               S01486
     C                     READ PMPCSDPD                 70               S01486
     C                     END                             E1
     C*
     C** OUTPUT AUDIT REPORT
     C                     WRITEHEADER
     C                     WRITEDETAILS
     C*
     C                     MOVE '1'       *INLR
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*                                                                  *
     C* #DROP - DROP RECORDS SAVED BEFORE RETENTION DATE                 *
     C*                                                                  *
     C********************************************************************
     C           #DROP     BEGSR
     C*
     C**********           MOVE QBCUST    WWCUST  60                                          CSD027
     C                     MOVE QBCUST    WWCUST  6                                           CSD027
     C*
     C***DROP*ALL*RECORDS*FROM*PMPORDPP*FOR*CUSTOMER**********************S01486
     C***********WWCUST    SETLLPMPORDPP                                  S01486
     C***********WWCUST    READEPMPORDPP                 71               S01486
     C** DROP ALL RECORDS FROM PMPORDPD FOR CUSTOMER                      S01486
     C           WWCUST    SETLLPMPORDPD                                  S01486
     C           WWCUST    READEPMPORDPD                 71               S01486
     C           *IN71     DOWEQ'0'                        B1
     C                     DELETPMPORTP0
     C***********WWCUST    READEPMPORDPP                 71               S01486
     C           WWCUST    READEPMPORDPD                 71               S01486
     C                     END                             E1
     C*
     C***DROP*ALL*RECORDS*FROM*PMCLOCPP*FOR*CUSTOMER**********************S01486
     C***********WWCUST    SETLLPMCLOCPP                                  S01486
     C***********WWCUST    READEPMCLOCPP                 71               S01486
     C** DROP ALL RECORDS FROM PMCLOCPD FOR CUSTOMER                      S01486
     C           WWCUST    SETLLPMCLOCPD                                  S01486
     C           WWCUST    READEPMCLOCPD                 71               S01486
     C           *IN71     DOWEQ'0'                        B1
     C                     DELETPMHOLDP0
     C***********WWCUST    READEPMCLOCPP                 71               S01486
     C           WWCUST    READEPMCLOCPD                 71               S01486
     C                     END                             E1
     C*
     C                     ENDSR
     C*************************************************************
     C** P R O G R A M    E N D
     C*************************************************************
     C           *PSSR     BEGSR
     C*
     C           @RUN      IFEQ *BLANK                                    S01486
     C                     MOVE 'Y'       @RUN    1                       S01486
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     WRITEHEADER                                    S01486
     C                     WRITEERRORAU                                   S01486
     C                     END                                            S01486
     C                     DUMP
     C**
     C                     MOVE '1'       *INLR
     C**
     C                     RETRN
     C**
     C                     ENDSR
**  CPY@
(c) Finastra International Limited 2001
