     H        1                                                           S01486
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Update event summary details')
     H***********                                                         S01486
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management                             *
     F*                                                               *
     F*  PM1460 - UPDATE EVENT SUMMARY DETAILS                        *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 21Feb00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01486             Date 19Aug94               *
     F*                 47719              DATE 24NOV92               *
     F*                 B07827             DATE 23MAY91               *
     F*                 R01163             DATE 02JAN91               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
     F*  CPB001 -  'Private Banking' Module                           *
     F*  S01486 - Private Banking upgrade to Release 10               *
     F*  47719  - CORRECT CONVERSION OF AMOUNTS BETWEEN CURRENCIES    *
     F*  B07827 - PROGRAM CHECKING INCORRECTLY FOR CHANGE OF PORTFOLIO*
     F*  R01163 - MONTHLY CASHFLOWS TO BE TOTALLED FOR NEXT 13 MONTHS *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F***SDBANKL1IF**E           K        DISK         KINFSR *PSSR       S01486
     F*******PREFIX*BJ****************************************************S01486
     F***SDPLCSPDIF**E           K        DISK         KINFSR *PSSR S01235S01486
     F*******PREFIX*QB**********************************************S01235S01486
     F***PMCFDYPPO***E           K        DISK         KINFSR *PSSR A     S01486
     FPMCFDYPDO   E           K        DISK         KINFSR *PSSR A        S01486
     F*      PREFIX QK
     F***PMCFMYPPO***E           K        DISK         KINFSR *PSSR A     S01486
     FPMCFMYPDO   E           K        DISK         KINFSR *PSSR A        S01486
     F*      PREFIX QL
     F***PMCFDYZZO***E                    DISK         KINFSR *PSSR A     S01486
     FPMCFDYPAO   E                    DISK         KINFSR *PSSR A        S01486
     F*      PREFIX QK
     F***PMCFMYZZO***E                    DISK         KINFSR *PSSR A     S01486
     FPMCFMYPAO   E                    DISK         KINFSR *PSSR A        S01486
     F*      PREFIX QL
     F***SDCURRL0IF**E           K        DISK         KINFSR *PSSR       S01486
     F*******PREFIX*A6****************************************************S01486
     F***PMPORTPPIF**E           K        DISK         KINFSR *PSSR       S01486
     FPMPORTPDIF  E           K        DISK         KINFSR *PSSR          S01486
     F*      PREFIX PN
     FPMEVNTL4IF  E           K        DISK         KINFSR *PSSR
     F*      PREFIX Q9
     F**
     FPM1460AUO   E                    PRINTER
     F**
     F*==================================================================
     F*                 FUNCTION OF INDICATORS
     F*
     F****71******-*ACCESS*LF/SDBANKL1************************************S01486
     F**  72      - ACCESS LF/PMEVNTL4
     F*
     F**  81      - COMPARE MONTH END DATE WITH EVENT DATE
     F**  82      - COMPARE YEAR END DATE WITH EVENT DATE
     F*
     F**  98      - DATE FORMAT
     F**  99      - LEAP/NON-LEAP YEAR
     F*
     F**  U8      - STANDARD RUN CONTROL ERROR
     F**  U7 U8   - STANDARD DATABASE ERROR
     F*
     F**  LR      - LAST RECORD ON FILE
     F*
     F**                 *************************
     F**                 ** INDICATORS NOT USED **
     F**                 *************************
     F*
     F**      ***************************************************
     F**      * 01   02   03   04   05   06   07   08   09   10 *
     F**      * 11   12   13   14   15   16   17   18   19   20 *
     F**      * 21   22   23   24   25   26   27   28   29   30 *
     F**      * 31   32   33   34   35   36   37   38   39   40 *
     F**      * 41   42   43   44   45   46   47   48   49   50 *
     F**      * 51   52   53   54   55   56   57   58   59   60 *
     F**      * 61   62   63   64   65   66   67   68   69   70 *
     F**      * 71   --   73   74   75   76   77   78   79   80 *
     F**      * --   --   83   84   85   86   87   88   89   90 *
     F**      * 91   92   93   94   95   96   97   --   --      *
     F**      ***************************************************
     F*
     F*==================================================================
     F/EJECT
     E*==================================================================
     E*
     E                    CPY@    1   1 80
     E*
      /COPY ZSRSRC,ZDATE2Z1
      /COPY ZSRSRC,ZPOWERZ1
     E*
     E******************* MONT       12  5 0             MONTH END ARRAY  R01163
     E                    MONT       13  5 0             MONTH END ARRAY  R01163
     E*
     E                    YEAR       50  5 0             YEAR END ARRAY
     E*
     E******************* OACM       12 15 0             OUT.CASH ARRAY M R01163
     E******************* IACM       12 15 0             INC.CASH ARRAY M R01163
     E                    OACM       13 15 0             OUT.CASH ARRAY M R01163
     E                    IACM       13 15 0             INC.CASH ARRAY M R01163
     E                    OACY       50 15 0             OUT.CASH ARRAY Y
     E                    IACY       50 15 0             INC.CASH ARRAY Y
     E*
     E*==================================================================
     E/EJECT
     I*==================================================================
     I*
     I**  LOCAL DATA AREA FOR DATA BASE ERRORS:
     I***LDA********UDS                            256                    S01486
     ILDA         DS                            256                       S01486
     I                                      132 183 DBLOT
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I**  LOCAL DATA AREA FOR RUN + 60 DAYS
     IPMSTAT     UDS                            256
     I                                       11  15 WWEV60
     I*
     I**  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION:
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     I**  DATA STRUCTURE FOR MONTH END DATE:
     IWWMONF     UDS                              6
     I                                        1   20WWDDAY
     I                                        3   40WWMMON
     I                                        5   60WWYYEA
     I*
     I**  DATA STRUCTURE FOR YEAR END DATE:
     IWWYEAF     UDS                              6
     I                                        1   20WWDDAZ
     I                                        3   40WWMMOO
     I                                        5   60WWYYEB
     I*
     ISDBANK    E DSSDBANKPD                                              S01486
     I** DUMMY RECORD FORMAT FOR BANK DETAILS                             S01486
     I*                                                                   CPB001
     ISDMMOD    E DSSDMMODPD                                              CPB001
     I** DUMMY RECORD FORMAT FOR MODULE DETAILS                           CPB001
     I*                                                                   CPB001
     ISDCUST    E DSSDCUSTPD                                              CPB001
     I** DUMMY RECORD FORMAT FOR CUSTOMER DETAILS                         CPB001
     I*                                                                   S01486
     ISDCURR    E DSSDCURRPD                                              S01486
     I** DUMMY RECORD FORMAT FOR CURRENCY DETAILS                         S01486
     I*                                                                   S01486
     ISDPLCS    E DSSDPLCSPD                                              S01486
     I** DUMMY RECORD FORMAT FOR PORTFOLIO MANAGEMENT CUSTOMER DETAILS    S01486
     I*                                                                   S01486
     IDSFDY     E DSDSFDY                                                 S01486
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01486
     I*                                                                   S01486
     IDSSDY     E DSDSSDY                                                 S01486
     I* SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE                S01486
     I*                                                                   S01486
     IDSLDY     E DSDSLDY                                                 CPB001
     I* THIRD DS FOR ACCESS PROGRAMS, VERY LONG DATA STRUCTURE            CPB001
     I**================================================================
     I/EJECT
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INDEX OF SUBROUTINES                                             *
      * MAIN     -  CHIEF PROGRAM                                        *
      * #INIT    -  INITIALIZE - ACCESS STANDING DATA                    *
      * #P03     -  DETERMINE MONTH END DATES                            *
      * #P04     -  DETERMINE YEAR END DATES                             *
      * #P01     -  PROCESS PORTFOLIO CASHFLOW EVENTS                    *
      * #P07     -  MAINTAIN DAY TOTALS                                  *
      * #P08     -  MAINTAIN MONTH TOTALS                                *
      * #P09     -  MAINTAIN YEAR TOTALS                                 *
      * #P05     -  UPDATE PORTFOLIO CASHFLOW DAY TOTALS                 *
      * #P06     -  UPDATE PORTFOLIO CASHFLOW MONTH/YEAR END TOTALS      *
      * #P02     -  PROCESS AUDIT                                        *
      * ZDATE2   -  CONVERT DAY NUMBER TO DATE                           *
      * ZDATE1   -  CONVERT DATE TO DAY NUMBER                           *
      * *PSSR    -  DATABASE ERROR                                       *
      *                                                                  *
      * EXTERNAL ROUTINES CALLED                                         *
      *                                                                  *
      ********************************************************************
     C*            MAIN PROGRAM  START
     C**================================================================
     C*
     C****DEFINE*KEY*LIST*TO*ACCESS*PMPORTPP:*****************************S01486
     C**  DEFINE KEY LIST TO ACCESS PMPORTPD:                             S01486
     C           PFKEY     KLIST
     C                     KFLD           Q9CNUM
     C                     KFLD           Q9PTFR
     C*
     C**  INITIALIZATION:
     C                     EXSR #INIT
     C*
     C**  PROCESS PORTFOLIO CASHFLOW EVENTS:
     C                     EXSR #P01
     C*
     C**  PROCESS AUDIT:
     C                     EXSR #P02
     C*
     C**  END OF PROGRAM:
     C                     MOVE '1'       *INLR
     C**================================================================
     C*            MAIN PROGRAM END
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P01     -  PROCESS PORTFOLIO CASHFLOW EVENTS                    *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * #P05     -  UPDATE PORTFOLIO CASHFLOW DAY TOTALS                 *
      * #P06     -  UPDATE PORTFOLIO CASHFLOW MONTH/YEAR END TOTALS      *
      * #P07     -  MAINTAIN DAY TOTALS                                  *
      * #P08     -  MAINTAIN MONTH TOTALS                                *
      * #P09     -  MAINTAIN YEAR TOTALS                                 *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -  CHIEF PROGRAM                                        *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWCNUM   -  CUSTOMER NUMBER                                      *
      * WWPTFR   -  PORTFOLIO REFERENCE                                  *
      * WWEVTD   -  EVENT DATE                                           *
      *                                                                  *
      ********************************************************************
     C           #P01      BEGSR                           * #P01 BEGSR *
     C*
     C**  READ PORTFOLIO CASHFLOW EVENTS
     C           *LOVAL    SETLLPMEVNTL4
     C                     READ PMEVNTL4                 72
     C*
     C**  PERFORM PROCESSING UNTIL END OF FILE REACHED
     C           *IN72     DOWEQ'0'                        B1
     C*
     C**  WHEN CHANGE OF CUSTOMER, PORTFOLIO, CCY OR DATE, UPDATE
     C**  PORTFOLIO CASHFLOW DAY TOTALS
     C*
     C********** WWCNUM    IFNE *ZEROS                     B2                                 CSD027
     C           WWCNUM    IFNE *BLANKS                    B2                                 CSD027
     C*
     C           WWICAS    IFNE *ZERO                      B3
     C           WWOCAS    ORNE *ZERO
     C*
     C           WWCNUM    CASNEQ9CNUM    #P05             B4
     C           WWPTFR    CASNEQ9PTFR    #P05
     C           WWECCY    CASNEQ9ECCY    #P05
     C           WWEVTD    CASNEQ9EVTD    #P05
     C                     END                             E4
     C*
     C                     END                             E3
     C                     END                             E2
     C*
     C**  WHEN CHANGE OF CUSTOMER OR PORTFOLIO, UPDATE PORTFOLIO
     C**  CASHFLOW MONTH AND YEAR END TOTALS
     C*
     C********** WWCNUM    IFNE *ZEROS                     B2                                 CSD027
     C           WWCNUM    IFNE *BLANKS                    B2                                 CSD027
     C           WWCNUM    CASNEQ9CNUM    #P06             B3
     C           WWPTFR    CASNEQ9PTFR    #P06
     C           WWECCY    CASNEQ9ECCY    #P06
     C                     END                             E3
     C                     END                             E2
     C*
     C**  WHEN CHANGE OF CUSTOMER, ACCESS CUSTOMER DETAILS
     C*
     C           WWCNUM    IFNE Q9CNUM                     B2
     C*
     C***********          MOVE Q9CNUM    WWCNUA  6                       S01486
     C***********WWCNUA    CHAINSDPLCSPD             72                   S01486
     C                     MOVELQ9CNUM    KEYCN                           S01486
     C                     CALL 'AOPLCSR0'                                S01486
     C                     PARM *BLANKS   RTCD                            S01486
     C                     PARM '*KEY'    OPTN                            S01486
     C                     PARM           KEYCN  10                       S01486
     C           SDPLCS    PARM SDPLCS    DSFDY                           S01486
     C*
     C************IN72     IFEQ '1'                        B3             S01486
     C           RTCD      IFNE *BLANKS                                   S01486
     C           BGN4ST    ANDNE'Y'                                       CPB001
     C           *LOCK     IN   LDA                                       S01486
     C                     Z-ADD3         DBASE            ***************
     C                     MOVEL'SDPLCSPD'DBFILE           * DB ERROR 03 *
     C                     MOVELQ9CNUM    DBKEY            ***************S01486
     C                     OUT  LDA                                       S01486
     C                     EXSR *PSSR                      ***************
     C                     END                             E3
     C*                                                                   CPB001
     C                     CALL 'AOCUSTR1'                                CPB001
     C                     PARM *BLANKS   RTCD                            CPB001
     C                     PARM '*KEY'    OPTN                            CPB001
     C                     PARM           KEYCN                           CPB001
     C                     PARM *BLANKS   KEYST   7                       CPB001
     C           SDCUST    PARM SDCUST    DSLDY                           CPB001
     C*                                                                   CPB001
     C           BBPRBA    IFEQ 'Y'                                       CPB001
     C           PFKEY     CHAINPMPORTPD             72                   CPB001
     C           *IN72     IFEQ '1'                                       CPB001
     C           *LOCK     IN   LDA                        ***************CPB001
     C                     Z-ADD19        DBASE            **DB ERROR 19**CPB001
     C                     MOVEL'PMPORT'  DBFILE           ***************CPB001
     C                     MOVEL*BLANKS   DBKEY                           CPB001
     C                     MOVELQ9CNUM    DBKEY                           CPB001
     C                     MOVE Q9PTFR    DBKEY                           CPB001
     C                     OUT  LDA                                       CPB001
     C                     EXSR *PSSR                                     CPB001
     C                     ELSE                                           CPB001
     C                     MOVELPNPTCY    QBCSBY                          CPB001
     C                     Z-ADDPNPCDP    QBBYDP                          CPB001
     C                     ENDIF                                          CPB001
     C                     ENDIF                                          CPB001
     C*
     C*****                Z-ADDQ9CNUM    WWCNUM                          B07827
     C*
     C**  ALSO,ACCESS CUSTOMER CURRENCY DETAILS
     C*
     C***********QBCSBY    CHAINSDCURRL0             72                   S01486
     C                     CALL 'AOCURRR0'                                S01486
     C                     PARM *BLANKS   RTCD                            S01486
     C                     PARM '*KEY'    OPTN                            S01486
     C                     PARM QBCSBY    KEYCY   3                       S01486
     C           SDCURR    PARM SDCURR    DSSDY                           S01486
     C*
     C************IN72     IFEQ '1'                        B3             S01486
     C           RTCD      IFNE *BLANKS                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     Z-ADD4         DBASE            ***************
     C                     MOVEL'SDCURRL0'DBFILE           * DB ERROR 04 *
     C                     MOVELQBCSBY    DBKEY                           S01486
     C                     OUT  LDA                                       S01486
     C                     EXSR *PSSR                      ***************
     C                     END                             E3
     C*
     C                     MOVE QBCSBY    WWCCCY
     C                     Z-ADDA6SPRT    WWCRAT
     C                     Z-ADDA6NBDP    WWCCDP
     C                     MOVE A6MDIN    WWCMUL
     C                     END                             E2
     C*
     C**  WHEN CHANGE OF CUSTOMER OR PORTFOLIO, ACCESS
     C**  PORTFOLIO DETAILS
     C*
     C           WWCNUM    IFNE Q9CNUM                     B2
     C           WWPTFR    ORNE Q9PTFR
     C*
     C           Q9PTFR    IFNE '9997'                     B3
     C***********PFKEY     CHAINPMPORTPP             72                   S01486
     C           PFKEY     CHAINPMPORTPD             72                   S01486
     C*
     C           *IN72     IFEQ '1'                        B4
     C           *LOCK     IN   LDA                                       S01486
     C                     Z-ADD5         DBASE            ***************
     C***********          MOVEL'PMPORTPP'DBFILE           * DB ERROR 05 *S01486
     C                     MOVEL'PMPORTPD'DBFILE           * DB ERROR 05 *S01486
     C                     MOVELQ9CNUM    DBKEY            ***************S01486
     C                     MOVE Q9PTFR    DBKEY                           S01486
     C                     OUT  LDA                                       S01486
     C                     EXSR *PSSR                      ***************
     C                     END                             E4
     C*
     C                     ELSE                            X3
     C                     MOVE QBCSBY    PNPTCY
     C                     END                             E3
     C*
     C                     MOVE Q9PTFR    WWPTFR
     C                     END                             E2
     C*
     C**********           Z-ADDQ9CNUM    WWCNUM                                       B07827 CSD027
     C                     MOVE Q9CNUM    WWCNUM                                              CSD027
     C*
     C**  WHEN CHANGE OF PORTFOLIO CURRENCY ACCESS  CURRENCY DETAILS
     C*
     C           WWPTCY    IFNE PNPTCY                     B2
     C*
     C***********PNPTCY    CHAINSDCURRL0             72                   S01486
     C                     CALL 'AOCURRR0'                                S01486
     C                     PARM *BLANKS   RTCD                            S01486
     C                     PARM '*KEY'    OPTN                            S01486
     C                     PARM PNPTCY    KEYCY                           S01486
     C           SDCURR    PARM SDCURR    DSSDY                           S01486
     C*
     C************IN72     IFEQ '1'                        B3             S01486
     C           RTCD      IFNE *BLANKS                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     Z-ADD6         DBASE            ***************
     C                     MOVEL'SDCURRL0'DBFILE           * DB ERROR 06 *
     C                     MOVELPNPTCY    DBKEY                           S01486
     C                     OUT  LDA                                       S01486
     C                     EXSR *PSSR                      ***************
     C                     END                             E3
     C*
     C                     MOVE PNPTCY    WWPTCY
     C                     Z-ADDA6SPRT    WWPRAT
     C                     Z-ADDA6NBDP    WWPCDP
     C                     MOVE A6MDIN    WWPMUL
     C                     END                             E2
     C*
     C**  WHEN CHANGE OF EVENT CCY ACCESS CURRENCY DETAILS
     C*
     C           WWECCY    IFNE Q9ECCY                     B2
     C*
     C***********Q9ECCY    CHAINSDCURRL0             72                   S01486
     C                     CALL 'AOCURRR0'                                S01486
     C                     PARM *BLANKS   RTCD                            S01486
     C                     PARM '*KEY'    OPTN                            S01486
     C                     PARM Q9ECCY    KEYCY                           S01486
     C           SDCURR    PARM SDCURR    DSSDY                           S01486
     C*
     C************IN72     IFEQ '1'                        B3             S01486
     C           RTCD      IFNE *BLANKS                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     Z-ADD7         DBASE            ***************
     C                     MOVEL'SDCURRL0'DBFILE           * DB ERROR 07 *
     C                     MOVELQ9ECCY    DBKEY                           S01486
     C                     OUT  LDA                                       S01486
     C                     EXSR *PSSR                      ***************
     C                     END                             E3
     C*
     C                     MOVE Q9ECCY    WWECCY
     C                     Z-ADDA6SPRT    WWERAT
     C                     Z-ADDA6NBDP    WWECDP
     C                     MOVE A6MDIN    WWEMUL
     C*
     C                     END                             E2
     C*
     C**  WHEN CHANGE OF EVENT DATE SAVE PREVIOUS DATE
     C           WWEVTD    IFNE Q9EVTD                     B2
     C                     Z-ADDQ9EVTD    WWEVTD
     C                     END                             E2
     C*
     C**  MAINTAIN DAY TOTALS
     C           Q9EVTD    IFLT WWNV60                     B2
     C                     EXSR #P07
     C                     END                             E2
     C*
     C**  MAINTAIN MONTH TOTALS
     C                     EXSR #P08
     C*
     C**  MAINTAIN YEAR TOTALS
     C                     EXSR #P09
     C*
     C**  READ PORTFOLIO CASHFLOW EVENTS
     C                     READ PMEVNTL4                 72
     C                     END                             E1
     C*
     C**  WHEN END OF FILE REACHED, PROCESS LAST RECORD
     C           *IN72     IFEQ '1'                        B1
     C********** WWCNUM    ANDNE*ZEROS                                                        CSD027
     C           WWCNUM    ANDNE*BLANKS                                                       CSD027
     C           WWICAS    IFNE *ZERO                      B2
     C           WWOCAS    ORNE *ZERO
     C                     EXSR #P05
     C                     END                             E2
     C                     EXSR #P06
     C                     END                             E1
     C*                                                    **************
     C                     ENDSR                           * #P01 ENDSR *
     C*                                                    **************
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P07     -  MAINTAIN DAY TOTALS                                  *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P01     -  PROCESS PORTFOLIO CASHFLOW EVENTS                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWBLCC   -  BALANCE IN CUSTOMER CURRENCY                         *
      * WWBLPC   -  BALANCE IN PORTFOLIO CURRENCY                        *
      *                                                                  *
      ********************************************************************
     C           #P07      BEGSR                           * #P07 BEGSR *
     C*                                                    **************
     C**  IF THE EVENT AMOUNT PAY/RECEIVE IS 'P';
     C**  1) ADD EVENT AMOUNT IN EVENT CURRENCY TO OUTGOING AMOUNT
     C**     IN CASHFLOW CURRENCY:
     C           Q9AMTR    IFEQ 'P'                        - B1
     C                     ADD  Q9ETAM    WWOCAS
     C                     END                             - E1
     C*
     C**  IF EVENT AMOUNT PAY/RECEIVE IS 'R',
     C**  ADD THE EVENT AMOUNTS:
     C           Q9AMTR    IFEQ 'R'                        - B1
     C                     ADD  Q9ETAM    WWICAS
     C                     END                             - E1
     C*                                                    **************
     C                     ENDSR                           * #P07 ENDSR *
     C*                                                    **************
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P08     -  MAINTAIN MONTH TOTALS                                *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P01     -  PROCESS PORTFOLIO CASHFLOW EVENTS                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           #P08      BEGSR                           * #P08 BEGSR *
     C*                                                    **************
     C**  REACH THE MONTH END ARRAY FOR THE ELEMENT WITH MONTH END DATE
     C**  GREATER THAN OR EQUAL TO THE EVENT DATE:
     C                     Z-ADD0         X
     C                     MOVE '0'       *IN81
     C           *IN81     DOWEQ'0'
     C********** X ******* ANDLT12                                        R01163
     C           X         ANDLT13                                        R01163
     C*
     C                     ADD  1         X
     C           MONT,X    IFGE Q9EVTD
     C                     MOVE '1'       *IN81
     C                     END
     C                     END
     C*
     C**  IF SEARCH IS SUCCESSFULL;
     C**  IF THE EVENT AMOUNT PAY/RECEIVE IS 'P';
     C**  1) ADD EVENT AMOUNT IN EVENT CURRENCY TO TOTAL OUTGOING
     C**     AMOUNT IN EVENT CURRENCY OF THE MONTH END ARRAY ELEMENT
     C**     FOUND ;
     C*
     C           *IN81     IFEQ '1'
     C           Q9AMTR    IFEQ 'P'
     C                     ADD  Q9ETAM    OACM,X
     C                     END
     C*
     C**  IF THE EVENT AMOUNT PAY/RECEIVE IS 'R';
     C**  ADD EVENT AMOUNTS TO THE CORRESPONDING TOTAL INCOMING AMOUNTS:
     C           Q9AMTR    IFEQ 'R'
     C                     ADD  Q9ETAM    IACM,X
     C                     END
     C*
     C                     END
     C*                                                    **************
     C                     ENDSR                           * #P08 ENDSR *
     C*                                                    **************
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P09     -  MAINTAIN YEAR TOTALS                                 *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P01     -  PROCESS PORTFOLIO CASHFLOW EVENTS                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           #P09      BEGSR                           * #P09 BEGSR *
     C*                                                    **************
     C**  REACH THE YEAR END ARRAY FOR THE ELEMENT WITH YEAR END DATE
     C**  GREATER THAN OR EQUAL TO THE EVENT DATE:
     C                     Z-ADD0         Y
     C                     MOVE '0'       *IN82
     C           *IN82     DOWEQ'0'
     C           Y         ANDLT50
     C*
     C                     ADD  1         Y
     C           YEAR,Y    IFGE Q9EVTD
     C                     MOVE '1'       *IN82
     C                     END
     C                     END
     C*
     C**  IF SEARCH IS SUCCESSFULL;
     C**  IF THE EVENT AMOUNT PAY/RECEIVE IS 'P';
     C**  1) ADD EVENT AMOUNT IN EVENT CURRENCY TO TOTAL OUTGOING
     C**     AMOUNT IN EVENT CURRENCY OF THE YEAR END ARRAY ELEMENT
     C**     FOUND ;
     C           *IN82     IFEQ '1'
     C           Q9AMTR    IFEQ 'P'
     C                     ADD  Q9ETAM    OACY,Y
     C                     END
     C*
     C**  IF THE EVENT AMOUNT PAY/RECEIVE IS 'R';
     C**  ADD EVENT AMOUNTS TO THE CORRESPONDING TOTAL INCOMING AMOUNTS:
     C           Q9AMTR    IFEQ 'R'
     C                     ADD  Q9ETAM    IACY,Y
     C                     END
     C*
     C                     END
     C*                                                    **************
     C                     ENDSR                           * #P09 ENDSR *
     C*                                                    **************
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P05     -  UPDATE PORTFOLIO CASHFLOW DAY TOTALS                 *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P01     -  PROCESS PORTFOLIO CASHFLOW EVENTS                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWBLCC   -  BALANCE IN CUSTOMER CURRENCY                         *
      * WWBLPC   -  BALANCE IN PORTFOLIO CURRENCY                        *
      * WWCNUM   -  CUSTOMER NUMBER                                      *
      * WWPTFR   -  PORTFOLIO REFERENCE                                  *
      * WWECCY   -  EVENT CURRENCY                                       *
      * WWEVTD   -  EVENT DATE                                           *
      * WWDAYT   -  RECORDS WRITTEN TO PMCFDYPP                          *
      *                                                                  *
      ********************************************************************
     C           #P05      BEGSR                           * #P05 BEGSR *
     C*                                                    **************
     C**  WRITE TO PORTFOLIO CASHFLOW DAY TOTALS:
     C**********           Z-ADDWWCNUM    QKCNUM                                              CSD027
     C                     MOVE WWCNUM    QKCNUM                                              CSD027
     C                     MOVE WWPTFR    QKPTFR
     C                     MOVE WWECCY    QKCCY
     C                     Z-ADDWWEVTD    QKDYNO
     C                     Z-ADDWWECDP    QKCDP
     C                     Z-ADDWWICAS    QKICAS
     C                     Z-ADDWWOCAS    QKOCAS
     C           QKICAS    SUB  QKOCAS    QKNECS
     C*
     C**   CONVERT NET CASHFLOW AMOUNT TO PORTFOLIO CURRENCY
     C                     Z-ADDQKNECS    ZAMTF
     C                     MOVE WWECCY    ZCCYF
     C                     MOVE WWPTCY    ZCCYT
     C                     Z-ADDWWERAT    ZRATEF
     C                     Z-ADDWWPRAT    ZRATET
     C                     MOVE WWEMUL    ZMDINF
     C                     MOVE WWPMUL    ZMDINT
     C                     Z-ADDWWECDP    ZCDPF
     C                     Z-ADDWWPCDP    ZCDPT
     C                     EXSR ZCCYCN
     C                     Z-ADDZAMTT     QKNEPT
     C*
     C**   CONVERT NET CASHFLOW AMOUNT TO CUSTOMER CURRENCY
     C                     Z-ADDQKNECS    ZAMTF
     C                     MOVE WWECCY    ZCCYF
     C                     MOVE WWCCCY    ZCCYT
     C                     Z-ADDWWERAT    ZRATEF
     C                     Z-ADDWWCRAT    ZRATET
     C                     MOVE WWEMUL    ZMDINF
     C                     MOVE WWCMUL    ZMDINT
     C                     Z-ADDWWECDP    ZCDPF
     C                     Z-ADDWWCCDP    ZCDPT
     C                     EXSR ZCCYCN
     C                     Z-ADDZAMTT     QKNECT
     C*
     C**   CONVERT NET CASHFLOW AMOUNT TO BASE CURRENCY
     C                     Z-ADDQKNECS    ZAMTF
     C                     MOVE WWECCY    ZCCYF
     C                     MOVE BJCYCD    ZCCYT
     C                     Z-ADDWWERAT    ZRATEF
     C                     Z-ADDWWBRAT    ZRATET
     C                     MOVE WWEMUL    ZMDINF
     C                     MOVE WWBMUL    ZMDINT
     C                     Z-ADDWWECDP    ZCDPF
     C                     Z-ADDWWBCDP    ZCDPT
     C                     EXSR ZCCYCN
     C                     Z-ADDZAMTT     QKNEBS
     C*
     C                     WRITEPMCFDYP0
     C*
     C**  ADD 1 TO COUNT OF DAY TOTALS RECORDS:
     C                     ADD  1         WWDAYT  50
     C*
     C                     Z-ADD0         WWOCAS
     C                     Z-ADD0         WWICAS
     C*                                                    **************
     C                     ENDSR                           * #P05 ENDSR *
     C*                                                    **************
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P06     -  UPDATE PORTFOLIO CASHFLOW MONTH/YEAR END TOTALS      *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P01     -  PROCESS PORTFOLIO CASHFLOW EVENTS                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWCNUM   -  CUSTOMER NUMBER                                      *
      * WWPTFR   -  PORTFOLIO REFERENCE                                  *
      * WWMOYT   -  RECORDS WRITTEN TO PMCFMYPP                          *
      *                                                                  *
      ********************************************************************
     C           #P06      BEGSR                           * #P06 BEGSR *
     C*
     C**  SET INCOMING AND OUTGOING AMOUNTS TO ZERO
     C                     Z-ADD0         QKICAS
     C                     Z-ADD0         QKOCAS
     C*                                                    **************
     C**  FOR EACH ELEMENT IN THE MONTH END ARRAY:
     C                     Z-ADD1         X
     C********** 1 ******* DO   12                         - B1           R01163
     C           1         DO   13                         - B1           R01163
     C*
     C**  IF ANY OF THE INCOMING OR OUTGOING AMOUNTS FOR THIS ARRAY
     C**  ELEMENT IS NON-ZERO;
     C           IACM,X    IFNE *ZERO
     C           OACM,X    ORNE *ZERO
     C*
     C**  1) SET MONTH/YEAR END DAY NUMBER EQUAL TO MONTH END DATE FROM
     C**     MONTH END ARRAY ELEMENT:
     C**  2) SET MONTH/YEAR INDICATOR EQUAL TO 'M':
     C**  3) SET EVERY INCOMING & OUTGOING CURRENCY EQUAL TO CORRESPON-
     C**     DING TOTAL INCOMING & OUTGOING AMOUNT FROM MONTH END ARRAY
     C**     ELEMENT;
     C**  4) WRITE TO PORTFOLIO CASHFLOW MONTH AND YEAR END TOTALS:
     C**********           Z-ADDWWCNUM    QLCNUM                                              CSD027
     C                     MOVE WWCNUM    QLCNUM                                              CSD027
     C                     MOVE WWPTFR    QLPTFR
     C                     Z-ADDMONT,X    QLMYNO
     C                     MOVE 'M'       QLMYIN
     C                     MOVE WWECCY    QLCCY
     C                     Z-ADDWWECDP    QLCDP
     C                     Z-ADDIACM,X    QLICAS
     C                     Z-ADDOACM,X    QLOCAS
     C           QLICAS    SUB  QLOCAS    QLNECS
     C*
     C**   CONVERT NET CASHFLOW AMOUNT TO PORTFOLIO CURRENCY
     C                     Z-ADDQLNECS    ZAMTF
     C                     MOVE WWECCY    ZCCYF
     C                     MOVE WWPTCY    ZCCYT
     C                     Z-ADDWWERAT    ZRATEF
     C                     Z-ADDWWPRAT    ZRATET
     C                     MOVE WWEMUL    ZMDINF
     C                     MOVE WWPMUL    ZMDINT
     C                     Z-ADDWWECDP    ZCDPF
     C                     Z-ADDWWPCDP    ZCDPT
     C                     EXSR ZCCYCN
     C                     Z-ADDZAMTT     QLNEPT
     C*
     C**   CONVERT NET CASHFLOW AMOUNT TO CUSTOMER CURRENCY
     C                     Z-ADDQLNECS    ZAMTF
     C                     MOVE WWECCY    ZCCYF
     C                     MOVE WWCCCY    ZCCYT
     C                     Z-ADDWWERAT    ZRATEF
     C                     Z-ADDWWCRAT    ZRATET
     C                     MOVE WWEMUL    ZMDINF
     C                     MOVE WWCMUL    ZMDINT
     C                     Z-ADDWWECDP    ZCDPF
     C                     Z-ADDWWCCDP    ZCDPT
     C                     EXSR ZCCYCN
     C                     Z-ADDZAMTT     QLNECT
     C*
     C**   CONVERT NET CASHFLOW AMOUNT TO BASE CURRENCY
     C                     Z-ADDQLNECS    ZAMTF
     C                     MOVE WWECCY    ZCCYF
     C                     MOVE BJCYCD    ZCCYT
     C                     Z-ADDWWERAT    ZRATEF
     C                     Z-ADDWWBRAT    ZRATET
     C                     MOVE WWEMUL    ZMDINF
     C                     MOVE WWBMUL    ZMDINT
     C                     Z-ADDWWECDP    ZCDPF
     C                     Z-ADDWWBCDP    ZCDPT
     C                     EXSR ZCCYCN
     C                     Z-ADDZAMTT     QLNEBS
     C*
     C                     WRITEPMCFMYP0
     C*
     C**  ADD 1 TO COUNT OF MONTH/YEAR END TOTALS RECORDS:
     C                     ADD  1         WWMOYT  50
     C                     END                             - E2
     C*
     C                     ADD  1         X
     C                     END                             - E1
     C*
     C**  FOR EACH ELEMENT IN THE YEAR END ARRAY:
     C                     Z-ADD1         Y
     C           1         DO   50                         - B1
     C*
     C**  IF ANY OF THE INCOMING OR OUTGOING AMOUNTS FOR THIS ARRAY
     C**  ELEMENT IS NON-ZERO;
     C           IACY,Y    IFNE *ZERO
     C           OACY,Y    ORNE *ZERO
     C*
     C**  1) SET MONTH/YEAR END DAY NUMBER EQUAL TO YEAR END DATE FROM
     C**     YEAR END ARRAY ELEMENT:
     C**  2) SET MONTH/YEAR INDICATOR EQUAL TO 'Y':
     C**  3) SET EVERY INCOMING & OUTGOING CURRENCY EQUAL TO CORRESPON-
     C**     DING TOTAL INCOMING & OUTGOING AMOUNT FROM YEAR END ARRAY
     C**     ELEMENT;
     C**  4) WRITE TO PORTFOLIO CASHFLOW MONTH AND YEAR END TOTALS:
     C**********           Z-ADDWWCNUM    QLCNUM                                              CSD027
     C                     MOVE WWCNUM    QLCNUM                                              CSD027
     C                     MOVE WWPTFR    QLPTFR
     C                     Z-ADDYEAR,Y    QLMYNO
     C                     MOVE 'Y'       QLMYIN
     C                     MOVE WWECCY    QLCCY
     C                     Z-ADDWWECDP    QLCDP
     C                     Z-ADDIACY,Y    QLICAS
     C                     Z-ADDOACY,Y    QLOCAS
     C           QLICAS    SUB  QLOCAS    QLNECS
     C*
     C**   CONVERT NET CASHFLOW AMOUNT TO PORTFOLIO CURRENCY
     C                     Z-ADDQLNECS    ZAMTF
     C                     MOVE WWECCY    ZCCYF
     C                     MOVE WWPTCY    ZCCYT
     C                     Z-ADDWWERAT    ZRATEF
     C                     Z-ADDWWPRAT    ZRATET
     C                     MOVE WWEMUL    ZMDINF
     C                     MOVE WWPMUL    ZMDINT
     C                     Z-ADDWWECDP    ZCDPF
     C                     Z-ADDWWPCDP    ZCDPT
     C                     EXSR ZCCYCN
     C                     Z-ADDZAMTT     QLNEPT
     C*
     C**   CONVERT NET CASHFLOW AMOUNT TO CUSTOMER CURRENCY
     C                     Z-ADDQLNECS    ZAMTF
     C                     MOVE WWECCY    ZCCYF
     C                     MOVE WWCCCY    ZCCYT
     C                     Z-ADDWWERAT    ZRATEF
     C                     Z-ADDWWCRAT    ZRATET
     C                     MOVE WWEMUL    ZMDINF
     C                     MOVE WWCMUL    ZMDINT
     C                     Z-ADDWWECDP    ZCDPF
     C                     Z-ADDWWCCDP    ZCDPT
     C                     EXSR ZCCYCN
     C                     Z-ADDZAMTT     QLNECT
     C*
     C**   CONVERT NET CASHFLOW AMOUNT TO BASE CURRENCY
     C                     Z-ADDQLNECS    ZAMTF
     C                     MOVE WWECCY    ZCCYF
     C                     MOVE BJCYCD    ZCCYT
     C                     Z-ADDWWERAT    ZRATEF
     C                     Z-ADDWWBRAT    ZRATET
     C                     MOVE WWEMUL    ZMDINF
     C                     MOVE WWBMUL    ZMDINT
     C                     Z-ADDWWECDP    ZCDPF
     C                     Z-ADDWWBCDP    ZCDPT
     C                     EXSR ZCCYCN
     C                     Z-ADDZAMTT     QLNEBS
     C*
     C                     WRITEPMCFMYP0
     C*
     C**  ADD 1 TO COUNT OF MONTH/YEAR END TOTALS RECORDS:
     C                     ADD  1         WWMOYT  50
     C                     END                             - E2
     C*
     C                     ADD  1         Y
     C                     END                             - E1
     C*
     C**  SET ALL INCOMING & OUTGOING AMOUNTS FOR EACH ELEMENT IN THE
     C**  MONTH END ARRAY TO '0':
     C                     Z-ADD*ZERO     IACM
     C                     Z-ADD*ZERO     OACM
     C*
     C**  SET ALL INCOMING & OUTGOING AMOUNTS FOR EACH ELEMENT IN THE
     C**  YEAR END ARRAY TO '0':
     C                     Z-ADD*ZERO     IACY
     C                     Z-ADD*ZERO     OACY
     C*                                                    **************
     C                     ENDSR                           * #P06 ENDSR *
     C*                                                    **************
     C*
     C**************************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P02     -  PROCESS AUDIT                                        *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -  CHIEF PROGRAM                                        *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWDAYT   -  RECORDS WRITTEN TO PMCFDYPP                          *
      * WWMOYT   -  RECORDS WRITTEN TO PMCFMYPP                          *
      *                                                                  *
      ********************************************************************
     C           #P02      BEGSR                           * #P02 BEGSR *
     C*                                                    **************
     C**  WRITE TO PORTFOLIO CASHFLOW DAY TOTALS AUDIT:
     C                     Z-ADDWWDAYT    QKNORE
     C                     WRITEPMCFDYZ0
     C*
     C**  WRITE TO PORTFOLIO CASHFLOW MONTH AND YEAR END TOTALS AUDIT:
     C                     Z-ADDWWMOYT    QLNORE
     C                     WRITEPMCFMYZ0
     C*
     C**  WRITE TO EVENT SUMMARY AUDIT REPORT:
     C                     Z-ADDWWDAYT    RRDAYT
     C                     Z-ADDWWMOYT    RRMOYT
     C*
     C                     WRITEHEADAU
     C                     WRITEAUDIT
     C***********          WRITEENDAU                                     S01486
     C*                                                    **************
     C                     ENDSR                           * #P02 ENDSR *
     C*                                                    **************
     C*
      ********************************************************************
      /EJECT
      /COPY ZSRSRC,ZDATE1Z2
      /EJECT
      /COPY ZSRSRC,ZDATE2Z2
      /EJECT
      /COPY ZSRSRC,ZCCYCN
      ****/EJECT                                                          47719
      ****/COPY ZSRSRC,ZCCYXR                                             47719
      /EJECT                                                              S01486
      /COPY ZSRSRC,ZCCYXR                                                 S01486
      /EJECT
      ********************************************************************
      *                                                                  *
      * #INIT    -  INITIALIZE - ACCESS STANDING DATA                    *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * *PSSR    -  DATABASE ERROR                                       *
      * #P03     -  DETERMINE MONTH END DATES                            *
      * #P04     -  DETERMINE YEAR END DATES                             *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -  CHIEF PROGRAM                                        *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           #INIT     BEGSR                           * #INIT BEGSR *
     C*                                                    ***************
     C**  PREPARE LDA:
     C           *NAMVAR   DEFN           LDA                             S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'PM1460'  DBPGM
     C                     OUT  LDA                                       S01486
     C*                                                    ***************
     C**  DEFINE WORK FIELDS
     C           *LIKE     DEFN Q9CNUM    WWCNUM
     C           *LIKE     DEFN Q9PTFR    WWPTFR
     C           *LIKE     DEFN Q9ECCY    WWECCY
     C           *LIKE     DEFN Q9EVTD    WWEVTD
     C           *LIKE     DEFN Q9PTFA    WWBLPC
     C           *LIKE     DEFN Q9CUCA    WWBLCC
     C           *LIKE     DEFN PNPTCY    WWPTCY
     C           *LIKE     DEFN QBCSBY    WWCCCY
     C           *LIKE     DEFN A6SPRT    WWBRAT
     C           *LIKE     DEFN A6MDIN    WWBMUL
     C           *LIKE     DEFN A6NBDP    WWBCDP
     C           *LIKE     DEFN A6SPRT    WWCRAT
     C           *LIKE     DEFN A6MDIN    WWCMUL
     C           *LIKE     DEFN A6NBDP    WWCCDP
     C           *LIKE     DEFN A6SPRT    WWPRAT
     C           *LIKE     DEFN A6MDIN    WWPMUL
     C           *LIKE     DEFN A6NBDP    WWPCDP
     C           *LIKE     DEFN A6SPRT    WWERAT
     C           *LIKE     DEFN A6MDIN    WWEMUL
     C           *LIKE     DEFN A6NBDP    WWECDP
     C           *LIKE     DEFN QLICAS    WWICAS
     C           *LIKE     DEFN QLOCAS    WWOCAS
     C*
     C**  GET BANK DETAILS:
     C***********          READ SDBANKL1                 71               S01486
     C***********                                                         S01486
     C************IN71     IFEQ '1'                        - B1           S01486
     C***********          Z-ADD01        DBASE            ***************S01486
     C***********          MOVE 'SDBANKL1'DBFILE           **DB ERROR 01**S01486
     C***********          MOVEL' NONE '  DBKEY            ***************S01486
     C***********          EXSR *PSSR                                     S01486
     C***********          END                             - E1           S01486
     C                     CALL 'AOBANKR0'                                S01486
     C                     PARM '*DBERR'  RTCD    7                       S01486
     C                     PARM '*FIRST'  OPTN    7                       S01486
     C           SDBANK    PARM SDBANK    DSFDY                           S01486
     C*                                                                   CPB001
     C**   Access module details                                          CPB001
     C                     CALL 'AOMMODR0'                                CPB001
     C                     PARM *BLANKS   @RTCD   7                       CPB001
     C                     PARM '*FIRST ' @OPTN   7                       CPB001
     C           SDMMOD    PARM SDMMOD    DSFDY                           CPB001
     C*
     C**  ACCESS BASE CURRENCY DETAILS
     C***********BJCYCD    CHAINSDCURRL0             72                   S01486
     C                     CALL 'AOCURRR0'                                S01486
     C                     PARM *BLANKS   RTCD                            S01486
     C                     PARM '*KEY'    OPTN                            S01486
     C                     PARM BJCYCD    KEYCY                           S01486
     C           SDCURR    PARM SDCURR    DSSDY                           S01486
     C*
     C************IN72     IFEQ '1'                                       S01486
     C           RTCD      IFNE *BLANKS                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     Z-ADD2         DBASE            ***************
     C                     MOVEL'SDCURRL0'DBFILE           * DB ERROR 02 *
     C                     MOVELBJCYCD    DBKEY                           S01486
     C                     OUT  LDA                                       S01486
     C                     EXSR *PSSR                      ***************
     C                     END
     C*
     C                     Z-ADDA6SPRT    WWBRAT
     C                     Z-ADDA6NBDP    WWBCDP
     C                     MOVE A6MDIN    WWBMUL
     C*
     C**  DETERMINE MONTH END DATES:
     C                     EXSR #P03
     C*
     C**  DETERMINE YEAR END DATES:
     C                     EXSR #P04
     C*
     C**  INITIALIZE ZDATE2 & ZDATE1 INDICATORS AND FIELDS OF KLIST:
     C                     MOVE '0'       *IN98
     C                     MOVE '0'       *IN99
     C**********           Z-ADD*ZERO     Q9CNUM                                              CSD027
     C                     MOVE *BLANKS   Q9CNUM                                              CSD027
     C                     MOVE *BLANK    Q9PTFR
     C                     Z-ADD*ZERO     Q9EVTD
     C                     MOVE WWEV60    WWNV60  50
     C*                                                    ***************
     C                     ENDSR                           * #INIT ENDSR *
     C*                                                    ***************
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P03     -  DETERMINE MONTH END DATES                            *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZDATE2   -  CONVERT DAY NUMBER TO DATE                           *
      * ZDATE1   -  CONVERT DATE TO DAY NUMBER                           *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #INIT    -  INITIALIZE - ACCESS STANDING DATA                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWMONF   -  MONTH - DATE                                         *
      * WWDDAY   -  MONTH - DAY NUMBER                                   *
      * WWWORK   -  DATE WORK FIELD                                      *
      * WWMMON   -  MONTH - MONTH NUMBER                                 *
      * WWYYEA   -  MONTH - YEAR NUMBER                                  *
      * WWMONN   -  MONTH - DAY NUMBER                                   *
      *                                                                  *
      ********************************************************************
     C           #P03      BEGSR                           * #P03 BEGSR *
     C*                                                    **************
     C**  CONVERT DATE OF NEXT WORKING DAY FROM DAY NUMBER TO DATE FORMAT;
     C**  STORE THIS CONVERT DATE AS A WORK DATE:
     C                     Z-ADDBJDNWD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     WWMONF
     C*
     C**  SET DAY NUMBER OF WORK DATE TO 1:
     C                     Z-ADD1         WWDDAY
     C*
     C**  SET MONTH END ARRAY INDEX COUNT TO 1:
     C                     Z-ADD1         X       20
     C*
     C****SO*LONG*AS*X*NOT*EQUAL*TO*12:                                   R01163
     C********** 1 ******* DO   12                         - B1           R01163
     C**  AS LONG AS X NOT EQUAL TO 13:                                   R01163
     C           1         DO   13                         - B1           R01163
     C*
     C**  SET MONTH NUMBER OF WORK DATE TO MONTH NUMBER OF
     C**  WORK DATE PLUS 1:
     C                     ADD  1         WWMMON
     C*
     C**  IF MONTH NUMBER OF WORK DATE GREATER THAN 12, SET IT TO 1:
     C           WWMMON    IFGT 12                         - B2
     C                     Z-ADD1         WWMMON
     C*
     C**  SET YEAR NUMBER OF WORK DATE TO YEAR NUMBER OF
     C**  WORK DATE PLUS 1:
     C                     ADD  1         WWYYEA
     C                     END                             - E2
     C*
     C**  CONVERT WORK DATE FROM DATE FORMAT TO DAY NUMBER:
     C                     MOVE WWMONF    ZDATE
     C                     EXSR ZDATE1
     C*
     C**  SUBSTRACT 1 FROM THIS DAY NUMBER TO GIVE A MONTH END DATE AND
     C**  MOVE THIS MONTH END DATE INTO THE MONTH END ARRAY:
     C                     Z-ADDZDAYNO    WWMONN  50
     C                     SUB  1         WWMONN
     C                     Z-ADDWWMONN    MONT,X
     C*
     C                     ADD  1         X
     C                     END                             - E1
     C*                                                    **************
     C                     ENDSR                           * #P03 ENDSR *
     C*                                                    **************
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P04     -  DETERMINE YEAR END DATES                             *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZDATE2   -  CONVERT DAY NUMBER TO DATE                           *
      * ZDATE1   -  CONVERT DATE TO DAY NUMBER                           *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #INIT    -  INITIALIZE - ACCESS STANDING DATA                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWYEAF   -  YEAR - DATE                                          *
      * WWYYEB   -  YEAR - YEAR NUMBER                                   *
      * WWDDAZ   -  YEAR - DAY NUMBER                                    *
      * WWYEAN   -  YEAR - DAY NUMBER                                    *
      *                                                                  *
      ********************************************************************
     C           #P04      BEGSR                           * #P04 BEGSR *
     C*
     C** CALCULATE MIDAS DAY NUMBER OF LAST DAY IN CURRENT YEAR BY
     C** 1. CONVERTING THE RUN DATE INTO DDMMYY FORMAT VIA SR/ZDATE1
     C** 2. SETTING THE DDMM PART OF THE RUN DATE TO 3112
     C** 3. CONVERTING THE DATE OBTAINED IN (2) TO MIDAS FORMAT VIA SR/ZDATE2
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     MOVE '0'       *IN98
     C                     EXSR ZDATE2
     C*
     C                     MOVE ZDATE     WWDAT1  6
     C                     MOVEL'3112'    WWDAT1
     C*
     C                     MOVE WWDAT1    ZDATE
     C                     EXSR ZDATE1
     C*
     C**  MOVE END OF YEAR DATE INTO THE FIRST ELEMENT OF THE YEAR
     C**  END ARRAY:
     C                     Z-ADDZDAYNO    YEAR,1
     C*
     C**  CONVERT END OF YEAR DATE FROM DAY NUMBER TO DATE FORMAT;
     C**  STORE THIS CONVERT DATE AS YEAR END DATE:
     C                     Z-ADDYEAR,1    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     WWYEAF
     C*
     C**  SET YEAR END ARRAY INDEX COUNT TO 2:
     C                     Z-ADD2         Y       30
     C*
     C**  SO LONG AS Y NOT EQUAL TO 50:
     C           2         DO   50                         - B1
     C*
     C**  SET YEAR NUMBER IN YEAR END DATE TO YEAR NUMBER IN
     C**  YEAR END DATE PLUS 1:
     C                     ADD  1         WWYYEB
     C*
     C**  CONVERT YEAR END DATE FROM DATE FORMAT TO DAY NUMBER;
     C**  STORE THIS CONVERT DATE AS YEAR END DATE:
     C                     MOVE WWYEAF    ZDATE
     C                     EXSR ZDATE1
     C*
     C**  IF YEAR END DATE IS INVALID, IT MUST BE 29TH FEBRUARY IN A
     C**  NON-LEAP YEAR:
     C           *IN99     IFEQ '1'                        - B2
     C*
     C**  SET DAY NUMBER OF YEAR END DATE TO DAY NUMBER
     C**  OF YEAR END DATE MINUS 1:
     C                     SUB  1         WWDDAZ
     C*
     C**  CONVERT YEAR END DATE FROM DATE FORMAT TO DAY NUMBER:
     C                     MOVE WWYEAF    ZDATE
     C                     EXSR ZDATE1
     C*
     C**  MOVE THIS YEAR END DATE INTO THE YEAR END ARRAY:
     C                     Z-ADDZDAYNO    WWYEAN  50
     C                     Z-ADDWWYEAN    YEAR,Y
     C*
     C**  SET DAY NUMBER OF YEAR END DATE TO DAY NUMBER OF YEAR END DATE
     C**  PLUS 1:
     C                     ADD  1         WWDDAZ
     C                     ELSE                            - X2
     C*
     C**  OTHERWISE, IF YEAR END DATE IS VALID,
     C**  MOVE THE YEAR END DATE INTO THE YEAR END ARRAY:
     C                     Z-ADDZDAYNO    WWYEAN
     C                     Z-ADDWWYEAN    YEAR,Y
     C                     END                             - E2
     C*
     C                     ADD  1         Y
     C                     END                             - E1
     C*                                                    **************
     C                     ENDSR                           * #P04 ENDSR *
     C*                                                    **************
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * *PSSR    -  DATABASE ERROR                                       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #INIT    -  INITIALIZE - ACCESS STANDING DATA                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWERRO   -  ERROR INDICATOR                                      *
      *                                                                  *
      ********************************************************************
     C           *PSSR     BEGSR                           * *PSSR BEGSR *
     C*                                                    ***************
     C           WWERRO    IFEQ ' '                        - B1
     C                     MOVE 'Y'       WWERRO  1
     C*
     C           DBASE     IFNE 0                          - B2
     C*
     C**  PRINT ERROR MESSAGE:
     C                     WRITEHEADAU
     C                     WRITEERRORAU
     C***********          WRITEENDAU                                     S01486
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     ELSE                            - X2
     C*
     C                     Z-ADD999       DBASE
     C                     END                             - E2
     C*
     C**  ROLL BACK CHANGES, PRINT DUMP AND CANCEL PROGRAM:
     C                     DUMP
     C                     END                             - E1
     C*
     C                     MOVE '1'       *INLR
     C*
     C                     RETRN
     C*                                                    ***************
     C                     ENDSR                           * *PSSR ENDSR *
     C*                                                    ***************
     C*
     C**=================================================================
     C/EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**  ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**  ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**  ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  POWER - ARRAY OF POWERS FOR CURRENCY CONVERSION
0000001
0000010
0000100
0001000
0010000
0100000
1000000
