     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Consolidated cashflow totals update')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management                             *
     F*                                                               *
     F*  PM1465 - CONSOLIDATED CASHFLOW TOTALS UPDATE                 *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 CPB001             Date 21Feb00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*                 S01486             Date 19AUG94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  CPB001 -  'Private Banking' Module                           *
     F*  S01486 - Private Banking upgrade to Release 10               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FPM1465AUO   E                    PRINTER
     F**
     F***SDBANKL1IF**E           K        DISK         KINFSR *PSSR       S01486
     F*******PREFIX*BJ****************************************************S01486
     F***PMCFDYZZUF  E                    DISK         KINFSR *PSSR       S01486
     FPMCFDYPAUF  E                    DISK         KINFSR *PSSR          S01486
     F*      PREFIX QK
     F***PMCFMYZZUF**E                    DISK         KINFSR *PSSR       S01486
     FPMCFMYPAUF  E                    DISK         KINFSR *PSSR          S01486
     F*      PREFIX QL                                                    S01486
     F**
     F***SDCURRL0IF**E           K        DISK         KINFSR *PSSR       S01486
     F*******PREFIX*A6****************************************************S01486
     F***PMCFDYPPO***E           K        DISK         KINFSR *PSSR A     S01486
     FPMCFDYPDO   E           K        DISK         KINFSR *PSSR A        S01486
     F*      PREFIX QK
     F***PMCFMYPPO***E           K        DISK         KINFSR *PSSR A     S01486
     FPMCFMYPDO   E           K        DISK         KINFSR *PSSR A        S01486
     F*      PREFIX QL
     F**
     FPMCDYDLLIF  E           K        DISK         KINFSR *PSSR
     F            PMCFDYP0                          KRENAMEPMCFDYPX
     F*      PREFIX QK
     FPMCMYDLLIF  E           K        DISK         KINFSR *PSSR
     F            PMCFMYP0                          KRENAMEPMCFMYPX
     F*      PREFIX QL
     F*==================================================================
     F*                 FUNCTION OF INDICATORS
     F*
     F****71******-*ACCESS*SDBANKL1***************************************S01486
     F**  72      - ACCESS PMCDYDLL
     F**  73      - ACCESS PMCMYDLL
     F****74******-*ACCESS*PMCFDYZZ***************************************S01486
     F****75******-*ACCESS*PMCFMYZZ***************************************S01486
     F**  74      - ACCESS PMCFDYPA                                       S01486
     F**  75      - ACCESS PMCFMYPA                                       S01486
     F*
     F*
     F**  U8      - STANDARD RUN CONTROL ERROR
     F**  U7 U8   - STANDARD DATABASE ERROR
     F*
     F**  LR      - LAST RECORD ON FILE
     F*
     F**                 *************************
     F**                 ** INDICATORS NOT USED **
     F**                 *************************
     F*
     F**      ***************************************************
     F**      * 01   02   03   04   05   06   07   08   09   10 *
     F**      * 11   12   13   14   15   16   17   18   19   20 *
     F**      * 21   22   23   24   25   26   27   28   29   30 *
     F**      * 31   32   33   34   35   36   37   38   39   40 *
     F**      * 41   42   43   44   45   46   47   48   49   50 *
     F**      * 51   52   53   54   55   56   57   58   59   60 *
     F**      * 61   62   63   64   65   66   67   68   69   70 *
     F**      * 71   --   --   --   --   76   77   78   79   80 *
     F**      * 81   82   83   84   85   86   87   88   89   90 *
     F**      * 91   92   93   94   95   96   97   98   99      *
     F**      ***************************************************
     F*
     F*==================================================================
     F/EJECT
     E*==================================================================
     E*
     E                    CPY@    1   1 80
     E*
     E*==================================================================
     E/EJECT
     I*==================================================================
     IPMCFDYPX
     I              QKCNUM                          IKCNUM
     I              QKPTFR                          IKPTFR
     I              QKDYNO                          IKDYNO
     I              QKCCY                           IKCCY
     I              QKCDP                           IKCDP
     I              QKICAS                          IKICAS
     I              QKOCAS                          IKOCAS
     I              QKNECS                          IKNECS
     I              QKNEPT                          IKNEPT
     I              QKNECT                          IKNECT
     I              QKNEBS                          IKNEBS
     IPMCFMYPX
     I              QLCNUM                          ILCNUM
     I              QLPTFR                          ILPTFR
     I              QLMYNO                          ILMYNO
     I              QLMYIN                          ILMYIN
     I              QLCCY                           ILCCY
     I              QLCDP                           ILCDP
     I              QLICAS                          ILICAS
     I              QLOCAS                          ILOCAS
     I              QLNECS                          ILNECS
     I              QLNEPT                          ILNEPT
     I              QLNECT                          ILNECT
     I              QLNEBS                          ILNEBS
     I*
     I**  LOCAL DATA AREA FOR DATA BASE ERRORS:
     I***LDA********UDS                            256                    S01486
     ILDA         DS                            256                       S01486
     I                                      132 183 DBLOT
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I**  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION:
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     ISDBANK    E DSSDBANKPD                                              S01486
     I** DUMMY RECORD FORMAT FOR BANK DETAILS                             S01486
     I*                                                                   S01486
     ISDCURR    E DSSDCURRPD                                              S01486
     I** DUMMY RECORD FORMAT FOR CURRENCY DETAILS                         S01486
     I*                                                                   S01486
     ISDMMOD    E DSSDMMODPD                                              CPB001
     I** DUMMY RECORD FORMAT FOR MODULE DETAILS                           CPB001
     I*                                                                   CPB001
     IDSFDY     E DSDSFDY                                                 S01486
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01486
     I*                                                                   S01486
     IDSSDY     E DSDSSDY                                                 S01486
     I* SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE                S01486
     I*                                                                   S01486
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INDEX OF SUBROUTINES                                             *
      * MAIN     -  CHIEF PROGRAM                                        *
      * #INIT    -  INITIALIZE - ACCESS STANDING DATA                    *
      * #P01     -  PROCESS PORTFOLIO CASHFLOW DAY TOTALS                *
      * #P04     -  UPDATE PORTFOLIO CASHFLOW DAY TOTALS                 *
      * #P02     -  PROCESS PORTFOLIO CASHFLOW MONTH/YEAR TOTALS         *
      * #P05     -  UPDATE PORTFOLIO CASHFLOW MONTH/YEAR END TOTALS      *
      * #P03     -  PROCESS AUDIT                                        *
      * *PSSR    -  DATABASE ERROR                                       *
      *                                                                  *
      * EXTERNAL ROUTINES CALLED                                         *
      *                                                                  *
      ********************************************************************
     C*            MAIN PROGRAM  START
     C**================================================================
     C*
     C**  INITIALIZATION:
     C                     EXSR #INIT
     C*
     C**  PROCESS PORTFOLIO CASHFLOW DAY TOTALS:
     C                     EXSR #P01
     C*
     C**  PROCESS PORTFOLIO CASHFLOW MONTH/YEAR TOTALS:
     C                     EXSR #P02
     C*
     C**  PROCESS AUDIT:
     C                     EXSR #P03
     C*
     C**  END OF PROGRAM:
     C                     MOVE '1'       *INLR
     C**================================================================
     C*            MAIN PROGRAM END
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P01     -  PROCESS PORTFOLIO CASHFLOW DAY TOTALS                *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * #P04     -  UPDATE PORTFOLIO CASHFLOW DAY TOTALS                 *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -  CHIEF PROGRAM                                        *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWCNUM   -  CUSTOMER NUMBER                                      *
      * WWDYNO   -  DAY NUMBER                                           *
      * WW98CC   -  9998 DAY TOTAL                                       *
      * WW99CC   -  9999 DAY TOTAL                                       *
      *                                                                  *
      ********************************************************************
     C           #P01      BEGSR                           * #P01 BEGSR *
     C*
     C                     MOVE '0'       *IN72
     C**********           Z-ADD*ZEROS    WWCNUM                                              CSD027
     C                     MOVE *BLANKS   WWCNUM                                              CSD027
     C*
     C**  READ PORTFOLIO CASHFLOW DAY TOTALS
     C           *LOVAL    SETLLPMCDYDLL
     C                     READ PMCDYDLL                 72
     C*
     C**  PERFORM PROCESSING UNTIL END OF FILE REACHED
     C           *IN72     DOWEQ'0'
     C*
     C**  IF CUSTOMER NUMBER OR DAY NUMBER ON THIS RECORD IS
     C**     DIFFERENT FROM THE PREVIOUS RECORD, AND IT IS NOT
     C**     THE FIRST RECORD READ
     C**     UPDATE PORTFOLIO CASHFLOW DAY TOTAL
     C********** WWCNUM    IFNE *ZEROS                                                        CSD027
     C           WWCNUM    IFNE *BLANKS                                                       CSD027
     C           WWCNUM    CASNEIKCNUM    #P04
     C           WWDYNO    CASNEIKDYNO    #P04
     C           WWCCY     CASNEIKCCY     #P04
     C                     END
     C                     END
     C*
     C**  SAVE PREVIOUS CUSTOMER NUMBER
     C           WWCNUM    IFNE IKCNUM
     C**********           Z-ADDIKCNUM    WWCNUM                                              CSD027
     C                     MOVE IKCNUM    WWCNUM                                              CSD027
     C                     END
     C*
     C**  SAVE PREVIOUS DAY NUMBER
     C           WWDYNO    IFNE IKDYNO
     C                     Z-ADDIKDYNO    WWDYNO
     C                     END
     C*
     C**  SAVE PREVIOUS CURRENCY CODE
     C           WWCCY     IFNE IKCCY
     C*
     C***********IKCCY     CHAINSDCURRL0             72                   S01486
     C                     CALL 'AOCURRR0'                                S01486
     C                     PARM *BLANKS   RTCD                            S01486
     C                     PARM '*KEY'    OPTN                            S01486
     C                     PARM IKCCY     KEYCY   3                       S01486
     C           SDCURR    PARM SDCURR    DSSDY                           S01486
     C*
     C************IN72     IFEQ '1'                                       S01486
     C           RTCD      IFNE *BLANKS                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     Z-ADD4         DBASE            ***************
     C                     MOVEL'SDCURRL0'DBFILE           * DB ERROR 05 *
     C                     MOVELIKCCY     DBKEY                           S01486
     C                     OUT  LDA                                       S01486
     C                     EXSR *PSSR                      ***************
     C                     END
     C*
     C                     MOVE IKCCY     WWCCY
     C                     Z-ADDA6NBDP    WWCDP
     C*
     C                     END
     C*
     C**  IF PORTFOLIO REFERENCE IS NOT '9997', INCREMENT 9998 TOTAL
     C           IKPTFR    IFNE '9997'
     C                     ADD  IKICAS    WW98IC
     C                     ADD  IKOCAS    WW98OC
     C                     ADD  IKNECS    WW98NC
     C                     ADD  IKNECT    WW98NP
     C                     ADD  IKNEBS    WW98NB
     C                     END
     C*
     C**  ADD AMOUNTS TO 9999 DAY TOTALS
     C           BGN4ST    IFNE 'Y'                                       CPB001
     C                     ADD  IKICAS    WW99IC
     C                     ADD  IKOCAS    WW99OC
     C                     ADD  IKNECS    WW99NC
     C                     ADD  IKNECT    WW99NP
     C                     ADD  IKNEBS    WW99NB
     C                     ENDIF                                          CPB001
     C*
     C**  READ PORTFOLIO CASHFLOW DAY TOTALS
     C                     READ PMCDYDLL                 72
     C                     END
     C*
     C**  PROCESS LAST RECORD READ
     C           *IN72     IFEQ '1'
     C********** WWCNUM    ANDNE*ZEROS                                                        CSD027
     C           WWCNUM    ANDNE*BLANKS                                                       CSD027
     C                     EXSR #P04
     C                     END
     C*                                                    **************
     C                     ENDSR                           * #P01 ENDSR *
     C*                                                    **************
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P04     -  UPDATE PORTFOLIO CASHFLOW DAY TOTALS                 *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P01     -  PROCESS PORTFOLIO CASHFLOW DAY TOTALS                *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWCNUM   -  CUSTOMER NUMBER                                      *
      * WWDYNO   -  DAY NUMBER                                           *
      * WW98CC   -  9998 DAY TOTAL                                       *
      * WW99CC   -  9999 DAY TOTAL                                       *
      **WWDAYT***-**RECORDS*WRITTEN*TO*PMCFDYPP***************************S01486
      * WWDAYT   -  RECORDS WRITTEN TO PMCFDYPD                          *S01486
      *                                                                  *
      ********************************************************************
     C           #P04      BEGSR                           * #P04 BEGSR *
     C*                                                    **************
     C**  IF 9998 DAY TOTAL IS NOT EQUAL TO ZERO:
     C**  1) WRITE '9998' RECORD TO PORTFOLIO CASHFLOW DAY TOTALS;
     C**  2) ADD 1 TO A COUNT OF DAY TOTALS RECORDS:
     C           WW98IC    IFNE 0                          - B1
     C           WW98OC    ORNE 0
     C*
     C**********           Z-ADDWWCNUM    QKCNUM                                              CSD027
     C                     MOVE WWCNUM    QKCNUM                                              CSD027
     C                     Z-ADDWWDYNO    QKDYNO
     C                     MOVE '9998'    QKPTFR
     C                     MOVE WWCCY     QKCCY
     C                     Z-ADDWWCDP     QKCDP
     C                     Z-ADDWW98IC    QKICAS
     C                     Z-ADDWW98OC    QKOCAS
     C                     Z-ADDWW98NC    QKNECS
     C                     Z-ADDWW98NP    QKNEPT
     C                     Z-ADDWW98NP    QKNECT
     C                     Z-ADDWW98NB    QKNEBS
     C                     WRITEPMCFDYP0
     C*
     C                     ADD  1         WWDAYT  50
     C                     END                             - E1
     C*
     C**  WRITE '9999' RECORD TO PORTFOLIO CASHFLOW DAY TOTALS;
     C**  ADD 1 TO A COUNT OF DAY TOTALS RECORDS:
     C           BGN4ST    IFNE 'Y'                                       CPB001
     C**********           Z-ADDWWCNUM    QKCNUM                                              CSD027
     C                     MOVE WWCNUM    QKCNUM                                              CSD027
     C                     Z-ADDWWDYNO    QKDYNO
     C                     MOVE '9999'    QKPTFR
     C                     MOVE WWCCY     QKCCY
     C                     Z-ADDWWCDP     QKCDP
     C                     Z-ADDWW99IC    QKICAS
     C                     Z-ADDWW99OC    QKOCAS
     C                     Z-ADDWW99NC    QKNECS
     C                     Z-ADDWW99NP    QKNEPT
     C                     Z-ADDWW99NP    QKNECT
     C                     Z-ADDWW99NB    QKNEBS
     C                     WRITEPMCFDYP0
     C*
     C                     ADD  1         WWDAYT
     C                     ENDIF                                          CPB001
     C*
     C**  SET 9998 & 9999 DAY TOTALS EQUAL TO ZERO:
     C*
     C                     Z-ADD*ZEROS    WW98IC
     C                     Z-ADD*ZEROS    WW98OC
     C                     Z-ADD*ZEROS    WW98NC
     C                     Z-ADD*ZEROS    WW98NP
     C                     Z-ADD*ZEROS    WW98NB
     C*
     C           BGN4ST    IFNE 'Y'                                       CPB001
     C                     Z-ADD*ZEROS    WW99IC
     C                     Z-ADD*ZEROS    WW99OC
     C                     Z-ADD*ZEROS    WW99NC
     C                     Z-ADD*ZEROS    WW99NP
     C                     Z-ADD*ZEROS    WW99NB
     C                     ENDIF                                          CPB001
     C*                                                    **************
     C                     ENDSR                           * #P04 ENDSR *
     C*                                                    **************
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P02     -  PROCESS PORTFOLIO CASHFLOW MONTH/YEAR TOTALS         *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * #P05     -  UPDATE PORTFOLIO CASHFLOW MONTH/YEAR END TOTALS      *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -  CHIEF PROGRAM                                        *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWCNUM   -  CUSTOMER NUMBER                                      *
      * WWMYNO   -  MONTH/YEAR END DAY NUMBER                            *
      * WWMYIN   -  MONTH/YEAR INDICATOR                                 *
      * WW98CI   -  9998 CUSTOMER INCOMING TOTAL                         *
      * WW98CO   -  9998 CUSTOMER OUTGOING TOTAL                         *
      * WW98BI   -  9998 BASE INCOMING TOTAL                             *
      * WW98BO   -  9998 BASE OUTGOING TOTAL                             *
      * WW99CI   -  9999 CUSTOMER INCOMING TOTAL                         *
      * WW99CO   -  9999 CUSTOMER OUTGOING TOTAL                         *
      * WW99BI   -  9999 BASE INCOMING TOTAL                             *
      * WW99BO   -  9999 BASE OUTGOING TOTAL                             *
      *                                                                  *
      ********************************************************************
     C           #P02      BEGSR                           * #P02 BEGSR *
     C*
     C                     MOVE '0'       *IN73
     C**********           Z-ADD*ZEROS    WWCNUM                                              CSD027
     C                     MOVE *BLANKS   WWCNUM                                              CSD027
     C*
     C**  READ PORTFOLIO CASHFLOW MONTH AND YEAR END TOTALS
     C           *LOVAL    SETLLPMCMYDLL
     C                     READ PMCMYDLL                 73
     C*
     C**  PERFORM PROCESSING UNTIL END OF FILE REACHED
     C           *IN73     DOWEQ'0'
     C*
     C**  IF CUSTOMER NUMBER, MONTH YEAR END DAY OR INDICATOR
     C**     DIFFERENT FROM THE PREVIOUS RECORD, AND IT IS NOT
     C**     THE FIRST RECORD READ
     C**     UPDATE PORTFOLIO CASHFLOW TOTAL
     C********** WWCNUM    IFNE *ZEROS                                                        CSD027
     C           WWCNUM    IFNE *BLANKS                                                       CSD027
     C           WWCNUM    CASNEILCNUM    #P05
     C           WWMYNO    CASNEILMYNO    #P05
     C           WWMYIN    CASNEILMYIN    #P05
     C           WWCCY     CASNEILCCY     #P05
     C                     END
     C                     END
     C*
     C**  SAVE PREVIOUS CUSTOMER NUMBER
     C           WWCNUM    IFNE ILCNUM
     C**********           Z-ADDILCNUM    WWCNUM                                              CSD027
     C                     MOVE ILCNUM    WWCNUM                                              CSD027
     C                     END
     C*
     C**  SAVE PREVIOUS MONTH/YEAR DAY NUMBER
     C           WWMYNO    IFNE ILMYNO
     C                     Z-ADDILMYNO    WWMYNO
     C                     END
     C*
     C**  SAVE PREVIOUS MONTH/YEAR INDICATOR
     C           WWMYIN    IFNE ILMYIN
     C                     MOVE ILMYIN    WWMYIN
     C                     END
     C*
     C**  SAVE PREVIOUS CURRENCY CODE
     C           WWCCY     IFNE ILCCY
     C*
     C***********ILCCY     CHAINSDCURRL0             73                   S01486
     C                     CALL 'AOCURRR0'                                S01486
     C                     PARM *BLANKS   RTCD                            S01486
     C                     PARM '*KEY'    OPTN                            S01486
     C                     PARM ILCCY     KEYCY   3                       S01486
     C           SDCURR    PARM SDCURR    DSSDY                           S01486
     C*
     C************IN73     IFEQ '1'                                       S01486
     C           RTCD      IFNE *BLANKS                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     Z-ADD5         DBASE            ***************
     C                     MOVEL'SDCURRL0'DBFILE           * DB ERROR 05 *
     C                     MOVELILCCY     DBKEY                           S01486
     C                     OUT  LDA                                       S01486
     C                     EXSR *PSSR                      ***************
     C                     END
     C*
     C                     MOVE ILCCY     WWCCY
     C                     Z-ADDA6NBDP    WWCDP
     C                     END
     C*
     C**  IF PORTFOLIO REFERENCE IS NOT '9997',
     C**  1) ADD INCOMING/OUTGOING CUSTOMER CURRENCY TO 9998 CUSTOMER
     C**     INCOMING/OUTGOING TOTALS;
     C**  2) ADD INCOMING/OUTGOING BASE CURRENCY TO 9998 BASE
     C**     INCOMING/OUTGOING TOTALS:
     C           ILPTFR    IFNE '9997'
     C                     ADD  ILICAS    WW98IC
     C                     ADD  ILOCAS    WW98OC
     C                     ADD  ILNECS    WW98NC
     C                     ADD  ILNECT    WW98NP
     C                     ADD  ILNEBS    WW98NB
     C                     END
     C*
     C**  ADD INCOMING/OUTGOING CUSTOMER CURRENCY TO 9999 CUSTOMER
     C**     INCOMING/OUTGOING TOTALS;
     C**  ADD INCOMING/OUTGOING BASE CURRENCY TO 9999 BASE
     C**     INCOMING/OUTGOING TOTALS:
     C           BGN4ST    IFNE 'Y'                                       CPB001
     C                     ADD  ILICAS    WW99IC
     C                     ADD  ILOCAS    WW99OC
     C                     ADD  ILNECS    WW99NC
     C                     ADD  ILNECT    WW99NP
     C                     ADD  ILNEBS    WW99NB
     C                     ENDIF                                          CPB001
     C*
     C**  READ PORTFOLIO CASHFLOW MONTH AND YEAR END
     C                     READ PMCMYDLL                 73
     C                     END
     C*
     C**  PROCESS LAST RECORD READ
     C           *IN73     IFEQ '1'
     C********** WWCNUM    ANDNE*ZEROS                                                        CSD027
     C           WWCNUM    ANDNE*BLANKS                                                       CSD027
     C                     EXSR #P05
     C                     END
     C*                                                    **************
     C                     ENDSR                           * #P02 ENDSR *
     C*                                                    **************
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P05     -  UPDATE PORTFOLIO CASHFLOW MONTH/YEAR END TOTALS      *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #P02     -  PROCESS PORTFOLIO CASHFLOW MONTH/YEAR TOTALS         *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWCNUM   -  CUSTOMER NUMBER                                      *
      * WWMYNO   -  MONTH/YEAR END DAY NUMBER                            *
      * WWMYIN   -  MONTH/YEAR INDICATOR                                 *
      * WW98CI   -  9998 CUSTOMER INCOMING TOTAL                         *
      * WW98CO   -  9998 CUSTOMER OUTGOING TOTAL                         *
      * WW98BI   -  9998 BASE INCOMING TOTAL                             *
      * WW98BO   -  9998 BASE OUTGOING TOTAL                             *
      * WW99CI   -  9999 CUSTOMER INCOMING TOTAL                         *
      * WW99CO   -  9999 CUSTOMER OUTGOING TOTAL                         *
      * WW99BI   -  9999 BASE INCOMING TOTAL                             *
      * WW99BO   -  9999 BASE OUTGOING TOTAL                             *
      **WWMOYT***-**RECORDS*WRITTEN*TO*PMCFMYPP***************************S01486
      * WWMOYT   -  RECORDS WRITTEN TO PMCFMYPD                          *S01486
      *                                                                  *
      ********************************************************************
     C           #P05      BEGSR                           * #P05 BEGSR *
     C*                                                    **************
     C**  IF 9998 CUSTOMER INCOMING/OUTGOING TOTALS ARE NOT EQUAL TO ZERO;
     C**  1) WRITE '9998' RECORD TO PORTFOLIO CASHFLOW MONTH & YEAR END
     C**     TOTALS;
     C**  2) ADD 1 TO A COUNT OF MONTH/YEAR END TOTALS RECORDS:
     C           WW98IC    IFNE 0                          - B1
     C           WW98OC    ORNE 0
     C*
     C**********           Z-ADDWWCNUM    QLCNUM                                              CSD027
     C                     MOVE WWCNUM    QLCNUM                                              CSD027
     C                     Z-ADDWWMYNO    QLMYNO
     C                     MOVE WWMYIN    QLMYIN
     C                     MOVE '9998'    QLPTFR
     C                     MOVE WWCCY     QLCCY
     C                     Z-ADDWWCDP     QLCDP
     C                     Z-ADDWW98IC    QLICAS
     C                     Z-ADDWW98OC    QLOCAS
     C                     Z-ADDWW98NC    QLNECS
     C                     Z-ADDWW98NP    QLNEPT
     C                     Z-ADDWW98NP    QLNECT
     C                     Z-ADDWW98NB    QLNEBS
     C                     WRITEPMCFMYP0
     C*
     C                     ADD  1         WWMOYT  50
     C                     END                             - E1
     C*
     C**  WRITE '9999' RECORD TO PORTFOLIO CASHFLOW MONTH & YEAR END
     C**  TOTALS;
     C**  ADD 1 TO A COUNT OF MONTH & YEAR END TOTALS RECORDS:
     C           BGN4ST    IFNE 'Y'                                       CPB001
     C**********           Z-ADDWWCNUM    QLCNUM                                              CSD027
     C                     MOVE WWCNUM    QLCNUM                                              CSD027
     C                     Z-ADDWWMYNO    QLMYNO
     C                     MOVE WWMYIN    QLMYIN
     C                     MOVE '9999'    QLPTFR
     C                     MOVE WWCCY     QLCCY
     C                     Z-ADDWWCDP     QLCDP
     C                     Z-ADDWW99IC    QLICAS
     C                     Z-ADDWW99OC    QLOCAS
     C                     Z-ADDWW99NC    QLNECS
     C                     Z-ADDWW99NP    QLNEPT
     C                     Z-ADDWW99NP    QLNECT
     C                     Z-ADDWW99NB    QLNEBS
     C                     WRITEPMCFMYP0
     C*
     C                     ADD  1         WWMOYT
     C                     ENDIF                                          CPB001
     C*
     C**  SET 9998 & 9999 CUSTOMER & BASE INCOMING/OUTGOING TOTALS TO
     C**  ZERO:
     C                     Z-ADD*ZEROS    WW98IC
     C                     Z-ADD*ZEROS    WW98OC
     C                     Z-ADD*ZEROS    WW98NC
     C                     Z-ADD*ZEROS    WW98NP
     C                     Z-ADD*ZEROS    WW98NB
     C           BGN4ST    IFNE 'Y'                                       CPB001
     C                     Z-ADD*ZEROS    WW99IC
     C                     Z-ADD*ZEROS    WW99OC
     C                     Z-ADD*ZEROS    WW99NC
     C                     Z-ADD*ZEROS    WW99NP
     C                     Z-ADD*ZEROS    WW99NB
     C                     ENDIF                                          CPB001
     C*                                                    **************
     C                     ENDSR                           * #P05 ENDSR *
     C*                                                    **************
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P03     -  PROCESS AUDIT                                        *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * *PSSR    -  DATABASE ERROR                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -  CHIEF PROGRAM                                        *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      **WWDAYT***-**RECORDS*WRITTEN*TO*PMCFDYPP***************************S01486
      **WWMOYT***-**RECORDS*WRITTEN*TO*PMCFMYPP***************************S01486
      * WWDAYT   -  RECORDS WRITTEN TO PMCFDYPD                          *S01486
      * WWMOYT   -  RECORDS WRITTEN TO PMCFMYPD                          *S01486
      *                                                                  *
      ********************************************************************
     C           #P03      BEGSR                           * #P03 BEGSR *
     C*                                                    **************
     C**  ACCESS PORTFOLIO CASHFLOW DAY TOTALS AUDIT:
     C***********          READ PMCFDYZZ                 74               S01486
     C                     READ PMCFDYPA                 74               S01486
     C*
     C           *IN74     IFEQ '1'                        - B1
     C                     Z-ADD2         DBASE            ***************
     C***********          MOVEL'PMCFDYZZ'DBFILE           * DB ERROR 02 *S01486
     C                     MOVEL'PMCFDYPA'DBFILE           * DB ERROR 02 *S01486
     C                     MOVEL' NONE '  DBKEY            ***************
     C                     EXSR *PSSR
     C                     END                             - E1
     C*
     C**  ADD COUNT OF DAY TOTALS RECORDS TO NUMBER OF LIVE RECORDS;
     C**  UPDATE PORTFOLIO CASHFLOW DAY TOTALS AUDIT:
     C                     ADD  WWDAYT    QKNORE
     C                     UPDATPMCFDYZ0
     C*
     C**  ACCESS PORTFOLIO CASHFLOW MONTH & YEAR END TOTALS AUDIT:
     C***********          READ PMCFMYZZ                 75               S01486
     C                     READ PMCFMYPA                 75               S01486
     C*
     C           *IN75     IFEQ '1'                        - B1
     C                     Z-ADD3         DBASE            ***************
     C***********          MOVEL'PMCFMYZZ'DBFILE           * DB ERROR 03 *S01486
     C                     MOVEL'PMCFMYPA'DBFILE           * DB ERROR 03 *S01486
     C                     MOVEL' NONE '  DBKEY            ***************
     C                     EXSR *PSSR
     C                     END                             - E1
     C*
     C**  ADD COUNT OF MONTH/YEAR END TOTALS RECORDS TO NUMBER OF LIVE
     C**  RECRDS;
     C**  UPDATE PORTFOLIO CASHFLOW MONTH & YEAR END TOTALS AUDIT:
     C                     ADD  WWMOYT    QLNORE
     C                     UPDATPMCFMYZ0
     C*
     C**  WRITE TO CONSOLIDATED CASH FLOW TOTALS UPDATE AUDIT REPORT:
     C                     Z-ADDWWDAYT    RRDAYT
     C                     Z-ADDWWMOYT    RRMOYT
     C*
     C                     WRITEHEADAU
     C                     WRITEAUDIT
     C***********          WRITEENDAU                                     S01486
     C*                                                    **************
     C                     ENDSR                           * #P03 ENDSR *
     C*                                                    **************
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #INIT    -  INITIALIZE - ACCESS STANDING DATA                    *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * *PSSR    -  DATABASE ERROR                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -  CHIEF PROGRAM                                        *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           #INIT     BEGSR                           * #INIT BEGSR *
     C*                                                    ***************
     C**  DEFINE WORK FIELDS
     C           *LIKE     DEFN IKCNUM    WWCNUM
     C           *LIKE     DEFN IKCCY     WWCCY
     C           *LIKE     DEFN IKCDP     WWCDP
     C           *LIKE     DEFN IKDYNO    WWDYNO
     C           *LIKE     DEFN ILMYNO    WWMYNO
     C           *LIKE     DEFN ILMYIN    WWMYIN
     C           *LIKE     DEFN IKICAS    WW98IC
     C           *LIKE     DEFN IKICAS    WW98OC
     C           *LIKE     DEFN IKICAS    WW98NC
     C           *LIKE     DEFN IKICAS    WW98NP
     C           *LIKE     DEFN IKICAS    WW98NB
     C           *LIKE     DEFN IKICAS    WW99IC
     C           *LIKE     DEFN IKICAS    WW99OC
     C           *LIKE     DEFN IKICAS    WW99NC
     C           *LIKE     DEFN IKICAS    WW99NP
     C           *LIKE     DEFN IKICAS    WW99NB
     C*
     C**  PREPARE LDA:
     C           *NAMVAR   DEFN           LDA                             S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'PM1465'  DBPGM
     C                     OUT  LDA                                       S01486
     C*
     C**  GET BANK DETAILS:
     C***********          READ SDBANKL1                 71               S01486
     C*
     C************IN71     IFEQ '1'                        - B1           S01486
     C***********          Z-ADD1         DBASE            ***************S01486
     C***********          MOVE 'SDBANKL1'DBFILE           **DB ERROR 01**S01486
     C***********          MOVEL' NONE '  DBKEY            ***************S01486
     C***********          EXSR *PSSR                      ***************S01486
     C***********          END                             - E1           S01486
     C                     CALL 'AOBANKR0'                                S01486
     C                     PARM '*DBERR'  RTCD    7                       S01486
     C                     PARM '*FIRST'  OPTN    7                       S01486
     C           SDBANK    PARM SDBANK    DSFDY                           S01486
     C*                                                                   CPB001
     C**   Access module details                                          CPB001
     C                     CALL 'AOMMODR0'                                CPB001
     C                     PARM *BLANKS   @RTCD   7                       CPB001
     C                     PARM '*FIRST ' @OPTN   7                       CPB001
     C           SDMMOD    PARM SDMMOD    DSFDY                           CPB001
     C***
     C****INITIALIZE FIELDS OF KLISTS:
     C*                                                    ***************
     C                     ENDSR                           * #INIT ENDSR *
     C*                                                    ***************
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * *PSSR    -  DATABASE ERROR                                       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #INIT    -  INITIALIZE - ACCESS STANDING DATA                    *
      * #P03     -  PROCESS AUDIT                                        *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWERRO   -  ERROR INDICATOR                                      *
      *                                                                  *
      ********************************************************************
     C           *PSSR     BEGSR                           * *PSSR BEGSR *
     C*                                                    ***************
     C           WWERRO    IFEQ ' '                        - B1
     C                     MOVE 'Y'       WWERRO  1
     C*
     C           DBASE     IFNE 0                          - B2
     C*
     C**  PRINT ERROR MESSAGE:
     C                     WRITEHEADAU
     C                     WRITEERRORAU
     C***********          WRITEENDAU                                     S01486
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     ELSE                            - X2
     C*
     C                     Z-ADD999       DBASE
     C                     END                             - E2
     C*
     C**  PRINT DUMP AND CANCEL PROGRAM:
     C                     DUMP
     C                     END                             - E1
     C*
     C                     MOVE '1'       *INLR
     C*
     C                     RETRN
     C*                                                    ***************
     C                     ENDSR                           * *PSSR ENDSR *
     C*                                                    ***************
     C*
     C**=================================================================
     C/EJECT
**  CPY@
(c) Finastra International Limited 2001
