     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Duplicate Start of Month Pos for Pool')       *
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management Module                          *
     F*                                                               *
     F*  PM1625 - Extract Non-SE and FF Positions for Pool            *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      *  Last Amend No. CLE148             Date 23Jul12               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 182961             Date 21Apr01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CFF004             Date 02Oct96               *
      *                 085862             Date 16Mar95               *
     F*                 S01486             DATE 11AUG94               *
     F*                 056460             DATE 07JUN93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CLE148 - Alpha Loan Reference                                *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus                                          *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  182961 - Database error occurred due to a blank key field    *
      *           (Instrument Type).  The default Instrument type in  *
      *           PF/SDPORTPD is taken if a blank instrument type is  *
      *           encountered.                                        *
     F*  CFF004 - Increase in size of Price Fields,(Recompiled Only)  *
     F*  085862 - RECOMPILED DUE TO CHANGE TO PF/PMPCAAPD             *
     F*   53103  - Additional field Reference/Shortname on PMPMIMPD   *
     F*            + Additional program parameter 'Run Type'          *
     F*            + New processing for Run mode 'M'                  *
     F*  S01486  -  Portfolio Management Upgrade to Release 10.       *
     F*  056460  -  SAVE DETAILS OF MATURE / FLAT POSITIONS           *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F**
     F**      FILE DEFINITIONS
     F**     ------------------
     F*
     F*****BANK*DETAILS*FILE************************-*Prefix*BJ********   S01486
     F***SDBANKL1IF**E           K        DISK         KINFSR *PSSR       S01486
     F*
     F**   FIRST POOL DETAILS                       - Prefix QP
     F***PMFPPLPPIF**E                    DISK         KINFSR *PSSR       S01486
     FPMFPPLQ0IF  E                    DISK         KINFSR *PSSR          S01486
     F*
     F**   POOL PORTFOLIO MEMBER DETAILS            - Prefix OS
     FPMPPTMPDIF  E           K        DISK         KINFSR *PSSR
     F*
     F**   PORTFOLIO HOLDINGS BY VALUATION GROUP    - Prefix QF
     FPMHDVNLLIF  E           K        DISK         KINFSR *PSSR
     F*
     F*****PERFORMANCE*MID*MONTH*POSITIONS*WORKFILE*-*Prefix*QF********   S01486
     F***PMPMIMPPO***E           K        DISK         KINFSR *PSSR A     S01486
     F**   PERFORMANCE MID MONTH POSITIONS WORKFILE - Prefix GE           S01486
     F***PMPMIMPDO***E           K        DISK         KINFSR *PSSRAS0148653103
     FPMPMIMPDUF  E           K        DISK         KINFSR *PSSR A        53103
     F*                                                                   53103
     F**   CASH EVENTS DUE TO FX DEALS              - Prefix QS           53103
     FPMPCAAL4IF  E           K        DISK         KINFSR *PSSR          53103
     FSDPORTPDIF  E                    DISK         KINFSR *PSSR          182961
     F*
     F**   AUDIT REPORT
     FPM1625AUO   E                    PRINTER      KINFSR *PSSR
     F*
     F/EJECT
     E*****************************************************************
     E*
     E**   ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
     E*****************************************************************
     E/EJECT
     E*****************************************************************
     I*
     I**   LOCAL DATA AREA
     I***LDA********UDS***********************     256                    S01486
     ILDA         DS                            256                       S01486
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I**   DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     ICPYR@$      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
     I*                                                                   S01486
     ISDBANK    E DSSDBANKPD                                              S01486
     I**  Data structure for Bank details                                 S01486
     I*                                                                   53103
     ISDPORT    E DSSDPORTPD                                              53103
     I**  Data structure for Portfolio details                            53103
     ISDPLIN    E DSSDPLINPD                                              53103
     I**  Data structure for Instrument details                           53103
     I*                                                                   S01486
     IDSFDY     E DSDSFDY                                                 S01486
     I**  Short data structure for access objects                         S01486
     I*                                                                   S01486
     I* Constants                                                         53103
     I              '00000010000000000000-C         C#15                  53103
     I              '0000000000'                                          53103
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*
     C           PPTDKY    KLIST
     C                     KFLD           QPCNUM
     C                     KFLD           QPPTFR
     C*                                                                   S01486
     C**  Copyright statement                                             S01486
     C*                                                                   S01486
     C                     MOVEACPY@      ACT@   80                       S01486
     C*                                                                   S01486
     C           *NAMVAR   DEFN           LDA                             S01486
     C*
     C**              START MAINLINE
     C**             ----------------
     C***********                                                         S01486
     C***ACCESS*LF/SDBANKL1*TO*OBTAIN*BANK*DETAILS*********************   S01486
     C***********          READ SDBANKL1                 70               S01486
     C***********                                                         S01486
     C***IF*RECORD*NOT*FOUND*OR*LAST*CHANGE*TYPE*=*'D'*THEN*DB*ERROR***   S01486
     C************IN70     IFEQ '1'                        B1             S01486
     C*                                                                   S01486
     C** Get bank details using access object                             S01486
     C*                                                                   S01486
     C                     CALL 'AOBANKR0'                                S01486
     C                     PARM *BLANKS   P@RTCD  7                       S01486
     C                     PARM '*FIRST ' P@OPTN  7                       S01486
     C           SDBANK    PARM SDBANK    DSFDY                           S01486
     C*                                                                   S01486
     C** Check if an error occurred                                       S01486
     C*                                                                   S01486
     C           P@RTCD    IFNE *BLANKS                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     Z-ADD1         DBASE            ****************
     C***********          MOVEL'SDBANKL1'DBFILE           * DBERROR  001 S01486
     C                     MOVE 'SDBANKPD'DBFILE           * DBERROR  001 S01486
     C                     MOVE *BLANKS   DBKEY            ****************
     C***********          MOVEL'PM1620  'DBPGM                           S01486
     C                     MOVEL'PM1625  'DBPGM                           S01486
     C                     OUT  LDA                                       S01486
     C                     EXSR *PSSR
     C                     END                             E1
     C*                                                                   53103
     C** ACCESS PF/SDPORTPD TO OBTAIN PM I.C.D.                           53103
     C                     CALL 'AOPORTR0'                                53103
     C                     PARM *BLANKS   WWRTCD  7                       53103
     C                     PARM '*FIRST ' WWOPTN  7                       53103
     C           SDPORT    PARM SDPORT    DSFDY                           53103
     C*                                                                   53103
     C** IF RECORD NOT FOUND THEN DB ERROR                                53103
     C           WWRTCD    IFNE *BLANKS                                   53103
     C           *LOCK     IN   LDA                                       53103
     C                     Z-ADD2         DBASE            ***************53103
     C                     MOVEL'SDPORTPD'DBFILE           * DBERROR  002 53103
     C                     MOVE *BLANKS   DBKEY            ***************53103
     C                     MOVEL'PM1620  'DBPGM                           53103
     C                     OUT  LDA                                       53103
     C                     EXSR *PSSR                                     53103
     C                     END                                            53103
     C*
     C** KEY FOR PMPTMTPP
     C           WWPPKY    KLIST
     C                     KFLD           QPCNUM
     C                     KFLD           QPPTFR
     C*
     C** KEY FOR PMHDVNLL
     C           WWHDKY    KLIST
     C                     KFLD           OSCNUM
     C                     KFLD           OSPTFR
     C*                                                                   53103
     C*  KLIST FOR PMPMIMPD                                               53103
     C           KPMIM     KLIST                                          53103
     C                     KFLD           QSCNUM                          53103
     C                     KFLD           QSPTFR                          53103
     C                     KFLD           QSINNR                          53103
     C                     KFLD           WWRESH 20                       53103
     C                     KFLD           WWMODI  2                       53103
     C*                                                                   53103
     C*  ENTRY PLIST                                                      53103
     C           *ENTRY    PLIST                                          53103
     C                     PARM           WWRTYP  1                       53103
     C*
     C**   PRODUCE ALL PERFORMANCE PERIODS REQUIRED
     C***********          READ PMFPPLPP                 90               S01486
     C                     READ PMFPPLQ0                 90               S01486
     C           *IN90     DOWEQ'0'                        B1
     C*
     C** READ THROUGH ALL MEMBERS FOR POOL PORTFOLIO
     C           WWPPKY    SETLLPMPPTMPD
     C           WWPPKY    READEPMPPTMPD                 61
     C*
     C           *IN61     DOWEQ'0'                        B2
     C*
     C** READ THROUGH ALL POSITIONS FOR POOL MEMBERS
     C           WWHDKY    SETLLPMHDVNLL
     C           WWHDKY    READEPMHDVNLL                 76
     C*
     C           *IN76     DOWEQ*OFF                       B3
     C           QFHLDV    IFEQ 'Y'                        B4
     C           QFDXTR    ANDGEQPELSD
     C           QFMODI    ANDNE'SE'
     C           QFMODI    ANDNE'FF'
     C           QFMODI    ANDNE'RE'
     C*
     C***********          MOVE QPCNUM    QFCNUM                          S01486
     C***********          MOVE QPPTFR    QFPTFR                          S01486
     C                     MOVE QPCNUM    GECNUM                          S01486
     C                     MOVE QPPTFR    GEPTFR                          S01486
     C**********           Z-ADDQFLIND    GELIND                                       S01486 CER020
     C                     MOVELQFLOIC    GELOIC                                              CER020
     C                     Z-ADDQFAMT1    GEAMT1                          S01486
     C                     Z-ADDQFACIS    GEACIS                          S01486
     C                     Z-ADDQFPLFX    GEPLFX                          S01486
     C                     Z-ADDQFMFXR    GEMFXR                          S01486
     C                     Z-ADDQFMPRC    GEMPRC                          S01486
     C**********           Z-ADDQFTRNB    GETRNB                                       S01486 CLE148
     C                     MOVELQFTRNB    GETRNB                                              CLE148
     C                     Z-ADDQFDXTR    GEDXTR                          S01486
     C                     Z-ADDQFMFXD    GEMFXD                          S01486
     C                     MOVELQFRECI    GERECI                          S01486
     C                     MOVELQFINNR    GEINNR                          S01486
     C                     MOVELQFVALG    GEVALG                          S01486
     C                     MOVELQFCCY1    GECCY1                          S01486
     C                     MOVELQFCNTR    GECNTR                          S01486
     C                     MOVELQFTDSS    GETDSS                          S01486
     C                     MOVELQFMFMD    GEMFMD                          S01486
     C                     MOVELQFMODI    GEMODI                          S01486
     C                     MOVELQFBYSF    GEBYSF                          S01486
     C                     MOVELQFFOIS    GEFOIS                          S01486
     C                     MOVELQFTXT2    GETXT2                          S01486
     C*                                                                   53103
     C*  SET 'REFERENCE/SHORTNAME'                                        53103
     C******************** MOVE *BLANKS   QFRESH                    53103 S01486
     C                     MOVE *BLANKS   GERESH                          S01486
     C                     SELEC                                          53103
     C           QFMODI    WHEQ 'SE'                                      53103
     C******************** MOVELQFTDSS    QFRESH    P               53103 S01486
     C                     MOVELQFTDSS    GERESH    P                     S01486
     C           QFMODI    WHEQ 'RE'                                      53103
     C******************** MOVELQFTXT2    QFRESH                    53103 S01486
     C                     MOVELQFTXT2    GERESH                          S01486
     C                     OTHER                                          53103
     C******************** MOVELQFTRNB    QFRESH    P               53103 S01486
     C                     MOVELQFTRNB    GERESH    P                     S01486
     C                     ENDSL                                          53103
     C                     WRITEPMPMIMP0
     C                     ADD  1         RRPERW
     C*
     C                     END                             E4
     C*
     C           WWHDKY    READEPMHDVNLL                 76
     C                     END                             E3
     C*                                                                   53103
     C*  FOR RUN TYPE 'M' WRITE DETAILS TO PMPMIMPD OF FX DEALS           53103
     C*  EXISTING IN ANOTHER PORTFOLIO BUT SETTLED IN CURRENT PORTF.      53103
     C           WWRTYP    IFEQ 'M'                                       53103
     C*                                                                   53103
     C           PPTDKY    CHAINPMPCAAL4             62                   53103
     C*                                                                   53103
     C           *IN62     DOWEQ*OFF                                      53103
     C*                                                                   53103
     C                     MOVELQSDLNO    WWRESH    P                     53103
     C                     MOVE 'FX'      WWMODI                          53103
     C           KPMIM     CHAINPMPMIMPD             65                   53103
     C           *IN65     IFEQ *ON                                       53103
     C                     MOVE 'M'       QFRECI                          53103
     C                     MOVE QSCNUM    QFCNUM                          53103
     C                     MOVE QSPTFR    QFPTFR                          53103
     C                     MOVE QSINNR    QFINNR                          53103
      *                                                                   182961
      ** Instrument type is defaulted from PF/SDPORTPD if a blank         182961
      ** instrument type is found.                                        182961
      ** Indicator 35 was used as the operation's resulting indicator.    182961
      *                                                                   182961
     C           QFINNR    IFEQ *BLANKS                                   182961
     C           1         SETLLSDPORTPD                                  182961
     C                     READ SDPORTPD                 35               182961
     C                     MOVE FCDINR    QFINNR                          182961
     C                     ENDIF                                          182961
     C*                                                                   53103
     C                     CALL 'AOPLINR0'
     C                     PARM '*MSG   ' WWRTCD  7                       53103
     C                     PARM '*KEY   ' WWOPTN  7                       53103
     C                     PARM QFINNR    WWKEY3  3                       53103
     C           SDPLIN    PARM SDPLIN    DSFDY                           53103
     C           WWRTCD    IFNE *BLANKS                                   53103
     C           *LOCK     IN   LDA                                       53103
     C                     Z-ADD3         DBASE            ***************53103
     C                     MOVEL'SDPLINPD'DBFILE           * DBERROR  003 53103
     C                     MOVELQFINNR    DBKEY            ***************53103
     C                     OUT  LDA                                       53103
     C                     EXSR *PSSR                                     53103
     C                     END                                            53103
     C*                                                                   53103
     C                     MOVE PDVALG    QFVALG                          53103
     C                     MOVE QSCCY     QFCCY1                          53103
     C                     MOVE FCCNCD    QFCNTR                          53103
     C**********           MOVE FCLICD    QFLIND                                        53103 CER020
     C                     MOVE FCLICD    QFLOIC                                              CER020
     C                     MOVE *BLANKS   QFTDSS                          53103
     C                     Z-ADD0         QFAMT1                          53103
     C                     Z-ADD0         QFACIS                          53103
     C                     Z-ADDQSCSHA    QFPLFX                          53103
     C                     Z-ADD1         QFMFXR                          53103
     C                     MOVE 'M'       QFMFMD                          53103
     C                     Z-ADD0         QFMPRC                          53103
     C**********           Z-ADDQSDLNO    QFTRNB                                        53103 CLE148
     C                     MOVELQSDLNO    QFTRNB                                              CLE148
     C                     MOVE 'FX'      QFMODI                          53103
     C                     MOVE 'B'       QFBYSF                          53103
     C                     MOVE *BLANKS   QFFOIS                          53103
     C                     MOVE *BLANKS   QFTXT2                          53103
     C                     Z-ADDQSCSHD    QFDXTR                          53103
     C                     MOVE C#15      QFMFXD                          53103
     C******************** MOVE WWRESH    QFRESH                    53103 S01486
     C                     MOVE WWRESH    GERESH                          S01486
     C                     Z-ADD0         QFNMDP                          72242
     C                     MOVE 'U'       QFSPBS                          72242
     C*                                                                   53103
     C                     WRITEPMPMIMP0                                  53103
     C                     ENDIF                                          53103
     C*                                                                   53103
     C           PPTDKY    READEPMPCAAL4                 62               53103
     C                     ENDDO                                          53103
     C*                                                                   53103
     C                     ENDIF                                          53103
     C*
     C           WWPPKY    READEPMPPTMPD                 61
     C                     END                             E2
     C*
     C***********          READ PMFPPLPP                 90               S01486
     C                     READ PMFPPLQ0                 90               S01486
     C                     END                             E1
     C*
     C** WRITE PERFORMANCE AUDIT REPORT
     C                     WRITEFORMTAU
     C                     WRITECONTROL                                   S01486
     C*                                                                   S01486
     C** Check if no details to report                                    S01486
     C*                                                                   S01486
     C           RRPERW    IFEQ *ZERO                                     S01486
     C                     WRITENODTLS                                    S01486
     C                     END                                            S01486
     C*
     C**   END PROGRAM
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   *PSSR     - Program error handling routine                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*                                                                   S01486
     C                     WRITEFORMTAU                                   S01486
     C                     WRITEDBERROR                                   S01486
     C                     DUMP
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C*
     C                     RETRN
     C*
     C                     ENDSR
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
