     H        1                                                           S01486
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Calc. Dates Used in Mon. Housekeeping')
     H***********                                                         S01486
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management Module                      *
     F*                                                               *
     F*  PM1800 - Calculate Dates Used in Monthly Housekeeping        *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01486             Date 11AUG94               *
     F*                 S01313             Date 28JAN93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  S01486 - Portfolio Management Upgrade to Release 10.         *
     F*  S01313 - PORTFOLIO PERFORMANCE                               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F** C R E A T I O N     P A R A M E T E R S                      *
     F**                                                              *
     F**                                                              *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F**
     F**      FILE DEFINITIONS
     F**     ------------------
     F*
     F*****BANK*DETAILS*FILE************************-*Prefix*BJ********   S01486
     F***SDBANKL1IF**E           K        DISK         KINFSR *PSSR       S01486
     F*                                                                   S01486
     F**  Audit Report                                                    S01486
     FPM1800AUO   E                    PRINTER      KINFSR *PSSR          S01486
     F*
     F/EJECT
     E*****************************************************************
     E*
     E**   ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
      /COPY ZSRSRC,ZDATE2Z1
     E*****************************************************************
     E/EJECT
     E*****************************************************************
     I*
     I**   DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     ICPYR@$      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
     I*
     I**   LOCAL DATA AREA
     I***LDA********UDS***********************     256                    S01486
     ILDA         DS                            256                       S01486
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I** DATA STRUCTURE USED TO EXTRACT DAY, MONTH & YEAR PARTS OF DATE
     I            DS                              6
     I                                        1   60ZDATE
     I                                        1   20WWDAY
     I                                        3   40WWMNTH
     I                                        5   60WWYEAR
     I*                                                                   S01486
     ISDBANK    E DSSDBANKPD                                              S01486
     I**  Data structure for Bank details                                 S01486
     I*                                                                   S01486
     IDSFDY     E DSDSFDY                                                 S01486
     I**  Short data structure for access objects                         S01486
     I*                                                                   S01486
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*
     C**              START MAINLINE
     C*
     C           *ENTRY    PLIST
     C                     PARM           WWSTNM  2
     C                     PARM           WWSTPP  5
     C*                                                                   S01486
     C**  Copyright statement                                             S01486
     C*                                                                   S01486
     C                     MOVEACPY@      ACT@   80                       S01486
     C*                                                                   S01486
     C           *NAMVAR   DEFN           LDA                             S01486
     C*
     C** INITIALISE DB ERROR FIELDS AND STORE PROGRAM NAME FOR DB
     C** ERROR FIELD
     C           *LOCK     IN   LDA                                       S01486
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     Z-ADD*ZEROS    DBASE
     C                     MOVEL'PM1800'  DBPGM
     C                     OUT  LDA                                       S01486
     C***********                                                         S01486
     C***ACCESS*LF/SDBANKL1*TO*OBTAIN*BANK*DETAILS*********************   S01486
     C***********          READ SDBANKL1                 70               S01486
     C***********                                                         S01486
     C***IF*RECORD*NOT*FOUND*OR*LAST*CHANGE*TYPE*=*'D'*THEN*DB*ERROR***   S01486
     C************IN70     IFEQ '1'                                       S01486
     C*                                                                   S01486
     C** Get bank details using access object                             S01486
     C*                                                                   S01486
     C                     CALL 'AOBANKR0'                                S01486
     C                     PARM *BLANKS   P@RTCD  7                       S01486
     C                     PARM '*FIRST ' P@OPTN  7                       S01486
     C           SDBANK    PARM SDBANK    DSFDY                           S01486
     C*                                                                   S01486
     C** Check if an error occurred                                       S01486
     C*                                                                   S01486
     C           P@RTCD    IFNE *BLANKS                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     Z-ADD1         DBASE            ****************
     C***********          MOVEL'SDBANKL1'DBFILE           * DBERROR 001 *S01486
     C                     MOVE 'SDBANKPD'DBFILE           * DBERROR 001 *S01486
     C                     MOVE *BLANKS   DBKEY            ****************
     C                     OUT  LDA                                       S01486
     C                     EXSR *PSSR
     C                     END
     C*
     C** SET OFF *IN98 SO DATE IN DDMMYY FORMAT
     C                     MOVE *OFF      *IN98
     C*
     C** CONVERT DATE OF NEXT WORKING DAY TO DDMMYY FORMAT AND EXTRACT
     C** 'MM' PART OF DATE
     C                     Z-ADDBJDNWD    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZDATE     WWORK4  4
     C                     MOVE WWORK4    WWSTNM
     C*
     C** CONVERT RUN DATE TO DDMMYY FORMAT AND CALCULATE START OF MONTH
     C** 10 MONTHS AGO
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     EXSR ZDATE2
     C*
     C                     Z-ADD1         WWDAY
     C*
     C           WWMNTH    IFGT 10
     C           WWMNTH    SUB  10        WWMNTH
     C                     ELSE
     C           WWMNTH    ADD  2         WWMNTH
     C           WWYEAR    SUB  1         WWYEAR
     C                     END
     C*
     C** CONVERT START OF PERFORMANCE PERIOD TO MIDAS DAY NUMBER
     C                     EXSR ZDATE1
     C                     MOVE ZDAYNO    WWSTPP
     C*
     C                     SETON                     LR
     C                     RETRN
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   *PSSR     - Program error handling routine                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*                                                                   S01486
     C                     WRITEHEADER                                    S01486
     C                     WRITEDBERROR                                   S01486
     C                     DUMP
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C                     ENDSR
      /COPY ZSRSRC,ZDATE1Z2
      /COPY ZSRSRC,ZDATE2Z2
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE IN FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
