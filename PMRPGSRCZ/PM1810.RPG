     H        1                                                           S01486
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Duplicate Start of Mon. Pos. for Pool')       *
     H***********                                                         S01486
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management Module                          *
     F*                                                               *
     F*  PM1810 - Duplicate Start of Month Positions for Pool Ptfr    *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CPM005             Date 29Jul96               *
     F*                 CFF004             Date 02OCT96               *
     F*                 S01486             Date 11Aug94               *
     F*                 057020             Date 16Jun93               *
     F*                 056460             Date 09Jun93               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  CPM005 - Private Banking Phase II                            *
     F*           Focus Group Changes Upgraded to DBA                 *
     F*           Just Recompiled because of PM File(s) changes       *
     F*  CFF004 - Increase in size of Price Fields,(Recompiled Only)  *
     F*  S01486 - Portfolio Management Upgrade to Release 10.         *
     F*  057020 - RECOMPILED DUE TO CHANGE IN FILES                   *
     F*  056460 - SAVE DETAILS OF MATURE / FLAT POSITIONS             *
     F*  WRITTEN FOR 056460                                           *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F** C R E A T I O N     P A R A M E T E R S                      *
     F**                                                              *
     F**                                                              *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F**
     F**      FILE DEFINITIONS
     F**     ------------------
     F*
     F*****BANK*DETAILS*FILE************************-*Prefix*BJ********   S01486
     F***SDBANKL1IF**E           K        DISK         KINFSR *PSSR       S01486
     F*
     F**   PORTFOLIO CUSTOMER DETAILS               - Prefix QB
     FSDPLCSPDIF  E                    DISK         KINFSR *PSSR
     F*
     F**   PORTFOLIO DETAILS                        - Prefix PN
     FPMPORTL1IF  E           K        DISK         KINFSR *PSSR
     F*
     F**   POOL PORTFOLIO MEMBER DETAILS            - Prefix OS
     FPMPPTMPDIF  E           K        DISK         KINFSR *PSSR
     F*
     F**   START OF MONTH PORTFOLIO POSITIONS       - Prefix QF
     F***PMPSTMPPO***E           K        DISK         KINFSR *PSSR A     S01486
     FPMPSTMQ0O   E           K        DISK         KINFSR *PSSR A        S01486
     F*
     F**   PORTFOLIO HOLDINGS                       - Prefix QF
     FPMHDVNLLIF  E           K        DISK         KINFSR *PSSR
     F*
     F**   AUDIT REPORT
     FPM1810AUO   E                    PRINTER      KINFSR *PSSR
     F*
     F/EJECT
     E*****************************************************************
     E*
     E**   ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
     E*****************************************************************
     E/EJECT
     E*****************************************************************
     I*
     I**   LOCAL DATA AREA
     I***LDA********UDS***********************     256                    S01486
     ILDA         DS                            256                       S01486
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I**   DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     ICPYR@$      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
     I*                                                                   S01486
     ISDBANK    E DSSDBANKPD                                              S01486
     I**  Data structure for Bank details                                 S01486
     I*                                                                   S01486
     IDSFDY     E DSDSFDY                                                 S01486
     I**  Short data structure for access objects                         S01486
     I*                                                                   S01486
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C           *ENTRY    PLIST
     C                     PARM           QFSTMN
     C*                                                                   47868
     C** KEY LIST USED IN ACCESSING LF/PMHDVNLL                           47868
     C           HDVNKY    KLIST                                          47868
     C                     KFLD           OSCNUM                          47868
     C                     KFLD           OSPTFR                          47868
     C*                                                                   47868
     C           PORTKY    KLIST                                          47868
     C**********           KFLD           WKCNUM  60                                    47868 CSD027
     C                     KFLD           WKCNUM  6                                           CSD027
     C                     KFLD           WKPTFR  4                       47868
     C*                                                                   47868
     C           PPTMKY    KLIST                                          47868
     C**********           KFLD           PNCNUM  60                                    47868 CSD027
     C                     KFLD           PNCNUM  6                                           CSD027
     C                     KFLD           PNPTFR  4                       47868
     C*                                                                   S01486
     C**  Copyright statement                                             S01486
     C*                                                                   S01486
     C                     MOVEACPY@      ACT@   80                       S01486
     C*                                                                   S01486
     C           *NAMVAR   DEFN           LDA                             S01486
     C*
     C**              START MAINLINE
     C**             ----------------
     C***********                                                         S01486
     C***ACCESS*LF/SDBANKL1*TO*OBTAIN*BANK*DETAILS*********************   S01486
     C***********          READ SDBANKL1                 70               S01486
     C***********                                                         S01486
     C***IF*RECORD*NOT*FOUND*OR*LAST*CHANGE*TYPE*=*'D'*THEN*DB*ERROR***   S01486
     C************IN70     IFEQ '1'                        B1             S01486
     C*                                                                   S01486
     C** Get bank details using access object                             S01486
     C*                                                                   S01486
     C                     CALL 'AOBANKR0'                                S01486
     C                     PARM *BLANKS   P@RTCD  7                       S01486
     C                     PARM '*FIRST ' P@OPTN  7                       S01486
     C           SDBANK    PARM SDBANK    DSFDY                           S01486
     C*                                                                   S01486
     C** Check if an error occurred                                       S01486
     C*                                                                   S01486
     C           P@RTCD    IFNE *BLANKS                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     Z-ADD1         DBASE            ****************
     C***********          MOVEL'SDBANKL1'DBFILE           * DBERROR  001 S01486
     C                     MOVE 'SDBANKPD'DBFILE           * DBERROR  001 S01486
     C                     MOVE *BLANKS   DBKEY            ****************
     C                     MOVEL'PM1810  'DBPGM
     C                     OUT  LDA                                       S01486
     C                     EXSR *PSSR
     C                     END                             E1
     C*
     C** READ THROUGH ALL POOL CUSTOMERS
     C                     READ SDPLCSPD                 60
     C*
     C           *IN60     DOWEQ'0'                        B1
     C*
     C** READ THROUGH ALL PORTFOLIOS FOR POOL CUSTOMER
     C                     MOVE QBCUST    WKCNUM
     C                     MOVE *LOVAL    WKPTFR
     C*
     C           PORTKY    SETLLPMPORTL1
     C           WKCNUM    READEPMPORTL1                 61
     C*
     C           *IN61     DOWEQ'0'                        B2
     C*
     C** PERFORM SR/#P01 TO EXTRACT DETAILS OF STARTING POSITION FOR
     C** POOL
     C                     EXSR #P01
     C*
     C** READ NEXT PORTFOLIO FOR CUSTOMER
     C           WKCNUM    READEPMPORTL1                 61
     C                     END                             E2
     C*
     C** READ NEXT POOL CUSTOMER DETAILS
     C                     READ SDPLCSPD                 60
     C                     END                             E1
     C*
     C** WRITE PERFORMANCE AUDIT REPORT
     C                     WRITEFORMTAU
     C                     WRITECONTROL                                   S01486
     C*                                                                   S01486
     C** Check if no details to report                                    S01486
     C*                                                                   S01486
     C           RRPERW    IFEQ *ZERO                                     S01486
     C                     WRITENODTLS                                    S01486
     C                     END                                            S01486
     C*
     C**   END PROGRAM
     C                     MOVE '1'       *INLR
     C                     RETRN
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #P01     - READ POOL PORTFOLIO MEMBER DETAILS AND EXTRACT        *
      *            POSITIONS                                             *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * *PSSR    - OUTPUT DTABASE ERROR DETAILS                          *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN PROCESSING ROUTINE                               *
      *                                                                  *
      ********************************************************************
     C           #P01      BEGSR
     C*
     C** READ THROUGH DETAILS OF MEMBERS OF POOL
     C           PPTMKY    SETLLPMPPTMPD
     C           PPTMKY    READEPMPPTMPD                 62
     C*
     C           *IN62     DOWEQ'0'                        B1
     C*
     C           OSRECI    IFEQ 'D'                        B2
     C*
     C** READ THROUGH ALL PORTFOLIO POSISTIONS
     C           HDVNKY    SETLLPMHDVNLL
     C           HDVNKY    READEPMHDVNLL                 63
     C*
     C           *IN63     DOWEQ'0'                        B3
     C*
     C** IF HOLDING VALUED AND FIDUCIARY, DEAL OR LOAN OUTPUT DETAILS
     C           QFHLDV    IFEQ 'Y'                        B4
     C           QFMODI    ANDEQ'FI'
     C           QFHLDV    OREQ 'Y'
     C           QFMODI    ANDEQ'MD'
     C           QFHLDV    OREQ 'Y'
     C           QFMODI    ANDEQ'ML'
     C           QFHLDV    OREQ 'Y'
     C           QFMODI    ANDEQ'FX'
     C           QFHLDV    OREQ 'Y'
     C           QFMODI    ANDEQ'LE'
     C*
     C** OUTPUT DETAILS FOR POOL PORTFOLIO
     C**********           Z-ADDOSPCNU    QFCNUM                                              CSD027
     C                     MOVE OSPCNU    QFCNUM                                              CSD027
     C                     MOVE OSPPTF    QFPTFR
     C                     MOVE 'N'       QFPPRC
     C*
     C                     WRITEPMPSTMP0
     C                     ADD  1         RRPERW
     C                     END                             E4             47868
     C*                                                                   47868
     C           HDVNKY    READEPMHDVNLL                 63
     C                     END                             E3             47868
     C                     END                             E2             47868
     C*                                                                   47868
     C           PPTMKY    READEPMPPTMPD                 62
     C                     END                             E1             47868
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C**                                                              *
     C**   *PSSR     - Program error handling routine                 *
     C**                                                              *
     C*****************************************************************
     C*
     C           *PSSR     BEGSR
     C*                                                                   S01486
     C                     WRITEFORMTAU                                   S01486
     C                     WRITEDBERROR                                   S01486
     C                     DUMP
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C*
     C                     RETRN
     C*
     C                     ENDSR
**  CPY@
(c) Finastra International Limited 2001
