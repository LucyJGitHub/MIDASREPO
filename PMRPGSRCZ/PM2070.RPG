     H        1                                                           S01486
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Deal amendments per deal enquiry')            *
     H***********                                                         S01486
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management Module                          *
     F*                                                               *
     F*  PM2070 - Deal Amendments per deal Enquiry                    *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      *  Last Amend No. CGL165             Date 15Feb17               *
      *  Prev Amend No. CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      *                 CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 11Jul06               *
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CDL038             Date 10May05               *
      *                 237063             Date 20Nov05               *
      *                 BUG7411            Date 01Jun05               *
      *                 CDL028             Date 07Feb05               *
      *                 229544             Date 15Sep04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CDL006             Date 20May99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 074286             Date 08Aug94               *
      *                 CPM005             Date 11Jul96               *
     F*                 CPM004             DATE 03MAR96               *
     F*                 094529             DATE 13OCT95               *
     F*                 085817             DATE 13MAR95               *
     F*                 081364             DATE 01FEB95               *
     F*                 081239             DATE 01FEB95               *
     F*                 S01486             DATE 12JUL94               *
     F*                 SA9108             DATE 06NOV91               *
     F*                 S01313             DATE 06NOV92               *
     F*                 049467             DATE 28JAN93               *
     F*                 001365     ADIF    DATE 13NOV91               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  237063 - EU Tax fixes upgrade to MP build 103 (Recompile).   *
      *  BUG7411- CDL028 fields missing from DLGHISPD (Recompile)     *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  229544 - Recompiled due to inserted fields in DLCHISPD.      *
      *  CDL006 - Dealing Fiduciary                                   *
     F*  074286 - DESCRIPTION FOR DTYP 'TI' NOT FOUND                 *
     F*  CPM005 - Private Banking Phase II                            *
     F*           Focus Group Changes Upgraded to DBA                 *
     F*  CPM004 - Bank Portfolios (RECOMPILED)                        *
     F*  094529 - Historic deals are now kept on DLBHISPD / DLCHISPD  *
     F*  085817 - Recompile due to changes in PMLDAPD                 *
     F*  081364 - Wrong field used - recompiled                       *
     F*  081239 - R10 Private Banking Rework                          *
     F*  S01486 - Portfolio Management Incorporation                  *
     F*  SA9108 - POOL SECURITIES TRANSACTIONS (ONLY RECOMPILED       *
     F*           FOR DTAARA/LDA)                                     *
     F*  S01313 - RECOMPILED DUE TO PORTFOLIO PERFORMANCE             *
     F*  049467 - RECOMPILED FOR RELEASE 3 PTF 02                     *
     F*  001365 - SYSTEM DOESN'T CHECK IN HISTORIC FILE FOR DEALS     *
     F*           MATURED ---> DUMP                                   *
     F*****************************************************************
     F* ID F  C  H  L    FUNCTION OF INDICATORS
     F*
     F*       02         CHAIN FAIL ON DEALS MASTER
     F*       03         CHAIN FAIL ON DEAMS DETAIL / END OF DEAMS FILE
     F********04*********CHAIN*FAIL*ON*SDCURRL0************************** S01486
     F*       11         DEAMS DETAILS REQUESTED FROM SUBFILE
     F*       12         LOKUP RESULT INDICATOR
     F*       25         ROLLUP REQUESTED
     F*       26         ROLLDOWN REQUESTED
     F*       61         CLEAR SUBFILE IF ON, DISPLAY SUBFILE IF OFF
     F*       62         SUBFILE END
     F*       U7         DATABASE ERROR (JOB SWITCH).
     F*       U8         DATABASE ERROR (JOB SWITCH).
     F*
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F***PM2070DDCF**E*                   WORKSTN                         S01486
     FPM2070DFCF  E                    WORKSTN                            S01486
     F                                        RRN   KSFILE PM2070S1
     FDEAMS   IF  E           K        DISK
     F            DEAMSDHF                          KIGNORE
     F*           DEAMSDIF                          KIGNORE
     F            DEAMSDJF                          KIGNORE
     F                                              KINFSR *PSSR
     FDEALSH  IF  E           K        DISK                               001365
     F                                              KINFSR *PSSR          001365
     F            DEALSDBF                          KRENAMEDEALHB
     F            DEALSDCF                          KRENAMEDEALHC
     F            DEALSDDF                          KRENAMEDEALHD
     F            DEALSDEF                          KRENAMEDEALHE
     F*********** DEALSDGF                          KRENAMEDEALHG         S01486
     F            DEALSDGF                          KIGNORE               S01486
     F            DLGHISD0                          KIGNORE               094529
     FDEALS   IF  E           K        DISK
     F            DEALSDAF                          KIGNORE
     F*           DEALSDBF                          KIGNORE
     F*           DEALSDCF                          KIGNORE
     F*           DEALSDDF                          KIGNORE
     F*           DEALSDEF                          KIGNORE
     F*           DEALSDGF                          KIGNORE
     F            DEALSDGF                          KIGNORE               S01486
     F            DEALSDFF                          KIGNORE
     F                                              KINFSR *PSSR
     F***SDCURRL0IF**E*          K        DISK                            S01486
     F***********                                   KINFSR *PSSR          S01486
     E*********************************************************************
     E*  TABLES AND ARRAYS
     E*    ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
     E*
     E*****ARRAY*TO*ENABLE*ATTENTION*KEY**********************************S01486
     E***********         ATN     1   1 22                                S01486
     E*
      /COPY ZSRSRC,ZDATE2Z1
      /COPY ZSRSRC,ZSEDITZ1
     E*
     E*    ARRAYS FOR DEAL AND AMENDMENT TYPES TRANSLATION
     E***********         DTYPA   9   9  2               DEAL TYPES       CDL006
     E                    DTYPA  15  15  2               DEAL TYPES       CDL006
     E***********         DTYPB   1   9 35               DEAL TYPES NARRATCDL006
     E                    DTYPB   1  15 35               DEAL TYPES       CDL006
     E                    AMTPA   5   5  2               DEAMS TYPES
     E                    AMTPB   1   5 23               DEAMS TYPES NARRATIVE
     E*
     E/EJECT
     I*
     I*    RENAME COMMON INPUT FIELDS
     IDEAMSDIF
     I              RECI                            RECID
     I              DLNO                            DLNOD
     I              BRTT                            BRTTD
     I              RTSP                            RTSPD
     I              CCY                             CCYD
     I              VDAT                            VDATD
     I              DTYP                            DTYPD
     I*
     I*****GROUP*DATA*AREA*********************************************** S01486
     I***WWGDA*****E*DSPMGRDAPP                                           S01486
     I*
     I***WWLDA*****E*DSPMLDAPP                                            S01486
     IWWLDA     E DSPMLDAPD                                               S01486
     ISDCURR    E DSSDCURRPD                                              S01486
     I** DUMMY RECORD FORMATS FOR ACCESS TO CURRENCIES DETAILS            S01486
     IDSFDY     E DSDSFDY                                                 S01486
     I**  Short DS for access programs                                    S01486
     IDSSDY     E DSDSSDY                                                 S01486
     I* Second DS For Access Programs, Long Data Structure                S01486
     I*
     I*    PROGRAM STATUS DATA STRUCTURE FOR WS/USER ID'S
     IPSDS       SDS
     I                                      244 253 WSID
     I                                      254 263 USID
     I**
     I** DATA STRUCTURE FOR SUBROUTINE ZSEDIT
     I**
     I            DS
     I                                        1  150ZFLD15
     I                                        1  15 ZFLDA
      /COPY ZSRSRC,ZSEDITZ2
     I*
     I*    DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*
     I*    L.D.A. DATA AREA TO HOLD INFORMATION ON DATABASE ERROR.
     I           UDS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 183 DBASE
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INDEX OF SUBROUTINES                                             *
      * MAIN     - MAIN PROCESSING                                       *
      * ACCESS   - ACCESS DEALS DETAILS                                  *
      * LOADSF   - LOAD SUBFILE                                          *
      * ZDATE2   - CONVERSION ROUTINE (DAY NUMBER TO DATE)               *
      * ZSEDIT   - EDITION SUBROUTINE TO NUMERIC FIELDS                  *
      * *PSSR    - PROCESS DATABASE ERROR                                *
      * #INIT    - INITIAL PROCESSING                                    *
      *                                                                  *
      * EXTERNAL ROUTINES CALLED                                         *
      * PM2100   - DEAL AMENDMENT ENQUIRY                                *
      **PMC1000**-*UPDATE*GROUP*JOB*DATA*AREA*****************************S01486
      **PMC1020**-*TERMINATE*GROUP*JOB*IF*DATABASE*ERROR******************S01486
      **PMC1010**-*READ*GROUP*JOB*DATA*AREA*******************************S01486
      **QCMDEXC**-*EXEC*AS/400*COMMAND************************************S01486
      * PMC1003  - TO RETRIEVE DATA AREA *LDA                            *S01486
      * PMC1001  - TO UPDATE DATA AREA *LDA                              *S01486
      * DBERRCTL - DATABASE ERROR PROCESSING                             *S01486
      *                                                                  *
      ********************************************************************
     C/EJECT
     C*
     C                     MOVEACPY@      CPY2@  80
     C*
     C*   Initial processing.
     C                     EXSR #INIT
     C*
     C*   Details access processing
     C                     EXSR ACCESS
     C*
     C*   Termination processing
     C                     RETRN
     C*
     C*****************************************************************
     C/EJECT
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * ACCESS   - ACCESS DEALS DETAILS                                  *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * *PSSR    - PROCESS DATABASE ERROR                                *
      * LOADSF   - LOAD SUBFILE                                          *
      * PM2100   - DEAL AMENDMENT ENQUIRY                                *
      **PMC1000**-*UPDATE*GROUP*JOB*DATA*AREA*****************************S01486
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN PROCESSING                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWDLNO   - DEAL NUMBER PARAMETER PASSED TO PROGRAM               *
      * WWVD1P   - FIRST PAGE DEAMS VALUE DATE                           *
      * WWSN1P   - FIRST PAGE DEAMS SEQUENCE NO                          *
      * WWVDSV   - CURRENT PAGE DEAMS VALUE DATE                         *
      * WWSNSV   - CURRENT PAGE DEAMS SEQUENCE NO                        *
      * WWRETC   - RETURN CODE FROM PM2100                               *
      * WWPARM   - GROUP JOB DATA STRUCTURE                              *
      *                                                                  *
      ********************************************************************
     C           ACCESS    BEGSR
     C*
     C*   ACCESS DEALS DETAILS
     C           WWDLNO    CHAINDEALS                02
     C           *IN02     IFEQ '1'                                       001365
     C           WWDLNO    CHAINDEALSH               02                   001365
     C                     END                                            001365
     C           *IN02     IFEQ '1'
     C                     Z-ADD2         QMERRN
     C***********          MOVEL'DEALS'   QMFILE                          S01486
     C                     MOVE 'DEALS'   QMFILE                          S01486
     C                     MOVELWWDLNO    QMKEY
     C                     EXSR *PSSR
     C                     END
     C*
     C****ACCESS*CURRENCY*CODES*FILE************************************* S01486
     C*   READ CURRENCY CODES DETAILS VIA ACCESS PROGRAM          R10036  S01486
     C***********CCY       CHAINSDCURRL0             04                   S01486
     C************IN04     IFEQ '1'                                       S01486
     C                     CALL 'AOCURRR0'                                S01486
     C                     PARM *BLANKS   @RTCD   7                       S01486
     C                     PARM '*KEY   ' @OPTN   7                       S01486
     C                     PARM CCY       @K101   3                       S01486
     C           SDCURR    PARM SDCURR    DSSDY                           S01486
     C           @RTCD     IFNE *BLANK                                    S01486
     C                     Z-ADD4         QMERRN
     C***********          MOVEL'SDCURR'  QMFILE                          S01486
     C                     MOVE 'SDCURRPD'QMFILE                          S01486
     C                     MOVELCCY       QMKEY
     C                     EXSR *PSSR
     C                     END
     C*
     C*    LOAD SUBFILE
     C                     Z-ADD0         VDAT
     C                     Z-ADD0         DASN
     C           DMSKEY    SETLLDEAMS                    03
     C                     EXSR LOADSF
     C*
     C*    SAVE FIRST RECORD ON FIRST PAGE
     C                     Z-ADDWWVDSV    WWVD1P  50
     C                     Z-ADDWWSNSV    WWSN1P  30
     C*
     C*    INITIALISE HEADER DISPLAY ZONES
     C                     MOVELQMCNUM    DDCNUM           CUSTOMER NUMBER
     C                     MOVELQMCRNM    DDCRNM           CUSTOMER NAME
     C                     MOVELQMPTF2    DDPTFR           PORTFOLIO REF
     C                     MOVELQMPTFN    DDPTFN              "      NARR
     C                     MOVELWWDLNO    DDDLNO           DEAL NUMBER
     C           DTYP      LOKUPDTYPA,X                  12DEAL TYPE
     C           *IN12     IFEQ '1'
     C                     MOVEADTYPB,X   DDDTYP
     C                     ELSE
     C                     MOVE *ALL'?'   DDDTYP
     C                     END
     C                     MOVELCCY       DDCCY            CURRENCY CODE
     C                     Z-ADDPCPL      ZFLD15           PRINCIPAL AMOUNT
     C                     Z-ADDA6NBDP    ZDECS
     C                     MOVE ' '       ZECODE
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDPCPL
     C           BRTT      IFNE 0
     C                     MOVELBRTT      DDBRTT           BASE RATE CODE
     C                     END
     C           RTSP      IFNE 0
     C                     Z-ADD0         ZFLD15           RATE/SPREAD
     C                     MOVE RTSP      ZFLDA
     C                     Z-ADD7         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDRTSP
     C                     ELSE
     C                     MOVE *BLANKS   DDRTSP
     C                     END
     C*
     C*   LOOP UNTIL QUIT REQUESTED
     C************INKC     DOUEQ'1'                                       081239
     C************INKL     OREQ '1'                                       081239
     C************INKV     OREQ '1'                                 S01486081239
     C           QMENQT    DOUEQ'Y'                                       081239
     C           QMENQT    OREQ 'C'                                       081239
     C           QMENQT    OREQ 'P'                                       081239
     C           QMENQT    OREQ 'D'                                       081239
     C           *INKL     OREQ '1'                                       081239
     C*
     C*   DISPLAY DETAILS SCREEN
     C                     WRITEPM2070F1
     C                     WRITEPM2070F2
     C*
     C                     EXFMTPM2070C1
     C*                                                                   081239
     C** If F3  pressed, exit program                                     081239
     C*                                                                   081239
     C           *INKC     IFEQ '1'                                       081239
     C                     MOVE 'Y'       QMENQT                          081239
     C                     CALL 'PMC1001'                                 081239
     C                     PARM           WWLDA                           081239
     C                     END                                            081239
     C**********                                                    081239CPM005
     C***If*F22*pressed, access Enquiry selection screen            081239CPM005
     C**********                                                    081239CPM005
     C********** *INKW     IFEQ '1'                                 081239CPM005
     C**********           CALL 'PMC0610'                           081239CPM005
     C**********                                                    081239CPM005
     C***Retrieve *LDA values                                       081239CPM005
     C**********                                                    081239CPM005
     C**********           CALL 'PMC1003'                           081239CPM005
     C**********           PARM           WWLDA                     081239CPM005
     C**********           END                                      081239CPM005
     C*
     C*   UPDATE SUBFILE ACCORDING TO ROLLUP/DOWN
     C           *IN25     IFEQ '1'
     C           *IN03     ANDEQ'0'                        DEAMS EOF
     C                     Z-ADDVDAT      WWVDSV           TAKE LAST
     C                     Z-ADDDASN      WWSNSV           RECORD READ
     C           DMSKY2    SETLLDEAMS                    03
     C                     EXSR LOADSF
     C                     END
     C*
     C           *IN26     IFEQ '1'
     C           WWVD1P    ANDNEWWVDSV                     NOT ON 1ST
     C*          WWSN1P    ANDNEWWSNSV                     PAGE
     C*
     C*    READ PREVIOUS RECORDS TO DISPLAY FORM FIRST OF CURRENT SCREEN
     C           DMSKY2    SETLLDEAMS                    03
     C                     Z-ADD12        RECNO   10
     C           *IN05     DOWNE'1'
     C           WWDLNO    ANDEQDLNOD
     C           RECNO     ANDGT0
     C                     READPDEAMS                    05
     C           RECID     IFNE '*'
     C           RECID     ANDNE'R'
     C                     SUB  1         RECNO
     C                     END
     C                     END
     C*
     C           *IN05     IFEQ '1'
     C           WWDLNO    ORNE DLNOD
     C                     Z-ADDWWVD1P    WWVDSV
     C                     Z-ADDWWSN1P    WWSNSV
     C           DMSKY2    SETLLDEAMS                    03
     C                     END
     C*
     C                     EXSR LOADSF
     C*
     C                     END
     C*
     C*    ACCESS DEAMS DETAILS IF REQUESTED
     C                     READCPM2070S1                 11
     C           *IN11     IFEQ '0'
     C                     UPDATPM2070S1
     C                     WRITEPM2070C1
     C                     CALL 'PM2100'
     C                     PARM           WWRETC  1
     C                     PARM           DDDLNO
     C                     PARM           DDVDA2
     C                     PARM           DDDASN
     C*                                                                   081239
     C** Retrieve *LDA values                                             081239
     C*                                                                   081239
     C                     CALL 'PMC1003'                                 081239
     C                     PARM           WWLDA                           081239
     C                     END
     C*
     C***********WWRETC    IFEQ '1'                        CMD3 PRESSED   081239
     C***********WWRETC    OREQ '9'                        DB ERROR       081239
     C***********          MOVE '1'       *INKC                           081239
     C***********          END                                            081239
     C*
     C                     END                             ENDDOU
     C***********                                                         081239
     C****EXIT*PROGRAM****************************************************081239
     C************INKC     IFEQ '1'                                       081239
     C***********          MOVE 'Y'       QMENQT                          081239
     C***********          CALL 'PMC1002'                                 S01486
     C***********          PARM           WWGDA                           S01486
     C***********          CALL 'PMC1001'                           S01486081239
     C***********          PARM           WWLDA                     S01486081239
     C***********          END                                            081239
     C*
     C                     ENDSR
     C*
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * LOADSF   - LOAD SUBFILE                                          *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZDATE2   - CONVERSION ROUTINE (DAY NUMBER TO DATE)               *
      * ZSEDIT   - EDITION SUBROUTINE TO NUMERIC FIELDS                  *
      * *PSSR    - PROCESS DATABASE ERROR                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * ACCESS   - ACCESS DEALS DETAILS                                  *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWVDSV   - CURRENT PAGE DEAMS VALUE DATE                         *
      * WWSNSV   - CURRENT PAGE DEAMS SEQUENCE NO                        *
      *                                                                  *
      ********************************************************************
     C           LOADSF    BEGSR
     C*    CLEAR SUBFILE
     C                     MOVE '1'       *IN61
     C                     WRITEPM2070C1
     C                     MOVE '0'       *IN61
     C*
     C                     Z-ADD1         RRN
     C           *IN03     DOUEQ'1'
     C           RRN       ORGT 12
     C           DMSKEY    READEDEAMS                    03
     C*
     C*    INITIALISE SUBFILE FIELDS
     C           RECID     IFNE '*'
     C           RECID     ANDNE'C'
     C           *IN03     ANDNE'1'
     C*
     C*    SAVE RECORD KEY ON CURRENT PAGE
     C           RRN       IFEQ 1
     C                     Z-ADDVDATD     WWVDSV  50
     C                     Z-ADDDASN      WWSNSV  30
     C                     END
     C*
     C                     MOVE ' '       DDACTN           ACTION
     C                     Z-ADDVDATD     ZDAYNO           VALUE DATE
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     DDVDAT
     C                     MOVELDASN      DDDASN           SEQUENCE NUMBER
     C           AMTP      LOKUPAMTPA,X                  12AMEND TYPE
     C           *IN12     IFEQ '1'
     C                     MOVEAAMTPB,X   DDAMTP
     C                     ELSE
     C                     MOVE *ALL'?'   DDAMTP
     C                     END
     C           DAAM      IFNE 0
     C                     Z-ADDDAAM      ZFLD15           PRINCIPAL AMOUNT
     C                     Z-ADDA6NBDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDDAAM
     C                     ELSE
     C                     MOVE *BLANKS   DDDAAM
     C                     END
     C           BRTTD     IFNE 0
     C                     MOVELBRTTD     DDBRT2           BASE RATE CODE
     C                     ELSE
     C                     MOVE *BLANKS   DDBRT2
     C                     END
     C           RTSPD     IFNE 0
     C                     Z-ADD0         ZFLD15           RATE/SPREAD
     C                     MOVE RTSPD     ZFLDA
     C                     Z-ADD7         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDRTS2
     C                     ELSE
     C                     MOVE *BLANKS   DDRTS2
     C                     END
     C                     Z-ADDVDATD     DDVDA2           RATE/SPREAD
     C                     WRITEPM2070S1
     C                     ADD  1         RRN
     C*
     C                     END                             END RECID
     C*
     C                     END                             ENDDOU
     C*
     C*    READ ANOTHER TIME TO DETECT END OF FILE
     C           *IN03     IFNE '1'
     C           DMSKEY    READEDEAMS                    03
     C                     END
     C*
     C           RRN       IFEQ 1                          NO RECORD LOADED
     C                     MOVE '1'       *IN59            PROTECT ZONE
     C                     MOVE *BLANKS   DDVDAT
     C                     MOVEL*BLANKS   DDDASN                       BER
     C                     MOVE *BLANKS   DDAMTP
     C                     MOVE *BLANKS   DDDAAM
     C                     MOVE *BLANKS   DDBRT2
     C                     MOVE *BLANKS   DDRTS2
     C                     WRITEPM2070S1
     C                     MOVE '0'       *IN59
     C                     END
     C*
     C                     ENDSR
     C*
     C*
      ********************************************************************
      /EJECT
      /COPY ZSRSRC,ZDATE2Z2
      /EJECT
      /COPY ZSRSRC,ZSEDITZ3
      /EJECT
      ********************************************************************
      *                                                                  *
      * *PSSR    - PROCESS DATABASE ERROR                                *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      **PMC1000**-*UPDATE*GROUP*JOB*DATA*AREA*****************************S01486
      **PMC1020**-*TERMINATE*GROUP*JOB*IF*DATABASE*ERROR******************S01486
      * PMC1001  - TO UPDATE DATA AREA *LDA                              *S01486
      * DBERRCTL - DATABASE ERROR PROCESSING                             *S01486
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * ACCESS   - ACCESS DEALS DETAILS                                  *
      * LOADSF   - LOAD SUBFILE                                          *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      **WWPARM***-*GROUP*JOB*DATA*STRUCTURE*******************************S01486
      **WWEND****-*GROUP*JOB*TERMINATION*VALUE****************************S01486
      **WWGRPJ***-*GROUP*JOB*VALUE****************************************S01486
      *                                                                  *
      ********************************************************************
     C           *PSSR     BEGSR
     C*
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVEL'Y'       QMDBER
     C                     MOVEL'PM2070'  QMPGM
     C                     MOVEL'Y'       QMENQT
     C*
     C*****UPDATE**GDA*************************************************** S01486
     C***********          CALL 'PMC1002'                                 S01486
     C***********          PARM           WWGDA                           S01486
     C                     CALL 'PMC1001'                                 S01486
     C                     PARM           WWLDA                           S01486
     C*
     C                     DUMP
     C*
     C***********          CALL 'PMC1020'                                 S01486
     C                     CALL 'DBERRCTL'                                S01486
     C                     RETRN                                          S01486
     C**
     C                     ENDSR
     C**
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #INIT    - INITIAL PROCESSING                                    *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      **PMC1010**-*READ*GROUP*JOB*DATA*AREA*******************************S01486
      **QCMDEXC**-*EXEC*AS/400*COMMAND************************************S01486
      * PMC1003  - TO RETRIEVE DATA AREA *LDA                            *S01486
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN PROCESSING                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWDLNO   - DEAL NUMBER PARAMETER PASSED TO PROGRAM               *
      * WWRETC   - RETURN CODE FROM PM2100                               *
      **WWPARM***-*GROUP*JOB*DATA*STRUCTURE*******************************S01486
      **WWFLD****-*COMMAND*PART*OF*QCMDEXEC*PARAMETER*********************S01486
      **WWNUMB***-*LENGHT*PART*OF*QCMDEXEC*PARAMETER**********************S01486
      * WWVDSV   - CURRENT PAGE DEAMS VALUE DATE                         *
      * WWSNSV   - CURRENT PAGE DEAMS SEQUENCE NO                        *
      *                                                                  *
      ********************************************************************
     C           #INIT     BEGSR
     C*
     C           *ENTRY    PLIST
     C                     PARM           WWDEAL  6
     C                     MOVE WWDEAL    WWDLNO  60
     C*
     C*    INITIALISE SUBFILE
     C                     Z-ADD0         RRN     30
     C*
     C*    CLEAR SUBFILE
     C                     MOVE '1'       *IN61
     C                     WRITEPM2070C1
     C                     MOVE '0'       *IN61
     C*
     C                     MOVEA'0'       *IN
     C                     MOVE '0'       *INKC
     C                     MOVE '0'       *INKL
     C                     MOVE '0'       *INKV                           S01486
     C                     MOVE '0'       WWRETC  1
     C                     Z-ADD1         X       20
     C*
     C*****RECEIVE*PARAMETERS*FROM*DATA*AREA**GDA**********************   S01486
     C**  Receive parameters from data area *LDA                          S01486
     C                     CALL 'PMC1003'
     C                     PARM           WWLDA
     C                     MOVELQMRUNA    DDRUNA
     C*
     C*****IF*ATTENTION*KEY*ENABLED*SET*THE*ATTENTION*PROGRAM*TO********* S01486
     C*****PMC0610******************************************************* S01486
     C***********QMATTN    IFEQ 'Y'                                       S01486
     C***********          MOVEAATN       WWFLD  22                       S01486
     C***********          Z-ADD22        WWNUMB 155                      S01486
     C***********          CALL 'QCMDEXC'                                 S01486
     C***********          PARM           WWFLD                           S01486
     C***********          PARM           WWNUMB                          S01486
     C***********                                                         S01486
     C***********          END                                            S01486
     C*
     C*    INIT KEY LIST
     C           DMSKEY    KLIST                           DEAMS KEY
     C                     KFLD           WWDLNO           (TRUNCATED)
     C           DMSKY2    KLIST                           DEAMS KEY
     C                     KFLD           WWDLNO           (COMPLETE)
     C                     KFLD           WWVDSV
     C                     KFLD           WWSNSV
     C*
     C                     ENDSR
     C** **  ATN - ARRAY TO ENABLE ATTN KEY****************************** S01486
     C** SETATNPGM PGM(PMC0610) ***************************************** S01486
     C*** ARRAY DTYPA BEFORE 74286. 'TL' CHANGED INTO 'TI'                074286
     C*** N.B. 'TL' IS DRS DEAL TYPE ON MMDELDPP. ON DEALSDC DTYP = 'TI'  074286
     C*** ITIPCDCLLNTDDLTLCI                                              074286
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**  ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**  ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**  ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  DTYPA - DEAL TYPE
ITIPCDCLLNTDDLTICIFLDPLPFTDTLT                                     074286 CDL006
**  DTYPB - DEAL TYPE NARRATIVE
DEPOSIT TAKEN
DEPOSIT PLACED
CALL/NOTICE DEPOSIT
CALL/NOTICE DEPOSIT PLACED
COMMERCIAL LOAN
TIME DEPOSIT
DEMAND LOAN
TIME LOAN
CD ISSUED
FIDUCIARY DEPOSIT TERM PLACING                                            CDL006
FIDUCIARY DEPOSIT CALL PLACING                                            CDL006
FIDUCIARY LOAN CALL PLACING                                               CDL006
FIDUCIARY DEPOSIT TERM TAKING                                             CDL006
FIDUCIARY DEPOSIT CALL TAKING                                             CDL006
FIDUCIARY LOAN CALL TAKING                                                CDL006
**  DAMTP - AMENDMENT TYPE
PIPDSCCISI
**  AMTPB - AMENDMENT TYPE NARRATIVE
PRINCIPAL INCREASE
PRINCIPAL DECREASE
RATE CHANGE
CAPITALIZE INTEREST
INTEREST PAYMENT DATE
