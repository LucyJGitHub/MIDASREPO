     H        1                                                           S01486
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Reconcile PM Pos. w/ CAPRHD Actions')
/*OVRF*: OVRDBF FILE(PMAPOSLX) TOFILE(PMAPOSLL) SHARE(*NO)          : *
     H***********                                                         S01486
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management Module                      *
     F*                                                               *
     F*  PM4030 - Reconcile PM Positions with CAPRHD Actions          *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CSE071             Date 19Jul05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 096640             Date 02Jul96               *
      *                 S01486             Date 11Aug94               *
     F*                 061063             DATE 24SEP93               *
     F*                 050894             DATE 01JUN93               *
     F*                 46080              DATE 19OCT92               *
     F*                 B08194             DATE 12AUG91               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
     F*  096640 - Apply 067409 - Do not consider records for matured  *
     F*           securities and intermediary records if position in  *
     F*           CAPRHD does not exist (due to portfolio transfer)   *
     F*  S01486 - Portfolio Management Upgrade to Release 10.         *
     F*  061063 - ONLY CHECK ACTIONS AFTER BACKVALUE DATE             *
     F*  050894 - USE 'RETENTION DATE' TO AVOID TO MANAGE DROPPED REC.*
     F*  46080  - CHECK PORTFOLIO INDICATOR RATHER THAN AP NUM CHANGE *
     F*           CHECK 'MPHAS' TO CALCULATE EVENT CONTROL DATE       *
     F*           CHECK PMSTAT TO TAKE EOM DATE                       *
     F*  B08194 - TIDY UP OF NEW RECONCILIATION PROGRAMS              *
     F*                                                               *
     F*****************************************************************
     F*****************************************************************
     F***CAPRHP**IF**E           K        DISK                            S01486
     FPMCAPRL5IF  E           K        DISK         KINFSR *PSSR          S01486
     FPMAPOSLLIF  E           K        DISK
     FPMAPOSLXIF  E           K        DISK
     F            PMCPOSP0                          KRENAMEPMCPOSX0
     FSECTY   IF  E           K        DISK                               096640
     F***TABSE***IF**E           K        DISK                     050894 S01486
     F************TABLETAF                          KIGNORE        050894 S01486
     F************TABTB11F                          KIGNORE        050894 S01486
     F************TABTE10F                          KIGNORE        050894 S01486
     F************TABTE20F                          KIGNORE        050894 S01486
     F************TABTG10F                          KIGNORE        050894 S01486
     F************TABTG20F                          KIGNORE        050894 S01486
     F************TABLETHF                          KIGNORE        050894 S01486
     F************TABLETJF                          KIGNORE        050894 S01486
     F************TABLETKF                          KIGNORE        050894 S01486
     F************TABLETLF                          KIGNORE        050894 S01486
     F************TABLETNF                          KIGNORE        050894 S01486
     F************TABLETPF                          KIGNORE        050894 S01486
     F************TABLETRF                          KIGNORE        050894 S01486
     F************TABLETXF                          KIGNORE        050894 S01486
     F************TABLET2F                          KIGNORE        050894 S01486
     F************TABLET3F                          KIGNORE        050894 S01486
     F************TABLET5F                          KIGNORE        050894 S01486
     F************TABTB40F                          KIGNORE        050894 S01486
     F***SDBANKPDIF**E                    DISK                            S01486
     F***PM4030P1O***E                    PRINTER                         S01486
     F*                                                                   S01486
     F** Reconcile Historic Portfolio Security Positions Audit List       S01486
     FPM4030P1O   E             25     PRINTER      KINFDS SPOOL      UC  S01486
     F*                                                                   S01486
     F** Reconcile Historic Portfolio Security Positions Run Controls     S01486
     FPM4030AUO   E             26     PRINTER      KINFDS SPOOLU         S01486
     F*                                                                   S01486
     F*****************************************************************   S01486
     F/EJECT                                                              S01486
     E*****************************************************************   S01486
     E*                                                                   S01486
     E** Array for COPYRIGHT                                              S01486
     E                    CPY@    1   1 80                                S01486
     E*****************************************************************   S01486
     E/EJECT                                                              S01486
     I*****************************************************************   S01486
     I*
     I*  RENAME FIELDS FROM PMCPOSOT
     IPMCPOSX0
     I              QARECI                          QXRECI
     I              QACNUM                          QXCNUM
     I              QAPTFR                          QXPTFR
     I              QATDSS                          QXTDSS
     I              QATVDA                          QXTVDA
     I              QAPDAT                          QXPDAT
     I              QANOML                          QXNOML
     I              QACSEX                          QXCSEX
     I              QAAPNM                          QXAPNM
     I              QAFXAP                          QXFXAP
     I              QAPILP                          QXPILP
     I              QAACDI                          QXACDI
     I              QABVLD                          QXBVLD
     I              QATNMC                          QXTNMC
     I              QATNMD                          QXTNMD
     I              QAPTCY                          QXPTCY
     I              QAPCDP                          QXPCDP
     I              QAAYNM                          QXAYNM
     ISECTYDF                                                             096640
     I              RECI                            SRECI                 096640
     I*
     I            DS                                                      050894
     I                                        1 256 WWDATE                050894
     I                                        6  100WWRDAT                061063
     I                                       36  400HKDAT                 050894
     I*                                                                   061063
     I** LOCAL DATA AREA FOR DATABASE ERROR DETAILS                       061063
     I***LDA********UDS***********************     256             061063 S01486
     ILDA         DS                            256                       S01486
     I                                      132 183 DBLOT                 061063
     I                                      132 141 DBFILE                061063
     I                                      142 170 DBKEY                 061063
     I                                      171 180 DBPGM                 061063
     I                                      181 1830DBASE                 061063
     I*                                                                   S01486
     I** Data structure for compilation - COPYRIGHT insertion             S01486
     ICPYR@$      DS                                                      S01486
     I                                        1  80 CPY@                  S01486
     I                                        1  20 CPY@$$                S01486
     I*                                                                   S01486
     IRUNDAT    E DS                                                      S01486
     I** Standard data area layout for system flags and run date          S01486
     I*                                                                   S01486
     ISDBANK    E DSSDBANKPD                                              S01486
     I**  Data structure for Bank details                                 S01486
     I*                                                                   S01486
     ISDSTRD    E DSSDSTRDPD                                              S01486
     I**  Data structure for Securities Trading ICD                       S01486
     I*                                                                   S01486
     IDSFDY     E DSDSFDY                                                 S01486
     I**  Short data structure for access objects                         S01486
     I*                                                                   S01486
     IDSSDY     E DSDSSDY                                                 S01486
     I**  Long data structure for access programs                         S01486
     I*                                                                   S01486
     ISPOOL       DS                                                      S01486
     I                                       83  92 SFILE                 S01486
     I                                    B 123 1240SFNUM                 S01486
     I                                    B 367 3680LINE                  S01486
     ISPOOLU      DS                                                      S01486
     I                                       83  92 SFILEU                S01486
     I                                    B 123 1240SFNUMU                S01486
     I                                    B 367 3680LINE1                 S01486
     I*                                                                   S01486
     I*****************************************************************   S01486
     I/EJECT                                                              S01486
     C*****************************************************************   S01486
     C*
     C***KLIST*TO*ACCESS*CAPRHP****************************************   S01486
     C** KLIST TO ACCESS PMCAPRL5                                         S01486
     C           KCAPR     KLIST
     C                     KFLD           XCSCN
     C                     KFLD           XPTFR
     C                     KFLD           XCSSC
     C                     KFLD           XCSDA
     C                     KFLD           XCSSE
     C*
     C***KLIST*FOR*READ*EQUAL*ON*CAPRHP********************************   S01486
     C** KLIST FOR READ EQUAL ON PMCAPRL5                                 S01486
     C           KCPRE     KLIST
     C                     KFLD           XCSCN
     C                     KFLD           XPTFR
     C                     KFLD           XCSSC
     C*
     C** KLIST TO ACCESS PMAPOSLX
     C           KAPOSX    KLIST
     C                     KFLD           QXCNUM
     C                     KFLD           QXPTFR
     C                     KFLD           QXTDSS
     C                     KFLD           QXTVDA
     C                     KFLD           WWPDAT
     C*
     C** KLIST TO ACCESS PMAPOSLX
     C           KAPOSE    KLIST
     C                     KFLD           QXCNUM
     C                     KFLD           QXPTFR
     C                     KFLD           QXTDSS
     C                     KFLD           QXTVDA
     C*                                                                   S01486
     C**  Copyright statement                                             S01486
     C*                                                                   S01486
     C                     MOVEACPY@      ACT@   80                       S01486
     C*                                                                   S01486
     C           *NAMVAR   DEFN           LDA                             S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     MOVE *BLANKS   DBFILE                          S01486
     C                     MOVE *BLANKS   DBKEY                           S01486
     C                     MOVEL'PM4030'  DBPGM                           S01486
     C                     Z-ADD0         DBASE                           S01486
     C                     OUT  LDA                                       S01486
     C*                                                                   S01486
     C** Initialise work fields                                           S01486
     C*                                                                   S01486
     C                     Z-ADD*ZEROS    WNORE   50                      S01486
     C                     Z-ADD*ZEROS    LINE                            S01486
     C                     Z-ADD*ZEROS    LINE1                           S01486
     C                     MOVE 'N'       WRECF   1                       S01486
     C                     MOVE '0'       *IN24                           S01486
     C*                                                                   S01486
     C** Access RUNDAT for multibranching indicator                       S01486
     C*                                                                   S01486
     C           *NAMVAR   DEFN           RUNDAT                          S01486
     C                     IN   RUNDAT                                    S01486
     C           AGMBIN    IFEQ 'Y'                                       S01486
     C                     MOVE '1'       *IN27                           S01486
     C                     END                                            S01486
     C*                                                                   050894
     C** OBTAIN LAST EVENT CONTROL DATE                                   050894
     C*                                                                   050894
     C           *NAMVAR   DEFN           PMSTAT256                       050894
     C                     IN   PMSTAT                                    050894
     C                     MOVE PMSTAT    WWDATE                          050894
     C*                                                                   S01486
     C** Ensure Audit Spool File recorded by RCF                          S01486
     C*                                                                   S01486
     C                     Z-ADDSFNUMU    ZSNUM2  60                      S01486
     C                     CALL 'ZSFILE'                                  S01486
     C                     PARM           SEQ     5                       S01486
     C                     PARM *BLANK    @PARM   3                       S01486
     C                     PARM           SFILEU                          S01486
     C                     PARM           ZSNUM2                          S01486
     C                     PARM           ZSERR                           S01486
     C           *LOCK     IN   LDA                                       S01486
     C*                                                                   S01486
     C** If an error occurred during ZSFILE processing,                   S01486
     C** Return to the calling program.                                   S01486
     C*                                                                   S01486
     C           ZSERR     IFEQ 'Y'                                       S01486
     C                     SETON                     U7U8LR               S01486
     C                     RETRN                                          S01486
     C                     END                                            S01486
     C***********                                                         S01486
     C***OTAIN*MIDAS*RUN*DATE******************************************   S01486
     C***********1         SETLLSDBANKPD                                  S01486
     C***********          READ SDBANKPD                 60               S01486
     C*                                                                   S01486
     C** Get bank details using access object                             S01486
     C*                                                                   S01486
     C                     CALL 'AOBANKR0'                                S01486
     C                     PARM *BLANKS   P@RTCD  7                       S01486
     C                     PARM '*FIRST ' P@OPTN  7                       S01486
     C           SDBANK    PARM SDBANK    DSFDY                           S01486
     C*                                                                   S01486
     C** Check if an error occurred                                       S01486
     C*                                                                   S01486
     C           P@RTCD    IFNE *BLANKS                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     Z-ADD1         DBASE            ***************S01486
     C                     MOVE 'SDBANKPD'DBFILE           * DBERROR  001 S01486
     C                     MOVE *BLANKS   DBKEY            ***************S01486
     C                     OUT  LDA                                       S01486
     C                     EXSR #LDBER                                    S01486
     C                     END                                            S01486
     C*
     C* Get Installation Control Data record for Securities trading       050894
     C*                                                                   050894
     C                     CALL 'AOSTRDR0'                                S01486
     C                     PARM *BLANKS   P@RTCD                          S01486
     C                     PARM '*FIRST ' P@OPTN                          S01486
     C           SDSTRD    PARM SDSTRD    DSSDY                           S01486
     C*                                                                   S01486
     C** Check if an error occurred                                       S01486
     C*                                                                   S01486
     C           P@RTCD    IFNE *BLANKS                                   S01486
     C           *LOCK     IN   LDA                                       S01486
     C                     Z-ADD3         DBASE            ***************S01486
     C                     MOVE 'SDSTRDPD'DBFILE           * DBERROR  003 S01486
     C                     MOVE *BLANKS   DBKEY            ***************S01486
     C                     OUT  LDA                                       S01486
     C                     EXSR #LDBER                                    S01486
     C                     END                                            S01486
     C*                                                                   S01486
     C***********          EXSR ZICDSE                             050894 S01486
     C***********                                                  050894 S01486
     C**If*error*in*ZICDSE,*abend*program**************************050894 S01486
     C***********                                                  050894 S01486
     C************IN99     IFEQ '1'                 ***************050894 S01486
     C***********          MOVE '02'      DBASE     **DB ERROR 02**050894 S01486
     C***********          MOVEL'TABLE'   DBFILE    ***************050894 S01486
     C***********          MOVELZTABKY    DBKEY                    050894 S01486
     C***********          SETON                     U7U8LR        050894 S01486
     C***********          RETRN                                   050894 S01486
     C***********          END                                     050894 S01486
     C****                                                          050894061063
     C**Calculate Retention date                                    050894061063
     C****                                                          050894061063
     C****       31        MULT RTST      WK3N    30                050894061063
     C****       HKDAT     SUB  WK3N      RTNTDT  50                050894061063
     C*                                                                   061063
     C** CALCULATE BACKVALUE DATE                                         061063
     C***********WWRDAT    SUB  BKVP      RTNTDT  50               061063 S01486
     C           WWRDAT    SUB  BVBVP     RTNTDT  50                      S01486
     C*                                                                   050894
     C** READ THROUGH PMAPOSLL CHECKING POSITIONS
     C***********          WRITEHEADERF                                   S01486
     C                     READ PMAPOSLL                 60
     C*
     C           *IN60     DOWEQ'0'                        B1
     C*                                                                   096640
     C* DO NOT PROCESS IF THE SECURITY IS NOT LIVE                        096640
     C*                                                                   096640
     C           QATDSS    CHAINSECTYDF              49                   096640
     C           *IN49     IFEQ '0'                                       096640
     C           SRECI     ANDEQ'D'                                       096640
     C                     ADD  1         WNORE                           S01486
     C                     MOVE 'N'       WWERRF  1
     C                     Z-ADD0         WWNOML 150
     C*
     C** IF LIVE OR INTERIM SAVE NOMINAL OB CHECK NUMBER OF RECORDS
     C           QARECI    IFNE 'H'                        B2
     C           QAPDAT    ANDGERTNTDT                                    050894
     C*
     C** CHECK IF HISTORIC POSITION EXISTS ON FILE PMAPOSLX
     C                     MOVE QACNUM    QXCNUM
     C                     MOVE QAPTFR    QXPTFR
     C                     MOVE QATDSS    QXTDSS
     C                     MOVE QATVDA    QXTVDA
     C***                  Z-ADD0         WWPDAT  50                      061063
     C                     Z-ADDQAPDAT    WWPDAT  50                      061063
     C*
     C***        KAPOSX    SETLLPMAPOSLX                                  050894
     C***        KAPOSE    READEPMAPOSLX                 61               050894
     C           KAPOSX    SETLLPMAPOSLX                                  050894
     C           KAPOSE    REDPEPMAPOSLX                 61               050894
     C*
     C** IF FIRST RECORD READ HISTORIC POSITION SAVE NOMINAL
     C                     Z-ADD0         WWPDAT  50                      B08194
     C           QXRECI    IFEQ 'H'                        B3
     C           *IN61     ANDEQ'0'
     C           QXRECI    OREQ 'I'                        B3             050894
     C           *IN61     ANDEQ'0'                                       050894
     C                     Z-ADDQXNOML    WWNOML
     C                     Z-ADDQXPDAT    WWPDAT                          B08194
     C                     END                             E3
     C*
     C** READ THROUGH ALL ACTIONS FOR POSITION ON CAPRHD
     C**********           MOVE QACNUM    XCSCN   60                                          CSD027
     C                     MOVE QACNUM    XCSCN   6                                           CSD027
     C                     MOVE QAPTFR    XPTFR   4
     C                     MOVE QATDSS    XCSSC  10
     C                     Z-ADD0         XCSDA   50
     C                     MOVE *LOVAL    XCSSE   1
     C*
     C***********KCAPR     SETLLCAPRHP                                    S01486
     C***********          READ CAPRHP                   61               S01486
     C           KCAPR     SETLLPMCAPRL5                                  S01486
     C                     READ PMCAPRL5                 61               S01486
     C*
     C** READ THROUGH ALL HISTORIC ACTIONS AND CALCULATE NOMINAL
     C           *IN61     DOWEQ'0'                        B3
     C           CSCN      ANDEQXCSCN
     C           PTFR      ANDEQXPTFR                                     B08194
     C           CSSC      ANDEQXCSSC
     C           CSDA      ANDLEQAPDAT
     C*                                                                   B08194
     C           CSDA      IFGT WWPDAT                     B4             B08194
     C*
     C** ADD TRADE NOMINAL TO POSITION
     C           CSOA      IFEQ 'TP'                       B5             B08194
     C           CSOA      OREQ 'TS'
     C*
     C           QATVDA    IFEQ 'T'                        B6             B08194
     C***        CSTB      ANDNE0
     C***        PMTI      ANDNE'*'                                       46080
     C           PMTI      ANDEQ'H'                                       46080
     C           QATVDA    OREQ 'V'
     C***        CSVB      ANDNE0
     C***        PMVI      ANDNE'*'                                       46080
     C           PMVI      ANDEQ'H'                                       46080
     C*
     C           CSOA      IFEQ 'TP'                       B7             B08194
     C           WWNOML    SUB  CSNL      WWNOML
     C                     ELSE
     C                     ADD  CSNL      WWNOML
     C                     END                             E7             B08194
     C*
     C                     END                             E6             B08194
     C*
     C                     END                             E5             B08194
     C*
     C** ADD DEPOTMOVEMENT NOMINAL TO POSITION
     C           CSOA      IFEQ 'CI'                       B5             B08194
     C           CSOA      OREQ 'CO'
     C*
     C           CSOA      IFEQ 'CO'                       B6             B08194
     C           WWNOML    SUB  CSNL      WWNOML
     C                     ELSE
     C                     ADD  CSNL      WWNOML
     C                     END                             E6             B08194
     C*
     C                     END                             E5             B08194
     C*                                                                   B08194
     C                     END                             E4             B08194
     C*
     C***********KCPRE     READECAPRHP                   61               S01486
     C           KCPRE     READEPMCAPRL5                 61               S01486
     C                     END                             E3
     C*
     C** IF ERROR OUTPUT DETAILS TO ERROR REPORT
     C           QANOML    IFNE WWNOML                     B3
     C           CSCN      ANDEQXCSCN                                     096640
     C           PTFR      ANDEQXPTFR                                     096640
     C           CSSC      ANDEQXCSSC                                     096640
     C                     MOVE QARECI    RRRECI
     C                     MOVE QACNUM    RRCNUM
     C                     MOVE QAPTFR    RRPTFR
     C                     MOVE QATDSS    RRTDSS
     C                     MOVE QAPDAT    RRPDAT
     C                     MOVE QATVDA    RRTVDA
     C                     Z-ADDWWNOML    RREXPS
     C                     Z-ADDQANOML    RRACPS
     C*
     C                     MOVE ' '       RREXSG
     C           WWNOML    IFLT 0
     C                     MOVE '-'       RREXSG
     C                     END
     C*
     C                     MOVE ' '       RRACSG
     C           QANOML    IFLT 0
     C                     MOVE '-'       RRACSG
     C                     END
     C*                                                                   S01486
     C** At least one record to be output on report                       S01486
     C*                                                                   S01486
     C                     MOVE 'Y'       WRECF                           S01486
     C*                                                                   S01486
     C** OPEN detail printer file                                         S01486
     C*                                                                   S01486
     C           *IN24     IFEQ '0'                                       S01486
     C                     OPEN PM4030P1                                  S01486
     C                     MOVE '1'       *IN24                           S01486
     C*                                                                   S01486
     C** Ensure Report Spool File recorded by RCF                         S01486
     C*                                                                   S01486
     C                     Z-ADDSFNUM     ZSNUM1  60                      S01486
     C                     CALL 'ZSFILE'                                  S01486
     C                     PARM           SEQ                             S01486
     C                     PARM *BLANK    SENTY   3                       S01486
     C                     PARM           SFILE                           S01486
     C                     PARM           ZSNUM1                          S01486
     C                     PARM           ZSERR   1                       S01486
     C           *LOCK     IN   LDA                                       S01486
     C*                                                                   S01486
     C** If an error occurred during ZSFILE processing,                   S01486
     C** Return to the calling program.                                   S01486
     C*                                                                   S01486
     C           ZSERR     IFEQ 'Y'                                       S01486
     C                     SETON                     U7U8LR               S01486
     C                     RETRN                                          S01486
     C                     END                                            S01486
     C*                                                                   S01486
     C** Output heading                                                   S01486
     C*                                                                   S01486
     C                     WRITEHEADERF                                   S01486
     C*                                                                   S01486
     C                     END                                            S01486
     C*                                                                   S01486
     C** Output heading in case of overflow                               S01486
     C*                                                                   S01486
     C           LINE      IFGT 55                                        S01486
     C                     WRITEHEADERF                                   S01486
     C                     END                                            S01486
     C*                                                                   S01486
     C** Write detail line to incorrect portfolio positions report        S01486
     C*                                                                   S01486
     C                     WRITEERRORF                                    S01486
     C*
     C***********          WRITEERRORF                 62                 S01486
     C***********                                                         S01486
     C************IN62     IFEQ '1'                        B4             S01486
     C***********          WRITEHEADERF                                   S01486
     C***********          END                             E4             S01486
     C                     END                             E3
     C                     END                             E2
     C                     END                                            096640
     C*
     C                     READ PMAPOSLL                 60
     C                     END                             E1
     C*                                                                   S01486
     C** Output heading in case of overflow                               S01486
     C*                                                                   S01486
     C           LINE      IFGT 55                                        S01486
     C           *IN24     ANDEQ'1'                                       S01486
     C                     WRITEHEADERF                                   S01486
     C                     END                                            S01486
     C*                                                                   S01486
     C           *IN24     IFEQ '1'                                       S01486
     C                     WRITETRAILF                                    S01486
     C                     END                                            S01486
     C*                                                                   S01486
     C                     Z-ADDWNORE     SNORE                           S01486
     C*                                                                   S01486
     C** Produce Run Controls                                             S01486
     C*                                                                   S01486
     C                     WRITEHEADAU                                    S01486
     C                     WRITECONTROL                                   S01486
     C*                                                                   S01486
     C** No details to report.                                            S01486
     C*                                                                   S01486
     C           WRECF     IFEQ 'N'                                       S01486
     C                     WRITENODTLS                                    S01486
     C                     END                                            S01486
     C*
     C***********          WRITETRAILF                 62                 S01486
     C                     SETON                     LR
     C                     RETRN                                          S01486
     C*                                                                   S01486
     C********************************************************************
     C*****SUBROUTINE-*ZICDSE************************************* 050894 S01486
     C*****ACCESS*ICD*RECORD*FROM*TABTB36*FOR*SECURITIES*TRADING** 050894 S01486
     C************************************************************ 050894 S01486
     C***1.TABTB36*MUST*BE*DECLARED*IN*THE*PROGRAM**************** 050894 S01486
     C***2.ON*RETURN*FROM*THIS*S/R*THE*PROGRAM*MUST*CHECK*U7/U8*-* 050894 S01486
     C*****AND*IF*ON*MUST*EXECUTE*STD*DATABASE*ERROR*ROUTINE****** 050894 S01486
     C***3.*IN99*IS*USED****************************************** 050894 S01486
     C************************************************************ 050894 S01486
     C************************************************************ 050894 S01486
     C************************************************************ 050894 S01486
     C***/COPY ZSRSRC,ZICDSEZ1                                            S01486
     C************************************************************ 050894 S01486
      /EJECT
     C*****************************************************************   S01486
     C*                                                               *   S01486
     C* #LDBER  - Database error processing                           *   S01486
     C*                                                               *   S01486
     C* Called by: Main                                               *   S01486
     C* Calls    : None                                               *   S01486
     C*                                                               *   S01486
     C*****************************************************************   S01486
     C           #LDBER    BEGSR                           ***  #LDBER ** S01486
     C*                                                                   S01486
     C                     WRITEHEADAU                                    S01486
     C                     WRITEDBERROR                                   S01486
     C*                                                                   S01486
     C           *IN24     IFEQ '1'                                       S01486
     C                     WRITEDBERR                                     S01486
     C                     END                                            S01486
     C*                                                                   S01486
     C                     EXSR *PSSR                                     S01486
     C*                                                                   S01486
     C                     ENDSR                                          S01486
     C*                                                                   S01486
     C*****************************************************************   S01486
     C*                                                               *   S01486
     C* *PSSR   - Subroutine to handle abnormal error conditions      *   S01486
     C*                                                               *   S01486
     C* Called by: Main                                               *   S01486
     C* Calls    : None                                               *   S01486
     C*                                                               *   S01486
     C*****************************************************************   S01486
     C           *PSSR     BEGSR                           ***  PSSR  *** S01486
     C*                                                                   S01486
     C           WRUN      IFEQ *BLANKS                                   S01486
     C                     MOVE 'Y'       WRUN    1                       S01486
     C                     DUMP                                           S01486
     C                     END                                            S01486
     C*                                                                   S01486
     C                     SETON                     U7U8LR               S01486
     C                     RETRN                                          S01486
     C*                                                                   S01486
     C                     ENDSR                                          S01486
     C*                                                                   S01486
     C********************************************************************
**  CPY@
(c) Finastra International Limited 2001
