     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Net Commitment by Currency Enquiry')          *
      *****************************************************************
      *                                                               *
      *  Midas - Portfolio Management Module                          *
      *                                                               *
      *  PM6020 - PM Net Commitment by Currency Enquiry               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CLE148             Date 23Jul12               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CPM005             Date 08Jul96               *
      *                 S01124             Date 13JUN94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CLE148 - Alpha Loan Reference (Recompile)                    *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CPM005 - Private Banking Phase II                            *
      *           Focus Group Changes Upgraded to DBA                 *
      *  S01124 - PBFG/7 - NET COMMITMENT BY CURRENCY                 *
      *                                                               *
      *****************************************************************
      *
      **  PREFIX IS 'FJ'
     FPMNTCMLLIF  E           K        DISK
     F                                              KINFSR *PSSR
      *
     F**PM6020DDCF  E                    WORKSTN      KINFDS DSPFDS       CPM005
     FPM6020DFCF  E                    WORKSTN      KINFDS DSPFDS         CPM005
     F                                        RRN1  KSFILE PM6020S1
      ******************************************************************
      *
      **  ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
      *
      /COPY ZSRSRC,ZSEDITZ1
      ******************************************************************
      *
      **  EXTERNAL FORMAT FOR GROUP DATA AREA
     I****WWGDA     E DSPMGRDAPP                                          CPM005
      *
     I****WWLDA     E DSPMLDAPP                                           CPM005
     IWWLDA     E DSPMLDAPD                                               CPM005
      *
      **  PROGRAM STATUS DATA STRUCTURE FOR WS/USER ID'S
     IPSDS       SDS
     I**********                            244 253 WSID                  CPM005
     I**********                            254 263 USID                  CPM005
     I                                      244 253 WKSTN                 CPM005
     I                                      254 263 USER                  CPM005
     IDSPFDS      DS
     I                                    B 378 3790CPFPOS
      *
      **  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      /COPY ZSRSRC,ZSEDITZ2
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INDEX OF SUBROUTINES                                             *
      * MAIN     - MAIN PROCESSING                                       *
      * #INIT    - SR INITIAL PROCESSING                                 *
      * #MAIN    - SR MAIN PROCESSING                                    *
      * #LOAD    - SR LOAD SUBFILE WITH DETAILS                          *
      * #TERM    - SR TERMINATION PROCESSING                             *
      * #WRIT    - SR WRITE SCREEN                                       *
      * #CALL    - SR TO CALL PM1701                                     *
      * *PSSR    - ERROR IN FILE                                         *
      *                                                                  *
      * EXTERNAL ROUTINES CALLED                                         *
      * PMC1003  - PGM RETRIEVE DATA AREA *LDA                           *
      **PMC1002**-*PGM*UPDATE*DATA*AREA**GDA******************************CPM005
      * PMC1001  - PGM UPDATE DATA AREA *LDA                             *
      * PM0701   - PGM POSITIONS FOR SELECTED CCY ENQUIRY                *
      **PMC1004**-*PGM*RETRIEVE*DATA*AREA**GDA****************************CPM005
      *                                                                  *
      ********************************************************************
     C                     EXSR #INIT
      *
     C                     EXSR #MAIN
      *
     C                     EXSR #TERM
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #INIT    - SR INITIAL PROCESSING                                 *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * PMC1003  - PGM RETRIEVE DATA AREA *LDA                           *
      * ZSEDIT   -                                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN PROCESSING                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWFLAG   - DISPLAY SCREEN                                        *
      * WWABCV   - CURRENT VALUE IN ABS FOR HEADER                       *
      * WWCAPC   - CAPITAL CHANGE IN ABS                                 *
      * WWFFOR   - CAPITAL CHANGE IN ABS                                 *
      * WWTOCV   - TOTAL CURRENT VALUE FOR HEADER                        *
      * WWWORK   - WORK FIELD FOR CALCULATION                            *
      * WWCOMT   - NET COMMITMENT FOR HEADER                             *
      * WWLDA    - GROUP DATA AREA                                       *
      * WWFLD2   - WORK FIELD FOR NET COMMITMENT                         *
      * WWFLD3   - WORK FIELD FOR NET COMMITMENT                         *
      *                                                                  *
      ********************************************************************
     C           #INIT     BEGSR
      *
      **  ACCESS PMNTCMLL
     C           PMNTC1    KLIST
     C**********           KFLD           K1CNUM  60                                          CSD027
     C                     KFLD           K1CNUM  6                                           CSD027
     C                     KFLD           K1PTFR  4
     C                     KFLD           K1CURR  3
      *
      **  INITIALIZE FIELD FOR ZSEDIT
     C                     MOVEL'J'       ZECODE
      *
      **  INITIALIZE WORK FIELDS
     C                     MOVEL*BLANK    WWFLAG  1
     C                     Z-ADD*ZEROS    WWABCV 150
     C                     Z-ADD*ZEROS    WWCAPC 150
     C                     Z-ADD*ZEROS    WWFFOR 150
     C                     Z-ADD*ZEROS    WWTOCV 150
     C                     Z-ADD*ZEROS    WWWORK 150
     C                     Z-ADD*ZEROS    WWCOMT 150
      *
      **  RETRIEVE DATA AREA
     C                     CALL 'PMC1003'
     C                     PARM           WWLDA
      *
      **  LOAD FIELDS FOR SCREEN HEADER
     C                     MOVELQMRUNA    DDRUNA
     C                     MOVE QMCNUM    DDCNUM
     C                     MOVELQMCRNM    DDCRNM
     C                     MOVELQMPTF2    DDPTFR
     C                     MOVELQMPTFN    DDPTFN
     C                     MOVELQMPTCY    DDCCY1
     C                     MOVELQMPTCY    DDCCY2
     C                     MOVELQMPTCY    DDPMCU
      *
      **  LOAD ALL RECORDS WITH SAME CUSTOMER,PORTFOLIO
     C**********           Z-ADDQMCNUM    K1CNUM                                              CSD027
     C                     MOVE QMCNUM    K1CNUM                                              CSD027
     C                     MOVELQMPTF1    K1PTFR
     C                     MOVEL*BLANKS   K1CURR
      *
      ** ACCESS TO PMNTCMLL TO CALCULATE CURRENT VALUE & NET COMMITMENT
     C           PMNTC1    SETLLPMNTCMLL
     C                     READ PMNTCMLL                 70
      *
     C           *IN70     DOWEQ*OFF                       *BW1
     C           FJCNUM    ANDEQQMCNUM
     C           FJPTFR    ANDEQQMPTF1
      *
      **  CALCULATE TOTAL CURRENT VALUE
     C                     Z-ADDFJCVAL    WWABCV
      *
     C                     ADD  WWABCV    WWTOCV
      *
      **  CALCULATE NET COMMITMENT
     C                     Z-ADDFJCAPC    WWCAPC
     C                     Z-ADDFJFFOR    WWFFOR
     C           WWABCV    ADD  WWCAPC    WWFLD2 150
     C           WWFLD2    ADD  WWFFOR    WWFLD3 150
     C                     ADD  WWFLD3    WWCOMT
      *
     C                     READ PMNTCMLL                 70
      *
     C                     ENDDO                           *EW1
      *
      **  EDIT TOTAL CURRENT VALUE IN ABS
     C                     Z-ADD*ZEROS    ZFLD15
     C                     Z-ADDWWTOCV    ZFLD15
     C                     Z-ADDQMPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDCUVL
      *
      **  EDIT TOTAL NET COMMITMENT IN ABS
     C                     Z-ADD*ZEROS    ZFLD15
     C                     Z-ADDWWCOMT    ZFLD15
     C                     Z-ADDQMPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DDNTCO
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #MAIN    - SR MAIN PROCESSING                                    *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * #LOAD    - SR LOAD SUBFILE WITH DETAILS                          *
      * #WRIT    - SR WRITE SCREEN                                       *
      **PMC1002**-*PGM*UPDATE*DATA*AREA**GDA******************************CPM005
      * PMC1001  - PGM UPDATE DATA AREA *LDA                              CPM005
      * #CALL    - SR TO CALL PM1701                                     *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN PROCESSING                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWFLAG   - DISPLAY SCREEN                                        *
      **WWGDA****-*GROUP*DATA*AREA****************************************CPM005
      *                                                                  *
      ********************************************************************
     C           #MAIN     BEGSR
      *
     C                     EXSR #LOAD
      *
      **  WRITE SCREEN
     C           WWFLAG    DOUEQ'Y'                        *BW1
     C           QMENQT    OREQ 'Y'
     C                     MOVEL'N'       WWFLAG
     C                     EXSR #WRIT
      *
      **  IF F3 TAKEN
     C           *INKC     IFEQ *ON                        *B1
     C                     MOVEL'Y'       QMENQT
      *
      **  UPDATE DATA AREA
     C***********          CALL 'PMC1002'                                 CPM005
     C***********          PARM           WWGDA                           CPM005
     C                     CALL 'PMC1001'                                 CPM005
     C                     PARM           WWLDA                           CPM005
     C                     END                             *E1
      *
      **  IF F12 TAKEN
     C           *INKL     IFEQ *ON                        *B1
     C                     MOVEL'Y'       WWFLAG
     C                     MOVEL'P'       QMENQT
     C***********          CALL 'PMC1002'                                 CPM005
     C***********          PARM           WWGDA                           CPM005
     C                     CALL 'PMC1001'                                 CPM005
     C                     PARM           WWLDA                           CPM005
     C                     END                             *E1
      *                                                                   CPM005
      **  IF F22 TAKEN                                                    CPM005
     C           *INKW     IFEQ '1'                                       CPM005
     C**                                                                  CPM005
     C           QMACTP    IFEQ *BLANKS                                   CPM005
     C           QMACTP    OREQ 'PM6020'                                  CPM005
     C           QMACTP    OREQ 'PM0701'                                  CPM005
     C**                                                                  CPM005
     C                     MOVEL'PM6020'  QMACTP    P                     CPM005
     C                     CALL 'PMC1001'                                 CPM005
     C                     PARM           WWLDA                           CPM005
     C**                                                                  CPM005
     C                     CALL 'PMC0610'                                 CPM005
     C**                                                                  CPM005
     C                     CALL 'PMC1003'                                 CPM005
     C                     PARM           WWLDA                           CPM005
     C**                                                                  CPM005
     C           QMENQT    IFEQ 'C'                                       CPM005
     C           QMENQT    OREQ 'P'                                       CPM005
     C           QMENQT    OREQ 'Y'                                       CPM005
     C                     MOVE 'Y'       WWFLAG                          CPM005
     C                     END                                            CPM005
     C**                                                                  CPM005
     C                     ELSE                                           CPM005
     C                     RETRN                                          CPM005
     C                     END                                            CPM005
     C**                                                                  CPM005
     C                     END                                            CPM005
      *
      **  IF SELECTION REQUESTED
     C           *IN60     IFEQ *OFF                       *B1
     C                     READCPM6020S1                 73
      *
      ** IF ANY RECORD SELECTED
     C           *IN73     IFEQ *OFF                       **B2
      *                                                                   CPM005
     C           DSACTN    IFNE ' '                        ***B3          CPM005
     C           DSACTN    ANDNE'D'                                       CPM005
     C                     MOVEL'1'       *IN82                           CPM005
     C                     MOVE ' '       DSACTN                          CPM005
     C                     END                             ***E3          CPM005
      *
     C           DSACTN    IFEQ ' '                        ***B3
     C                     MOVEL*BLANKS   DSACTN
     C                     UPDATPM6020S1
     C                     Z-ADDRRN1      SFLRRN
     C                     ELSE                            ***X3
     C                     EXSR #CALL
     C                     ENDIF                           ***E3
      *
     C                     ELSE                            **X2
     C                     Z-ADDCPFPOS    SFLRRN
     C                     ENDIF                           **E2
      *
     C                     ENDIF                           *E1
      *
     C                     ENDDO                           *EW1
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #LOAD    - SR LOAD SUBFILE WITH DETAILS                          *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZSEDIT   -                                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #MAIN    - SR MAIN PROCESSING                                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWWORK   - WORK FIELD FOR CALCULATION                            *
      * WWCOMT   - NET COMMITMENT FOR HEADER                             *
      * WWPERC   - DISPLAY PERCENTAGE                                    *
      * WWFLD1   - WORK FIELD FOR UNHEDGED POSITION                      *
      *                                                                  *
      ********************************************************************
     C           #LOAD     BEGSR
      *
      **  CLEAR SUBFILE
     C                     Z-ADD0         RRN1    40
     C                     Z-ADD1         SFLRRN
     C                     MOVE *ON       *IN61
     C                     MOVE *OFF      *IN62
     C                     WRITEPM6020C1
     C                     MOVE *OFF      *IN61
      *
      ** ACCESS TO PMNTCMLL TO OUTPUT DETAILS IN SUBFILE
     C           PMNTC1    SETLLPMNTCMLL
     C                     READ PMNTCMLL                 70
      *
     C           *IN70     DOWEQ*OFF                       *BW1
     C           FJCNUM    ANDEQQMCNUM
     C           FJPTFR    ANDEQQMPTF1
      *
     C                     MOVEL*BLANKS   DSACTN
     C                     MOVELFJCURR    DSCURR
      *
      **  EDIT CURRENT VALUE
     C           FJCVAL    IFNE *ZEROS                     *B1
     C                     Z-ADD*ZEROS    ZFLD15
     C                     Z-ADDFJCVAL    ZFLD15
     C                     Z-ADDQMPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DSCVAL
     C                     ELSE                            *X1
     C                     MOVEL*BLANKS   DSCVAL
     C                     ENDIF                           *E1
      *
      **  EDIT FORWARD CAPITAL CHANGE
     C           FJCAPC    IFNE *ZEROS                     *B1
     C                     Z-ADD*ZEROS    ZFLD15
     C                     Z-ADDFJCAPC    ZFLD15
     C                     Z-ADDQMPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DSCAPC
     C                     ELSE                            *X1
     C                     MOVEL*BLANKS   DSCAPC
     C                     ENDIF                           *E1
      *
      **  EDIT FORWARD COMMITMENTS
     C           FJFFOR    IFNE *ZEROS                     *B1
     C                     Z-ADD*ZEROS    ZFLD15
     C                     Z-ADDFJFFOR    ZFLD15
     C                     Z-ADDQMPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DSFFOR
     C                     ELSE                            *X1
     C                     MOVEL*BLANKS   DSFFOR
     C                     ENDIF                           *E1
      *
      **  CALCULATE FORWARD NET COMMITMENT
     C                     Z-ADD*ZEROS    WWWORK
     C           FJCVAL    ADD  FJCAPC    WWWORK
     C                     ADD  FJFFOR    WWWORK
      *
      **  EDIT NET COMMITMENT
     C           WWWORK    IFNE *ZEROS                     *B1
     C                     Z-ADD*ZEROS    ZFLD15
     C                     Z-ADDWWWORK    ZFLD15
     C                     Z-ADDQMPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DSNTCO
     C                     ELSE                            *X1
     C                     MOVEL*BLANKS   DSNTCO
     C                     ENDIF                           *E1
      *
      **  CALCULATE & EDIT % COMMITMENT BY CCY
     C           WWCOMT    IFNE *ZEROS                     *B1
     C           WWWORK    DIV  WWCOMT    ZZPERC  43H
     C           ZZPERC    MULT 100       WWPERC  41
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPERC    ZFLD15
     C                     Z-ADD1         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DSPERC
     C                     ELSE                            *X1
     C                     MOVEL*BLANKS   DSPERC
     C                     ENDIF                           *E1
      *
      **  EDIT FX F&O AMOUNTS
     C           FJFORX    IFNE *ZEROS                     *B1
     C                     Z-ADD*ZEROS    ZFLD15
     C                     Z-ADDFJFORX    ZFLD15
     C                     Z-ADDQMPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DSFORX
     C                     ELSE                            *X1
     C                     MOVEL*BLANKS   DSFORX
     C                     ENDIF                           *E1
      *
      **  CALCULATE & EDIT UNHEDGED AMOUNT
     C                     Z-ADD*ZEROS    WWFLD1 150
     C           WWWORK    ADD  FJFORX    WWFLD1
      *
     C           WWFLD1    IFNE *ZEROS                     *B1
     C                     Z-ADD*ZEROS    ZFLD15
     C                     Z-ADDWWFLD1    ZFLD15
     C                     Z-ADDQMPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    DSUNAM
     C                     ELSE                            *X1
     C                     MOVEL*BLANKS   DSUNAM
     C                     ENDIF                           *E1
      *
      **  WRITE RECORD IN SUBFILE
     C                     ADD  1         RRN1
     C                     WRITEPM6020S1
      *
      **  READ NEXT RECORD
     C                     READ PMNTCMLL                 70
      *
     C                     ENDDO                           *EW1
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #TERM    - SR TERMINATION PROCESSING                             *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN PROCESSING                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           #TERM     BEGSR
      *
     C                     RETRN
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #WRIT    - SR WRITE SCREEN                                       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #MAIN    - SR MAIN PROCESSING                                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           #WRIT     BEGSR
      *
     C                     WRITEPM6020F3                                  CPM005
     C                     WRITEPM6020F2
     C                     WRITEPM6020F1
      *
      **  IF NO DETAILS READ, INIT SUBFILE TO PROCESS IT WITH NO DETAILS
     C           RRN1      IFEQ 0
     C                     MOVE *ON       *IN62
     C                     ELSE
     C                     MOVE *OFF      *IN62
     C                     END
      *
     C                     EXFMTPM6020C1
      *                                                                   CPM005
     C                     MOVE '0'       *IN82                           CPM005
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #CALL    - SR TO CALL PM1701                                     *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * PMC1001  - PGM UPDATE DATA AREA *LDA                             *
      * PM0701   - PGM POSITIONS FOR SELECTED CCY ENQUIRY                *
      * PMC1003  - PGM RETRIEVE DATA AREA *LDA                           *CPM005
      **PMC1004**-*PGM*RETRIEVE*DATA*AREA**GDA****************************CPM005
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #MAIN    - SR MAIN PROCESSING                                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWLDA    - GROUP DATA AREA                                       *
      * WWRANG   - SAVED FIELD FORE RRN                                  *
      **WWGDA****-*GROUP*DATA*AREA****************************************CPM005
      * WWFLAG   - DISPLAY SCREEN                                        *
      *                                                                  *
      ********************************************************************
     C           #CALL     BEGSR
      *
     C                     MOVEL*BLANK    DSACTN
     C                     UPDATPM6020S1
     C                     Z-ADDRRN1      SFLRRN
      *
      **  SETUP DATA AREA TO PASS PARAMETERS
     C                     MOVELDSCURR    QMCCY1
     C                     MOVE 'A'       QMTOTT
     C                     MOVE 'N'       QMATTN
      *
      **  UPDAT *LDA
     C                     CALL 'PMC1001'
     C                     PARM           WWLDA
      *
     C                     CALL 'PM0701'
      *
      ****RETRIEVE GDA                                                    CPM005
      **  RETRIEVE LDA                                                    CPM005
     C***********          CALL 'PMC1004'                                 CPM005
     C***********          PARM           WWGDA                           CPM005
     C                     CALL 'PMC1003'                                 CPM005
     C                     PARM           WWLDA                           CPM005
      *
      **  EXIT LOOP IF REQUEST RETURN TO PORTFOLIO OR CUSTOMER SCREEN
     C           QMENQT    IFEQ 'Y'
     C           QMENQT    OREQ 'C'                                       CPM005
     C           QMENQT    OREQ 'P'                                       CPM005
     C                     MOVEL'Y'       WWFLAG
     C                     END
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      /COPY ZSRSRC,ZSEDITZ3
      /EJECT
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * *PSSR    - ERROR IN FILE                                         *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      **PMC1002**-*PGM*UPDATE*DATA*AREA**GDA******************************CPM005
      * PMC1001  - PGM UPDATE DATA AREA *LDA                              CPM005
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      **WWGDA****-*GROUP*DATA*AREA****************************************CPM005
      *                                                                  *
      ********************************************************************
     C           *PSSR     BEGSR
      *
     C                     MOVE *ON       *INU7
     C                     MOVE *ON       *INU8
     C                     MOVEL'Y'       QMDBER
     C                     MOVEL'Y'       QMENQT
     C                     MOVEL'PM6020'  QMPGM
      *
      ***UPDATE *GDA                                                      CPM005
      ** UPDATE *LDA                                                      CPM005
     C***********          CALL 'PMC1002'                                 CPM005
     C***********          PARM           WWGDA                           CPM005
     C                     CALL 'PMC1001'                                 CPM005
     C                     PARM           WWLDA                           CPM005
      *
     C                     DUMP
      *
     C                     ENDSR
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2001
