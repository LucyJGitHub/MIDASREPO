     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Assets Liabilities Summary by Inst.')
      *****************************************************************
      *                                                               *
      *  Midas - Portfolio Management Module                          *
      *                                                               *
      *  PM6060 - PM Assets & Liabilities Summary by Instrument       *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CPM005             Date 18Jul96               *
      *                 100484             Date 07Mar96               *
      *                 092864             DATE 11SEP95               *
      *                 S01124             DATE 01JUN94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CPM005 - Private Banking Phase II                            *
      *           Focus Group Changes Upgraded to DBA                 *
      *  100484 - PAGE NUMBERS ARE INCREASED EVEN IF THE 2 SUMMARY    *
      *           PAGES ARE NOT PRINTED AND IT RESULTS IN A GAP FOR   *
      *           THE FOLLOWING PAGES. COMPLETE FIX 092864.           *
      *  092864 - WHEN THE SECTIONS CURRENCY SUMMARY OR INSTRUMENT    *
      *           SUMMARY ARE REQUESTED ON THE VALUATION REPORT,      *
      *           IF THERE'S NO POSITIONS FOR THE REQUESTED PORTFOLIO,*
      *           DO NOT PRINT THE PAGES (CCY/INSTRUMENT SUMMARY)     *
      *  S01124 - PBFG/1 - CHANGE PM CUSTOMER/PORTFOLIO BASE CURRENCY *
      *           PBFG/5 - INSTRUMENT ASSET / LIABILITY SPLIT         *
      *                                                               *
      *****************************************************************
     F***SDPORTL0IF  E           K        DISK         KINFSR *PSSR       CPM005
      *******PREFIX P9                                                    CPM005
     F***SDPLINL4IF  E           K        DISK         KINFSR *PSSR       CPM005
      *******PREFIX PD                                                    CPM005
     FPMASLIL2IF  E           K        DISK         KINFSR *PSSR
      *      PREFIX FE
     FPMASLIL1IF  E           K        DISK         KINFSR *PSSR
     F            PMASLID0                          KRENAMEPMASLID1
      *      PREFIX FE
     FPMPCBCL1IF  E           K        DISK         KINFSR *PSSR
      *      PREFIX FM
     FPM6060AUO   E                    PRINTER      KINFDS SPOOLU     UC  CPM005
     F                                              KINFSR *PSSR          CPM005
     FPM9950P1O   E             70     PRINTER      KINFSR *PSSR      UC
     FPM0950P1O   E             71     PRINTER      KINFSR *PSSR      UC
     F            P951HDR                           KRENAMER951HDR
     F            P951DET                           KRENAMER951DET
     F            P951TOT                           KRENAMER951TOT
     F            P952HDR1                          KRENAMER952HDR1
     F            P952HDR2                          KRENAMER952HDR2
     F            P952DET1                          KRENAMER952DET1
     F            P952DET2                          KRENAMER952DET2
     F            P952DET3                          KRENAMER952DET3
     F            P952HDR3                          KRENAMER952HDR3
     F            P952DET4                          KRENAMER952HDR4
     F            P952DET5                          KRENAMER952HDR5
     F            P952DET6                          KRENAMER952HDR6
     F            P952DET7                          KRENAMER952HDR7
     F            P952DET8                          KRENAMER952HDR8
     F            P952DET9                          KRENAMER952HDR9
     F            P952DT10                          KRENAMER952DT10
     F            P952DT11                          KRENAMER952DT11
     F            P952DT12                          KRENAMER952DT12
     F            P952D12A                          KRENAMER952D12A
     F            P952DT13                          KRENAMER952DT13
     F            P952DT14                          KRENAMER952DT14
     F            P952DT15                          KRENAMER952DT15
     F            P952DT16                          KRENAMER952DT16
     F            P952DT17                          KRENAMER952DT17
     F            P952DT18                          KRENAMER952DT18
     F            P952DT19                          KRENAMER952DT19
     F            P952DT20                          KRENAMER952DT20
     F            P952DT22                          KRENAMER952DT22
     F            P952DT23                          KRENAMER952DT23
     F            P952DT24                          KRENAMER952DT24
     F            P952DT25                          KRENAMER952DT25
     F            P952DT26                          KRENAMER952DT26
     F            P953HDR                           KRENAMER953HDR
     F            P953TUL                           KRENAMER953TUL
     F            P953TOT                           KRENAMER953TOT
     F            P953VUL                           KRENAMER953VUL
     F            P953VDL                           KRENAMER953VDL
     F            P953VHD                           KRENAMER953VHD
     F            P953IDL                           KRENAMER953IDL
     F            P954HDR                           KRENAMER954HDR
     F            P954DET                           KRENAMER954DET
     F            P954TOT                           KRENAMER954TOT
     F            P955HDR                           KRENAMER955HDR
     F            P955DET                           KRENAMER955DET
     F            P955TOT                           KRENAMER955TOT
     F            P949HDR                           KRENAMER949HDR
     F            P949DET                           KRENAMER949DET
     F            P949TOT                           KRENAMER949TOT
     F            P956HDR                           KRENAMER956HDR
     F            P956DET1                          KRENAMER956DET1
     F            P956DET2                          KRENAMER956DET2
     F            P957HDR                           KRENAMER957HDR
     F            P957DET                           KRENAMER957DET
     F            P957TOT                           KRENAMER957TOT
     F            P958HDR                           KRENAMER958HDR
     F            P958DET1                          KRENAMER958DET1
     F            P958DET2                          KRENAMER958DET2
     F            P960HDR                           KRENAMER960HDR
     F            P960HD1                           KRENAMER960HD1
     F            P960HD2                           KRENAMER960HD2
     F            P960DT1                           KRENAMER960DT1
     F            P960DT2                           KRENAMER960DT2
     F            P961HDR                           KRENAMER961HDR
     F            P961DET                           KRENAMER961DET
     F            P961TOT                           KRENAMER961TOT
     F            P960TXT                           KRENAMER960TXT
     F            P606HD1                           KRENAMER606HD1
     F            P606HD2                           KRENAMER606HD2
     F            P606DET                           KRENAMER606DET
     F            P606POS                           KRENAMER606POS
     F            P606POR                           KRENAMER606POR
      *****************************************************************
      *
      **  ARRAY TO OVERRIDE MESSAGE TEXT
     E**********          CMD     1   1 39                                CPM005
     E                    CMD     1   1 41                                CPM005
      *
      **  ARRAY FOR NARRATIVE
     E                    NAR         4 32
      *
      **  ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
      /COPY ZSRSRC,ZDATE2Z1
      /COPY ZSRSRC,ZSEDITZ1
      *
      *****************************************************************
      *
      **  MESSAGE FILE NAME
     I            DS
     I**********                              1  39 WWCMD                 CPM005
     I                                        1  41 WWCMD                 CPM005
     I                                       31  32 W1LANG
      *
      **  LOCAL DATA AREA FOR DATA BASE ERRORS:
     ILDA         DS                            256
     I**********                            132 183 DBLOT                 CPM005
     I**********                            132 141 DBFILE                CPM005
     I**********                            142 170 DBKEY                 CPM005
     I**********                            171 180 DBPGM                 CPM005
     I**********                            181 1830DBASE                 CPM005
     I                                      134 141 DBFILE                CPM005
     I                                      142 170 DBKEY                 CPM005
     I                                      171 180 DBPGM                 CPM005
     I                                      181 1830DBASE                 CPM005
      *                                                                   CPM005
     ISDPORT    E DSSDPORTPD                                              CPM005
      ** Data Structure for Portfolio Mgmt Details Access Program         CPM005
      *                                                                   CPM005
     ISDPLIN    E DSSDPLINPD                                              CPM005
      ** Data Structure for Instrument Details                            CPM005
      *                                                                   CPM005
     IDSFDY     E DSDSFDY                                                 CPM005
      ** Short Data Structure for Access Programs                         CPM005
      *
     ISPOOLU      DS                                                      CPM005
      ** File Information Data Structure for PM6060AU                     CPM005
     I                                       83  92 SFILEU                CPM005
     I                                    B 123 1240SFNUMU                CPM005
      *                                                                   CPM005
      **  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION:
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
      *
      **  DATA STRUCTURE FOR DATABASE ERROR ON PMASLIL1
     IWWKEY1      DS
     I**********                              1   60K1CNUM                                    CSD027
     I                                        1   6 K1CNUM                                    CSD027
     I                                        7  10 K1PTFR
     I                                       11  12 K1POTY
      *
      **  DATA STRUCTURE FOR DATABASE ERROR ON PMASLIL0
     IWWKEY2      DS
     I**********                              1   60K2CNUM                                    CSD027
     I                                        1   6 K2CNUM                                    CSD027
     I                                        7  10 K2PTFR
     I                                       11  12 K2POTY
     I                                       13  15 K2DGRC
     I                                       16  170K2DGRS
     I                                       18  190K2INSS
      *
      **  DATA STRUCTURE FOR DATABASE ERROR ON PMPCBCL1
     IWWKEY3      DS
     I**********                              1   60K3CNUM                                    CSD027
     I                                        1   6 K3CNUM                                    CSD027
     I                                        7  10 K3PTFR
      *
      **  DS USED TO PASS PARAMETERS TO CALLED PROGRAMS
     I***WWPARM****E*DSPM095XPP                                           CPM005
     IWWPARM    E DSPM095XPD                                              CPM005
      *
      /COPY ZSRSRC,ZSEDITZ2
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INDEX OF SUBROUTINES                                             *
      * MAIN     - MAIN PROCESSING                                       *
      * #INIT    - INITIAL PROCESSING                                    *
      * #MAIN    - MAIN PROCESSING                                       *
      * #WDET    - SETUP FIELDS AND WRITE DETAILS INSTRUMENT             *
      * #WTOT    - SETUP FIELDS AND WRITE TOTAL INSTRUMENT               *
      * #WPOR    - SETUP FIELDS AND WRITE TOTAL PORTFOLIO                *
      * #TERM    - TERMINATION PROCESSING                                *
      * *PSSR    - ERROR IN FILE                                         *
      *                                                                  *
      * EXTERNAL ROUTINES CALLED                                         *
      * QCMDEXC  - OVERRIDE MESSAGE FILE                                 *
      * ZA1310   - SETUP MESSAGE TO PRINT NARRATIVE                      *
      *                                                                  *
      ********************************************************************
     C                     EXSR #INIT
      *
      **  EXECUTE MAIN PROCESSING ONLY IF TOTAL PORTFOLIO ASSET OR        092864
      **  TOTAL PORTFOLIO LIABILITY NOT EQUAL TO ZERO                     092864
     C           WWPOTA    IFNE *ZEROS                                    092864
     C           WWPOTL    ORNE *ZEROS                                    092864
     C                     EXSR #MAIN
     C                     END                                            092864
      *
     C                     EXSR #TERM
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #INIT    - INITIAL PROCESSING                                    *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * QCMDEXC  - OVERRIDE MESSAGE FILE                                 *
      * ZA1310   - SETUP MESSAGE TO PRINT NARRATIVE                      *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN PROCESSING                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWPARM   - DATA STRUCTURE TO PASS PARAMETERS                     *
      * WWDISP   - INDICATE WICH HEADER TO BE PRINTED                    *
      * WWCMD    - COMMAND TO OVERRIDE MESSAGE FILE                      *
      * WWCNUM   - CUSTOMER NUMBER                                       *
      * WWCNA1   - CUSTOMER NAME & ADDRESS 1                             *
      * WWCNA2   - CUSTOMER NAME & ADDRESS 2                             *
      * WWCNA3   - CUSTOMER NAME & ADDRESS 3                             *
      * WWCNA4   - CUSTOMER NAME & ADDRESS 4                             *
      * WWCRNM   - CUSTOMER REPORT NAME                                  *
      * WWPTCY   - PORTFOLIO CURRENCY                                    *
      * WWPTFN   - PORTFOLIO NARRATIVE                                   *
      * WWLANG   - LANGUAGE ID                                           *
      * WWXTRS   - EXTRA STATEMENT INDICATOR                             *
      * WWLXDT   - LAST EXTRACT DATE                                     *
      * WWBMSN   - SHORTNAME                                             *
      * WWECDT   - EVENT CONTROL DATE                                    *
      * WWPTFR   - PORTFOLIO REFERENCE                                   *
      * WWPTAC   - WORK FIELD TOTAL ASSET FOR POSITION TYPE 'C '         *
      * WWPTLC   - WORK FIELD TOTAL LIABILITY FOR POSITION TYPE 'C '     *
      * WWPTAF   - WORK FIELD TOTAL ASSET FOR POSITION TYPE 'FC'         *
      * WWPTLF   - WORK FIELD TOTAL LIABILITY FOR POSITION TYPE 'FC'     *
      * WWPTAO   - WORK FIELD TOTAL ASSET FOR POSITION TYPE 'FO'         *
      * WWPTLO   - WORK FIELD TOTAL LIABILITY FOR POSITION TYPE 'FO'     *
      * WWWORK   - WORK FIELD TO CALCULATE PORTFOLIO TOTAL               *
      * WWPOTA   - WORK FIELD TOTAL PORTFOLIO ASSET                      *
      * WWPOTL   - WORK FIELD TOTAL PORTFOLIO LIABILITY                  *
      * WWPNBR   - PAGE NUMBER                                           *
      * WWPROG   - DETERMINE WICH PGM CALL THIS ONE                      *
      *                                                                  *
      ********************************************************************
     C           #INIT     BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           WWPARM
     C                     PARM           WWDISP  1
     C                     PARM           WWPROG  6
      *
      **  SET UP PARAMETER TO FORM OUTPUT LINE
     C           ZA1311    PLIST
     C                     PARM           MSGID   7
     C                     PARM           MSGDT0 40
     C                     PARM           MSGDT1 40
     C                     PARM           MSGDT2 40
     C                     PARM           MSGDT3 40
     C                     PARM           MSGDT4 40
     C                     PARM           MSGDT5 40
     C                     PARM           MSGDT6 40
     C                     PARM           MSGDT7 40
     C                     PARM           MSGDT8 40
     C                     PARM           MSGDT9 40
     C                     PARM           RESULT200
      *
      **  KLIST FOR USE WITH PMASLIL1
     C           PMASL1    KLIST
     C                     KFLD           K1CNUM
     C                     KFLD           K1PTFR
     C                     KFLD           K1POTY
      *
      **  KLIST FOR USE WITH PMASLIL2
     C           PMASL2    KLIST
     C                     KFLD           K2CNUM
     C                     KFLD           K2PTFR
     C                     KFLD           K2POTY
     C                     KFLD           K2DGRS
     C                     KFLD           K2DGRC
     C                     KFLD           K2INSS
      *
      **  KLIST FOR USE WITH PMPCBCL1
     C           PMPCL1    KLIST
     C                     KFLD           K3CNUM
     C                     KFLD           K3PTFR
      *
     C                     MOVEL'J'       ZECODE
     C                     MOVEACMD       WWCMD
     C                     MOVE *OFF      *IN98
     C                     MOVE *OFF      *IN02
      *
      ** DETERMINE WICH PROGRAM CALL THIS ONE : *IN01 IS ON IF PM0950
     C                     MOVE *OFF      *IN01
     C           WWPROG    IFEQ 'PM0950'
     C                     MOVE *ON       *IN01
     C                     END
      *
     C  N01                OPEN PM9950P1
     C   01                OPEN PM0950P1
      *
      **  PREPARE LDA:
     C           *NAMVAR   DEFN           LDA
     C**********           MOVEL*BLANK    DBLOT                           CPM005
     C**********           MOVEL'PM6060'  WBPGM  10                       CPM005
     C                     MOVEL'PM6060'  DBPGM  10                       CPM005
      *
     C                     MOVE WWCNUM    R6CNUM
     C                     MOVE WWCNUM    R6CUST
     C                     MOVELWWCNA1    R6CNA1
     C                     MOVELWWCNA2    R6CNA2
     C                     MOVELWWCNA3    R6CNA3
     C                     MOVELWWCNA4    R6CNA4
     C/COPY WNCPYSRC,PM6060C001
     C                     MOVELWWCRNM    R6CRNM
     C/COPY WNCPYSRC,PM6060C002
     C                     MOVELWWCRNM    R6NAME
     C                     MOVELWWPTCY    R6CCY1
     C                     MOVELWWPTCY    R6CCY2
     C                     MOVELWWPTFN    R6PTFN
     C                     MOVELWWPTFN    R6PNAR
     C                     MOVELWWLXDT    R6LXTD
     C                     MOVELWWPTFR    R6PREF
     C                     MOVELWWACON    R6ACNM                          CPM005
     C                     MOVELWWAPHN    R6ACPH                          CPM005
      *
      **  ACCESS TO PORTFOLIO STANDING DATA
     C********** *LOVAL    SETLLSDPORTL0                                  CPM005
     C**********           READ SDPORTL0                 63               CPM005
     C                     CALL 'AOPORTR0'                                CPM005
     C                     PARM *BLANKS   WRTCD                           CPM005
     C                     PARM '*FIRST'  WOPTN                           CPM005
     C           SDPORT    PARM SDPORT    DSFDY                           CPM005
      *
     C           WRTCD     IFNE *BLANKS                                   CPM005
     C********** *IN63     IFEQ '1'                        B1             CPM005
     C           *LOCK     IN   LDA                                       CPM005
     C                     Z-ADD3         DBASE   30       ***************
     C                     MOVE 'SDPORTPD'DBFILE           * DB ERROR 03 *CPM005
     C                     MOVE *BLANKS   DBKEY            ***************CPM005
     C**********           MOVEL'SDPORTL0'WBFILE 10                       CPM005
     C**********           MOVEL*BLANKS   WBKEY  29                       CPM005
     C                     OUT  LDA                                       CPM005
     C                     EXSR *PSSR
     C                     ENDIF                           E1
      *
     C********** WWPTFR    IFEQ P9R997                     B1             CPM005
     C           WWPTFR    IFEQ FCR997                     B1             CPM005
     C                     MOVEL'9997'    KKPTFR  4
     C                     ELSE                            X1
     C********** WWPTFR    IFEQ P9R998                     B2             CPM005
     C           WWPTFR    IFEQ FCR998                     B2             CPM005
     C                     MOVEL'9998'    KKPTFR
     C                     ELSE                            X2
     C********** WWPTFR    IFEQ P9R999                     B3             CPM005
     C           WWPTFR    IFEQ FCR999                     B3             CPM005
     C                     MOVEL'9999'    KKPTFR
     C                     ELSE                            X3
     C                     MOVELWWPTFR    KKPTFR
     C                     ENDIF                           E3
     C                     ENDIF                           E2
     C                     ENDIF                           E1
      *
      **  IF CHANGE OF LANGUAGE CODE OBTAIN NEW TEXT
     C           WWLANG    IFNE WSLANG
     C                     MOVE WWLANG    WSLANG  2
     C                     MOVE WWLANG    W1LANG
     C**********           Z-ADD39        MSGLEN 155                      CPM005
     C                     Z-ADD41        MSGLEN 155                      CPM005
     C                     CALL 'QCMDEXC'
     C                     PARM           WWCMD
     C                     PARM           MSGLEN
      **  GET MES. PORTFOLIO
     C**********           MOVE 'PM95903' MSGID                           CPM005
     C                     MOVE 'PM95485' MSGID                           CPM005
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    R6LT28
      **  GET MES. DATED
     C**********           MOVE 'PM95900' MSGID                           CPM005
     C                     MOVE 'PM95483' MSGID                           CPM005
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    R6LT25
      **  GET MES. AS OF
     C**********           MOVE 'PM95901' MSGID                           CPM005
     C                     MOVE 'PM95484' MSGID                           CPM005
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    R6LT26
      **  GET MES. OUR REF
     C**********           MOVE 'PM95102' MSGID                           CPM005
     C                     MOVE 'PM95494' MSGID                           CPM005
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    R6LT02
      **  GET MES. CLIENT:
     C**********           MOVE 'PMS5102' MSGID                           CPM005
     C                     MOVE 'PM95401' MSGID                           CPM005
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    M95102
     C                     MOVELRESULT    R6LT27
      **  GET MES. REF.NO.
     C**********           MOVE 'PMS5104' MSGID                           CPM005
     C                     MOVE 'PM95407' MSGID                           CPM005
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    M95104
      **  GET MES. YOUR ADVISER:                                          CPM005
     C                     MOVE 'PM95408' MSGID                           CPM005
     C                     CALL 'ZA1310'  ZA1311                          CPM005
     C                     MOVELRESULT    M95105                          CPM005
      **  GET MES. ASSET AND LIABILITIES.........SUMMARY
     C                     MOVE 'PM99601' MSGID
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    M99601
      **  GET MES. ASSET
     C                     MOVE 'PM99603' MSGID
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    M99603
      **  GET MES. LIABILITY
     C                     MOVE 'PM99604' MSGID
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    M99604
      **  GET MES. VALUE
     C                     MOVE 'PM99605' MSGID
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    M99605
     C                     MOVELRESULT    C99605
      **  GET MES. %
     C                     MOVE 'PM99606' MSGID
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    M99606
     C                     MOVELRESULT    C99606
     C                     END
      **  GET MES. 'TOTAL CURRENT POSITIONS'
     C                     MOVE 'PM99612' MSGID
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    NAR,1
      **  GET MES. 'TOTAL FORWARD CLOSED PROFIT/LOSS'
     C                     MOVE 'PM99613' MSGID
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    NAR,2
      **  GET MES. 'TOTAL FORWARD OPEN PROFIT/LOSS'
     C                     MOVE 'PM99614' MSGID
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    NAR,3
      **  GET MES. 'TOTAL PORTFOLIO ASSET/LIABILITY'
     C                     MOVE 'PM99615' MSGID
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    NAR,4
      **  GET MES. CURRENT POSITIONS
     C                     MOVE 'PM99616' MSGID
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    M99608 35
      **  GET MES. FORWARD CLOSED P/L
     C                     MOVE 'PM99617' MSGID
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    M99609 35
      **  GET MES. FORWARD OPEN P/L
     C                     MOVE 'PM99618' MSGID
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    M99611 35
      *
      **  SET UP MESSAGE EXTRA / REGULAR VALUATION STATEMENT
     C                     MOVE *BLANKS   M99607
     C           WWXTRS    IFEQ 'Y'
     C**********           MOVE 'PMS5119' MSGID                           CPM005
     C                     MOVE 'PM95418' MSGID                           CPM005
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    M99607
     C                     ELSE
     C           WWXTRS    IFEQ 'C'
     C**********           Z-ADDWWCNUM    K3CNUM                                              CSD027
     C                     MOVE WWCNUM    K3CNUM                                              CSD027
      *
     C           KKPTFR    IFEQ '9997'
     C           KKPTFR    OREQ '9998'
     C           KKPTFR    OREQ '9999'
     C                     MOVE *BLANKS   K3PTFR
     C                     ELSE
     C                     MOVELKKPTFR    K3PTFR
     C                     ENDIF
      *
     C           PMPCL1    CHAINPMPCBCL1             63
      *
     C           *IN63     IFEQ '1'                        B1
     C                     MOVE *BLANKS   K3PTFR
     C           PMPCL1    CHAINPMPCBCL1             63
      *
     C           *IN63     IFEQ '1'                        B1
     C**********           Z-ADD2         WBASE
     C**********           MOVEL'PMPCBCL1'WBFILE
     C**********           MOVELWWKEY3    WBKEY
     C           *LOCK     IN   LDA                        ***************CPM005
     C                     Z-ADD2         DBASE            * DB ERROR 02 *CPM005
     C                     MOVE 'PMPCBCL1'DBFILE           ***************CPM005
     C                     MOVE WWKEY3    DBKEY                           CPM005
     C                     OUT  LDA                                       CPM005
     C                     EXSR *PSSR
     C                     ENDIF                           E2
     C                     ENDIF                           E1
      *
      **  GET MES. 'DUE TO CURRENCY CHANGE FROM xxx TO YYY'
     C                     MOVE *BLANKS   MSGDT0
     C                     MOVELFMSTCY    MSGDT0
     C                     MOVE *BLANKS   MSGDT1
     C                     MOVELFMNBCY    MSGDT1
     C                     MOVE 'PM99607' MSGID
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    M99607
     C                     ELSE
     C**********           MOVE 'PMS5118' MSGID                           CPM005
     C                     MOVE 'PM95417' MSGID                           CPM005
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    M99607
     C                     END
     C                     END
      *
      **  FORMAT PRINTED 'AS OF' INFORMATION
     C                     MOVE *BLANKS   MSGDT0
     C                     MOVELWWLXDT    MSGDT0
     C**********           MOVE 'PMS5114' MSGID                           CPM005
     C                     MOVE 'PM95406' MSGID                           CPM005
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    R6PRDT
      *
      **  FORMAT PRINTED PLACE AND DATE
     C                     MOVE *BLANKS   MSGDT0
     C                     MOVELWWBMSN    MSGDT0
     C                     MOVE *BLANKS   MSGDT1
     C                     MOVELWWECDT    MSGDT1
     C**********           MOVE 'PMS5116' MSGID                           CPM005
     C                     MOVE 'PM95402' MSGID                           CPM005
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    R6BRDT
      *
      **  ACCESS TO PMASLIL1 TO RETRIEVE TOTAL PORTFOLIO WITH ALL POSITION TYPE
     C**********           Z-ADDWWCNUM    K1CNUM                                              CSD027
     C                     MOVE WWCNUM    K1CNUM                                              CSD027
     C                     MOVELKKPTFR    K1PTFR
     C*
     C**   CONVERT EVENT CONTROL DATE
     C                     Z-ADDWWECD     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    R6DATE
     C                     MOVE ZADATE    WWDATE  7
     C                     MOVELWWDATE    R6VDNA
      *
      **  FIRST ACCESS WITH 'C '=TOTAL CURRENT POSITIONS
     C                     MOVEL'C '      K1POTY
      *
     C           PMASL1    CHAINPMASLIL1             60
      *
      **  RECORD FOUND SAVE ASSET & LIABILITY VALUE FOR THIS POSITION TYPE
     C           *IN60     IFEQ '0'                        B1
     C                     Z-ADDFEASSV    WWPTAC 150
     C                     Z-ADDFELIAV    WWPTLC 150
     C                     MOVE 'Y'       WWFSC   1
     C                     ELSE
     C                     Z-ADD*ZEROS    WWPTAC
     C                     Z-ADD*ZEROS    WWPTLC
     C                     MOVE 'N'       WWFSC   1
     C                     END                             E1
      *
      **  SECOND ACCESS WITH 'FC'=TOTAL FORWARD CLOSED POSITIONS
     C                     MOVEL'FC'      K1POTY
      *
     C           PMASL1    CHAINPMASLIL1             60
      *
      **  RECORD FOUND SAVE ASSET & LIABILITY VALUE FOR THIS POSITION TYPE
     C           *IN60     IFEQ '0'                        B1
     C                     Z-ADDFEASSV    WWPTAF 150
     C                     Z-ADDFELIAV    WWPTLF 150
     C                     MOVE 'Y'       WWFSFC  1
     C                     ELSE
     C                     MOVE 'N'       WWFSFC  1
     C                     Z-ADD*ZEROS    WWPTAF 150
     C                     Z-ADD*ZEROS    WWPTLF 150
     C                     END                             E1
      *
     C                     MOVEL'FO'      K1POTY
      *
      **  THIRD ACCESS WITH 'FO'=TOTAL FORWARD OPEN POSITIONS
     C           PMASL1    CHAINPMASLIL1             60
      *
      **  RECORD FOUND SAVE ASSET & LIABILITY VALUE FOR THIS POSITION TYPE
     C           *IN60     IFEQ '0'                        B1
     C                     Z-ADDFEASSV    WWPTAO 150
     C                     Z-ADDFELIAV    WWPTLO 150
     C                     MOVE 'Y'       WWFSFO  1
     C                     ELSE
     C                     MOVE 'N'       WWFSFO  1
     C                     Z-ADD*ZEROS    WWPTAO 150
     C                     Z-ADD*ZEROS    WWPTLO 150
     C                     END                             E1
      *
      **  TOTAL OF PORTFOLIO ASSET
     C                     Z-ADD*ZEROS    WWWORK 150
     C           WWPTAC    ADD  WWPTAF    WWWORK
     C           WWWORK    ADD  WWPTAO    WWPOTA 150
      *
      **  TOTAL OF PORTFOLIO LIABILITY
     C                     Z-ADD*ZEROS    WWWORK
     C           WWPTLC    ADD  WWPTLF    WWWORK
     C           WWWORK    ADD  WWPTLO    WWPOTL 150
      *
      **  WRITE HEADER ONLY IF TOTAL PORTFOLIO ASSET/LIABILITY NOT ZERO   100484
     C           WWPOTA    IFNE *ZEROS                                    100484
     C           WWPOTL    ORNE *ZEROS                                    100484
      *
      **  WRITE THE RIGHT HEADER,IF WWDISP='1' IT MUST BE THE FIRST SUMMARY PAGE
      **  GET MES. PAGE
     C                     ADD  1         WWPNBR
     C                     MOVE *BLANKS   MSGDT0
     C                     MOVELWWPNBR    MSGDT0
     C**********           MOVE 'PMS5501' MSGID                           CPM005
     C                     MOVE 'PM95405' MSGID                           CPM005
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    R6PAGE
      *
      **  SETUP FIELDS ON PRTF TO BLANKS. THESE FIELDS ARE ONLY USED IN PM6080
     C                     MOVEL*BLANKS   M99610
     C                     MOVEL*BLANKS   R6NTVL
     C                     MOVEL*BLANKS   R6LINE
     C                     MOVEL*BLANKS   R6NTPO
     C                     MOVEL*BLANKS   R6LIND
     C                     MOVEL*BLANKS   R6NTTT
      *
      ** OUTPUT POSITION TYPE HEADING
     C                     MOVE *BLANKS   WM9960
     C           WWFSC     IFEQ 'Y'
     C                     MOVELM99608    WM9960
     C                     ELSE
     C           WWFSFC    IFEQ 'Y'
     C                     MOVELM99609    WM9960
     C                     ELSE
     C           WWFSFO    IFEQ 'Y'
     C                     MOVELM99611    WM9960
     C                     END
     C                     END
     C                     END
      *
      ****WRITE*HEADER ONLY IF TOTAL PORTFOLIO ASSET/LIABILITY NOT  092864100484
     C***********WWPOTA    IFNE *ZEROS                              092864100484
     C***********WWPOTL    ORNE *ZEROS                              092864100484
      ***********                                                   092864100484
     C           WWDISP    IFEQ '1'
     C  N01                WRITEP606HD1
     C   01                WRITER606HD1
     C                     ELSE
     C  N01                WRITEP606HD2
     C   01                WRITER606HD2
     C                     END
     C                     END                                            092864
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #MAIN    - MAIN PROCESSING                                       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * #WDET    - SETUP FIELDS AND WRITE DETAILS INSTRUMENT             *
      * #WTOT    - SETUP FIELDS AND WRITE TOTAL INSTRUMENT               *
      * #WPOR    - SETUP FIELDS AND WRITE TOTAL PORTFOLIO                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN PROCESSING                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWCNUM   - CUSTOMER NUMBER                                       *
      * WWPTFR   - PORTFOLIO REFERENCE                                   *
      * WWPOTY   - WORK FIELD TO SAVE POSITION TYPE                      *
      *                                                                  *
      ********************************************************************
     C           #MAIN     BEGSR
      *
      **  SETUP KEY TO ACCESS FILE
     C**********           Z-ADDWWCNUM    K2CNUM                                              CSD027
     C                     MOVE WWCNUM    K2CNUM                                              CSD027
     C                     MOVELKKPTFR    K2PTFR
     C                     MOVEL*BLANKS   K2POTY
     C                     MOVE *BLANKS   K2DGRC
     C                     Z-ADD*ZEROS    K2DGRS
     C                     Z-ADD*ZEROS    K2INSS
      *
      **  POSITION ON THE FIRST RECORD WITH SAME CUSTOMER & PORTFOLIO
     C           PMASL2    SETLLPMASLIL2
     C                     READ PMASLIL2                 61
      *
      ** NO RECORD FOUND, PRINT NO DETAILS
     C           *IN61     IFEQ *ON
     C           FECNUM    ORNE WWCNUM
     C           FEPTFR    ORNE KKPTFR
     C           FEPOTY    OREQ 'UV'
     C                     MOVE *ON       *IN02
     C                     ENDIF
      *
      **  DO WHILE NOT END OF FILE
      **           AND SAME CUSTOMER
      **           AND SAME PORTFOLIO
      **           AND POSITION TYPE DIFFERENT TO 'UV'
     C           *IN61     DOWEQ'0'                        BW1
     C           FECNUM    ANDEQWWCNUM
     C           FEPTFR    ANDEQKKPTFR
     C           FEPOTY    ANDNE'UV'
      *
      **  SAVE NEW POSITION TYPE
     C                     MOVELFEPOTY    WWPOTY  2
      *
      **  DO WHILE NOT END OF FILE
      **           AND SAME CUSTOMER
      **           AND SAME PORTFOLIO
      **           AND SAME POSITION TYPE
      **           AND POSITION TYPE DIFFERENT TO 'UV'
     C           *IN61     DOWEQ'0'                        BW2
     C           FECNUM    ANDEQWWCNUM
     C           FEPTFR    ANDEQKKPTFR
     C           FEPOTY    ANDEQWWPOTY
     C           FEPOTY    ANDNE'UV'
      *
      **  WRITE DETAIL LINE
     C                     EXSR #WDET
      *
     C                     READ PMASLIL2                 61
      *
     C                     ENDDO                           EW2
      *
      ** OUTPUT POSITION TYPE HEADING
     C                     MOVE *BLANKS   WM9960
     C           FECNUM    IFEQ WWCNUM
     C           FEPTFR    ANDEQKKPTFR
     C                     SELEC
     C           FEPOTY    WHEQ 'C '
     C                     MOVELM99608    WM9960
     C           FEPOTY    WHEQ 'FC'
     C                     MOVELM99609    WM9960
     C           FEPOTY    WHEQ 'FO'
     C                     MOVELM99611    WM9960
     C                     ENDSL
     C                     END
     C*
      *
      **  WRITE TOTAL LINE FOR SAME POSITION TYPE
     C                     EXSR #WTOT
      *
     C                     ENDDO                           EW1
      *
      **  WRITE TOTAL LINE FOR PORTFOLIO
     C                     EXSR #WPOR
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #WDET    - SETUP FIELDS AND WRITE DETAILS INSTRUMENT             *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * *PSSR    - ERROR IN FILE                                         *
      * ZSEDIT   -                                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #MAIN    - MAIN PROCESSING                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWPCDP   - CURRENCY DECIMAL PLACE                                *
      * WWPASS   - WORK FIELD TO PRINT PERCENTAGE                        *
      * WWPOTA   - WORK FIELD TOTAL PORTFOLIO ASSET                      *
      * WWPLIA   - WORK FIELD TO PRINT PERCENTAGE OF LIABILITY           *
      * WWPOTL   - WORK FIELD TOTAL PORTFOLIO LIABILITY                  *
      * WWDISP   - INDICATE WICH HEADER TO BE PRINTED                    *
      *                                                                  *
      ********************************************************************
     C           #WDET     BEGSR
      *
      **  ACCESS TO INSTRUMENT FILE TO RETRIEVE NARRATIVE
     C********** FEINSC    CHAINSDPLINL4             63                   CPM005
     C                     CALL 'AOPLINR0'                                CPM005
     C                     PARM '       ' WRTCD   7                       CPM005
     C                     PARM '*KEY    'WOPTN   7                       CPM005
     C                     PARM FEINSC    WKEY    3                       CPM005
     C           SDPLIN    PARM SDPLIN    DSFDY                           CPM005
      *
     C********** *IN63     IFEQ '1'                        B1             CPM005
     C           WRTCD     IFNE *BLANKS                    B1             CPM005
     C**********           Z-ADD1         WBASE
     C**********           MOVEL'SDPLINL4'WBFILE
     C**********           MOVELFEINSC    WBKEY
     C           *LOCK     IN   LDA                        ***************CPM005
     C                     Z-ADD1         DBASE            * DB ERROR 01 *CPM005
     C                     MOVE 'SDPLINL4'DBFILE           ***************CPM005
     C                     MOVE FEINSC    DBKEY                           CPM005
     C                     OUT  LDA                                       CPM005
     C                     EXSR *PSSR
     C                     ENDIF                           E1
      *
      **  PRINT INSTRUMENT NARRATIVE
     C                     MOVELPDISTN    R6INST
      *
      **  PRINT ASSET VALUE
     C           FEASSV    IFNE *ZEROS
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE FEASSV    ZFLD15
     C                     Z-ADDWWPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6ASSV
     C                     ELSE
     C                     MOVEL*BLANKS   R6ASSV
     C                     END
      *
      **  PRINT LIABILITY VALUE
     C           FELIAV    IFNE *ZEROS
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE FELIAV    ZFLD15
     C                     Z-ADDWWPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6LIAV
     C                     ELSE
     C                     MOVEL*BLANKS   R6LIAV
     C                     END
      *
      **  PRINT PERCENTAGE OF ASSET
     C           FEASSV    IFNE *ZEROS                     B1
     C                     Z-ADD*ZEROS    ZZASSV
     C                     Z-ADD*ZEROS    WWPASS
     C           FEASSV    DIV  WWPOTA    ZZASSV  43H
     C           ZZASSV    MULT 100       WWPASS  41
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPASS    ZFLD15
     C                     Z-ADD1         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6ASPC
     C                     ELSE                            X1
     C                     MOVEL*BLANKS   R6ASPC
     C                     END                             E1
      *
      **  PRINT PERCENTAGE OF LIABILITY
     C           FELIAV    IFNE *ZEROS                     B1
     C                     Z-ADD*ZEROS    ZZLIAV
     C                     Z-ADD*ZEROS    WWPLIA
     C           FELIAV    DIV  WWPOTL    ZZLIAV  43H
     C           ZZLIAV    MULT 100       WWPLIA  41
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPLIA    ZFLD15
     C                     Z-ADD1         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6LIPC
     C                     ELSE                            X1
     C                     MOVEL*BLANKS   R6LIPC
     C                     END                             E1
      *
     C           R6ASSV    IFNE *BLANKS                    B1
     C           R6LIAV    ORNE *BLANKS
     C  N01                WRITEP606DET
     C   01                WRITER606DET
     C                     END                             E1
      *
      **  OVERFLOW
     C           *IN70     IFEQ *ON                        B1
     C           *IN71     OREQ *ON
     C                     ADD  1         WWPNBR
     C                     MOVE *BLANKS   MSGDT0
     C                     MOVELWWPNBR    MSGDT0
     C**********           MOVE 'PMS5501' MSGID                           CPM005
     C                     MOVE 'PM95405' MSGID                           CPM005
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    R6PAGE
     C           WWDISP    IFEQ '1'                        B2
     C  N01                WRITEP606HD1
     C   01                WRITER606HD1
     C                     ELSE                            X2
     C  N01                WRITEP606HD2
     C   01                WRITER606HD2
     C                     END                             E2
     C                     MOVE *OFF      *IN70
     C                     MOVE *OFF      *IN71
     C                     END                             E1
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #WTOT    - SETUP FIELDS AND WRITE TOTAL INSTRUMENT               *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZSEDIT   -                                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #MAIN    - MAIN PROCESSING                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWPOTY   - WORK FIELD TO SAVE POSITION TYPE                      *
      * WWPTAC   - WORK FIELD TOTAL ASSET FOR POSITION TYPE 'C '         *
      * WWPCDP   - CURRENCY DECIMAL PLACE                                *
      * WWPASS   - WORK FIELD TO PRINT PERCENTAGE                        *
      * WWPOTA   - WORK FIELD TOTAL PORTFOLIO ASSET                      *
      * WWPTLC   - WORK FIELD TOTAL LIABILITY FOR POSITION TYPE 'C '     *
      * WWPOTL   - WORK FIELD TOTAL PORTFOLIO LIABILITY                  *
      * WWPTAF   - WORK FIELD TOTAL ASSET FOR POSITION TYPE 'FC'         *
      * WWPTLF   - WORK FIELD TOTAL LIABILITY FOR POSITION TYPE 'FC'     *
      * WWPTAO   - WORK FIELD TOTAL ASSET FOR POSITION TYPE 'FO'         *
      * WWPTLO   - WORK FIELD TOTAL LIABILITY FOR POSITION TYPE 'FO'     *
      * WWDISP   - INDICATE WICH HEADER TO BE PRINTED                    *
      *                                                                  *
      ********************************************************************
     C           #WTOT     BEGSR
      *
      **  PRINT POSITION TYPE NARRATIVE,ASSET,LIABILITY & PERCENTAGE
     C           WWPOTY    IFEQ 'C '                       B1
     C                     MOVELNAR,1     R6PONA    P
      *
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPTAC    ZFLD15
     C                     Z-ADDWWPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6POTA
      *
     C           WWPTAC    IFNE *ZEROS                     B2
     C                     Z-ADD*ZEROS    ZZASSV
     C                     Z-ADD*ZEROS    WWPASS
     C           WWPTAC    DIV  WWPOTA    ZZASSV    H
     C           ZZASSV    MULT 100       WWPASS
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPASS    ZFLD15
     C                     Z-ADD1         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6PTAP
     C                     ELSE                            X2
     C                     MOVEL'  0.0'   R6PTAP
     C                     END                             E2
      *
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPTLC    ZFLD15
     C                     Z-ADDWWPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6POTL
      *
     C           WWPTLC    IFNE *ZEROS                     B2
     C                     Z-ADD*ZEROS    ZZASSV
     C                     Z-ADD*ZEROS    WWPASS
     C           WWPTLC    DIV  WWPOTL    ZZASSV    H
     C           ZZASSV    MULT 100       WWPASS
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPASS    ZFLD15
     C                     Z-ADD1         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6PTLP
     C                     ELSE                            X2
     C                     MOVEL'  0.0'   R6PTLP
     C                     END                             E2
      *
     C                     END                             E1
      *
     C           WWPOTY    IFEQ 'FC'                       B1
     C                     MOVELNAR,2     R6PONA    P
      *
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPTAF    ZFLD15
     C                     Z-ADDWWPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6POTA
      *
     C           WWPTAF    IFNE *ZEROS                     B2
     C                     Z-ADD*ZEROS    ZZASSV
     C                     Z-ADD*ZEROS    WWPASS
     C           WWPTAF    DIV  WWPOTA    ZZASSV    H
     C           ZZASSV    MULT 100       WWPASS
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPASS    ZFLD15
     C                     Z-ADD1         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6PTAP
     C                     ELSE                            X2
     C                     MOVEL'  0.0'   R6PTAP
     C                     END                             E2
      *
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPTLF    ZFLD15
     C                     Z-ADDWWPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6POTL
      *
     C           WWPTLF    IFNE *ZEROS                     B2
     C                     Z-ADD*ZEROS    ZZASSV
     C                     Z-ADD*ZEROS    WWPASS
     C           WWPTLF    DIV  WWPOTL    ZZASSV    H
     C           ZZASSV    MULT 100       WWPASS
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPASS    ZFLD15
     C                     Z-ADD1         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6PTLP
     C                     ELSE                            X2
     C                     MOVEL'  0.0'   R6PTLP
     C                     END                             E2
      *
     C                     END                             E1
      *
     C           WWPOTY    IFEQ 'FO'                       B1
     C                     MOVELNAR,3     R6PONA    P
      *
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPTAO    ZFLD15
     C                     Z-ADDWWPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6POTA
      *
     C           WWPTAO    IFNE *ZEROS                     B2
     C                     Z-ADD*ZEROS    ZZASSV
     C                     Z-ADD*ZEROS    WWPASS
     C           WWPTAO    DIV  WWPOTA    ZZASSV    H
     C           ZZASSV    MULT 100       WWPASS
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPASS    ZFLD15
     C                     Z-ADD1         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6PTAP
     C                     ELSE                            X2
     C                     MOVEL'  0.0'   R6PTAP
     C                     END                             E2
      *
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPTLO    ZFLD15
     C                     Z-ADDWWPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6POTL
      *
     C           WWPTLO    IFNE *ZEROS                     B2
     C                     Z-ADD*ZEROS    ZZASSV
     C                     Z-ADD*ZEROS    WWPASS
     C           WWPTLO    DIV  WWPOTL    ZZASSV    H
     C           ZZASSV    MULT 100       WWPASS
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPASS    ZFLD15
     C                     Z-ADD1         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6PTLP
     C                     ELSE                            X2
     C                     MOVEL'  0.0'   R6PTLP
     C                     END                             E2
      *
     C                     END                             E1
      *
     C  N01                WRITEP606POS
     C   01                WRITER606POS
      *
      **  OVERFLOW
     C           *IN70     IFEQ *ON                        B1
     C           *IN71     OREQ *ON
     C                     ADD  1         WWPNBR
     C                     MOVE *BLANKS   MSGDT0
     C                     MOVELWWPNBR    MSGDT0
     C**********           MOVE 'PMS5501' MSGID                           CPM005
     C                     MOVE 'PM95405' MSGID                           CPM005
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    R6PAGE
     C           WWDISP    IFEQ '1'                        B2
     C  N01                WRITEP606HD1
     C   01                WRITER606HD1
     C                     ELSE                            X2
     C  N01                WRITEP606HD2
     C   01                WRITER606HD2
     C                     END                             E2
     C                     MOVE *OFF      *IN70
     C                     MOVE *OFF      *IN71
     C                     END                             E1
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #WPOR    - SETUP FIELDS AND WRITE TOTAL PORTFOLIO                *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZSEDIT   -                                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #MAIN    - MAIN PROCESSING                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWPOTA   - WORK FIELD TOTAL PORTFOLIO ASSET                      *
      * WWPCDP   - CURRENCY DECIMAL PLACE                                *
      * WWPOTL   - WORK FIELD TOTAL PORTFOLIO LIABILITY                  *
      * WWPASS   - WORK FIELD TO PRINT PERCENTAGE                        *
      * WWPLIA   - WORK FIELD TO PRINT PERCENTAGE OF LIABILITY           *
      * WWDISP   - INDICATE WICH HEADER TO BE PRINTED                    *
      *                                                                  *
      ********************************************************************
     C           #WPOR     BEGSR
      *
     C                     MOVELNAR,4     R6PONA    P
      *
      **  PRINT TOTAL PORTFOLIO ASSET
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPOTA    ZFLD15
     C                     Z-ADDWWPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6POTA
      *
      **  PRINT TOTAL PORTFOLIO LIABILITY
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPOTL    ZFLD15
     C                     Z-ADDWWPCDP    ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6POTL
      *
      **  PRINT PERCENTAGE OF ASSET
     C           WWPOTA    IFNE *ZEROS
     C                     Z-ADD*ZEROS    ZZASSV
     C                     Z-ADD*ZEROS    WWPASS
     C           WWPOTA    DIV  WWPOTA    ZZASSV  43H
     C           ZZASSV    MULT 100       WWPASS  41
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPASS    ZFLD15
     C                     Z-ADD1         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6POAP
     C                     ELSE
     C                     MOVEL'  0.0'   R6POAP
     C                     END
      *
      **  PRINT PERCENTAGE OF LIABILITY
     C           WWPOTL    IFNE *ZEROS
     C                     Z-ADD*ZEROS    ZZLIAV
     C                     Z-ADD*ZEROS    WWPLIA
     C           WWPOTL    DIV  WWPOTL    ZZLIAV  43H
     C           ZZLIAV    MULT 100       WWPLIA  41
     C                     Z-ADD*ZEROS    ZFLD15
     C                     MOVE WWPLIA    ZFLD15
     C                     Z-ADD1         ZDECS
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R6POLP
     C                     ELSE
     C                     MOVEL'  0.0'   R6POLP
     C                     END
      *
     C  N01                WRITEP606POR
     C   01                WRITER606POR
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #TERM    - TERMINATION PROCESSING                                *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAIN PROCESSING                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWPROG   - DETERMINE WICH PGM CALL THIS ONE                      *
      *                                                                  *
      ********************************************************************
     C           #TERM     BEGSR
      *
     C  N01                CLOSEPM9950P1
     C   01                CLOSEPM0950P1
      *
     C                     MOVE '1'       *INLR
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * *PSSR    - ERROR IN FILE                                         *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #WDET    - SETUP FIELDS AND WRITE DETAILS INSTRUMENT             *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           *PSSR     BEGSR
      *
     C********** *LOCK     IN   LDA                                       CPM005
     C**********           MOVE WBFILE    DBFILE                          CPM005
     C**********           MOVE WBKEY     DBKEY                           CPM005
     C**********           Z-ADDWBASE     DBASE                           CPM005
     C**********           MOVE WBPGM     DBPGM                           CPM005
     C**********           OUT  LDA                                       CPM005
     C                     IN   LDA                                       CPM005
     C*                                                                   CPM005
     C                     OPEN PM6060AU                                  CPM005
     C                     WRITEPM6060F0                                  CPM005
     C                     WRITEDBERROR                                   CPM005
      *                                                                   CPM005
      ** Report printer file to RCF via ZSFILE                            CPM005
      *                                                                   CPM005
     C                     Z-ADDSFNUMU    ZSNUMU  60                      CPM005
     C                     EXSR SRZSFL                                    CPM005
      *                                                                   CPM005
     C                     CLOSEPM6060AU                                  CPM005
      *
     C                     MOVE '9'       WWRTCD
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     DUMP
      *
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************   CPM005
      /EJECT                                                              CPM005
      *****************************************************************   CPM005
      *                                                               *   CPM005
      *  Subroutine  :  SRZSFL                                        *   CPM005
      *  Function    :  Call ZSFILE                                   *   CPM005
      *                                                               *   CPM005
      *  Called by   :  #MAIN - Main Processing                       *   CPM005
      *                 *PSSR - File Error Handler                    *   CPM005
      *  Calls       :  ZSFILE                                        *   CPM005
      *                                                               *   CPM005
      *****************************************************************   CPM005
     C           SRZSFL    BEGSR                                          CPM005
      *                                                                   CPM005
     C                     CALL 'ZSFILE'                                  CPM005
     C                     PARM           SEQ     5                       CPM005
     C                     PARM           @ENT    3                       CPM005
     C                     PARM           SFILEU                          CPM005
     C                     PARM           ZSNUMU                          CPM005
     C                     PARM *BLANKS   ZSERR   1                       CPM005
      *                                                                   CPM005
      ** Error during ZSFILE processing, return to calling program        CPM005
      *                                                                   CPM005
     C           ZSERR     IFEQ 'Y'                                       CPM005
     C                     SETON                     U7U8LR               CPM005
     C                     RETRN                                          CPM005
     C                     ENDIF                                          CPM005
      *                                                                   CPM005
     C                     ENDSR                                          CPM005
     C**=================================================================
      /COPY ZSRSRC,ZSEDITZ3
      /COPY ZSRSRC,ZDATE2Z2
     C**=================================================================
     C***   CMD - COMMAND TO OVERIDE MESSAGE FILE                         CPM005
     C***OVRMSGF MSGF(QUSERMSG) TOMSGF(YYLGMSGF)                          CPM005
**   CMD - COMMAND TO OVERIDE MESSAGE FILE
OVRMSGF MSGF(QUSERMSG) TOMSGF(YYPMUSRMSG)
**  CPY@
(c) Finastra International Limited 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
