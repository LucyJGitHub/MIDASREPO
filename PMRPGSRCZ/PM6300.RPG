     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Change Cust/Port Base Ccy Report')
      *****************************************************************
      *                                                               *
      *  Midas - Portfolio Management Module                          *
      *                                                               *
      *  PM6300 - PM Change Customer/Portfolio Currency Maintenance   *
      *           Today Report                                        *
      *                                                               *
      *  Function:  This program produces a list of the Customer/     *
      *  (I/C)      Portfolio currencies which have been changed.     *
      *                                                               *
      *  Called By: PMC6300 - PM Change Customer/Portfolio Currency   *
      *                       Maintenance Today Report CLP            *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      *                 176443             Date 18Apr00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CPM005             Date 19Jun96               *
      *                 S01124             Date 26May94               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  176443 - Avoid MCH3601 to be sent.                           *
      *           Pass correct parameter to AOCUSTR0.                 *
      *  CPM005 - Private Banking Phase II                            *
      *           Focus Group Changes Upgraded to DBA                 *
      *  S01124 - PBFG/1 - Change Customer/Portfolio Base Currency    *
      *                                                               *
      *****************************************************************
     F***SDCUSTL1IF**E           K        DISK         KINFSR *PSSR       CPM005
      *******PREFIX*BB                                                    CPM005
     FPMPCBCL2IF  E           K        DISK         KINFSR *PSSR
      *      PREFIX FM
     F***PMPCBCZZIF**E                    DISK         KINFSR *PSSR       CPM005
     FPMPCBCPAIF  E                    DISK         KINFSR *PSSR          CPM005
      *      PREFIX FM
     F***SDBANKL1IF**E           K        DISK         KINFSR *PSSR       CPM005
      *******PREFIX*BJ                                                    CPM005
     F***PM6300P1O***E             70     PRINTER      KINFSR *PSSR       CPM005
     F***PM6300AUO***E                    PRINTER      KINFSR *PSSR       CPM005
     FPM6300P1O   E             70     PRINTER      KINFSR *PSSR      UC  CPM005
     F                                              KINFDS SPOOL1         CPM005
     FPM6300AUO   E                    PRINTER      KINFSR *PSSR          CPM005
     F                                              KINFDS SPOOLU         CPM005
      *****************************************************************
     E                    CPY@    1   1 80
      *
      /COPY ZSRSRC,ZDATE2Z1
      *****************************************************************
      *
      **  LOCAL DATA AREA FOR DATA BASE ERRORS:
     I****LDA****    DS                            256
     I***********                           132 183 DBLOT
     I***********                           132 141 DBFILE
     I***********                           142 170 DBKEY
     I***********                           171 180 DBPGM
     I***********                           181 1830DBASE
     ILDA       E DSLDA                         256                       CPM005
      *                                                                   CPM005
      ** Local data area for database error details                       CPM005
      ** *LOCK IN LDA immediately before and OUT LDA immediately          CPM005
      ** after each database error handling.                              CPM005
      **                                                                  CPM005
      **                                    134 141 DBFILE                CPM005
      **       Defines:                     142 170 DBKEY                 CPM005
      **                                    171 180 DBPGM                 CPM005
      **                                    181 1830DBASE                 CPM005
      *
     ISPOOL1      DS                                                      CPM005
      *                                                                   CPM005
      ** File Information Data Structure for PM6300P1                     CPM005
      *                                                                   CPM005
     I                                       83  92 SFILE1                CPM005
     I                                    B 123 1240SFNUM1                CPM005
     I                                    B 188 1890OFLLN1                CPM005
     I                                    B 367 3680PRTLN1                CPM005
      *                                                                   CPM005
     ISPOOLU      DS                                                      CPM005
      *                                                                   CPM005
      ** File Information Data Structure for PM6300AU                     CPM005
      *                                                                   CPM005
     I                                       83  92 SFILEU                CPM005
     I                                    B 123 1240SFNUMU                CPM005
      *                                                                   CPM005
      **  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION:
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     ISDBANK    E DSSDBANKPD                                              CPM005
      *                                                                   CPM005
      **  SD Bank Details                                                 CPM005
      *                                                                   CPM005
     ISDCUST    E DSSDCUSTPD                                              CPM005
      *                                                                   CPM005
      **  SD Customer Details                                             CPM005
      *                                                                   CPM005
     IDSFDY     E DSDSFDY                                                 CPM005
      *                                                                   CPM005
      ** Short Data Structure                                             CPM005
     IDSSDY     E DSDSSDY                                                 176443
      **  Second DS for access programs, Long Data Structure              176443
      *                                                                   CPM005
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INDEX OF SUBROUTINES                                             *
      * MAIN     -                                                       *
      * #INIT    - INITIAL PROCESSING                                    *
      * #MAIN    - MAIN PROCESSING                                       *
      * #TERM    - TERMINATION PROCESSING                                *
      * *PSSR    - ERROR IN FILE                                         *
      *                                                                  *
      * EXTERNAL ROUTINES CALLED                                         *
      *                                                                  *
      ********************************************************************
     C                     EXSR #INIT
      *
     C                     EXSR #MAIN
      *
     C                     EXSR #TERM
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #INIT    - INITIAL PROCESSING                                    *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * *PSSR    - ERROR IN FILE                                         *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           #INIT     BEGSR
      *
      **  PREPARE LDA:
     C           *NAMVAR   DEFN           LDA
     C***********          MOVEL*BLANK    DBLOT                           CPM005
     C                     MOVEL'PM6300'  DBPGM
      *
      ****GET*INSTALLATION*CONTROL*DATA*RECORD*1:**********************   CPM005
     C************LOVAL    SETLLSDBANKL1                                  CPM005
     C***********          READ SDBANKL1                 71               CPM005
      ***********                                                         CPM005
      ****IF*RECORD*FOUND:*********************************************   CPM005
     C************IN71     IFEQ '0'                        *B1            CPM005
      *                                                                   CPM005
      ** Receive Parameter List                                           CPM005
      *                                                                   CPM005
     C           *ENTRY    PLIST                                          CPM005
     C                     PARM           PSEQ    5                       CPM005
     C                     MOVELPSEQ      SEQ                             CPM005
      *                                                                   CPM005
      ** Access the Bank Details                                          CPM005
      *                                                                   CPM005
     C                     CALL 'AOBANKR0'                                CPM005
     C                     PARM *BLANKS   WRTCD   7                       CPM005
     C                     PARM '*FIRST ' WOPTN   7                       CPM005
     C           SDBANK    PARM SDBANK    DSFDY                           CPM005
      *                                                                   CPM005
     C           WRTCD     IFNE *BLANKS                                   CPM005
     C           *LOCK     IN   LDA                                       CPM005
     C                     Z-ADD001       DBASE            ***************CPM005
     C                     MOVE 'SDBANKPD'DBFILE           * DB ERROR 01 *CPM005
     C                     MOVEL'*FIRST'  DBKEY            ***************CPM005
     C                     OUT  LDA                                       CPM005
     C                     EXSR *PSSR                                     CPM005
     C                     ENDIF                                          CPM005
      *
      **  CHECK SYSTEM DATE FORMAT (DDMMYY OR MMDDYY):
     C           BJDFIN    IFEQ 'M'                        **B2
     C                     MOVE '1'       *IN98            ON => MMDDYY
     C                     END                             **E2
      ***********                                                         CPM005
     C***********          ELSE                            *X1            CPM005
      ***********                                                         CPM005
      ****OTHERWISE*DB*ERROR:******************************************   CPM005
     C***********          Z-ADD1         DBASE            ***************CPM005
     C***********          MOVEL'SDBANKL1'DBFILE           * DB ERROR 01 *CPM005
     C***********          MOVEL*BLANKS   DBKEY            ***************CPM005
     C***********          EXSR *PSSR                                     CPM005
     C***********          END                             *E1            CPM005
      *
      **  GET CHANGE BASE CURRENCY TRAILER
     C***********1         SETLLPMPCBCZZ                                  CPM005
     C***********          READ PMPCBCZZ                 71               CPM005
     C           1         SETLLPMPCBCPA                                  CPM005
     C                     READ PMPCBCPA                 71               CPM005
      *
      **  IF RECORD NOT FOUND, DATABASE ERROR
     C           *IN71     IFEQ '1'                        *B1
     C                     Z-ADD2         DBASE            ***************
     C***********          MOVEL'PMPCBCZZ'DBFILE           * DB ERROR 02 *CPM005
     C                     MOVE 'PMPCBCPA'DBFILE           * DB ERROR 02 *CPM005
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     END                             *E1
      *
      *
     C***********          WRITEHEADER                                    CPM005
     C                     MOVE 'N'       WOPEN   1                       CPM005
      *
     C                     Z-ADD*ZEROS    WWNOIN  50
     C                     Z-ADD*ZEROS    WWNOAM  50
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #MAIN    - MAIN PROCESSING                                       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZDATE2   -                                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           #MAIN     BEGSR
      *
     C           *LOVAL    SETLLPMPCBCL2
     C                     READ PMPCBCL2                 72
      *
     C           *IN72     IFEQ '1'                        *B1
     C***********          WRITENODET                                     CPM005
     C                     MOVE 'Y'       WNODET  1                       CPM005
     C                     EXSR AUDIT                                     CPM005
     C                     ELSE                            *X1
      *
     C                     MOVE 'N'       WNODET                          CPM005
      *                                                                   CPM005
     C           *IN72     DOWEQ'0'                        *BW1
      *
     C           FMCUST    IFNE WWCUST
     C           FMRECI    ANDEQ'D'
     C           FMPTFR    ORNE WWPTFR
     C           FMRECI    ANDEQ'D'
      *                                                                   CPM005
      ** Open printer file                                                CPM005
      *                                                                   CPM005
     C           WOPEN     IFEQ 'N'                                       CPM005
     C                     MOVE 'Y'       WOPEN                           CPM005
     C                     OPEN PM6300P1                                  CPM005
      *                                                                   CPM005
      ** RCF Processing for PM6300P1 File                                 CPM005
      *                                                                   CPM005
     C                     EXSR RCFP1                                     CPM005
      *                                                                   CPM005
      ** Write the Header                                                 CPM005
      *                                                                   CPM005
     C                     WRITEHEADER                                    CPM005
     C                     ENDIF                                          CPM005
      *                                                                   CPM005
     C**********           MOVE FMCUST    WWCUST  60                                          CSD027
     C                     MOVE FMCUST    WWCUST  6                                           CSD027
     C                     MOVE FMPTFR    WWPTFR  4
     C*
     C                     MOVE FMCUST    RRCUST
     C*
     C** SETUP CUSTOMER SHORTNAME
     C***********RRCUST    CHAINSDCUSTL1             75                   CPM005
      *                                                                   CPM005
      ** Access the Customer Details                                      CPM005
      *                                                                   CPM005
     C                     MOVELRRCUST    WRCUST 10                       CPM005
     C                     CALL 'AOCUSTR0'                                CPM005
     C                     PARM *BLANKS   WRTCD                           CPM005
     C                     PARM '*KEY   ' WOPTN                           CPM005
     C                     PARM           WRCUST                          CPM005
     C                     PARM *BLANKS   WKSTS   7                       CPM005
     C***********SDCUST    PARM SDCUST    DSFDY                     CPM005176443
     C           SDCUST    PARM SDCUST    DSSDY                           176443
      *                                                                   CPM005
     C************IN75     IFEQ '0'                                       CPM005
     C           WRTCD     IFEQ *BLANKS                                   CPM005
     C                     MOVELBBCSSN    RRCSSN
     C                     ELSE
     C                     MOVE *BLANKS   RRCSSN
     C                     END
     C*
     C                     MOVE FMPTFR    RRPTFR
      *
     C                     Z-ADDFMCDAT    ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZDATE     RRCDAT
      *
     C                     MOVE FMSTCY    RRSTCY
     C                     MOVE FMNBCY    RRNBCY
      *
     C           FMCHTP    IFEQ 'I'                        **B2
     C                     ADD  1         WWNOIN
     C                     MOVEL'INSERT'  RRCHTP    P
     C                     ELSE                            **X2
     C                     ADD  1         WWNOAM
     C                     MOVEL'AMEND'   RRCHTP    P
     C                     END                             **B2
      *
     C                     Z-ADDFMLCD     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZADATE    RRLCD
      *
     C                     MOVELFMUSER    RRUSER
      *
     C                     WRITEDETAI
      *
     C           *IN70     IFEQ '1'                        **B2
     C                     WRITEHEADER
     C                     MOVE '0'       *IN70
     C                     END                             **E2
     C                     END
      *
     C                     READ PMPCBCL2                 72
      *
     C                     END                             *EW1
      *
     C                     END                             *E1
      *
     C                     Z-ADDWWNOIN    RRNOIN
     C                     Z-ADDWWNOAM    RRNOAM
     C                     Z-ADDFMNORE    R1NORE
      *
     C***********          WRITEHEADAU                                    CPM005
     C***********          WRITECONTROAU                                  CPM005
     C***********          WRITEENDAU                                     CPM005
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #TERM    - TERMINATION PROCESSING                                *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           #TERM     BEGSR
      *                                                                   CPM005
     C           WOPEN     IFEQ 'Y'                                       CPM005
     C                     WRITEENDRE
      *                                                                   CPM005
      ** Close printer file                                               CPM005
      *                                                                   CPM005
     C                     CLOSEPM6300P1                                  CPM005
     C                     ENDIF                                          CPM005
      *                                                                   CPM005
     C                     EXSR AUDIT                                     CPM005
      *                                                                   CPM005
     C                     MOVE '1'       *INLR
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * *PSSR    - ERROR IN FILE                                         *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #INIT    - INITIAL PROCESSING                                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           *PSSR     BEGSR
      *
     C************LOCK     IN   LDA                                       CPM005
     C***********          OUT  LDA                                       CPM005
      ***********                                                         CPM005
     C***********          Z-ADDDBASE     WWBASE                          CPM005
     C***********          MOVELDBFILE    WWFILE                          CPM005
     C***********          MOVELDBKEY     WWKEY                           CPM005
     C***********          MOVELDBPGM     WWPGM                           CPM005
      ***********                                                         CPM005
     C***********          WRITEHEADER                                    CPM005
     C***********          WRITEERRORAU                                   CPM005
     C***********          WRITEENDRE                                     CPM005
      *
      ***  Check to see that *PSSR has not already been called.           CPM005
      *                                                                   CPM005
     C           WRUN      IFEQ *BLANK                                    CPM005
     C                     MOVE 'Y'       WRUN    1                       CPM005
      *                                                                   CPM005
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     DUMP
     C                     EXSR AUDIT                                     CPM005
     C                     ENDIF                                          CPM005
      *
      ***  If *PSSR already run, then just end the program here.          CPM005
      *                                                                   CPM005
     C                     MOVE '1'       *INU7                           CPM005
     C                     MOVE '1'       *INU8                           CPM005
     C                     MOVE '1'       *INLR                           CPM005
      *                                                                   CPM005
     C                     RETRN
      *
     C                     ENDSR
      *                                                                   CPM005
      *****************************************************************   CPM005
      /TITLE SR/AUDIT                                                     CPM005
      *****************************************************************   CPM005
      *                                                               *   CPM005
      *  AUDIT  - Subroutine to Output Audit report and End Program.  *   CPM005
      *                                                               *   CPM005
      *  Called By: Main Processing, PROCES, *PSSR                    *   CPM005
      *  Calls: None.                                                 *   CPM005
      *                                                               *   CPM005
      *****************************************************************   CPM005
      *                                                                   CPM005
     C           AUDIT     BEGSR                           *** AUDIT  *** CPM005
      *                                                                   CPM005
      ** RCF Processing for Audit File.                                   CPM005
      *                                                                   CPM005
     C                     EXSR RCFAU                                     CPM005
      *                                                                   CPM005
      ** Output Report Header and File Controls.                          CPM005
      *                                                                   CPM005
     C                     WRITEHEADAU                                    CPM005
     C                     WRITECONTROAU                                  CPM005
      *                                                                   CPM005
      ** If there is a DB Error, Output the DB Error Information.         CPM005
      *                                                                   CPM005
     C           *INU7     IFEQ *ON                                       CPM005
     C                     WRITEDBERROR                                   CPM005
     C                     ELSE                                           CPM005
      *                                                                   CPM005
      ** Or, if no records read, Output "No Details" message.             CPM005
      *                                                                   CPM005
     C           WNODET    IFEQ 'Y'                                       CPM005
     C                     WRITENODTLS                                    CPM005
     C                     ENDIF                                          CPM005
     C                     ENDIF                                          CPM005
      *                                                                   CPM005
      ** End Program and Return.                                          CPM005
      *                                                                   CPM005
     C                     MOVE '1'       *INLR                           CPM005
     C                     RETRN                                          CPM005
      *                                                                   CPM005
     C                     ENDSR                                          CPM005
      *                                                                   CPM005
      *****************************************************************   CPM005
      /TITLE SR/RCFP1                                                     CPM005
      *****************************************************************   CPM005
      *                                                               *   CPM005
      *  RCFP1  - Subroutine to register the P1 Printer File via RCF. *   CPM005
      *                                                               *   CPM005
      *****************************************************************   CPM005
      *                                                                   CPM005
     C           RCFP1     BEGSR                            *** RCFP1  ***CPM005
      *                                                                   CPM005
      ** Ensure Report Spool File recorded by RCF.                        CPM005
      *                                                                   CPM005
     C                     Z-ADDSFNUM1    ZSNUM   60                      CPM005
      *                                                                   CPM005
     C                     CALL 'ZSFILE'                                  CPM005
     C                     PARM           SEQ     5                       CPM005
     C                     PARM *BLANKS   SENTY   3                       CPM005
     C                     PARM           SFILE1                          CPM005
     C                     PARM           ZSNUM                           CPM005
     C                     PARM           ZSERR   1                       CPM005
      *                                                                   CPM005
      ** If Error occurs during ZSFILE processing, then return to the     CPM005
      ** Calling Program.                                                 CPM005
      *                                                                   CPM005
     C           ZSERR     IFEQ 'Y'                                       CPM005
     C                     MOVE '1'       *INU7                           CPM005
     C                     MOVE '1'       *INU8                           CPM005
     C                     MOVE '1'       *INLR                           CPM005
     C                     RETRN                                          CPM005
     C                     ENDIF                                          CPM005
      *                                                                   CPM005
     C                     ENDSR                                          CPM005
      *                                                                   CPM005
      *****************************************************************   CPM005
      /TITLE SR/RCFAU                                                     CPM005
      *****************************************************************   CPM005
      *                                                               *   CPM005
      *  RCFAU  - Subroutine to register the AU Printer File via RCF. *   CPM005
      *                                                               *   CPM005
      *****************************************************************   CPM005
      *                                                                   CPM005
     C           RCFAU     BEGSR                            *** RCFAU  ***CPM005
      *                                                                   CPM005
      ** Ensure Audit Spool File recorded by RCF.                         CPM005
      *                                                                   CPM005
     C                     Z-ADDSFNUMU    ZSNUMU  60                      CPM005
      *                                                                   CPM005
     C                     CALL 'ZSFILE'                                  CPM005
     C                     PARM           SEQ                             CPM005
     C                     PARM *BLANKS   SENTY                           CPM005
     C                     PARM           SFILEU                          CPM005
     C                     PARM           ZSNUMU                          CPM005
     C                     PARM *BLANK    ZSERR                           CPM005
      *                                                                   CPM005
      ** If Error occurs during ZSFILE processing, then return to the     CPM005
      ** Calling Program.                                                 CPM005
      *                                                                   CPM005
     C           ZSERR     IFEQ 'Y'                                       CPM005
     C                     MOVE '1'       *INU7                           CPM005
     C                     MOVE '1'       *INU8                           CPM005
     C                     MOVE '1'       *INLR                           CPM005
     C                     RETRN                                          CPM005
     C                     ENDIF                                          CPM005
      *                                                                   CPM005
     C                     ENDSR                                          CPM005
      *                                                                   CPM005
     C**=================================================================
     C/EJECT
      /COPY ZSRSRC,ZDATE2Z2
     C/EJECT
**  CPY@
(c) Finastra International Limited 2001
**  ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**  ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**  ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
