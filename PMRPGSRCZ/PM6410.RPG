     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Setup Details for Portfolio Statement')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management Module                          *
     F*                                                               *
     F*  PM6410 - SETUP DETAILS FOR PORTFOLIO STATEMENTS              *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CPM005             Date 19JUN96               *
     F*                 S01124             Date 20JUN94               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  CPM005 - Private Banking Phase 2                             *
     F*           Focus Group Changes upgraded to DBA                 *
     F*  S01124  -  PBFG/1 - CHANGE CUSTOMER/PORTFOLIO BASE CURRENCY  *
     F*                                                               *
     F*****************************************************************
     F***PMRQXSPPO***E                    DISK         KINFSR *PSSR       CPM005
     FPMRQXSPDO   E                    DISK         KINFSR *PSSR          CPM005
     FPMPCBCL1IF  E           K        DISK         KINFSR *PSSR
     F***SDBANKL1IF**E           K        DISK         KINFSR *PSSR       CPM005
      *****************************************************************
     E                    CPY@    1   1 80
      *****************************************************************
      *
      **  LOCAL DATA AREA FOR DATA BASE ERRORS:
     ILDA         DS                            256
     I***********                           132 183 DBLOT                 CPM005
     I***********                           132 141 DBFILE                CPM005
     I                                      134 183 DBLOT                 CPM005
     I                                      134 141 DBFILE                CPM005
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      *
      **  DATA STRUCTURE FOR COMPILATION  - COPYRIGHT INSERTION:
     ICPYR@#      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@##
     I*                                                                   CPM005
     ISDBANK    E DSSDBANKPD                                              CPM005
     I** EXTERNAL DS FOR BANK DETAILS                                     CPM005
     I*                                                                   CPM005
     IDSFDY     E DSDSFDY                                                 CPM005
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                CPM005
     I*                                                                   CPM005
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INDEX OF SUBROUTINES                                             *
      * MAIN     -                                                       *
      * #INIT    - INITIAL PROCESSING                                    *
      * #MAIN    - MAIN PROCESSING                                       *
      * #TERM    - TERMINATION PROCESSING                                *
      * *PSSR    - ERROR IN FILE                                         *
      *                                                                  *
      * EXTERNAL ROUTINES CALLED                                         *
      *                                                                  *
      ********************************************************************
     C                     EXSR #INIT
      *
     C                     EXSR #MAIN
      *
     C                     EXSR #TERM
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #INIT    - INITIAL PROCESSING                                    *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * *PSSR    - ERROR IN FILE                                         *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           #INIT     BEGSR
      *
      **  PREPARE LDA:
     C           *NAMVAR   DEFN           LDA
     C                     MOVEL*BLANK    DBLOT
     C                     MOVEL'PM6410'  DBPGM
      *
      **  GET INSTALLATION CONTROL DATA RECORD 1:
     C************LOVAL    SETLLSDBANKL1                                  CPM005
     C***********          READ SDBANKL1                 71               CPM005
     C                     CALL 'AOBANKR0'                                CPM005
     C                     PARM *BLANKS   WWRTCD  7                       CPM005
     C                     PARM '*FIRST ' WWOPTN  7                       CPM005
     C           SDBANK    PARM SDBANK    DSFDY                           CPM005
      *
      **  IF RECORD NOT FOUND, DB ERROR
     C************IN71     IFEQ '1'                        *B1            CPM005
     C           WWRTCD    IFNE *BLANKS                                   CPM005
     C                     Z-ADD1         DBASE            ***************
     C***********          MOVEL'SDBANKL1'DBFILE           * DB ERROR 01 *CPM005
     C                     MOVE 'SDBANKPD'DBFILE           * DB ERROR 01 *CPM005
     C                     MOVEL*BLANKS   DBKEY            ***************
     C                     EXSR *PSSR
     C                     END                             *E1
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #MAIN    - MAIN PROCESSING                                       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZDATE2   -                                                       *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           #MAIN     BEGSR
      *
     C           *LOVAL    SETLLPMPCBCL1
     C                     READ PMPCBCL1                 72
      *
     C           *IN72     DOWEQ'0'                        *BW1
      *
     C**********           Z-ADDFMCUST    QOCNUM           Customer Number                    CSD027
     C                     MOVE FMCUST    QOCNUM           Customer Number                    CSD027
      *
     C           FMPTFR    IFEQ *BLANKS
     C                     MOVE FMPTF2    QOPTFR           Portfolio Ref.
     C                     ELSE
     C                     MOVE FMPTFR    QOPTFR           Portfolio Ref.
     C                     END
      *
     C                     Z-ADDFMCDAT    QOVLOR           Date Extra Statement
     C                     MOVE 'I'       QOWHEN           When Produced
     C                     MOVE FMSTTP    QOVLNT           Type Extra Statement
     C                     MOVE FMSTFM    QOFRMT           Format Extra Statemen
     C                     MOVE FMPODT    QOS1VR           Position Details
     C                     MOVE FMSETT    QOS2VR           Sector Breakdown
     C                     MOVE FMPEPY    QOS3VR           Past Year Performance
     C                     MOVE FMPETY    QOS4VR           Tax Year Performance
     C                     MOVE FMPEEX    QOS5VR           Additional Performanc
     C                     MOVE 'N'       QOS6VR           Daily Cashflow
     C                     MOVE 'N'       QOS7VR           Monthly Cashflow
     C                     MOVE 'N'       QOS8VR           Yearly Cashflow
     C                     MOVE '99999'   QOTNLA           Transaction Number
     C                     MOVE FMUSER    QOUSER           Requested by User
     C                     Z-ADDFMTNLU    QOTNLU           TNLU Unique Key
     C                     MOVE FMSUIN    QOS9VR           Instrument Summary
     C                     MOVE FMSUCY    QOS0VR           Currency Summary
      *
     C                     WRITEPMRQXSP0
      *
     C                     READ PMPCBCL1                 72
     C                     END                             *EW1
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #TERM    - TERMINATION PROCESSING                                *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     -                                                       *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           #TERM     BEGSR
      *
     C                     MOVE '1'       *INLR
     C                     RETRN
      *
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * *PSSR    - ERROR IN FILE                                         *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #INIT    - INITIAL PROCESSING                                    *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           *PSSR     BEGSR
      *
     C           *LOCK     IN   LDA
     C                     OUT  LDA
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C                     DUMP
      *
     C                     RETRN
      *
     C                     ENDSR
     C**=================================================================
     C/EJECT
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
