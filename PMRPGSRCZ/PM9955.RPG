     H        1                                                           S01486
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Port.Valn Statement:Port Summary-Indy')       *
     H***********                                                         S01486
     F*****************************************************************
     F*                                                               *
     F*  Midas - Portfolio Management                             *
     F*                                                               *
     F*  PM9955 - PORTFOLIO VALUATION STATEMENT:                      *
     F*           PORTFOLIO SUMMARY - INDUSTRY                        *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      *  Last Amend No. CER059             Date 19Jul10               *
      *  Prev Amend No. CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 076624             Date 21AUG96               *
     F*                 S01486             Date 06SEP94               *
     F*                 056552  LJ         DATE 16JUN93               *
     F*                 48007              DATE 01DEC92               *
     F*                 47719              DATE 24NON92               *
     F*                 S01313             DATE 11JUN92               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus                                          *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  076624 - PM9950P1 change - recompile                         *
     F*  S01486 - Private Banking upgrade to Release 10               *
     F*  056552 - VALUATION STATEMENT OUTPUT NARRATIVES DETAILS       *
     F*  48007  - SUNDRY FIXES TO VALUATION STATEMENT                 *
     F*  47719  - CORRECT CONVERSION OF AMOUNTS BETWEEN CURRENCIES    *
     F*  S01313 - REWRITTEN FOR PORTFOLIO PERFORMANCE                 *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F**
     F**      FILE DEFINITIONS
     F**     ------------------
     F*
     F**   PM INDUSTRY TOTALS. PREFIX - QI
     F***PMINDTPPIF**E           K        DISK         KINFSR *PSSR       S01486
     FPMINDTPDIF  E           K        DISK         KINFSR *PSSR          S01486
     F***********                                                         S01486
     F*****LOCAL*INDUSTRY*CODES.*PREFIX*-*AO******************************S01486
     F***SDINDSL1IF**E           K        DISK         KINFSR *PSSR       S01486
     F*
     F**   PM PORTFOLIO TOTALS. PREFIX - QC
     F***PMPOTTPPIF**E           K        DISK         KINFSR *PSSR       S01486
     FPMPOTTPDIF  E           K        DISK         KINFSR *PSSR          S01486
     F**   Audit Control                                                  S01486
     FPM9955AUO   E                    PRINTER      KINFSR *PSSR      UC  S01486
     F                                              KINFDS SPOOLU         S01486
     F*
     F**   PORTFOLIO VALUATION STATEMENT PORTFOLIO SUMMARY
     FPM9950P1O   E             60     PRINTER      KINFDS OVRFL1     UC
     F                                              KINFSR *PSSR
     F*
     F********************************************************************
     F/EJECT
     F********************************************************************
     F**                                                                 *
     F** ID F  C  H  L    FUNCTION OF INDICATORS                         *
     F**                 ------------------------                        *
     F** 30-33 NUMBER DECIMAL PLACES: PORTFOLIO CURRENCY                 *
     F***50*-*FILE*INDICATOR*ON*PMINDTPP**********************************S01486
     F***51*-*FILE*INDICATOR*ON*SDINDSL1**********************************S01486
     F***52*-*FILE*INDICATOR*ON*PMPOTTPP**********************************S01486
     F** 50 - FILE INDICATOR ON PMINDTPD                                 *S01486
     F** 52 - FILE INDICATOR ON PMPOTTPD                                 *S01486
     F**    60 - OVERFLOW INDICATOR ON PM9950S1                          *
     F**          U7, U8 - DATABASE ERROR HANDLING                       *
     F********************************************************************
     F/EJECT
     E********************************************************************
     E*
     E** ARRAY FOR COPYRIGHT
     E                    CPY@    1   1 80
     E*
     E** ARRAY USED IN CURRENCY AMOUNT CONVERSION
      ****/COPY ZSRSRC,ZPOWERZ1                                           47719
     E*
     E**   FOR USE WITH ZSEDIT
      /COPY ZSRSRC,ZSEDITZ1
     E*
     E**   ARRAY TO FACILITATE OUTPUT OF UNDERLINES FOR TOTALS
     E                    UL1        21  1
     E                    UL2        21  1
     E********************************************************************
     E/EJECT
     I********************************************************************
     I*
     I** DATA STRUCTURE FOR COMPILATION - COPYRIGHT INSERTION
     ICPYR@$      DS
     I                                        1  80 CPY@
     I                                        1  20 CPY@$$
     IPSDS       SDS
     I*
     I**   PROGRAM STATUS DATA STRUCTURE
     I                                      244 253 JOB
     I                                      254 263 USER
     I*
     I**   FILE INFORMATION DATA STRUCTURE
     IOVRFL1      DS
     I*
     I**   FOR OVERFLOW
     I                                    B 188 1890WWOFL1
     I                                    B 367 3680WWPTL1
     I*                                                                   S01486
     ISPOOLU      DS                                                      S01486
     I**    SPOOL FILE INFORMATION DATA STRUCTURE                         S01486
     I                                       83  92 SFILEU                S01486
     I                                    B 123 1240SFNUMU                S01486
     ILDA         DS                            256
     I*
     I**   LOCAL DATA AREA
     I                                      132 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I**   ZSEDIT DATA STRUCTURE
      /COPY ZSRSRC,ZSEDITZ2
     I*
     I**   DATA STRUCTURE TO SET LOCAL INDUSTRY CODE
     I            DS
     I**********                              1   30QILIND                                    CER020
     I                                        1   3 QILOIC                                    CER020
     I                                        1   3 WWLIND
     I*
     I** DS USED TO PASS PARAMETERS TO CALLED PROGRAMS
     I***WWPARM****E*DSPM095XPP                                           S01486
     IWWPARM    E DSPM095XPD                                              S01486
     I*                                                                   S01486
     ISDINDS    E DSSDINDSPD                                              S01486
     I** DUMMY RECORD FORMAT FOR INDUSTRY DETAILS                         S01486
     I*                                                                   S01486
     IDSFDY     E DSDSFDY                                                 S01486
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE                S01486
     I*                                                                   S01486
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * INDEX OF SUBROUTINES                                             *
      * MAIN     - MAINLINE                                              *
      * #A       - INITIALISATION                                        *
      * #B       - PROCESS INDUSTRY DETAILS                              *
      * #BA      - PRODUCE VALUATION STATEMENT                           *
      * #C       - PRODUCE PORTFOLIO TOTALS                              *
      * #P06     - FORMAT TOTALS UNDERLINE                               *
      * *PSSR    - PSSR SUBROUTINE                                       *
      * ZSEDIT   - EDIT NUMERICS FOR OUTPUT TO REPORT                    *
      * ZCCYCN   - CURRENCY CONVERSION SUBROUTINE 1                      *
      * ZCCYXR   - CURRENCY CONVERSION SUBROUTINE 2                      *
      *                                                                  *
      * EXTERNAL ROUTINES CALLED                                         *
      * SUBR23R3 - MESSAGE RETRIEVAL SUBROUTINE                          *
      *                                                                  *
      ********************************************************************
     C/EJECT
     C           *ENTRY    PLIST
     C                     PARM           WWPARM
     C*
     C**   PLIST FOR USE WITH MESSAGE TEXT HANDLING
     C           SUBR23    PLIST
     C                     PARM           VARMSG  7
     C                     PARM           TEXT   40
     C                     PARM           LEVEL   10
     C                     PARM           RETCD   10
     C                     PARM           TXTL    40
     C**
     C** SET UP PARAMETER TO FORM OUTPUT LINE
     C           ZA1311    PLIST
     C                     PARM           MSGID   7
     C                     PARM           MSGDT0 40
     C                     PARM           MSGDT1 40
     C                     PARM           MSGDT2 40
     C                     PARM           MSGDT3 40
     C                     PARM           MSGDT4 40
     C                     PARM           MSGDT5 40
     C                     PARM           MSGDT6 40
     C                     PARM           MSGDT7 40
     C                     PARM           MSGDT8 40
     C                     PARM           MSGDT9 40
     C                     PARM           RESULT200
     C*
     C*****KEY*LIST*TO*ACCESS*FILES*PMPOTTPP*&*PMINDTPP*******************S01486
     C**   KEY LIST TO ACCESS FILES PMPOTTPD & PMINDTPD                   S01486
     C           WWKEY1    KLIST
     C                     KFLD           WWCNUM
     C                     KFLD           WWPTFF
     C********************************************************************
     C/EJECT
     C********************************************************************
     C*
     C**              START MAINLINE
     C**             ----------------
     C*
     C**   INITIALISATION
     C                     EXSR #A
     C*
     C**   PROCESS INDUSTRY DETAILS
     C                     EXSR #B
     C*
     C**   PRODUCE PORTFOLIO TOTALS
     C                     EXSR #C
     C*
     C**   END PROGRAM
     C                     CLOSEPM9950P1
     C                     RETRN
     C                     MOVE '1'       *INLR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #A       - INITIALISATION                                        *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * SUBR23R3 - MESSAGE RETRIEVAL SUBROUTINE                          *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAINLINE                                              *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWHDWT   - HEADER WRITTEN FLAG                                   *
      * WWDEC0   - WORKFIELD DECIMAL PLACES: 0                           *
      * WWDEC1   - WORKFIELD DECIMAL PLACES: 1                           *
      * WWDEC2   - WORKFIELD DECIMAL PLACES: 2                           *
      * WWDEC3   - WORKFIELD DECIMAL PLACES: 3                           *
      * WWECDT   - EVENT CONTROL DATE                                    *
      * WWCRNM   - CUSTOMER NAME                                         *
      * WWPTFR   - PORTFOLIO REFERENCE                                   *
      * WWPTFN   - PORTFOLIO NARRATIVE                                   *
      * WWPTCY   - PORTFOLIO CURRENCY                                    *
      * WWPCDP   - PORTFOLIO CURRENCY NUMBER OF DECIMAL PLACES           *
      * WWKEY1   - KEY LIST                                              *
      * WWTCVL   - TOTAL CURRENT VALUE                                   *
      * WWTINT   - TOTAL INTEREST                                        *
      * WWTVPS   - TRADE/VALUE DATE INDICATOR                            *
      * WWTOTL   - PORTFOLIO TOTAL                                       *
      * WWCVAL   - INDUSTRY CURRENT VALUE                                *
      * WWTVAL   - INDUSTRY TOTAL                                        *
      *                                                                  *
      ********************************************************************
     C           #A        BEGSR
     C*
     C**   INITIALISE DB ERROR FIELDS AND STORE PROGRAM NAME FOR
     C**   DB ERROR FIELD.
     C                     MOVE *BLANKS   WBFILE 10
     C                     MOVE *BLANKS   WBKEY  29
     C                     MOVE *BLANKS   WBASE   30
     C                     MOVEL'PM9955'  WBPGM  10
     C                     Z-ADD3         LEVEL
     C                     Z-ADD40        TXTL
     C                     MOVE 'N'       WWHDWT  1
     C                     MOVE 'J'       ZECODE
     C                     Z-ADD0         X       20
     C                     Z-ADD0         Y       20
     C                     Z-ADD0         WWDEC0 150
     C                     Z-ADD0         WWDEC1 151
     C                     Z-ADD0         WWDEC2 152
     C                     Z-ADD0         WWDEC3 153
     C*
     C/COPY WNCPYSRC,PM9955C001
     C                     MOVE WWCRNM    P1CRNM
     C/COPY WNCPYSRC,PM9955C002
     C                     MOVE WWPTFN    R2PTFN
     C                     MOVE WWCNUM    R1CNUM
     C                     MOVELWWACON    R1ACNM
     C                     MOVE WWAPHN    R1ACPH
     C*
     C** ON CHANGE OF LANGUAGE ACCESS NEW MESSAGE TEXT
     C           WWLANG    IFNE WSLANG
     C                     MOVE WWLANG    WSLANG  2
     C*
      * GET MES. CLIENT:
     C***********          MOVEL'PMS5502' VARMSG                          S01486
     C                     MOVEL'PM95401' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      M95102
      * GET MES. PORTFOLIO SUMMARY BY COUNTRY
     C***********          MOVEL'PMS5504' VARMSG                          S01486
     C                     MOVEL'PM95466' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      MSGDT0
     C*
     C***********          MOVE 'PMS5503' MSGID                           S01486
     C                     MOVE 'PM95403' MSGID                           S01486
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    P1REPT
      * GET MES. REF.NO.
     C***********          MOVEL'PMS5505' VARMSG                          S01486
     C                     MOVEL'PM95407' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      M95104
      * GET MES. YOUR ADVISER:
     C***********          MOVEL'PMS5506' VARMSG                          S01486
     C                     MOVEL'PM95408' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      M95105
      * GET MES. INDUSTRY
     C***********          MOVEL'PMS5508' VARMSG                          S01486
     C                     MOVEL'PM95421' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      MS5508
      * GET MES. TOTAL AMOUNTS
     C***********          MOVEL'PMS5509' VARMSG                          S01486
     C                     MOVEL'PM95413' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      MS5509
      **GET*MES.*PORTFOLIO*SHARES*IN*%************************************S01486
     C***********          MOVEL'PMS5510' VARMSG                          S01486
     C***********          CALL 'SUBR23R3'SUBR23                          S01486
     C***********          MOVELTEXT      MS5510 31                       S01486
      * GET MES. TOTAL
     C***********          MOVEL'PMS5511' VARMSG                          S01486
     C                     MOVEL'PM95415' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      MS5511
      **GET*MES.*AS*OF****************************************************S01486
     C***********          MOVEL'PMS5114' VARMSG                          S01486
     C***********          CALL 'SUBR23R3'SUBR23                          S01486
     C***********          MOVELTEXT      M95114 15                       S01486
      * GET MES. HOLD MAIL
     C***********          MOVEL'PMS5117' VARMSG                          S01486
     C                     MOVEL'PM95410' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      M95117 29
      * GET MES. INTERNAL USE
     C***********          MOVEL'PMS5120' VARMSG                          S01486
     C                     MOVEL'PM95411' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23
     C                     MOVELTEXT      M95120 29
     C                     END
     C*
     C** FORMAT DETAILS WHICH CHANGE EACH TIME
     C           WWHLDM    IFEQ 'H'
     C                     MOVELM95117    P1HOLD 29
     C                     ELSE
     C           WWHLDM    IFEQ 'A'
     C                     MOVELM95120    P1HOLD 29
     C                     ELSE
     C                     MOVE *BLANKS   P1HOLD
     C                     END
     C                     END
     C*
     C** FORMAT REFERENCE CURRENCY
     C                     MOVE *BLANKS   MSGDT0
     C                     MOVELWWPTCY    MSGDT0
     C***********          MOVE 'PMS5507' MSGID                           S01486
     C                     MOVE 'PM95409' MSGID                           S01486
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    R1RCCY
     C*
     C** FORMAT PRINTED 'AS OF' INFORMATION
     C                     MOVE *BLANKS   MSGDT0
     C                     MOVELWWLXDT    MSGDT0
     C***********          MOVE 'PMS5114' MSGID                           S01486
     C                     MOVE 'PM95406' MSGID                           S01486
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    P1PRDT
     C*
     C** FORMAT PRINTED PLACE AND DATE
     C                     MOVE *BLANKS   MSGDT0
     C                     MOVELWWBMSN    MSGDT0
     C                     MOVE *BLANKS   MSGDT1
     C                     MOVELWWECDT    MSGDT1
     C***********          MOVE 'PMS5116' MSGID                           S01486
     C                     MOVE 'PM95402' MSGID                           S01486
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    P1BRDT
     C                     MOVE *BLANKS   MSGDT1
      **GET*MES.*IN*******************************************************S01486
     C***********          MOVEL'PMS5233' VARMSG                          S01486
     C***********          CALL 'SUBR23R3'SUBR23                          S01486
     C***********          MOVELTEXT      MS5233 17                       S01486
     C*
      * GET MES. IN                                                       056552
     C***********          MOVEL'PMS5815' VARMSG                    056552S01486
     C                     MOVEL'PM95414' VARMSG                          S01486
     C                     CALL 'SUBR23R3'SUBR23                          056552
     C                     MOVELTEXT      MS5815 17                       056552
     C                     MOVE WWPTCY    WWPCCY
     C*
     C**   SET DECIMAL PLACES AS NECESSARY
     C                     MOVEA'0000'    *IN,30
     C           30        ADD  WWPCDP    X
     C                     MOVEA'1'       *IN,X
     C*
     C*****ACCESS*PMPOTTPP************************************************S01486
     C**   ACCESS PMPOTTPD                                                S01486
     C***********WWKEY1    CHAINPMPOTTPP             52                   S01486
     C           WWKEY1    CHAINPMPOTTPD             52                   S01486
     C*
     C**   IF RECORD NOT FOUND THEN ZEROISE TOTAL CURRENT VALUE AND
     C**   TOTAL INTEREST
     C           *IN52     IFEQ '1'
     C*
     C                     Z-ADD*ZERO     WWTCVL 150
     C                     Z-ADD*ZERO     WWTINT 150
     C                     ELSE
     C*
     C           WWTVPS    IFEQ 'T'
     C*
     C           QCPTPA    SUB  QCPTNA    WWTCVL
     C                     SUB  QCPTMV    WWTCVL
     C                     ELSE
     C*
     C           WWTVPS    IFEQ 'V'
     C*
     C           QCPVPA    SUB  QCPVNA    WWTCVL
     C                     SUB  QCPVMV    WWTCVL
     C                     END
     C*
     C                     END
     C*
     C                     Z-ADDQCPTAI    WWTINT
     C*
     C                     END
     C*
     C           WWTCVL    ADD  WWTINT    WWTOTL 150
     C*
     C                     Z-ADD*ZERO     WWCVAL 150
     C                     Z-ADD*ZERO     WWTVAL 150
     C*
     C** OPEN PRINTER FILE
     C                     OPEN PM9950P1
     C*
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #B       - PROCESS INDUSTRY DETAILS                              *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * #BA      - PRODUCE VALUATION STATEMENT                           *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAINLINE                                              *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWKEY1   - KEY LIST                                              *
      * WWHDWT   - HEADER WRITTEN FLAG                                   *
      * WWLINE   - LINES LEFT ON PRINTER PAGE                            *
      *                                                                  *
      ********************************************************************
     C           #B        BEGSR
     C*
     C*****SET*LOWER*LIMITS*AND*READ*PMINDTPP*FOR*THE*PASSED*CUSTOMER*****S01486
     C**   SET LOWER LIMITS AND READ PMINDTPD FOR THE PASSED CUSTOMER     S01486
     C**   & PORTFOLIO REFERENCE
     C***********WWKEY1    SETLLPMINDTPP                                  S01486
     C           WWKEY1    SETLLPMINDTPD                                  S01486
     C*
     C           *IN50     DOUEQ'1'                        B1
     C*
     C           WWHDWT    IFEQ 'N'                        B2
     C*
     C                     ADD  1         WWPNBR
     C                     MOVE *BLANKS   MSGDT0
     C                     MOVELWWPNBR    MSGDT0
     C***********          MOVE 'PMS5501' MSGID                           S01486
     C                     MOVE 'PM95405' MSGID                           S01486
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    R1PAGE
     C                     WRITEP951HD2
     C                     WRITEP955HDR
     C                     MOVE 'Y'       WWHDWT
     C*
     C                     ELSE                            X2
     C*
     C                     EXSR #BA
     C*
     C           WWOFL1    SUB  WWPTL1    WWLINE  20
     C*
     C           WWLINE    IFLT 3                          B3
     C                     ADD  1         WWPNBR
     C                     MOVE *BLANKS   MSGDT0
     C                     MOVELWWPNBR    MSGDT0
     C***********          MOVE 'PMS5501' MSGID                           S01486
     C                     MOVE 'PM95405' MSGID                           S01486
     C                     CALL 'ZA1310'  ZA1311
     C                     MOVELRESULT    R1PAGE
     C                     WRITEP951HD2
     C                     WRITEP955HDR
     C                     END                             E3
     C*
     C                     END                             E2
     C*
     C***********WWKEY1    READEPMINDTPP                 50               S01486
     C           WWKEY1    READEPMINDTPD                 50               S01486
     C*
     C                     END                             E1
     C*
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #BA      - PRODUCE VALUATION STATEMENT                           *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZSEDIT   - EDIT NUMERICS FOR OUTPUT TO REPORT                    *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * #B       - PROCESS INDUSTRY DETAILS                              *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWLIND   - LOCAL INDUSTRY CODE                                   *
      * WWRTCD   - RETURN CODE                                           *
      * WWTVPS   - TRADE/VALUE DATE INDICATOR                            *
      * WWCVAL   - INDUSTRY CURRENT VALUE                                *
      * WWDEC0   - WORKFIELD DECIMAL PLACES: 0                           *
      * WWDEC1   - WORKFIELD DECIMAL PLACES: 1                           *
      * WWDEC2   - WORKFIELD DECIMAL PLACES: 2                           *
      * WWDEC3   - WORKFIELD DECIMAL PLACES: 3                           *
      * WWTVAL   - INDUSTRY TOTAL                                        *
      * WWTOTL   - PORTFOLIO TOTAL                                       *
      * WWPERC   - WORKFIELD TO STORE PERCENTAGE                         *
      * WWPPER   - PORTFOLIO PERCENTAGE                                  *
      *                                                                  *
      ********************************************************************
     C           #BA       BEGSR
     C*
     C**   ACCESS SDINDSL1
     C***********WWLIND    CHAINSDINDSL1             51                   S01486
     C                     CALL 'AOINDSR0'                                S01486
     C                     PARM *BLANKS   RTCD    7                       S01486
     C                     PARM '*KEY'    OPTN    7                       S01486
     C                     PARM WWLIND    KEYIN   3                       S01486
     C           SDINDS    PARM SDINDS    DSFDY                           S01486
     C*
     C**   IF RECORD NOT FOUND THEN DB ERROR
     C************IN51     IFEQ '1'                                       S01486
     C           RTCD      IFNE *BLANKS                                   S01486
     C                     MOVE '001'     WBASE
     C                     MOVE *BLANKS   WBFILE           ****************
     C                     MOVEL'SDINDSL1'WBFILE           * DBERROR  001 *
     C                     MOVELWWLIND    WBKEY            ****************
     C                     MOVE '9'       WWRTCD
     C                     EXSR *PSSR
     C                     ELSE                            X1
     C*
     C           *IN51     IFEQ '0'                        B2
     C*
     C                     MOVELAOLINA    R5INNM
     C*
     C           WWTVPS    IFEQ 'T'                        B3
     C*
     C           QIPTPA    SUB  QIPTNA    WWCVAL
     C           WWCVAL    SUB  QIPTMV    WWCVAL                          48007
     C                     ELSE                            X3
     C*
     C           WWTVPS    IFEQ 'V'                        B4
     C*
     C           QIPVPA    SUB  QIPVNA    WWCVAL
     C           WWCVAL    SUB  QIPVMV    WWCVAL                          48007
     C                     END                             E4
     C*
     C                     END                             E3
     C*
     C**   MOVE INDUSTRY CURRENT VALUE TO FIELD WITH CORRECT NUMBER OF
     C**   DECIMAL PLACES
     C           *IN30     IFEQ '1'                        B3
     C                     MOVE WWCVAL    WWDEC0
     C                     Z-ADDWWDEC0    ZFLD15
     C                     ELSE                            X3
     C*
     C           *IN31     IFEQ '1'                        B4
     C                     MOVE WWCVAL    WWDEC1
     C                     Z-ADDWWDEC1    ZFLD15    H
     C                     ELSE                            X4
     C*
     C           *IN32     IFEQ '1'                        B5
     C                     MOVE WWCVAL    WWDEC2
     C                     Z-ADDWWDEC2    ZFLD15    H
     C                     ELSE                            X5
     C*
     C           *IN33     IFEQ '1'                        B6
     C                     MOVE WWCVAL    WWDEC3
     C                     Z-ADDWWDEC3    ZFLD15    H
     C                     END                             E6
     C*
     C                     END                             E5
     C*
     C                     END                             E4
     C*
     C                     END                             E3
     C*
     C                     Z-ADDWWCVAL    ZFLD15
     C                     Z-ADDWWPCDP    ZDECS
     C*
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R5CVAL 18
     C*
     C** SET UP FIELD CONTAINING TRADE OR VALUE DATE ACCRUED
     C** INTEREST AS APPROPRIATE
     C                     Z-ADDQIPTAI    WWACIN 150
     C*
     C           WWACIN    IFNE *ZERO                      B3
     C*
     C**   MOVE ACCRUED INTEREST TO A FIELD WITH CORRECT NUMBER OF
     C**   DECIMAL PLACES
     C           *IN30     IFEQ '1'                        B4
     C                     MOVE WWACIN    WWDEC0
     C                     Z-ADDWWDEC0    ZFLD15
     C                     ELSE                            X4
     C*
     C           *IN31     IFEQ '1'                        B5
     C                     MOVE WWACIN    WWDEC1
     C                     Z-ADDWWDEC1    ZFLD15    H
     C                     ELSE                            X5
     C*
     C           *IN32     IFEQ '1'                        B6
     C                     MOVE WWACIN    WWDEC2
     C                     Z-ADDWWDEC2    ZFLD15    H
     C                     ELSE                            X6
     C*
     C           *IN33     IFEQ '1'                        B7
     C                     MOVE WWACIN    WWDEC3
     C                     Z-ADDWWDEC3    ZFLD15    H
     C                     END                             E7
     C*
     C                     END                             E6
     C*
     C                     END                             E5
     C*
     C                     END                             E4
     C*
     C           WWCVAL    ADD  WWACIN    WWTVAL
     C*
     C**   MOVE INDUSTRY TOTAL TO A FIELD WITH CORRECT NUMBER OF
     C**   DECIMAL PLACES
     C           *IN30     IFEQ '1'                        B4
     C                     MOVE WWTVAL    WWDEC0
     C                     Z-ADDWWDEC0    ZFLD15
     C                     ELSE                            X4
     C*
     C           *IN31     IFEQ '1'                        B5
     C                     MOVE WWTVAL    WWDEC1
     C                     Z-ADDWWDEC1    ZFLD15    H
     C                     ELSE                            X5
     C*
     C           *IN32     IFEQ '1'                        B6
     C                     MOVE WWTVAL    WWDEC2
     C                     Z-ADDWWDEC2    ZFLD15    H
     C                     ELSE                            X6
     C*
     C           *IN33     IFEQ '1'                        B7
     C                     MOVE WWTVAL    WWDEC3
     C                     Z-ADDWWDEC3    ZFLD15    H
     C                     END                             E7
     C*
     C                     END                             E6
     C*
     C                     END                             E5
     C*
     C                     END                             E4
     C*
     C                     Z-ADDWWTVAL    ZFLD15
     C                     Z-ADDWWPCDP    ZDECS
     C*
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R5TVAL
     C*
     C                     ELSE                            X3
     C*
     C                     MOVE R5CVAL    R5TVAL
     C                     Z-ADDWWCVAL    WWTVAL
     C*
     C                     END                             E3
     C*
     C**   CALCULATE PORTFOLIO PERCENTAGE AS NECESSARY
     C           WWTOTL    IFNE *ZERO                      B3
     C*
     C           WWTVAL    DIV  WWTOTL    WWPERC  73H
     C*
     C           WWPERC    MULT 100       WWPPER  41
     C*
     C           WWPERC    IFLE 9.999                      B4
     C           WWPERC    ANDGE-9.999
     C                     Z-ADD0         ZFLD15
     C                     MOVE WWPPER    ZFLD15
     C                     Z-ADD1         ZDECS
     C                     EXSR ZSEDIT
     C*
     C                     MOVE ZOUT21    R5PPER
     C*
     C                     ELSE                            X4
     C                     MOVEL'  N/A '  R5PPER
     C                     END                             E4
     C*
     C                     ELSE                            X3
     C*
     C                     MOVE *ZERO     R5PPER
     C*
     C                     END                             E3
     C*
     C**   WRITE INDUSTRY DETAIL LINE
     C           WWCVAL    IFNE 0                          B3
     C           WWACIN    ORNE 0
     C                     WRITEP955DET
     C                     END                             E3
     C*
     C                     END                             E2
     C*
     C                     END                             E1
     C*
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * #C       - PRODUCE PORTFOLIO TOTALS                              *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      * ZSEDIT   - EDIT NUMERICS FOR OUTPUT TO REPORT                    *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      * MAIN     - MAINLINE                                              *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      * WWDEC0   - WORKFIELD DECIMAL PLACES: 0                           *
      * WWDEC1   - WORKFIELD DECIMAL PLACES: 1                           *
      * WWDEC2   - WORKFIELD DECIMAL PLACES: 2                           *
      * WWDEC3   - WORKFIELD DECIMAL PLACES: 3                           *
      * WWTINT   - TOTAL INTEREST                                        *
      *                                                                  *
      ********************************************************************
     C           #C        BEGSR
     C*
     C**   MOVE TOTAL CURRENT VALUE TO FIELD WITH CORRECT NUMBER OF
     C**   DECIMAL PLACES
     C           *IN30     IFEQ '1'
     C                     MOVE WWTCVL    WWDEC0
     C                     Z-ADDWWDEC0    ZFLD15
     C                     ELSE
     C*
     C           *IN31     IFEQ '1'
     C                     MOVE WWTCVL    WWDEC1
     C                     Z-ADDWWDEC1    ZFLD15    H
     C                     ELSE
     C*
     C           *IN32     IFEQ '1'
     C                     MOVE WWTCVL    WWDEC2
     C                     Z-ADDWWDEC2    ZFLD15    H
     C                     ELSE
     C*
     C           *IN33     IFEQ '1'
     C                     MOVE WWTCVL    WWDEC3
     C                     Z-ADDWWDEC3    ZFLD15    H
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     Z-ADDWWTCVL    ZFLD15
     C                     Z-ADDWWPCDP    ZDECS
     C*
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R5TCVL 15
     C*
     C           WWTINT    IFNE *ZERO
     C*
     C**   MOVE TOTAL INTEREST TO A FIELD WITH CORRECT NUMBER OF
     C**   DECIMAL PLACES
     C           *IN30     IFEQ '1'
     C                     MOVE WWTINT    WWDEC0
     C                     Z-ADDWWDEC0    ZFLD15
     C                     ELSE
     C*
     C           *IN31     IFEQ '1'
     C                     MOVE WWTINT    WWDEC1
     C                     Z-ADDWWDEC1    ZFLD15    H
     C                     ELSE
     C*
     C           *IN32     IFEQ '1'
     C                     MOVE WWTINT    WWDEC2
     C                     Z-ADDWWDEC2    ZFLD15    H
     C                     ELSE
     C*
     C           *IN33     IFEQ '1'
     C                     MOVE WWTINT    WWDEC3
     C                     Z-ADDWWDEC3    ZFLD15    H
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C**   MOVE TOTAL PORTFOLIO CURRENCY TO FIELD WITH CORRECT NUMBER
     C**   OF DECIMAL PLACES
     C           *IN30     IFEQ '1'
     C                     MOVE WWTOTL    WWDEC0
     C                     Z-ADDWWDEC0    ZFLD15
     C                     ELSE
     C*
     C           *IN31     IFEQ '1'
     C                     MOVE WWTOTL    WWDEC1
     C                     Z-ADDWWDEC1    ZFLD15    H
     C                     ELSE
     C*
     C           *IN32     IFEQ '1'
     C                     MOVE WWTOTL    WWDEC2
     C                     Z-ADDWWDEC2    ZFLD15    H
     C                     ELSE
     C*
     C           *IN33     IFEQ '1'
     C                     MOVE WWTOTL    WWDEC3
     C                     Z-ADDWWDEC3    ZFLD15    H
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     END
     C*
     C                     Z-ADDWWTOTL    ZFLD15
     C                     Z-ADDWWPCDP    ZDECS
     C*
     C                     EXSR ZSEDIT
     C                     MOVE ZOUT21    R5TOTL
     C*
     C                     ELSE
     C                     MOVE R5TCVL    R5TOTL
     C*
     C                     END
     C*
     C**   WRITE PORTFOLIO TOTALS
     C           WWTCVL    IFNE 0
     C           WWTINT    ORNE 0
     C                     WRITEP955TOT
     C                     END
     C*
     C                     ENDSR
      ********************************************************************
      /EJECT
      ********************************************************************
      *                                                                  *
      * *PSSR    - PSSR SUBROUTINE                                       *
      *                                                                  *
      * SUBROUTINE CALLS:                                                *
      *                                                                  *
      * SUBROUTINE CALLED FROM:                                          *
      *                                                                  *
      * ROUTINE USES VARIABLES:                                          *
      *                                                                  *
      ********************************************************************
     C           *PSSR     BEGSR
     C*
     C** UPDATE DTAARA/LDA WITH DETAILS OF ERROR
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE WBFILE    DBFILE
     C                     MOVE WBKEY     DBKEY
     C                     Z-ADDWBASE     DBASE
     C                     MOVE WBPGM     DBPGM
     C                     OUT  LDA
     C                     OPEN PM9955AU                                  S01486
     C                     WRITEPM9955F0                                  S01486
     C                     WRITEPM9955F1                                  S01486
     C*                                                                   S01486
     C** Report printer file to RCF via ZSFILE                            S01486
     C*                                                                   S01486
     C                     Z-ADDSFNUMU    ZSNUMU  60                      S01486
     C                     CALL 'ZSFILE'                                  S01486
     C                     PARM           SEQ     5                       S01486
     C                     PARM           @ENT    3                       S01486
     C                     PARM           SFILEU                          S01486
     C                     PARM           ZSNUMU                          S01486
     C                     PARM *BLANKS   ZSERR   1                       S01486
     C*                                                                   S01486
     C** Error during ZSFILE processing, return to calling program        S01486
     C*                                                                   S01486
     C           ZSERR     IFEQ 'Y'                                       S01486
     C                     SETON                     U7U8LR               S01486
     C                     END                                            S01486
     C*
     C                     DUMP
     C*
     C                     MOVE '9'       WWRTCD
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
     C*
     C                     RETRN
     C*
     C           #PSSR0    ENDSR
      ********************************************************************
      /EJECT
      /COPY ZSRSRC,ZSEDITZ3
      ****/EJECT                                                          47719
      ****/COPY ZSRSRC,ZCCYCN                                             47719
      ****/EJECT                                                          47719
      ****/COPY ZSRSRC,ZCCYXR                                             47719
     C/EJECT
** CPY@
(c) Misys International Banking Systems Ltd. 2001
