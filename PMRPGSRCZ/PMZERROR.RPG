     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Standard error report program.')
      *****************************************************************
      *                                                               *
      *  Midas - Portfolio Management Module                          *
      *                                                               *
      *  PMZERROR      - Standard error report                        *
      *                                                               *
      *  Function:  This program provides a standard error report.    *
      *             It is passed parameters which include the         *
      *              failing program's status data structure.         *
      *                                                               *
      *  Called By: Many programs, as required.                       *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 09Dec05               *
      *  Prev Amend No. CSC022             Date 24Feb04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CPM004             Date 07Feb96               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSC022 - Commitment Control Changes for MidasPlus            *
      *  CPM004 - Bank Portfolios                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Indicator usage                                              *
      *  ~~~~~~~~~~~~~~~                                              *
      *  61 -- Printer overflow.                                      *
      *  62 -- A value matches one of the elements of the             *
      *        commited jobs array.(for CSC022)                       *
      *  90 -- Error detected.                                        *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  Subroutine usage                                             *
      *  ~~~~~~~~~~~~~~~~                                             *
      *                                                               *
      *  SRINIT -- Provides initialisation and definitions.           *
      *                                                               *
      *  Copied in routines:                                          *
      *                                                               *
      *  *PSSR  -- Program error routine.                             *
      *  SRFILE -- File error routine.                                *
      *  SRERR  -- Error reporting routine.                           *
      *                                                               *
      *****************************************************************
     F/SPACE
      *-------------------------------------------------------------------
      * Copied-in file definitions:
      *
     F/COPY WNCPYSRC,ERRORCFPG
      *-------------------------------------------------------------------
     FPMZERRP1O   E             61     PRINTER      KINFSR SRFILE
     F                                              KINFDS PRTINF
      * Printer file
      *-------------------------------------------------------------------
     FSDBANKPDIF  E                    DISK         KINFSR SRFILE
      * Bank details
      *-------------------------------------------------------------------
     E/EJECT
      *-------------------------------------------------------------------
      * Error processing array:
      *
     E/COPY PMCPYSRC,SRERRE
      *
     E                    CPY@    1   1 80               Copyright
     E                    EDT        80  1               Edit string.
      ** Array of Commitment Job Names                                                        CSC022
     E                    CMTARR     10 10                                                    CSC022
      *
      * Copied-in array definitions:
      *
     E/COPY WNCPYSRC,ERRORCEPG
      *-------------------------------------------------------------------
     I/EJECT
      *-------------------------------------------------------------------
      * Define run-data data area:
      *
     IRUNDTA    E DSRUNDAT
      *...................................................................
      * Error processing data structures:
      *
     I/COPY PMCPYSRC,SRERRI
      *...................................................................
     IPRTINF      DS
      * Printer file information data structure:
      *
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *...................................................................
      * Input parameter data structures -- printer file field names:
      *
     IF3PSDS      DS
      *
      * Program status data structure:
     I                                        1  10 P3PGM
     I                                       11  150P3STCD
     I                                       16  200P3STCP
     I                                       21  28 P3SQNO
     I                                       29  36 P3RTVN
     I                                       37  390P3PMCT
     I                                       40  46 P3MSID
     I                                       47  50 P3MINB
     I                                       51  80 P3MSWK
     I                                       81  90 P3PGLB
     I                                       91 170 P3MSDA
     I                                      171 174 P3EXID
     I                                      201 208 P3ERFL
     I                                      209 243 P3ERST
     I                                      244 253 P3JOB
     I                                      254 263 P3USR
     I                                      264 2690P3JNO
     I                                      270 2750P3EDT
     I                                      276 2810P3SDT
     I                                      282 2870P3STM
     I                                      288 293 P3CPDT
     I                                      294 299 P3CPTM
     I                                      300 303 P3CPLV
     I                                      304 313 P3SRFL
     I                                      314 323 P3SRLB
     I                                      324 333 P3SRMB
     IF4SEND      DS
      *
      * Send message data structure
     I                                        1  10 P4PGM
     I                                       11  20 P4FILE
     I                                       21  50 P4KEY
     I                                       51  550P4ERNB
     I                                       56 135 P4NARR
     I                                      136 145 P4STK
     I                                      146 175 P4PREF
      *...................................................................
      * Data structure for compilation - Copyright insertion:
      *
     ICPYR@#      DS
     I                                        1  80 CPY@
      *                                                                                       CSC022
      ** Data structure for SCCMTJOB data area                                                CSC022
     ISCCMT       DS                            256                                           CSC022
     I                                        1   30CMTNUM                                    CSC022
     I                                        4 103 CMTJOB                                    CSC022
      ** First DS for Access programs - short data structure                                  CSC022
     IDSFDY     E DSDSFDY                                                                     CSC022
      ** External DS for API ICD                                                              CSC022
     ISCSARD    E DSSCSARDPD                                                                  CSC022
      *                                                                                       CSC022
     I********************************************************************
     C/EJECT
      ********************************************************************
      *                 M A I N L I N E
      ********************************************************************
     C           *ENTRY    PLIST
     C                     PARM           W0RTN   7
     C                     PARM           F3PSDS
     C                     PARM           F4SEND
     C                     PARM           F4STK 100
      *
      ** Initialize SAR work flag and Commit flag                                             CSC022
     C                     MOVEL'N'       CSC022  1                                           CSC022
     C                     MOVEL' '       WSKPCM  1                                           CSC022
      *                                                                                       CSC022
      ** Determine if Commitment Control Changes for MidasPlus                                CSC022
      ** (CSC022) is installed                                                                CSC022
      *                                                                                       CSC022
     C                     CALL 'AOSARDR0'                                                    CSC022
     C                     PARM *BLANKS   @RTCD   7                                           CSC022
     C                     PARM '*VERIFY' @OPTN   7                                           CSC022
     C                     PARM 'CSC022'  PSARD   6                                           CSC022
     C           SCSARD    PARM SCSARD    DSFDY                                               CSC022
      **                                                                                      CSC022
      *                                                                                       CSC022
      ** If feature is on, set up SAR work flag                                               CSC022
      *                                                                                       CSC022
     C           @RTCD     IFEQ *BLANKS                                                       CSC022
     C                     MOVEL'Y'       CSC022                                              CSC022
     C                     MOVEL'N'       WSKPCM                                              CSC022
     C           *NAMVAR   DEFN SCCMTJOB  SCCMT                                               CSC022
     C                     IN   SCCMT                                                         CSC022
     C           CMTNUM    IFNE 0                                                             CSC022
     C                     MOVEACMTJOB    CMTARR                                              CSC022
     C           P3JOB     LOKUPCMTARR                   62                                   CSC022
     C           *IN62     IFEQ *ON                                                           CSC022
     C                     MOVEL'Y'       WSKPCM                                              CSC022
     C                     ENDIF                                                              CSC022
     C                     ENDIF                                                              CSC022
     C                     ELSE                                                               CSC022
      *                                                                                       CSC022
      ** else, database error (return code other than *NRF)                                   CSC022
      *                                                                                       CSC022
     C           @RTCD     IFNE '*NRF   '                                                     CSC022
     C           *LOCK     IN   LDA                                                           CSC022
     C                     MOVEL'PMZERROR'DBPGM                                               CSC022
     C                     MOVEL'SCSARDPD'DBFILE                                              CSC022
     C                     MOVEL'CSC022'  DBKEY                                 ************* CSC022
     C                     Z-ADD900       DBASE                                 * DBASE 900 * CSC022
     C                     OUT  LDA                                             ************* CSC022
     C                     EXSR *PSSR                                                         CSC022
     C                     ENDIF                                                              CSC022
      *                                                                                       CSC022
     C                     ENDIF                                                              CSC022
      * Add subroutine to stack:
      *
     C                     ADD  1         Q
     C                     MOVEL'MAIN'    @STK,Q
      *
      * Initialise program:
      *
     C                     EXSR SRINIT
      *
      * Split the subroutine stack value:
      *
     C                     MOVELF4STK     P4STK1
     C                     MOVE F4STK     P4STK2
      *
      * Produce the report:
      *
     C                     WRITEF1HED1
     C                     WRITEF2HED2
     C                     WRITEF3DETL
     C                     WRITEF4DETL
     C                     WRITEF5END
      *
      * Unwind subroutine stack:
      *
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
      *
      * Return to calling program:
      *
     C                     SETON                     LR
      ********************************************************************
     C/EJECT
      ********************************************************************
      **  Subroutine SRINIT provides initialisation and definition.     **
      ********************************************************************
     C           SRINIT    BEGSR                           * S R I N I T *
      *
      * Add subroutine to stack:
      *
     C                     ADD  1         Q       50
     C                     MOVEL'SRINIT'  @STK,Q
      *
      * Move copyright statement:
      *
     C                     MOVEACPY@      CPY2@  80
      *
      * Get Run-date:
      *
     C           *NAMVAR   DEFN RUNDAT    RUNDTA
     C                     IN   RUNDTA
     C                     MOVE AGMRDT    ##MRDT  7        Midas Run date
     C                     MOVE AGMRDT    WUMRDT  7        Midas Run date
     C                     MOVE AGRDNB    WURDNB  50       Run day number
     C                     MOVE AGSUC     WUSUC   1        Set up complete
     C                     MOVE AGDFF     WUDFF   1        Date Format
     C                     MOVE AGMBIN    WUMBIN  1        Multi Branched
      *
      * Extract bank details:
      *
     C           1         CHAINSDBANKPD             9090
     C           *IN90     IFEQ *ON
     C                     MOVEL'SDBANKPD'W0FILE
     C                     MOVEL'*FIRST  'W0KEY            ***************
     C                     Z-ADD1         W0ERNB           ** Error 1.  **
     C                     MOVEL'MEM5003' W0MSGD           ***************
     C                     MOVEL'MIDAS  ' W0MSGF
     C                     EXSR SRERR
     C                     ENDIF
      *...................................................................
     C/EJECT
      *...................................................................
      * Set multi-branch indicator:
      *
     C                     SETOF                     70
     C           WUMBIN    IFNE 'Y'
     C                     SETON                     70
     C                     ENDIF
      *
     C           EXINIT    TAG                             <<<=== EXINIT
      *
      * Unwind subroutine stack:
      *
     C                     MOVE *BLANKS   @STK,Q
     C                     SUB  1         Q
      *
     C                     ENDSR
      ********************************************************************
     C/EJECT
      ********************************************************************
      * /Copy point for calculations:
      *
      *
      *
      * Note: this routine is copied in, and modified,
      *       to avoid recursively calling this program
      *
     C/COPY WNCPYSRC,ERRORCCPG
      ********************************************************************
      ** Subroutine *PSSR handles program errors within THIS program.
      ********************************************************************
     C           *PSSR     BEGSR                           ** *PSSR  **
      *
     C                     DUMP
      *   ROLLBACK data if either of the following is true:                                   CSC022
      *     1. Commitment Control Changes switch is OFF; or                                   CSC022
      *     2. Commitment Control Changes switch is ON and                                    CSC022
      *        this particular job is not included in the                                     CSC022
      *        commitment jobs in data area SCCMTJOB                                          CSC022
     C           CSC022    IFEQ 'N'                                                           CSC022
     C           CSC022    OREQ 'Y'                                                           CSC022
     C           WSKPCM    ANDEQ'N'                                                           CSC022
     C                     ROLBK                       90
     C                     ENDIF                                                              CSC022
      *
      * If second time through, halt:
      *
     C           @@PSSR    IFNE *BLANK
     C                     SETON                     H1  LR
     C                     RETRN
     C                     ENDIF
      *
     C                     MOVE 'Y'       @@PSSR  1
      *
      * Set LDA values and format message for MOPERQ:
      *
     C                     MOVE ##PGM     ERPGM
     C                     MOVE ##RTVN    ERFILE
     C           ##MSID    CAT  ##SQNO    ERKEY     P
     C                     Z-ADD998       ERERNB
     C                     MOVEL##MSWK    ERNARR
      *
      * Move data to the LDA data area:
      *
     C           *LOCK     IN   LDA
     C                     MOVELW0KEY     DBKEY
     C                     Z-ADDW0ERNB    DBASE
     C                     MOVELW0FILE    DBFILE
     C                     MOVEL##PGM     DBPGM
     C                     OUT  LDA
      * End the program:
     C                     SETON                     U7U8LR
     C                     MOVE '*ERROR*' W0RTN   7
     C                     RETRN
      *
     C                     ENDSR
      ********************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRFILE   : *PSSR routine for all files                       *
      *                                                               *
      *  CALLED BY: IBM controlled - invalid access to file           *
      *                                                               *
      *  CALLS    : PMZERROR                                          *
      *                                                               *
      * In the INIT sub-routine, the fields W0MSGD and W0MSGF should  *
      * be set to the default message I.D and message file that are   *
      * used for the module. If not, the defaults of MEM5002 and MIDAS*
      * will be used.                                                 *
      *                                                               *
      *****************************************************************
     C           SRFILE    BEGSR
      *
      * Dump program
      *
     C                     DUMP
      *   ROLLBACK data if either of the following is true:                                   CSC022
      *     1. Commitment Control Changes switch is OFF; or                                   CSC022
      *     2. Commitment Control Changes switch is ON and                                    CSC022
      *        this particular job is not included in the                                     CSC022
      *        commitment jobs in data area SCCMTJOB                                          CSC022
     C           CSC022    IFEQ 'N'                                                           CSC022
     C           CSC022    OREQ 'Y'                                                           CSC022
     C           WSKPCM    ANDEQ'N'                                                           CSC022
     C                     ROLBK                       90
     C                     ENDIF                                                              CSC022
      *
      * If called again, seton LR and return
      *
     C           @@FILE    IFNE *BLANKS
     C                     MOVEL'1'       *INLR
     C                     RETRN
     C                     END
      *
     C                     MOVEL'Y'       @@FILE  1
      *
      * Send message to MOPERQ
      *
     C                     MOVEL*BLANKS   ERDTA
     C                     MOVEL##PGM     ERPGM
      *
     C           Q         IFGT 1
     C           Q         ANDLT101
     C                     MOVEL@STK,Q    ERSTK
     C                     ELSE
     C                     MOVEL'*Nostkin'ERSTK
     C                     END
      *
     C                     MOVEL##ERST    ERKEY
     C                     Z-ADD999       ERERNB
     C                     MOVEL##ERFL    ERFILE
     C                     MOVELW0PREF    ERPREF
      *
      * Get message for sending and place in msg field
      *
     C                     CALL 'SDRTVTXT'             9090
     C                     PARM 'MEM5002' #MSGID
     C                     PARM 'MIDAS   '#MSGF
     C                     PARM *BLANKS   #MSGTX
      *
     C                     MOVEL#MSGTX    ERNARR
      *
      * Send message
      *
     C                     CALL 'AOCREPT'              9090
     C                     PARM 'MEM5000' #MSGID  7
     C                     PARM 'MIDAS  ' #MSGF  10
     C                     PARM *BLANKS   #MSGFL 10
     C                     PARM ERDTA     #MSGDT256
     C                     PARM '*PRV'    #MSGR   5
     C                     PARM '*'       #PRGM  10
     C                     PARM 'MOPERQ ' #MSGQ  10
     C                     PARM '*INFO  ' #MSGT   7
      *
      * Fill LDA with error data
      *
     C           *LOCK     IN   LDA
     C                     MOVELW0KEY     DBKEY
     C                     Z-ADDW0ERNB    DBASE
     C                     MOVELW0FILE    DBFILE
     C                     MOVEL##PGM     DBPGM
     C                     OUT  LDA
      *
      * Close down program
      *
     C                     SETON                     U7U8LR
     C                     MOVEL'*ERROR*' W0RTN   7
     C*
     C                     RETRN
      *
     C           EXFILE    ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *  SRERR    : Report error and close down program               *
      *                                                               *
      *  CALLED BY: All subroutines                                   *
      *                                                               *
      *  CALLS    : PMZERROR                                          *
      *                                                               *
      * In the INIT sub-routine, the fields W0MSGD and W0MSGF should  *
      * be set to the default message I.D and message file that are   *
      * used for the module. If not, the defaults of MEM5001 and MIDAS*
      * will be used.                                                 *
      *                                                               *
      *****************************************************************
     C           SRERR     BEGSR
      *
      * Define work variables
      *
     C           *LIKE     DEFN ERKEY     W0KEY
     C           *LIKE     DEFN ERERNB    W0ERNB
     C           *LIKE     DEFN ERFILE    W0FILE
     C           *LIKE     DEFN ERPREF    W0PREF           Primary Ref.
      *
     C           *LIKE     DEFN #MSGID    W0MSGD           Msg I.D.
     C           *LIKE     DEFN #MSGF     W0MSGF           Msg File
      *
     C                     Z-ADDQ         Q       50
      *
      * Dump program
      *
     C                     DUMP
      *   ROLLBACK data if either of the following is true:                                   CSC022
      *     1. Commitment Control Changes switch is OFF; or                                   CSC022
      *     2. Commitment Control Changes switch is ON and                                    CSC022
      *        this particular job is not included in the                                     CSC022
      *        commitment jobs in data area SCCMTJOB                                          CSC022
     C           CSC022    IFEQ 'N'                                                           CSC022
     C           CSC022    OREQ 'Y'                                                           CSC022
     C           WSKPCM    ANDEQ'N'                                                           CSC022
     C                     ROLBK                       90
     C                     ENDIF                                                              CSC022
      *
      * Set up the details of the message to be sent to MOPERQ
      *
     C                     MOVEL*BLANKS   ERDTA
     C                     MOVEL##PGM     ERPGM
      *
     C           Q         IFGT 1
     C           Q         ANDLT101
     C                     MOVEL@STK,Q    ERSTK
     C                     ELSE
     C                     MOVEL'*Nostkin'ERSTK
     C                     END
      *
     C                     MOVELW0KEY     ERKEY
     C                     Z-ADDW0ERNB    ERERNB
     C                     MOVELW0FILE    ERFILE
     C                     MOVELW0PREF    ERPREF
      *
      * Get message for message code. If the msg code or the msg file
      *  have not been set up then use the defaults.
      *
     C           W0MSGD    IFEQ *BLANKS
     C                     MOVEL'MEM5001' C#FMSG  7
     C                     ELSE
     C                     MOVELW0MSGD    C#FMSG
     C                     END
      *
     C           W0MSGF    IFEQ *BLANKS
     C                     MOVEL'MIDAS   'C#FMSF 10
     C                     ELSE
     C                     MOVELW0MSGF    C#FMSF
     C                     END
      *
     C                     CALL 'SDRTVTXT'             9090
     C                     PARM C#FMSG    #MSGID
     C                     PARM C#FMSF    #MSGF
     C                     PARM *BLANKS   #MSGTX
      *
     C                     MOVEL#MSGTX    ERNARR
      *
      * Send message
      *
     C                     CALL 'AOCREPT'              9090
     C                     PARM 'MEM5000' #MSGID  7
     C                     PARM 'MIDAS  ' #MSGF  10
     C                     PARM *BLANKS   #MSGFL 10
     C                     PARM ERDTA     #MSGDT256
     C                     PARM '*PRV '   #MSGR   5
     C                     PARM '*'       #PRGM  10
     C                     PARM 'MOPERQ ' #MSGQ  10
     C                     PARM '*INFO  ' #MSGT   7
      *
      * Fill LDA with error data
      *
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVELW0KEY     DBKEY
     C                     Z-ADDW0ERNB    DBASE
     C                     MOVELW0FILE    DBFILE
     C                     MOVEL##PGM     DBPGM
     C                     OUT  LDA
      *
      * Close down program
      *
     C                     SETON                     U7U8LR
     C                     MOVEL'*ERROR*' W0RTN   7
      *
      * End the program:
     C                     RETRN
      *
     C           EXERR     ENDSR
      ********************************************************************
     O/EJECT
      ********************************************************************
      * /Copy point for output specifications:
      *
     O/COPY WNCPYSRC,ERRORCOPG
      *
      ********************************************************************
** CPY@ -- Copyright statement
(c) Misys International Banking Systems Ltd. 2001
