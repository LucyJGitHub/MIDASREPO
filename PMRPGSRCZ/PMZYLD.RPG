     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas PM Securities yield calculation')                *
      *****************************************************************
      *                                                               *
      *  Midas - Portfolio Management Module                          *
      *                                                               *
      *  PMZYLD - SECURITIES YIELD CALCULATION                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD050604           Date 21Feb20               *
      *                 MD046248           Date 27Oct17               *
      *                 226449             Date 24Jun15               *
      *                 MD034776           Date 08Jul15               *
      *                 AR1067108          Date 11Dec12               *
      *                 AR1001183A         Date 20Nov12               *
      *                 CRE073             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSE075             Date 17Nov05               *
      *                 CSE071             Date 19Jul05               *
      *                 CSE037             Date 29Apr02               *
      *                 194370             Date 12Jul01               *
      *                 CAS006             Date 21Jan03               *
      * Midas Release 4.01 -------------------------------------------*
      *                 CPK015             Date 17Jun01               *
      *                 CSE031             Date 06Dec01               *
      *                 180707             Date 25Jul00               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CSD006             Date 11Oct00               *
      * Midas DBA 3.04 -----------------------------------------------*
      *                 CSE023             Date 12Jul00               *
      * Midas DBA 3.03 -----------------------------------------------*
      *                 CAP137             Date 07Feb00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CPL002             Date 08Mar99               *
      *                 130862             Date 18Oct98               *
      *                 CSE005             Date 04Feb97               *
      *                 CPM005             Date 14Nov96               *
      *                 CPM004             DATE 07FEB96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *             (Recompile)                                       *
      *  MD050604 - Average price of position changed incorrectly.    *
      *           - Recompiled due to changes in copy source ZCNSDZ1. *
      *  MD046248 - Finastra Rebranding                               *
      *  226449 - Recompiled over changes made in /COPY ZDYYRZ3.      *
      *         - Applied for MD034387.                               *
      *  MD034776 - Reverse AR1001183A to allow negative interest     *
      *             rate. (Recompile)                                 *
      *  AR1067108 - PMC1140 Component failed during Clean COB        *
      *              Applied fix AR1001183A                           *
      *  AR1001183A - Coupon rate was still used in computation of    *
      *              interest instead of using the details in FRN tab *
      *              (Reopen) (Recompile)                             *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CSE075 - US Enhanced Treasury Upgrade to MidasPlus           *
      *           (Recompile)                                         *
      *  CSE071 - Multiple Holidays Re Securities (recompile)         *
      *  CSE037 - Bond Pricing - 3 Decimal Places in Trades Input     *
      *  194370 - Recompile after change on copy sources ZYLDZ2,      *
      *           For securities with divisor basis = '5' and first   *
      *           coupon date > valuation date (rundate + 1), set     *
      *           event control date to first coupon date before      *
      *           processing SR/ZDYYR. This is to ensure that the     *
      *           correct hypothetical last and next coupon dates are *
      *           used in deriving the price/yield rate.              *
      *  CAS006 - Hedge Accounting Phase B (Recompile)                *
      *  CPK015 - Conversion of ZYIELD from PLI to RPG.               *
      *           Recompile over changed ZYLDZ2.                      *
      *  CSE031 - Coupon Fixing for Floating Rate Notes.              *
      *           (recompilation due to change to ZNCD/ZLCD/ZACCR).   *
      *  180707 - No Position Settlements generated. Recompile over   *
      *           changes in /COPY ZNCDZ3.                            *
      *  CSD006 - Recompiled over changed SECTYD.                     *
      *  CSE023 - Treaty Withholding Tax (SECTYD)                     *
      *  CAP137 - Conversion of SE Security inputs into modular       *
      *           structure to use as APIs (SECTYD)                   *
      *  CPL002 - Midas-Plato Interface (Recompiled)                  *
      *  130862 - Recompiled over ZLCDZ1 & ZDAYSZ2 changes.           *
      *  CSE005 - Effect of holidays on FRN Coupon Dates              *
      *           (recompilation due to change to ZNCD/ZLCD/ZACCR).   *
      *  CPM005 - Private Banking Phase 2  - Recompile only           *
      *           Focus Group Changes upgraded to DBA                 *
      *  CPM004 - Bank Portfolios                                     *
      *                                                               *
      *****************************************************************
      *
     FSEDEV   IF  E           K        DISK
     FSECEO   IF  E           K        DISK
     F            SEDEVDF                           KRENAMESEDEVDO
     F            SNDEVDF                           KRENAMESNDEVDO
     E/COPY ZSRSRC,ZNCDZ1
     E/COPY ZSRSRC,ZDATE2Z1
     E/COPY ZSRSRC,ZPOWER8Z1
     E                    ZF29   14  32  5 0             29TH FEB ARRAY
     E**  Copyright array.
     E                    ##CPY   1   1 80
     ISEDEVDF
     I              NMCY                            NMCYSD
     ISEDEVDO
     I              NMCY                            NMCYSD
     I/COPY ZSRSRC,ZYLDZ1
     I##SECT    E DSSECTYD
     I                                      183 230 CDDS
     I                                      183 230 CD
     I                                      231 242 CRDS
     I                                      231 242 CR
     I##INVT    E DSINVTPD
     I              RECI                            IRECI
     I              LCD                             ILCD
     I              CHTP                            ICHTP
     I              TNLU                            ITNLU
     I              NDEC                            INDEC                                     CSE037
     I/COPY WNCPYSRC,PMZYLDI001
     C**********************************************************************
     C*
     C* COPYRIGHT
     C                     MOVEA##CPY     ##CPY2 80
      *
      * Parameters
      *
     C           *ENTRY    PLIST
     C                     PARM           ##YP    1
     C                     PARM           ##SECT
     C                     PARM           ##INVT
     C                     PARM           ##IDAT  50
     C                     PARM           ##NOML 150
     C                     PARM           ##ACIN  1
     C                     PARM           ##PDY  158
     C                     PARM           ##PRIC 158
     C                     PARM           ##CDP   10
     C                     PARM           ##RETC  6
     C/COPY WNCPYSRC,PMZYLDC001
      *
      **  Terminate program
      *
     C           ##RETC    IFEQ '*CLOSE'
     C                     SETON                     LR
     C                     RETRN
     C                     END
      *
      ** Initialize fields
      *
     C                     MOVE '0'       *IN98
     C                     Z-ADD##CDP     ZCDP
      *
      ** Do the Yield Calculation if the security is fixed income
      *
     C           PROT      IFNE '2'
     C           PROT      ANDNE'4'
     C           PROT      ANDNE'7'
     C           MATY      ANDNE0
     C           SPBS      ANDNE'U'
     C                     EXSR YLDCAL
     C                     ELSE
     C           ##YP      IFEQ 'Y'
     C                     Z-ADD*ZERO     ##PDY
     C                     ELSE
     C                     Z-ADD##PDY     ##PRIC
     C                     END
     C                     END
      *
      ** Return
      *
     C                     RETRN
      *****************************************************************
     C/SPACE 5
     C**********************************************************************
      ** Yield Calculation
     C**********************************************************************
     C           YLDCAL    BEGSR
      *
      * Set Nominal
      *
     C                     Z-ADD##NOML    ZNOML
      *
      * Default day and divisor basis if necessary
      *
     C           DYBS      IFEQ '4'
     C                     MOVE '3'       DYBS
     C                     MOVE '1'       DVBS
     C                     END
      *
      * Set Parameters for Australian Yield Method
      *
     C           SYBS      IFEQ 'AU'
     C                     EXSR AUSYLD
     C                     END
      *
      * Date of valuation
      *
     C                     Z-ADD##IDAT    ECD     50
     C                     Z-ADD##IDAT    ZFDATE
      *
      * Convert % price to yield
      *
     C           ##YP      IFEQ 'Y'
     C                     Z-ADD##PRIC    ZPRICE 158
     C           ZPRICE    IFGT *ZERO
     C                     EXSR ZPRCO
     C                     Z-ADDZPRCOT    ##PDY
     C                     ELSE
     C                     Z-ADD*ZERO     ##PDY
     C                     END
     C                     END
      *
      * Convert yield to % price
      *
     C           ##YP      IFEQ 'P'
     C                     Z-ADD##PDY     ZPRCIN 158
     C           ZPRCIN    IFGT *ZERO
     C                     EXSR ZPRCI
     C                     Z-ADDZPRCOT    ##PRIC
     C                     ELSE
     C                     Z-ADD*ZERO     ##PRIC
     C                     END
     C                     END
      *
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
     C**********************************************************************
      * Set Parameters for Australian Yield Method
     C**********************************************************************
     C           AUSYLD    BEGSR
      *
      * Set Cum/Ex-coupon indicator
      *
     C                     MOVEL##ACIN    SCUMEX
      *
      * Determine interest on nominal
      *
     C                     CALL 'PMZINN'
     C                     PARM           ##SECT
     C                     PARM           ##INVT
     C                     PARM           ##IDAT  50
     C                     PARM           ##NOML 150
     C                     PARM           ##ACIN  1
     C                     PARM *ZERO     ##INT  150
     C                     PARM *ZERO     ##DAYS  50
     C                     PARM *ZERO     ##LCD   50
     C                     PARM *ZERO     ##NCD   50
     C                     PARM           ##CDP   10
     C                     PARM 'N'       ##FULL  1
     C                     PARM *BLANK    ##RETC  6
      *
      * Make interest figure a percentage
      *
     C           ##NOML    IFNE 0
     C           ##INT     DIV  ##NOML    WRKPI
     C                     ELSE
     C                     Z-ADD0         WRKPI
     C                     END
      *
      * If ex-coupon reverse the sign
      *
     C           ##ACIN    IFEQ 'X'
     C                     Z-SUBWRKPI     WRKPI
     C                     END
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
      *****************************************************************
     C           *PSSR     BEGSR
     C                     SETON                     LR
     C                     DUMP
     C                     MOVEL'*ERROR'  ##RETC
     C                     RETRN
     C                     ENDSR
      *****************************************************************
     C/SPACE 5
     C/COPY ZSRSRC,ZCNSDZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZACCZZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZACCRZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZDAYSZ2
     C/SPACE 5
     C/COPY ZSRSRC,ZNCDZ3
     C/SPACE 5
     C/COPY ZSRSRC,ZDYYRZ3
     C/SPACE 5
     C/COPY ZSRSRC,ZDATE1Z2
     C/SPACE 5
     C/COPY ZSRSRC,ZDATE2Z2
     C/SPACE 5
     C/COPY ZSRSRC,ZLCDZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZRATEZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZPRCOZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZPRCIZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZYLDOZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZYLDIZ1
     C/SPACE 5
     C/COPY ZSRSRC,ZYLDZ2
     C/SPACE 5
     C/COPY ZSRSRC,ZCALCD
     C/SPACE 5
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**  POWER8 - ARRAY OF POWERS FOR CURRENCY CONVERSION
00000001
00000010
00000100
00001000
00010000
00100000
01000000
10000000
**   ZF29 DAY NOS OF FEB 29 TO 2096 (INCLUDING 2000)
0006001521029820444305904073650882610287117481320914670161311759219053
2051421975234362489726358278192928030741322023366335124365853804639507
40968424294389045351
** ##CPY  **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
