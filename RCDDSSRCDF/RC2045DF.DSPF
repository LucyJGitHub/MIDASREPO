      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RC Ledger Detail review 2')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Autorecs II Module
     A*                                                               *
     A*   DSPF/RC2045DF - Ledger Details Review                       *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Prev Amend No. 112563             Date 07Feb97               *
     A*                 S01449             Date 25Oct93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  S01449 - Autorecs II module                                  *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CA12
      *
     A          R RC2045F1                  OVERLAY
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            @USER         10A  O  1  2
     A                                  1 13'RC2045'
     A                                  1 28'LEDGER DETAILS REVIEW'
     A                                      DSPATR(UL HI)
     A            RUNA           7A  O  1 72
     A            @JOB          10   O  2  2
     A                                  2 71TIME
     A                                  4  2'Account        :'
     A            @HCUST         6   O  4 20
     A                                      DSPATR(HI)
     A            @HCCY          3   O  4 27
     A                                      DSPATR(HI)
      **Start of change CGL029
     A**********  @HACOD         4   O  4 31                                                  CGL029
     A**********                            DSPATR(HI)                                        CGL029
     A**********  @HACSQ         2   O  4 36                                                  CGL029
     A**********                            DSPATR(HI)                                        CGL029
     A**50******                        4 40'Branch :'                                        CGL029
     A**50******  @HBRCA         3   O  4 50                                                  CGL029
     A**********                            DSPATR(HI)                                        CGL029
     A            @HACOD        10   O  4 31DSPATR(HI)
     A            @HACSQ         2   O  4 42DSPATR(HI)
     A  50                              4 46'Branch :'
     A  50        @HBRCA         3   O  4 56DSPATR(HI)
      **End of change CGL029
      *
     A                                  5  2'Date Created   :'
     A            @HDATC         7   O  5 20
     A                                      DSPATR(HI)
      **Start of change CGL029
     A**********                        5 40'Status :'                                        CGL029
     A**********  @HSTAT        20   O  5 50                                                  CGL029
     A**********                            DSPATR(HI)                                        CGL029
     A                                  5 46'Status :'
     A            @HSTAT        20   O  5 56DSPATR(HI)
      **End of change CGL029
      *
     A  71                              6  2'Last download  - Date :'
     A  71        @HDATD         7   O  6 27
     A  71                                  DSPATR(HI)
     A  71                              6 40'Time :'
     A  71        @HTIMD         6  0O  6 48EDTWRD('  :  :  ')
     A  71                                  DSPATR(HI)
     A  71                              6 60'Sequence :'
     A  71        @H1SEQ         3   O  6 72
     A  71                                  DSPATR(HI)
      *
     A                                  8  2'Tag'
     A                                  8  8'Data'
      *
     A          R RC2045F2                  SFL
     A            @DTAG          5A  O  9  2
     A                                      DSPATR(HI)
     A            @DDAT         70A  O  9  8
      *
     A          R RC2045F3                  SFLCTL(RC2045F2)
     A  20                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N20N21                               SFLCLR
     A                                      SFLSIZ(0500)
     A                                      SFLPAG(0013)
     A  22                                  SFLEND
     A                                      VLDCMDKEY(31)
     A                                      OVERLAY
     A  23                                  CA08
     A  24                                  CA09
     A  70                                  CA24
      *
      *
      *
     A**                                             Begin /112563/
     A***********                      23  2'F12-Previous'
     A* 23 ******                      23 16'F8-Next Msg'
     A* 24 ******                      23 29'F9-Previous Msg'
     A* 70 ******                      23 69'F24-Re-send'
     A                                 23  2'F12=Previous'
     A  23                             23 16'F8=Next Msg'
     A  24                             23 29'F9=Previous Msg'
     A  70                             23 69'F24=Re-send'
     A**                                             End   /112563/
      *
      **  Message subfile Record format
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
      *
      **  Message Subfile Control Record format
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
