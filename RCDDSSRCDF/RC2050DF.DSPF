      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RC Statements review')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Autorecs II Module
     A*                                                               *
     A*   DSPF/RC2050DF - Statements review                           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 112563             Date 07Feb97               *
      *                 S01449             Date 08Oct93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  S01449 - Autorecs II module                                  *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      CA03
      *
     A          R RC2050F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            @USER         10A  O  1  2
     A                                  1 13'RC2050'
     A                                  1 32'STATEMENTS REVIEW'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O  1 72
     A            @JOB          10A  O  2  2
     A                                  2 71TIME
      *
     A          R RC2050F2
     A                                      OVERLAY
      *
      *
     A                                  5  2'Start Date :'
     A            SDAT           6A  B  5 16
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
      *
     A  10                              5 24'Branch :'
     A  10        SBCH           3A  B  5 34
     A  10N32                               DSPATR(HI)
     A  10 32                               DSPATR(RI)
     A  10 32                               DSPATR(PC)
     A                                  7  2'Status (Enter X) :  '
     A            STS1           1A  B  7 24
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                  7 27'Arrived'
     A            STS2           1A  B  8 24
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                  8 27'Flagged for re-send'
     A            STS3           1A  B  9 24
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                  9 27'Downloaded to PC component'
      *
     A**                                             Begin /112563/
     A***********                      23  2'F3-Exit'
     A                                 23  2'F3=Exit'
     A**                                             End   /112563/
      *
     A          R RC2050F3
     A                                      OVERLAY
     A                                  3  2'Start Date :'
     A            SDAT           6A  O  3 16
     A                                      DSPATR(HI)
     A  10                              3 25'Branch :'
     A  10        SBCH           3A  O  3 35
     A  10                                  DSPATR(HI)
     A                                  4  2'Status     :'
     A            STS4          60A  O  4 16
     A                                      DSPATR(HI)
      *
     A                                  6  2'?'
     A                                  6  8'Branch'
     A                                  6 16'Date'
     A                                  6 25'Sender'
     A                                  6 40'T.R.N.'
     A                                  6 58'Status'
      *
     A          R RC2050F4
     A**                                             Begin /112563/
     A***********                      23  2'F3-Exit'
     A***********                      23 12'F12-Previous'
     A                                 23  2'F3=Exit'
     A                                 23 12'F12=Previous'
     A**                                             End   /112563/
     A                                 23 27'Rollup/Rolldown'
     A                                 23 47'Possible Action Code(s)'
      *
     A          R RC2050F5
     A                                  5  2'Account        :'
     A                                  5 54'Branch   :'
     A                                  6  2'Date Created   :'
     A                                  7  2'Last Download  -'
     A                                  7 54'Sequence :'
     A
      *
     A          R RC2050F6                  SFL
     A  24                                  SFLNXTCHG
     A            SELECT         1A  B  8  2
     A  38                                  DSPATR(RI)
     A            SFBRCA         3A  O  8  8
     A            SFCDAT         7A  O  8 16
     A            SFSTID        12A  O  8 25
     A            SFTRNO        16A  O  8 40
     A            SFSTAT        20A  O  8 58
     A            SFMOR         28A  H
      *
      *
     A          R RC2050F7                  SFLCTL(RC2050F6)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CA05
     A                                      CA12
     A  20                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N20N21                               SFLCLR
     A  22                                  SFLEND
     A N22                                  ROLLUP(23)
     A                                      OVERLAY
      *
      *
     A            DSPREC         4S 0H      SFLRCDNBR
     A**                                             Begin /112563/
     A***********                      23  2'F3-Exit'
     A***********                      23 11'F12-Previous'
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Previous'
     A                                 23 25'F5=Refresh'
     A**                                             End   /112563/
     A                                 23 37'Rollup/Rolldown'
     A                                 23 54'Valid action codes'
     A            SATN           2A  O 23 73
     A**                                             Begin /112563/
     A***********                      23 25'F5-Refresh'
     A**                                             End   /112563/
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     *
     **  Message Subfile Control Record format
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
      *
