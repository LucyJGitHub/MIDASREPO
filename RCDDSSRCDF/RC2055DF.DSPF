      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RC Statements review 2')
     A*%%TS  SD  19931027  155136  FBANK       REL-V2R2M0  5738-PW1
     A*****************************************************************
     A*                                                               *
     A*  Midas - Autorecs II Module
     A*                                                               *
     A*   DSPF/RC2055DF - Statements Review                           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 112563             Date 07Feb97               *
      *                 S01449             Date 25Oct93               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  S01449 - Autorecs II module                                  *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA12
     A*
     A          R RC2055F1
     A*%%TS  SD  19931027  155136  FBANK       REL-V2R2M0  5738-PW1
     A                                      OVERLAY
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            @USER         10A  O  1  2
     A                                  1 13'RC2055'
     A                                  1 32'STATEMENTS REVIEW'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            RUNA           7A  O  1 72
     A                                  2 71TIME
     A            @JOB          10A  O  2  2
     A                                  4  2'Message type   :'
     A            @HMTPY         3A  O  4 20DSPATR(HI)
     A                                  4 25'Status :'
     A            @HSTAT        20A  O  4 35DSPATR(HI)
     A  50                              4 60'Branch :'
     A  50        @HBRCA         3A  O  4 70
     A  50                                  DSPATR(HI)
     A                                  5  2'Date Received  :'
     A            @HADAT         7A  O  5 20DSPATR(HI)
     A                                  5 35'MOR :'
     A            @HMOR         28A  O  5 42DSPATR(HI)
     A                                  6  2'Sender         :'
     A            @HSTID        12A  O  6 20DSPATR(HI)
     A                                  6 35'TRN :'
     A            @HTRNO        16A  O  6 42DSPATR(HI)
     A  71                              7  2'Last download  - Date :'
     A  71        @HDATD         7A  O  7 27
     A  71                                  DSPATR(HI)
     A  71                              7 39'Time :'
     A  71        @HTIMD         6  0O  7 47EDTWRD('  :  :  ')
     A  71                                  DSPATR(HI)
     A  71                              7 60'Sequence :'
     A  71        @H1SEQ         3A  O  7 72
     A  71                                  DSPATR(HI)
     A                                  9  2'Tag'
     A                                  9  8'Data'
      *
     A          R RC2055F2                  SFL
     A            @DTAG          5A  O 10  2
     A                                      DSPATR(HI)
     A            @DDAT         70A  O 10  8
      *
     A          R RC2055F3                  SFLCTL(RC2055F2)
     A  20                                  SFLDSP
     A  21                                  SFLDSPCTL
     A N20N21                               SFLCLR
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0012)
     A  22                                  SFLEND
     A                                      VLDCMDKEY(31)
     A                                      OVERLAY
     A  23                                  CA08
     A  24                                  CA09
     A  70                                  CA24
      *
      *
      *
     A**                                             Begin /112563/
     A***********                      23  2'F12-Previous'
     A* 23 ******                      23 16'F8-Next Msg'
     A* 24 ******                      23 29'F9-Previous Msg'
     A* 70 ******                      23 69'F24-Re-send'
     A                                 23  2'F12=Previous'
     A  23                             23 16'F8=Next Msg'
     A  24                             23 29'F9=Previous Msg'
     A  70                             23 69'F24=Re-send'
     A**                                             End   /112563/
      *
      **  Message subfile Record format
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
      *
      **  Message Subfile Control Record format
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
