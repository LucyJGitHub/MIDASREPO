      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RC Reconciliation ac main')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Autorecs II Module
     A*                                                               *
     A*   DSPF/RC2100DF - Reconciliation accounts maintenance         *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01449             Date DDMmmYY               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  S01449 - Autorecs II module                                  *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*         /////////////////////////////////////////             *
     A*        // WARNING: When creating this display //              *
     A*       //   file, take options RSTDSP(*YES)   //               *
     A*      //                       SHARE (*YES)  //                *
     A*     /////////////////////////////////////////                 *
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      CA03
      *
      *
     A          R RC2100F1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A            @USER         10A  O  1  2
     A                                  1 13'RC2100'
     A                                  1 22'RECONCILIATION ACCOUNTS MAINTENANC-
     A                                      E'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT         7A  O  1 72
     A            @JOB          10A  O  2  2
     A                                  2 71TIME
      *
     A          R RC2100F2
     A                                      OVERLAY
      *
     A                                  5 15'Action Code :'
     A            SACT           1A  B  5 30
     A N31                                  DSPATR(HI)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A  10                              7  2'Branch :'
     A  10        SBCH           3A  B  7 12
     A  10N32                               DSPATR(HI)
     A  10 32                               DSPATR(RI)
     A  10 32                               DSPATR(PC)
     A                                  7 19'Account :'
     A            SCUS           6A  B  7 30
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A            SCCY           3A  B  7 37
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
      **Start of change CGL029
     A**********  SCOD           4A  B  7 41                                                  CGL029
     A            SCOD          10A  B  7 41
      **End of change CGL029
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
      **Start of change CGL029
     A**********  SSEQ           2A  B  7 46                                                  CGL029
     A            SSEQ           2A  B  7 52
      **End of change CGL029
     A N33                                  DSPATR(HI)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A N40                             10 18'Customer :'
     A N40        BBCNA1        35A    10 30
     A N40                             11 18'A/c name :'
     A N40        ANAM          20A    11 30
     A N40                             13 18'Defined as RCII account'
     A                                 23  2'F3-Exit'
     A          R SFLMSGR                   SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMQ                      SFLPGMQ
     *
     **  Message Subfile Control Record format
     A          R SFLMSGC                   SFLCTL(SFLMSGR)
     A                                      SFLINZ
     A                                      SFLPAG(1)
     A                                      SFLSIZ(5)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A N95                                  SFLEND
     A                                      OVERLAY
     A            PGMQ                      SFLPGMQ
