      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RC II - Ledger Detail Download')
      *****************************************************************
      *                                                               *
      *  Midas - Automatic Reconciliations II                         *
      *                                                               *
      *   RC2020P1 - Ledger Details Download                          *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 X00000             Date 00Xxx00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R RC2020F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                      SPACEA(2)
     A                                     2'Reference RC2020P1'
     A            BJURPT        53        35
     A                                    93'DATE'
     A            BJMRDT         7        98
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
     A  30                                 2'Branch'
     A  30        A8BRCD         3         9
      *
     A                                    43'LEDGER DETAILS DOWNLOAD'
     A                                      SPACEA(1)
     A                                    43'~~~~~~~~~~~~~~~~~~~~~~~'
     A                                      SPACEA(1)
     A          R RC2020F2                  SPACEA(20)
     A                                    30'DOWNLOAD FOR RCII PC COMPONENT  :'
     A            @PCID         10        66
     A                                      SPACEA(2)
     A                                    30'DATE & TIME OF DOWNLOAD         :'
     A            BJMRDT         7        66
     A            @TIMEN         6  0     80EDTWRD('  :  :  ')
     A                                      SPACEA(2)
     A                                    30'JOB, USER, NUMBER               :'
     A            @JOB          10        66
     A            @USER         10        80
     A            @NUM           6        94
      *
     A          R RC2020F3                  SPACEA(10)
     A                                    50'*** PC ID NOT RECOGNISED ***'
      *
     A          R RC2020F4                  SPACEA(10)
     A                                    50'*** NO DETAILS FOR BRANCH ***'
      *
     A          R RC2020F5                  SPACEA(1)
     A                                    30'ACCOUNT       :'
     A            @HCUST         6        47
     A            @HCCY          3        54
      **Start of change CGL029
     A**********  @HACOD         4        58                                                  CGL029
     A**********  @HACSQ         2        63                                                  CGL029
     A            @HACOD        10        58
     A            @HACSQ         2        69
      **End of change CGL029
     A                                      SPACEA(1)
     A                                    30'DATE CREATED  :'
     A            @HDATC         7        47
     A                                      SPACEA(1)
     A                                    30'DOWNLOAD SEQ. :'
     A            @DSEQ          3  0     47
     A                                      SPACEA(2)
     A                                    30'TAG'
     A                                    47'DATA'
     A                                      SPACEA(1)
     A                                    30'~~~'
     A                                    47'~~~~'
     A                                      SPACEA(1)
      *
     A          R RC2020F6                  SPACEA(1)
     A            @DTAG          5        30
     A            @DDAT         70        47
      *
     A          R RC2020F7                  SPACEB(3)
     A                                    50'*** END OF REPORT FOR BRANCH'
     A            A8BRCD         3        79
     A                                    83'***'
     A  98                                  SPACEA(3)
     A  98                                56'*** END OF REPORT ***'
      *
     A          R RC2020F8                  SPACEB(3)
     A                                    50'*** END OF REPORT ***'
      *
     A          R RC2020F9
     A                                    28'LEDGER DOWNLOAD DATABASE ERROR +
     A                                       REPORT.'
     A                                      SPACEA(12)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
