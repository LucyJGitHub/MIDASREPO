/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RC II - Ledger Details Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Automatic Reconciliations II                         *
      *                                                               *
      *   RC2065AU - Ledger Details Audit Report                      *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 091250             Date 27Feb95               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  091250 - AU reports should go to MPRINTAU. Change to         *
      *           standard creation parameters.                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R RC2065A1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                      SPACEA(2)
     A                                     2'Reference RC2065AU'
     A            BJURPT        53        35
     A                                    93'DATE'
     A            BJMRDT         7        98
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    40'LEDGER DETAILS REPORT'
     A                                      SPACEA(1)
     A                                    40'~~~~~~~~~~~~~~~~~~~~~'
     A                                      SPACEA(1)
      *
     A          R RC2065A2                  SPACEB(9)
     A                                    20'Options taken -'
     A                                      SPACEA(3)
     A  10N12                             83'Branch'
     A  10N12     @@BCH          3        94
     A  10                                30'Level'
     A  10        @@LVL          1        44SPACEA(3)
     A                                    30'Start Date - '
     A            @SDATE         7        44
     A                                    83'End Date - '
     A            @EDATE         7        94
     A                                      SPACEA(3)
     A                                    30'Status'
     A            @STS1          1        44
     A                                    46'Created'
     A                                      SPACEA(1)
     A            @STS2          1        44
     A                                    46'Flagged for Re-send'
     A                                      SPACEA(1)
     A            @STS3          1        44
     A                                    46'Downloaded to PC'
     A                                      SPACEA(1)
      *
     A          R RC2065A3                  SPACEB(20)
     A
     A                                    50'*** NO MESSAGES TO REPORT ***'
     A                                      SPACEA(1)
      *
     A          R RC2065A4
     A                                    28'LEDGER DETAILS DATABASE ERROR +
     A                                       REPORT.'
     A                                      SPACEA(12)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
