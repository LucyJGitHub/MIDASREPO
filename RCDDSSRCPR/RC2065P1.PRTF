      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RC II - Ledger Details Report')
      *****************************************************************
      *                                                               *
      *  Midas - Automatic Reconciliations II                         *
      *                                                               *
      *   RC2065P1 - Ledger Details Audit Report                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. X00000             Date 00XXX00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R RC2065F1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                      SPACEA(2)
     A                                     2'Reference RC2065P1'
     A            BJURPT        53        35
     A                                    93'DATE'
     A            BJMRDT         7        98
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
     A  10 13                              2'Branch'
     A  10 13     SAVBRC         3         9
      *
     A                                    43'LEDGER DETAILS REPORT'
     A                                      SPACEA(1)
     A                                    43'~~~~~~~~~~~~~~~~~~~~~'
     A                                      SPACEA(1)
      *
     A          R RC2065F2                  SPACEB(9)
     A                                    20'Options taken -'
     A                                      SPACEA(3)
     A  10N12                             83'Branch'
     A  10N12     @@BCH          3        94
     A  10                                30'Level'
     A  10        @@LVL          1        44SPACEA(3)
     A                                    30'Start Date - '
     A            @SDATE         7        44
     A                                    83'End Date - '
     A            @EDATE         7        94
     A                                      SPACEA(3)
     A                                    30'Status'
     A            @STS1          1        44
     A                                    46'Created'
     A                                      SPACEA(1)
     A            @STS2          1        44
     A                                    46'Flagged for Re-send'
     A                                      SPACEA(1)
     A            @STS3          1        44
     A                                    46'Downloaded to PC'
     A                                      SPACEA(1)
      *
     A          R RC2065F3                  SPACEB(3)
     A                                    50'** END OF REPORT FOR BRANCH'
     A            SAVBRC         3        78
     A                                    82'**'
     A  49                                  SPACEA(3)
     A  49                                56'*** END OF REPORT ***'
      *
     A          R RC2065F4                  SPACEB(3)
     A                                    50'*** END OF REPORT ***'
      *
     A          R RC2065F5
      *
     A  61                                18'CONTINUED...'
     A  61                                  SPACEA(2)
      *
     A                                    30'ACCOUNT            :'
     A            @HCUST         6        51
     A            @HCCY          3        58
      **Start of change CGL029
     A**********  @HACOD         4        62                                                  CGL029
     A**********  @HACSQ         2        67                                                  CGL029
     A            @HACOD        10        62
     A            @HACSQ         2        73
      **End of change CGL029
      *
      **Start of change CGL029
     A**********                          75'DATE CREATED :'                                  CGL029
     A**********  @HDATC         7        90                                                  CGL029
     A                                    79'DATE CREATED :'
     A            @HDATC         7        94
      **End of change CGL029
     A                                      SPACEA(1)
     A                                    30'STATUS             :'
     A            @HSTAT        20        51
      *
     A  71                                  SPACEA(1)
     A  71                                30'LAST DOWNLOAD DATE :'
     A  71        @HDATD         7A       51
      **Start of change CGL029
     A**71******                          75'TIME         :'                                  CGL029
     A**71******  @HTIMD         6  0     90EDTWRD('  :  :  ')                                CGL029
     A  71                                79'TIME         :'
     A  71        @HTIMD         6  0     94EDTWRD('  :  :  ')
      **End of change CGL029
     A  71                                  SPACEA(1)
     A  71                                30'DOWNLOAD SEQUENCE  :'
     A  71        @H1SEQ         3A       51
      *
     A                                    30'TAG'
     A                                      SPACEB(3)
     A                                    49'DATA'
     A                                      SPACEA(1)
     A                                    30'~~~'
     A                                    49'~~~~'
     A                                      SPACEA(1)
      *
     A          R RC2065F6                  SPACEA(1)
     A            @DTAG          5        30
     A            @DDAT         70        49
