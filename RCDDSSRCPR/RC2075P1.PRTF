      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RC II - Statements Report')
      *****************************************************************
      *                                                               *
      *  Midas - Automatic Reconciliations II                         *
      *                                                               *
      *   RC2075P1 - Statements Report                                *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *   PREV AMEND NO. X00000            DATE 00XXX00               *
      *                  X00000            DATE 00XXX00               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
     A*********************************************************************
     A                                      REF(MFRFL)
     A          R RC2075F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                      SPACEA(2)
     A                                     2'Reference RC2075P1'
     A            BJURPT        53        35
     A                                    93'DATE'
     A            BJMRDT         7        98
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
     A  10 13                              2'Branch'
     A  10 13     SAVBRC         3         9
      *
     A                                    43'STATEMENTS REPORT'
     A                                      SPACEA(1)
     A                                    43'~~~~~~~~~~~~~~~~~'
     A                                      SPACEA(1)
      *
     A          R RC2075F2                  SPACEB(9)
     A                                    20'Options taken -'
     A                                      SPACEA(3)
     A  10N12                             83'Branch'
     A  10N12     @@BCH          3        94
     A  10                                30'Level'
     A  10        @@LVL          1        44SPACEA(3)
     A                                    30'Start Date - '
     A            @SDATE         7        44
     A                                    83'End Date - '
     A            @EDATE         7        94
     A                                      SPACEA(3)
     A                                    30'Status'
     A            @STS1          1        44
     A                                    46'Arrived'
     A                                      SPACEA(1)
     A            @STS2          1        44
     A                                    46'Flagged for Re-send'
     A                                      SPACEA(1)
     A            @STS3          1        44
     A                                    46'Downloaded to PC'
     A                                      SPACEA(1)
      *
     A          R RC2075F3                  SPACEB(3)
     A                                    50'** END OF REPORT FOR BRANCH'
     A            SAVBRC         3        78
     A                                    82'**'
     A  49                                  SPACEA(3)
     A  49                                56'*** END OF REPORT ***'
      *
     A          R RC2075F4                  SPACEB(3)
     A                                    50'*** END OF REPORT ***'
      *
     A          R RC2075F5
      *
     A  61                                18'CONTINUED...'
     A  61                                  SPACEA(2)
      *
     A                                    30'MESSAGE TYPE       :'
     A            @HMTPY         3A       51
     A                                    65'STATUS :'
     A            @HSTAT        20A       74
     A                                      SPACEA(1)
     A                                    30'DATE RECEIVED      :'
     A            @HADAT         7A       51
     A                                    65'MOR    :'
     A            @HMOR         28A       74
     A                                      SPACEA(1)
     A                                    30'SENDER             :'
     A            @HSTID        12A       51
     A                                    65'TRN    :'
     A            @HTRNO        16A       74
     A  71                                  SPACEA(1)
     A  71                                30'LAST DOWNLOAD DATE :'
     A  71        @HDATD         7A       51
     A  71                                65'TIME   :'
     A  71        @HTIMD         6  0     74EDTWRD('  :  :  ')
     A  71                                  SPACEA(1)
     A  71                                30'DOWNLOAD SEQUENCE  :'
     A  71        @H1SEQ         3A       51
      *
     A                                    30'TAG'
     A                                      SPACEB(3)
     A                                    51'DATA'
     A                                      SPACEA(1)
     A                                    30'~~~'
     A                                    51'~~~~'
     A                                      SPACEA(1)
      *
     A          R RC2075F6                  SPACEA(1)
     A            @DTAG          5        30
     A            @DDAT         70        51
