/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RC II - Drop Ledger Details')
      *****************************************************************
      *                                                               *
      *  Midas - Automatic Reconciliations II                         *
      *                                                               *
      *   RC2080P1 - Drop Ledger Details                              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 091250             Date 27Feb95               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  091250 - AU reports should go to MPRINTAU. Change to         *
      *           standard creation parameters.                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A          R RC2080F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                      SPACEA(2)
     A                                     2'Reference RC2080P1'
     A            BJURPT        53        35
     A                                    93'DATE'
     A            BJMRDT         7        98
     A                                   119'PAGE'
     A                                   124PAGNBR EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    43'DROP LEDGER DETAILS'
     A                                      SPACEA(4)
     A                                     2'THE FOLLOWING DETAILS HAVE BEEN +
     A                                       DROPPED:'
     A                                      SPACEA(4)
      *
     A          R RC2080F2                  SPACEA(1)
     A                                     2'ACCOUNT   :'
     A            @HCUST         6        15
     A            @HCCY          3        21
      **Start of change CGL029
     A**********  @HACOD         4        25                                                  CGL029
     A**********  @HACSQ         2        30                                                  CGL029
     A**********  @HBRCA         3        33                                                  CGL029
     A            @HACOD        10        25
     A            @HACSQ         2        36
     A            @HBRCA         3        39
      **End of change CGL029
     A                                    50'DATE CREATED  :'
     A            @HDATC         7        67
     A                                    80'DOWNLOAD SEQ. :'
     A            R1DSEQ         3  0     97
      *
     A          R RC2080F3                  SPACEB(3)
     A                                    50'*** END OF REPORT ***'
      *
     A          R RC2080F4
     A                                      SKIPB(10)
     A                                    28'DROP LEDGER DETAILS DATABASE +
     A                                       ERROR REPORT.'
     A                                      SPACEA(12)
     A                                    34'**** DATABASE ERROR ****'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
     A                                      SPACEA(1)
     A                                    20'*    FILE NAME'
     A            DBFILE         8        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    KEY'
     A            DBKEY         29        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    PROGRAM NAME'
     A            DBPGM         10        39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'*    AT'
     A            DBASE          3  0     39
     A                                    73'*'
     A                                      SPACEA(1)
     A                                    20'******************************'
     A                                    50'************************'
