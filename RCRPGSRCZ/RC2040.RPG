     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RC II - Ledger Details Review')
      *****************************************************************
      *                                                               *
      *  Midas - Autorecs II Module                                   *
      *                                                               *
      *  RC2040 - Ledger Details Review                               *
      *                                                               *
      *  Function:  This program allows users of Automatic            *
      *             Reconciliations II to review General Ledger       *
      *             details, and if appropriate flag them for         *
      *             re-send to the PC component.                      *
      *  I/C                                                          *
      *                                                               *
      *  Called By: RCC2040 - Ledger Details Review                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01449             Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  S01449 - Autorecs II module.                                 *
      *                                                               *
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *                                                               *
      *****************************************************************
     FRC2040DFCF  E                    WORKSTN
     F                                        RRN   KSFILE RC2040F6
     F                                              KINFDS INFDS
     FRCLDGRL1IF  E           K        DISK
     F            RCLDGRD0                          KRENAMERCLDGRD1
     FRCLDGRL2IF  E           K        DISK
     F            RCLDGRD0                          KRENAMERCLDGRD2
      *
      *****************************************************************
      *                                                               *
      *   Indicator Usage                                             *
      *   ---------------                                             *
      *                                                               *
      *                                                               *
      *   10        Multibranching Indicator                          *
      *   11        EOF changed records on subfile                    *
      *                                                               *
      *   21        SFLDSPCTL                                         *
      *   22        SFLEND                                            *
      *   23        Rollup enabled on Subfile                         *
      *   24        SFLNXTCHG                                         *
      *                                                               *
      *   30        General Screen Error                              *
      *   31        Screen Date in Error                              *
      *   32        Screen Branch in Error                            *
      *   33        Screen 'X' Select in Error                        *
      *                                                               *
      *   40        Branch Conditioning                               *
      *   49        EOF RCLDGRx                                       *
      *   50        Matching Record Found                             *
      *                                                               *
      *   KC        F3 Enabled                                        *
      *   KE        F5 Enabled                                        *
      *                                                               *
      *   U7 & U8   Database error                                    *
      *                                                               *
      *****************************************************************
      /EJECT
     E                    TXT     1   3 20
      ** Screen text: compile time
      *
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
     E                    STA        60  1
      ** Screen text
      *
     E                    TRN       200 16
      ** Transaction Reference Numbers.
      *
     E                    ATN       200  1
      ** Action Codes
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
     IRUNDAT    E DSRUNDAT
     I                                       12  12 DATF
     I                                       13  13 MBIN
      ** Data Area giving Installation Control Details
      *
     IZMUSER    E DSZMUSER
      **  User profile details
      *
     IINFDS       DS
     I                                    B 378 3790LSTRRN
      **  Last subfile start RRN
      *
     ISTAT        DS                              3
     I                                        1   1 STS1
     I                                        2   2 STS2
     I                                        3   3 STS3
      ** Status Selection from Display File
      *
     ISATN        DS                              2
     I                                        1   1 @A1
     I                                        2   2 @A2
      ** Available Action Codes For User
      *
     I            DS
     I                                        1  60 STS4
     I                                        1  60 STA
      ** Status for headings
      *
     I            DS
     I                                        1   60SDATN
     I                                        1   6 SDAT
      ** Dates
      *
     ISDBANK    E DSSDBANKPD
      **  External Data structure for bank details.
      *
     ISDBRCH    E DSSDBRCHPD
      **  External Data structure for branch details.
      *
     IDSFDY     E DSDSFDY
      **  Short data structure for access programs
      *
     IDSSDY     E DSDSSDY
      **  Long data structure for access programs
      *
     IPSDS       SDS
     I                                      244 253 @JOB
     I                                      254 263 @USER
     I                                      264 269 @NUM
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *   Index to subroutines                                        *
      *   --------------------                                        *
      *                                                               *
      *   DETL      Detail process                                    *
      *   SUMM      Process message summary                           *
      *   GETR      Get next valid record                             *
      *   LINK      Access single message enquiry function            *
      *   VSEL      Validate selection                                *
      *   INIT      Initial process                                   *
      *   *PSSR     Program Exception Error Routine                   *
      *                                                               *
      *****************************************************************
      /EJECT
      *
      **  ZM1001 ERROR
     C           PLIST3    PLIST
     C                     PARM           @MSGID 10
     C                     PARM           @MSDTA 78
      *
      ** Initial process
     C                     EXSR INIT
      *
      ** Detail process
     C                     EXSR DETL
      *
      ** Terminate program
     C                     SETON                     LR
      /EJECT
      *****************************************************************
      * Subroutine  :  DETL                                           *
      * Function    :  Detail processing                              *
      *                                                               *
      * Called by   :  Main process                                   *
      * Calls       :  VSEL Validate selection                        *
      *                SUMM Process message summary                   *
      *****************************************************************
      *
     C           DETL      BEGSR
      *
      **  Display selection screen until F3 pressed
     C           *INKC     DOWEQ'0'
      *
      **  If no errors clear input fields
     C           *IN30     IFEQ '0'
     C                     MOVE *BLANKS   SDAT
     C                     MOVE *BLANKS   SBCH
      *
     C                     MOVE *BLANKS   STS1
     C                     MOVE *BLANKS   STS2
     C                     MOVE *BLANKS   STS3
     C                     END
      *
     C                     WRITERC2040F1
     C                     WRITESFLMSGC
     C                     EXFMTRC2040F2
     C                     CALL 'ZM1002'
      *
      **  Save Input Fields
     C                     MOVE SDAT      @SDAT   7
     C                     MOVE SBCH      @SBCH   3
     C                     MOVE STS1      @STS1   1
     C                     MOVE STS2      @STS2   1
     C                     MOVE STS3      @STS3   1
      *
      * If F5 pressed then restore input fields
     C                     SETOF                     3031
     C                     SETOF                     3233
      *
      **  If F3 not pressed validate selection
     C           *INKC     IFEQ '0'
     C                     EXSR VSEL
      *
      **  If selection valid
     C           *IN30     IFEQ '0'
     C                     MOVE *BLANKS   SELECT
      *
      **  Process message summary
     C                     EXSR SUMM
     C                     END
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * Subroutine  :  SUMM                                           *
      * Function    :  Process message summary                        *
      *                                                               *
      * Called by   :  DETL Detail process                            *
      * Calls       :  GETR Get next valid record                     *
      *                LINK link to single message enquiry            *
      *****************************************************************
      *
     C           SUMM      BEGSR
      *
      **  Write status selection to display field for screen: add
      **  delimiters of ';' if more than one selected.
     C                     CLEARSTA
      *
     C                     Z-ADD1         Z       30
      *
     C           STS1      IFEQ 'X'
     C                     MOVELTXT,1     @WK20  20
     C                     MOVEA@WK20     STA,Z
     C                     ADD  7         Z
     C                     END
     C           STS2      IFEQ 'X'
     C           Z         IFGT 1
     C                     MOVE ';'       STA,Z
     C                     ADD  2         Z
     C                     END
     C                     MOVELTXT,2     @WK20
     C                     MOVEA@WK20     STA,Z
     C                     ADD  19        Z
     C                     END
     C           STS3      IFEQ 'X'
     C           Z         IFGT 1
     C                     MOVE ';'       STA,Z
     C                     ADD  2         Z
     C                     END
     C                     MOVELTXT,3     @WK20
     C                     MOVEA@WK20     STA,Z
     C                     END
      *
      **  Set lower limits on the appropriate file
     C           REST      TAG
     C           *IN40     IFEQ '1'
     C           KEY2      SETLLRCLDGRL2
     C                     ELSE
     C           KEY1      SETLLRCLDGRL1
     C                     END
      *
      **  Get first valid record
     C                     EXSR GETR
      *
      **  Write 'No records match selection criteria'
     C           *IN49     IFEQ '1'
     C                     MOVEL'MEM1020' @MSGID
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     30
     C                     ELSE
      *
      **  Clear subfile
     C                     SETOF                     2021
     C                     WRITERC2040F7
     C                     SETON                     2021
     C                     Z-ADD1         RRN     50
      *
      **  Perform rollup
     C           *IN23     DOUEQ'0'
      *
      **  RRN of first record on page
     C                     Z-ADDRRN       DSPREC
     C                     Z-ADD0         @CNT    20
      *
      **  Fill subfile page
     C           *IN49     DOWEQ'0'
     C           @CNT      ANDLT14
      *
      **  Write subfile record
     C                     CALL 'ZA0140'
     C                     PARM R1CDAT    ZMDAY   50
     C                     PARM DATF      ZDATF   1
     C                     PARM           ZMDATE  60
     C           SFCDAT    PARM           ZMDAT7  7
     C                     PARM           ZMNU2   1
     C                     PARM           ZMNU3   80
      *
     C           R1RPTS    SCAN 'CRD'     Q       20
     C                     MOVELTXT,Q     SFSTAT
     C                     MOVELR1BRCA    SFBRCA
     C                     MOVELR1ACID    SFACID
     C                     MOVELR1REFR    SFREFR
     C                     WRITERC2040F6
     C                     ADD  1         RRN
     C                     ADD  1         @CNT
      *
      **  Obtain next record matching selection criteria
     C                     EXSR GETR
     C                     END
      *
      **  Display subfile until F3, F12 or rollup pressed
     C           *INKC     DOUEQ'1'
     C           *INKL     OREQ '1'
     C           *IN23     OREQ '1'
      *
      **  If EOF set on SFLEND
     C           *IN49     IFEQ '1'
     C                     SETON                     22
     C                     END
      *
      **  Display subfile
     C                     WRITERC2040F3
     C                     WRITESFLMSGC
     C                     EXFMTRC2040F7
     C                     CALL 'ZM1002'
      *
     C                     SETOF                     2230
      *
      ** If F5 pressed then goto main process to reselect the input
     C           *INKE     IFEQ '1'
     C                     GOTO REST
     C                     END
      *
     C                     Z-ADDLSTRRN    DSPREC
     C                     MOVE *BLANKS   SELECT
      *
      **  If F3, F12 or rollup not pressed perform lower link
     C           *INKC     IFEQ '0'
     C           *INKL     ANDEQ'0'
     C           *IN23     ANDEQ'0'
     C                     EXSR LINK
     C                     END
     C                     END
     C                     END
     C                     END
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * Subroutine  :  GETR                                           *
      * Function    :  Get next valid record                          *
      *                                                               *
      * Called by   :  SUMM Process message summary                   *
      * Calls       :  -                                              *
      *****************************************************************
      *
     C           GETR      BEGSR
      *
      **  Do until EOF or matching record found
     C           *IN49     DOUEQ'1'
     C           *IN50     OREQ '0'
     C                     SETOF                         50
      *
      **  Read appropriate file
     C           *IN40     IFEQ '1'
     C                     READ RCLDGRL2                 49
     C                     ELSE
     C                     READ RCLDGRL1                 49
     C                     END
      *
      **  If branch entered check for upper limit
     C           MBIN      IFEQ 'Y'
     C           SBCH      ANDNE'ALL'
     C           SBCH      ANDNER1BRCA
     C                     SETON                     49
     C                     END
      *
      **  Match status selection against record
     C           R1RPTS    IFEQ 'C'
     C           STS1      ANDNE'X'
     C                     SETON                     50
     C                     END
      *
     C           R1RPTS    IFEQ 'R'
     C           STS2      ANDNE'X'
     C                     SETON                     50
     C                     END
      *
     C           R1RPTS    IFEQ 'D'
     C           STS3      ANDNE'X'
     C                     SETON                     50
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * Subroutine  :  LINK                                           *
      * Function    :  access the single message enquiry function     *
      *                                                               *
      * Called by   :  SUMM Process message summary                   *
      * Calls       :  RC2045 single message enquiry function         *
      *                *PSSR                                          *
      *****************************************************************
      *
     C           LINK      BEGSR
      *
      **  Save next RCLDGRPD record
     C                     MOVE R1BRCA    HBRCA   3
     C                     MOVE R1REFR    HREFR  16
     C**********           MOVE R1ACID    HACID  18                                           CGL029
     C                     MOVE R1ACID    HACID  24                                           CGL029
     C                     MOVE R1CDAT    HCDAT   50
     C                     MOVE R1RPTS    HRPTS   1
     C                     MOVE R1SDAT    HSDAT   50
     C                     MOVE R1STIM    HSTIM   60
     C                     MOVE R1DSEQ    HDSEQ   30
     C                     MOVE R1RDAT    HRDAT   50
     C                     MOVE R1RTIM    HRTIM   60
     C                     MOVE R1LJOB    HLJOB  10
     C                     MOVE R1LUSR    HLUSR  10
     C                     MOVE R1LJNO    HLJNO   6
     C                     Z-ADDRRN       SRRN    50
      *
     C                     MOVE *BLANKS   RTCD
     C                     MOVE *BLANKS   TRN
     C                     MOVE *BLANKS   ATN
     C                     Z-ADD0         X       30
      *
     C           *IN11     DOUEQ'1'
     C                     READCRC2040F6                 11
      *
      **  Advance pointer to first non-blank record
     C           *IN11     DOWEQ'0'
     C           SELECT    ANDEQ' '
     C                     UPDATRC2040F6
     C                     READCRC2040F6                 11
     C                     END
      *
     C           *IN11     IFEQ '0'
     C           SELECT    IFEQ @A1
     C           SELECT    OREQ @A2
      *
      **  Store enquire & re-send selections
     C                     ADD  1         X
     C                     MOVE SFREFR    TRN,X
     C                     MOVE SELECT    ATN,X
     C                     MOVE ' '       SELECT
     C                     UPDATRC2040F6
      *
      **  Selection not equal to E,R,
     C                     ELSE
      *
      **  Not authorised to action code
     C           SELECT    IFEQ 'E'
     C           SELECT    OREQ 'R'
     C                     MOVEL'MEM1077' @MSGID
      *
      **  action code invalid
     C                     ELSE
     C                     MOVEL'MEM1021' @MSGID
     C                     END
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     243038
     C                     UPDATRC2040F6
     C                     SETOF                     2438
     C                     END
     C                     END
     C                     END
      *
      **  Process enquire selections
     C           X         IFGT 0
     C                     Z-ADDX         @X      30
     C                     Z-ADD1         X
      *
     C           X         DOWLE@X
     C           RTCD      ANDNE'12'
     C                     MOVE *BLANKS   FILA
      *
      **  If F8 pressed process next selection
     C           RTCD      IFEQ '08'
     C                     ADD  1         X
     C                     END
      *
      **  If F9 pressed process previous selection
     C           RTCD      IFEQ '09'
     C                     SUB  1         X
     C                     END
      *
      **  If single selection suppress F8 & F9
     C           @X        IFEQ 1
     C                     MOVE 'S'       FILA
     C                     END
      *
      **  If first selection suppress F9
     C           X         IFEQ 1
     C           FILA      ANDNE'S'
     C                     MOVE 'F'       FILA
     C                     END
      *
      **  If last selection suppress F8
     C           X         IFEQ @X
     C           FILA      ANDNE'S'
     C                     MOVE 'L'       FILA
     C                     END
      *
      **  Perform lower link
     C                     MOVE TRN,X     REFR
     C                     MOVE ATN,X     ACTN
     C                     CALL 'RC2045'
     C                     PARM           REFR   16
     C                     PARM           ACTN    1
     C                     PARM           FILA    1
     C                     PARM           RTCD    2
     C                     PARM *BLANKS   @ERR    1
      *
     C           @ERR      IFEQ '1'
     C                     EXSR *PSSR
     C                     END
      *
     C                     END
     C                     END
      *
      **  Restore next RCLDGRPD record
     C                     MOVE HBRCA     R1BRCA
     C                     MOVE HREFR     R1REFR
     C                     MOVE HACID     R1ACID
     C                     MOVE HCDAT     R1CDAT
     C                     MOVE HRPTS     R1RPTS
     C                     MOVE HSDAT     R1SDAT
     C                     MOVE HSTIM     R1STIM
     C                     MOVE HDSEQ     R1DSEQ
     C                     MOVE HRDAT     R1RDAT
     C                     MOVE HRTIM     R1RTIM
     C                     MOVE HLJOB     R1LJOB
     C                     MOVE HLUSR     R1LUSR
     C                     MOVE HLJNO     R1LJNO
     C                     Z-ADDSRRN      RRN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * Subroutine  :  VSEL                                           *
      * Function    :  validate selection                             *
      *                                                               *
      * Called by   :  DETL Detail Processing                         *
      * Calls       :  -                                              *
      *****************************************************************
      *
     C           VSEL      BEGSR
      *
      ** Validate the Start Date.
      ** If start date is blank use yesterdays date. Use the standard
      ** program ZM0110 to obtain this from JNSTAT. The date will be in
      ** Midas Day number format.
     C           SDAT      IFEQ *BLANK
     C                     CALL 'ZM0110'
     C                     PARM           @@DAYN
      *
     C                     CALL 'ZA0140'
     C                     PARM @@DAYN    ZMDAY   50
     C                     PARM DATF      ZDATF   1
     C           SDATN     PARM           ZMDATE  60
     C                     PARM           ZMNU1   7
     C                     PARM           ZMNU2   1
     C                     PARM           ZMNU3   80
      *
     C                     ELSE
      *
      **  Check the start date is in valid format.
     C                     MOVE SDAT      @@DATE
     C                     CALL 'ZA0270'
     C                     PARM           @@DATE  60
     C                     PARM BJDFIN    @@DFMT  1
     C                     PARM           @@RTN   1
     C                     PARM           @@DAYN  50
      *
     C           @@RTN     IFNE '0'
     C                     MOVEL'MEM1015' @MSGID
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     3031
     C                     END
      *
      ** Check the start date is not later than the run date.
     C           @@DAYN    IFGT BJRDNB
     C                     MOVEL'MEM1016' @MSGID
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     3031
     C                     END
     C                     END
      *
      **  Validate branch
     C           MBIN      IFEQ 'Y'
      *
      **  If branch is blank, set to user's default.
     C           SBCH      IFEQ *BLANKS
     C           BANK      IFEQ 'Y'
     C                     MOVE 'ALL'     SBCH
     C                     ELSE
     C                     MOVE DBRN      SBCH
     C                     END
     C                     END
      *
      **  ...if not multi-branch system move SINGLE BRANCH
     C                     ELSE
     C                     MOVE BJSBRC    SBCH
     C                     END
      *
     C           SBCH      IFEQ 'ALL'
      *
      **  If user does not have Bank level authority
      **  issue error message.
     C           BANK      IFNE 'Y'
     C                     MOVEL'MEM1037' @MSGID
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     30
     C                     ELSE
     C                     MOVE 'ER'      SATN
     C                     END
      *
     C                     ELSE
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY    '@OPTN   7
     C                     PARM SBCH      @DSNB   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
      ** If Branch not found on file issue error message
     C           @RTCD     IFNE *BLANKS
      *
     C                     MOVEL'MEM1036' @MSGID
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     3032
      *
     C                     ELSE
      *
      **  If user not System Routing Officer or does not have Bank
      **  level authority check authority.
     C           BANK      IFNE 'Y'
      *
     C                     CALL 'ZVRAT'
     C                     PARM SBCH      ZBR     3
     C                     PARM           ACODES  6
     C                     PARM 0         ERR     10
      *
      **  If authorised save action codes, else issue error message.
     C           ERR       IFEQ 0
     C           ACODES    ANDNE*BLANKS
     C                     MOVELACODES    SATN
     C                     ELSE
     C                     MOVEL'MEM1019' @MSGID
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     3032
     C                     END
      *
     C                     ELSE
     C                     MOVE 'ER'      SATN
     C                     END
      *
     C                     END
      *
     C                     END
      *
      ** Set indicator 40 to condition file to use dependant on
      ** whether branch is all (*IN40 = 1) or not (*IN40 = 0).
     C           SBCH      IFEQ 'ALL'
     C                     MOVE '1'       *IN40
     C                     ELSE
     C                     MOVE '0'       *IN40
     C                     END
      *
      **  Validate the status field
      **  'X' must be entered in status fields
     C           STS1      IFNE ' '
     C           STS1      ANDNE'X'
     C           STS2      ORNE ' '
     C           STS2      ANDNE'X'
     C           STS3      ORNE ' '
     C           STS3      ANDNE'X'
     C                     MOVEL'MEM1032' @MSGID
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     3033
     C                     END
      *
      ** If left blank, move 'X' to all
     C           STAT      IFEQ '   '
     C                     MOVEL'XXX'     STAT
     C                     END
      *
      ** Set up the key fields
     C                     MOVE SBCH      @KBRCH
     C                     Z-ADD@@DAYN    @KDAYN
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * Subroutine  :  INIT                                           *
      * Function    :  Initial process                                *
      *                                                               *
      * Called by   :  main process                                   *
      * Calls       :  *PSSR                                          *
      *****************************************************************
      *
     C           INIT      BEGSR
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      BIS@   80
      *
      ** Read in Installation Data
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
      ** Read in user details
     C           *NAMVAR   DEFN           ZMUSER
     C                     IN   ZMUSER
      *
      **  Set up LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'RC2040'  DBPGM
     C                     MOVE *BLANKS   DBASE
     C                     OUT  LDA
      *
      **  Access SDBANKPD for date format
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE
     C                     MOVEL'FIRST'   DBKEY            * DB ERROR 01 *
     C                     MOVEL'SDBANKPD'DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      ** If the multibranching indicator is on, set on indicator 10
     C           MBIN      IFEQ 'Y'
     C                     MOVE '1'       *IN10
     C                     END
      *
      ** Set up Key lists
     C           KEY1      KLIST
     C                     KFLD           @KBRCH  3
     C                     KFLD           @KDAYN  50
      *
     C           KEY2      KLIST
     C                     KFLD           @KDAYN
      *
      ** initialise variables
     C                     MOVEL'RC2040'  PGMQ   10
     C                     MOVEL*BLANKS   MSGKEY  4
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * Subroutine  :  *PSSR                                          *
      * Function    :  Called automatically if a program error occurs *
      *                or directly in case of database errors.        *
      *                This program DUMPs the program just once.      *
      *                                                               *
      * Called by   :  INIT Initial Processing                        *
      *                LINK Link to single message                    *
      *                                                               *
      * Calls       :  -                                              *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      *
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDSR
      *
      ********************************************************************
**  TXT
Created
Flagged for re-send
Downloaded to PC
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
