     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RC II - Statements report pmt')
      *****************************************************************
      *                                                               *
      *  Midas - Autorecs II Module                                   *
      *                                                               *
      *  RC2070 - Statements Report - Prompt                          *
      *                                                               *
      *  Function:  This program allows users of Automatic            *
      *             Reconciliations II to report on                   *
      *             Statement Details.                                *
      *  I/C                                                          *
      *                                                               *
      *  Called By: FCC0201 - RCF Prompt                              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01449             Date DDMmmYY               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  S01449 - Autorecs II module.                                 *
      *                                                               *
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
      *                                                               *
      *****************************************************************
     FRC2070DFCF  E                    WORKSTN
     F                                              KINFDS INFDS
      *
      *****************************************************************
      *                                                               *
      *   Indicator Usage                                             *
      *   ---------------                                             *
      *                                                               *
      *   10        Multibranching Indicator                          *
      *   11        EOF changed records on subfile                    *
      *                                                               *
      *   30        General Screen Error                              *
      *   31        Screen Date in Error (Start Date)                 *
      *   32        Screen Date in Error (End Date)                   *
      *   33        Screen 'X' Select in Error                        *
      *                                                               *
      *   40        Branch Conditioning                               *
      *   49        EOF RCLDGRx                                       *
      *   50        Matching Record Found                             *
      *                                                               *
      *   KC        F3 Enabled                                        *
      *   KE        F5 Enabled                                        *
      *                                                               *
      *   U7 & U8   Database error                                    *
      *                                                               *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
      /SPACE 3
     ILDA       E DSLDA                         256
      ** Local data area for database error details
      *
     IRUNDAT    E DSRUNDAT
     I                                    P   8  100RUNN
     I                                       12  12 DATF
     I                                       13  13 MBIN
      ** Data Area giving Installation Control Details
      *
     I@RPARM      DS                            100
     I                                        1   50@SDAT
     I                                        6  100@EDAT
     I                                       11  11 @STS1
     I                                       12  12 @STS2
     I                                       13  13 @STS3
      **  Parameters for &RPARM - 100 long string passed to RCF
      *
     IZMUSER    E DSZMUSER
      **  User profile details
      *
     IINFDS       DS
     I                                    B 378 3790LSTRRN
      **  Last subfile start RRN
      *
     ISTAT        DS                              3
     I                                        1   1 STS1
     I                                        2   2 STS2
     I                                        3   3 STS3
      ** Status Selection from Display File
      *
     I            DS
     I                                        1   60SDATN
     I                                        1   6 SDAT
      ** Dates
      *
     I            DS
     I                                        1   60EDATN
     I                                        1   6 EDAT
      ** Dates
      *
     ISDBANK    E DSSDBANKPD
      **  External Data structure for bank details.
      *
     ISDBRCH    E DSSDBRCHPD
      **  External Data structure for branch details.
      *
     IDSFDY     E DSDSFDY
      **  Short data structure for access programs
      *
     IDSSDY     E DSDSSDY
      **  Long data structure for access programs
      *
     IPSDS       SDS
     I                                      244 253 @JOB
     I                                      254 263 @USER
     I                                      264 269 @NUM
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *   Index to subroutines                                        *
      *   --------------------                                        *
      *                                                               *
      *   DETL      Detail process                                    *
      *   VSEL      Validate selection                                *
      *   INIT      Initial process                                   *
      *   TERM      Termination Processing                            *
      *   *PSSR     Program Exception Error Routine                   *
      *                                                               *
      *****************************************************************
      /EJECT
      *
      **  RCF Parmeters
     C           *ENTRY    PLIST
     C                     PARM           @SEQ    5
     C                     PARM           @LVL    1
     C                     PARM           @ENT    1
     C                     PARM           @RPARM100
     C                     PARM           RACT    1
     C                     PARM           RCMD    1
      *
      **  ZM1001 ERROR
     C           PLIST3    PLIST
     C                     PARM           @MSGID 10
     C                     PARM           @MSDTA 78
      *****************************************************************
      * Subroutine  :  MAIN                                           *
      * Function    :  Main Process                                   *
      *                                                               *
      * Called by   :  -                                              *
      * Calls       :  INIT  Initial processing                       *
      *                DETL  Detail processing                        *
      *                TERM  Termination processing                   *
      *****************************************************************
      *
      ** Initial process
     C                     EXSR INIT
      *
      ** Detail process
     C                     EXSR DETL
      *
      ** Perform termination process
     C                     EXSR TERM
      /EJECT
      *****************************************************************
      * Subroutine  :  DETL                                           *
      * Function    :  Detail processing                              *
      *                                                               *
      * Called by   :  MAIN Main process                              *
      * Calls       :  VSEL Validate selection                        *
      *****************************************************************
      *
     C           DETL      BEGSR
      *
      **  Display selection screen until F3 pressed
     C           *INKC     DOUEQ'1'
     C           *INKL     OREQ '1'
     C           *IN30     OREQ '0'
      *
     C                     WRITERC2070F1
     C                     WRITESFLMSGC
     C                     EXFMTRC2070F2
     C                     CALL 'ZM1002'
      *
      **  If F3 not pressed validate selection
     C           *INKC     IFEQ '0'
     C                     SETOF                     3031
     C                     SETOF                     3233
     C                     EXSR VSEL
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * Subroutine  :  VSEL                                           *
      * Function    :  validate selection                             *
      *                                                               *
      * Called by   :  DETL Detail Processing                         *
      * Calls       :  -                                              *
      *****************************************************************
      *
     C           VSEL      BEGSR
      *
      ** Validate the Start Date.
      ** If start date is blank use todays date. Use the standard
      ** program ZM0080 to obtain this from RUNDAT.
     C           SDAT      IFEQ *BLANK
      *
     C                     CALL 'ZA0140'
     C           @@DAYN    PARM RUNN      ZMDAY   50
     C                     PARM DATF      ZDATF   1
     C           SDATN     PARM           ZMDATE  60
     C                     PARM           ZMNU1   7
     C                     PARM           ZMNU2   1
     C                     PARM           ZMNU3   80
      *
     C                     ELSE
      *
      **  Check the start date is in valid format.
     C                     MOVE SDAT      @@DATE
     C                     CALL 'ZA0270'
     C                     PARM           @@DATE  60
     C                     PARM BJDFIN    @@DFMT  1
     C                     PARM           @@RTN   1
     C                     PARM           @@DAYN  50
      *
     C           @@RTN     IFNE '0'
     C                     MOVEL'MEM1015' @MSGID
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     3031
     C                     END
      *
      ** Check the start date is not later than the run date.
     C           @@DAYN    IFGT BJRDNB
     C                     MOVEL'MEM1016' @MSGID
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     3031
     C                     END
     C                     END
      *
      ** Validate the End Date.
      ** If end date is blank use todays date.
     C           EDAT      IFEQ *BLANK
     C                     MOVE BJRDNB    @@EDYN  50
      *
     C                     CALL 'ZA0140'
     C                     PARM @@EDYN    ZMDAY   50
     C                     PARM DATF      ZDATF   1
     C           EDATN     PARM           ZMDATE  60
     C                     PARM           ZMNU1   7
     C                     PARM           ZMNU2   1
     C                     PARM           ZMNU3   80
      *
     C                     ELSE
      *
      **  Check the end date is in valid format.
     C                     MOVE EDAT      @@DATE
     C                     CALL 'ZA0270'
     C                     PARM           @@DATE  60
     C                     PARM BJDFIN    @@DFMT  1
     C                     PARM           @@RTN   1
     C                     PARM           @@EDYN  50
      *
     C           @@RTN     IFNE '0'
     C                     MOVEL'MEM1015' @MSGID
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     3032
     C                     END
      *
      ** Check the end date is not later than the run date.
     C           @@EDYN    IFGT BJRDNB
     C                     MOVEL'MEM1016' @MSGID
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     3032
     C                     END
     C                     END
      *
      **  Validate the status field
      **  'X' must be entered in status fields
     C           STS1      IFNE ' '
     C           STS1      ANDNE'X'
     C           STS2      ORNE ' '
     C           STS2      ANDNE'X'
     C           STS3      ORNE ' '
     C           STS3      ANDNE'X'
     C                     MOVEL'MEM1032' @MSGID
     C                     CALL 'ZM1001'  PLIST3
     C                     SETON                     3033
     C                     END
      *
      ** If left blank, move 'X' to all
     C           STAT      IFEQ '   '
     C                     MOVEL'XXX'     STAT
     C                     END
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      * Subroutine  :  INIT                                           *
      * Function    :  Initial process                                *
      *                                                               *
      * Called by   :  MAIN  Main process                             *
      * Calls       :  *PSSR Error handling                           *
      *****************************************************************
      *
     C           INIT      BEGSR
      *
      ** Set up copyright parameter
     C                     MOVEACPY@      BIS@   80
      *
      ** Read in Installation Data
     C           *NAMVAR   DEFN           RUNDAT
     C           *NAMVAR   DEFN           LDA
     C                     IN   RUNDAT
      *
      **  Set up LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'RC2070'  DBPGM
     C                     MOVE *BLANKS   DBASE
     C                     OUT  LDA
      *
      **  Access SDBANKPD for date format
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE
     C                     MOVEL'FIRST'   DBKEY            * DB ERROR 01 *
     C                     MOVEL'SDBANKPD'DBFILE
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      ** If the multibranching indicator is on, set on indicator 10
     C           MBIN      IFEQ 'Y'
     C                     MOVE '1'       *IN10
     C                     END
      *
      ** initialise variables
     C                     MOVEL'RC2070'  PGMQ   10
     C                     MOVEL*BLANKS   MSGKEY  4
      *
     C                     ENDSR
      /EJECT
      **************************************************************************
      * Subroutine  :  TERM                                           *
      * Function    :  Termination Processing                         *
      *                                                               *
      * Called by   :  MAIN Main process                              *
      * Calls       :  -                                              *
      **************************************************************************
     C           TERM      BEGSR
      *
      **  If F3 was pressed send termination code of 'E' back to RCF
     C           *INKC     IFEQ '1'
     C                     MOVE 'E'       RCMD
     C                     END
      *
      **  If F12 was pressed send termination code of 'F' back to RCF
     C           *INKL     IFEQ '1'
     C                     MOVE 'F'       RCMD
     C                     END
      *
     C           *INKC     IFEQ '0'
     C           *INKL     ANDEQ'0'
      *
      **  Set up &RPARM with selection criteria parameters
     C                     MOVE @@DAYN    @SDAT
     C                     MOVE @@EDYN    @EDAT
     C                     MOVE STS1      @STS1
     C                     MOVE STS2      @STS2
     C                     MOVE STS3      @STS3
      *
     C                     ELSE
      *
     C                     MOVE *BLANKS   @RPARM
     C                     END
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
     C/EJECT
      *****************************************************************
      * Subroutine  :  *PSSR                                          *
      * Function    :  Error handling                                 *
      *                                                               *
      * Called by   :  INIT Initial Processing                        *
      * Calls       :  -                                              *
      *****************************************************************
      *
     C           *PSSR     BEGSR                           ** *PSSR SR **
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     DUMP
      *
     C                     END
      *
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     ENDSR
      *
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
