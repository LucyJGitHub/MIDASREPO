     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RC Drop ledger details')
      *****************************************************************
      *                                                               *
      *  Midas - Automatic Reconciliations II module                  *
      *                                                               *
      *  RC2080 - Ledger Details Drop                                 *
      *                                                               *
      *  Function:  Drop historic downloaded RCII Ledger Details      *
      *                                                               *
      *             This program writes ledger details which should   *
      *             be retained to 2 backup files. On successful      *
      *             completion the backups are copied *REPLACE over   *
      *             the master files. All other messages are dropped. *
      *                                                               *
      *             To be dropped a ledger detail MT950 must be:      *
      *              - downloaded to PC component (status D)          *
      *              - older than (creation date - retention period)  *
      *                                                               *
      *             The retention period used is the same as for      *
      *             incoming SWIFT statements (from SDMGMEPD).        *
      *                                                               *
      *  Close of Business                                            *
      *                                                               *
      *  Called By: RCC2080 - Ledger Details Drop                     *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 219242             Date 01Jul03               *
      *                 219241             Date 01Jul03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 069250             Date 03May94               *
      *                                    Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  219242 - Not possible to create MT103+ payment msg from      *
      *           non-FT transaction (Recompile)                      *
      *  219241 - Defaulting of "Details of Charges" assign a         *
      *           system value (Recompile)                            *
      *  069250 - Remove dummy OVRDBF command from creation parms.    *
      *                                                               *
      *****************************************************************
      *  C R E A T I O N     P A R A M E T E R S                      *
      *                                                               *
/******:*OVRDBF*(File*in*program)*(file*on*system)******************:**   069250
      *                                                               *
     FRCLDGRL0IF  E           K        DISK
     FRCLDGDL0IF  E           K        DISK
     FRCLDGRBKO   E                    DISK
     F            RCLDGRD0                          KRENAMERCLDGRDK
     FRCLDGDBKO   E                    DISK
     F            RCLDGDD0                          KRENAMERCLDGDDK
     FRC2080AUO   E             20     PRINTER
      /EJECT
      *****************************************************************
      *                                                               *
      *   Indicator Usage                                             *
      *   ---------------                                             *
      *                                                               *
      *   10        EoF RCLDGRL0                                      *
      *   11        EoF RCLDGDL0                                      *
      *                                                               *
      *   20        Overflow                                          *
      *                                                               *
      *   U7 & U8   Database error                                    *
      *                                                               *
      *****************************************************************
      /EJECT
     E                    CPY@    1   1 80
      ** Array containing Copyright statement
      *
     I            DS
     I**********                              1  18 R1ACID                                    CGL029
     I                                        1  24 R1ACID                                    CGL029
     I                                        1   6 @HCUST
     I                                        7   9 @HCCY
     I                                       10  19 @HACOD                                    CGL029
     I                                       20  21 @HACSQ                                    CGL029
     I                                       22  24 @HBRCA                                    CGL029
     I**********                             10  13 @HACOD                                    CGL029
     I**********                             14  15 @HACSQ                                    CGL029
     I**********                             16  18 @HBRCA                                    CGL029
      **  DS to break up account details
      *
     ILDA       E DSLDA                         256
      ** LDA for database error reporting
      *
     ISDBANK    E DSSDBANKPD
      **  Data structure for bank details table
      *
     ISDMGME    E DSSDMGMEPD
      **  Data structure for MG/ME I.C.D.
      *
      /EJECT
      *
      *****************************************************************
      *                                                               *
      *   Index to subroutines                                        *
      *   --------------------                                        *
      *                                                               *
      *   INIT      Initial process                                   *
      *   RETAIN    Write details to backup file                      *
      *   REPORT    Report dropped details                            *
      *   *PSSR     Error handling                                    *
      *                                                               *
      *****************************************************************
      *
      /EJECT
      *
      ** Execute initial process
     C                     EXSR INIT
      *
      ** Access reconciliation account record
     C                     READ RCLDGRL0                 10
      *
      ** DoWhile not end-of-file
     C           *IN10     DOWEQ'0'
      *
      ** If details eligible for retention, write to backup file - else
      ** dropped, so write to report
     C           R1CDAT    IFGE @DDAT
     C           R1RPTS    ORNE 'D'
     C                     EXSR RETAIN
     C                     ELSE
     C                     EXSR REPORT
     C                     ENDIF
      *
      ** Read again
     C                     READ RCLDGRL0                 10
      *
     C                     ENDDO
      *
      ** Write End of Report
     C                     WRITERC2080F3
      *
      ** Terminate RPG program
     C                     SETON                     LR
      /EJECT
      *****************************************************************
      * Subroutine  :  RETAIN                                         *
      * Function    :  Write details to backup file                   *
      *                                                               *
      * Called by   :  Main process                                   *
      * Calls       :  -                                              *
      *****************************************************************
      *
     C           RETAIN    BEGSR
      *
      ** Write reference to backup
     C                     WRITERCLDGRDK
      *
      ** Access data
     C           R1REFR    CHAINRCLDGDL0             11
      *
      ** If no record found, perform database error processing
     C           *IN11     IFEQ '1'
     C           *LOCK     IN   LDA
     C                     MOVE '003'     DBASE            * * * * * * * *
     C                     MOVELR1REFR    DBKEY            *  DBERR 003  *
     C                     MOVEL'RCLDGDL0'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** For each data record
     C           *IN11     DOWEQ'0'
      *
      ** Write data to backup
     C                     WRITERCLDGDDK
      *
      ** Read again
     C           R1REFR    READERCLDGDL0                 11
      *
     C                     ENDDO
      *
     C                     ENDSR                           RETAIN
      /EJECT
      *****************************************************************
      * Subroutine  :  REPORT                                         *
      * Function    :  Report dropped details                         *
      *                                                               *
      * Called by   :  Main process                                   *
      * Calls       :  ZA0140 - day number to date conversion         *
      *****************************************************************
      *
     C           REPORT    BEGSR
      *
      **  Write header on overflow
     C           *IN20     IFEQ '1'
     C                     WRITERC2080F1
     C                     SETOF                     20
     C                     END
      *
      ** Format creation date
     C                     CALL 'ZA0140'
     C                     PARM R1CDAT    ZMDAY   50
     C                     PARM BJDFIN    ZDATF   1
     C                     PARM           ZMDATE  60
     C           @HDATC    PARM           ZMDAT7  7
     C                     PARM           ZMNU2   1
     C                     PARM           ZMNU3   80
      *
      **  Write dropped message details
     C                     WRITERC2080F2
      *
     C                     ENDSR                           REPORT
     C/EJECT
      *****************************************************************
      * Subroutine  :  INIT                                           *
      * Function    :  Perform initial processing                     *
      *                                                               *
      * Called by   :  Main process                                   *
      * Calls       :  AOBANKR0                                       *
      *****************************************************************
      *
     C           INIT      BEGSR
      *
      ** Initialise object copyright statement
     C                     MOVEACPY@      BIS@   80
      *
      ** Initialise LDA
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVEL'RC2080'  DBPGM
     C                     MOVE *BLANKS   DBASE
     C                     OUT  LDA
      *
      ** Access SDBANKPD for bank ICD
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C                     PARM           SDBANK
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE '001'     DBASE            * * * * * * * *
     C                     MOVEL'FIRST'   DBKEY            *  DBERR 001  *
     C                     MOVEL'SDBANKPD'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Access SDMGMEPD for bank ICD
     C                     CALL 'AOMGMER0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C                     PARM           SDMGME
      *
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVE '002'     DBASE            * * * * * * * *
     C                     MOVEL'FIRST'   DBKEY            *  DBERR 002  *
     C                     MOVEL'SDMGMEPD'DBFILE           * * * * * * * *
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
      ** Calculate drop date (rundate - retention period); details
      ** created before this date should be dropped
     C           BJRDNB    SUB  ENDSMN    @DDAT   50
      *
      ** Write headings
     C                     WRITERC2080F1
      *
     C                     ENDSR                           INIT
      /EJECT
      *****************************************************************
      * Subroutine  :  *PSSR                                          *
      * Function    :  Called automatically if a program error occurs *
      *                or directly in case of database errors.        *
      *                This program DUMPs the program just once.      *
      *                                                               *
      * Called by   :  Automatically                                  *
      *                INIT                                           *
      *                                                               *
      * Calls       :  -                                              *
      *****************************************************************
      *
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
      *
      ** DUMP and write database error report
     C                     DUMP
     C                     WRITERC2080F4
     C                     ENDIF
      *
     C                     SETON                     U7U8LR
      *
     C                     RETRN
      *
     C                     ENDSR                           *PSSR
      *
      ********************************************************************
**  CPY@
(c) Finastra International Limited 2001
