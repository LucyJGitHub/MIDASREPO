/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas RE COB Retail Advice Control Program')          */
/*********************************************************************/
/*                                                                   */
/*       Midas    - Retail Module                                    */
/*                                                                   */
/*       REC000079- COB Retail Advice Control Program                */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2004           */
/*                                                                   */
/*       Last Amend No. CUT008             Date 08Feb11              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CRE020  *Create    Date 20Jan04              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CUT008  - CRTOBJ rewrite.                                   */
/*                 Change of CRTOBJ parameters.                      */
/*       CRE020 - Midas Plus Online Printing of Advices (GE7)        */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&PPROG) TYPE(*CHAR) LEN(10) +
                          VALUE(REC000079)
             DCL        VAR(&CCG015) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(70)
             DCL        VAR(&ACTION) TYPE(*CHAR) LEN(8)
             DCL        VAR(&RTCD) TYPE(*CHAR) LEN(7) VALUE('       ')
             DCL        VAR(&OPTN) TYPE(*CHAR) LEN(7) VALUE('*VERIFY')
             DCL        VAR(&SAR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&W0RTN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&WCOMT) TYPE(*CHAR) LEN(3) VALUE('YES')
             DCL        VAR(&LDA)   TYPE(*CHAR)  LEN(256)
             DCL        VAR(&MEM)    TYPE(*CHAR) LEN(50)
             DCL        VAR(&CGUSRMSG) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MIDASLG) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MULT) TYPE(*CHAR) LEN(2)
             DCL        VAR(&ERRORC) TYPE(*DEC) LEN(1) VALUE(0)
             DCL        VAR(&TIMEO) TYPE(*DEC) LEN(5)
             DCL        VAR(&SLEVEL) TYPE(*DEC) LEN(4)
             DCL        VAR(&USER) TYPE(*CHAR) LEN(25)
             DCL        VAR(&GROUP) TYPE(*CHAR) LEN(50)
             DCL        VAR(&ICTERM) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&SCSARD) TYPE(*CHAR) LEN(200)
             DCL        VAR(&DAT) TYPE(*CHAR) LEN(5)
             DCLF       FILE(SDBANKPD)
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2004')
/** Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
             CHGJOB     SWS(XXXXXX00)
 
             CHGVAR     VAR(&SAR) VALUE('CCG015')
             CALL       PGM(AOSARDR0) PARM(&RTCD &OPTN &SAR &SCSARD)
 
             IF         COND(&RTCD *EQ '       ') THEN(DO)
                  CHGVAR     VAR(&CCG015) VALUE('Y')
             ENDDO
 
             IF         COND(&RTCD *NE '*NRF    ' *AND &RTCD *NE +
                          '       ') THEN(DO)
                  CHGVAR     VAR(&MSG) VALUE('Correspondence check  +
                          Enhancement failed')
                  GOTO (ABNOR)
             ENDDO
 
/**  Attempt to Start Commitment Control. If unable to start  */
/**  it because it is already active, then indicate that the  */
/**  program is running in I/C Termination.                   */
 
             STRCMTCTL  LCKLVL(*CHG) NFYOBJ(MNTYF (*FILE)) +
                          CMTSCOPE(*JOB)
             MONMSG (CPF8351) EXEC(CHGVAR (&ICTERM) VALUE('Y'))
 
/**  Override message file, MIDAS, to correct language file  */
 
             CALL (SF0410) PARM(&GROUP &USER &SLEVEL &TIMEO  +
                &ERRORC &MULT)
             IF (&MULT *EQ '  ') (CHGVAR (&MULT) VALUE('GB'))
             CHGVAR (&MIDASLG) VALUE(&MULT *CAT 'MIDAS     ')
             OVRMSGF MSGF(MIDAS) TOMSGF(&MIDASLG)
             CHGVAR     VAR(&CGUSRMSG) VALUE(&MULT *CAT 'CGUSRMSG  ')
             OVRMSGF    MSGF(CGUSRMSG) TOMSGF(&CGUSRMSG)
 
/**  Create LDA  */
 
             CRTDTAARA (QTEMP/LDA) TYPE(*CHAR) LEN(256) +
                TEXT('MIDAS SD AS400 LOCAL DATA AREA EQUIVALENT')
             MONMSG (CPF0000)
             CHGDTAARA  DTAARA(LDA) VALUE(' ')
 
             RCVF
             CHGVAR     VAR(&DAT) VALUE(&BJRDNB)
             CPYF       FROMFILE(*LIBL/RSACMTPD) +
                          TOFILE(QTEMP/RERSACPD) MBROPT(*REPLACE) +
                          CRTFILE(*YES) INCREL((*IF VUDT *LE &DAT))
 
/**********  CRTOBJ     SRCMBR(RERSACL0) SRCFILE(REDDSSRCQT) +                         */ /*CUT008*/
/**********               PLIB(QTEMP)                                                  */ /*CUT008*/
 
/**********    CRTOBJ     SRCMBR(RERSACL1) SRCFILE(REDDSSRCQT) +                       */ /*CUT008*/
/**********               PLIB(QTEMP)                                                  */ /*CUT008*/
 
/**********    CRTOBJ     SRCMBR(RERSACL2) SRCFILE(REDDSSRCQT) +                       */ /*CUT008*/
/**********               PLIB(QTEMP)                                                  */ /*CUT008*/
 
/**********    CRTOBJ     SRCMBR(RERSACL3) SRCFILE(REDDSSRCQT) +                       */ /*CUT008*/
/**********               PLIB(QTEMP)                                                  */ /*CUT008*/
 
             CRTOBJ     OBJ(QTEMP/RERSACL0) SRCFILE(REDDSSRCQT) +
                          SRCMBR(*OBJ)                                                    /*CUT008*/
             CRTOBJ     OBJ(QTEMP/RERSACL1) SRCFILE(REDDSSRCQT) +
                          SRCMBR(*OBJ)                                                    /*CUT008*/
             CRTOBJ     OBJ(QTEMP/RERSACL2) SRCFILE(REDDSSRCQT) +
                          SRCMBR(*OBJ)                                                    /*CUT008*/
             CRTOBJ     OBJ(QTEMP/RERSACL3) SRCFILE(REDDSSRCQT) +
                          SRCMBR(*OBJ)                                                    /*CUT008*/
 
             OVRDBF     FILE(RERSACPD) TOFILE(QTEMP/RERSACPD) +
                          OVRSCOPE(*JOB) OPNSCOPE(*JOB)
             OVRDBF     FILE(RERSACL0) TOFILE(QTEMP/RERSACL0) +
                          OVRSCOPE(*JOB) OPNSCOPE(*JOB)
             OVRDBF     FILE(RERSACL1) TOFILE(QTEMP/RERSACL1) +
                          OVRSCOPE(*JOB) OPNSCOPE(*JOB)
             OVRDBF     FILE(RERSACL2) TOFILE(QTEMP/RERSACL2) +
                          OVRSCOPE(*JOB) OPNSCOPE(*JOB)
             OVRDBF     FILE(RERSACL3) TOFILE(QTEMP/RERSACL3) +
                          OVRSCOPE(*JOB) OPNSCOPE(*JOB)
 
             IF         COND(&CCG015 *EQ 'Y') THEN(DO)
 
/** Call Extract File Instance Management */
                 CHGVAR     VAR(&ACTION) VALUE('*PREPARE')
                 CALL       PGM(CGC3605) PARM(&ACTION)
 
 
/**  Call the Retail Advice  Print  */
 
                 CALL       PGM(CG003820) PARM(&W0RTN &WCOMT &PPROG +
                                &CCG015)
                 CALL       PGM(CG003830) PARM(&W0RTN &WCOMT &PPROG +
                                &CCG015)
                 CALL       PGM(CG003840) PARM(&W0RTN &WCOMT &PPROG +
                                &CCG015)
                 CALL       PGM(CG003850) PARM(&W0RTN &WCOMT &PPROG +
                                &CCG015)
                 CALL       PGM(CG003860) PARM(&W0RTN &WCOMT &PPROG +
                                &CCG015)
                 CALL       PGM(CG003870) PARM(&W0RTN &WCOMT &PPROG +
                                &CCG015)
                 RCLRSC
 
                 IF         COND(&W0RTN *NE '       ') THEN(DO)
                      CHGVAR  VAR(&MSG) VALUE(&PPROG *TCAT '- Retail +
                          Advice Correspondence Extract failure')
                      GOTO       CMDLBL(ABNOR)
                 ENDDO
 
                 CALL       PGM(CGC3604)
 
                 IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                     RTVDTAARA  DTAARA(LDA (134 50)) RTNVAR(&MEM)
                     SNDPGMMSG  MSGID(MEM0001) MSGF(MIDAS)  +
                          MSGDTA(&MEM) TOPGMQ(*EXT) TOMSGQ(MOPERQ)
                     GOTO       CMDLBL(ABNOR)
                 ENDDO
             ENDDO
 
             IF         COND(&CCG015 *EQ 'N') THEN(DO)
 
                 CALL       PGM(RE000883) PARM(&W0RTN &PPROG)
                 CALL       PGM(RE000884) PARM(&W0RTN &PPROG)
                 CALL       PGM(RE000885) PARM(&W0RTN &PPROG)
                 CALL       PGM(RE000886) PARM(&W0RTN &PPROG)
                 CALL       PGM(RE000887) PARM(&W0RTN &PPROG)
                 CALL       PGM(RE000888) PARM(&W0RTN &PPROG)
 
                 IF         COND(&W0RTN *NE '       ') THEN(DO)
                      CHGVAR   VAR(&MSG) VALUE(&PPROG *TCAT '- Retail +
                          Advice Print failure')
                      GOTO       CMDLBL(ABNOR)
                 ENDDO
 
             ENDDO
 
             GOTO       CMDLBL(END)
 
ABNOR:
             CHGJOB     SWS(XXXXXXX1)
 
             IF         COND(&CCG015 *EQ 'Y') THEN(DO)
                 CHGVAR     VAR(&ACTION) VALUE('*CLEAR  ')
                 CALL       PGM(CGC3605) PARM(&ACTION)
             ENDDO
 
             ROLLBACK
             MONMSG     MSGID(CPF0000 MCH0000)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                  RTVDTAARA (LDA) RTNVAR(&LDA)
                  MONMSG     MSGID(CPF0000 MCH0000)
                  CHGVAR (&MEM) VALUE(%SST(&LDA 134 50))
                  SNDPGMMSG MSGID(MEM0001) MSGF(MIDAS) MSGDTA(&MEM) +
                  TOPGMQ(*EXT) TOMSGQ(MOPERQ MRUNQ)
                  MONMSG     MSGID(CPF0000 MCH0000)
             ENDDO
 
             SNDPGMMSG MSG(&MSG) TOPGMQ(*EXT) TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)
END:
             DLTOVR     FILE(RERSACPD) LVL(*JOB)
             MONMSG     MSGID(CPF9841)
             DLTOVR     FILE(RERSACL0) LVL(*JOB)
             MONMSG     MSGID(CPF9841)
             DLTOVR     FILE(RERSACL1) LVL(*JOB)
             MONMSG     MSGID(CPF9841)
             DLTOVR     FILE(RERSACL2) LVL(*JOB)
             MONMSG     MSGID(CPF9841)
             DLTOVR     FILE(RERSACL3) LVL(*JOB)
             MONMSG     MSGID(CPF9841)
             CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Misys International Banking Systems Ltd.')
             CALL       PGM(REC000080)
 
             ENDPGM
