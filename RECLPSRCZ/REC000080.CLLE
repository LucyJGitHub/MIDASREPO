/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas RE Drop Records from READVCPD')                 */
/*********************************************************************/
/*                                                                   */
/*       Midas - Retail Module                                       */
/*                                                                   */
/*       REC000080 - Midas RE Drop Records from READVCPD             */
/*                                                                   */
/*       Function: This program will drop records from the Online    */
/*                 Printing of Advice Index File (READVCPD) when     */
/*                 1) The Record's Last Change Type is D             */
/*                 and Authorize indicator is Y                      */
/*                 and Print Index Advice Indicator is Y.            */
/*                 2) The READVCPD Record does not exist on the      */
/*                 system (i.e. no record in master file)            */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2004           */
/*                                                                   */
/*       Last Amend No. AR1040782          Date 17Apr13              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. CRE020  *Create    Date 20Jan04              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       AR1040782 - Program fails on copy of empty XREADVCPD        */
/*                when READVCPD and XREADVCPD are empty.             */
/*                (Child: AR1040783)                                 */
/*       CRE020 - Midas Plus Online Printing of Advices (GE7)        */
/*                                                                   */
/*********************************************************************/
             PGM
/**/
             DCL        VAR(&LDA)   TYPE(*CHAR)  LEN(256)
             DCL        VAR(&MEM)    TYPE(*CHAR) LEN(50)
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DPLIB) TYPE(*CHAR) LEN(7) VALUE('  DPLIB')
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2001')
/**/
/*    Global monitor message                                         */
/**/
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
/**/
             SNDPGMMSG  MSG('REC000880 - Midas RE Drop Records+
                          from READVCPD') TOMSGQ(MRUNQ)
/**/
             CHGJOB     SWS(00000000)
/**/
/*    Setup system prefix variable from SDSTAT data area.            */
/**/
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(%SUBSTRING(&DPLIB 1 2)) VALUE(&SYSID)
/**/
/*    Call RE000880 to check records deleted from their master       */
/*    files and those having                                         */
/**/
             CALL       PGM(RE000080)
/**/
/*    Database error processing                                      */
/**/
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
                RTVDTAARA  DTAARA(LDA) RTNVAR(&LDA)
                CHGVAR VAR(&MEM) VALUE(%SST(&LDA 134 50))
                GOTO CMDLBL(ABNOR)
             ENDDO
/**/
/*    Remove READVCPD records that has already been processed        */
/**/
             CLRPFM     FILE(XREADVCPD)                                                /*AR1040782*/
             MONMSG     MSGID(CPF0000)                                                 /*AR1040782*/
/**/
/**********  CPYF       FROMFILE(READVCPD) TOFILE(&DPLIB/XREADVCPD) +
                          MBROPT(*REPLACE) CRTFILE(*YES) +
                          INCREL((*IF RCHTYP *NE '*'))                              */ /*AR1040782*/
             CPYF       FROMFILE(READVCPD) TOFILE(&DPLIB/XREADVCPD) +
                          MBROPT(*ADD) CRTFILE(*YES) +
                          INCREL((*IF RCHTYP *NE '*'))                                 /*AR1040782*/
             CLRPFM     FILE(READVCPD)
/**********  CPYF       FROMFILE(XREADVCPD) TOFILE(READVCPD) +
                          FROMMBR(*ALL) MBROPT(*REPLACE)                            */ /*AR1040782*/
             CPYF       FROMFILE(XREADVCPD) TOFILE(READVCPD) +
                          FROMMBR(*ALL) MBROPT(*ADD)                                   /*AR1040782*/
             DLTF       FILE(XREADVCPD)
             GOTO       CMDLBL(END)
/**/
/*   Abnormal termination - batch job                                */
/**/
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSG('Midas RE Drop Records from READVCPD +
                          ENDED ABNORMALLY') TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
/**/
END:         CHGVAR     VAR(&CPYFLD) VALUE('(c) +
                          Misys International Banking Systems Ltd.')
             ENDPGM
