/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas RE Account Balance Check Update Job')           */
/*********************************************************************/
/*                                                                   */
/*       Midas - Retail Module                                       */
/*                                                                   */
/*       REC001501 - Account Balance Check Update Job                */
/*                                                                   */
/*       (c) Finastra International Limited 2010                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. AR1089591          Date 05Apr13              */
/*                      AR970585           Date 18May12              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/*                      CAP205  *CREATE    Date 15Feb10              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       AR1089591 - ABC Data Queue Recovery Process                 */
/*                   (Child:AR1089593)                               */
/*                 - Applied for MD019899                            */
/*       AR970585 - Erroneous Today's Available Balance if Account   */
/*                  has a Blocked Collateral                         */
/*                - Delay the job so that retrieval to GLCOLLQD will */
/*                  be set properly                                  */
/*                - Applied fix for AR975746                         */
/*       CAP205 - Teller Related APIs - Account Balance Check        */
/*                                                                   */
/*********************************************************************/
             PGM
             DCL        VAR(&JOBNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&JOBNBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&DTQNAM)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTQLIB)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTQLEN)  TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&DTQDTA)  TYPE(*CHAR) LEN(4021)
             DCL        VAR(&DTQWAIT) TYPE(*DEC)  LEN(5 0)
             DCL        VAR(&JOBDET) TYPE(*CHAR) LEN(28)                               /*AR1089591*/
/*/COPY WNCPYSRC,REH00361                                            */
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2010')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
/*/COPY WNCPYSRC,REH00362                                            */
 
/* Create data area in QTEMP */
 
             CRTDTAARA  DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256)
             MONMSG     MSGID(CPF1023)
 
/* Issue start commitment control */
 
             STRCMTCTL  LCKLVL(*CHG) NFYOBJ(MNTYF (*FILE)) +
                          CMTSCOPE(*JOB)
             MONMSG     MSGID(CPF8351)
 
/* Retrieve job information */
 
             RTVJOBA    JOB(&JOBNAME) USER(&JOBUSER) NBR(&JOBNBR)
 
             ALCOBJ     OBJ((REABCJLCK *DTAARA *EXCL)) WAIT(0)
             MONMSG     MSGID(CPF1002) EXEC(GOTO CMDLBL(ENDPGM))
 
             CHGVAR     VAR(&DTQNAM) VALUE('CREABCQ')
             CHGVAR     VAR(&DTQLIB) VALUE('*LIBL')
             CHGVAR     VAR(&DTQLEN) VALUE(4021)
/**********  CHGVAR     VAR(&DTQDTA) VALUE(' ') */                                     /*AR1089591*/
             CHGVAR     VAR(&DTQWAIT) VALUE(-1)
 
/* Receive message from data que until 'END' message is encountered or error */
/* has been encountered */
 
RECEIVE:
             CHGVAR     VAR(&DTQDTA) VALUE(' ')                                        /*AR1089591*/
             CALL       PGM(QRCVDTAQ) PARM(&DTQNAM &DTQLIB &DTQLEN +
                          &DTQDTA &DTQWAIT)
 
             IF         COND(&DTQDTA *EQ ' ') THEN(DO)
             GOTO       CMDLBL(RECEIVE)
             ENDDO
/*/COPY WNCPYSRC,REH00363                                            */
 
             IF         COND(%SST(&DTQDTA 1 3) = 'END') THEN(DO)
             GOTO       CMDLBL(ENDPGM)
             ENDDO
 
/* Delay 1s to set the retrieval of record in GLCOLLQD */                               /*AR970585*/
                                                                                        /*AR970585*/
             IF         COND(%SST(&DTQDTA 1 12) = 'MNT_GLCOLLPD') +
                          THEN(DO)                                                      /*AR970585*/
             DLYJOB     DLY(1)                                                          /*AR970585*/
             ENDDO                                                                      /*AR970585*/
                                                                                        /*AR970585*/
/* Call Account Balance Check I/C update program */
 
             CALL       PGM(RE001501) PARM(&DTQDTA)
             IF         COND(%SWITCH(XXXXXX11)) THEN(DO)
 
/**Errors*encountered,*resend*back*data*to*CREABCQ**/                                  /*AR1089591*/
/**********  CALL       PGM(QSNDDTAQ) PARM(&DTQNAM &DTQLIB +   */                      /*AR1089591*/
/**********               &DTQLEN  &DTQDTA)                    */                      /*AR1089591*/
/**********  GOTO       CMDLBL(ABNOR)                          */                      /*AR1089591*/
                                                                                       /*AR1089591*/
             ROLLBACK                                                                  /*AR1089591*/
             CHGVAR     VAR(&JOBDET) VALUE(&JOBNBR *TCAT '/' *TCAT +
                          &JOBUSER *TCAT '/' *TCAT &JOBNAME)                           /*AR1089591*/
 
             SNDMSG     MSG('ABC Online Balance Updater job +
                          ABCONUP_XX has failed ' *CAT &JOBDET +
                          *TCAT '. Refer to logs for details. The +
                          job will skip failed message and continue +
                          processing succeeding messages from ABC +
                          Data Queue.') TOMSGQ(MOPERQ)                                 /*AR1089591*/
             CHGJOB     SWS(XXXXXX00)                                                  /*AR1089591*/
                                                                                       /*AR1089591*/
/* Write the poison message to ABC Exception Errors file through RE001511 */           /*AR1089591*/
             CALL       PGM(RE001511) PARM('ExMsgWrite' '  ' &JOBDET +
                          &DTQDTA)                                                     /*AR1089591*/
                                                                                       /*AR1089591*/
/* Carry on with processing succeeding messages. */                                    /*AR1089591*/
             GOTO       CMDLBL(RECEIVE)                                                /*AR1089591*/
 
             ENDDO
             ELSE       CMD(GOTO CMDLBL(RECEIVE))
 
 ABNOR:
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          REC001501 ended abnormally - see job +
                          log') TOMSGQ(MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
             ROLLBACK
 ENDPGM:
             ENDCMTCTL
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000)
             ENDPGM
