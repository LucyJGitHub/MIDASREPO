/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas RE RTS program control')                        */
/*********************************************************************/
/*                                                                   */
/*       Midas - Retail Module                                       */
/*                                                                   */
/*       REC4150 - Teller Sign On                                    */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2004           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*       Last Amend No. BG18886            Date 28May08              */
/*       Prev Amend No. BUG15975A          Date 07Mar08              */
/*                      BUG15975           Date 29Feb08              */
/*                      249977             Date 10Sep07              */
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      TDA035  *CREATE    Date 25Mar04              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       BG18886 - Amend non-standard codes.                         */
/*       BUG15975A - Change blank validation                         */
/*       BUG15975 - Need to select screen more than once before it   */
/*                  loads (Recompile)                                */
/*       249977 - RTS signon should only happen for RTS menu items   */
/*       TDA035 - RTS Signon changes for MidasPlus.                  */
/*                Program to run for webfacing RTS screens.          */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PROG &PARM1 &PARM2 &PARM3)
 
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Misys International Banking Systems Ltd. +
                          2004')
             DCL        VAR(&PROG)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&PARM1)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&PARM2)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&PARM3)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&CMD)    TYPE(*CHAR) LEN(100) VALUE(' ')
             DCL        VAR(&APOST)  TYPE(*CHAR) LEN(1)   VALUE('''')
             DCL        VAR(&PRTCD)  TYPE(*CHAR) LEN(7)   VALUE(' ')
             DCL        VAR(&MSGDTA) TYPE(*CHAR) LEN(80)  VALUE(' ')
             DCL        VAR(&MSGCMD) TYPE(*CHAR) LEN(75)  VALUE(' ')
             DCL        VAR(&TLID)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&TLBC)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&MBR)    TYPE(*CHAR) LEN(4)
             DCL        VAR(&DTAA) TYPE(*CHAR) LEN(5)                                     /*249977*/
 
/**  Global Monitor Message */
 
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) EXEC(GOTO +
                          CMDLBL(ABNORMAL))
 
             CHGVAR     VAR(&MSGDTA) VALUE('REC004150 - Retail Teller +
                          Control Program for Webfacing (' *CAT +
                          &PROG *TCAT ')')
             SNDPGMMSG  MSG(&MSGDTA) TOMSGQ(MRUNQ)
 
/**  Initialise job switches  */
 
             CHGJOB     SWS(XXXXXX00)
 
/**  Setup the command to run the Retail program based from SFMENUPD */
 
             CHGVAR     VAR(&CMD) VALUE('CALL PGM(' *CAT &PROG *CAT +
                          ')')
             IF         COND(&PARM1 *NE '     ') THEN(DO)
                 CHGVAR     VAR(&CMD) VALUE(&CMD *BCAT 'PARM(' *CAT &APOST +
                                      *CAT &PARM1 *TCAT &APOST)
 
                 IF         COND(&PARM2 *NE '     ') THEN( +
                     CHGVAR     VAR(&CMD) VALUE(&CMD *BCAT &APOST *CAT +
                                          &PARM2 *TCAT &APOST))
                 IF         COND(&PARM3 *NE '     ') THEN( +
                     CHGVAR     VAR(&CMD) VALUE(&CMD *BCAT &APOST *CAT +
                                          &PARM3 *TCAT &APOST))
 
                 CHGVAR     VAR(&CMD) VALUE(&CMD *TCAT ')')
                 CHGVAR     VAR(&MSGCMD) VALUE('REC004150 - Run the +
                              command:' *BCAT &CMD)
             ENDDO
 
/**  Create dataarea    */
 
             CRTDTAARA  DTAARA(QTEMP/RTSDTA) TYPE(*CHAR) LEN(256) +
                          TEXT('RTS Teller Transaction Job Dataarea')
             MONMSG     MSGID(CPF1023)
 
/**  Check whether the Teller-ID is active                     */
/**  If the dataarea RTSDTA is blank, call REC4130 and use the */
/**  Teller-ID defined for the user profile to sign on.        */
 
             RTVDTAARA  DTAARA(QTEMP/RTSDTA (1 3)) RTNVAR(&TLID)
             RTVDTAARA  DTAARA(QTEMP/RTSDTA (4 3)) RTNVAR(&TLBC)
 
             IF         COND((&TLID *EQ '   ') *OR (&TLBC *EQ +
                          '   ')) THEN(DO)                                                /*249977*/
             CALL       PGM(REC004132) PARM(&PRTCD 'Y' 'N' 'Y')                           /*249977*/
             IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNORMAL))                                               /*249977*/
/*********   IF         COND(&PRTCD *NE *BLANKS) THEN(GOTO +
                          CMDLBL(ENDPGM))                            */     /*249977*/ /*BUG15975A*/
             IF         COND(&PRTCD *NE '       ') THEN(GOTO +
                          CMDLBL(ENDPGM))                                              /*BUG15975A*/
             RTVDTAARA  DTAARA(QTEMP/RTSDTA (1 3)) RTNVAR(&TLID)                          /*249977*/
             RTVDTAARA  DTAARA(QTEMP/RTSDTA (4 3)) RTNVAR(&TLBC)                          /*249977*/
             ENDDO                                                                        /*249977*/
                                                                                          /*249977*/
/************CHGVAR     VAR(&MBR) VALUE('T'****&TLID)                                 */ /*BG18886*/
             CHGVAR     VAR(&MBR) VALUE('T' *CAT &TLID) /*BG18886*/
 
/*********** IF         COND((&TLID *EQ '   ') *OR (&TLBC *EQ +
                          '   ')) THEN(GOTO CMDLBL(TAGSIGN))                           */ /*249977*/
 
/**  Perform file overrides when Teller ID is active     */
/**  These overrides were copied from REC4130            */
 
             CHGVAR     VAR(&MSGDTA) VALUE('REC004150 - The +
                          Teller-ID' *BCAT &TLID *BCAT 'is +
                          currently signed on.')
             SNDPGMMSG  MSG(&MSGDTA) TOMSGQ(MRUNQ)
 
             OVRDBF     FILE(CHQDPPD) SHARE(*YES) SEQONLY(*NO)
             OVRDBF     FILE(RSACMTPD) SHARE(*YES) SEQONLY(*NO)
 
             OVRDBF     FILE(TTRANPD) MBR(&MBR) SECURE(*YES) +
                          SHARE(*YES) SEQONLY(*NO)
 
             OVRDBF     FILE(TTRANL1) MBR(&MBR) SECURE(*YES) +
                          SHARE(*YES) SEQONLY(*NO)
 
             OVRDBF     FILE(TTRANPT) MBR(&MBR) SECURE(*YES) +
                          SHARE(*YES) SEQONLY(*NO)
 
             OVRDBF     FILE(TTNARPD) SECURE(*YES) SHARE(*YES) +
                          SEQONLY(*NO)
 
/**  Rebuild access path  */
 
             OPNDBF     FILE(TTRANL1) OPTION(*ALL) MBR(&MBR)
             CLOF       OPNID(TTRANL1)
 
             GOTO       CMDLBL(TAGCMD)
 
/**  Teller identifier prompt - Automatic login using the Teller ID from  */
/**  the current user profile (MUSERDD)                                   */
 
TAGSIGN:
/*********** CALL       PGM(REC004132) PARM(&PRTCD 'Y' 'N' 'Y')                        */ /*249977*/
/*********** IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ABNORMAL))                                            */ /*249977*/
 
/**  Call the RTS program based from the input parameter */
/**  and if the Teller SignOn is successful              */
 
TAGCMD:
             IF         COND(&PRTCD *EQ '       ') THEN(DO)
 
                SNDPGMMSG  MSG(&MSGCMD) TOMSGQ(MRUNQ)
 
                CALL       PGM(QCMDEXC) PARM(&CMD 100)
                IF         COND(%SWITCH(XXXXXX11)) THEN(GOTO +
                          CMDLBL(ERROR))
 
/** This code was copied from RE4110 */
                CHGJOB     SWS(000000XX)
                RCLRSC
                MONMSG     MSGID(CPF0000 MCH0000)
 
             ENDDO
 
             RTVDTAARA  DTAARA(QTEMP/RTSDTA (1 3)) RTNVAR(&TLID)                          /*249977*/
             IF         COND(&TLID *NE '   ') THEN(DO)                                    /*249977*/
/**********     CHGVAR     VAR(&DTAA) VALUE('RT'****&TLID)                 */ /*249977*/ /*BG18886*/
                CHGVAR     VAR(&DTAA) VALUE('RT' *CAT &TLID)                  /*249977*/ /*BG18886*/
                DLCOBJ     OBJ((&DTAA *DTAARA *EXCL))                                     /*249977*/
                CHGDTAARA  DTAARA(QTEMP/RTSDTA) VALUE(' ')                                /*249977*/
                SNDPGMMSG  MSG('REC004150 - The Teller-ID' *BCAT &TLID +
                             *BCAT 'has signed off successfully.') +
                             TOMSGQ(MRUNQ)                                                /*249977*/
             ENDDO                                                                        /*249977*/
             GOTO       CMDLBL(ENDPGM)
 
ABNORMAL:
             CHGVAR     VAR(&MSGDTA) VALUE('Program REC004150 ended +
                          abnormally.')
             MONMSG     MSGID(CPF0000 MCH0000)
 
             SNDPGMMSG  MSG(&MSGDTA) TOPGMQ(*EXT) TOMSGQ(MRUNQ MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
             DMPCLPGM
             MONMSG     MSGID(CPF0000 MCH0000)
 
ERROR:
             CHGVAR     VAR(&MSGDTA) VALUE('Transaction failed. +
                          Re-input necessary      ')
             MONMSG     MSGID(CPF0000 MCH0000)
             SNDPGMMSG  MSG(&MSGDTA) TOPGMQ(*EXT) TOMSGQ(MRUNQ MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
 
ENDPGM:
             CHGVAR     VAR(&CPYFLD) VALUE('(c) Misys International +
                          Banking Systems Ltd.')
             ENDPGM
 
