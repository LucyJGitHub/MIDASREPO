/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas RE RTS deallocate dataarea')                    */
/*********************************************************************/
/*                                                                   */
/*       Midas - Retail Module                                       */
/*                                                                   */
/*       REC004152 - Deallocate Teller-ID dataarea RTxxx             */
/*                                                                   */
/*       (c) Finastra International Limited 2004                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/*       Prev Amend No. BG18886            Date 28May08              */
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      TDA035  *CREATE    Date 31Mar04              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       BG18886 - Amend non-standard codes.                         */
/*       TDA035 - RTS Signon changes for MidasPlus.                  */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&PRTCD &PTLID)
 
             DCL        VAR(&CPYFLD) TYPE(*CHAR) LEN(64) VALUE('(c) +
                          Finastra International Limited +
                          2004')
 
             DCL        VAR(&PRTCD)   TYPE(*CHAR) LEN(7)
             DCL        VAR(&PTLID)   TYPE(*CHAR) LEN(3)
             DCL        VAR(&DTAA)    TYPE(*CHAR) LEN(5)
             DCL        VAR(&TLBC)    TYPE(*CHAR) LEN(3)
 
/**  Global Monitor Message */
 
             MONMSG     MSGID(CPF0000 RPG0000 MCH0000) EXEC(GOTO +
                          CMDLBL(ABNORMAL))
 
/**  Send info to message queue */
 
             SNDPGMMSG  MSG('REC004152 - To deallocate RTS dataarea +
                          RTxxx for Teller-ID:' *BCAT &PTLID) TOMSGQ(MRUNQ)
 
/**  Initialise job switches and fields */
 
             CHGJOB     SWS(XXXXXX00)
             CHGVAR     VAR(&PRTCD) VALUE('       ')
 
/**  Get Teller-ID from RTSDTA if the parameter is blank */
 
             IF         COND(&PTLID *EQ '   ') THEN(DO)
               RTVDTAARA  DTAARA(QTEMP/RTSDTA (1 3)) RTNVAR(&PTLID)
               RTVDTAARA  DTAARA(QTEMP/RTSDTA (4 3)) RTNVAR(&TLBC)
 
               IF         COND((&PTLID *EQ '   ') *OR (&TLBC *EQ '   +
                            ')) THEN(GOTO CMDLBL(ENDPGM))
             ENDDO
 
/** Deallocate the RTxxx dataarea */
 
/************CHGVAR     VAR(&DTAA) VALUE('RT'****&PTLID)                              */ /*BG18886*/
             CHGVAR     VAR(&DTAA) VALUE('RT' *CAT &PTLID)                               /*BG18886*/
             DLCOBJ     OBJ((&DTAA *DTAARA *EXCL))
 
/** Check if dataarea is allocated to another job */
 
             ALCOBJ     OBJ((&DTAA *DTAARA *EXCL)) WAIT(0)
             MONMSG     MSGID(CPF1002) EXEC(DO)
               CHGVAR     VAR(&PRTCD) VALUE('*NO_DLC')
               SNDPGMMSG  MSG('REC004152 - Unable to unlock dataarea:' +
                            *BCAT &DTAA) TOMSGQ(MRUNQ MOPERQ)
             ENDDO
 
/** Deallocate the RTxxx dataarea */
 
             DLCOBJ     OBJ((&DTAA *DTAARA *EXCL))
 
             GOTO ENDPGM
 
ABNORMAL:
             CHGVAR     VAR(&PRTCD) VALUE('*ABNOR')
             CHGJOB     SWS(XXXXXX11)
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Program +
                          REC004152 ended abnormally - see job +
                          log') TOMSGQ(MRUNQ MOPERQ)
             MONMSG     MSGID(CPF0000 MCH0000)
             DMPCLPGM
             MONMSG     MSGID(CPF0000 MCH0000)
 
ENDPGM:
             CHGVAR     VAR(&CPYFLD) VALUE('(c) Finastra International +
                          Limited')
             ENDPGM
 
