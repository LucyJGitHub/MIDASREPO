/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas RE Shadow post update End TS')                  */
/*********************************************************************/
/*                                                                   */
/*       Midas - Midas Retail                                        */
/*                                                                   */
/*       REC0672T4 - Midas RE Shadow post update End TS              */
/*                                                                   */
/*       (c) Finastra International Limited 2012                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. AR1091723          Date 26Aug13              */
/*                      CSC056  *CREATE    Date 06Aug12              */
/*                                                                   */
/*-------------------------------------------------------------------*/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       AR1091723 - Remove deletions of main DTAQs                  */
/*       CSC056 - COB Restructure - SC COB Optimisation Phase 1      */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&SNDDTAQ) TYPE(*CHAR) LEN(10) +
                           VALUE('RESPOSSVDQ')
             DCL        VAR(&DRIVER) TYPE(*CHAR) LEN(10) +
                           VALUE(RESPODPD)
             DCL        VAR(&CNAM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SYSID) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DMLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DTAQLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERROR) TYPE(*CHAR) LEN(1)
 
             COPYRIGHT  TEXT('(c) Finastra International Limited +
                           2012')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Send message to MRUNQ */
 
             CHGVAR     VAR(&CNAM) VALUE('REC0672T4')
             SNDPGMMSG  MSG(&CNAM *TCAT ' - Midas RE Shadow post +
                           update - End Task Split') TOMSGQ(MRUNQ)
 
/* Determine full DMLIB library name */
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(&DMLIB) VALUE(&SYSID *CAT 'DMLIB')
 
/* Determine full DTAQLIB library name */
 
             RTVDTAARA  DTAARA(SDSTAT (6 2)) RTNVAR(&SYSID)
             CHGVAR     VAR(&DTAQLIB) VALUE(&SYSID *CAT 'DPLIB')
 
/* Delete main data queue */
 
/**********  DLTDTAQ    DTAQ(&DTAQLIB/&SNDDTAQ)                    */                  /*AR1091723*/
/**********  MONMSG     MSGID(CPF2105 CPF9801)                     */                  /*AR1091723*/
 
/* Retrieve first member description */
 
             RTVMBRD    FILE(&DRIVER) RTNMBR(&MEMBER)
 
/* Delete sub-task data queue */
 
LOOP:
             DLTDTAQ    DTAQ(&DTAQLIB/&MEMBER)
             MONMSG     MSGID(CPF2105 CPF9801)
 
/* Retrieve next member description */
 
             RTVMBRD    FILE(&DRIVER) MBR(&MEMBER *NEXT) +
                           RTNMBR(&MEMBER)
             MONMSG     MSGID(CPF3049) EXEC(GOTO CMDLBL(REMOVE))
 
/* Loop for all members in driving file */
 
             GOTO       CMDLBL(LOOP)
 
REMOVE:
 
/* Remove all members from driving file and trailer file */
 
             RMVM       FILE(&DRIVER) MBR(*ALL)
             MONMSG     MSGID(CPF7301 CPF7303)
 
/* Generate audit report */
 
             CHGVAR     VAR(&ERROR) VALUE('N')
             CALL       PGM(RE0672T4) PARM(&ERROR)
             IF         COND(&ERROR *EQ 'Y') THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
/* Copy the data from temporary output files to the master files */
 
             CPYF       FROMFILE(REMEMOPD) TOFILE(MEMOS) +
                           FROMMBR(*ALL) MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2870) EXEC(DO)
                CLRPFM     FILE(MEMOS)
             ENDDO
 
             CPYF       FROMFILE(REOUTCPD) TOFILE(REMEMLPD) +
                           FROMMBR(*ALL) MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817) CMPDTA(CPF2870) EXEC(DO)
                CLRPFM     FILE(REMEMLPD)
             ENDDO
 
             CPYF       FROMFILE(GLACCMPD) TOFILE(RSACMTPD) +
                           FROMMBR(*ALL) MBROPT(*ADD)
 
             GOTO       CMDLBL(END)
 
ABNOR:
             SNDPGMMSG  MSG('Program ' *BCAT &CNAM *TCAT ' ended +
                           abnormally') TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000)
             CHGJOB     SWS(XXXXXX11)
 
END:
 
             ENDPGM
