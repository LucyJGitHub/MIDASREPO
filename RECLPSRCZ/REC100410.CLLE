/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas RE Cash Management Hierarchy Validation -INT')  */
/*********************************************************************/
/*                                                                   */
/*       Midas - Retail module                                       */
/*                                                                   */
/*       REC100410 - Cash Management Hierarchy Validation            */
/*                   - INTERACTIVE                                   */
/*                                                                   */
/*       (c) Finastra International Limited 2002                     */
/*                                                                   */
/*       Last Amend No. AR945574             Date 29Mar12            */
/*       Prev Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      BUG7298            Date 17May05              */
/*                      CSC023             Date 30Jan04              */
/*                      214254               Date 17Mar03            */
/*                      214360               Date 03Feb03            */
/*                      CRE008 *CREATE       Date 19Feb02            */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       AR945574 - Send SBMJOB confirmation to screen               */
/*                - Call to RE100410 is being bypassed               */
/*                - (Child:AR945575)                                 */
/*       MD046248 - Finastra Rebranding                              */
/*       BUG7298 - Change to message field &MSGARR.                  */
/*       CSC023 - MidasPlus additional packaging.  SBMJOB change.    */
/*                OUTQ must always be *JOBD.                         */
/*       214254 - Cash Management falls over in COB                  */
/*       214360 - Cash Management Deferred Posting                   */
/*       CRE008 - Cash Management                                    */
/*                                                                   */
/*********************************************************************/
/************PGM                                                                       */ /*214254*/
             PGM        PARM(&AUTH)                                                       /*214254*/
 
             DCL        VAR(&AUTH)   TYPE(*CHAR) LEN(1)                                   /*214254*/
             DCL        VAR(&MEM)    TYPE(*CHAR) LEN(70)
             DCL        VAR(&TYPE)   TYPE(*CHAR) LEN(1)
 
             DCL        VAR(&SYS)       TYPE(*CHAR) LEN(2)
             DCL        VAR(&USER)      TYPE(*CHAR) LEN(10)
             DCL        VAR(&LANG)      TYPE(*CHAR) LEN(2)
             DCL        VAR(&TRANSTYPE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&HIIDA)     TYPE(*CHAR) LEN(9)
             DCL        VAR(&UPDATEYN)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&BUFFER)    TYPE(*CHAR) LEN(3000)
/********    DCL        VAR(&MSGARR)    TYPE(*CHAR) LEN(2000)                              BUG7298*/
             DCL        VAR(&MSGARR)    TYPE(*CHAR) LEN(9999)                            /*BUG7298*/
             DCL        VAR(&APIRETC)   TYPE(*CHAR) LEN(1)
 
             DCL        VAR(&SDSTAT) TYPE(*CHAR) LEN(256)
             DCL        VAR(&BUFFER2)   TYPE(*CHAR) LEN(9999)                            /*BUG7298*/
             DCL        VAR(&BUFFER3)   TYPE(*CHAR) LEN(9999)                            /*BUG7298*/
             DCL        VAR(&BUFFER4)   TYPE(*CHAR) LEN(9999)                            /*BUG7298*/
             DCL        VAR(&BUFFER5)   TYPE(*CHAR) LEN(9999)                            /*BUG7298*/
             DCL        VAR(&BUFFER6)   TYPE(*CHAR) LEN(9999)                            /*BUG7298*/
             DCL        VAR(&BUFFER7)   TYPE(*CHAR) LEN(9999)                            /*BUG7298*/
             DCL        VAR(&BUFFER8)   TYPE(*CHAR) LEN(9999)                            /*BUG7298*/
             DCL        VAR(&BUFFER9)   TYPE(*CHAR) LEN(9999)                            /*BUG7298*/
             DCL        VAR(&RESERVID) TYPE(*CHAR) LEN(15)                               /*BUG7298*/
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(50)                                   /*AR945574*/
             DCL        VAR(&MESSAGE) TYPE(*CHAR) LEN(50)                               /*AR945574*/
 
             DCLF       FILE(REACCHPD)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2002')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Check if this job can be run now */
 
/************ALCOBJ     OBJ((RECMVEPD *FILE *EXCLRD)) WAIT(1)                          */ /*214360*/
             ALCOBJ     OBJ((RECMVEPD *FILE *EXCLRD)) WAIT(4)                             /*214360*/
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(DO)
                SNDPGMMSG  MSGID(MEM5053) MSGF(MIDAS) MSGDTA(&MEM) +
                             TOPGMQ(*EXT)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
/* If interactive, submit this job */
 
             RTVJOBA    TYPE(&TYPE)
             IF         COND(&TYPE = '1') THEN(DO)
/************SBMJOB     JOB(CM_HIR_REC) JOBD(MBATCH) JOBQ                              */ /*214254*/
/************ USER(*JOBD) RTGDTA(*JOBD) RQSDTA('CALL +                                 */ /*214254*/
/************ REC100410') INLLIBL(*CURRENT) MSGQ(MOPERQ)                               */ /*214254*/
/************SNDPGMMSG  MSGID(MEM5054) MSGF(MIDAS) MSGDTA                              */ /*214360*/
/************             TOPGMQ(*EXT)                                                 */ /*214360*/
/************   SBMJOB     CMD(CALL PGM(REC100410) PARM(&AUTH)) +                         /*CSC023*/
/************                JOB(CM_HIR_REC) JOBD(MBATCH) +                               /*CSC023*/
/************                JOBQ(INTERFACE) USER(*JOBD) RTGDTA(*JOBD) +                  /*CSC023*/
/************                INLLIBL(*CURRENT) MSGQ(MOPERQ)                    /*214254*/ /*CSC023*/
                SBMJOB     CMD(CALL PGM(REC100410) PARM(&AUTH)) +
                             JOB(CM_HIR_REC) JOBD(MBATCH) +
                             JOBQ(INTERFACE) USER(*JOBD) RTGDTA(*JOBD) +
                             INLLIBL(*CURRENT) MSGQ(MOPERQ) OUTQ(*JOBD)                   /*CSC023*/
                GOTO       CMDLBL(NORMAL)
             ENDDO
 
             CHGJOB     SWS(00000000)
 
/* Clear files */
 
             CLRPFM     FILE(RECMVEPD)
 
/* Set constant variables */
 
             RTVDTAARA  DTAARA(SDSTAT *ALL) RTNVAR(&SDSTAT)
             CHGVAR     VAR(&SYS)       VALUE(%SST(&SDSTAT 6 2))
             RTVJOBA    USER(&USER)
             CHGVAR     VAR(&LANG)      VALUE('GB')
             CHGVAR     VAR(&TRANSTYPE) VALUE('HIER')
             CHGVAR     VAR(&UPDATEYN)  VALUE('N')
 
/* Read all hierarchies */
 
LOOP:        RCVF
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(NORMAL))
 
             IF         COND((&AUTH = 'Y' *AND &HISTAT = 'L') *OR +
                          (&AUTH *NE 'Y')) THEN(DO)                                       /*214254*/
                                                                                          /*214254*/
                CHGVAR     VAR(&HIIDA) VALUE(&HIID)
                CHGVAR     VAR(&BUFFER) VALUE(&HIIDA *CAT &HIHTYP)
 
/**********     CALL       PGM(APCTRANVU) PARM(&SYS &USER &LANG +
                             &TRANSTYPE &UPDATEYN +
                             &APIRETC)                         *****************************SAMPO1*/
             CALL       PGM(APCTRANVU) PARM(&SYS &USER &LANG +
                          &TRANSTYPE &UPDATEYN &BUFFER &BUFFER2 +
                          &BUFFER3 &BUFFER4 &BUFFER5 &BUFFER6 +
                          &BUFFER7 &BUFFER8 &BUFFER9 &MSGARR +
                          &APIRETC &RESERVID)                                            /*BUG7298*/
 
/*********** GOTO       CMDLBL(DOUG)                           ***************************AR945574*/
                IF         COND(&MSGARR *NE '   ') THEN(CALL +
                             PGM(RE100410) PARM(&HIID &HIHISN &HIHTYP +
                             &MSGARR))
 
DOUG:
             ENDDO                                                                        /*214254*/
 
             GOTO LOOP
 
 
/* Normal ending: */
/*================*/
 
NORMAL:
             DLCOBJ     OBJ((RECMVEPD *FILE *EXCLRD))
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
             IF         COND(&TYPE = '1') THEN(DO)                                        /*214360*/
/*************  SNDPGMMSG  MSGID(MEM5054) MSGF(MIDAS) MSGDTA(&MEM) +                      AR945574*/
/*************               TOPGMQ(*EXT)                                        *214360* AR945574*/
/*  Call SCC0010 to send information message to screen */                               /*AR945574*/
                RTVMSG     MSGID(MEM5054) MSGF(GBMIDAS) MSG(&MESSAGE)                   /*AR945574*/
                MONMSG     MSGID(CPF0000 MCH0000)                                       /*AR945574*/
                CHGDTAARA  DTAARA(MIDASMSG (251 50)) VALUE(&MESSAGE)                    /*AR945574*/
                MONMSG     MSGID(CPF0000 MCH0000)                                       /*AR945574*/
                CALL       PGM(SCC0010) PARM('REC100410' 'ENTER' ' ')                   /*AR945574*/
                MONMSG     MSGID(CPF0000 MCH0000)                                       /*AR945574*/
             ENDDO                                                                        /*214360*/
 
             GOTO       CMDLBL(ENDPGM)
 
/* Abnormal ending: */
/*==================*/
 
ABNOR:
             RCLACTGRP  ACTGRP(*ELIGIBLE)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
             DLCOBJ     OBJ((RECMVEPD *FILE *EXCLRD))
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
ENDPGM:
             ENDPGM