/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas RE Cash Management Hierarchy List')             */
/*********************************************************************/
/*                                                                   */
/*       Midas - Retail Module                                       */
/*                                                                   */
/*       REC100501 - Cash Management Hierarchy List                  */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2002           */
/*                                                                   */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Last Amend No. CRE008 *CREATE       Date 19Feb02            */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       CRE008 - Cash Management                                    */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&RSEQ &RLEV &RENT)
 
             DCL        VAR(&RSEQ) TYPE(*CHAR) LEN(5)
             DCL        VAR(&RLEV) TYPE(*CHAR) LEN(1)
             DCL        VAR(&RENT) TYPE(*CHAR) LEN(3)
 
             DCL        VAR(&PRE)    TYPE(*CHAR) LEN(2)
             DCL        VAR(&SDSTAT) TYPE(*CHAR) LEN(256)
             DCL        VAR(&DPLIB)  TYPE(*CHAR) LEN(7)
 
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2002')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(00000000)
 
             SNDPGMMSG  MSG('REC100501 - Cash Management Hierarchy +
                          List') TOMSGQ(MRUNQ)
 
/** Check if LDA data area exists and create it if not **/
 
             CHKOBJ     OBJ(LDA) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9801) EXEC(CRTDTAARA +
                          DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256) +
                          VALUE(' ') TEXT('Midas SD LOCAL DATA AREA +
                          EQUIVALENT') AUT(*USE))
 
/* Determine file library */
 
             RTVDTAARA  DTAARA(SDSTAT *ALL) RTNVAR(&SDSTAT)
             CHGVAR     VAR(&PRE) VALUE(%SST(&SDSTAT 6 2))
             CHGVAR     VAR(&DPLIB) VALUE(&PRE *TCAT 'DPLIB')
 
 
/* Fill input file from RECMHLTPD */
 
             CRTDUPOBJ  OBJ(RECMHLTPD) FROMLIB(&DPLIB) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             MONMSG     MSGID(CPF0000)
             CPYF       FROMFILE(RECMHLPD) TOFILE(QTEMP/RECMHLTPD) +
                          MBROPT(*REPLACE) FMTOPT(*MAP)
             MONMSG     MSGID(CPF0000)
 
 
/* Use RECMHLTPD in QTEMP */
 
             OVRDBF     FILE(RECMHLTPD) TOFILE(QTEMP/RECMHLTPD)
 
             CALL       PGM(RE100501) PARM(&RSEQ &RLEV &RENT)
 
/* Abnormal ending: */
 
             IF         COND(%SWITCH(XXXXXXX1)) THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             RCLACTGRP  ACTGRP(*ELIGIBLE)
 
/* Normal ending: */
/*================*/
 
NORMAL:
             GOTO       CMDLBL(ENDPGM)
 
/* Abnormal ending: */
/*==================*/
 
ABNOR:
             CHGJOB     SWS(XXXXXX11)
 
             RCLACTGRP  ACTGRP(*ELIGIBLE)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
/* Report an error */
 
             CHGVAR     VAR(&MEM) VALUE('REC100501 - Cash Management +
                          Hierarchy List FAILED')
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
             SNDPGMMSG  MSGID(MEM5052) MSGF(MIDAS) MSGDTA(&MEM) +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
ENDPGM:
             ENDPGM
