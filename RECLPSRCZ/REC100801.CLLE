/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI    TEXT('Midas RE Cash Management Hierarchy Validation')       */
/*********************************************************************/
/*                                                                   */
/*       Midas - Retail module                                       */
/*                                                                   */
/*       REC100801 - Cash Management Hierarchy Validation            */
/*                                                                   */
/*       (c) Misys International Banking Systems Ltd. 2002           */
/*                                                                   */
/*       Last Amend No. AR676213             Date 23Nov10            */
/*       Prev Amend No. 260134               Date 01May09            */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CRE008 *CREATE       Date 19Feb02            */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       AR676213 - Apply fix 252661  (Child: AR676229)              */
/*       252661 - Remove calling REC100999 in ABNOR tagged           */
/*       260134 - Upgrade recovery to Midas Plus standard            */
/*       CRE008 - Cash Management                                    */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&CNAM &CSEQ)
 
             DCL        VAR(&CNAM)   TYPE(*CHAR) LEN(10)
             DCL        VAR(&CSEQ)   TYPE(*DEC)  LEN(5 0)
 
             DCL        VAR(&MEM)    TYPE(*CHAR) LEN(70)
             DCL        VAR(&JRNID) TYPE(*CHAR) LEN(10) +
                          VALUE('JREC100801')
             DCL        VAR(&FILE01) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILE02) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILE03) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILE04) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILE05) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILE06) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILE07) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILE08) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILE09) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILE10) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ERROR)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&MODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&HIER) TYPE(*DEC)  LEN(9 0)
             DCL        VAR(&HTYP) TYPE(*CHAR) LEN(2)
             DCL        VAR(&FLDNAME) TYPE(*CHAR) LEN(750 )
             DCL        VAR(&MSGID)   TYPE(*CHAR) LEN(525 )
             DCL        VAR(&MSGDTA)  TYPE(*CHAR) LEN(3375)
             DCL        VAR(&IX)      TYPE(*DEC)  LEN(3 0)
             DCL        VAR(&STAT)   TYPE(*CHAR) LEN(1)
             DCL        VAR(&CSEQA) TYPE(*CHAR) LEN(5)                                    /*260134*/
             DCL        VAR(&STEXT) TYPE(*CHAR) LEN(50)                                   /*260134*/
 
             COPYRIGHT TEXT('(c) Misys International Banking Systems Ltd. +
                          2002')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
             CHGJOB     SWS(00000000)
 
             SNDPGMMSG  MSG('REC100801 - Cash Management Hierarchy +
                          Validation') TOMSGQ(MRUNQ)
 
/*                                                                                        /*260134*/
/* Write current journal sequence number to file SCJSEQPD, using command SCWRTJREG.       /*260134*/
/*                                                                                        /*260134*/
             CHGVAR     VAR(&CSEQA) VALUE(&CSEQ)                                          /*260134*/
             CHGVAR     VAR(&STEXT) VALUE('Start of ' *CAT &CNAM +
                          *CAT &CSEQA)                                                    /*260134*/
             SCWRTJREG  COMP(&CNAM) CMPSEQ(&CSEQ) FLAG('S') +
                          TEXT(&STEXT)                                                    /*260134*/
/* Mark files as NOT valid until this component ends normally */
 
             CHGDTAARA  DTAARA(RECMCCTL (1 1)) VALUE('N')
 
/* Files updated */
 
             CHGVAR     VAR(&FILE01) VALUE('REZSMXPD')
 
/* If restart */
/* Component Journalling End */
 
             CHGVAR     VAR(&STAT) VALUE(' ')
             CALL       PGM(CB0160) PARM(&CNAM &CSEQ &STAT)
             IF         COND(&STAT *EQ 'Y') THEN(DO)
/**********     CALL       PGM(REC100999) PARM(&JRNID 'Y' 'P' &FILE01 +
                             &FILE02 &FILE03 &FILE04 &FILE05 &FILE06 +
                             &FILE07 &FILE08 &FILE09 &FILE10 &ERROR)   */                 /*260134*/
             CALL       PGM(REC100999) PARM(&JRNID 'Y' 'P' &FILE01 +
                          &FILE02 &FILE03 &FILE04 &FILE05 &FILE06 +
                          &FILE07 &FILE08 &FILE09 &FILE10 &ERROR +
                          &CNAM &CSEQ)                                                    /*260134*/
                IF         COND(&ERROR = 'Y') THEN(GOTO CMDLBL(ABNOR))
             ENDDO
 
/* Component Journalling Start */
 
             CALL       PGM(REC100998) PARM(&JRNID &FILE01 &FILE02 +
                          &FILE03 &FILE04 &FILE05 &FILE06 &FILE07 +
                          &FILE08 &FILE09 &FILE10 &ERROR)
             IF         COND(&ERROR = 'Y') THEN(GOTO CMDLBL(ABNOR))
 
/* Flag as run */
             CHGVAR     VAR(&STAT) VALUE('Y')
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &STAT)
 
 
/* Clear 'new' cash management hierarchy files */
 
             CLRPFM     FILE(RELCMHDPD)
             CLRPFM     FILE(RELCMHLPD)
             CLRPFM     FILE(RELZSHDPD)
             CLRPFM     FILE(RELZSHLPD)
             CLRPFM     FILE(RELGAHDPD)
             CLRPFM     FILE(RELGAHLPD)
 
/* Overrides */
 
             OVRDBF     FILE(REACCHPD) TOFILE(REACCHPD) SHARE(*NO)
 
/* Cash Management Hierarchy Validation */
/*======================================*/
 
             CALL       PGM(RE100400) PARM(&MODE &HIER &HTYP +
                          &FLDNAME &MSGID &MSGDTA &IX)
 
 
/* Abnormal ending: */
 
             IF         COND(%SWITCH(XXXXXXX1)) THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             RCLACTGRP  ACTGRP(*ELIGIBLE)
 
/* Cash Management Hierarchy Chain Validation */
/*============================================*/
 
/* Overrides */
 
             OVRDBF     FILE(RECMHLL1) TOFILE(RELCMHLL1)
             OVRDBF     FILE(RECMHLL0) TOFILE(RELCMHLL0)
             OVRDBF     FILE(RECMHLL4) TOFILE(RELCMHLL4)
             OVRDBF     FILE(REZSHLL0) TOFILE(RELZSHLL0)
             OVRDBF     FILE(REGAHLL0) TOFILE(RELGAHLL0)
 
             CALL       PGM(RE100403) PARM(&MODE &HIER &HTYP +
                          &FLDNAME &MSGID &MSGDTA &IX)
 
/* Abnormal ending: */
 
             IF         COND(%SWITCH(XXXXXXX1)) THEN(DO)
             GOTO       CMDLBL(ABNOR)
             ENDDO
 
             RCLACTGRP  ACTGRP(*ELIGIBLE)
 
 
/* Cash Management Hierarchy Relationship Validation */
/*===================================================*/
 
/* Overrides */
 
             OVRDBF     FILE(RELCMHDL0X) TOFILE(RELCMHDL0) SHARE(*NO)
             OVRDBF     FILE(RECMHLL1) TOFILE(RELCMHLL1)
             OVRDBF     FILE(RECMHLL0) TOFILE(RELCMHLL0)
             OVRDBF     FILE(RECMHLL4) TOFILE(RELCMHLL4)
             OVRDBF     FILE(REZSHLL0) TOFILE(RELZSHLL0)
             OVRDBF     FILE(REGAHLL0) TOFILE(RELGAHLL0)
 
             CALL       PGM(RE100404) PARM(&MODE &HIER &HTYP +
                          &FLDNAME &MSGID &MSGDTA &IX)
 
/* Abnormal ending: */
 
             IF         COND(%SWITCH(XXXXXXX1)) THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             RCLACTGRP  ACTGRP(*ELIGIBLE)
 
 
/* Normal ending: */
/*================*/
 
NORMAL:
/* Component Journalling End */
/**********  CALL       PGM(REC100999) PARM(&JRNID 'N' ' ' &FILE01 +
                          &FILE02 &FILE03 &FILE04 &FILE05 &FILE06 +
                          &FILE07 &FILE08 &FILE09 &FILE10 &ERROR)      */                 /*260134*/
             CALL       PGM(REC100999) PARM(&JRNID 'N' ' ' &FILE01 +
                          &FILE02 &FILE03 &FILE04 &FILE05 &FILE06 +
                          &FILE07 &FILE08 &FILE09 &FILE10 &ERROR +
                          &CNAM &CSEQ)                                                    /*260134*/
             IF         COND(&ERROR = 'Y') THEN(GOTO CMDLBL(ABNOR))
 
/* Flag as completed */
             CHGVAR     VAR(&STAT) VALUE('N')
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &STAT)
 
/*                                                                                        /*260134*/
/* Write current journal sequence number to file SCJSEQPD, using command SCWRTJREG.       /*260134*/
/*                                                                                        /*260134*/
             CHGVAR     VAR(&STEXT) VALUE(' ')                                            /*260134*/
             CHGVAR     VAR(&STEXT) VALUE('End of ' *CAT &CNAM +
                          *CAT &CSEQA)                                                    /*260134*/
             SCWRTJREG  COMP(&CNAM) CMPSEQ(&CSEQ) FLAG('E') +
                          TEXT(&STEXT)                                                    /*260134*/
/* Mark files as valid */
 
             CHGDTAARA  DTAARA(RECMCCTL (1 1)) VALUE('Y')
 
             GOTO       CMDLBL(ENDPGM)
 
/* Abnormal ending: */
/*==================*/
 
ABNOR:
             CHGJOB     SWS(XXXXXX11)
 
             RCLACTGRP  ACTGRP(*ELIGIBLE)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
/* Component Journalling End */
 
/**********  CALL       PGM(REC100999) PARM(&JRNID 'Y' 'C' &FILE01 +
                          &FILE02 &FILE03 &FILE04 &FILE05 &FILE06 +
                          &FILE07 &FILE08 &FILE09 &FILE10 &ERROR)      */                 /*260134*/
/**********  CALL       PGM(REC100999) PARM(&JRNID 'Y' 'C' &FILE01 + */                   /*252661*/
/**********               &FILE02 &FILE03 &FILE04 &FILE05 &FILE06 + */                    /*252661*/
/**********               &FILE07 &FILE08 &FILE09 &FILE10 &ERROR + */                     /*252661*/
/**********               &CNAM &CSEQ)  */                                     /*260134*/ /*252661*/
/**********  MONMSG     MSGID(CPF0000 MCH0000 RPG0000) */                                 /*252661*/
             IF         COND(&ERROR *NE 'Y') THEN(DO)                                     /*260134*/
/* Flag as completed                                                                      /*260134*/
             CHGVAR     VAR(&STAT) VALUE('N')                                             /*260134*/
             CALL       PGM(CB0150) PARM(&CNAM &CSEQ &STAT)                               /*260134*/
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)                                    /*260134*/
                                                                                          /*260134*/
/*                                                                                        /*260134*/
/* Write current journal sequence number to file SCJSEQPD, using command SCWRTJREG.       /*260134*/
/*                                                                                        /*260134*/
             CHGVAR     VAR(&STEXT) VALUE(' ')                                            /*260134*/
             CHGVAR     VAR(&STEXT) VALUE('End of ' *CAT &CNAM +
                          *CAT &CSEQA)                                                    /*260134*/
             SCWRTJREG  COMP(&CNAM) CMPSEQ(&CSEQ) FLAG('E') +
                          TEXT(&STEXT)                                                    /*260134*/
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)                                    /*260134*/
             ENDDO                                                                        /*260134*/
 
/* Report an error */
 
             CHGVAR     VAR(&MEM) VALUE('REC100801 - Cash Management +
                          Hierarchy Validation FAILED')
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
             SNDPGMMSG  MSGID(MEM5052) MSGF(MIDAS) MSGDTA(&MEM) +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
ENDPGM:
             ENDPGM
