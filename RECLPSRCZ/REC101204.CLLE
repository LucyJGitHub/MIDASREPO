/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas RE I/C Zero Balancing/Sweeping')                */
/*********************************************************************/
/*                                                                   */
/*       Midas - Retail module                                       */
/*                                                                   */
/*       REC101204 - Input Cycle Zero Balancing/Sweeping             */
/*                                                                   */
/*       (c) Finastra International Limited 2002                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/*       Prev Amend No. BUG27417A          Date 19May10              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/*                      CAP204             Date 03Mar10              */
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*                      CRE008 *CREATE     Date 19Feb02              */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       BUG27417A - Initialise merged posting files before merging. */
/*       CAP204 - Retail Account Transfer                            */
/*       CRE008 - Cash Management                                    */
/*                                                                   */
/*********************************************************************/
             PGM        PARM(&RTIMEX &RHIER &RLINK)
 
             DCL        VAR(&RTIMEX) TYPE(*CHAR) LEN(7)
             DCL        VAR(&RHIER)  TYPE(*CHAR) LEN(11)
             DCL        VAR(&RLINK)  TYPE(*CHAR) LEN(11)
 
             DCL        VAR(&RTIME)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&MEM)   TYPE(*CHAR) LEN(50)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2002')
 
/* Global monitor message */
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Check if this job is already active */
/* End abnormally if it is */
             ALCOBJ     OBJ((RESWPEXC *DTAARA *EXCLRD)) WAIT(2)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             CHGVAR     VAR(&RTIME) VALUE(%SST(&RTIMEX  2  6))
 
             CHGJOB SWS(00000000)
 
/** Check if LDA data area exists and create it if not **/
             CHKOBJ     OBJ(LDA) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9801) EXEC(CRTDTAARA +
                   DTAARA(QTEMP/LDA) TYPE(*CHAR) LEN(256) +
                   VALUE(' ') TEXT('Midas SD LOCAL +
                   DATA AREA EQUIVALENT') AUT(*USE))
 
/* Start commitment control */
 
             STRCMTCTL  LCKLVL(*CHG) NFYOBJ(MNTYF (*FILE)) +
                          CMTSCOPE(*JOB)
             MONMSG     MSGID(CPF0000)
 
/** Merge all postings from ACPO1 and GLACPOL0 to single file GLACPML0 */                 /*CAP204*/
                                                                                          /*CAP204*/
             CLRPFM     FILE(GLACPMPD)                                                 /*BUG27417A*/
             CLRPFM     FILE(GLACRMPD)                                                 /*BUG27417A*/
             CPYF       FROMFILE(ACPO1) TOFILE(GLACPMPD) +
                          MBROPT(*REPLACE) RCDFMT(APOSTPDF)                               /*CAP204*/
             MONMSG    (CPF2817) CMPDTA(CPF2869)                                          /*CAP204*/
                                                                                          /*CAP204*/
             CPYF       FROMFILE(GLACPOL0) TOFILE(GLACPMPD) +
                          MBROPT(*ADD) RCDFMT(APOSTPDF)                                   /*CAP204*/
             MONMSG    (CPF2817) CMPDTA(CPF2869)                                          /*CAP204*/
                                                                                          /*CAP204*/
             CPYF       FROMFILE(ACPO1) TOFILE(GLACRMPD) +
                          MBROPT(*REPLACE) RCDFMT(APOSTRAF)                               /*CAP204*/
             MONMSG    (CPF2817) CMPDTA(CPF2869)                                          /*CAP204*/
                                                                                          /*CAP204*/
/* Do overrides */
 
             OVRDBF FILE(GECXPD) TOFILE(GLPSTTPD) SHARE(*NO)
             OVRDBF FILE(GLAPSTZS) TOFILE(GLPSTZSPD) SHARE(*NO)
 
/* I/C Zero Balancing/Sweeping */
 
             CALL       PGM(RE101302) PARM('I/C' &RTIME &RHIER &RLINK)
 
/* Abnormal ending: */
 
             IF         COND(%SWITCH(XXXXXXX1)) THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             RCLACTGRP  ACTGRP(*ELIGIBLE)
 
/* Normal ending: */
/*================*/
 
NORMAL:
             GOTO       CMDLBL(END)
 
/* Abnormal ending: */
/*==================*/
 
 ABNOR:
/* Report an error */
             CHGVAR     VAR(&MEM) VALUE('Input Cycle Zero +
                          Balancing/Sweeping FAILED')
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
             SNDPGMMSG  MSGID(MEM5052) MSGF(MIDAS) MSGDTA(&MEM) +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
END:         DLCOBJ     OBJ((RESWPEXC *DTAARA *EXCLRD))
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
ENDPGM:      ENDPGM
