/*********************************************************************/
/*STD    CLPBASEBND                                                  */
/*EXI *  TEXT('Midas RE Group A/c Shadow Posting Replication')       */
/*********************************************************************/
/*                                                                   */
/*       Midas - Retail module                                       */
/*                                                                   */
/*       REC101502 - Group A/c Shadow Posting Replication            */
/*                                                                   */
/*       (c) Finastra International Limited 2002                     */
/*                                                                   */
/*       Last Amend No. MD046248           Date 27Oct17              */
/* Bank Fusion Midas 1.4 Base ---------------------------------------*/
/* Midas Plus 1.4 Base 04 -------------------------------------------*/
/* Midas Plus 1.4 Base ----------------------------------------------*/
/* Midas Plus 1.3 ---------------- Base -----------------------------*/
/*       Prev Amend No. BUG7279            Date 19May05              */
/*       Last Amend No. CRE008 *CREATE       Date 19Feb02            */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*       MD046248 - Finastra Rebranding                              */
/*       BUG7279 - Upgrade to MidasPlus                              */
/*       CRE008 - Cash Management                                    */
/*                                                                   */
/*********************************************************************/
             PGM
 
             DCL        VAR(&MEM) TYPE(*CHAR) LEN(50)
             DCL        VAR(&TEMPENTYP) TYPE(*CHAR) LEN(2)
             DCL        VAR(&JSEQN) TYPE(*DEC) LEN(10)
             DCL        VAR(&JSEQA) TYPE(*CHAR)  LEN(10)
             DCL        VAR(&NDSURCVN) TYPE(*CHAR) LEN(10)                               /*BUG7279*/
             DCL        VAR(&NDSURSEQ) TYPE(*DEC) LEN(10 0)                              /*BUG7279*/
 
             DCLF       FILE(REGARSPD)
 
             COPYRIGHT TEXT('(c) Finastra International Limited +
                          2002')
 
/* Global monitor message */
 
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(GOTO +
                          CMDLBL(ABNOR))
 
/* Check if this job is already active */
/* End abnormally if it is */
 
             ALCOBJ     OBJ((REGARSPD *FILE *EXCLRD)) WAIT(2)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000) EXEC(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             CHGJOB SWS(00000000)
 
             STRCMTCTL  LCKLVL(*CHG) NFYOBJ(MNTYF (*FILE)) +
                          CMTSCOPE(*JOB)
             MONMSG     MSGID(CPF0000)
 
             OVRDBF     FILE(REGARSPD) SHARE(*NO)
 
             RCVF
 
             CHGVAR     VAR(&JSEQN) VALUE(&GSLJES)
 
/* Check if journal sequence exists. */
/* If journal sequence does not exist, set to restart at first entry */
 
/************RTVJRNE****JRN(ICJRN) FROMENT(&JSEQN) TOENT(&JSEQN) +
                          RTNENTTYP(&TEMPENTYP)********************************************BUG7279*/
             RTVJRNE    JRN(ICJRN) RCVRNG(*CURCHAIN) FROMENT(&JSEQN) +
                          TOENT(&JSEQN) SEARCH(*DESCEND) +
                          RTNENTTYP(&TEMPENTYP)                                          /*BUG7279*/
             MONMSG     MSGID(CPF0000) EXEC(DO)
                CHGVAR     VAR(&JSEQN) VALUE(0)
                RTVJRNE    JRN(ICJRN) FROMENT(*FIRST) TOENT(*FIRST) +
                             RTNSEQNBR(&JSEQN)
                MONMSG     MSGID(CPF0000)
             ENDDO
 
             CHGVAR     VAR(&JSEQN) VALUE(&JSEQN + 1)
 
/*******     RCVJRNE    JRN(ICJRN) EXITPGM(RE101601) FILE((RSACMTPD +
                          *FIRST) (FFACMVD *ALL)) RCVRNG(ICRCV001 +
                          *CURRENT) FROMENT(&JSEQN) ENTTYP(PT TR) +
                          ENTFMT(*TYPE1) DELAY(*NEXTENT)                                ***BUG7279*/
/*                                                                                         BUG7279*/
/* Get the name of the journal receiver that was attached at the                           BUG7279*/
/* beginning of next day set up.                                                           BUG7279*/
/* This is equivalent to the beginning of 'ICRCV001' in earlier releases of Midas.         BUG7279*/
/*                                                                                         BUG7279*/
             SCRTVJEVT  EVENT(NDSUJR) RECEIVER(&NDSURCVN) +
                          SEQ(&NDSURSEQ)                                                 /*BUG7279*/
             MONMSG     MSGID(CPF0000 MCH0000) EXEC(DO)                                  /*BUG7279*/
               RCVJRNE    JRN(ICJRN) EXITPGM(RE101601) FILE((RSACMTPD +
                            *FIRST) (FFACMVD *ALL)) RCVRNG(*CURCHAIN) +
                            FROMENT(&JSEQN) ENTTYP(PT TR) +
                            ENTFMT(*TYPE1) DELAY(*NEXTENT)                               /*BUG7279*/
               GOTO       CMDLBL(McSKIP)                                                 /*BUG7279*/
             ENDDO                                                                       /*BUG7279*/
             RCVJRNE    JRN(ICJRN) EXITPGM(RE101601) FILE((RSACMTPD +
                          *FIRST) (FFACMVD *ALL)) RCVRNG(&NDSURCVN +
                          *CURRENT) FROMENT(&JSEQN) ENTTYP(PT TR) +
                          ENTFMT(*TYPE1) DELAY(*NEXTENT)                                 /*BUG7279*/
McSKIP:                                                                                  /*BUG7279*/
 
             DLTOVR     FILE(*ALL)
 
/* Abnormal ending: */
 
             IF         COND(%SWITCH(XXXXXXX1)) THEN(DO)
                GOTO       CMDLBL(ABNOR)
             ENDDO
 
             RCLACTGRP  ACTGRP(*ELIGIBLE)
 
/* Normal ending: */
/*================*/
 
NORMAL:
             GOTO       CMDLBL(END)
 
/* Abnormal ending: */
/*==================*/
 
 ABNOR:
             ROLLBACK
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
/* Report an error */
 
             CHGVAR     VAR(&MEM) VALUE('Group A/c Shadow +
                          Posting Replication FAILED')
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
             SNDPGMMSG  MSGID(MEM5052) MSGF(MIDAS) MSGDTA(&MEM) +
                          TOMSGQ(MOPERQ MRUNQ)
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
END:         DLCOBJ     OBJ((REGARSPD *FILE *EXCLRD))
             MONMSG     MSGID(CPF0000 MCH0000 RPG0000)
 
ENDPGM:      ENDPGM
