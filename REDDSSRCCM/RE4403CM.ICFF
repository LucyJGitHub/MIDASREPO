      *****************************************************************
/*STD *  ICFFBASE                                                     *
/*EXI *  TEXT('Midas RE CI Store and Forward Transaction Handler')
/*ADDA*: ADDICFDEVE RE4403CM CIPGMD *REQUESTER *APPC                : *                       153824
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  ICFF/RE4403CM - Cashier Interface (CS) -                     *
      *                  Communications link between the Midas        *
      *                  system on the AS/400 and the remote branch   *
      *                  communications controller for Store & Forward*
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. 153824             Date 21Jan99               *
      *  Prev Amend No. 142976             DATE 12NOV98               *
      *                 CRT003  *CREATE    DATE 22JUL96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  153824 - Cashier comm files have incorrect creation parms    *
      *  142976 - Creation parameters should specify CIPGMD not CSPGMD*
      *  CRT003 - Cashier Interface.                                  *
      *                                                               *
      *****************************************************************
     A*
     A                                      INDARA
     A                                      RCVFAIL(25)
     A                                      RCVCONFIRM(26)
     A                                      RCVDETACH(27)
      *
      **  Send Invite to Receive.
      *
     A          R INVITE
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      INVITE
      *
      **  Receive incoming data from CASHIER.
      *
     A          R DATAIN
      *
     A            FIELDI      1024
      *
      **  Write data to CASHIER with 'CONFIRM'.
      *
     A          R OUTACK
     A                                      FRCDTA
     A                                      INVITE
     A                                      CONFIRM
     A            FIELDO      2560
      *
      **  Write data to CASHIER without confirm.
      *
     A          R OUTNAK
     A                                      FRCDTA
     A                                      INVITE
     A            FIELDO      2560
      *
      **  Status format.
      *
     A          R STATO
     A                                      FRCDTA
     A                                      INVITE
     A            OSTATD       111
      *
      **  KSOF message.
      *
     A          R KSOFO
     A                                      FRCDTA
     A                                      INVITE
     A            OKSOFD       113
      *
      **  KSON message.
      *
     A          R KSONO
     A                                      FRCDTA
     A                                      INVITE
     A            OKSOND       113
