      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Sweeps maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  DSPF/RE0254DF - Sweeps Maintenance Display File              *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CRE401             Date 26Nov09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CRE004  *CREATE    Date 20Jan97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE401 - 4-Eyes Checking Gaps - Sweeps (Applied BUG23989)    *
      *  BUG23989- Display of action on To Authorise status           *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CRE004 - Sweep Accounting                                    *
      *                                                               *
      *****************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      CA03(03 'Exit')
     A                                      INDARA
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
      *
     A          R RE0254F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE0254'
     A                                  1 32'SWEEPS MAINTENANCE'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A          R RE0254F1
     A                                      OVERLAY
     A                                      CF12(12 'Initial Screen')
     A                                  4  2'Sweep Reference'
     A            SSWEP          6   B  4 24
     A N32                                  DSPATR(HI)
     A N32                                  DSPATR(UL)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A                                  4 35'Action Code'
     A            SACTN          1   B  4 48
     A N33                                  DSPATR(HI)
     A N33                                  DSPATR(UL)
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
      *
     A          R RE0254F2
     A                                      OVERLAY
     A                                      CF05(05 'Refresh')
     A  31                                  CF10(10 'Delete')
     A**Start of Change CRE401
     A  59                                  CF11(11 'Authorise')
     A**End of Change CRE401
     A                                      CF12(12 'Initial Screen')
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
      *
     A                                  4  2'Sweep Reference'
     A            SSWEP          6   O  4 24DSPATR(HI)
     A                                  4 35'Action Code'
     A            SACTN          1   O  4 48DSPATR(HI)
     A**Start of Change CRE401
     A**Start of Change BUG23989
     A**58********SAUTH         12A  O  4 67DSPATR(RI)                                      BUG23989
     A  58        SAUTH         21A  O  4 60DSPATR(RI)
     A**End of Change BUG23989
     A**End of Change CRE401
      *
     A*/COPY WNCPYSRC,RE0254DF01
     A  82                              5  2'Transaction Branch'
     A  82        SBRCA          3   B  5 24
     A N34                                  DSPATR(HI)
     A N34N70                               DSPATR(UL)
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A N82                                  DSPATR(ND)
     A                                  7  2'Credit A/c of Origin'
     A**Start of change CGL029
     A**82******                        7 24'Branch-Customer-Ccy-A/c Code-Seq'                CGL029
     A*N82******                        7 24'Customer-Ccy-A/c Code-Seq'                       CGL029
     A**82******  SDRBR          3   B  7 58                                                  CGL029
     A  82                              8 18'Branch-Customer-Ccy-A/c Code-Seq'
     A N82                              8 18'Customer-Ccy-A/c Code-Seq'
     A  82        SDRBR          3   B  8 52
     A**End of change CGL029
     A N35                                  DSPATR(HI)
     A N35N70                               DSPATR(UL)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A N82                                  DSPATR(ND)
     A**Start of change CGL029
     A**********  SDCUS          6   B  7 62                                                  CGL029
     A            SDCUS          6   B  8 56
     A**End of change CGL029
     A N36                                  DSPATR(HI)
     A N36N70                               DSPATR(UL)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A**Start of change CGL029
     A**********  SDCCY          3   B  7 69                                                  CGL029
     A            SDCCY          3   B  8 63
     A**End of change CGL029
     A N37                                  DSPATR(HI)
     A N37N70                               DSPATR(UL)
     A  37                                  DSPATR(RI)
     A  37                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A**Start of change CGL029
     A**********  SDACD          4   B  7 73                                                  CGL029
     A            SDACD         10   B  8 67
     A**End of change CGL029
     A N38                                  DSPATR(HI)
     A N38N70                               DSPATR(UL)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A**Start of change CGL029
     A**********  SDACS          2   B  7 78                                                  CGL029
     A            SDACS          2   B  8 78
     A**End of change CGL029
     A N39                                  DSPATR(HI)
     A N39N70                               DSPATR(UL)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A**Start of change CGL029
     A**80******                        8 24'Retail account number'                           CGL029
     A**80******  SDRAN         10   B  8 58                                                  CGL029
     A  80                              9 18'Retail account number'
     A  80        SDRAN         10   B  9 52
     A**End of change CGL029
     A N40                                  DSPATR(HI)
     A N40N70                               DSPATR(UL)
     A  40                                  DSPATR(RI)
     A  40                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A N80                                  DSPATR(ND)
     A                                 10  2'Credit Sweep Account'
     A**Start of change CGL029
     A**82******                       10 24'Branch-Customer-Ccy-A/c Code-Seq'                CGL029
     A*N82******                       10 24'Customer-Ccy-A/c Code-Seq'                       CGL029
     A**82******  SCBRC          3   B 10 58                                                  CGL029
     A  82                             11 18'Branch-Customer-Ccy-A/c Code-Seq'
     A N82                             11 18'Customer-Ccy-A/c Code-Seq'
     A  82        SCBRC          3   B 11 52
     A**End of change CGL029
     A N41                                  DSPATR(HI)
     A N41N70                               DSPATR(UL)
     A  41                                  DSPATR(RI)
     A  41                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A N82                                  DSPATR(ND)
     A**Start of change CGL029
     A**********  SCCUS          6   B 10 62                                                  CGL029
     A            SCCUS          6   B 11 56
     A**End of change CGL029
     A N42                                  DSPATR(HI)
     A N42N70                               DSPATR(UL)
     A  42                                  DSPATR(RI)
     A  42                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A**Start of change CGL029
     A**********  SCCCY          3   B 10 69                                                  CGL029
     A            SCCCY          3   B 11 63
     A**End of change CGL029
     A N43                                  DSPATR(HI)
     A N43N70                               DSPATR(UL)
     A  43                                  DSPATR(RI)
     A  43                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A**Start of change CGL029
     A**********  SCSCA          4   B 10 73                                                  CGL029
     A            SCSCA         10   B 11 67
     A**End of change CGL029
     A N44                                  DSPATR(HI)
     A N44N70                               DSPATR(UL)
     A  44                                  DSPATR(RI)
     A  44                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A**Start of change CGL029
     A**********  SCACD          2   B 10 78                                                  CGL029
     A            SCACD          2   B 11 78
     A**End of change CGL029
     A N45                                  DSPATR(HI)
     A N45N70                               DSPATR(UL)
     A  45                                  DSPATR(RI)
     A  45                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A**Start of change CGL029
     A**80******                       11 24'Retail account number'                           CGL029
     A**80******  SCRAN         10   B 11 58                                                  CGL029
     A  80                             12 18'Retail account number'
     A  80        SCRAN         10   B 12 52
     A**End of change CGL029
     A N46                                  DSPATR(HI)
     A N46N70                               DSPATR(UL)
     A  46                                  DSPATR(RI)
     A  46                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A N80                                  DSPATR(ND)
     A*/COPY WNCPYSRC,RE0254DF02
     A  81                             13 24'Profit Centre'
     A  81        SPRFC          4   B 13 43
     A N47                                  DSPATR(HI)
     A N47N71                               DSPATR(UL)
     A  47                                  DSPATR(RI)
     A  47                                  DSPATR(PC)
     A  71                                  DSPATR(PR)
     A N81                                  DSPATR(ND)
     A                                 14 24'Transaction Type'
     A            STRAT          5   B 14 43
     A N48                                  DSPATR(HI)
     A N48N70                               DSPATR(UL)
     A  48                                  DSPATR(RI)
     A  48                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                 15 24'Narrative'
     A            SPNAR         30   B 15 43
     A N49                                  DSPATR(HI)
     A N49N70                               DSPATR(UL)
     A  49                                  DSPATR(RI)
     A  49                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                 16 24'Currency'
     A            SCCY           3   B 16 43
     A N50                                  DSPATR(HI)
     A N50N70                               DSPATR(UL)
     A  50                                  DSPATR(RI)
     A  50                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                 17 24'Sweep Date'
     A            SNXTP          6   B 17 43
     A N51                                  DSPATR(HI)
     A N51N70                               DSPATR(UL)
     A  51                                  DSPATR(RI)
     A  51                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                 17 58'Frequency'
     A            SPAYF          1   B 17 69
     A N52                                  DSPATR(HI)
     A N52N70                               DSPATR(UL)
     A  52                                  DSPATR(RI)
     A  52                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                 18 24'Sweep Day Number'
     A            SDYTP          2   B 18 43
     A N53                                  DSPATR(HI)
     A N53N70                               DSPATR(UL)
     A  53                                  DSPATR(RI)
     A  53                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                 19 24'Final Date'
     A            SDLPY          6   B 19 43
     A N54                                  DSPATR(HI)
     A N54N70                               DSPATR(UL)
     A  54                                  DSPATR(RI)
     A  54                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A*/COPY WNCPYSRC,RE0254DF03
     A                                 20 24'Threshold Amount'
     A            SSTHR         14   B 20 43
     A N55                                  DSPATR(HI)
     A N55N70                               DSPATR(UL)
     A  55                                  DSPATR(RI)
     A  55                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                 20 58'Threshold Sign'
     A            SSTHRS         1   B 20 75
     A N56                                  DSPATR(HI)
     A N56N70                               DSPATR(UL)
     A  56                                  DSPATR(RI)
     A  56                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A                                 21 24'Sweep Type'
     A            SSWTP          1   B 21 43
     A N57                                  DSPATR(HI)
     A N57N70                               DSPATR(UL)
     A  57                                  DSPATR(RI)
     A  57                                  DSPATR(PC)
     A  70                                  DSPATR(PR)
     A**Start of Change CRE401
     A  58                             22 58'Last user'
     A  58        SLUSR         10A  O 22 69DSPATR(HI)
     A**End of Change CRE401
     A*/COPY WNCPYSRC,RE0254DF04
     A          R RE0254F3
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A  30                             23 14'F12=Initial screen'
     A**Start of Change CRE401
     A*N30N31***                       23 14'F5=Refresh'                                      CRE401
     A N30N31N59                       23 14'F5=Refresh'
     A**End of Change CRE401
     A N30 31                          23 14'F10=Delete'
     A**Start of Change CRE401
     A  59                             23 14'F11=Authorise'
     A  59                                  DSPATR(HI)
     A**End of Change CRE401
     A N30                             23 29'F12=Initial screen'
      *
     A*================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
