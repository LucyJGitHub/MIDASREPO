      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Audit Letter Request Screen')
     A*****************************************************************
     A*                                                               *
     A*  Midas  RETAIL MODULE
     A*                                                               *
     A*  RE0340DF - AUDIT LETTER REQUEST INPUT SCREEN FORMAT          *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 112563             Date 06Feb97               *
      *                 S01413             Date 13Apr93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  112563 - Header and footer changes required for GUI          *
     A*           Multilanguage support.                              *
     A*  S01413 - Header Box Standardised                             *
     A*                                                               *
     A*****************************************************************
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      CF03(03 'CANCEL REQUEST')
     A                                      CF06(06 'END OF REQUEST')
     A                                      CF12(12 'RESTART REQUEST')
     A          R RE0340F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A            USRID         10A  O 01 02
     A                                  1 13'RE0340A'
     A            SWSID         10   O 02 02
     A                                 01 23'RETAIL AUDIT LETTER REQUEST'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            SRUNA          7A  O 01 72
     A                                 02 71TIME
     A                                 03 22'Requested customers :'
     A*
     A          R RE0340F3
     A                                      OVERLAY
     A  69        ERR           55A  O 18  5
     A                                      DSPATR(RI HI)
     A                                 20  5'Enter Customer number/Shortname'
     A            CUSN          10A  B 20 37
     A                                      DSPATR(HI)
     A  61                                  DSPATR(RI)
     A N61                                  DSPATR(UL)
     A  82                                  DSPATR(PR)
     A                                 21 10'or'
     A                                 21 13'Parent number/Shortname'
     A            PARN          10A  B 21 37
     A                                      DSPATR(HI)
     A  63                                  DSPATR(RI)
     A N63                                  DSPATR(UL)
     A  82                                  DSPATR(PR)
     A  88                             21 55'Branch -'
     A  88        BRCIN          3A  O 21 64
     A**                                             Begin /112563/
     A***********                      22 02'Press F3 to end,'
     A***********                      22 19'F6 to accept request'
     A***********                      22 40'or F12 to return'
     A                                 22 02'F3=Exit  F6=Accept Request  F12=-
     A                                      Cancel'
     A**                                             End   /112563/
     A                                 23 02'Press Rollup/down to view request+
     A                                       ed customers'
     A  82                             24 02'NO FURTHER REQUESTS ALLOWED AF+
     A                                      TER SELECTING ''ALL'' ACCOUNTS'
     A                                      DSPATR(RI)
     A*
     A          R RE0340F2                  SFL
     A            CNOUT         10A  O 05 22DSPATR(HI)
     A*
     A          R RE0340C1                  SFLCTL(RE0340F2)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A                                      OVERLAY
     A  51                                  SFLCLR
     A  53                                  SFLDSP
     A  52                                  SFLDSPCTL
     A  50                                  SFLEND
     A            PGREC1         4S 0H      SFLRCDNBR
