/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Cash Management Hierarchy Details Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE100451DF - Cash Management Hierarchy Details Enquiry       *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 29Nov05               *
      *                 BG8539             Date 15Sep05               *
      *                 CGL029             Date 01Sep03               *
      *                 216375             Date 10Apr03               *
      *                 214360             Date 03Feb03               *
      *                 213910             Date 23Jan03               *
      *                 211210             Date 11Dec02               *
      *                 CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BG8539 - Screen not dispaly properly in WebFacing            *
      *           Add overlay to FOOTER format                        *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  216375 - Show charge accounts as retail numbers if possible  *
      *  214360 - Cash Management Deferred Posting                    *
      *  213910 - Show blocked links                                  *
      *  211210 - Various action code errors                          *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A                                      CF12
     A                                      CF16
     A                                      CF18
     A                                      CF20
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   GROUP A/C HIERARCHY DETAILS                                      *
      * -------------------------------------------------------------------*
      *
     A          R GROUPACH
     A                                      OVERLAY
     A                                      BLINK
      *
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A            DDUSR     R        O  1  2REFFLD(USER)
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'RE100451'
     A                                  1 27'CASH MANAGEMENT HIERARCHY DETAILS'
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
      *
     A                                  4  2'Hierarchy'
     A            CDHISN        10A  O  4 14DSPATR(HI)
     A                                  4 26'Hierarchy Type'
     A            CDHTYP         2A  O  4 42DSPATR(HI)
     A                                  4 47'Hierarchy ID'
     A            CDHIER         9S 0O  4 61DSPATR(HI)
      *
     A                                  5  2'Description'
     A            CDDESC        50A  O  5 14DSPATR(HI)
      *
     A                                  6  2'Main Branch'
     A            CDBRCA         3A  O  6 16DSPATR(HI)
     A                                  6 27'Date Effective From'
     A            DDDEFR         7A  O  6 47DSPATR(HI)
     A                                  6 57'Review Date'
     A            DDRVDT         7A  O  6 74DSPATR(HI)
      *
     A                                  7  2'Input By User'
     A            CDINBU        10A  O  7 16DSPATR(HI)
     A                                  7 27'Authorized By User'
     A            CDAUBU        10A  O  7 46DSPATR(HI)
     A                                  7 57'Last Change Date'
     A            DDLCDT         7A  O  7 74DSPATR(HI)
      *
     A                                  8  2'Currency'
     A            GDCCY          3A  O  8 13DSPATR(HI)
      *
     A                                  9  2'Top on Balance Sheet'
     A            DDTOPB         3A  O  9 23DSPATR(HI)
      *
     A                                 10  2'Link Effective At Last Capitalizat-
     A                                      ion Date'
     A            DDLELC         3A  O 10 47DSPATR(HI)
      *
     A                                 11  2'Unlink Effective At Last Capitaliz-
     A                                      ation Date'
     A            DDUELC         3A  O 11 47DSPATR(HI)
      *
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'PRESS ENTER F3=Exit  F12=Cancel'
      *
      * -------------------------------------------------------------------*
      *   ZERO BALANCING/SWEEPING DETAILS                                  *
      * -------------------------------------------------------------------*
      *
     A          R ZEROBLSW
     A                                      OVERLAY
     A                                      BLINK
      *
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A            DDUSR     R        O  1  2REFFLD(USER)
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'RE100451'
     A                                  1 27'CASH MANAGEMENT HIERARCHY DETAILS'
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
      *
     A                                  3  2'Hierarchy'
     A            CDHISN        10A  O  3 14DSPATR(HI)
     A                                  3 26'Hierarchy Type'
     A            CDHTYP         2A  O  3 42DSPATR(HI)
     A                                  3 47'Hierarchy ID'
     A            CDHIER         9S 0O  3 61DSPATR(HI)
      *
     A                                  5  2'Description'
     A            CDDESC        50A  O  5 14DSPATR(HI)
      *
     A                                  6  2'Main Branch'
     A            CDBRCA         3A  O  6 16DSPATR(HI)
     A                                  6 27'Date Effective From'
     A            DDDEFR         7A  O  6 47DSPATR(HI)
     A                                  6 57'Review Date'
     A            DDRVDT         7A  O  6 74DSPATR(HI)
      *
     A                                  7  2'Input By User'
     A            CDINBU        10A  O  7 16DSPATR(HI)
     A                                  7 27'Authorized By User'
     A            CDAUBU        10A  O  7 46DSPATR(HI)
     A                                  7 57'Last Change Date'
     A            DDLCDT         7A  O  7 74DSPATR(HI)
      *
     A                                  8  2'Holidays'
     A            DDSHOC         7A  O  8 16DSPATR(HI)
     A                                  8 27'Check For Cover on Top'
     A            DDCHKC         3A  O  8 50DSPATR(HI)
      *
     A                                  9  2'Transaction Type - Top'
     A            ZDTYPT         5A  O  9 25DSPATR(HI)
     A                                  9 32'Transaction Type - Sweep'
     A            ZDTYPS         5A  O  9 57DSPATR(HI)
      *
     A                                 10  2'Input Cycle Sweeping Frequency'
     A            ZDICSF        48A  O 10 33DSPATR(HI)
      *
     A                                 11  2'Input Cycle Sweeping/Topping'
     A            DDICSI        48A  O 11 33DSPATR(HI)
      *
     A  30                             12  2'External'
     A  30        DDEXTL         3A  O 12 11DSPATR(HI)
     A  30                             12 15'Master Account'
     A  30        DDMAST         3A  O 12 30DSPATR(HI)
     A  30                             12 34'Narrative'
     A  30        ZDENAR        30A  O 12 44DSPATR(HI)
      *
     A  30                             13  2'External Forward Value Sweeping'
     A  30        DDEFVS         3A  O 13 34DSPATR(HI)
     A  30                             13 38'Forward Value Days'
     A  30        ZDEFVD         3A  O 13 57DSPATR(HI)
     A  30                             13 61'Back Value Days'
     A  30        ZDEBVD         3A  O 13 77DSPATR(HI)
      *
     A  30                             14  2'Destination'
     A  30        ZDDEST        18A  O 14 14DSPATR(HI)
     A  30                             14 33'Type'
     A  30        ZDDSTT         1A  O 14 38DSPATR(HI)
     A  30                             14 41'Senders Correspondent'
     A  30        ZDSNDC         2A  O 14 63DSPATR(HI)
      *
     A  30                             15  2'Receivers Correspondent'
     A  30        ZDRCVC        11A  O 15 26DSPATR(HI)
     A  30                             15 38'Cover Message Required'
     A  30        DDMCMR         3A  O 15 61DSPATR(HI)
      *
      *                                                           /*214360*/
     A**30*****************************16  2'External Account'
     A**30********ZDEXA1********35A**O*16 19DSPATR(HI)
     A**30********ZDEXA2********35A**O*17 19DSPATR(HI)
     A**30********ZDEXA3********35A**O*18 19DSPATR(HI)
     A**30********ZDEXA4********35A**O*19 19DSPATR(HI)
     A**30********ZDEXA5********35A**O*20 19DSPATR(HI)
      *
     A**30*****************************21  2'103' DSPATR(UL)
     A**30*****************************21  6'Code'
     A**30********ZDICD3*********4A**O*21 11DSPATR(HI)
     A**30*****************************21 16'Charges'
     A**30********ZDCHG3*********3A**O*21 24DSPATR(HI)
     A**30*****************************21 28'Account'
     A**30********ZDCAC3********18A**O*21 36DSPATR(HI)
     A**30*****************************21 55'Amount'
     A**30********DDCAM3********17A**O*21 62DSPATR(HI)
      *
     A**30*****************************22  6'Value Date Offset'
     A**30********ZDMVDO*********1A**O*22 24DSPATR(HI)
     A**30*****************************22 26'Bank Operation Code'
     A**30********ZDBOCD*********4A**O*22 46DSPATR(HI)
      *
     A**30*****************************23  2'101' DSPATR(UL)
     A**30*****************************23  6'Code'
     A**30********ZDICD1*********4A**O*23 11DSPATR(HI)
     A**30*****************************23 16'Charges'
     A**30********ZDCHG1*********3A**O*23 24DSPATR(HI)
     A**30*****************************23 28'Account'
     A**30********ZDCAC1********18A**O*23 36DSPATR(HI)
     A**30*****************************23 55'Amount'
     A**30********DDCAM1********17A**O*23 62DSPATR(HI)
      *
     A*********************************24  2'PRESS ENTER F3=Exit  F12=Cancel'
      *
     A  30                             16  2'External Account'
     A  30        DDEXA11       25A  O 16 19DSPATR(HI)
     A  30        ZDEXA2        35A  O 16 45DSPATR(HI)
     A  30        DDEXA12       10A  O 17 19DSPATR(HI)
     A  30        ZDEXA3        35A  O 17 45DSPATR(HI)
     A  30        ZDEXA4        35A  O 18 45DSPATR(HI)
     A  30        ZDEXA5        35A  O 19 45DSPATR(HI)
      *
     A  30                             20  2'103' DSPATR(UL)
     A  30                             20  6'Code'
     A  30        ZDICD3         4A  O 20 11DSPATR(HI)
     A  30                             20 16'Charges'
     A  30        ZDCHG3         3A  O 20 24DSPATR(HI)
     A**Start of change CGL029
     A**30******                       20 28'Account'                                         CGL029
     A  30                             20 28'A/c'
     A**End of change CGL029
     A*                                                           /*216375*/
     A**30********ZDCAC3        18A  O 20 36DSPATR(HI)
     A**Start of change CGL029
     A**30******  DDCAC3        18A  O 20 36DSPATR(HI)                                        CGL029
     A  30        DDCAC3        24A  O 20 32DSPATR(HI)
     A**End of change CGL029
     A*                                                           /*216375*/
     A**Start of change CGL029
     A**30******                       20 55'Amount'                                          CGL029
     A  30                             20 57'Amt.'
     A**End of change CGL029
     A  30        DDCAM3        17A  O 20 62DSPATR(HI)
      *
     A  30                             21  6'Value Date Offset'
     A  30        ZDMVDO         1A  O 21 24DSPATR(HI)
     A  30                             21 26'Bank Operation Code'
     A  30        ZDBOCD         4A  O 21 46DSPATR(HI)
      *
     A  30                             22  2'101' DSPATR(UL)
     A  30                             22  6'Code'
     A  30        ZDICD1         4A  O 22 11DSPATR(HI)
     A  30                             22 16'Charges'
     A  30        ZDCHG1         3A  O 22 24DSPATR(HI)
     A**Start of change CGL029
     A**30******                       22 28'Account'                                         CGL029
     A  30                             22 28'A/c'
     A**End of change CGL029
     A*                                                           /*216375*/
     A**30********ZDCAC1        18A  O 22 36DSPATR(HI)
     A**Start of change CGL029
     A**30******  DDCAC1        18A  O 22 36DSPATR(HI)                                        CGL029
     A  30        DDCAC1        24A  O 22 32DSPATR(HI)
     A**End of change CGL029
     A*                                                           /*216375*/
     A**Start of change CGL029
     A**30******                       22 55'Amount'                                          CGL029
     A**30******  DDCAM1        17A  O 22 62DSPATR(HI)                                        CGL029
     A  30                             22 57'Amt.'
     A  30        DDCAM1        17A  O 22 62DSPATR(HI)
     A**End of change CGL029
      *
     A  30                             23  2'Deferred Posting'
     A  30        DDDEFP         3A  O 23 19DSPATR(HI)
     A  30                             23 23'Deferred Posting Zero Narrative'
     A  30        DDDEFZ         3A  O 23 55DSPATR(HI)
      *
     A                                 24  2'PRESS ENTER F3=Exit  F12=Cancel'
      *                                                           /*214360*/
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A            DDACTN         1A  I  6  2
     A N65                                  DSPATR(HI UL)
     A  65                                  DSPATR(RI PC)
     A            CLCCAT         1A  O  6  4DSPATR(HI)
     A  51                                  DSPATR(RI BL)
     A            DDACCS        75A  O  6  6DSPATR(HI)
     A  50                                  DSPATR(RI)
     A            CLLINK         9S 0H
     A            CLCBRC         3A  H
      * Begin /CSD027/
     A**********  CLCCUS         6S 0H                                                        CSD027
     A            CLCCUS         6A  H
      * End /CSD027/
     A            CLCCCY         3A  H
     A**Start of change CGL029
     A**********  CLCACD         4S 0H                                                        CGL029
     A            CLCACD        10S 0H
     A**End of change CGL029
     A            CLCASN         2S 0H
     A            C_ACNO        10A  H
     A            C_ANAM        20A  H
     A            ZLSTYP         1A  H
     A*                                                           /*213910*/
     A            DDBLOCK        1A  H
     A*                                                           /*213910*/
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A  01                                  SFLDSP
     A  02                                  SFLDSPCTL
     A N01N02                               SFLCLR
     A  03                                  SFLEND
     A                                      OVRATR
     A                                      ROLLDOWN(05)
     A                                      ROLLUP(06)
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'RE100451'
     A                                  1 27'CASH MANAGEMENT HIERARCHY LINKS'
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A*                                                      /*211210*/
     A            DDOPTS        40      3  2
     A*                                                      /*211210*/
      *
     A                                  4  2'Hierarchy'
     A            CDHISN        10A  O  4 14DSPATR(HI)
     A                                      OVRDTA
     A                                  4 26'Hierarchy Type'
     A            CDHTYP         2A  O  4 42DSPATR(HI)
     A                                      OVRDTA
     A                                  4 47'Hierarchy ID'
     A            CDHIER         9S 0O  4 61DSPATR(HI)
     A                                      OVRDTA
      *
     A            DDNARR        79A     5  2 DSPATR(UL)
     A                                      OVRDTA
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
      *                                                           /*BG8539*/
     A                                      OVERLAY
      *                                                           /*BG8539*/
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit  F12=Cancel  -
     A                                      F16=A/c Id  F18=Details  F20=Names -
     A                                          Rollup/Rolldown'
     A                                      OVRATR
