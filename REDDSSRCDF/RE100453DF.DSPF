      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Cash Management A/c Movements Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE100453DF - Cash Management A/c Movements Enquiry           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BG8539             Date 15Sep05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BG8539 - Screen not display properly in WebFacing            *
      *           Add overlay to FOOTER format                        *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A                                      CF12
     A                                      CF16
     A                                      CF18
     A                                      CF20
     A                                      CF22
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MOVEMENTS                                                        *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A  23                                  SFLNXTCHG
     A            DDACTN         1A  B  7  2 DSPATR(HI)
     A N24                                   DSPATR(PR)
     A  24                                   DSPATR(UL)
     A            EPSTD          7A  O  7  4 DSPATR(HI)
     A  25                                   DSPATR(RI)
     A            EVALD          7A  O  7 12 DSPATR(HI)
     A  25                                   DSPATR(RI)
     A            EPNAR         30A  O  7 20 DSPATR(HI)
     A  25                                   DSPATR(RI)
     A            EPSTA         18A  O  7 51 DSPATR(HI)
     A  25                                   DSPATR(RI)
     A            ESPOS          5A  O  7 72 DSPATR(HI)
     A  25                                   DSPATR(RI)
     A            EXRFI          3A  O  7 78 DSPATR(HI)
     A  25                                   DSPATR(RI)
     A            EDRCR          1A  H
     A            XRFN          10S 0H
     A            SRCFIL        10A  H
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLSIZ(1500)
     A                                      SFLPAG(0015)
     A                                      BLINK
     A  11                                  SFLDSP
     A  12                                  SFLDSPCTL
     A N11N12                               SFLCLR
     A  13                                  SFLEND
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'RE100453'
     A                                  1 27'CASH MANAGEMENT ACCOUNT MOVEMENTS -
     A                                      ENQUIRY'
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  3  2'Account'
     A**Start of change CGL029
     A**********  DDACC         22A  O  3 11DSPATR(HI)                                        CGL029
     A**********  DDRAN         10A  O  3 34DSPATR(HI)                                        CGL029
     A**********  DDANAM        20A  O  3 45DSPATR(HI)                                        CGL029
     A**********  DDPV          15A  O  3 66DSPATR(HI)                                        CGL029
     A            DDACC         28A  O  3 11DSPATR(HI)
     A            DDRAN         10A  O  3 40DSPATR(HI)
     A            DDANAM        20A  O  4  3DSPATR(HI)
     A            DDPV          15A  O  4 24DSPATR(HI)
     A**End of change CGL029
     A                                      OVRDTA
      *
     A**Start of change CGL029
     A**********                        4  2'Customer'                                        CGL029
     A**********  BBCRNM        20A  O  4 11DSPATR(HI)                                        CGL029
     A**********  BBCRTN        10A  O  4 32DSPATR(HI)                                        CGL029
     A                                  4 40'Customer'
     A            BBCRNM        20A  O  4 49DSPATR(HI)
     A            BBCRTN        10A  O  4 70DSPATR(HI)
     A**End of change CGL029
      *
     A                                  5  4 'Posting'
     A                                  5 12 ' Value '
     A                                  5 20 'Narrative'
     A                                  5 51 'Posting Amount'
     A                                  5 69 'D/C'
     A                                  5 73 'SPOS'
     A                                  5 78 'Z/G'
     A            DDPSTD         6A  B  6  5
     A  51                                  DSPATR(RI)
     A  50
     AO 51                                  DSPATR(PC)
     A N51                                  DSPATR(HI)
     A N50                                  DSPATR(PR)
     A  50                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDVALD         6A  B  6 13
     A  52                                  DSPATR(RI)
     A N50
     AO 52                                  DSPATR(PC)
     A N52                                  DSPATR(HI)
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(UL)
     A                                      OVRDTA
     A            DDPNAR        30A  B  6 20DSPATR(HI UL)
     A                                      OVRDTA
     A            DDPSTA        18A  B  6 51
     A N53                                  DSPATR(HI UL)
     A  53                                  DSPATR(RI PC)
     A                                      OVRDTA
     A            DDDRCR         1A  B  6 70
     A N54                                  DSPATR(HI UL)
     A  54                                  DSPATR(RI PC)
     A                                      OVRDTA
     A            DDSPOS         5A  B  6 72DSPATR(HI UL)
     A                                      OVRDTA
     A            DDXRFI         3A  B  6 78DSPATR(HI UL)
     A                                      OVRDTA
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
      *                                                           /*BG8539*/
     A                                      OVERLAY
      *                                                           /*BG8539*/
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit F12=Cancel F16=Narr F18=+
     A                                      Posting/Value F20=Int.Statement F22+
     A                                      =Out Msgs'
