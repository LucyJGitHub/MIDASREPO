      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Cash Management Interest StatementEnquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE100454DF - Cash Management Interest Statement Enquiry      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      *  Last Amend No. MD046682           Date 24Jul17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BG8539             Date 15Sep05               *
      *  Prev Amend No. CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046682 - Display 'CASH MANAGEMENT' only if CRE008 is on.   *
      *  BG8539 - Screen not display properly in WebFacing            *
      *           Add overlay to FOOTER format                        *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A                                      CF12
     A                                      CF18
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MOVEMENTS                                                        *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A            EDRCRIN        1A  O  7  2
     A                                       DSPATR(HI)
     A            ESDAT          7A  O  7  4
     A                                       DSPATR(HI)
     A            EEDAT          7A  O  7 12
     A                                       DSPATR(HI)
     A            EACBAL        20A  O  7 20
     A                                       DSPATR(HI)
     A            ENODYS         7A  O  7 41
     A                                       DSPATR(HI)
     A            EINTRT        13A  O  7 49
     A                                       DSPATR(HI)
     A            EINTAC        17A  O  7 63
     A                                       DSPATR(HI)
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLSIZ(1500)
     A                                      SFLPAG(0015)
     A                                      BLINK
     A  11                                  SFLDSP
     A  12                                  SFLDSPCTL
     A N11N12                               SFLCLR
     A  13                                  SFLEND
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'RE100454'
      ** Start of change MD046682
     A**********                        1 27'CASH MANAGEMENT INTEREST STATEMENT-            MD046682
     A**********                             ENQUIRY'                                       MD046682
     A  20                              1 27'CASH MANAGEMENT INTEREST STATEMENT-
     A                                       ENQUIRY'
     A N20                              1 33'INTEREST STATEMENT ENQUIRY'
      ** End of change MD046682
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  3  2'Account'
      ** Start of change MD046682
     A**********  DDACC         22A  O  3 11DSPATR(HI)                                      MD046682
     A            DDACC         28A  O  3 11DSPATR(HI)
     A**********  DDRAN         10A  O  3 34DSPATR(HI)                                      MD046682
     A            DDRAN         10A  O  3 40DSPATR(HI)
     A**********  DDANAM        20A  O  3 45DSPATR(HI)                                      MD046682
     A            DDANAM        20A  O  3 51DSPATR(HI)
      ** End of change MD046682
      *
     A                                  4  2'Customer'
     A            BBCRNM        20A  O  4 11DSPATR(HI)
     A            BBCRTN        10A  O  4 32DSPATR(HI)
      *
     A                                  5  1 'D/C'
     A            DDDRCR         1A  B  6  2DSPATR(HI UL)
     A                                  6  4 ' Start '
     A                                  6 12 '  End  '
     A                                  6 20 '     Balance '
     A                                  6 42 'Days'
     A                                  6 55 ' Rate'
     A                                  6 63 '     Interest'
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
      *                                                           /*BG8539*/
     A                                      OVERLAY
      *                                                           /*BG8539*/
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit  F12=Cancel'
