      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Cash Management Posting Details Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE100455DF - Cash Management Posting Details Enquiry         *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A                                      CF12
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   GROUP A/C EXTENSION                                              *
      * -------------------------------------------------------------------*
      *
     A          R GROUPAC
     A  20                                  CF16
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'RE100455'
     A                                  1 27'CASH MANAGEMENT POSTING DETAILS -
     A                                      ENQUIRY'
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  3  2'Account'
     A**Start of change CGL029
     A**********  DDACC         22A  O  3 11DSPATR(HI)                                        CGL029
     A**********  DDRAN         10A  O  3 34DSPATR(HI)                                        CGL029
     A**********  DDANAM        20A  O  3 45DSPATR(HI)                                        CGL029
     A            DDACC         28A  O  3 11DSPATR(HI)
     A            DDRAN         10A  O  3 40DSPATR(HI)
     A            DDANAM        20A  O  3 51DSPATR(HI)
     A**End of change CGL029
      *
     A                                  4  2'Customer'
     A            DDCRNM        20A  O  4 11DSPATR(HI)
     A            DDCRTN        10A  O  4 32DSPATR(HI)
      *
     A                                  6  2'Posting Date'
     A            DDPSTD         7A  O  6 21DSPATR(HI)
      *
     A                                  7  2'Value Date'
     A            DDVALD         7A  O  7 21DSPATR(HI)
      *
     A                                  8  2'Narrative'
     A            DDPNAR        30A  O  8 21DSPATR(HI)
      *
     A                                  9  2'Amount'
     A            DDPSTA        18A  O  9 21DSPATR(HI)
      *
     A                                 10  2'Source Account'
     A**Start of change CGL029
     A**********  DDSACC        22A  O 10 21DSPATR(HI)                                        CGL029
     A**********  DDSRAN        10A  O 10 44DSPATR(HI)                                        CGL029
     A            DDSACC        28A  O 10 21DSPATR(HI)
     A            DDSRAN        10A  O 10 50DSPATR(HI)
     A**End of change CGL029
      *
     A                                 11  2'When Generated'
     A            DDGDATG        7A  O 11 21DSPATR(HI)
     A            DDGMODEN      35A  O 11 29DSPATR(HI)
      *
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit  F12=Cancel'
     A  20                             24 23'F16=Other Details'
     A                                      DSPATR(HI)
      *
      * -------------------------------------------------------------------*
      *   ZERO BALANCING/SWEEPING EXTENSION                                *
      * -------------------------------------------------------------------*
      *
     A          R ZEROBLSWEP
     A  20                                  CF16
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'RE100455'
     A                                  1 27'CASH MANAGEMENT POSTING DETAILS -
     A                                      ENQUIRY'
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  3  2'Account'
     A**Start of change CGL029
     A**********  DDACC         22A  O  3 11DSPATR(HI)                                        CGL029
     A**********  DDRAN         10A  O  3 34DSPATR(HI)                                        CGL029
     A**********  DDANAM        20A  O  3 45DSPATR(HI)                                        CGL029
     A            DDACC         28A  O  3 11DSPATR(HI)
     A            DDRAN         10A  O  3 40DSPATR(HI)
     A            DDANAM        20A  O  3 51DSPATR(HI)
     A**End of change CGL029
      *
     A                                  4  2'Customer'
     A            DDCRNM        20A  O  4 11DSPATR(HI)
     A            DDCRTN        10A  O  4 32DSPATR(HI)
      *
     A                                  6  2'Posting Date'
     A            DDPSTD         7A  O  6 21DSPATR(HI)
      *
     A                                  7  2'Value Date'
     A            DDVALD         7A  O  7 21DSPATR(HI)
      *
     A                                  8  2'Narrative'
     A            DDPNAR        30A  O  8 21DSPATR(HI)
      *
     A                                  9  2'Amount'
     A            DDPSTA        18A  O  9 21DSPATR(HI)
      *
     A                                 10  2'Description'
     A            DDTEXT        50A  O 10 21DSPATR(HI)
      *
     A                                 11  2'Target Balance'
     A            DDTBAL         3A  O 11 21DSPATR(HI)
      *
     A                                 12  2'Association Date'
     A            DDCBSA         3A  O 12 21DSPATR(HI)
      *
     A                                 13  2'MT94X Field 61 - 6'
     A            AZF616         4A  O 13 21DSPATR(HI)
      *
     A                                 14  2'               - 7'
     A            AZF617        16A  O 14 21DSPATR(HI)
      *
     A                                 15  2'               - 8'
     A            AZF618        18A  O 15 21DSPATR(HI)
      *
     A                                 16  2'               - 9'
     A            AZF619        35A  O 16 21DSPATR(HI)
      *
     A                                 17  2'MT94X Field 86 - 1'
     A            DDF861        60A  O 17 21DSPATR(HI)
      *
     A                                 18  2'               - 2'
     A            DDF862        60A  O 18 21DSPATR(HI)
      *
     A                                 19  2'               - 3'
     A            DDF863        60A  O 19 21DSPATR(HI)
      *
     A                                 20  2'               - 4'
     A            DDF864        60A  O 20 21DSPATR(HI)
      *
     A                                 21  2'               - 5'
     A            DDF865        60A  O 21 21DSPATR(HI)
      *
     A                                 22  2'When Generated'
     A            DDZDATG        7A  O 22 21DSPATR(HI)
     A            DDZMODEN      35A  O 22 29DSPATR(HI)
      *
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit  F12=Cancel'
     A  20                             24 23'F16=Other Details'
     A                                      DSPATR(HI)
