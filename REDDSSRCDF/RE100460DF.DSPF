      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Cash Management Hierarchy Selection Prmpt')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE100460DF - Cash Management Hierarchy Selection Prompt      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BG8539             Date 15Sep05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 213195             Date 10Jan03               *
      *                 212840             Date 10Jan03               *
      *                 211210             Date 11Dec02               *
      *                 CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BG8539 - Screen not display properly in WebFacing            *
      *           Add overlay to FOOTER1 and FOOTER2 formats          *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  213195 - Wrong hierarchy description on purge & sweep screens*
      *  212840 - Missing info on link id                             *
      *  211210 - Various action code errors                          *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A                                      CF12
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   PROMPT                                                           *
      * -------------------------------------------------------------------*
      *
     A          R PROMPT
     A                                      OVERLAY
     A                                      BLINK
      *
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A            DDUSR     R        O  1  2REFFLD(USER)
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'RE100460'
     A                                  1 27'CASH MANAGEMENT HIERARCHY SELECTIO-
     A                                      N'
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
      *
     A                                  6  2'Show hierarchies containing'
     A                                  6 30'ONE of the following:'
      *
     A                                  8  2'Link ID'
     A            DDLINK         9A  B  8 17
     A N51                                  DSPATR(HI UL)
     A  51                                  DSPATR(RI PC)
      *
     A                                  9  2'or'
      *
     A                                 10  2'Retail Account'
     A            DDACNO        10A  B 10 17
     A N52                                  DSPATR(HI UL)
     A  52                                  DSPATR(RI PC)
      *
     A                                 11  2'or'
      *
     A                                 12  2'Account ID'
     A            DDBRCA         3A  B 12 17
     A N53                                  DSPATR(HI UL)
     A  53                                  DSPATR(RI PC)
     A            DDCNUM         6A  B 12 21
     A N54                                  DSPATR(HI UL)
     A  54                                  DSPATR(RI PC)
     A            DDCCY          3A  B 12 28
     A N55                                  DSPATR(HI UL)
     A  55                                  DSPATR(RI PC)
     A**Start of change CGL029
     A**********  DDACOD         4A  B 12 32                                                  CGL029
     A            DDACOD        10A  B 12 32
     A**End of change CGL029
     A N56                                  DSPATR(HI UL)
     A  56                                  DSPATR(RI PC)
     A**Start of change CGL029
     A**********  DDACSQ         2A  B 12 37                                                  CGL029
     A            DDACSQ         2A  B 12 43
     A**End of change CGL029
     A N57                                  DSPATR(HI UL)
     A  57                                  DSPATR(RI PC)
      *
     A                                 13  2'or'
      *
     A                                 14  2'Customer'
     A            DDCUST        10A  B 14 17
     A N58                                  DSPATR(HI UL)
     A  58                                  DSPATR(RI PC)
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A            DDACTN         1A  I  8  2
     A N65                                  DSPATR(HI UL)
     A  65                                  DSPATR(RI PC)
     A            CDHISN        10A  O  8  4DSPATR(HI)
     A            DDDESC        49A  O  8 15DSPATR(HI)
     A            CDHTYP         2A  O  8 65DSPATR(HI)
     A            DDEXTL         1A  O  8 68DSPATR(HI)
     A            DDMAST         1A  O  8 70DSPATR(HI)
     A            CLLINK         9S 0O  8 72DSPATR(HI)
     A            CDHIER         9S 0H
     A            CDBRCA         3A  H
     A*                                                      /*213195*/
     A            CDDESC        50A  H
     A*                                                      /*213195*/
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLSIZ(1300)
     A                                      SFLPAG(0013)
     A                                      BLINK
     A  11                                  SFLDSP
     A  12                                  SFLDSPCTL
     A N11N12                               SFLCLR
     A*                                                      /*212840*/
     A**13                                  SFLEND
     A*                                                      /*212840*/
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'RE100460'
     A                                  1 27'CASH MANAGEMENT HIERARCHY SELECTIO-
     A                                      N'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A*                                                      /*211210*/
     A            DDOPTS        40      4  2OVRDTA
     A*                                                      /*211210*/
     A                                  5 68'E'
     A                                  5 70'M'
     A                                  6  4'Shortname'
     A                                  6 15'Description'
     A                                  6 65'Ty'
     A                                  6 68'X'
     A                                  6 70'A'
     A                                  6 72'Link'
     A                                  7  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL)
      *
      * -------------------------------------------------------------------*
      *   FOOTER 1                                                         *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER1
      *                                                           /*BG8539*/
     A                                      OVERLAY
      *                                                           /*BG8539*/
     A                                      PUTOVR
      *
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit  F12=Cancel'
      *
      * -------------------------------------------------------------------*
      *   FOOTER 2                                                         *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER2
      *                                                           /*BG8539*/
     A                                      OVERLAY
      *                                                           /*BG8539*/
     A                                      PUTOVR
      *
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit  F12=Cancel'
