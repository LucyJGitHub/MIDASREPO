      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Cash Management Balance Purge Request')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE100462DF - Cash Management Balance Purge Request           *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BG8539             Date 15Sep05               *
      *                 212904             Date 14Jan03               *
      *                 CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BG8539 - Screen not display properly in WebFacing            *
      *           Add overlay to FOOTER and CONFIRM formats           *
      *  212904 - Balance purge errors                                *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *                                                               *
      *****************************************************************
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A                                      CF12
      *
      * -------------------------------------------------------------------*
      * * MESSAGE SUBFILE FORMAT                                           *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBF                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      SFL
     A                                      SFLMSGRCD(22)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   MESSAGE SUBFILE CONTROL                                          *
      * -------------------------------------------------------------------*
      *
     A          R MSGSUBCT                  TEXT('MSG SUBFILE CONTRL')
     A                                      SFLCTL(MSGSUBF)
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A                                      SFLDSP
     A  40                                  SFLEND
     A                                      SFLSIZ(5)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A            DDPGMQ                    SFLPGMQ
      *
      * -------------------------------------------------------------------*
      *   HIERARCHY                                                        *
      * -------------------------------------------------------------------*
      *
     A          R HIERARCHY
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A            DDUSR     R        O  1  2REFFLD(USER)
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'RE100462'
     A                                  1 27'CASH MANAGEMENT BALANCE PURGE'
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
      *
     A                                  4  2'Hierarchy'
     A            I_HISN        10A  O  4 14DSPATR(HI)
      *
     A                                  5  2'Description'
     A            I_DESC        50A  O  5 14DSPATR(HI)
      *
     A                                  8  2'Maximum Debit To Purge:'
     A            DDMAXD        17A  B  8 27
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51
     AO 70                                  DSPATR(PC)
     A N70N51                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 46'(Blank = no maximum)'
      *
     A                                  9  2'Maximum Credit To Purge:'
     A            DDMAXC        17A  B  9 27
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI PC)
     A N70N52                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 46'(Blank = no maximum)'
      *
     A*                                                           /*212904*/
     A*********************************10**2'Net or Gross:'
     A************DDZBNG*********1A**B*10*27
     A*N53**********************************DSPATR(HI)
     A**53**********************************DSPATR(RI PC)
     A*N70N53*******************************DSPATR(UL)
     A**70**********************************DSPATR(PR)
     A**************************************OVRDTA
     A*                                                           /*212904*/
      *
     A                                 12  2'Input Y or N to confirm:'
     A  70                                  DSPATR(ND)
     A                                      OVRATR
     A            DDCONFIRM      1A  B 12 27
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI PC)
     A N70N54                               DSPATR(UL)
     A  70                                  DSPATR(PR ND)
     A                                      OVRDTA
      *
      * -------------------------------------------------------------------*
      *   HIERARCHY LINK                                                   *
      * -------------------------------------------------------------------*
      *
     A          R HIERLINK
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
      *
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 14'RE100462'
     A                                  1 27'CASH MANAGEMENT BALANCE PURGE'
     A                                      DSPATR(HI UL)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
      *
     A                                  4  2'Hierarchy'
     A            I_HISN        10A  O  4 12 DSPATR(HI)
     A            I_DESC        50A  O  4 24 DSPATR(HI)
      *
     A                                  5  2'Link ID'
     A            I_LINK         9S 0O  5 12 DSPATR(HI)
      *
     A                                  6  2'Account'
     A            DDACC         22A  O  6 12 DSPATR(HI)
     A            DDRAN         10A  O  6 35 DSPATR(HI)
     A            DDANAM        20A  O  6 46 DSPATR(HI)
      *
     A                                  8  2'Maximum Debit To Purge:'
     A            DDMAXD        17A  B  8 27
     A N51                                  DSPATR(HI)
     A  51                                  DSPATR(RI)
     A  51
     AO 70                                  DSPATR(PC)
     A N70N51                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  8 46'(Blank = no maximum)'
      *
     A                                  9  2'Maximum Credit To Purge:'
     A            DDMAXC        17A  B  9 27
     A N52                                  DSPATR(HI)
     A  52                                  DSPATR(RI PC)
     A N70N52                               DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      OVRDTA
     A                                  9 46'(Blank = no maximum)'
      *
     A*                                                           /*212904*/
     A*********************************10**2'Net or Gross:'
     A************DDZBNG*********1A**B*10*27
     A*N53**********************************DSPATR(HI)
     A**53**********************************DSPATR(RI PC)
     A*N70N53*******************************DSPATR(UL)
     A**70**********************************DSPATR(PR)
     A**************************************OVRDTA
     A*                                                           /*212904*/
      *
     A                                 12  2'Input Y or N to confirm:'
     A  70                                  DSPATR(ND)
     A                                      OVRATR
     A            DDCONFIRM      1A  B 12 27
     A N54                                  DSPATR(HI)
     A  54                                  DSPATR(RI PC)
     A N70N54                               DSPATR(UL)
     A  70                                  DSPATR(PR ND)
     A                                      OVRDTA
      *
      * -------------------------------------------------------------------*
      *   CONFIRMATION                                                     *
      * -------------------------------------------------------------------*
      *
     A          R CONFIRM
      *                                                           /*BG8539*/
     A                                      OVERLAY
      *                                                           /*BG8539*/
     A                                      PUTOVR
     A                                 14  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 16 12'Balance purging has been scheduled-
     A                                       for execution.'
     A                                 18 12'Press enter to return to the -
     A                                      previous screen'
     A                                 24  2'                                  -
     A                                                                         -
     A                                             '
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
      *                                                           /*BG8539*/
     A                                      OVERLAY
      *                                                           /*BG8539*/
     A                                      PUTOVR
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit  F12=Cancel               -
     A                                         '
     A                                      OVRATR
