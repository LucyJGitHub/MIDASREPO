      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Cash Management I/C Sweeping Monitor')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE100480DF - Cash Management Input Cycle Sweeping Monitor    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BG8539             Date 15Sep05               *
      *  Prev Amend No. CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BG8539 - Screen not display properly in WebFacing            *
      *           Add overlay to FOOTER format                        *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
      *                                                               *
     A                                      CHGINPDFT
     A                                      PRINT(MPRINT)
     A                                      REF(MFRFL)
     A                                      MSGLOC(22)
     A                                      CF03
     A                                      CF05
     A                                      CF12
      *
      * -------------------------------------------------------------------*
      * * SUBFILE RECORD                                                   *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLREC                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A            DDACTN         1   B  6  2DSPATR(HI)
     A  50                                  DSPATR(UL)
     A N50                                  DSPATR(PR)
     A            ILTIME         6  0O  6  4EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A            DDNARR        66   O  6 13DSPATR(HI)
     A            ILJBNM        10   H
     A            ILJBUS        10   H
     A            DDJBNO         6   H
      *
      * -------------------------------------------------------------------*
      *   SUBFILE CONTROL                                                  *
      * -------------------------------------------------------------------*
      *
     A          R SUBFLCTL                  SFLCTL(SUBFLREC)
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      SFLSIZ(1700)
     A                                      SFLPAG(0017)
     A                                      BLINK
     A  11                                  SFLDSP
     A  12                                  SFLDSPCTL
     A N11N12                               SFLCLR
     A  13                                  SFLEND
     A            DDUSR     R        O  1  2REFFLD(USER)
     A                                      OVRDTA
     A            DDPGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'RE100480'
     A                                  1 25'CASH MANAGEMENT INPUT CYCLE +
     A                                      SWEEPING MONITOR'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            BJMRDT    R        O  1 72REFFLD(RUNA)
     A                                      OVRDTA
     A            DDJOB     R        O  2  2REFFLD(JOJOB)
     A                                      OVRDTA
     A            DDTIME         6  0   2 71EDTWRD('  :  :  ')
     A                                      OVRDTA
     A                                  4  5'Time'
     A                                  4 13'Event'
     A                                  5  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
      *
      * -------------------------------------------------------------------*
      *   FOOTER                                                           *
      * -------------------------------------------------------------------*
      *
     A          R FOOTER
      *                                                           /*BG8539*/
     A                                      OVERLAY
      *                                                           /*BG8539*/
     A                                      PUTOVR
      *
     A                                 23  2'                                  -
     A                                                                         -
     A                                             '
     A                                      DSPATR(UL)
     A                                 24  2'F3=Exit  F5=Refresh  Rollup  Rolld-
     A                                      own             '
