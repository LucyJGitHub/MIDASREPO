      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Zero Balancing Stops Mnt Display File')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  DSPF/RE101102DF - Zero Balancing Stops Maintenance Display   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
 
     A                                      REF(MFRFL)
     A                                      CA03(03)
     A**Start of change CGL029
     A**********                            DSPSIZ(24 80 *DS3)                                CGL029
     A                                      DSPSIZ(*DS4)
     A**End of change CGL029
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
 
      ** Header
 
     A          R RE101102F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A            #0USER    R        O  1  2REFFLD(USER)
     A  26                              1 13'RE101102S0'
     A N26                              1 13'RE101102S1'
     A                                  1 28'Zero Balancing Stops Maintenance'
     A                                      DSPATR(UL HI)
     A                                  1 66'DATE'
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 66'TIME'
     A            #0TIME    R        O  2 72REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
 
      ** Subfile Record Format (Screen 1)
 
     A          R RE101102S0
     A                                      SFL
     A  84                                  SFLNXTCHG
     A            #0PRCD         5  0H      TEXT('Processing Date')
     A            #0HIER         9  0H      TEXT('Hierarchy ID')
     A            #0ACTN         1   B  7  5TEXT('Action Code')
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI UL)
     A            #0PRC1         7   O  7 12TEXT('Processing Date')
     A                                      DSPATR(HI)
     A            #0HISN        10   O  7 26TEXT('Hierarchy Shortname')
     A                                      DSPATR(HI)
     A            #0LINK         9   O  7 45TEXT('Link ID')
     A                                      DSPATR(HI)
     A            #0STPN         2   O  7 60TEXT('Stop Number')
     A                                      DSPATR(HI)
     A            #0STAT         8   O  7 70TEXT('Status')
     A                                      DSPATR(HI)
 
      ** Subfile Control (Screen 1)
 
     A          R RE101102C0
     A                                      SFLCTL(RE101102S0)
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      OVERLAY
     A  80                                  SFLCLR
     A  81                                  SFLDSP
     A                                      SFLDSPCTL
     A N80 81 82                            SFLEND
     A N82                                  ROLLUP(31)
     A                                      CA05(05)
 
     A            #0RLRN         4  0H      SFLRCDNBR
     A                                  5 14'Date'
     A                                  5 22'Hierarchy Shortname'
     A                                  5 46'Link ID'
     A                                  5 56'Stop Number'
     A                                  5 71'Status'
 
      ** Subfile Record Format (Screen 2)
 
     A          R RE101102S1
     A                                      SFL
     A            #1VALD         7   O 13  2TEXT('Value Date')
     A            #1PNAR        24   O 13 10TEXT('Narrative')
     A            #1PSTA        17   O 13 35TEXT('Amount')
     A            #1DRCR         2   O 13 53TEXT('DR/CR')
     A**Start of change CGL029
     A**********  #1ACCT        22   O 13 56TEXT('Account')                                   CGL029
     A            #1ACCT        28   O 13 56TEXT('Account')
     A**End of change CGL029
 
      ** Subfile Control (Screen 2)
 
     A          R RE101102C1
     A                                      SFLCTL(RE101102S1)
     A**Start of change CGL029
     A**********                            SFLPAG(4)  SFLSIZ(50)                             CGL029
     A                                      SFLPAG(4)  SFLSIZ(50)
     A**End of change CGL029
     A                                      OVERLAY
     A  51                                  SFLDSP
     A                                      SFLDSPCTL
     A  52                                  SFLCLR
     A  51                                  SFLEND
     A            #1RLRN         4  0H
     A                                  4  2'Hierarchy'
     A            #1HISN        10   O  4 17DSPATR(HI)
     A            #1DESC        50   O  4 29DSPATR(HI)
     A                                  5  2'Child Account'
     A**Start of change CGL029
     A**********  #1CHAC        22   O  5 17DSPATR(HI)                                        CGL029
     A**********  #1CHRE        10   O  5 40DSPATR(HI)                                        CGL029
     A**********                        5 55'Link ID'                                         CGL029
     A**********  #1LINK         9   O  5 64DSPATR(HI)                                        CGL029
     A            #1CHAC        28   O  5 17DSPATR(HI)
     A            #1CHRE        10   O  5 46DSPATR(HI)
     A                                  5 61'Link ID'
     A            #1LINK         9   O  5 71DSPATR(HI)
     A**End of change CGL029
     A                                  6  2'Parent Account'
     A**Start of change CGL029
     A**********  #1PAAC        22   O  6 17DSPATR(HI)                                        CGL029
     A**********  #1PARE        10   O  6 40DSPATR(HI)                                        CGL029
     A            #1PAAC        28   O  6 17DSPATR(HI)
     A            #1PARE        10   O  6 46DSPATR(HI)
     A**End of change CGL029
     A                                  7  2'Processing Date'
     A            #1PRCD         7   O  7 18DSPATR(HI)
     A                                  7 28'Stop Number'
     A            #1STPN         2   O  7 41DSPATR(HI)
     A                                  7 50'Released'
     A            #1RELD         3   O  7 60DSPATR(HI)
     A                                  7 65'Deleted'
     A            #1DELD         3   O  7 74DSPATR(HI)
     A                                  9  2'Maximum DR Value in a Day'
     A            #1MDVD        17   O  9 28DSPATR(HI)
     A                                  9 51'Maximum DR Count in a Day'
     A            #1MDCD         3   O  9 77DSPATR(HI)
     A                                 10  2'Total DR Value on'
     A            #1PRC1         7   O 10 20
     A            #1TVDR        17   O 10 28DSPATR(HI)
     A                                 10 51'Total DR Count on'
     A            #1PRC2         7   O 10 69
     A            #1TCDR         3   O 10 77DSPATR(HI)
     A                                 11  2'STOPPED ENTRIES'
     A                                      DSPATR(HI)
     A                                 12  2'Value'
     A                                      DSPATR(UL)
     A                                 12 10'Narrative'
     A                                      DSPATR(UL)
     A                                 12 46'Amount'
     A                                      DSPATR(UL)
     A                                 12 56'Account'
     A                                      DSPATR(UL)
 
      ** Subfile Record Format (Screen 2)
 
     A          R RE101102S2
     A                                      SFL
     A            #2VALD         7   O 19  2TEXT('Value Date')
     A            #2PNAR        24   O 19 10TEXT('Narrative')
     A            #2PSTA        17   O 19 35TEXT('Amount')
     A            #2DRCR         2   O 19 53TEXT('DR/CR')
     A**Start of change CGL029
     A            #2ACCT        22   O 19 56TEXT('Account')                                   CGL029
     A**********  #2ACCT        28   O 19 56TEXT('Account')
     A**End of change CGL029
 
      ** Subfile Control (Screen 2)
 
     A          R RE101102C2
     A                                      SFLCTL(RE101102S2)
     A                                      SFLPAG(4)  SFLSIZ(50)
     A                                      OVERLAY
     A  53                                  SFLDSP
     A                                      SFLDSPCTL
     A  54                                  SFLCLR
     A  53                                  SFLEND
     A N27 28                               CA10(10)
     A                                      CA12(12)
     A N27 29                               CA16(16)
     A            #2RLRN         4  0H
     A                                 17  2'ORIGINATING ENTRIES'
     A                                      DSPATR(HI)
     A                                 18  2'Value'
     A                                      DSPATR(UL)
     A                                 18 10'Narrative'
     A                                      DSPATR(UL)
     A                                 18 46'Amount'
     A                                      DSPATR(UL)
     A                                 18 56'Account'
     A                                      DSPATR(UL)
 
      ** Command Line
 
     A          R RE101102F2                TEXT('Command keys')
     A                                      OVERLAY
     A  26                             23  2'E=Enquire   D=Delete   R=Release -
     A                                        F3=Main Menu   F5=Refresh'
     A  27                             23  2'F3=Main Menu   F12=Cancel'
     A  28                             23  2'F3=Main Menu   F10=Delete   +
     A                                      F12=Cancel'
     A  29                             23  2'F3=Main Menu   F12=Cancel   +
     A                                      F16=Release'
 
      ** Message Record Format
 
     A          R RE101102S3                TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
 
      ** Message Control
 
     A          R RE101102C3                TEXT('Program Messages')
     A                                      SFLCTL(RE101102S3)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
 
