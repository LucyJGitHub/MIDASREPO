      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE MT101 & MT103 Translation MNT per Dest DF')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  DSPF/RE101104DF - MT101 & MT103 Translation Maintenance per  *
      *                    Destination                                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 211210             Date 11Dec02               *
      *  Prev Amend No. CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  211210 - Various action code errors                          *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
 
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
 
      ** Header
 
     A          R RE101104F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'RE101104'
     A                                  1 66'DATE'
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 25'MT101/MT103 Translation +
     A                                      Destination'
     A                                      DSPATR(UL HI)
     A                                  2 66'TIME'
     A            #0TIME    R        O  2 72REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
 
      ** Subfile Record Format (Screen 1)
 
     A          R RE101104S0
     A                                      SFL
     A  84                                  SFLNXTCHG
     A            #0ACTN         1   B  7  5TEXT('Action Code')
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI UL)
     A            #0DEST        11   O  7 15TEXT('Destination')
     A                                      DSPATR(HI)
     A            #0DESC        30   O  7 34TEXT('Description')
     A                                      DSPATR(HI)
 
      ** Subfile Control (Screen 1)
 
     A          R RE101104C0
     A                                      SFLCTL(RE101104S0)
     A                                      SFLPAG(14) SFLSIZ(15)
     A                                      OVERLAY
     A  80                                  SFLCLR
     A N80 81                               SFLDSP
     A N80                                  SFLDSPCTL
     A N80 81 82                            SFLEND
     A N82                                  ROLLUP(31)
      *                                                      /*211210*/
     A**************************************CF09
     A N15                                  CF09
      *                                                      /*211210*/
 
     A            #0RLRN         4S 0H      SFLRCDNBR
     A  81                              5 15'Destination'
     A  81                              5 34'Description'
 
      ** Subfile Record Format (Screen 2)
 
     A          R RE101104S1
     A                                      SFL
     A  84                                  SFLNXTCHG
     A            #1ACTN         1A  B 11  2TEXT('Action Code')
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI UL)
     A  20                                  DSPATR(PR ND)
     A            #1RULE         2A  O 11  5TEXT('Rule Number')
     A  20                                  DSPATR(ND)
     A            #1MT3T         5A  O 11 10TEXT('MT103 Message Tag')
     A  20                                  DSPATR(ND)
     A            #1OPER         2A  O 11 18TEXT('Operator')
     A  20                                  DSPATR(ND)
     A            #1MT1T         5A  O 11 23TEXT('MT101 Message Tag')
     A            #1MTHD         1A  O 11 33TEXT('MT101 Message Tag')
     A            #1FLDC        35A  O 11 40TEXT('Field Component')
 
      ** Subfile Control (Screen 2)
 
     A          R RE101104C1
     A                                      SFLCTL(RE101104S1)
     A                                      SFLPAG(10) SFLSIZ(11)
     A                                      OVERLAY
     A  85                                  SFLCLR
     A N85 86                               SFLDSP
     A N85                                  SFLDSPCTL
     A N85 86 87                            SFLEND
     A N87                                  ROLLUP(31)
     A                                      CF05
     A*                                                      /*211210*/
     A**************************************CF09
     A N15                                  CF09
     A*                                                      /*211210*/
     A                                      CF12
     A            #1RLRN         4S 0H      SFLRCDNBR
     A  86                              5  2'Destination:'
     A                                      DSPATR(HI)
     A  86        #0DEST        11   O  5 15TEXT('Destination')
     A                                      DSPATR(HI)
     A  86        #0DESC        30   O  5 34TEXT('Description')
     A                                      DSPATR(HI)
 
     A  86                              8  2'?'
     A  86                              8  4'Rule'
     A  86                              8 10'MT103'
     A  86                              8 23'MT101'
     A  86                              8 31'Header?'
     A  86                              8 40'Field Component'
     A  86                              9  5'No.'
     A  86                              9 11'Tag'
     A  86                              9 24'Tag'
 
      ** Screen 3
 
     A          R RE101104D0                TEXT('Detail Line')
     A                                      OVERLAY
     A                                      CF05
     A  24                                  CF10
     A                                      CF12
     A                                  5  2'Destination:'
     A                                      DSPATR(HI)
     A            #0DEST        11A  O  5 15TEXT('Destination')
     A                                      DSPATR(HI)
     A            #0DESC        30A  B  5 34TEXT('Description')
     A                                      DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  47                                  DSPATR(RI PC)
     A N26N47                               DSPATR(UL)
     A                                  8  2'Rule Number...........:'
     A            #1RULE         2A  B  8 27TEXT('Rule Number')
     A N26                                  DSPATR(HI)
     A  51
     AO 26                                  DSPATR(PR)
     A  32                                  DSPATR(RI PC)
     A N26N32N51                            DSPATR(UL)
     A                                 10  2'MT103 Tag.............:'
     A            #1MT3T         5A  B 10 27TEXT('MT103 Tag')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  33                                  DSPATR(RI PC)
     A N26N33                               DSPATR(UL)
     A                                 12  2'Map from MT101:'
     A                                 12 21'Tag'
     A                                      DSPATR(UL)
     A                                 12 29'Header?'
     A                                      DSPATR(UL)
     A                                 12 38'Field Component'
     A                                      DSPATR(UL)
     A                                 13  2'       and  1 :'
     A            #0MTG1         5A  B 13 21TEXT('MT101 Tag 1')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  34                                  DSPATR(RI PC)
     A N26N34                               DSPATR(UL)
     A            #0HDR1         1A  B 13 31TEXT('Header Flag 1')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  61                                  DSPATR(RI PC)
     A N26N61                               DSPATR(UL)
     A            #0FLD1        35A  B 13 38TEXT('Field Component 1')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  35                                  DSPATR(RI PC)
     A N26N35                               DSPATR(UL)
     A                                 14  2'       and  2 :'
     A            #0MTG2         5A  B 14 21TEXT('MT101 Tag 2')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  36                                  DSPATR(RI PC)
     A N26N36                               DSPATR(UL)
     A            #0HDR2         1A  B 14 31TEXT('Header Flag 2')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  62                                  DSPATR(RI PC)
     A N26N62                               DSPATR(UL)
     A            #0FLD2        35A  B 14 38TEXT('Field Component 2')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  37                                  DSPATR(RI PC)
     A N26N37                               DSPATR(UL)
     A                                 15  2'       and  3 :'
     A            #0MTG3         5A  B 15 21TEXT('MT101 Tag 3')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  38                                  DSPATR(RI PC)
     A N26N38                               DSPATR(UL)
     A            #0HDR3         1A  B 15 31TEXT('Header Flag 3')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  63                                  DSPATR(RI PC)
     A N26N63                               DSPATR(UL)
     A            #0FLD3        35A  B 15 38TEXT('Field Component 3')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  39                                  DSPATR(RI PC)
     A N26N39                               DSPATR(UL)
     A                                 16  2'       and  4 :'
     A            #0MTG4         5A  B 16 21TEXT('MT101 Tag 4')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  40                                  DSPATR(RI PC)
     A N26N40                               DSPATR(UL)
     A            #0HDR4         1A  B 16 31TEXT('Header Flag 4')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  64                                  DSPATR(RI PC)
     A N26N64                               DSPATR(UL)
     A            #0FLD4        35A  B 16 38TEXT('Field Component 4')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  41                                  DSPATR(RI PC)
     A N26N41                               DSPATR(UL)
     A                                 17  2'       and  5 :'
     A            #0MTG5         5A  B 17 21TEXT('MT101 Tag 5')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  42                                  DSPATR(RI PC)
     A N26N42                               DSPATR(UL)
     A            #0HDR5         1A  B 17 31TEXT('Header Flag 5')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  65                                  DSPATR(RI PC)
     A N26N65                               DSPATR(UL)
     A            #0FLD5        35A  B 17 38TEXT('Field Component 5')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  43                                  DSPATR(RI PC)
     A N26N43                               DSPATR(UL)
     A                                 18  2'       and  6 :'
     A            #0MTG6         5A  B 18 21TEXT('MT101 Tag 6')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  44                                  DSPATR(RI PC)
     A N26N44                               DSPATR(UL)
     A            #0HDR6         1A  B 18 31TEXT('Header Flag 6')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  66                                  DSPATR(RI PC)
     A N26N66                               DSPATR(UL)
     A            #0FLD6        35A  B 18 38TEXT('Field Component 6')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  45                                  DSPATR(RI PC)
     A N26N45                               DSPATR(UL)
     A                                 20  2'Or Map from Zero Balancing+
     A                                      /Sweeping Details:'
     A            #0ZSWD         1A  B 20 48TEXT('Map from file Flag')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  67                                  DSPATR(RI PC)
     A N26N67                               DSPATR(UL)
 
      ** Record Key
 
     A          R RE101104D1                TEXT('Record Key')
     A                                      OVERLAY
     A                                      CF12
     A                                  5  2'Destination:'
     A                                      DSPATR(HI)
     A            #0DEST        11A  B  5 15TEXT('Destination')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  46                                  DSPATR(RI PC)
     A N26N46                               DSPATR(UL)
     A                                  7  2'Description:'
     A                                      DSPATR(HI)
     A            #0DESC        30A  B  7 15TEXT('Description')
     A N26                                  DSPATR(HI)
     A  26                                  DSPATR(PR)
     A  47                                  DSPATR(RI PC)
     A N26N47                               DSPATR(UL)
 
      ** Command Keys
 
     A          R RE101104F1                TEXT('Command Keys')
     A                                      OVERLAY
     A  21                             22  2'A=Amend   D=Delete   E=Enquire'
     A                                      DSPATR(HI)
     A*                                                      /*211210*/
     A**21*****************************23**2'F3=Main Menu   F9=Add Record'
     A**************************************DSPATR(HI)
     A  21N15                          23  2'F3=Main Menu   F9=Add Record'
     A                                      DSPATR(HI)
     A  21 15                          23  2'F3=Main Menu                '
     A                                      DSPATR(HI)
     A*                                                      /*211210*/
     A  22                             22  2'E=Enquire   D=Delete   A=Amend'
     A                                      DSPATR(HI)
     A*                                                      /*211210*/
     A**22*****************************23**2'F3=Exit   F5=Refresh   F9=Add   +
     A**************************************F12=Previous'
     A**************************************DSPATR(HI)
     A  22N15                          23  2'F3=Exit   F5=Refresh   F9=Add   +
     A                                      F12=Previous'
     A                                      DSPATR(HI)
     A  22 15                          23  2'F3=Exit   F5=Refresh            +
     A                                      F12=Previous'
     A                                      DSPATR(HI)
     A*                                                      /*211210*/
     A  23 24                          23  2'F3=Main Menu   F5=Refresh   +
     A                                      F10=Confirm Delete   F12=Previous'
     A                                      DSPATR(HI)
     A  23N24                          23  2'F3=Main Menu   F5=Refresh   +
     A                                      F12=Previous'
     A                                      DSPATR(HI)
     A  48                             23  2'F3=Main Menu   F12=Previous'
     A                                      DSPATR(HI)
 
      ** Message Record Format
 
     A          R RE101104S2                TEXT('Program Messages')
     A                                      SFL SFLMSGRCD(24)
     A            #2MSGK                    SFLMSGKEY
     A            #2PGMQ                    SFLPGMQ
 
      ** Message Control
 
     A          R RE101104C2                TEXT('Program Messages')
     A                                      SFLCTL(RE101104S2)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #2PGMQ                    SFLPGMQ
 
