      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE MT101/MT103 Generation by Sweep Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE102005DF - Cash Management ICD Maintenance                 *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      *                 211210             Date 11Dec02               *
      *                 CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  211210 - Various action code errors                          *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03
 
     A          R RE102005F0                TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                      OVERLAY
 
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'RE102005DF'
     A            #0RUND    R        O  1 74REFFLD(RUNA)
 
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 24'MT101/MT103 Generation +
     A                                       by Sweep Enquiry'
     A                                      DSPATR(HI)
     A                                  2 73TIME
 
     A          R RE102005S0                TEXT('Enquiry Screen Subfile')
     A                                      SFL
 
     A            #0ACTN         1A  B  8  2TEXT('Action Code')
     A                                      DSPATR(HI)
     A*                                                      /*211210*/
     A                                      VALUES('E')
     A*                                                      /*211210*/
     A            #0OMSR        16A     8  5TEXT('Outgoing Msg SWIFT Ref.')
     A                                      DSPATR(HI)
     A            #0MSGT         3A     8 22TEXT('Message Type')
     A                                      DSPATR(HI)
     A            #0DEST        20A     8 27TEXT('Destination')
     A                                      DSPATR(HI)
     A            #0DATE         7A     8 48TEXT('Generation Date')
     A                                      DSPATR(HI)
     A            #0SLSH         1A     8 56TEXT('Slash')
     A                                      DSPATR(HI)
     A            #0TIME         6Y 0   8 58TEXT('Generation Time')
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A            #0MSGS        13A     8 67TEXT('Message Status')
     A                                      DSPATR(HI)
     A                                  9  5'Top A/c    :'
     A**Start of change CGL029
     A**********  #0TOPA        22A     9 18TEXT('Top Account')                               CGL029
     A**********                            DSPATR(HI)                                        CGL029
     A**********                        9 43'Amount:'                                         CGL029
     A**********  #0AMNT        22A     9 51TEXT('Amount')                                    CGL029
     A            #0TOPA        28A     9 18TEXT('Top Account')
     A                                      DSPATR(HI)
     A                                  9 49'Amount:'
     A            #0AMNT        22A     9 57TEXT('Amount')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A                                 10  5'Settle A/c :'
     A            #0SETA        10A    10 18TEXT('Settlement Account')
     A                                      DSPATR(HI)
     A                                 10 30'External A/c :'
     A            #0EXTA        35A    10 45TEXT('External Account')
     A                                      DSPATR(HI)
 
     A          R RE102005C0                TEXT('Enquiry Screen Control')
     A                                      SFLCTL(RE102005S0)
     A                                      OVERLAY BLINK PUTOVR
     A  02                                  SFLDSP
     A  03                                  SFLDSPCTL
     A  07                                  SFLEND
     A  01                                  SFLCLR
     A                                      SFLSIZ(6)
     A                                      SFLPAG(5)
     A N07                                  ROLLUP(04)
 
     A                                  4  5'T.R.N.'
     A                                  4 22'Msg'
     A                                  4 31'Destination'
     A                                  4 52'Generation'
     A                                  4 70'Msg'
     A                                  5 22'Type'
     A                                  5 52'Date/Time'
     A                                  5 69'Status'
     A            #0IMSR        16A  I  6  5TEXT('Outgoing Msg SWIFT Ref.')
     A            #0IMST         3A  I  6 22TEXT('Message Type')
     A            #0IDES        20A  I  6 27TEXT('Destination')
     A            #0IDAT         7A  I  6 48TEXT('Generation Date')
     A            #0CSLH         1A  O  6 56TEXT('Slash')
     A            #0ITIM         8A  I  6 58TEXT('Generation Time')
     A            #0IMSS        13A  I  6 67TEXT('Message Status')
     A                                      CHECK(LC)
 
     A          R RE102005F1                TEXT('Enquiry Screen Footer')
     A                                      OVERLAY
 
     A                                 24  2'F3=Exit'
     A  06                             24 11'E=Enquire'
     A  06                             24 22'Rollup/Rolldown'
 
     A          R RE102005F2                TEXT('No Record to Display')
     A                                      OVERLAY
 
     A                                 12 26'*** NO DATA TO DISPLAY ***'
     A                                      DSPATR(HI)
