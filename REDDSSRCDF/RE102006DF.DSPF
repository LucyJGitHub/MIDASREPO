      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE MT101/MT103 Reconciliation Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  DSPF/RE102006DF - MT101/MT103 Reconciliation Enquiry         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. 212995             Date 06Jan03               *
      *                 211210             Date 11Dec02               *
      *                 CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  212995 - Allow deletion of input cycle sweeps                *
      *  211210 - Various action code errors                          *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
 
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
 
      ** Header
     A          R RE102006F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'RE102006'
     A                                  1 66'DATE'
     A            #0RUNA    R        O  1 73REFFLD(RUNA)
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 25'MT101/MT103 Reconciliation +
     A                                      Enquiry'
     A                                      DSPATR(UL HI)
     A                                  2 66'TIME'
     A            #0TIME    R        O  2 72REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
 
      ** Subfile Record Format (Screen 1)
 
     A          R RE102006S0
     A                                      SFL
     A  83                                  SFLNXTCHG
     A            #0ACTN         1   B  7  2TEXT('Action Code')
     A  30                                  DSPATR(RI PC)
     A N30                                  DSPATR(HI UL)
     A*                                                      /*211210*/
     A*                                                      /*212995*/
     A***********                           VALUES('E')
     A                                      VALUES('D' 'E' ' ')
     A*                                                      /*212995*/
     A*                                                      /*211210*/
     A            #0OMSR        16   O  7  5TEXT('MT101 T.R.N.')
     A                                      DSPATR(HI)
     A            #0DEST        11   O  7 23TEXT('Destination')
     A                                      DSPATR(HI)
     A            #0MGDE         7   O  7 36TEXT('Generation Date')
     A                                      DSPATR(HI)
     A            #0MGTM    R        O  7 46TEXT('Generation Time')
     A                                      REFFLD(JOTIME)
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A            #0STAT        22   O  7 58TEXT('Reconciliation Status')
     A                                      DSPATR(HI)
 
     A                                  8  5'MT103 MIR :'
     A            #0SMIR        28   O  8 17TEXT('MT103 MIR')
     A                                      DSPATR(HI)
     A                                  8 46'Recv Date/Time:'
     A            #0SDAT         7   O  8 62TEXT('Receive Date')
     A                                      DSPATR(HI)
     A                                  8 69'/'
     A            #0STIM         4  0O  8 70TEXT('Receive Time')
     A                                      EDTWRD('  :  ')
     A                                      DSPATR(HI)
     A*                                                      /*212995*/
     A            #0MTYN         1A  H      TEXT('Matched Y/N')
     A*                                                      /*212995*/
     A                                  9  1' '
 
      ** Subfile Control (Screen 1)
 
     A          R RE102006C0
     A                                      SFLCTL(RE102006S0)
     A                                      SFLPAG(05) SFLSIZ(06)
     A                                      OVERLAY
     A                                      PUTOVR
     A  80                                  SFLCLR
     A N80 81                               SFLDSP
     A N80                                  SFLDSPCTL
     A N80 81 82                            SFLEND
     A N82                                  ROLLUP(31)
     A                                      CF15
 
     A            #0RLRN         4S 0H      SFLRCDNBR
     A                                  4  5'MT101 T.R.N.'
     A                                  4 23'Destination'
     A                                  4 36'Generation Date/Time'
     A                                  4 58'Reconciliation Status'
     A            #0ROMS        16   B  5  5TEXT('MT101 T.R.N.')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A            #0RDES        11   B  5 23TEXT('Destination')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
     A            #0RMGD         7   B  5 36TEXT('Generation Date')
     A                                      DSPATR(HI UL PC)
     A                                      OVRATR
     A                                      OVRDTA
 
      ** Screen 2
 
     A          R RE102006D0                TEXT('Detail Line')
     A                                      OVERLAY
      *                                                      /*212995*/
     A     32                               CF10
      *                                                      /*212995*/
     A                                      CF12
     A                                  5  5'MT101 Details'
     A                                      DSPATR(UL)
     A            #0STT1        22   O  5 21
     A                                      DSPATR(HI)
     A                                  6  5'MT101 T.R.N. . . . . .:'
     A            #0OMS1        16   O  6 34TEXT('MT101 T.R.N.')
     A                                      DSPATR(HI)
     A                                  7  5'Destination. . . . . .:'
     A            #0DES1        11   O  7 34TEXT('Destination')
     A                                      DSPATR(HI)
     A                                  8  5'Value Date . . . . . .:'
     A            #0SVD1         7   O  8 34TEXT('Value Date')
     A                                      DSPATR(HI)
     A                                  9  5'Amount . . . . . . . .:'
     A            #0AMT1        21   O  9 34TEXT('Amount')
     A                                      DSPATR(HI)
     A                                 10  5'Top Account. . . . . .:'
     A**Start of change CGL029
     A**********  #0TOPA        18   O 10 34TEXT('Top Account')                               CGL029
     A            #0TOPA        24   O 10 34TEXT('Top Account')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A                                 11  5'External Account . . .:'
     A            #0EXA1        35   O 11 34TEXT('External Account')
     A                                      DSPATR(HI)
 
     A                                 13  5'MT103 Details'
     A                                      DSPATR(UL)
     A                                 14  5'MT103 Msg Input Ref. .:'
     A            #0SMI1        28   O 14 34TEXT('MT103 Msg Input Ref')
     A                                      DSPATR(HI)
     A                                 15  5'MT103 T.R.N. . . . . .:'
     A            #0SMSR        16   O 15 34TEXT('MT103 T.R.N.')
     A                                      DSPATR(HI)
     A                                 16  5'Value Date . . . . . .:'
     A            #0SVD2         7   O 16 34TEXT('Value Date')
     A                                      DSPATR(HI)
     A                                 17  5'Amount . . . . . . . .:'
     A            #0AMT2        21   O 17 34TEXT('Amount')
     A                                      DSPATR(HI)
 
      ** Command Keys
 
     A          R RE102006F1                TEXT('Command Keys')
     A                                      OVERLAY
     A*                                                      /*212995*/
     A**21*******                      22  2'E=Enquire'
     A  21 35                          22  2'E=Enquire'
     A                                      DSPATR(HI)
     A  21 34                          22  2'D=Delete  E=Enquire'
     A*                                                      /*212995*/
     A                                      DSPATR(HI)
     A  21 26                          23  2'F3=Exit   F15=All Reconciled   +
     A                                      Rollup/Rolldown'
     A                                      DSPATR(HI)
     A  21 27                          23  2'F3=Exit   F15=All Not Reconciled  +
     A                                       Rollup/Rolldown'
     A                                      DSPATR(HI)
     A  21 28                          23  2'F3=Exit   F15=All MT101   +
     A                                      Rollup/Rolldown'
     A                                      DSPATR(HI)
     A  22                             23  2'F3=Exit   F12=Previous'
     A                                      DSPATR(HI)
     A*                                                      /*212995*/
     A  32                             23  2'F3=Exit   F10=Confirm Delete   +
     A                                      F12=Previous'
     A                                      DSPATR(HI)
     A*                                                      /*212995*/
 
      ** Message Record Format
 
     A          R RE102006S1                TEXT('Program Messages')
     A                                      SFL SFLMSGRCD(24)
     A            #1MSGK                    SFLMSGKEY
     A            #1PGMQ                    SFLPGMQ
 
      ** Message Control
 
     A          R RE102006C1                TEXT('Program Messages')
     A                                      SFLCTL(RE102006S1)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            #1PGMQ                    SFLPGMQ
 
