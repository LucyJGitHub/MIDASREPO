      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE In MT101/MT103 Processed by Cash Mgmt Enq')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE102010DF - Incoming MT101/MT103 Processed by Cash Managemt *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. 211210             Date 11Dec02               *
      *                 CRE008  *CREATE    Date 10Jun02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  211210 - Various action code errors                          *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CA03
 
     A          R RE102010F0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
     A                                      OVERLAY
 
     A            #0USER    R        O  1  2REFFLD(USER)
     A                                  1 13'RE102010DF'
     A            #0RUND    R        O  1 74REFFLD(RUNA)
 
     A            #0WSID    R        O  2  2REFFLD(JOJOB)
     A                                  2 18'Incoming MT101/MT103 Processed +
     A                                       by Cash Management'
     A                                      DSPATR(HI)
     A                                  2 73TIME
 
     A          R RE102010S0                TEXT('Enquiry Screen Subfile')
     A                                      SFL
 
     A            #0ACTN         1A  B  8  2TEXT('Action Code')
     A                                      DSPATR(HI)
     A*                                                      /*211210*/
     A                                      VALUES('E')
     A*                                                      /*211210*/
     A            #0SNDR        11A     8  5TEXT('Sender')
     A                                      DSPATR(HI)
     A            #0DATE         7A     8 17TEXT('Received Date')
     A                                      DSPATR(HI)
     A            #0SLSH         1A     8 25TEXT('Slash')
     A                                      DSPATR(HI)
     A            #0TIME         6Y 0   8 27TEXT('Received Time')
     A                                      EDTWRD('  :  :  ')
     A                                      DSPATR(HI)
     A            #0OMSR        16A     8 36TEXT('Outgoing Msg SWIFT Ref.')
     A                                      DSPATR(HI)
     A            #0MSGT         3A     8 53TEXT('Message Type')
     A                                      DSPATR(HI)
     A            #0MSGS        15A     8 57TEXT('Processed as')
     A                                      DSPATR(HI)
     A                                  9  5'Top A/c    :'
     A**Start of change CGL029
     A**********  #0TOPA        22A     9 18TEXT('Top Account')                               CGL029
     A            #0TOPA        28A     9 18TEXT('Top Account')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********                        9 43'Amount:'                                         CGL029
     A**********  #0AMNT        22A     9 51TEXT('Amount')                                    CGL029
     A                                  9 49'Amount:'
     A            #0AMNT        22A     9 57TEXT('Amount')
     A**End of change CGL029
     A                                      DSPATR(HI)
     A                                 10  5'Settle A/c :'
     A            #0SETA        10A    10 18TEXT('Settlement Account')
     A                                      DSPATR(HI)
     A            #0MIR         28A  H
 
     A          R RE102010C0                TEXT('Enquiry Screen Control')
     A                                      SFLCTL(RE102010S0)
     A                                      OVERLAY BLINK PUTOVR
     A  02                                  SFLDSP
     A  03                                  SFLDSPCTL
     A  07                                  SFLEND
     A  01                                  SFLCLR
     A                                      SFLSIZ(6)
     A                                      SFLPAG(5)
     A N07                                  ROLLUP(04)
 
     A                                  4  5'Sender'
     A                                  4 21'Received'
     A                                  4 36'T.R.N.'
     A                                  4 53'Msg'
     A                                  4 59'Processed'
     A                                  5 21'Date/Time'
     A                                  5 53'Type'
     A                                  5 62'As'
     A            #0ISND        11A  I  6  5TEXT('Sender')
     A            #0IDAT         7A  I  6 17TEXT('Received Date')
     A            #0CSLH         1A  O  6 25TEXT('Slash')
     A            #0ITIM         8A  I  6 27TEXT('Received Time')
     A            #0IMSR        16A  I  6 36TEXT('Outgoing Msg SWIFT Ref.')
     A            #0IMST         3A  I  6 53TEXT('Message Type')
     A            #0IMSS        15A  I  6 57TEXT('Processed As')
     A                                      CHECK(LC)
 
     A          R RE102010F1                TEXT('Enquiry Screen Footer')
     A                                      OVERLAY
 
     A                                 24  2'F3=Exit'
     A  06                             24 11'E=Enquire'
     A  06                             24 22'Rollup/Rolldown'
 
     A          R RE102010F2                TEXT('No Record to Display')
     A                                      OVERLAY
 
     A                                 12 26'*** NO DATA TO DISPLAY ***'
     A                                      DSPATR(HI)
