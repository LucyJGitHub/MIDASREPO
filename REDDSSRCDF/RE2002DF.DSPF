      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Password file Maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE2002DF - Password File Maintanence                         *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CRE023  *CREATE    Date 29Jul05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE023 - 2 O'clock Flag Upgrade to MidasPlus                 *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      PRINT(MPRINT)
      *
      *****************************************************************
      *
      *  Initial screen - RE2002S1
      *
      *****************************************************************
     A          R RE2002S1
     A                                      TEXT('Usage:RE2002')
     A                                      CF03
     A                                  1 65TIME
     A                                  1 74'WID'
     A            WSID           2A  O  1 78
     A                                  3 25'ODSEC PASSWORD FILE MAINTENANCE'
     A                                      DSPATR(UL)
     A                                  8 25'ENTER MASTER USER ID  :'
     A            SMSID          8A  B  8 49DSPATR(HI)
     A                                 10 25'ENTER MASTER PASSWORD :'
     A            MPSWD          8A  B 10 49DSPATR(ND)
     A            ERRMSG        30A  O 19 26DSPATR(HI)
     A                                 22 33'Press F3 to end'
      *
      *****************************************************************
      *
      *
      *****************************************************************
     A          R RE2002S2
     A                                      TEXT('Usage:RE2002')
     A                                      CF03
     A                                  1 65TIME
     A                                  1 74'WID'
     A            WSID           2A  O  1 78
     A                                  3 25'ODSEC PASSWORD FILE MAINTENANCE'
     A                                      DSPATR(UL)
     A                                  8 25'ENTER ACTION CODE :'
     A            ACODE          1A  B  8 45DSPATR(HI)
     A                                 10 25'ENTER USER ID     :'
     A            USID           8A  B 10 45DSPATR(HI)
     A            ERRMSG        30A  O 19 26DSPATR(HI)
     A                                 22 33'Press F3 to end'
      *
      *****************************************************************
      *
      *
      *****************************************************************
     A          R RE2002S3
     A                                      TEXT('Usage:RE2002')
     A                                      CF03
     A                                      CF12
     A                                  1 65TIME
     A                                  1 74'WID'
     A            WSID           2A  O  1 78
     A                                  3 25'ODSEC PASSWORD FILE MAINTENANCE'
     A                                      DSPATR(UL)
     A                                 14 26'ENTER PASSWORD   :'
     A            PSWD           8A  B 14 45DSPATR(ND)
     A            ERRMSG        30A  O 19 26DSPATR(HI)
     A                                 22 20'Press F3 to end or F12 for initial-
     A                                       screen'
     A                                 16 26'CLASS TYPE       :'
     A            SCTYP          1A  B 16 45
      *****************************************************************
      *
      *
      *****************************************************************
     A          R RE2002S4
     A                                      TEXT('Usage:RE2002')
     A                                      CF03
     A                                      CF10
     A                                      CF12
     A                                  1 65TIME
     A                                  1 74'WID'
     A            WSID           2A  O  1 78
     A                                  3 25'ODSEC PASSWORD FILE MAINTENANCE'
     A                                      DSPATR(UL)
     A                                 16 26'      CLASS TYPE  :'
     A            GCTYP          1A  O 16 46
     A                                      DSPATR(HI)
     A            ERRMSG        30A  O 19 26DSPATR(HI)
     A                                 22  5'Press F3 to end or press F10 to de-
     A                                      lete or press F12 for previous scre-
     A                                      en'
      *****************************************************************
      *
      *
      *
      *****************************************************************
     A          R RE2002S5
     A                                      TEXT('Usage:RE2002')
     A                                      CF03
     A                                      CF12
     A                                  1 65TIME
     A                                  1 74'WID'
     A            WSID           2A  O  1 78
     A                                  3 25'ODSEC PASSWORD FILE MAINTENANCE'
     A                                      DSPATR(UL)
     A                                 14 26'ENTER OLD MASTER PASSWORD    :'
     A            MPSWD          8A  B 14 57DSPATR(ND)
     A                                 16 26'RE-ENTER NEW MASTER PASSWORD :'
     A  47                                  DSPATR(BL)
     A            NMPSWD         8A  B 16 57DSPATR(ND)
     A            ERRMSG        30A  O 19 26DSPATR(HI)
     A                                 22 17'Press F3 to end or press F12 for i-
     A                                      nitial screen'
