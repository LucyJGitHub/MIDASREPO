      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Outward Clearing Trans.Types Maint')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  REG005DF - Outward Clearing Transaction Types Maintenance    *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CRE023  *CREATE    Date 29Jul05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE023 - 2 O'clock Flag Upgrade to MidasPlus                 *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
      *================================================================
     A          R SFL01                     SFL
     A  27                                  SFLNXTCHG
     A            #SEL           1A  B  8  3
     A                                      VALUES(' ' 'D')
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(ND PR)
     A            #GTTYP         5A  B  8  9
     A  22                                  DSPATR(UL)
     A N22                                  DSPATR(PR)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A            #GOCLR         1A  B  8 23
     A                                      DSPATR(UL)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A            #GRCLT         1A  B  8 42
     A                                      DSPATR(UL)
     A  32                                  DSPATR(RI)
     A            #GRCLP         1A  B  8 61
     A                                      DSPATR(UL)
     A  32                                  DSPATR(RI)
      *================================================================
     A          R SFC01                     SFLCTL(SFL01)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CF03
     A                                      CA05
     A                                      CF09
     A N30                                  ROLLUP(08)
     A                                      OVERLAY
     A  25                                  SFLDSP
     A  26                                  SFLDSPCTL
     A  29                                  SFLCLR
     A  30                                  SFLEND
     A            RCD01          4S 0H      SFLRCDNBR(CURSOR)
     A            ##USR         10A  O  1  2TEXT('*USER')
     A            ##PGM         10A  O  1 13TEXT('*PROGRAM')
     A            ##MRDT         7A  O  1 72TEXT('Midas Run Date')
     A            ##JOB         10A  O  2  2TEXT('*JOB')
     A                                  4  8'Retail'
     A                                  5  5' Trans.Type'
     A            #POS           5A  B  6  9TEXT('Retail Transaction Type')
     A N22                                  DSPATR(UL)
     A  22                                  DSPATR(ND)
     A  22                                  DSPATR(PR)
     A                                      CHANGE(21)
     A                                  2 24'Outward Clearing Transaction Types'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                  4 19'Outward'
     A                                  4 35'Reverse Today''s'
     A                                  4 54'Reverse Previous'
     A                                  5 19'Clearing'
     A                                  5 35'Outward Clearing'
     A                                  5 54'Outward Clearing'
      *=========================================================================
     A          R FTR01                     TEXT('Command keys.')
     A            #FTR01        78     23  2TEXT('Command Text 1')
      *=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
      *=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  26
     AON26                                  SFLEND
     A            ##PGM                     SFLPGMQ
