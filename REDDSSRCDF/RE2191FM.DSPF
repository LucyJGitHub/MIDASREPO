      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Manual Take-on of Int.Pay.History')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Retail Module                               *
     A*                                                                  *
     A*      DSPF/RE2191FM - MANUAL TAKE-ON OF INTEREST PAYMENT HISTORY  *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01383             Date 27Apr92               *
     A*                                                                  *
     A*------------------------------------------------------------------*
     A*                                                                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*       S01383 - BASIC RATE TAX INCORPORATION                      *
     A*                NEW PROGRAM                                       *
     A*                                                                  *
     A********************************************************************
      *                                                               *
      *    F U N C T I O N    O F   I N D I C A T O R S               *
      *    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~               *
      *         03         CMD/3 PRESSED                              *
      *         12         CMD/12 PRESSED                             *
      *         30         ERROR FOUND                                *
      *         31         CUSTOMER NUMBER IN ERROR                   *
      *         32         DEAL NUMBER IN ERROR                       *
      *         33         CURRENCY CODE IN ERROR                     *
      *         34         ACCOUNT CODE IN ERROR                      *
      *         35         ACCOUNT SEQUENCE IN ERROR                  *
      *         36         GROSS INTEREST IN ERROR                    *
      *         37         BRT AMOUNT IN ERROR                        *
      *         38         INTEREST PAYMENT DATE IN ERROR             *
      *         39         S352 INDICATOR IN ERROR                    *
      *         40         'CONFIRM' FIELD IN ERROR                   *
      *         41         BRANCH CODE IN ERROR                       *
      *         50         'CONFIRM' LINE TO APPEAR ON SCREEN         *
      *         60         PROTECT ALL FIELDS WHEN CONFIRMING         *
      *                                                               *
      *****************************************************************
     A                                      PRINT
     A                                      CA03(03)
     A                                      CA12(12)
     A*
     A          R RE2191F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            USRID         10   O  1  2
     A                                  1 20'MANUAL TAKE-ON OF INTEREST +
     A                                      PAYMENT HISTORY'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            BJMRDT         7   O  1 72
     A            WID           10   O  2  2
     A                                  2 71TIME
     A                                  4  8'Customer number'
     A            CUSNO         10   B  4 24
     A  31                                  DSPATR(RI PC)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(PR)
     A                                  6 18'Deal number'
     A            DEAL           6   B  6 30
     A  32                                  DSPATR(RI PC)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(PR)
     A                                  8 18'Currency'
     A**Start of change CGL029
     A**********  CURR           3   B  8 30                                                  CGL029
     A            CURR           3   B  8 27
     A**End of change CGL029
     A  33                                  DSPATR(RI PC)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(PR)
     A**Start of change CGL029
     A**********                        8 37'A/c code'                                        CGL029
     A**********  ACODE          4   B  8 46                                                  CGL029
     A                                  8 34'A/c code'
     A            ACODE         10   B  8 43
     A**End of change CGL029
     A  34                                  DSPATR(RI PC)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(PR)
     A**Start of change CGL029
     A**********                        8 54'A/c seq'                                         CGL029
     A**********  ACSEQ          2   B  8 63                                                  CGL029
     A                                  8 56'A/c seq'
     A            ACSEQ          2   B  8 64
     A**End of change CGL029
     A  35                                  DSPATR(RI PC)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(PR)
     A                                  8 69'Branch'
     A            BRANCH         3   B  8 76
     A  41                                  DSPATR(RI PC)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(PR)
     A                                 10  8'Enter amounts in sterling:'
     A                                 12 18'Gross Interest  $'
     A            GINT          16   B 12 36
     A  36                                  DSPATR(RI PC)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(PR)
     A                                 14 18'BRT Deducted    $'
     A            BRT           16   B 14 36
     A  37                                  DSPATR(RI PC)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(PR)
     A                                 16  8'Interest payment date'
     A            INTDT          6   B 16 34
     A  38                                  DSPATR(RI PC)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(PR)
     A                                 18  8'S352 produced Y/N'
     A            IND352         1   B 18 34
     A  39                                  DSPATR(RI PC)
     A N60                                  DSPATR(UL)
     A  60                                  DSPATR(PR)
     A  50                             20 50'CONFIRM Y/N'
     A  50        CONF           1   B 20 63DSPATR(PC)
     A  50 40                               DSPATR(RI)
     A*
     A            ERMSG         76A  O 22 02
     A N30                                  DSPATR(ND)
     A  30                                  DSPATR(HI RI)
     A            ERCD          76A  O 23 02
     A N30                                  DSPATR(ND)
     A  30                                  DSPATR(HI RI)
     A N60                             24  2'F3 = End, +
     A                                      F12 = Initial Screen'
