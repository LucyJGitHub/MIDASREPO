      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Manual Adjustment to Int.Pay.History')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Retail Module                               *
     A*                                                                  *
     A*     DSPF/RE2192FM - MANUAL ADJUSTMENT TO INTEREST PAYMENT HISTORY*
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01383             Date 27Apr92               *
     A*                                                                  *
     A*------------------------------------------------------------------*
     A*                                                                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*       S01383 - BASIC RATE TAX INCORPORATION                      *
     A*                NEW PROGRAM                                       *
     A*                                                                  *
     A********************************************************************
      *                                                               *
      *    F U N C T I O N    O F   I N D I C A T O R S               *
      *    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~               *
      *         03         CMD/3 PRESSED                              *
      *         12         CMD/12 PRESSED                             *
      *         30         ERROR FOUND                                *
      *         31         CUSTOMER NUMBER IN ERROR                   *
      *         32         DEAL NUMBER IN ERROR                       *
      *         33         DEAL CURRENCY IN ERROR                     *
      *         34         ACCOUNT CURRENCY IN ERROR                  *
      *         35         ACCOUNT CODE IN ERROR                      *
      *         36         ACCOUNT SEQUENCE IN ERROR                  *
      *         37         CREDITED BRT (STERLING) IN ERROR           *
      *         38         CREDITED BRT (ORIG. CCY) IN ERROR          *
      *         39         'CONFIRM' FIELD IN ERROR                   *
      *         40         ORIGINAL CCY BRT DEDUCTED                  *
      *         41         BASE CCY BRT DEDUCTED                      *
      *         42         'CONFIRM Y/N' LINE                         *
      *         43         BRANCH IN ERROR                            *
      *         50         'CONFIRM' SCREEN TO APPEAR ON SCREEN       *
      *                                                               *
      *****************************************************************
     A                                      PRINT
     A                                      CA03(03)
     A                                      CA12(12)
     A*
     A          R RE2192F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            USRID         10   O  1  2
     A                                  1 20'MANUAL ADJUSTMENT TO INTEREST +
     A                                      PAYMENT HISTORY'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            BJMRDT         7   O  1 72
     A            WID           10   O  2  2
     A                                  2 71TIME
     A                                  4  8'Customer'
     A            CUSNO         10   B  4 17
     A  31                                  DSPATR(RI PC)
     A                                  6 18'Deal number'
     A            DEAL           6   B  6 30
     A  32                                  DSPATR(RI PC)
     A                                  6 38'Deal ccy'
     A            DLCUR          3   B  6 47
     A  33                                  DSPATR(RI PC)
     A                                  8 18'A/c currency'
     A            CURR           3   B  8 31
     A  34                                  DSPATR(RI PC)
     A**Start of change CGL029
     A**********                        8 37'A/c code'                                        CGL029
     A**********  ACODE          4   B  8 46                                                  CGL029
     A                                  8 36'A/c code'
     A            ACODE         10   B  8 45
     A**End of change CGL029
     A  35                                  DSPATR(RI PC)
     A**Start of change CGL029
     A**********                        8 54'A/c seq'                                         CGL029
     A**********  ACSEQ          2   B  8 63                                                  CGL029
     A                                  8 57'A/c seq'
     A            ACSEQ          2   B  8 65
     A**End of change CGL029
     A  36                                  DSPATR(RI PC)
     A                                  8 69'Branch'
     A            BRANCH         3   B  8 76
     A  43                                  DSPATR(RI PC)
     A                                 10  8'Amount of BRT to be +
     A                                      credited:'
     A                                 12 18'In sterling:'
     A            BRTSTG        16   B 12 36
     A  37                                  DSPATR(RI PC)
     A                                 13 15'or In original ccy:'
     A            BRTCCY        16   B 13 36
     A  38                                  DSPATR(RI PC)
     A*
     A  50                             15  8'Total BRT previously +
     A                                      deducted:'
     A  50                             17 18'In sterling:'
     A  50        PRVSTG        16   O 17 36
     A  50                             18 18'In original ccy:'
     A  50        PRVCCY        16   O 18 36
     A  50 41                          19 18'In base ccy:'
     A  50 41     PRVBSE        16   O 19 36
     A  50 42                          20 54'CONFIRM Y/N'
     A                                      DSPATR(HI)
     A  50 42     CONF           1   B 20 67DSPATR(PC)
     A  50 42 39                            DSPATR(RI)
     A*
     A            ERMSG         76A  O 22 02
     A N30                                  DSPATR(ND)
     A  30                                  DSPATR(HI RI)
     A            ERCD          76A  O 23 02
     A N30                                  DSPATR(ND)
     A  30                                  DSPATR(HI RI)
     A                                 24  2'F3 = End, +
     A                                      F12 = Initial Screen'
