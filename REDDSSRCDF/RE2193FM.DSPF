      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Request Production of S352 Statement')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Retail Module                               *
     A*                                                                  *
     A*     DSPF/RE2193FM - REQUEST PRODUCTION OF S352 STATEMENT         *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01383             Date 27Apr92               *
     A*                                                                  *
     A*------------------------------------------------------------------*
     A*                                                                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*       S01383 - BASIC RATE TAX INCORPORATION                      *
     A*                NEW PROGRAM                                       *
     A*                                                                  *
     A********************************************************************
      *                                                               *
      *    F U N C T I O N    O F   I N D I C A T O R S               *
      *    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~               *
      *         03         CMD/3 PRESSED                              *
      *         12         CMD/12 PRESSED                             *
      *         30         ERROR FOUND                                *
      *         31         CUSTOMER NUMBER IN ERROR                   *
      *         32         DEAL NUMBER IN ERROR                       *
      *         33         CURRENCY CODE IN ERROR                     *
      *         34         ACCOUNT CODE IN ERROR                      *
      *         35         ACCOUNT SEQUENCE IN ERROR                  *
      *         36         BRANCH IN ERROR                            *
      *                                                               *
      *****************************************************************
     A                                      PRINT
     A                                      CA03(03)
     A                                      CA12(12)
     A*
     A          R RE2193F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            USRID         10   O  1  2
     A                                  1 18'REQUEST PRODUCTION OF S352 +
     A                                      STATEMENT'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            BJMRDT         7   O  1 72
     A            WID           10   O  2  2
     A                                  2 71TIME
     A                                  4  6'Customer'
     A            CUSNO         10   B  4 16
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(HI)
     A                                  6  6'Enter deal number or account +
     A                                      reference :'
     A                                  8 13'Deal number'
     A            DEAL           6   B  8 25
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(HI)
     A                                 10 13'A/c Currency'
     A            CURR           3   B 10 26
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(HI)
     A                                 10 31'A/c code'
     A**Start of change CGL029
     A**********  ACODE          4   B 10 40                                                  CGL029
     A            ACODE         10   B 10 40
     A**End of change CGL029
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(HI)
     A**Start of change CGL029
     A**********                       10 47'A/c seq'                                         CGL029
     A**********  ACSEQ          2   B 10 55                                                  CGL029
     A                                 10 52'A/c seq'
     A            ACSEQ          2   B 10 60
     A**End of change CGL029
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(HI)
     A**Start of change CGL029
     A**********                       10 60'Branch'                                          CGL029
     A**********  BRANCH         3   B 10 67                                                  CGL029
     A                                 10 64'Branch'
     A            BRANCH         3   B 10 71
     A**End of change CGL029
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(HI)
     A*
     A            ERMSG         76A  O 22 02
     A N30                                  DSPATR(ND)
     A  30                                  DSPATR(HI RI)
     A            ERCD          76A  O 23 02
     A N30                                  DSPATR(ND)
     A  30                                  DSPATR(HI RI)
     A                                 24  2'F3 = End, +
     A                                      F12 = Initial Screen'
