      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE S17 Report Request')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Retail Module                               *
     A*                                                                  *
     A*      DSPF/RE2201DF - S17 REPORT REQUEST                          *
     A*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     A*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01383             Date 27Apr92               *
     A*                                                                  *
     A*------------------------------------------------------------------*
     A*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
     A*       S01383 - BASIC RATE TAX INCORPORATION                      *
     A*                NEW PROGRAM                                       *
     A*                                                                  *
     A********************************************************************
     A*                                                               *
     A*    F U N C T I O N    O F   I N D I C A T O R S               *
     A*    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~               *
     A*         09         PROTECT INPUT FIELDS                       *
     A*         10         DISPLAY ERROR MESSAGE                      *
     A*         11         NON-DISPLAY BANK                           *
     A*         12         INDIVIDUALS FIELD IN ERROR                 *
     A*         13         CORPORATES FIELD IN ERROR                  *
     A*         14         BANKS FIELD IN ERROR                       *
     A*         15         DATAFILE FIELD IN ERROR                    *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A          R S17MNUF                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      CA03
     A            USRID         10   O  1  2
     A                                  1 29'S17 INTERIM REPORT REQUEST'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            RUNA           7   O  1 72
     A            WID           10   O  2  2
     A                                  2 71TIME
     A                                  5 55'Y/N'
     A                                  9 29'1.  Include Individuals ?'
     A            INDV           1A  B  9 56
     A N12N09                               DSPATR(UL)
     A  12                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
     A  09                                  DSPATR(PR)
     A                                 11 29'2.  Include Corporates ?'
     A            CORP           1A  B 11 56
     A N13N09                               DSPATR(UL)
     A  13                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
     A  09                                  DSPATR(PR)
     A                                 13 29'3.  Include Banks ?'
     A  11                                  DSPATR(ND)
     A            BANK           1A  B 13 56
     A N11N14N09                            DSPATR(UL)
     A  11                                  DSPATR(ND)
     A  09
     AO 11                                  DSPATR(PR)
     A  14                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
     A                                 15 35'Produce Datafile ?'
     A            DATF           1A  B 15 56
     A N15N09                               DSPATR(UL)
     A  15                                  DSPATR(RI)
     A  15                                  DSPATR(PC)
     A  09                                  DSPATR(PR)
     A            ERMSG         77A  O 22  3
     A N10                                  DSPATR(ND)
     A                                 23  2'F3 = Main Menu'
