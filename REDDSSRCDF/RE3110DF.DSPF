      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Manual Adjmnt. to the Commissions')
     A*****************************************************************
     A*                                                               *
     A*  Midas Retail Module
     A*                                                               *
     A*  DSPF/RE3110DF - RETAIL MANUAL ADJUSTEMENT TO COMMISSIONS     *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01413  *CREATE    Date 13Apr93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  S01413 - Retail 3 Incorporation                              *
     A*           and changes for Release 10 standards                *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      REF(MFRFL)
     A          R RE3110F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      KEEP
     A                                      HOME
     A                                      CF03
     A                                      CF12
     A*
     A*
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE3110'
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 20'MANUAL ADJUSTMENT TO +
     A                                      COMMISSIONS'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                  4  1'Branch'
     A            SBRCA     R        B  4  8REFFLD(BRCA)
     A N16N01                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  16                                  DSPATR(PC RI)
     A  01                                  DSPATR(PR)
     A                                  4 12'Cust.no'
     A            SCNUM     R        B  4 20REFFLD(CNUMA)
     A N11N01                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  11                                  DSPATR(PC RI)
     A  01                                  DSPATR(PR)
     A                                  4 27'Ccy'
     A            SCCY      R        B  4 31REFFLD(CCY)
     A N12N01                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  12                                  DSPATR(PC RI)
     A  01                                  DSPATR(PR)
     A                                  4 35'A/C Code'
     A**Start of change CGL029
     A**********  SACOD     R        B  4 44REFFLD(ACODA)                                     CGL029
     A            SACOD         10   B  4 44
     A**End of change CGL029
     A N13N01                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  13                                  DSPATR(PC RI)
     A  01                                  DSPATR(PR)
     A**Start of change CGL029
     A**********                        4 49'Seq'                                             CGL029
     A**********  SACSQ     R        B  4 53REFFLD(ACSQA)                                     CGL029
     A                                  4 55'Seq'
     A            SACSQ     R        B  4 59REFFLD(ACSQA)
     A**End of change CGL029
     A N14N01                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  14                                  DSPATR(PC RI)
     A  01                                  DSPATR(PR)
     A                                  5  1'Retail no'
     A            SACNO     R        B  5 11REFFLD(RETL)
     A N15N01                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  15                                  DSPATR(PC RI)
     A  01                                  DSPATR(PR)
     A                                 23  2'F3=End of job    '
     A                                 23 29'F12=Initial screen'
     A          R RE3110F2
     A                                      OVERLAY
     A                                      KEEP
     A                                      HOME
     A                                      CF03
     A                                      CF12
     A*
     A*
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE3110'
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 20'MANUAL ADJUSTMENT TO +
     A                                      COMMISSIONS'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                  4  1'Branch'
     A            SBRCA     R        O  4  8REFFLD(BRCA)
     A                                      DSPATR(HI)
     A                                  4 12'Cust.no'
     A            SCNUM     R        O  4 20REFFLD(CNUMA)
     A                                      DSPATR(HI)
     A                                  4 27'Ccy'
     A            SCCY      R        O  4 31REFFLD(CCY)
     A                                      DSPATR(HI)
     A                                  4 35'A/C Code'
     A**Start of change CGL029
     A**********  SACOD     R        O  4 44REFFLD(ACODA)                                     CGL029
     A            SACOD         10   O  4 44
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********                        4 49'Seq'                                             CGL029
     A**********  SACSQ     R        O  4 53REFFLD(ACSQA)                                     CGL029
     A                                  4 55'Seq'
     A            SACSQ     R        O  4 59REFFLD(ACSQA)
     A**End of change CGL029
     A                                      DSPATR(HI)
     A                                  5  1'Retail no'
     A            SACNO     R        O  5 11REFFLD(RETL)
     A                                      DSPATR(HI)
     A                                  7  4'Outstanding interest debit'
     A            INTDRO        14   O  7 32
     A                                  7 50'Credit'
     A            INTCRO        14   O  7 60
     A                                  9 16'Commission on highest O/D'
     A            CHDBEO        14   O  9 42
     A            CCHDB3        14   B  9 60
     A N41                                  DSPATR(UL)
     A  41                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            CCHDBS         1   B  9 76
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A                                 10 23'Account commission'
     A            ACCEAO        14   O 10 42
     A            CACCC3        14   B 10 60
     A N42                                  DSPATR(UL)
     A  42                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            CACCCS         1   B 10 76
     A N62                                  DSPATR(UL)
     A  62                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A                                 11 16'Commission on DR interest'
     A            CDIEO         14   O 11 42
     A            CCDIC3        14   B 11 60
     A N43                                  DSPATR(UL)
     A  43                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            CCDICS         1   B 11 76
     A N63                                  DSPATR(UL)
     A  63                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A                                 12 10'Commission on inactive accounts'
     A            IACEO         14   O 12 42
     A            CIACC3        14   B 12 60
     A N44                                  DSPATR(UL)
     A  44                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            CIACCS         1   B 12 76
     A N64                                  DSPATR(UL)
     A  64                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A                                 13 12'Commission on minimum balance'
     A            CMBEO         14   O 13 42
     A            CCMBC3        14   B 13 60
     A N45                                  DSPATR(UL)
     A  45                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            CCMBCS         1   B 13 76
     A N65                                  DSPATR(UL)
     A  65                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A                                 14  5'Commission on no of daily statemen-
     A                                      ts'
     A            NOIPO         14   O 14 42
     A            CCINS3        14   B 14 60
     A N46                                  DSPATR(UL)
     A  46                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A            CCINSS         1   B 14 76
     A N66                                  DSPATR(UL)
     A  66                                  DSPATR(RI)
     A                                      DSPATR(HI)
     A                                 15 25'Fixed commission'
     A            FXDCO         14   O 15 42
     A                                 19 17'Total Commission Charges'
     A            TOTCO         14   O 19 42
     A                                 23  2'F3=End of job    '
     A                                 23 29'F12=Initial screen'
     A          R RE3110E0                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A          R RE3110F0                  SFLCTL(RE3110E0)
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A N81                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
