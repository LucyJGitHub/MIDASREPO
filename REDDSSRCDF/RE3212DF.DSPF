      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE VAT & Taux d'Usure Maintenance.')
     A*****************************************************************
     A*                                                               *
     A*  Midas    Retail  Module
     A*                                                               *
     A*  DSPF/RE3212DF - VAT & TAUX D'USURE MAINTENANCE               *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BG2487             Date 19May04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01413  *CREATE    Date 13Apr93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BG2487 - Validate all fields including 10-digit Account      *
      *           Code.                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  S01413 - Retail 3 Incorporation                              *
     A*           and changes for Release 10 standards.               *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(MFRFL)
      * Start of BG2487
     A                                      MSGLOC(24)
      * End of BG2487
     A                                      CF03
      * Start of BG2487
     A                                      INDARA
      * End of BG2487
     A                                      PRINT(MPRINT)
     A          R RE3212F1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A*
     A*
     A            USER      R        O  1  2
     A                                  1 13'RE3212'
     A N05N06                           1 24'FEATURES NOT USED AT THIS INSTALLA-
     A                                      TION'
     A N05N06                               DSPATR(UL)
     A N05N06                               DSPATR(HI)
     A  05N06                           1 34'VAT SETUP'
     A  05N06                               DSPATR(UL)
     A  05N06                               DSPATR(HI)
     A N05 06                           1 30'TAUX D''USURE SETUP'
     A N05 06                               DSPATR(UL)
     A N05 06                               DSPATR(HI)
     A  05 06                           1 27'VAT AND TAUX D''USURE SETUP'
     A  05 06                               DSPATR(UL)
     A  05 06                               DSPATR(HI)
     A            RUNA      R        O  1 72
     A            JOB       R        O  2  2REFFLD(JOJOB)
     A                                  2 71TIME
     A                                 23  2'F3=End of job    Enter to continue'
     A  05                              3 29'VAT Receivable Account'
     A  05                              4 02'Branch'
     A  05        #1BRCA         3A  B  4 09
      * Start of change BG2487
     A  05N11                               DSPATR(UL)
     A**05******                            DSPATR(HI)
     A  05N11                               DSPATR(HI)
     A  11                                  DSPATR(RI)
     A  11                                  DSPATR(PC)
      * End of change BG2487
     A  05                              4 14'Customer'
     A  05        #1CNUM         6N  B  4 23
      * Start of change BG2487
     A  05N12                               DSPATR(UL)
     A**05******                            DSPATR(HI)
     A  05N12                               DSPATR(HI)
     A  12                                  DSPATR(RI)
     A  12                                  DSPATR(PC)
      * End of change BG2487
     A  05                              4 31'Currency'
     A  05        #1CCY          3A  B  4 40
      * Start of change BG2487
     A  05N13                               DSPATR(UL)
     A**05******                            DSPATR(HI)
     A  05N13                               DSPATR(HI)
     A  13                                  DSPATR(RI)
     A  13                                  DSPATR(PC)
      * End of change BG2487
     A**Start of change CGL029
     A**05******                        4 45'Account Code'                                    CGL029
     A**05******  #1ACOD         4N  B  4 58                                                  CGL029
     A  05                              4 45'A/C Code'
     A  05        #1ACOD        10N  B  4 54
     A**End of change CGL029
      * Start of change BG2487
     A  05N14                               DSPATR(UL)
     A**05******                            DSPATR(HI)
     A  05N14                               DSPATR(HI)
     A  14                                  DSPATR(RI)
     A  14                                  DSPATR(PC)
      * End of change BG2487
     A**Start of change CGL029
     A**05******                        4 64'Account Seq'                                     CGL029
     A**05******  #1ACSQ         2N  B  4 76                                                  CGL029
     A  05                              4 66'A/C Seq'
     A  05        #1ACSQ         2N  B  4 74
     A**End of change CGL029
      * Start of change BG2487
     A  05N15                               DSPATR(UL)
     A**05******                            DSPATR(HI)
     A  05N15                               DSPATR(HI)
     A  15                                  DSPATR(RI)
     A  15                                  DSPATR(PC)
      * End of change BG2487
     A  05                              6 02'Auto-post VAT Y/N'
     A  05        #1BTVA         1A  B  6 22
      * Start of change BG2487
     A  05N16                               DSPATR(UL)
     A**05******                            DSPATR(HI)
     A  05N16                               DSPATR(HI)
     A  16                                  DSPATR(RI)
     A  16                                  DSPATR(PC)
      * End of change BG2487
     A  05                              6 27'VAT rate'
     A  05        #1XTVA         5Y 2B  6 37EDTWRD('0  .  ')
      * Start of change BG2487
     A  05N17                               DSPATR(UL)
     A**05******                            DSPATR(HI)
     A  05N17                               DSPATR(HI)
     A  17                                  DSPATR(RI)
     A  16                                  DSPATR(PC)
      * End of change BG2487
     A  06                             10  2'Taux Usure'
     A  06                             10 13':'
     A  06                             10 36'Foreign Corr.'
     A  06                             10 50':'
     A  06                             12  2'Reg. Bank'
     A  06                             12 12':'
     A  06                             12 36'Banque Emission'
     A  06                             12 52':'
     A  06                             14  2'Credit Pop./Mut.'
     A  06                             14 19':'
     A  06                             14 36'Organ. Fin. Fran.'
     A  06                             14 54':'
     A  06                             16  2'Etab. Fin. Fran.'
     A  06                             16 19':'
     A  06                             16 31'Head Office'
     A  06                             16 43':'
     A  06                             18  2'Agence'
     A  06                             18  9':'
     A  06                             18 25'Societes'
     A  06                             20  2'Particuliers'
     A  06                             20 15':'
     A  06                             20 28'Divers'
     A  06                             20 35':'
     A  06                             20 49'Soc. Investiss.'
     A  06                             20 65':'
     A  06                             18 34':'
     A  06                             18 49'Org. Assurances'
     A  06                             16 56'Dir. Bqe'
     A  06                             16 65':'
     A  06                             18 65':'
     A  06        #1USUR         4Y 2B 10 15EDTWRD('  .  ')
     A  06                                  DSPATR(HI)
     A  06        #1RGB1         2A  B 12 14
     A  06                                  DSPATR(HI)
     A  06        #1RGB2         2A  B 12 17
     A  06                                  DSPATR(HI)
     A  06        #1RGB3         2A  B 12 20
     A  06                                  DSPATR(HI)
     A  06        #1BMS1         2A  B 12 54
     A  06                                  DSPATR(HI)
     A  06        #1BMS2         2A  B 12 57
     A  06                                  DSPATR(HI)
     A  06        #1BMS3         2A  B 12 60
     A  06                                  DSPATR(HI)
     A  06        #1CPM1         2A  B 14 21
     A  06                                  DSPATR(HI)
     A  06        #1CPM2         2A  B 14 24
     A  06                                  DSPATR(HI)
     A  06        #1CPM3         2A  B 14 27
     A  06                                  DSPATR(HI)
     A  06        #1ORG1         2A  B 14 56
     A  06                                  DSPATR(HI)
     A  06        #1ORG2         2A  B 14 59
     A  06                                  DSPATR(HI)
     A  06        #1ORG3         2A  B 14 62
     A  06                                  DSPATR(HI)
     A  06        #1ETA1         2A  B 16 21
     A  06                                  DSPATR(HI)
     A  06        #1ETA2         2A  B 16 24
     A  06                                  DSPATR(HI)
     A  06        #1ETA3         2A  B 16 27
     A  06                                  DSPATR(HI)
     A  06        #1HEA1         2A  B 16 45
     A  06                                  DSPATR(HI)
     A  06        #1HEA2         2A  B 16 48
     A  06                                  DSPATR(HI)
     A  06        #1HEA3         2A  B 16 51
     A  06                                  DSPATR(HI)
     A  06        #1DIR1         2A  B 16 67
     A  06                                  DSPATR(HI)
     A  06        #1DIR2         2A  B 16 70
     A  06                                  DSPATR(HI)
     A  06        #1DIR3         2A  B 16 73
     A  06                                  DSPATR(HI)
     A  06        #1AGC1         2A  B 18 11
     A  06                                  DSPATR(HI)
     A  06        #1AGC2         2A  B 18 14
     A  06                                  DSPATR(HI)
     A  06        #1AGC3         2A  B 18 17
     A  06                                  DSPATR(HI)
     A  06        #1SOC1         2A  B 18 36
     A  06                                  DSPATR(HI)
     A  06        #1SOC2         2A  B 18 39
     A  06                                  DSPATR(HI)
     A  06        #1SOC3         2A  B 18 42
     A  06                                  DSPATR(HI)
     A  06        #1ASS1         2A  B 18 67
     A  06                                  DSPATR(HI)
     A  06        #1ASS2         2A  B 18 70
     A  06                                  DSPATR(HI)
     A  06        #1ASS3         2A  B 18 73
     A  06                                  DSPATR(HI)
     A  06        #1PAR1         2A  B 20 17
     A  06                                  DSPATR(HI)
     A  06        #1PAR2         2A  B 20 20
     A  06                                  DSPATR(HI)
     A  06        #1PAR3         2A  B 20 23
     A  06                                  DSPATR(HI)
     A  06        #1DIV1         2A  B 20 37
     A  06                                  DSPATR(HI)
     A  06        #1DIV2         2A  B 20 40
     A  06                                  DSPATR(HI)
     A  06        #1DIV3         2A  B 20 43
     A  06                                  DSPATR(HI)
     A  06        #1INV1         2A  B 20 67
     A  06                                  DSPATR(HI)
     A  06        #1INV2         2A  B 20 70
     A  06                                  DSPATR(HI)
     A  06        #1INV3         2A  B 20 73
     A  06                                  DSPATR(HI)
     A  06        #1FOR1         2A  B 10 52
     A  06                                  DSPATR(HI)
     A  06        #1FOR2         2A  B 10 55
     A  06                                  DSPATR(HI)
     A  06        #1FOR3         2A  B 10 58
     A  06                                  DSPATR(HI)
     A  06        #1FOR4         2A  B 10 61
     A  06                                  DSPATR(HI)
     A  06        #1FOR5         2A  B 10 64
     A  06                                  DSPATR(HI)
     A  06        #1FOR6         2A  B 10 67
     A  06                                  DSPATR(HI)
      * Start of BG2487
     A          R RE3212S0                  TEXT('MESSAGE SUBFILE')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SMGKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
     A          R RE3212C0                  TEXT('CONTROL RECORD')
     A*
     A                                      SFLCTL(RE3212S0)
     A                                      SFLSIZ(10)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A            SPGMQ                     SFLPGMQ
