      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Retail linked account maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas   Retail  Module
     A*                                                               *
     A*  DSPF/RE3270DF - RETAIL LINKED ACCOUNT MAINTENANCE            *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 116520             Date 09Jan98               *
      *                 062527             Date 15Nov93               *
     A*                 S01413 *CREATE     DATE 13APR93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  116520 - Wrong Help tags referenced in DDS.                  *
     A*  062527 - Change link and unlink dates to DDMMYY format       *
     A*  S01413 - Retail 3 Incorporation                              *
     A*                                                               *
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      REF(MFRFL)
     A*/COPY WNCPYSRC,RE3270DF16
     A*                                              Begin /116520/
     A*                                              End   /116520/
     A*/COPY WNCPYSRC,RE3270DF17
     A          R RE3270F1
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A                                      KEEP
     A                                      HOME
     A                                      CF03
     A                                      CF04
     A                                      CF12
     A*
     A*/COPY WNCPYSRC,RE3270DF18
     A*                                              Begin /116520/
     A*/COPY WNCPYSRC,RE3270DF01
     A*                                              End   /116520/
     A*
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE3270'
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 25'LINKED ACCOUNTS MAINTENANCE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                  4  2'Retail No.'
     A  20                                  DSPATR(ND)
     A                                  4 14'or'
     A  20                                  DSPATR(ND)
     A                                  4 18'Branch'
     A                                  4 26'Cust No'
     A                                  4 35'Ccy'
     A**Start of change CGL029
     A**********                        4 40'Code'                                            CGL029
     A**********                        4 46'Seq.'                                            CGL029
     A                                  4 40'A/C Code'
     A                                  4 52'Seq.'
     A**End of change CGL029
     A            SACNO     R        B  6  2REFFLD(RETL)
     A  20                                  DSPATR(ND)
     A N11N03                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  11                                  DSPATR(PC RI)
     A  20
     AO 03                                  DSPATR(PR)
     A**03        SBRCA     R        B  6 18REFFLD(BRCA)
     A**03                                  DSPATR(PR)
     A**03                                  DSPATR(HI)
     A**03 10                               DSPATR(PC RI)
     A            SBRCA     R        B  6 19REFFLD(BRCA)
     A N10N03                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  10                                  DSPATR(PC RI)
     A  03                                  DSPATR(PR)
     A            SCNUM     R        B  6 26REFFLD(CNUMA)
     A N06N03                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  06                                  DSPATR(PC RI)
     A  03                                  DSPATR(PR)
     A            SCCY      R        B  6 35REFFLD(CCY)
     A N07N03                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  07                                  DSPATR(PC RI)
     A  03                                  DSPATR(PR)
     A**Start of change CGL029
     A**********  SACOD     R        B  6 40REFFLD(ACODA)                                     CGL029
     A            SACOD         10   B  6 40
     A**End of change CGL029
     A N08N03                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  08                                  DSPATR(PC RI)
     A  03                                  DSPATR(PR)
     A**Start of change CGL029
     A**********  SACSQ     R        B  6 46REFFLD(ACSQA)                                     CGL029
     A            SACSQ     R        B  6 52REFFLD(ACSQA)
     A**End of change CGL029
     A N09N03                               DSPATR(UL)
     A                                      DSPATR(HI)
     A  09                                  DSPATR(PC RI)
     A  03                                  DSPATR(PR)
     A**Start of change CGL029
     A**********                        6 53'Secondary account'                               CGL029
     A                                  6 59'Secondary account'
     A**End of change CGL029
     A N02        PACNO     R        B  8  2REFFLD(RETL)
     A  20                                  DSPATR(ND)
     A*/COPY WNCPYSRC,RE3270DF02
     A N02N14                               DSPATR(UL)
     A*/COPY WNCPYSRC,RE3270DF03
     A N02                                  DSPATR(HI)
     A N02 14                               DSPATR(PC RI)
     A  20
     AON02 04                               DSPATR(PR)
     A N02        PBRCD     R        B  8 19REFFLD(BRCA)
     A*/COPY WNCPYSRC,RE3270DF04
     A N02N18                               DSPATR(UL)
     A*/COPY WNCPYSRC,RE3270DF05
     A N02N18                               DSPATR(HI)
     A N02 04                               DSPATR(PR)
     A N02 18                               DSPATR(PC RI)
     A N02                             12 20'     Link date'
     A N02        PCNUM     R        B  8 26REFFLD(CNUMA)
     A N02                                  DSPATR(HI)
     A*/COPY WNCPYSRC,RE3270DF06
     A N02N19                               DSPATR(UL)
     A*/COPY WNCPYSRC,RE3270DF07
     A N02 19                               DSPATR(PC RI)
     A N02 04                               DSPATR(PR)
     A N02        PCCY      R        B  8 35REFFLD(CCY)
     A N02                                  DSPATR(HI)
     A*/COPY WNCPYSRC,RE3270DF19
     A                                      DSPATR(PR)
     A*/COPY WNCPYSRC,RE3270DF20
     A**Start of change CGL029
     A*N02******  PACOD     R        B  8 40REFFLD(ACODA)                                     CGL029
     A N02        PACOD         10   B  8 40
     A**End of change CGL029
     A*/COPY WNCPYSRC,RE3270DF08
     A N02N12                               DSPATR(UL)
     A*/COPY WNCPYSRC,RE3270DF09
     A N02                                  DSPATR(HI)
     A N02 12                               DSPATR(PC RI)
     A N02 04                               DSPATR(PR)
     A**Start of change CGL029
     A*N02******  PACSQ     R        B  8 46REFFLD(ACSQA)                                     CGL029
     A N02        PACSQ     R        B  8 52REFFLD(ACSQA)
     A**End of change CGL029
     A*/COPY WNCPYSRC,RE3270DF10
     A N02N13                               DSPATR(UL)
     A*/COPY WNCPYSRC,RE3270DF11
     A N02                                  DSPATR(HI)
     A N02 13                               DSPATR(PC RI)
     A N02 04                               DSPATR(PR)
     A**Start of change CGL029
     A*N02******                        8 53'Primary account'                                 CGL029
     A N02                              8 59'Primary account'
     A**End of change CGL029
     A**********         START OF CHANGE                   062527
     A*N02******  PVALL          7A  B 12 35                              062527
     A N02        PVALL          6A  B 12 35
     A*                  END OF CHANGE                     062527
     A N02N15N05                            DSPATR(UL)
     A N02                                  DSPATR(HI)
     A N02 15                               DSPATR(PC RI)
     A N02 05                               DSPATR(PR)
     A N01                             15 18'     Unlink date'
     A**********         START OF CHANGE                   062527
     A*N01******  PVALU          7A  B 15 35                              062527
     A N01        PVALU          6A  B 15 35
     A*                  END OF CHANGE                     062527
     A N01N16N17                            DSPATR(UL)
     A N01                                  DSPATR(HI)
     A N01 16                               DSPATR(PC)
     A N01 17                               DSPATR(PR)
     A*/COPY WNCPYSRC,RE3270DF12
     A                                 23  2'F3=End of job    '
     A                                 23 29'F12=Initial screen'
     A N21                             23 56'F4=Unlinking'
     A          R RE3270E0                  SFL
     A                                      SFLMSGRCD(24)
     A            DDSFKY                    SFLMSGKEY
     A            DDPGMQ                    SFLPGMQ
     A**
     A          R RE3270F0                  SFLCTL(RE3270E0)
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0001)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A N81                                  SFLEND
     A            DDPGMQ                    SFLPGMQ
     A          R RE3270HE
     A*
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE3270'
     A            SRUNA     R        O  1 72REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 25'LINKED ACCOUNT MAINTENANCE'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                  5  6'Customer'
     A            SBRCA     R        O  5 15REFFLD(BRCA)
     A                                      DSPATR(HI)
     A            SCNUM     R        O  5 19REFFLD(CNUMA)
     A                                      DSPATR(HI)
     A            SCCY      R        O  5 26REFFLD(CCY)
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  SACOD     R        O  5 30REFFLD(ACODA)                                     CGL029
     A            SACOD         10   O  5 30
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  SACSQ     R        O  5 35REFFLD(ACSQA)                                     CGL029
     A            SACSQ     R        O  5 41REFFLD(ACSQA)
     A**End of change CGL029
     A                                      DSPATR(HI)
     A                                  5 44'Retail No'
     A  20                                  DSPATR(ND)
     A            SACNO     R        O  5 54REFFLD(RETL)
     A                                      DSPATR(HI)
     A  20                                  DSPATR(ND)
     A                                  7  5'Select'
     A                                  7 68'Lnk Date'
     A*/COPY WNCPYSRC,RE3270DF13
     A          R RE3270PB                  SFL
     A            SELECT         1A  B 10  8DSPATR(UL)
     A                                      DSPATR(HI)
     A            DBRCD     R        O 10 15REFFLD(BRCA)
     A                                      DSPATR(HI)
     A            DCNUM     R        O 10 19REFFLD(CNUMA)
     A                                      DSPATR(HI)
     A            DCCY      R        O 10 26REFFLD(CCY)
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  DACOD     R        O 10 30REFFLD(ACODA)                                     CGL029
     A            DACOD         10   O 10 30
     A**End of change CGL029
     A                                      DSPATR(HI)
     A**Start of change CGL029
     A**********  DACSQ     R        O 10 35REFFLD(ACSQA)                                     CGL029
     A            DACSQ     R        O 10 41REFFLD(ACSQA)
     A**End of change CGL029
     A                                      DSPATR(HI)
     A            DACNO     R        O 10 54REFFLD(RETL)
     A                                      DSPATR(HI)
     A  20                                  DSPATR(ND)
     A            DVALL          7A  O 10 68DSPATR(HI)
     A*/COPY WNCPYSRC,RE3270DF14
     A          R RE3270PC                  OVERLAY
     A                                      SFLCTL(RE3270PB)
     A                                      SFLSIZ(80)
     A                                      SFLPAG(10)
     A*
     A N50                                  SFLDSP
     A N50                                  SFLDSPCTL
     A  50                                  SFLCLR
     A  82                                  SFLEND
     A*
     A                                      CF03(03)
     A                                      CF12(12)
     A            RN             4S 0H      SFLRCDNBR
     A*
     A*/COPY WNCPYSRC,RE3270DF15
     A                                 23 02'F3=End of job    '
     A                                 23 29'F12=Initial screen'
