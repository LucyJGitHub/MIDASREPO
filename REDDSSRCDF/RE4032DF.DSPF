      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE A/C Passbook Print and Update')               *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                    *
     A*                                                               *
     A*  DSPF/RE4032DF - Savings A/C Passbook Update Print            *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG17966           Date 25Apr08               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 090199             Date 07Jul95               *
     A*                 CRT001  *CREATE    DATE 28FEB95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  BUG17966 - Screen looks as if text is overwritten            *
     A*  090199 - RE-COMPILATION TO CORRECT MULTI-LANGUAGE            *
     A*           REFERENCES.                                         *
     A*                                                               *
     A*  CRT001 - Retail Teller System                                *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A****************************************************************
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
      *
      *---------------------------------------------------------------------
     A***  SAVINGS A/C PASSBOOK UPD/PRT
     A          R RE4032F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
      ** Start of change BUG17966
     A**********                            CLRL(*NO)
      ** End of change BUG17966
     A                                      PUTOVR
     A                                      CF03
     A                                      CF05
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4032'
     A                                  1 28'PASSBOOK UPDATE REQUEST'
     A                                      DSPATR(HI UL)
     A                                  1 67'DATE '
     A            SMRDT     R        O  1 73REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                      EDTWRD('0 :  :  ')
      *
     A                                 04  6'          '
     A                                 04 18'                    '
     A                                 04 40'   '
      *
     A                                 13  6'Enter next'
      *
     A                                 14  6'Retail a/c number'
     A            SRACNO        10A  B 14 24TEXT('RETAIL A/C NO')
     A                                      DSPATR(HI)
     A N11                                  DSPATR(UL)
     A  11                                  DSPATR(PC RI)
      *
     A                                 15  6'Line number'
     A            SLINO          3  0B 15 24TEXT('LINE NUMBER')
     A                                      DSPATR(HI)
     A N12                                  DSPATR(UL)
     A  12                                  DSPATR(PC RI)
      *
     A                                 16  6'B/F passbook bal.'
     A            SBFBAL        14A  B 16 24TEXT('B/F PASSBOOK BAL.')
     A                                      DSPATR(HI)
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(PC RI)
      *
     A                                 23  2'F3=Exit  F5=Refresh'
     A                                 23 23'            '
      *
      *---------------------------------------------------------------------
     A          R RE4032F1                  TEXT('SAVINGS A/C PASSBOOK UPD/PRT')
     A                                      OVERLAY
      ** Start of change BUG17966
     A**********                            CLRL(*NO)
      ** End of change BUG17966
     A                                      PUTOVR
     A     15                               ALARM
     A                                      CF01
     A                                      CF03
     A* *IN15 - MORE THAN ONE PAGE
     A* CMD/6 ADDED FOR CHANGE PAGE
     A     15                               CF06
     A    N14                               CF12
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4032'
     A                                  1 28'PASSBOOK UPDATE REQUEST'
     A                                      DSPATR(HI UL)
     A                                  1 67'DATE '
      *
     A            SMRDT     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                      EDTWRD('0 :  :  ')
      *
      *
     A*/COPY WNCPYSRC,RE4032DF01
     A            SACNO         10A  O 04  6TEXT('ACCOUNT NUMBER')
     A                                      DSPATR(HI)
     A            SANAM         20   O 04 18TEXT('ACCOUNT NAME')
     A                                      DSPATR(HI)
     A            SCCY           3   O 04 40TEXT('ACCOUNT CURRENCY')
     A                                      DSPATR(HI)
     A                                 06  2'Last line printed'
     A            SLLPR          3  0O 06 21TEXT('LAST LINE PRINTED')
     A                                      DSPATR(HI)
      *
     A                                 06 25'Last passbook bal.'
     A            SCFBAL        22A  O 06 44TEXT('FILE PASSBOOK BAL.')
     A                                      DSPATR(HI)
      *
     A*/COPY WNCPYSRC,REH00031
     A                                 14  6'Retail a/c number'
     A            SRACNO        10A  O 14 24TEXT('RETAIL A/C NO')
     A                                      DSPATR(HI)
      *
     A                                 15  6'Line number'
     A            SLINO          3  0O 15 24TEXT('LINE NUMBER')
     A                                      DSPATR(HI)
      *
     A                                 16  6'B/F passbook bal.'
     A            SBFBAL        14A  O 16 24TEXT('B/F PASSBOOK BAL.')
     A                                      DSPATR(HI)
      *
     A*/COPY WNCPYSRC,RE4032DF02
     A N15                             23  2'F1=Accept  F3=Exit'
     A    N14                          23 22'F12=Previous'
     A  15                             23 22'F3=Exit  F6=Turn Page'
      *
      *---------------------------------------------------------------------
     A          R RE4032S0                  TEXT('MESSAGE SUBFILE')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SMGKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      *---------------------------------------------------------------------
     A          R RE4032C0                  TEXT('CONTROL RECORD')
      *
     A                                      SFLCTL(RE4032S0)
     A                                      SFLSIZ(10)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
      *---------------------------------------------------------------------
      ****************************************************************
