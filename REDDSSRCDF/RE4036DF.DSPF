      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE T&N Rate Changes at rollover - Prompt')
      *****************************************************************
      *                                                               *
      *  Midas - Retail T&N Accounts                                  *
      *                                                               *
      *  RE4036DF - Midas RE T&N Rate Changes at rollover - Prompt    *
      *             details display                                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CRE015 *CREATE     Date 18Dec09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE015 - Retail Term and Notice Accounts                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      PRINT(*LIBL/MPRINT)
      *
     A          R RE4036HH
     A            PSUSER        10A  O  1  2
     A            PSPROCNAME    10A  O  1 13
     A            AGMRDT         7A  O  1 72
     A            PSJOBNAME     10A  O  2  2
     A                                  2 25'Term & Notice Rate Changes'
     A                                      DSPATR(HI)
     A                                  2 71TIME
      *
     A          R RE4036DD                  TEXT('Details Screen')
     A                                      CA03(03)
     A                                      OVERLAY
     A                                  5  2'Type choices, press Enter.'
     A                                      COLOR(BLU)
     A                                  8  2'Start Date . . . . . . .'
     A            STDATE         6   B  8 29
     A                                      DSPATR(UL)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N61                                  DSPATR(HI)
     A                                  9  2'End Date . . . . . . . .'
     A                                 10  2'Mode of Operation  . . .'
     A            ENDATE         6   B  9 29
     A                                      DSPATR(UL)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A N62                                  DSPATR(HI)
     A            OPMODE         5   B 10 29
     A                                      DSPATR(UL)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A N63                                  DSPATR(HI)
     A                                  8 42'Date DDMMYY or prev run date'
     A                                  9 42'Date DDMMYY or run date'
     A                                 10 42'AUDIT or UPDATE'
      *
     A          R RE4036FF                  TEXT('Program Footer')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
      *
     A          R MSGSFL                    SFL
     A                                      TEXT('Program messages')
     A                                      SFLMSGRCD(24)
     A            SCMGKY                    SFLMSGKEY
     A            PSPROCNAME                SFLPGMQ(10)
      *
     A          R MSGCTL                    SFLCTL(MSGSFL)
     A                                      TEXT('Program messages')
     A                                      OVERLAY
     A                                      BLINK
     A                                      PUTOVR
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A  10                                  SFLEND
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0001)
     A            PSPROCNAME                SFLPGMQ(10)
