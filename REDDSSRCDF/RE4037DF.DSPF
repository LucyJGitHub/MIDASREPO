      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE T&N Account Code Check - Prompt')
      *****************************************************************
      *                                                               *
      *  Midas - Retail T&N Accounts                                  *
      *                                                               *
      *  DSPF/RE4037DF - Midas RE T&N Account Code Check - Prompt     *
      *                                                               *
      *  Function:   This display enables the prompt screen of the    *
      *              Term and Notice Account Code Check.              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CRE015 *CREATE     Date 18Dec09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE015 - Retail Term and Notice Accounts                     *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      INDARA
      *
     A          R RE4037HH                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2009')
      *
     A                                      PUTOVR
      *
     A                                      OVERLAY
     A            @USER         10A  O  1  2
     A            @PGM          10A  O  1 13
     A            SCDAT#         7A  O  1 72
     A            @JOB          10A  O  2  2TEXT('WORKSTATION ID')
     A                                  2 25'Term & Notice Account Code +
     A                                       Check'
     A                                      DSPATR(HI)
     A                                  2 71TIME
     A                                      EDTWRD('0 :  :  ')
     A                                  5  2'Type choices, press Enter.'
      *
     A          R RE4037DD
     A                                      CA03(03)
     A                                      OVERLAY
     A                                  8  2'Mode of operation'
     A            PMODE          7   B  8 25
     A                                      DSPATR(UL)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A N61                                  DSPATR(HI)
     A                                  8 41'*AUDIT, *CRTDLT, *CREATE, +
     A                                       *DELETE'
      *
     A          R RE4037FF                  TEXT('Program Footer')
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
      *
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            SCMGKY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ(10)
      *
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(0001) SFLSIZ(0020)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
      *
     A  10                                  SFLEND
     A            @PGM                      SFLPGMQ(10)
