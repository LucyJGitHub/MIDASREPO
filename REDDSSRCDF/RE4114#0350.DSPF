      *****************************************************************
/*STD *  DSPFBASE                                                     *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE4114#0 - Midas RE Cash/Cheque Deposit in A/C currency      *
      *                                                               *
      *  (C) Copyright Midas-Kapiti International Ltd. 2000           *
      *                                                               *
      *  Last Amend No. MD046354           Date 13Oct17               *
      *  Prev Amend No. EPM004  *CREATE    Date 08May00               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046354 - Add window processing for Retail Teller System    *
      *             transaction screens.                              *
      *  EPM004 - Addition of extended narrative field to the Retail  *
      *           Teller Transaction Details                          *
      *                                                               *
      *****************************************************************
     A                                      PRINT
     A                                      DSPSIZ(27 132)
     A                                      HELP
     A                                      HLPDOC(Hxxx@yyy MMINS ABSENG/DBAR2)
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'Reset')
     A                                      CF12(12 'Previous')
     A          R WINDOWF                   TEXT('(C) Midas-Kapiti +
     A                                      International Ltd. 1997')
     A                                      WINDOW(&SWROW &SWCOL 8 67)
     A                                      USRRSTDSP
     A                                      KEEP
     A                                      OVERLAY
     A** Start of Change MD046354
     A                                      PUTOVR
     A** End of Change MD046354
     A          H                           HLPARA(03 01 03 67)
     A                                      HLPDOC(Hxxx@yyy MMINS ABSENG/DBAR2)
     A            ##PGM         10A  H      TEXT('*PROGRAM')
     A                                  1 13'RE4114F1'
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
     A            STITL         50A  O  1  1OVRDTA
      *----------------------------------------------------------------
     A                                  3  1'Extended Narrative :'
     A                                      OVRATR
     A            SEXNA         60A  B  5  5OVRDTA
      *----------------------------------------------------------------
     A                                  7  1'F3=Main Menu'
     A                                  7 24'F5=Refresh'
     A                                  7 46'F12=Previous'
     A*=========================================================================
      *    Dummy record to prevent full-screen display being cleared
     A          R ASSUMEF
     A                                      ASSUME
     A                                  1  3' '
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
