      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Foreign Exchange Transactions Detail Wind')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  DSPF/RE4120#0 - Foreign Exchange Transactions Detail Window. *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT002 *CREATE     Date  1Dec95               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRT002 - Retail Branch Access.                               *
      *                                                               *
      *****************************************************************
     A                                      PRINT(MPRINT)
     A                                      CA03(03 'Exit')
     A                                      CF05(05 'Reset')
     A                                      CF12(12 'Previous')
     A          R RE4120F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      WINDOW(&SWROW &SWCOL 11 48)
     A                                      WDWBORDER((*COLOR WHT) (*DSPATR HI)-
     A                                       (*CHAR '...:::.:'))
     A                                      KEEP
     A                                      OVERLAY
     A            SWROW          3S 0P
     A            SWCOL          3S 0P
      *
      ** Replace fields below with required fields for new window
      *
     A                                  2  4'Cheque issuer no:'
     A                                  3  4'Cheque serial no:'
     A                                  4  4'Buyer''s Id type :'
     A                                  5  4'Buyer''s Id no   :'
     A                                  6  4'Buyer''s name    :'
     A                                  7  4'Full cheque no. :'
      *
     A            SISSU          6   B  2 22TEXT('Cheque issuer no.')
     A N46                                  DSPATR(HI)
     A  46                                  DSPATR(RI PC)
     A            STCSN         12   B  3 22TEXT('Cheque serial no.')
     A N47N49                               DSPATR(HI)
     A  47
     AO 49                                  DSPATR(RI PC)
     A            SIDTP          2   B  4 22TEXT('Buyer''s ID type')
     A N48                                  DSPATR(HI)
     A  48                                  DSPATR(RI PC)
     A            SBYID         20   B  5 22TEXT('Buyer''s ID no.')
     A                                      DSPATR(HI)
     A            SBYNM         20   B  6 22TEXT('Buyer''s name')
     A                                      DSPATR(HI)
     A            STCNO         24   B  7 22TEXT('Full cheque no.')
     A                                      DSPATR(HI)
      *
     A                                  9  4'F3=Exit'
     A                                  9 13'F5=Refresh'
     A                                  9 25'F12=Previous'
      *
      **   Dummy record to prevent full-screen display being cleared
      *
     A          R RE4120F1
     A                                      KEEP
     A                                      ASSUME
     A                                      OVERLAY
     A                                      PUTOVR
     A            DUMMYF         1A  O  1 80DSPATR(ND)
      *
     A          R RE4120F2                  TEXT('Program messages')
     A                                      SFL SFLMSGRCD(10)
     A            MSGKEY                    SFLMSGKEY
     A            PGMNAM                    SFLPGMQ
      *
     A          R RE4120F3                  TEXT('Program messages')
     A                                      WINDOW(RE4120F0)
     A                                      SFLCTL(RE4120F2)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            PGMNAM                    SFLPGMQ
