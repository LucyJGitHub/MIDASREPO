      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Charges and credit')                          *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Teller System                                 *
     A*                                                               *
     A*  DSPF/RE4121DF - Charges and Credits                          *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG15976A          Date 25Apr08               *
      *                 BUG15976           Date 04Apr08               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CRT026             Date 04Oct05               *
      *                 CAAA03             Date 03May04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 093546             Date 12Sep95               *
      *                 090199             Date 07Jul95               *
     A*                 CRT001  *CREATE    DATE 28FEB95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG15976A - Remove CLRL(*NO) since it causes error messages  *
      *              to overlap with details screen in webfacing      *
      *  BUG15976 - Screen looks as if text is overwritten            *
      *  CRT026 - Retail Teller Password Change                       *
      *  CAAA03 - Webfacing changes.                                  *
     A*  093546 - Change 'Ledger Balance' text to 'Today's Projected  *
     A*           Ledger Balance'.  Align other fields.               *
     A*  090199 - RE-COMPILATION TO CORRECT MULTI-LANGUAGE            *
     A*           REFERENCES.                                         *
     A*                                                               *
     A*  CRT001 - Retail Teller System.                               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      REF(MFRFL)
      **Start of change CAAA03
     A                                      DSPSIZ(*DS4)
     A**********                            MSGLOC(24)                                        CAAA03
     A                                      MSGLOC(25)
      **End of change CAAA03
     A*
     A*----------------------------------------------------------------
     A*
     A**  Initial Screen
     A*
     A          R RE4121F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
      ** Start of change BUG15976A
     A**********                            CLRL(*NO)
      ** End of change BUG15976A
     A                                      PUTOVR
     A                                      CF03
     A                                      CF05
     A                                      CF10
     A*
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4121'
     A                                  1 30'CHARGES AND CREDITS'
     A                                      DSPATR(HI UL)
     A                                  1 67'DATE '
     A            SMRDT     R        O  1 73REFFLD(RUNA)
     A*
     A            SWID      R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME EDTWRD('  :  :  ')
      **Start of change CAAA03
     A  22        SMSG1         72   O 09 06TEXT('Message Array 1')
     A                                      DSPATR(HI RI)
     A  22        SMSG2         72   O 10 06TEXT('Message Array 2')
     A                                      DSPATR(HI RI)
      **End of change CAAA03
     A*
     A**  Transaction completion details
     A*
     A  45                             10 06'TRANS NO. COMPLETED -'
     A                                      DSPATR(HI)
     A  45        STTLU          5  0O 10 28TEXT('TELLER TRANSACTION NUMBER')
     A                                      DSPATR(HI)
     A  41                             11 06'ACCOUNT CLOSED'
     A                                      DSPATR(HI)
     A  42                             11 06'ACCOUNT NOT CLOSED'
     A                                      DSPATR(HI)
      **Start of change CAAA03
     A  20                             11 06'TRANSACTION TERMINATED'
     A                                      DSPATR(HI)
     A                                 13 06'Enter next'
      **End of change CAAA03
      **Start of change BUG15976
     A  21                             11 06'OVERRIDE REQUIRED'
     A                                      DSPATR(HI)
      **End of change BUG15976
     A*
     A**  A/C number prompt
     A*
     A                                 14  6'Retail a/c number '
     A            SACNO1        10   B 14 30TEXT('RETAIL A/C NO.')
     A                                      DSPATR(HI)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                      OVRDTA
     A*
     A                                 15  6'Currency code'
     A            SCCY1          3   B 15 30TEXT('CURRENCY')
     A                                      DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                      OVRDTA
     A*
     A                                 23  2'F3=Exit   F5=Refresh'
     A                                 23 25'F10=A/C closure enquiry'
     A*
     A*----------------------------------------------------------------
     A*
      **Start of change BUG15976
     A**********R RE4121F1                  TEXT('WARNING/TERMINAL SCREEN')                 BUG15976
      **Start*of*change*CAAA03                                                              BUG15976
     A**********                            OVERLAY                                         BUG15976
      **End*of*change*CAAA03                                                                BUG15976
     A**********                            CLRL(14)                                        BUG15976
      **Start*of*change*CAAA03                                                              BUG15976
     A**********                            PUTOVR                                          BUG15976
      **End*of*change*CAAA03                                                                BUG15976
      **End of change BUG15976
     A*/COPY WNCPYSRC,RE4121DF08
     A*
      **Start of change CAAA03                                                                CAAA03
     A**********  SACNO2        10   O 04 06TEXT('RETAIL A/C NO.')                            CAAA03
     A**********                            DSPATR(HI)                                        CAAA03
     A**********  SANAM         20   O 04 18TEXT('ACCOUNT NAME')                              CAAA03
     A**********                            DSPATR(HI)                                        CAAA03
     A**********  SCCY2          3   O 04 40TEXT('ACCOUNT CURRENCY')                          CAAA03
     A**********                            DSPATR(HI)                                        CAAA03
     A**********                                                                              CAAA03
      **End of change CAAA03
     A*/COPY WNCPYSRC,RE4121DF01
      **Start of change CAAA03                                                                CAAA03
     A**29******                       05 06'Interest'                                        CAAA03
     A**********                                     Begin /093546/                           CAAA03
      **End of change CAAA03
     A**29******  SINTR         22   O 05 28TEXT('NET INTEREST')
      **Start of change CAAA03                                                                CAAA03
     A**29******  SINTR         22   O 05 46TEXT('NET INTEREST')                              CAAA03
     A**********                                     End   /093546/                           CAAA03
     A**********                            DSPATR(HI)                                        CAAA03
     A**********                                                                              CAAA03
     A**29*76***                       06 06'Withholding Tax'                                 CAAA03
     A**********                                     Begin /093546/                           CAAA03
      **End of change CAAA03
     A**29*76***  SWTAX         22   O 06 28TEXT('WITHHOLDING TAX')
      **Start of change CAAA03                                                                CAAA03
     A**29*76***  SWTAX         22   O 06 46TEXT('WITHHOLDING TAX')                           CAAA03
     A**********                                     End   /093546/                           CAAA03
     A**********                            DSPATR(HI)                                        CAAA03
     A**********                                                                              CAAA03
     A**29******                       07 06'Service Charge'                                  CAAA03
     A**********                                     Begin /093546/                           CAAA03
      **End of change CAAA03
     A**29******  SCHRG         22   O 07 28TEXT('SERVICE CHARGE')
      **Start of change CAAA03                                                                CAAA03
     A**29******  SCHRG         22   O 07 46TEXT('SERVICE CHARGE')                            CAAA03
     A**********                                     End   /093546/                           CAAA03
     A**********                            DSPATR(HI)                                        CAAA03
     A**********                                                                              CAAA03
     A**29******                       08 06'Close balance'                                   CAAA03
     A**********                                     Begin /093546/                           CAAA03
      **End of change CAAA03
     A**29******  SCLOB         22   O 08 28TEXT('CLOSING BALANCE')
      **Start of change CAAA03                                                                CAAA03
     A**29******  SCLOB         22   O 08 46TEXT('CLOSING BALANCE')                           CAAA03
     A**********                                     End   /093546/                           CAAA03
     A**********                            DSPATR(HI)                                        CAAA03
     A**********                                                                              CAAA03
      **End of change CAAA03
     A*/COPY WNCPYSRC,RE4121DF02
      **Start of change CAAA03                                                                CAAA03
     A**22******  SMSG1         72   O 09 06TEXT('MESSAGE ARRAY 1')                           CAAA03
     A**********                            DSPATR(HI RI)                                     CAAA03
     A**********                                                                              CAAA03
     A**22******  SMSG2         72   O 10 06TEXT('MESSAGE ARRAY 2')                           CAAA03
     A**********                            DSPATR(HI RI)                                     CAAA03
     A**********                                                                              CAAA03
      **End of change CAAA03
      **Start of change BUG15976
     A**20******                       11 06'TRANSACTION TERMINATED'                        BUG15976
     A**********                            DSPATR(HI)                                      BUG15976
     A**21******                       11 06'OVERRIDE REQUIRED'                             BUG15976
     A**********                            DSPATR(HI)                                      BUG15976
     A*
     A*/COPY WNCPYSRC,RE4121DF07
     A**********                       13 06'Enter next'                                    BUG15976
      **End of change BUG15976
     A*
     A*----------------------------------------------------------------
     A*
     A          R RE4121F2                  TEXT('DETAIL SCREEN')
     A                                      OVERLAY
      ** Start of change BUG15976A
     A**********                            CLRL(*NO)
      ** End of change BUG15976A
     A                                      PUTOVR
     A                                      CF03
     A                                      CF05
     A                                      CF12
     A*
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4121'
     A                                  1 30'CHARGES AND CREDITS'
     A                                      DSPATR(HI UL)
     A                                  1 67'DATE '
     A            SMRDT     R        O  1 73REFFLD(RUNA)
     A*
     A            SWID      R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME EDTWRD('  :  :  ')
     A*
     A*/COPY WNCPYSRC,RE4121DF03
      **Start of change CAAA03
     A            SACNO2        10   O 04 06TEXT('Retail A/C No.')
     A                                      DSPATR(HI)
     A            SANAM         20   O 04 18TEXT('Account Name')
     A                                      DSPATR(HI)
     A            SCCY2          3   O 04 40TEXT('Account Currency')
     A                                      DSPATR(HI)
     A  29                             05 06'Interest'
     A  29        SINTR         22   O 05 46TEXT('Net Interest')
     A                                      DSPATR(HI)
     A  29 76                          06 06'Withholding Tax'
     A  29 76     SWTAX         22   O 06 46TEXT('Withholding Tax')
     A                                      DSPATR(HI)
     A  29                             07 06'Service Charge'
     A  29        SCHRG         22   O 07 46TEXT('Service Charge')
     A                                      DSPATR(HI)
     A  29                             08 06'Close balance'
     A  29        SCLOB         22   O 08 46TEXT('Closing Balance')
     A                                      DSPATR(HI)
     A  22        SMSG1         72   O 09 06TEXT('Message Array 1')
     A                                      DSPATR(HI RI)
     A  22        SMSG2         72   O 10 06TEXT('Message Array 2')
     A                                      DSPATR(HI RI)
     A  20                             11 06'TRANSACTION TERMINATED'
     A                                      DSPATR(HI)
     A  21                             11 06'OVERRIDE REQUIRED'
     A                                      DSPATR(HI)
      **End of change CAAA03
     A                                 13  6'Transaction type 1'
     A*
     A            STYP1          5   B 13 25TEXT('TRANSACTION TYPE 1')
     A                                      DSPATR(HI)
     A     27N32                            DSPATR(UL)
     A        32                            DSPATR(PC)
     A        32                            DSPATR(RI)
     A                                      OVRDTA
     A*
     A                                 14  6'Amount 1'
     A*
     A            STAM1         14   B 14 25TEXT('TRANSACTION AMOUNT 1')
     A                                      DSPATR(HI)
     A     27N33                            DSPATR(UL)
     A        33                            DSPATR(PC)
     A        33                            DSPATR(RI)
     A                                      OVRDTA
     A*
     A                                 15  6'Transaction type 2'
     A*
     A            STYP2          5   B 15 25TEXT('TRANSACTION TYPE 2')
     A                                      DSPATR(HI)
     A     27N34                            DSPATR(UL)
     A        34                            DSPATR(PC)
     A        34                            DSPATR(RI)
     A                                      OVRDTA
     A*
     A                                 16  6'Amount 2'
     A*
     A            STAM2         14   B 16 25TEXT('TRANSACTION AMOUNT 2')
     A                                      DSPATR(HI)
     A     27N35                            DSPATR(UL)
     A        35                            DSPATR(PC)
     A        35                            DSPATR(RI)
     A                                      OVRDTA
     A*
     A                                 17  6'Value date'
     A*
     A            SVLDT          6   B 17 25TEXT('VALUE DATE')
     A                                      DSPATR(HI)
     A     27N36                            DSPATR(UL)
     A        36                            DSPATR(PC)
     A        36                            DSPATR(RI)
     A                                      OVRDTA
     A*
     A                                 18  6'Dept code'
     A*
     A            SDEPC          3   B 18 25TEXT('DEPARTMENT CODE')
     A                                      DSPATR(HI)
     A     27N37                            DSPATR(UL)
     A        37                            DSPATR(PC)
     A        37                            DSPATR(RI)
     A                                      OVRDTA
     A*
     A        16                       19  6'Profit Centre'
     A*
     A            SPRFC          4A  B 19 25TEXT('PROFIT CENTRE')
     A                                      DSPATR(HI)
     A     27N15                            DSPATR(UL)
     A        15                            DSPATR(PC)
     A        15                            DSPATR(RI)
     A       N16                            DSPATR(ND PR)
     A*
     A*/COPY WNCPYSRC,RE4121DF04
     A                                 23  2'F3=Exit   F5=Refresh'
     A                                 23 25'F12=Previous'
     A*
     A*----------------------------------------------------------------
     A*
     A          R RE4121F3                  TEXT('OVERRIDE SCREEN')
     A                                      PROTECT
     A                                      OVERLAY
      ** Start of change BUG15976A
     A**********                            CLRL(*NO)
      ** End of change BUG15976A
     A                                      PUTOVR
     A*/COPY WNCPYSRC,RE4121DF09
     A                                      CF01
     A                                      CF02
     A                                      CF03
     A                                      CF12
      **Start of change BUG15976
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4121'
     A                                  1 30'CHARGES AND CREDITS'
     A                                      DSPATR(HI UL)
     A                                  1 67'DATE '
     A            SMRDT     R        O  1 73REFFLD(RUNA)
     A*
     A            SWID      R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME EDTWRD('  :  :  ')
     A*
     A            SACNO2        10   O 04 06TEXT('Retail A/C No.')
     A                                      DSPATR(HI)
     A            SANAM         20   O 04 18TEXT('Account Name')
     A                                      DSPATR(HI)
     A            SCCY2          3   O 04 40TEXT('Account Currency')
     A                                      DSPATR(HI)
     A  29                             05 06'Interest'
     A  29        SINTR         22   O 05 46TEXT('Net Interest')
     A                                      DSPATR(HI)
     A  29 76                          06 06'Withholding Tax'
     A  29 76     SWTAX         22   O 06 46TEXT('Withholding Tax')
     A                                      DSPATR(HI)
     A  29                             07 06'Service Charge'
     A  29        SCHRG         22   O 07 46TEXT('Service Charge')
     A                                      DSPATR(HI)
     A  29                             08 06'Close balance'
     A  29        SCLOB         22   O 08 46TEXT('Closing Balance')
     A                                      DSPATR(HI)
      **End of change BUG15976
     A*
     A*/COPY WNCPYSRC,RE4121DF05
     A*                                              Begin /093546/
     A**28******                       05  6'Ledger balance'
     A**28******  SLDBL         22   O 05 28TEXT('LEDGER BALANCE')
     A  28                             05  6'Today''s Projected +
     A                                      Ledger Balance'
     A  28        SLDBL         22   O 05 46TEXT('LEDGER BALANCE')
     A*                                              End   /093546/
     A                                      DSPATR(HI)
     A*
     A*/COPY WNCPYSRC,REH00034
     A  28                             06  6'Available balance'
     A*/COPY WNCPYSRC,REH00035
     A*                                              Begin /093546/
     A**28******  SAVBL         22   O 06 28TEXT('LEDGER BALANCE')
     A*/COPY WNCPYSRC,REH00036
     A  28        SAVBL         22   O 06 46TEXT('LEDGER BALANCE')
     A*/COPY WNCPYSRC,REH00037
     A*                                              End   /093546/
     A                                      DSPATR(HI)
     A*
     A  28                             07 06'             '
     A  28                             07 24'                         '
      **Start of change CAAA03
     A  22        SMSG1         72   O 09 06TEXT('Message Array 1')
     A                                      DSPATR(HI RI)
     A  22        SMSG2         72   O 10 06TEXT('Message Array 2')
     A                                      DSPATR(HI RI)
     A  20                             11 06'TRANSACTION TERMINATED'
     A                                      DSPATR(HI)
     A  21                             11 06'OVERRIDE REQUIRED'
     A                                      DSPATR(HI)
     A                                 13  6'Transaction type 1'
     A            STYP1          5   O 13 25TEXT('Transaction Type 1')
     A                                      DSPATR(HI)
     A                                 14  6'Amount 1'
     A            STAM1         14   O 14 25TEXT('Transaction Amount 1')
     A                                      DSPATR(HI)
     A                                 15  6'Transaction type 2'
     A            STYP2          5   O 15 25TEXT('Transaction Type 2')
     A                                      DSPATR(HI)
     A                                 16  6'Amount 2'
     A            STAM2         14   O 16 25TEXT('Transaction Amount 2')
     A                                      DSPATR(HI)
     A                                 17  6'Value date'
     A            SVLDT          6   O 17 25TEXT('Value Date')
     A                                      DSPATR(HI)
     A                                 18  6'Dept code'
     A            SDEPC          3   O 18 25TEXT('Department Code')
     A                                      DSPATR(HI)
     A        16                       19  6'Profit Centre'
     A            SPRFC          4A  O 19 25TEXT('Profit Centre')
     A                                      DSPATR(HI)
     A       N16                            DSPATR(ND)
      **End of change CAAA03
     A*
     A  26                             21  6'Override teller id'
     A  26        SOVTI          3   B 21 26TEXT('OVERRIDE TELLER ID')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N38                                  DSPATR(UL)
     A  38                                  DSPATR(RI)
     A*
     A  26                             21 31'passcode'
     A  26        SOVPC          6   B 21 40TEXT('OVERRIDE PASSCODE')
     A                                      OVRDTA
     A                                      DSPATR(ND)
     A  39                                  DSPATR(RI)
      ** Start of change CRT026
      *
     A  54                             21  6'Override user id?'
     A  54        SOVUR         10   B 21 24TEXT('OVERRIDE USER ID')
     A                                      DSPATR(HI)
     A N38                                  DSPATR(UL)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A  54                             21 36'Password?'
     A  54        SOVPW         10   B 21 46TEXT('OVERRIDE PASSWORD')
     A                                      DSPATR(ND)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A                                      OVRDTA
      *
      ** End of change CRT026
     A*
     A*/COPY WNCPYSRC,RE4121DF06
     A                                 23  2'F1=Override   F2=Display balance-
     A                                         F3=Exit   F12=Previous'
     A*
     A*----------------------------------------------------------------
     A*
     A          R RE4121S0                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
      **Start of change CAAA03
     A**********                            SFLMSGRCD(24)                                     CAAA03
     A                                      SFLMSGRCD(25)
      **End of change CAAA03
     A            SMGKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
     A*
     A*----------------------------------------------------------------
     A*
     A          R RE4121C0                  TEXT('CONTROL RECORD')
     A*
     A                                      SFLCTL(RE4121S0)
     A                                      SFLSIZ(10)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A            SPGMQ                     SFLPGMQ
     A*
