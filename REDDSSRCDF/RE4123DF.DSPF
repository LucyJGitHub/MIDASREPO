      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Account Closure Display')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  DSPF/RE4123DF - Account Closure                              *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CRT026             Date 04Oct05               *
      *  Prev Amend No. CAAA03             Date 22Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 090199             Date 07Jul95               *
      *                 CRT001  *CREATE    Date 28Feb95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CRT026 - Retail Teller Password Change                       *
      *  CAAA03 - Webfacing Changes                                   *
     A*  090199 - RE-COMPILATION TO CORRECT MULTI-LANGUAGE            *
     A*           REFERENCES.                                         *
     A*                                                               *
     A*  CRT001 - Retail Teller System                                *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
      *
      *---------------------------------------------------------------------
     A          R RE4123F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A                                      CA03
     A                                      CA05
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4123'
     A                                  1 25'ACCOUNT CLOSURE, ZERO BALANCE'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
      ** A/C number prompt
     A                                 14  6'Retail a/c number '
     A            SACNO         10   B 14 30TEXT('RETAIL A/C NO.')
     A                                      DSPATR(HI)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
      *
     A                                 15  6'Currency code'
     A            SCCY           3   B 15 30TEXT('CURRENCY')
     A                                      DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
      *
     A                                 23  2'F3=Exit  F5=Refresh'
      *
      *---------------------------------------------------------------------
     A          R RE4123F1                  TEXT('WARNING/TERMINAL')
     A                                      OVERLAY
     A                                      CA03
     A                                      CA05
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4123'
     A                                  1 25'ACCOUNT CLOSURE, ZERO BALANCE'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A            SACNO1        10   O 04 06TEXT('RETAIL A/C NO.')
     A                                      DSPATR(HI)
     A            SANAM         20   O 04 18TEXT('ACCOUNT NAME')
     A                                      DSPATR(HI)
     A            SCCY1          3   O 04 40TEXT('ACCOUNT CURRENCY')
     A                                      DSPATR(HI)
      *
     A            SMSG1         72   O 08 06TEXT('MESSAGE ARRAY 1')
     A                                      DSPATR(HI RI)
      *
     A            SMSG2         72   O 09 06TEXT('MESSAGE ARRAY 2')
     A                                      DSPATR(HI RI)
      *
     A                                 10 06'TRANSACTION TERMINATED'
     A                                      DSPATR(HI)
     A                                 13 06'Enter next'
      *
      ** A/C number prompt
     A                                 14  6'Retail a/c number '
     A            SACNO         10   B 14 30TEXT('RETAIL A/C NO.')
     A                                      DSPATR(HI)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
      *
     A                                 15  6'Currency code'
     A            SCCY           3   B 15 30TEXT('CURRENCY')
     A                                      DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
      *
     A                                 23  2'F3=Exit  F5=Refresh'
      *
     A          R RE4123F2                  TEXT('Trans.Completed')
     A                                      OVERLAY
     A                                      CA03
     A                                      CA05
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4123'
     A                                  1 25'ACCOUNT CLOSURE, ZERO BALANCE'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A  43                             10 06'TRANS NO. COMPLETED -'
     A                                      DSPATR(HI)
     A  43        STTLU          5  0O 10 28TEXT('TELLER TRANSACTION NUMBER')
     A                                      DSPATR(HI)
     A  41                             11 06'ACCOUNT CLOSED'
     A                                      DSPATR(HI)
     A  42                             11 06'ACCOUNT NOT CLOSED'
     A                                      DSPATR(HI)
     A                                 13 06'Enter next'
      *
      ** A/C number prompt
     A                                 14  6'Retail a/c number '
     A            SACNO         10   B 14 30TEXT('RETAIL A/C NO.')
     A                                      DSPATR(HI)
     A N30                                  DSPATR(UL)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
      *
     A                                 15  6'Currency code'
     A            SCCY           3   B 15 30TEXT('CURRENCY')
     A                                      DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
      *
     A                                 23  2'F3=Exit  F5=Refresh'
      *
     A          R RE4123F3                  TEXT('WARNING/TERMINAL')
     A                                      OVERLAY
     A                                      CA03
     A                                      CA05
     A                                      CA10
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4123'
     A                                  1 25'ACCOUNT CLOSURE, ZERO BALANCE'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A            SACNO1        10   O 04 06TEXT('RETAIL A/C NO.')
     A                                      DSPATR(HI)
     A            SANAM         20   O 04 18TEXT('ACCOUNT NAME')
     A                                      DSPATR(HI)
     A            SCCY1          3   O 04 40TEXT('ACCOUNT CURRENCY')
     A                                      DSPATR(HI)
     A                                 05 06'Interest'
     A            SINTR         22   O 05 18TEXT('NET INTEREST')
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,RE4123DF01
      *
     A  76                             06 06'W/H Tax'
     A  76        SWTAX         22   O 06 18TEXT('WITHHOLDING TAX')
     A                                      DSPATR(HI)
      *
     A                                 07 06'S/Charge'
     A            SCHRG         22   O 07 18TEXT('SERVICE CHARGE')
     A                                      DSPATR(HI)
      *
     A*/COPY WNCPYSRC,RE4123DF05
     A                                 08 06'Close bal.'
     A            SCLOB         22   O 08 18TEXT('CLOSING BALANCE')
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,RE4123DF06
      *
     A            SMSG1         72   O 09 06TEXT('MESSAGE ARRAY 1')
      ** Start of change CAAA03
     A**********                            DSPATR(HI RI)                                     CAAA03
     A                                      DSPATR(HI)
     A  21                                  DSPATR(RI)
      ** End of change CAAA03
      *
     A            SMSG2         72   O 10 06TEXT('MESSAGE ARRAY 2')
      ** Start of change CAAA03
     A**********                            DSPATR(HI RI)                                     CAAA03
     A                                      DSPATR(HI)
     A  21                                  DSPATR(RI)
      ** End of change CAAA03
     A*/COPY WNCPYSRC,RE4123DF02
      *
     A                                 23  2'F3=Exit  F5=Refresh'
     A                                 23 23'F10=Delete'
      *
     A          R RE4123F5                  TEXT('OVERIDE SCREEN')
     A                                      PROTECT
     A                                      OVERLAY
     A                                      CF01
     A                                      CA03
     A                                      CA12
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4123'
     A                                  1 25'ACCOUNT CLOSURE, ZERO BALANCE'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A            SACNO         10   O  4 06TEXT('RETAIL A/C NO.')
     A                                      DSPATR(HI)
     A            SANAM         20   O 04 18TEXT('ACCOUNT NAME')
     A                                      DSPATR(HI)
     A            SCCY           3   O 04 40TEXT('ACCOUNT CURRENCY')
     A                                      DSPATR(HI)
     A                                 05 06'Interest'
     A            SINTR         22   O 05 18TEXT('NET INTEREST')
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,RE4123DF03
      *
     A                                 06 06'S/Charge'
     A            SCHRG         22   O 06 18TEXT('SERVICE CHARGE')
     A                                      DSPATR(HI)
      *
     A*/COPY WNCPYSRC,RE4123DF07
     A                                 07 06'Close bal.'
     A            SCLOB         22   O 07 18TEXT('CLOSING BALANCE')
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,RE4123DF09
      *
     A            SMSG1         72   O 08 06TEXT('MESSAGE ARRAY 1')
     A                                      DSPATR(HI RI)
     A*/COPY WNCPYSRC,RE4123DF08
      *
     A            SMSG2         72   O 09 06TEXT('MESSAGE ARRAY 2')
     A                                      DSPATR(HI RI)
      **Start of change CRT026
     A  54
     AO 26                             10 06'OVERRIDE REQUIRED'
     A  54
     AO 26                                  DSPATR(HI)
     A**26******                       10 06'OVERRIDE REQUIRED'                               CRT026
     A**26******                            DSPATR(HI)                                        CRT026
      **End of change CRT026
     A*/COPY WNCPYSRC,RE4123DF04
      *
      *---------------------------------------------------------------------
      *
     A  26                             21  6'Override teller id?'
     A  26        SOVTI          3   B 21 26TEXT('OVERRIDE TELLER ID')
     A  26                                  DSPATR(HI)
     A  26N38                               DSPATR(UL)
     A  26 38                               DSPATR(RI)
      *
     A  26                             21 31'passcode?'
     A  26        SOVPC          6A  B 21 42TEXT('OVERRIDE PASSCODE')
     A  26                                  DSPATR(ND)
     A  26 39                               DSPATR(RI)
      ** Start of change CRT026
      *
     A  54                             21  6'Override user id?'
     A  54        SOVUR         10   B 21 24TEXT('OVERRIDE USER ID')
     A                                      DSPATR(HI)
     A N38                                  DSPATR(UL)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
      *
     A  54                             21 36'Password?'
     A  54        SOVPW         10   B 21 46TEXT('OVERRIDE PASSWORD')
     A                                      DSPATR(ND)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
      *
      ** End of change CRT026
      *
     A                                 23  2'F1=Override  F3=Exit -
     A                                       F12=Previous'
      *
      *---------------------------------------------------------------------
     A          R RE4123S0                  TEXT('MESSAGE SUBFILE')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SMGKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      *---------------------------------------------------------------------
     A          R RE4123C0                  TEXT('CONTROL RECORD')
      *
     A                                      SFLCTL(RE4123S0)
     A                                      SFLSIZ(20)
     A                                      SFLPAG(1)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A                                      PUTOVR
     A                                      BLINK
     A            SPGMQ                     SFLPGMQ
      *
      ****************************************************************
