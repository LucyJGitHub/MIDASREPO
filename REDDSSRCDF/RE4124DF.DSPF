      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Transaction Reversals')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  DSPF/RE4124DF - Transaction Reversals                        *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CRT026             Date 04Oct05               *
      *  Prev Amend No. CAAA03             Date 23Apr04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 093546             Date 12Sep95               *
     A*                 090199             Date 07Jul95               *
     A*                 CRT001  *CREATE    DATE 28FEB95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  CRT026 - Retail Teller Password Change                       *
      *  CAAA03 - Webfacing Changes                                   *
     A*  093546 - Change 'Ledger Balance' text to 'Today's Projected  *
     A*           Ledger Balance'.  Align other fields.               *
     A*  090199 - RE-COMPILATION TO CORRECT MULTI-LANGUAGE            *
     A*           REFERENCES.                                         *
     A*                                                               *
     A*  CRT001 - Retail Teller System                                *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
      *
      *---------------------------------------------------------------------
     A          R RE4124F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
      ** Start of change CAAA03
     A**********                            CLRL(*NO)                                         CAAA03
      ** End of change CAAA03
     A                                      PUTOVR
     A                                      CF03
     A                                      CF05
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4124'
      *
     A                                  1 29'TRANSACTION REVERSALS'
     A                                      DSPATR(HI UL)
      *
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWID      R        O  2  2REFFLD(JOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
      *
     A                                 13  6'Enter next '
      *
     A                                 14  6'Batch number'
     A            SBATNO         3A  B 14 24TEXT('BATCH NO')
     A                                      DSPATR(HI)
     A N11                                  DSPATR(UL)
     A  11                                  DSPATR(PC RI)
      *
     A                                 15  6'Transaction no.'
     A            STTNO          5A  B 15 24TEXT('TRANSACTION NO.')
     A                                      DSPATR(HI)
     A N12                                  DSPATR(UL)
     A  12                                  DSPATR(PC RI)
     A*/COPY WNCPYSRC,RE4124DF08
      *
     A                                 16  6'Retail a/c number'
     A            SRACNO        10A  B 16 24TEXT('RETAIL A/C NO.')
     A                                      DSPATR(HI)
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(PC RI)
      *
     A                                 17  6'Currency'
     A            SCCY           3A  B 17 24TEXT('CURRENCY')
     A                                      DSPATR(HI)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(PC RI)
      *
     A                                 18  6'Amount'
     A            SAMT          14A  B 18 24TEXT('AMOUNT')
     A                                      DSPATR(HI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PC RI)
     A*/COPY WNCPYSRC,RE4124DF09
      *
     A                                 23  2'F3=Exit  F5=Refresh'
      *
      *---------------------------------------------------------------------
     A          R RE4124F1                  TEXT('INITIAL TERM.COND SCREEN')
     A                                      OVERLAY
     A                                      CLRL(*NO)
     A                                      PUTOVR
     A                                      CF03
     A                                      CF05
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4124'
      *
     A                                  1 29'TRANSACTION REVERSALS'
     A                                      DSPATR(HI UL)
      *
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWID      R        O  2  2REFFLD(JOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A            SMSG1         72   O 09 06TEXT('MESSAGE ARRAY 1')
     A                                      DSPATR(HI RI)
      *
     A            SMSG2         72   O 10 06TEXT('MESSAGE ARRAY 2')
     A                                      DSPATR(HI RI)
      *
     A                                 11 06'TRANSACTION TERMINATED'
     A                                      DSPATR(HI)
      *
     A                                 13  6'Enter next '
      *
     A                                 14  6'Batch number'
     A            SBATNO         3A  B 14 24TEXT('BATCH NO')
     A                                      DSPATR(HI)
     A N11                                  DSPATR(UL)
     A  11                                  DSPATR(PC RI)
      *
     A                                 15  6'Transaction no.'
     A            STTNO          5A  B 15 24TEXT('TRANSACTION NO.')
     A                                      DSPATR(HI)
     A N12                                  DSPATR(UL)
     A  12                                  DSPATR(PC RI)
     A*/COPY WNCPYSRC,RE4124DF10
      *
     A                                 16  6'Retail a/c number'
     A            SRACNO        10A  B 16 24TEXT('RETAIL A/C NO.')
     A                                      DSPATR(HI)
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(PC RI)
      *
     A                                 17  6'Currency'
     A            SCCY           3A  B 17 24TEXT('CURRENCY')
     A                                      DSPATR(HI)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(PC RI)
      *
     A                                 18  6'Amount'
     A            SAMT          14A  B 18 24TEXT('AMOUNT')
     A                                      DSPATR(HI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PC RI)
     A*/COPY WNCPYSRC,RE4124DF11
      *
     A                                 23  2'F3=Exit  F5=Refresh'
      *
      *---------------------------------------------------------------------
     A          R RE4124F2                  TEXT('TRANS.COMPLETED SCREEN')
     A                                      OVERLAY
     A                                      CLRL(*NO)
     A                                      PUTOVR
     A                                      CF03
     A                                      CF05
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4124'
      *
     A                                  1 29'TRANSACTION REVERSALS'
     A                                      DSPATR(HI UL)
      *
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWID      R        O  2  2REFFLD(JOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
      *
     A                                 11 06'TRANS NO. COMPLETED -'
     A                                      DSPATR(HI)
     A            @TTRNO         5S 0O 11 28TEXT('TELLER TRANS NO')
     A                                      DSPATR(HI)
      *
     A                                 13  6'Enter next '
      *
     A                                 14  6'Batch number'
     A            SBATNO         3A  B 14 24TEXT('BATCH NO')
     A                                      DSPATR(HI)
     A N11                                  DSPATR(UL)
     A  11                                  DSPATR(PC RI)
      *
     A                                 15  6'Transaction no.'
     A            STTNO          5A  B 15 24TEXT('TRANSACTION NO.')
     A                                      DSPATR(HI)
     A N12                                  DSPATR(UL)
     A  12                                  DSPATR(PC RI)
     A*/COPY WNCPYSRC,RE4124DF12
      *
     A                                 16  6'Retail a/c number'
     A            SRACNO        10A  B 16 24TEXT('RETAIL A/C NO.')
     A                                      DSPATR(HI)
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(PC RI)
      *
     A                                 17  6'Currency'
     A            SCCY           3A  B 17 24TEXT('CURRENCY')
     A                                      DSPATR(HI)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(PC RI)
      *
     A                                 18  6'Amount'
     A            SAMT          14A  B 18 24TEXT('AMOUNT')
     A                                      DSPATR(HI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(PC RI)
     A*/COPY WNCPYSRC,RE4124DF13
      *
     A                                 23  2'F3=Exit  F5=Refresh'
      *
      *---------------------------------------------------------------------
     A          R RE4124F3                  TEXT('ACC.TRANS.REVERSAL SCREEN')
     A                                      OVERLAY
      ** Start of change CAAA03
     A**********                            CLRL(*NO)                                         CAAA03
      ** End of change CAAA03
     A                                      PUTOVR
     A*/COPY WNCPYSRC,RE4124DF06
     A                                      CF01
     A                                      CF02
     A                                      CF03
     A                                      CF12
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4124'
      *
     A                                  1 29'TRANSACTION REVERSALS'
     A                                      DSPATR(HI UL)
      *
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWID      R        O  2  2REFFLD(JOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A*/COPY WNCPYSRC,RE4124DF05
      *
     A            @IACNO        10A  O  4  6TEXT('ACCOUNT NUMBER')
     A                                      DSPATR(HI)
      *
     A            @ACNM         20A  O  4 18TEXT('ACCOUNT NAME')
     A                                      DSPATR(HI)
      *
     A            @ICCY          3A  O  4 40TEXT('CURRENCY')
     A                                      DSPATR(HI)
      *
     A                                 05 06'Function'
     A            @FNCD          3A  O 05 27TEXT('FUNCTION CODE')
     A                                      DSPATR(HI)
     A            @FNNM         30A  O 05 32TEXT('FUNCTION DESCRIPTION')
     A                                      DSPATR(HI)
      *
     A*/COPY WNCPYSRC,RE4124DF01
     A  09                             06 06'A/c ccy equivalent'
     A*                                              Begin /093546/
     A**09******  @SCYEQ        17A  O 06 27TEXT('A/c CCY EQUIVALENT')
     A  09        @SCYEQ        17A  O 06 46TEXT('A/c CCY EQUIVALENT')
     A*                                              End   /093546/
     A                                      DSPATR(HI)
      *
     A*                                              Begin /093546/
     A**23******                       07 06'Ledger Balance'
     A**23******  @LEDBL        22A  O 07 24TEXT('LEDGER BALANCE')
     A  23                             07 06'Today''s Projected +
     A                                      Ledger Balance'
     A  23        @LEDBL        22A  O 07 46TEXT('LEDGER BALANCE')
     A*                                              End   /093546/
     A                                      DSPATR(HI)
      *
     A  23                             08 06'Avail. Balance'
     A*                                              Begin /093546/
     A**23******  @AVLBL        22A  O 08 24TEXT('AVAIL. BALANCE')
     A  23        @AVLBL        22A  O 08 46TEXT('LEDGER BALANCE')
     A*                                              End   /093546/
     A                                      DSPATR(HI)
      *
     A  24        SMSG1         72   O 09 06TEXT('MESSAGE ARRAY 1')
     A                                      DSPATR(HI RI)
      *
     A  24        SMSG2         72   O 10 06TEXT('MESSAGE ARRAY 2')
     A                                      DSPATR(HI RI)
      *
      **Start of change CRT026
     A  54
     AO 21                             11 06'OVERRIDE REQUIRED'
     A**21******                       11 06'OVERRIDE REQUIRED'                               CRT026
      **End of change CRT026
     A                                      DSPATR(HI)
      **Start of change CRT026
     A*N21******                       11 06'VALIDATION COMPLETE'                             CRT026
     A N21N54                          11 06'VALIDATION COMPLETE'
      **End of change CRT026
     A                                      DSPATR(HI)
      *
      *
     A                                 14  6'Batch number'
     A            SBATNO         3A  O 14 24TEXT('BATCH NO')
     A                                      DSPATR(HI)
      *
     A                                 15  6'Transaction no.'
     A            STTNO          5A  O 15 24TEXT('TRANSACTION NO.')
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,RE4124DF14
      *
     A                                 16  6'Retail a/c number'
     A            SRACNO        10A  O 16 24TEXT('RETAIL A/C NO.')
     A                                      DSPATR(HI)
      *
     A                                 17  6'Currency'
     A            SCCY           3A  O 17 24TEXT('CURRENCY')
     A                                      DSPATR(HI)
      *
     A                                 18  6'Amount'
     A            SAMT          14A  O 18 24TEXT('AMOUNT')
     A                                      DSPATR(HI)
      *
      *
     A*/COPY WNCPYSRC,RE4124DF02
      *
      **Start of change CRT026
     A**********                       21  6'Override teller id?'                             CRT026
     A N54                             21  6'Override teller id?'
      **End change CRT026
     A N21                                  DSPATR(ND)
      **Start of change CRT026
     A**********  SOTLI          3A  B 21 26TEXT('OVERRIDE TELLER ID')                        CRT026
     A N54        SOTLI          3A  B 21 26TEXT('OVERRIDE TELLER ID')
      **End change CRT026
     A N21                                  DSPATR(ND PR)
     A  21                                  DSPATR(HI)
     A N11                                  DSPATR(UL)
     A  11                                  DSPATR(PC RI)
      *
      **Start of change CRT026
     A**********                       21 31'Passcode?'                                       CRT026
     A N54                             21 31'Passcode?'
      **End change CRT026
     A N21                                  DSPATR(ND)
      **Start of change CRT026
     A**********  SOTPC          6A  B 21 41TEXT('OVERRIDE TELLER PASS CODE')                 CRT026
     A N54        SOTPC          6A  B 21 41TEXT('OVERRIDE TELLER PASS CODE')
      **End change CRT026
     A                                      DSPATR(ND)
     A N21                                  DSPATR(PR)
     A N12                                  DSPATR(UL)
     A  12                                  DSPATR(PC RI)
      *
      ** Start of change CRT026
      *
     A  54                             21  6'Override user id?'
     A  54        SOVUR         10   B 21 24TEXT('OVERRIDE USER ID')
     A                                      DSPATR(HI)
     A N11                                  DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  11                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A  54                             21 36'Password?'
     A  54        SOVPW         10   B 21 46TEXT('OVERRIDE PASSWORD')
     A                                      DSPATR(ND)
     A  12                                  DSPATR(PC)
     A  12                                  DSPATR(RI)
     A                                      OVRDTA
      *
      ** End of change CRT026
     A                                 23  2'F1=Accept  F2=Display balance'
     A                                 23 33'F3=Exit  F12=Previous'
      *
      *---------------------------------------------------------------------
     A          R RE4124F4                  TEXT('FX -TRANS.REVERSAL SCREEN')
     A                                      OVERLAY
      ** Start of change CAAA03
     A**********                            CLRL(*NO)                                         CAAA03
      ** End of change CAAA03
     A                                      PUTOVR
     A*/COPY WNCPYSRC,RE4124DF07
     A                                      CF01
     A                                      CF03
     A                                      CF12
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4124'
      *
     A                                  1 29'TRANSACTION REVERSALS'
     A                                      DSPATR(HI UL)
      *
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWID      R        O  2  2REFFLD(JOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
      *
     A                                 05 06'Function'
     A            @FNCD          3A  O 05 27TEXT('FUNCTION CODE')
     A                                      DSPATR(HI)
     A            @FNNM         30A  O 05 32TEXT('FUNCTION DESCRIPTION')
     A                                      DSPATR(HI)
      *
     A*/COPY WNCPYSRC,RE4124DF03
     A                                 06 06'Sell ccy equivalent'
     A            @SCYEQ        17A  O 06 27TEXT('Sell CCY EQUIVALENT')
     A                                      DSPATR(HI)
     A            @SECCY         3A  O 06 47TEXT('Sell CCY')
     A                                      DSPATR(HI)
      *
     A  24        SMSG1         72   O 09 06TEXT('MESSAGE ARRAY 1')
     A                                      DSPATR(HI RI)
      *
     A  24        SMSG2         72   O 10 06TEXT('MESSAGE ARRAY 2')
     A                                      DSPATR(HI RI)
      *
     A*/COPY WNCPYSRC,RE4124DF04
      **Start of change CRT026
     A  54
     AO 21                             11 06'OVERRIDE REQUIRED'
     A**21******                       11 06'OVERRIDE REQUIRED'                               CRT026
      **End of change CRT026
     A                                      DSPATR(HI)
      **Start of change CRT026
     A*N21******                       11 06'VALIDATION COMPLETE'                             CRT026
     A N21N54                          11 06'VALIDATION COMPLETE'
      **End of change CRT026
     A                                      DSPATR(HI)
      *
     A                                 14  6'Batch number'
     A            SBATNO         3A  O 14 24TEXT('BATCH NO')
     A                                      DSPATR(HI)
      *
     A                                 15  6'Transaction no.'
     A            STTNO          5A  O 15 24TEXT('TRANSACTION NO.')
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,RE4124DF15
      *
     A                                 16  6'Retail a/c number'
     A            SRACNO        10A  O 16 24TEXT('RETAIL A/C NO.')
     A                                      DSPATR(HI)
      *
     A                                 17  6'Currency'
     A            SCCY           3A  O 17 24TEXT('CURRENCY')
     A                                      DSPATR(HI)
      *
     A                                 18  6'Amount'
     A            SAMT          14A  O 18 24TEXT('AMOUNT')
     A                                      DSPATR(HI)
     A*/COPY WNCPYSRC,RE4124DF16
      *
      **Start of change CRT026
     A**********                       21  6'Override teller id?'                             CRT026
     A N54                             21  6'Override teller id?'
      **End of change CRT026
     A N21                                  DSPATR(ND)
      **Start of change CRT026
     A**********  SOTLI          3A  B 21 26TEXT('OVERRIDE TELLER ID')                        CRT026
     A N54        SOTLI          3A  B 21 26TEXT('OVERRIDE TELLER ID')
      **End of change CRT026
     A N21                                  DSPATR(ND PR)
     A  21                                  DSPATR(HI)
     A N11                                  DSPATR(UL)
     A  11                                  DSPATR(PC RI)
      *
      **Start of change CRT026
     A**********                       21 31'Passcode?'                                       CRT026
     A N54                             21 31'Passcode?'
      **End of change CRT026
     A N21                                  DSPATR(ND)
      **Start of change CRT026
     A**********  SOTPC          6A  B 21 41TEXT('OVERRIDE TELLER PASS CODE')                 CRT026
     A N54        SOTPC          6A  B 21 41TEXT('OVERRIDE TELLER PASS CODE')
      **End of change CRT026
     A                                      DSPATR(ND)
     A N21                                  DSPATR(PR)
     A N12                                  DSPATR(UL)
     A  12                                  DSPATR(PC RI)
      *
      ** Start of change CRT026
      *
     A  54                             21  6'Override user id?'
     A  54        SOVUR         10   B 21 24TEXT('OVERRIDE USER ID')
     A                                      DSPATR(HI)
     A N11                                  DSPATR(UL)
     A  11                                  DSPATR(PC)
     A  11                                  DSPATR(RI)
     A                                      OVRDTA
      *
     A  54                             21 36'Password?'
     A  54        SOVPW         10   B 21 46TEXT('OVERRIDE PASSWORD')
     A                                      DSPATR(ND)
     A  12                                  DSPATR(PC)
     A  12                                  DSPATR(RI)
     A                                      OVRDTA
      *
      ** End of change CRT026
     A                                 23  2'F1=Accept  F3=Exit'
     A                                 23 25'F12=Previous'
      *
      *---------------------------------------------------------------------
     A          R RE4124S0                  TEXT('MESSAGE SUBFILE')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SMGKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      *---------------------------------------------------------------------
     A          R RE4124C0                  TEXT('CONTROL RECORD')
      *
     A                                      SFLCTL(RE4124S0)
     A                                      SFLSIZ(10)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
      *---------------------------------------------------------------------
      ****************************************************************
