      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Online Trans Journal Print')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  DSPF/RE4126DF - Online Transaction Journal Prompt            *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 090199             Date 07Jul95               *
      *                 CRT001  *CREATE    Date 28Feb95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  090199 - RE-COMPILATION TO CORRECT MULTI-LANGUAGE            *
     A*           REFERENCES.                                         *
     A*                                                               *
     A*  CRT001 - Retail Teller System                                *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
      *
      *---------------------------------------------------------------------
     A          R RE4126F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
     A                                      CF03
     A                                      CA12
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4126'
     A                                  1 30'TRANSACTION JOURNAL'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A            SPMPT         10A  O  5 24TEXT('Screen Prompt')
     A                                  5 35'. . . . . . . :'
     A            SENQY          3A  B  5 51TEXT('Teller/Batch No')
     A                                      DSPATR(HI)
     A N31                                  DSPATR(UL)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
      *
     A  32                              7 24'First transaction'
     A  32                              7 42'number :'
     A  32        SFROM          5  0B  7 51
     A  32                                  DSPATR(HI)
     A  32N62                               DSPATR(UL)
     A  32 62                               DSPATR(RI)
     A  32 62                               DSPATR(PC)
      *
     A  33                              9 24'Last  transaction'
     A  33                              9 42'number :'
     A  33        STO            5  0B  9 51
     A  33                                  DSPATR(HI)
     A  33N63                               DSPATR(UL)
     A  33 63                               DSPATR(RI)
     A  33 63                               DSPATR(PC)
      *
     A  34                             23  2'F3=Exit  F12=Previous Screen'
     A N34                             23  2'F3=Exit'
      *---------------------------------------------------------------------
     A*
     A** MESSAGE SUBFILE
     A*
     A          R RE4126S0                  TEXT('MESSAGE SUBFILE')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SMGKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      *---------------------------------------------------------------------
     A          R RE4126C0                  TEXT('CONTROL RECORD')
      *
     A                                      SFLCTL(RE4126S0)
     A                                      SFLSIZ(10)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
