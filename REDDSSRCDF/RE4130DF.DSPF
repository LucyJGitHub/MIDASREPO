      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Teller sign on/off display')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  DSPF/RE4130DF - Teller Sign On/Off Display                   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 090199             DATE 07JUL95               *
     A*  PREV AMEND NO. CRT001  *CREATE    DATE 28FEB95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  090199 - RE-COMPILATION TO CORRECT MULTI-LANGUAGE            *
     A*           REFERENCES.                                         *
     A*                                                               *
     A*  CRT001 - Retail Teller System.                               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      CF05
     A                                      MSGLOC(24)
     A*
     A*---------------------------------------------------------------------
     A*
     A**  Teller Sign On
     A*
     A          R RE4130F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A*
     A*
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4130'
     A N09                              1 34'TELLER SIGN ON'
     A                                       DSPATR(HI UL)
     A  09                              1 29'TELLER SIGN OFF OVERRIDE'
     A                                       DSPATR(HI UL)
     A                                  1 67'DATE '
     A            SMRDT     R        O  1 73REFFLD(RUNA)
     A*
     A            SWID      R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME EDTWRD('  :  :  ')
     A*
     A                                  5  2'Teller ID . . . . . . . :'
     A            STLID          3A  B  5 28TEXT('TELLER ID')
     A                                      DSPATR(HI)
     A N33N31                               DSPATR(UL)
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A  33N31                               DSPATR(PR)
     A*
     A                                  7  2'Teller pass code  . . : '
     A            STLPC          6A  I  7 28TEXT('TELLER PASS CODE')
     A                                      DSPATR(ND)
     A N32                                  DSPATR(UL)
     A  32                                  DSPATR(RI)
     A  32                                  DSPATR(PC)
     A*
     A                                 23  2'F3=Exit   F5=Refresh'
     A*
     A*---------------------------------------------------------------------
     A*
     A          R RE4130S0                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SMGKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
     A*
     A*---------------------------------------------------------------------
     A          R RE4130C0                  TEXT('CONTROL RECORD')
     A                                      SFLCTL(RE4130S0)
     A                                      SFLSIZ(20)
     A                                      SFLPAG(1)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A                                      PUTOVR
     A                                      BLINK
     A            SPGMQ                     SFLPGMQ
