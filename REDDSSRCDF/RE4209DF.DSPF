      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Clearing/Payroll input')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  DSPF/RE4209DF - Clearing Payroll Input                       *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
     A*  Last Amend No. BUG17964           DATE 28Apr08               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CRT026             Date 22May06               *
      *                 CSD027             Date 29Nov05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4.01 -------------------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 091241             Date 21Jul95               *
      *                 090199             Date 07Jul95               *
     A*                 CRT001  *CREATE    DATE 28FEB95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  BUG17964 - Screen looks as if text is overwritten            *
      *  CRT026 - Retail Teller Password Change                       *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  091241 - Bank Code field extended from 7A to 11A.            *
     A*  090199 - RE-COMPILATION TO CORRECT MULTI-LANGUAGE            *
     A*           REFERENCES.                                         *
     A*  CRT001 - Retail Teller System                                *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
      *
      *---------------------------------------------------------------------
      ** Common Details Screen
     A          R RE4209F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
      ** Start of change BUG17964
     A**********                            CLRL(*NO)
      ** End of change BUG17964
     A                                      PUTOVR
     A                                      CF03
     A                                      CF05
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4209'
      *
     A                                  1 21'CLEARING/PAYROLL COMMON +
     A                                      DETAILS INPUT'
     A                                      DSPATR(HI UL)
      *
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWID      R        O  2  2REFFLD(JOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                 04  6'Batch number'
     A            SBTNO          3   B 04 24TEXT('BATCH NUMBER')
     A                                      DSPATR(HI)
     A     27N30                            DSPATR(UL)
     A        30                            DSPATR(PC)
     A        30                            DSPATR(RI)
     A                                      OVRDTA
      *
     A                                 04 31'Action'
     A            SACTN          1   B 04 39TEXT('ACTION CODE')
     A                                      DSPATR(HI)
     A     27N31                            DSPATR(UL)
     A        31                            DSPATR(PC)
     A        31                            DSPATR(RI)
     A                                      OVRDTA
      *
     A                                 06 06'Branch code'
     A            SBRCH          3   B 06 24TEXT('BRANCH CODE')
     A                                      DSPATR(HI)
     A     27N32                            DSPATR(UL)
     A        32                            DSPATR(PC)
     A        32                            DSPATR(RI)
     A                                      OVRDTA
      *
     A                                 07 06'Currency code'
     A            SCCY           3   B 07 24TEXT('CURRENCY CODE')
     A                                      DSPATR(HI)
     A     27N33                            DSPATR(UL)
     A        33                            DSPATR(PC)
     A        33                            DSPATR(RI)
     A                                      OVRDTA
      *
     A                                 08 06'Transaction type'
     A            STTYP          5   B 08 24TEXT('TRANSACTION TYPE')
     A                                      DSPATR(HI)
     A     27N34                            DSPATR(UL)
     A        34                            DSPATR(PC)
     A        34                            DSPATR(RI)
     A                                      OVRDTA
      *
     A                                 09 06'Department'
     A            SDEPC          3   B 09 24TEXT('DEPARTMENT CODE')
     A     27N35                            DSPATR(UL)
     A        35                            DSPATR(PC)
     A        35                            DSPATR(RI)
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A                                 10 06'Value date'
     A            SVLDT          6   B 10 24TEXT('VALUE DATE')
     A                                      DSPATR(HI)
     A     27N36                            DSPATR(UL)
     A        36                            DSPATR(PC)
     A        36                            DSPATR(RI)
     A                                      OVRDTA
      *
     A        28                       11 06'Profit centre'
     A            SPRFC          4   B 11 24TEXT('PROFIT CENTRE')
     A                                      DSPATR(HI)
     A     27N37                            DSPATR(UL)
     A        37                            DSPATR(PC)
     A        37                            DSPATR(RI)
     A       N28                            DSPATR(ND PR)
     A                                      OVRDTA
      *
     A  21        SMSG1         72   O 22 06TEXT('MESSAGE ARRAY 1')
     A                                      DSPATR(HI RI)
      *
     A                                 23  2'F3=Exit  F5=Refresh'
      *
      *---------------------------------------------------------------------
     A          R RE4209F1                  TEXT('OVERRIDE SCREEN')
     A                                      PROTECT
     A                                      OVERLAY
      ** Start of change BUG17964
     A**********                            CLRL(*NO)
      ** End of change BUG17964
     A                                      PUTOVR
     A                                      CF01
     A                                      CF03
     A                                      CF12
      *
     A  26                             21  6'Override teller id'
     A  26        SOVTI          3   B 21 26TEXT('OVERRIDE TELLER ID')
     A                                      OVRDTA
     A                                      DSPATR(HI)
     A N38                                  DSPATR(UL)
     A  38                                  DSPATR(RI)
      *
     A  26                             21 31'passcode'
     A  26        SOVPC          6   B 21 40TEXT('OVERRIDE PASSCODE')
     A                                      OVRDTA
     A                                      DSPATR(ND)
     A  39                                  DSPATR(RI)
      ** Start of change CRT026
      *
     A  54                             21  6'Override user id?'
     A  54        SOVUR         10   B 21 24TEXT('OVERRIDE USER ID')
     A                                      DSPATR(HI)
     A N38                                  DSPATR(UL)
     A  38                                  DSPATR(RI)
     A  38                                  DSPATR(PC)
     A                                      OVRDTA
      *
     A  54                             21 36'Password?'
     A  54        SOVPW         10   B 21 46TEXT('OVERRIDE PASSWORD')
     A                                      DSPATR(ND)
     A  39                                  DSPATR(RI)
     A  39                                  DSPATR(PC)
     A                                      OVRDTA
      *
      ** End of change CRT026
      *
     A                                 23  2'F1=Override  F3=Exit  +
     A                                      F12=Previous'
      *
      *---------------------------------------------------------------------
     A          R RE4209F2                  TEXT('INSTRUCTION SCREEN')
     A                                      OVERLAY
      ** Start of change BUG17964
     A**********                            CLRL(*NO)
      ** End of change BUG17964
      *
     A                                 23  2'F3=Exit  F5=Refresh'
      *
      *---------------------------------------------------------------------
     A          R RE4209S1                  TEXT('ITEM DETAILS SUBFILE')
      *
     A                                      SFL
     A  04                                  SFLNXTCHG
      *
     A            SACNO         10   B 09 02TEXT('RETAIL ACCOUNT NUMBER')
     A    N27                               DSPATR(PR)
     A     27                               DSPATR(UL)
     A        40                            DSPATR(PC)
     A                                      DSPATR(HI)
      *
     A            SANAM         20   O 09 18TEXT('RETAIL ACCOUNT NAME')
     A                                      DSPATR(HI)
      *
     A            STNAM         14   B 09 40TEXT('TRANSACTION AMOUNT')
     A    N27                               DSPATR(PR)
     A     27                               DSPATR(UL)
     A        41                            DSPATR(PC)
     A                                      DSPATR(HI)
      *
      *                                              Begin /091241/
     A************SRBCD          7   B 09 56TEXT('BANK CODE')
     A            SRBCD         11   B 09 56TEXT('BANK CODE')
      *                                              End   /091241/
     A    N27                               DSPATR(PR)
     A     27                               DSPATR(UL)
     A        42                            DSPATR(PC)
     A                                      DSPATR(HI)
      *
      *                                              Begin /091241/
     A************SCQNF          8   B 09 66TEXT('CHEQUE/REFERENCE')
     A            SCQNF          8   B 09 68TEXT('CHEQUE/REFERENCE')
      *                                              End   /091241/
     A    N27                               DSPATR(PR)
     A     27                               DSPATR(UL)
     A        43                            DSPATR(PC)
     A                                      DSPATR(HI)
      *
     A*/COPY WNCPYSRC,RE4209DF01
     A            SMSG2         72   O 10 02TEXT('MESSARY ARRAY 2')
     A*/COPY WNCPYSRC,RE4209DF02
     A     22                               DSPATR(RI)
      *
     A            SWI            2  0H      TEXT('WARNING ERROR INDEX')
      *
     A            SOV           25   H      TEXT('OVERRIDE CONDITIONS')
      *
     A            S@AMT         13  0H      TEXT('EDITED AMOUNT')
      *
     A            SRRNM          5  0H      TEXT('RELATIVE RECORD NUMBER')
      * Begin /CSD027/
     A**********  S@CNUM         6  0H      TEXT('CUSTOMER NUMBER')                           CSD027
     A            S@CNUM         6A  H      TEXT('CUSTOMER NUMBER')
      * End /CSD027/
      *
     A            S@CCY          3   H      TEXT('CURRENCY CODE')
      *
     A**Start of change CGL029
     A**********  S@ACOD         4  0H      TEXT('ACCOUNT CODE')                              CGL029
     A            S@ACOD        10  0H      TEXT('ACCOUNT CODE')
     A**End of change CGL029
      *
     A            S@ACSQ         2  0H      TEXT('ACCOUNT SEQUENCE NO.')
     A            S@UCQB         1   H      TEXT('UPDATE CHEQUE BOOK IND.')
     A            S@BITN         1  0H      TEXT('BIT-POSITION OF ELEMENT')
     A            S@I            2  0H      TEXT('STATUS ARRAY INDEX')
      *---------------------------------------------------------------------
     A          R RE4209C1                  TEXT('ITEMS SUBFILE CONTROL')
      *
     A                                      CF03
     A                                      CF05
     A                                      OVERLAY
     A                                      SFLCTL(RE4209S1)
     A*/COPY WNCPYSRC,RE4209DF03
     A                                      SFLSIZ(7)
     A                                      SFLPAG(6)
     A*/COPY WNCPYSRC,RE4209DF04
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A  09                                  SFLCLR
     A  10                                  SFLEND
     A            SSFRRN         4  0H      SFLRCDNBR(CURSOR)
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4209'
      *
     A     23                           1 30'CLEARING ITEMS INPUT'
     A                                      DSPATR(HI UL)
      *
     A     24                           1 30'PAYROLL ITEMS INPUT'
     A                                      DSPATR(HI UL)
      *
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
      *
     A            SWID      R        O  2  2REFFLD(JOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
      *
     A                                 03 02'Batch'
     A            SBTNO          3   O 03 08TEXT('BATCH NUMBER')
     A                                      DSPATR(HI)
      *
     A                                 03 18'Status'
     A            STATUS         6   O 03 25TEXT('STATUS')
     A                                      DSPATR(HI)
      *
     A                                 03 34'Branch'
     A            SBRCH          3   O 03 41TEXT('BRANCH CODE')
     A                                      DSPATR(HI)
      *
     A                                 03 47'Currency'
     A            SCCY           3   O 03 56TEXT('CURRENCY CODE')
     A                                      DSPATR(HI)
      *
     A                                 03 63'Trans. type'
     A            STTYP          5   O 03 75TEXT('TRANSACTION TYPE')
     A                                      DSPATR(HI)
      *
     A                                 04 08'Last item no'
     A            SBITU          3  0O 04 21TEXT('LAST BATCH ITEM USED')
     A                                      DSPATR(HI)
      *
     A        28                       04 26'Profit Centre'
     A            SPRFC          4   O 04 40TEXT('PROFIT CENTRE')
     A                                      DSPATR(HI)
     A       N28                            DSPATR(ND)
      *
     A                                 04 45'Department'
     A            SDEPC          3   O 04 56TEXT('DEPARTMENT CODE')
     A                                      DSPATR(HI)
      *
     A                                 04 63'Value date'
     A            SVLDT          6   O 04 74TEXT('VALUE DATE')
     A                                      DSPATR(HI)
      *
     A  21        SMSG1         72   O 05 06TEXT('MESSAGE ARRAY 1')
     A                                      DSPATR(HI RI)
      *
     A                                 07 02'Retail a/c no.'
     A                                      DSPATR(UL)
      *
     A                                 07 18'Account Name'
     A                                      DSPATR(UL)
      *
     A                                 07 40'Amount'
     A                                      DSPATR(UL)
      *
     A                                 07 56'Bank'
     A                                      DSPATR(UL)
      *
      *                                              Begin /091241/
     A**********                       07 66'Cheque Ref.'
     A                                 07 68'Cheque Ref.'
      *                                              End   /091241/
     A                                      DSPATR(UL)
      *
      *---------------------------------------------------------------------
      *
     A          R RE4209S0                  TEXT('MESSAGE SUBFILE')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SMGKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      *---------------------------------------------------------------------
     A          R RE4209C0                  TEXT('CONTROL RECORD')
      *
     A                                      SFLCTL(RE4209S0)
     A                                      SFLSIZ(10)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
      *---------------------------------------------------------------------
      ****************************************************************
