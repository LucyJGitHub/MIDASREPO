      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Presented cheques maintenance')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  DSPF/RE4217DF - Cheque Book Maintenance                      *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG3229            Date 23Aug04               *
      *                 CRE019             Date 12Jan04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 090199             Date 07Jul95               *
      *                 CRT001  *CREATE    Date 28Feb95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG3229 - Process Cheque Books only, not GIRO.               *
      *  CRE019 - Cheque Processing and Maintenance.                  *
     A*  090199 - RE-COMPILATION TO CORRECT MULTI-LANGUAGE            *
     A*           REFERENCES.                                         *
     A*                                                               *
     A*  CRT001 - Retail Teller System.                               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      CF05
     A                                      MSGLOC(24)
     A*
     A*---------------------------------------------------------------------
     A**  Cheque Book details
     A*---------------------------------------------------------------------
     A*
     A          R RE4217F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                      OVERLAY
      **Start of change CRE019
     A  20 25                               CF12
     A  20N25                               CF15
      **End of change CRE019
     A*
     A*
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4217'
     A                                  1 28'PRESENTED CHEQUES MAINTENANCE'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SMRDT     R        O  1 73REFFLD(RUNA)
     A*
     A            SWID      R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME EDTWRD('  :  :  ')
     A*
     A                                  4  2'Action (I or R)'
     A            SACTN          1A  B  4 19TEXT('ACTION CODE')
     A                                      DSPATR(HI)
      **Start of change CRE019
     A*N12N31***                            DSPATR(UL)                                        CRE019
     A N12N31N26                            DSPATR(UL)
      **End of change CRE019
     A  31                                  DSPATR(RI PC)
      **Start of change CRE019                                                                CRE019
     A**12N31***                            DSPATR(PR)
     A  12N31
     AO 20 26                               DSPATR(PR)
      **End of change CRE019
      *
     A                                  6  2'Retail a/c number . . . :'
     A            SRACNO        10A  B  6 28TEXT('RETAIL A/C NO')
     A                                      DSPATR(HI)
      **Start of change CRE019
     A*N12N32***                            DSPATR(UL)                                        CRE019
     A N12N32N28                            DSPATR(UL)
      **End of change CRE019
     A  32                                  DSPATR(RI PC)
      **Start of change CRE019
     A**12N32***                            DSPATR(PR)                                        CRE019
     A  12N32
     AO 20 28                               DSPATR(PR)
      **End of change CRE019
      *
     A                                  7  2'Giro indicator  . . . . :'
     A            SGCIN          1A  B  7 28
     A                                      DSPATR(HI)
      **Start of change CRE019
     A*N12N33***                            DSPATR(UL)                                        CRE019
     A N12N33N28                            DSPATR(UL)
      **End of change CRE019
     A  33                                  DSPATR(RI PC)
      **Start of change CRE019
     A**12N33***                           DSPATR(PR)                                         CRE019
     A  12N33
     AO 20 28                               DSPATR(PR)
      **End of change CRE019
      *
     A                                  8  2'Starting cheque number. :'
     A            SSCQN          8A  B  8 28
     A                                      DSPATR(HI)
     A N12N34                               DSPATR(UL)
     A  34                                  DSPATR(RI PC)
     A  12N34                               DSPATR(PR)
      *
     A                                  9  2'Ending Cheque number. . :'
     A            SECQN          8A  B  9 28
     A                                      DSPATR(HI)
     A N12N35                               DSPATR(UL)
     A  35                                  DSPATR(RI PC)
     A  12N35                               DSPATR(PR)
     A*
     A*
      **Start of change CRE019
     A**********                       23  2'F3=Exit   F5=Refresh'                            CRE019
     A N20                             23  2'F3=Exit   F5=Refresh'
     A  20N25                          23  2'F3=Exit   F5=Refresh   F15=Select'
     A  20 25                          23  2'F3=Exit   F5=Refresh   F12=Cancel'
      **End of change CRE019
     A*
      **Start of change CRE019
     A*---------------------------------------------------------------------
     A**  Cheque Book Subfile
     A*---------------------------------------------------------------------
      *
     A          R RE4217S1                  TEXT('Cheque Book Subfile')
     A                                      SFL
     A  82                                  SFLNXTCHG
      *
     A            #1SEL          1   B  9  4
     A N22N21                               DSPATR(UL HI)
     A  21                                  DSPATR(RI PC)
     A  22N21                               DSPATR(ND PR)
     A            #1ACNO        10   O  9  9
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(ND)
      **Start of change BUG3229
     A**********  #1GCQI         1   O  9 23                                                 BUG3229
     A            #1GCQI         4   O  9 23
      **End of change BUG3229
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(ND)
      **Start of change BUG3229
     A**********  #1SECQ        17   O  9 28                                                 BUG3229
     A            #1SECQ        17   O  9 31
      **End of change BUG3229
     A N22                                  DSPATR(HI)
     A  22                                  DSPATR(ND)
      **Start of change BUG3229
     A**********  #1PRCQ        29   O  9 48                                                 BUG3229
     A            #1PRCQ        29   O  9 51
      **End of change BUG3229
     A N27                                  DSPATR(HI)
     A  27                                  DSPATR(ND)
     A*---------------------------------------------------------------------
     A**  Cheque Book Control
     A*---------------------------------------------------------------------
      *
     A          R RE4217C1                  TEXT('Cheque Book Control')
     A                                      SFLCTL(RE4217S1)
     A                                      SFLSIZ(14)
     A                                      SFLPAG(13)
     A                                      CF09
     A                                      CF12
     A N87                                  SFLDSP
     A N87                                  SFLDSPCTL
     A  86N87                               SFLEND(*MORE)
     A  87                                  SFLCLR
     A N86                                  ROLLUP(88)
     A                                      OVERLAY
      *
     A            #1SFRN         4S 0H      SFLRCDNBR
      *
     A            #1USER    R        O  1  2REFFLD(USER)
     A                                  1 13'RE4217DF'
     A                                  1 28'PRESENTED CHEQUES MAINTENANCE'
     A                                      DSPATR(HI UL)
     A
     A            #1RUNA    R        O  1 72REFFLD(RUNA)
     A            #1WSID    R        O  2  2REFFLD(WSTI)
     A                                  2 72TIME
      *
     A                                  4  9'Branch:'
     A            #1KBRC         3   B  4 17
     A N23                                  DSPATR(HI UL)
     A  23                                  DSPATR(RI PC)
     A                                  4 23'Customer:'
     A            #1KCUS        10   B  4 33
     A N24                                  DSPATR(HI UL)
     A  24                                  DSPATR(RI PC)
      *
     A                                  6  2'Select'
     A                                  6  9'Retail A/C'
      **Start of change BUG3229
     A**********                        6 21'GIRO'                                           BUG3229
     A**********                        6 27'Start/End Cheque No'                            BUG3229
     A**********                        6 48'Presented Cheques'                              BUG3229
     A                                  6 21'GIRO/CHQ'
     A                                  6 30'Start/End Cheque No'
     A                                  6 51'Presented Cheques'
      **End of change BUG3229
      *
     A            #1KACN        10   B  7  9
     A                                      DSPATR(HI UL)
      **Start of change BUG3229
     A**********  #1KPRC         8   B  7 53                                                 BUG3229
     A            #1KPRC         8   B  7 56
      **End of change BUG3229
     A                                      DSPATR(HI UL)
      *
     A*---------------------------------------------------------------------
     A**  Footer for Subfile
     A*---------------------------------------------------------------------
      *
     A          R RE4217F1
     A                                      OVERLAY
     A                                 23  2'R=Reverse  F3=End  F5=Refresh  +
     A                                       F9=Add  F12=Cancel'
      *
      **End of change CRE019
     A*---------------------------------------------------------------------
     A**  Message subfile
     A*---------------------------------------------------------------------
     A*
     A          R RE4217S0                  TEXT('MESSAGE SUBFILE')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SMGKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
     A*
     A*---------------------------------------------------------------------
     A**  Message subfile control record
     A*---------------------------------------------------------------------
     A*
     A          R RE4217C0                  TEXT('CONTROL RECORD')
     A                                      SFLCTL(RE4217S0)
     A                                      SFLSIZ(20)
     A                                      SFLPAG(1)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLINZ
     A  10                                  SFLEND
     A                                      PUTOVR
     A                                      BLINK
     A            SPGMQ                     SFLPGMQ
     A*****************************************************************
