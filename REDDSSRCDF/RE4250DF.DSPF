      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Bureau De Change Control Table Dsp')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  DSPF/RE4250DF - Bureau De Change Control Table - Display     *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. 090199             DATE 07JUL95               *
     A*  PREV AMEND NO. CRT001  *CREATE    DATE 28FEB95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  090199 - RE-COMPILATION TO CORRECT MULTI-LANGUAGE            *
     A*           REFERENCES.                                         *
     A*                                                               *
     A*  CRT001 - Retail Teller System                                *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      CA03(71 'F3')
     A                                      CF05
     A                                      MSGLOC(24)
      *--------------------------------------------------------------------
     A*
     A          R RE4250F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                      PUTOVR
     A                                      KEEP
     A*
     A*
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4250'
     A                                  1 26'BUREAU DE CHANGE CONTROL TABLE'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A*
     A            D#0JIN         2A  B  6 45TEXT('0JI - NAC')
     A                                      DSPATR(HI)
     A  31                                  DSPATR(RI PC)
     A N31                                  DSPATR(UL)
     A                                      OVRDTA
     A*
     A            D#0JIA         2A  B  6 60TEXT('0JI - ACC')
     A                                      DSPATR(HI)
     A  32                                  DSPATR(RI PC)
     A N32                                  DSPATR(UL)
     A                                      OVRDTA
     A*
     A            D#0JKN         2A  B 12 45TEXT('0JK - NAC')
     A                                      DSPATR(HI)
     A  33                                  DSPATR(RI PC)
     A N33                                  DSPATR(UL)
     A                                      OVRDTA
     A*
     A            D#0JKA         2A  B 12 60TEXT('0JK - ACC')
     A                                      DSPATR(HI)
     A  34                                  DSPATR(RI PC)
     A N34                                  DSPATR(UL)
     A                                      OVRDTA
     A*
     A            D#0JLN         2A  B 14 45TEXT('0JL - NAC')
     A                                      DSPATR(HI)
     A  35                                  DSPATR(RI PC)
     A N35                                  DSPATR(UL)
     A                                      OVRDTA
     A*
     A            D#0JLA         2A  B 14 60TEXT('0JL - ACC')
     A                                      DSPATR(HI)
     A  36                                  DSPATR(RI PC)
     A N36                                  DSPATR(UL)
     A                                      OVRDTA
     A*
     A            D#0JON         2A  B 16 45TEXT('0JO - NAC')
     A                                      DSPATR(HI)
     A  37                                  DSPATR(RI PC)
     A N37                                  DSPATR(UL)
     A                                      OVRDTA
     A*
     A            D#0JOA         2A  B 16 60TEXT('0JO - ACC')
     A                                      DSPATR(HI)
     A  38                                  DSPATR(RI PC)
     A N38                                  DSPATR(UL)
     A                                      OVRDTA
     A*
     A            D#0JNN         2A  B 18 45TEXT('0JN - NAC')
     A                                      DSPATR(HI)
     A  39                                  DSPATR(RI PC)
     A N39                                  DSPATR(UL)
     A                                      OVRDTA
     A*
     A            D#0JNA         2A  B 18 60TEXT('0JN - ACC')
     A                                      DSPATR(HI)
     A  40                                  DSPATR(RI PC)
     A N40                                  DSPATR(UL)
     A                                      OVRDTA
     A*
     A            D#0JQN         2A  B 20 45TEXT('0JQ - NAC')
     A                                      DSPATR(HI)
     A  41                                  DSPATR(RI PC)
     A N41                                  DSPATR(UL)
     A                                      OVRDTA
     A*
     A            D#0JQA         2A  B 20 60TEXT('0JQ - ACC')
     A                                      DSPATR(HI)
     A  42                                  DSPATR(RI PC)
     A N42                                  DSPATR(UL)
     A                                      OVRDTA
     A*
     A                                  4 45'Buy            Sell   '
     A                                  5 45'Currency       Currency'
     A*
     A                                  6  2'Foreign exchange transactions'
     A*
     A                                 10 45'Nonaccount     Account'
     A                                 11 45'Currency       Currency'
     A*
     A                                 12  2'Clearing cheque deposit, NAC '
     A                                 14  2'Cheque withdrawal, NAC       '
     A                                 16  2'Cash withdrawal, NAC         '
     A                                 18  2'Cash deposit , NAC           '
     A                                 20  2'House cheque deposit, NAC    '
     A*
     A                                 22 42'Possible codes: VP VN VS DP DN DS'
     A*
     A                                 23  2'F3=End  F5=Refresh'
     A*
     A          R RE4250S0                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SMGKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
     A*
     A          R RE4250C0                  TEXT('Program messages')
     A                                      SFLCTL(RE4250S0)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(10)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  20
     AON20                                  SFLEND
     A            SPGMQ                     SFLPGMQ
