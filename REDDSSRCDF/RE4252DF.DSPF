      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Bureau De Change Rates Defaults Dsp')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  DSPF/RE4252DF - Bureau De Change Rates Defaults - Display    *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG3984            Date 12Aug04               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 150864             Date 07Jan99               *
      *                 CEU024             Date 14Jul98               *
      *                 090199             Date 07Jul95               *
      *                 CRT001  *CREATE    Date 28Feb95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  BUG3984 - Added OVERLAY for webfacing changes.               *
      *  150864 - If rates are non-amendable, amount (scaling factor) *
      *           should not be amendable.                            *
      *  CEU024 - EMU: RTS Enhancement                                *
     A*  090199 - RE-COMPILATION TO CORRECT MULTI-LANGUAGE            *
     A*           REFERENCES.                                         *
     A*                                                               *
     A*  CRT001 - Retail Teller System                                *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A                                      CA03
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
     A*
     A          R RE4252F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      **Start of change BUG3984
     A                                      OVERLAY
      **End of change BUG3984
     A*
     A                                      CF05
     A                                      PUTOVR
     A*
     A*
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4252'
     A                                  1 25'BUREAU DE CHANGE RATES DEFAULTS'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A*
     A                                  4  2'Action Code ..:'
     A            SACTN     R        B  4 18REFFLD(ACTN)
     A                                      DSPATR(HI)
     A  21                                  DSPATR(RI PC)
     A N21                                  DSPATR(UL)
     A*
     A                                  5  2'Currency .....:'
     A            SCCY           3A  B  5 18
     A                                      DSPATR(HI)
     A  22                                  DSPATR(RI PC)
     A N22                                  DSPATR(UL)
     A*
     A                                 23  2'F3=End  F5=Refresh'
     A*
     A          R RE4252F1                  TEXT('DETAIL FORMAT')
      **Start of change BUG3984
     A                                      OVERLAY
      **End of change BUG3984
     A N41N40                               CF05
     A                                      CF12
     A  41                                  CF10
     A                                      PUTOVR
     A                                      KEEP
     A*
     A*
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4252'
     A                                  1 25'BUREAU DE CHANGE RATES DEFAULTS'
     A                                      DSPATR(UL HI)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A*
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A*
     A                                  4  2'Currency ..:'
     A            SCCY           3A  O  4 15
     A                                      DSPATR(HI)
     A*
     A                                  4 20'Currency Name ..:'
     A            SCNAM         14A  O  4 38
     A                                      DSPATR(HI)
     A*
     A                                  7  2'Amount....................:'
     A            D#AMNT         9   B    +2
     A                                      DSPATR(HI)
     A  23                                  DSPATR(RI PC)
      *                                              Begin /150864/
     A**40*******                           DSPATR(PR)
     A*N40N23****                           DSPATR(UL)
     A  35
     AO 40                                  DSPATR(PR)
     A N40N23N35                            DSPATR(UL)
      *                                              End   /150864/
     A                                      OVRDTA
     A*
      *                                              Begin /CEU024/
     A***********                      10  2'Exchange Buy Rate ........:'
     A N31                             10  2'Exchange Buy Rate ........:'
     A  31                             10  2'Euro ccy Buy Rate ........:'
      *                                              End   /CEU024/
     A            D#DNRT         9   B    +2
     A                                      DSPATR(HI)
     A  24                                  DSPATR(RI PC)
      *                                              Begin /CEU024/
     A**40*******                           DSPATR(PR)
     A*N40N24****                           DSPATR(UL)
     A  35
     AO 40                                  DSPATR(PR)
     A N40N24N35                            DSPATR(UL)
      *                                              End   /CEU024/
     A                                      OVRDTA
     A*
      *                                              Begin /CEU024/
     A***********                      11  2'Exchange Sell Rate........:'
     A N31                             11  2'Exchange Sell Rate........:'
     A  31                             11  2'Euro ccy Sell Rate........:'
      *                                              End   /CEU024/
     A            D#DPRT         9   B    +2
     A                                      DSPATR(HI)
     A  25                                  DSPATR(RI PC)
      *                                              Begin /CEU024/
     A**40*******                           DSPATR(PR)
     A*N40N25****                           DSPATR(UL)
     A  35
     AO 40                                  DSPATR(PR)
     A N40N25N35                            DSPATR(UL)
      *                                              End   /CEU024/
     A                                      OVRDTA
     A*
      *                                              Begin /CEU024/
     A***********                      12  2'Exchange Average Rate.....:'
     A N31                             12  2'Exchange Average Rate.....:'
     A  31                             12  2'Euro ccy Average Rate.....:'
      *                                              End   /CEU024/
     A            D#DSRT         9   B    +2
     A                                      DSPATR(HI)
     A  26                                  DSPATR(RI PC)
      *                                              Begin /CEU024/
     A**40*******                           DSPATR(PR)
     A*N40N26*****                          DSPATR(UL)
     A  35
     AO 40                                  DSPATR(PR)
     A N40N26N35                            DSPATR(UL)
      *                                              End   /CEU024/
     A                                      OVRDTA
     A*
      *                                              Begin /CEU024/
     A***********                      15  2'Exchange Cash Buy Rate....:'
     A N31                             15  2'Exchange Cash Buy Rate....:'
     A  31                             15  2'Euro ccy Cash Buy Rate....:'
      *                                              End   /CEU024/
     A            D#VNRT         9   B    +2
     A                                      DSPATR(HI)
     A  27                                  DSPATR(RI PC)
      *                                              Begin /CEU024/
     A**40*******                           DSPATR(PR)
     A*N40N27****                           DSPATR(UL)
     A  35
     AO 40                                  DSPATR(PR)
     A N40N27N35                            DSPATR(UL)
      *                                              End   /CEU024/
     A                                      OVRDTA
     A*
      *                                              Begin /CEU024/
     A***********                      16  2'Exchange Cash Sell Rate...:'
     A N31                             16  2'Exchange Cash Sell Rate...:'
     A  31                             16  2'Euro ccy Cash Sell Rate...:'
      *                                              End   /CEU024/
     A            D#VPRT         9   B    +2
     A                                      DSPATR(HI)
     A  28                                  DSPATR(RI PC)
      *                                              Begin /CEU024/
     A**40*******                           DSPATR(PR)
     A*N40N28****                           DSPATR(UL)
     A  35
     AO 40                                  DSPATR(PR)
     A N40N28N35                            DSPATR(UL)
      *                                              End   /CEU024/
     A                                      OVRDTA
     A*
      *                                              Begin /CEU024/
     A***********                      17  2'Exchange Cash Average Rate:'
     A N31                             17  2'Exchange Cash Average Rate:'
     A  31                             17  2'Euro ccy Cash Average Rate:'
      *                                              End   /CEU024/
     A            D#VSRT         9   B    +2
     A                                      DSPATR(HI)
     A  29                                  DSPATR(RI PC)
      *                                              Begin /CEU024/
     A**40*******                           DSPATR(PR)
     A*N29N40****                           DSPATR(UL)
     A  35
     AO 40                                  DSPATR(PR)
     A N29N40N35                            DSPATR(UL)
      *                                              End   /CEU024/
     A                                      OVRDTA
      *                                              Begin /CEU024/
     A  36        SOTIN          7   O  9 50
     A                                      DSPATR(HI)
     A  36                              9 58'Exchange Rates'
     A                                      DSPATR(HI)
     A  36        SEUBR          9   O 10 55
     A                                      DSPATR(HI)
     A  36        SEUSR          9   O 11 55
     A                                      DSPATR(HI)
     A  36        SEUAR          9   O 12 55
     A                                      DSPATR(HI)
     A  36        SEUCB          9   O 15 55
     A                                      DSPATR(HI)
     A  36        SEUCS          9   O 16 55
     A                                      DSPATR(HI)
     A  36        SEUCA          9   O 17 55
     A                                      DSPATR(HI)
     A  36 37                          21  3'** PRESS ENTER again to +
     A                                       ACCEPT RATES **'
     A                                      DSPATR(HI)
      *                                              End   /CEU024/
     A*
     A N41N40                          23  2'F3=Exit  F5=Refresh  F12=Previous'
     A N41 40                          23  2'F3=Exit  F12=Previous'
     A  41 40                          23  2'F3=Exit  F10=Delete  F12=Previous'
     A*
     A          R RE4252S0                  TEXT('Program messages')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SMGKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
     A*
     A          R RE4252C0                  TEXT('Program messages')
      **Start of change BUG3984
     A                                      OVERLAY
      **End of change BUG3984
     A                                      SFLCTL(RE4252S0)
     A                                      SFLPAG(01)
     A                                      SFLSIZ(10)
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  20
     AON20                                  SFLEND
     A            SPGMQ                     SFLPGMQ
