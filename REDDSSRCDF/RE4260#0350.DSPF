/*STD *  DSPFBASE
/*EXI *  TEXT('Midas RE Travellers cheques register details wind')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  DSPF/RE4260#0 - Travellers Cheques Register Detail Window    *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Prev Amend No. BUG17964           Date 28Apr08               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *                 196730             Date 14Sep01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CRT002  *CREATE    Date  01Dec95              *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG17964 - Screen looks as if text is overwritten            *
      *             (Recompile)                                       *
      *  196730 - Database error caused by incorrect window position. *
      *           Row & column for bottom part of window are fixed so *
      *           should not be variable for the rest of the window.  *
     A*  CRT002 - Retail Branch Access.                               *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      INDARA
      *
     A          R RE4260S0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SFL
      *
     A            SF2TCS        12   O  5  6TEXT('TC SERIAL NO.')
     A            SF2BYN        20   B  5 21TEXT('TC BUYER''S NAME')
     A N11                                  DSPATR(PR)
     A  11                                  DSPATR(HI UL)
     A            SF2IDT         2   O  5 44TEXT('ID TYPE')
     A            SF2BYI        20   B  5 50TEXT('BUYER''S ID')
     A N11                                  DSPATR(PR)
     A  11                                  DSPATR(HI UL)
     A            SF2TCP         1   B  6 14TEXT('SETTLED')
     A N11                                  DSPATR(PR)
     A  11                                  DSPATR(HI UL)
     A            SF2TCN        24   B  6 25TEXT('FULL TC CHEQUE NO.')
     A N11                                  DSPATR(PR)
     A  11                                  DSPATR(HI UL)
      *
     A          R RE4260C0                  TEXT('WINDOW SUBFILE CONTROL')
     A                                      SFLCTL(RE4260S0)
     A N70N72                               SFLDSP
     A N70                                  SFLDSPCTL
     A  70                                  SFLCLR
     A                                      SFLPAG(3) SFLSIZ(4)
     A                                      SFLDROP(CF20)
      *
     A                                      CF03
     A                                      CF10
     A                                      CF12
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
      ** Begin 196730
     A**********                            WINDOW(&SWROW &SWCOL 11 74)
     A                                      WINDOW(7 2 11 74)
      ** End 196730
     A                                      WDWBORDER((*CHAR '...::: :'))
     A                                      USRRSTDSP
      *
      *
      ** Begin 196730
     A**********  SWROW          3S 0P
     A**********  SWCOL          3S 0P
     A            SWROW          3S 0H
     A            SWCOL          3S 0H
      ** End 196730
     A            @RRN2          4S 0H      SFLRCDNBR
      *
     A                                  2  6'TC Serial No.'
     A                                      DSPATR(HI)
     A                                  2 23'TC Buyer''s Name'
     A                                      DSPATR(HI)
     A                                  2 41'ID Type'
     A                                      DSPATR(HI)
     A                                  2 55'Buyer''s ID'
     A                                      DSPATR(HI)
     A                                  3 11'Settled'
     A                                      DSPATR(HI)
     A                                  3 28'Full TC Cheque No.'
     A                                      DSPATR(HI)
      *
     A          R RE4260F0
     A                                      KEEP
     A                                      ASSUME
     A                                      OVERLAY
     A                                      PUTOVR
     A            DUMMYF         1A  O  1 80DSPATR(ND)
      *
     A          R RE4260F1
     A                                      KEEP
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      WINDOW(18 2 3 74 *NOMSGLIN)
     A                                      WDWBORDER((*CHAR '   :::.:'))
     A                                      USRRSTDSP
     A            SFNK          70A  O  2  2TEXT('FUNCTION KEYS')
     A                                      OVRDTA
     A            SMSG          70A  O  3  2TEXT('MESSAGE')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
