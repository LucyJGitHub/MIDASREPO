/*STD *  DSPFBASE
/*EXI *  TEXT('Midas RE Travellers Cheques Register Control DSPF')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  DSPF/RE4260DF - Traveler's Cheques Register Control Display  *
     A*                  File                                         *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
     A*  Last Amend No. BUG17964           Date 28Apr08               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
     A*  Prev Amend No. 140770             Date 14Aug00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 CRT002  *CREATE    DATE  1DEC95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  BUG17964 - Screen looks as if text is overwritten            *
     A*  140770 - Correct confirmation of new trav. cheques inserted. *
     A*  CRT002 - Retail Branch Access.                               *
     A*                                                               *
     A*****************************************************************
     A                                      PRINT(MPRINT)
     A                                      INDARA
     A                                      REF(MFRFL)
     A                                      MSGLOC(24)
     A                                      CHGINPDFT
      *
     A          R RE4260F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A                                      PUTOVR
      ** Start of change BUG17964
     A**********                            CLRL(*NO)
      ** End of change BUG17964
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4260'
     A                                  1 26'TRAVELLERS CHEQUES REGISTER'
     A                                      DSPATR(HI UL)
     A                                  1 67'DATE'
     A            SRUNA     R        O  1 72REFFLD(RUNA)
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME'
     A                                  2 72TIME
      *
     A          R RE4260F1                  TEXT('INITIAL SCREEN')
     A                                      OVERLAY
     A                                      PUTOVR
      ** Start of change BUG17964
     A**********                            CLRL(*NO)
      ** End of change BUG17964
     A                                      CF03
     A                                      CF05
      *
      *
     A                                 10 10'Action Code               :'
     A            SACTN          1   B 10 39TEXT('ACTION CODE')
     A                                      DSPATR(HI)
     A N11                                  DSPATR(UL)
     A  11                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                 11 10'Travellers Cheque Issuer  :'
     A            SISSU          6   B 11 39TEXT('TRAVELLERS CHEQUE ISSUER')
     A                                      DSPATR(HI)
     A N12                                  DSPATR(UL)
     A  12                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                 12 10'Travellers Cheque Currency:'
     A            SCCY           3   B 12 39TEXT('TRAVELLERS CHEQUE CURRENCY')
     A                                      DSPATR(HI)
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                 13 10'Denomination              :'
     A            SDNOM         10   B 13 39TEXT('DENOMINATION')
     A                                      DSPATR(HI)
     A N14                                  DSPATR(UL)
     A  14                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                 14 10'Starting Cheque No.       :'
     A            SSTCN         12   B 14 39TEXT('STARTING CHEQUE NO.')
     A                                      DSPATR(HI)
     A N15                                  DSPATR(UL)
     A  15                                  DSPATR(RI PC)
     A                                      OVRDTA
      *
     A          R RE4260F2                  TEXT('SECOND SCREEN')
     A                                      OVERLAY
     A                                      PUTOVR
      ** Start of change BUG17964
     A**********                            CLRL(*NO)
      ** End of change BUG17964
      *
      *
     A                                  4  3'Travellers Cheque Issuer  :'
     A            SISSU          6   O  4 32TEXT('TRAVELLERS CHEQUE ISSUER')
     A                                      DSPATR(HI)
     A            SINST         20   O  4 47TEXT('ISSUER NAME')
     A                                      DSPATR(HI)
     A                                  5  3'Travellers Cheque Currency:'
     A            SCCY           3   O  5 32TEXT('TRAVELLERS CHEQUE CURRENCY')
     A                                      DSPATR(HI)
     A                                  5 47'Denomination     :'
     A            SDNOM         10   O  5 66TEXT('DENOMINATION')
     A                                      DSPATR(HI)
      *
     A          R RE4260F3                  TEXT('ENQUIRY SCREEN')
     A                                      OVERLAY
     A                                      PUTOVR
      ** Start of change BUG17964
     A**********                            CLRL(*NO)
      ** End of change BUG17964
      *
      *
     A                                  6  3'Starting Cheque No.       :'
     A            SSTCN         12   O  6 32TEXT('STARTING CHEQUE NO.')
     A                                      DSPATR(HI)
     A                                  6 47'Ending Cheque No.:'
     A            SETCN         12   O  6 66TEXT('ENDING CHEQUE NO.')
     A                                      DSPATR(HI)
     A                                  7  3'No. of Cheques In Book    :'
     A            SNOTC          2   O  7 32TEXT('NO. OF CHEQUES IN BOOK')
     A                                      DSPATR(HI)
     A                                  7 47'No. of Sequential Books:'
     A            SNOSB          4   O  7 72TEXT('NO. OF SEQUENTIAL BOOKS')
     A                                      DSPATR(HI)
     A                                  8  3'Date Received from Issuer :'
     A            SDTIN          6   O  8 32TEXT('DATE RECEIVED FROM ISSUER')
     A                                      DSPATR(HI)
     A                                  8 47'Cheque Book Expiry Date:'
     A            SDEXP          6   O  8 72TEXT('CHEQUE BOOK EXPIRY DATE')
     A                                      DSPATR(HI)
     A                                  9  3'Cheque Book Sort Code     :'
     A            SBSRT          6   O  9 32TEXT('CHEQUE BOOK SORT CODE')
     A                                      DSPATR(HI)
      *
     A          R RE4260F4                  TEXT('INSERT SCREEN')
     A                                      OVERLAY
     A                                      PUTOVR
      ** Start of change BUG17964
     A**********                            CLRL(*NO)
      ** End of change BUG17964
     A                                      CF03
     A                                      CF05
     A                                      CF12
      *
      *
     A                                  6  3'Starting Cheque No.       :'
     A            SSTCN         12   O  6 32TEXT('STARTING CHEQUE NO.')
     A                                      DSPATR(HI)
     A                                  6 47'Ending Cheque No.:'
     A            SETCN         12   B  6 66TEXT('ENDING CHEQUE NO.')
     A                                      DSPATR(HI)
     A N17                                  DSPATR(UL)
     A  17                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                  7  3'No. of Cheques In Book    :'
     A            SNOTC          2   B  7 32TEXT('NO. OF CHEQUES IN BOOK')
     A                                      DSPATR(HI)
     A N18                                  DSPATR(UL)
     A  18                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                  7 47'No. of Sequential Books:'
     A            SNOSB          4   B  7 72TEXT('NO. OF SEQUENTIAL BOOKS')
     A                                      DSPATR(HI)
     A N19                                  DSPATR(UL)
     A  19                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                  8  3'Date Received from Issuer :'
     A            SDTIN          6   B  8 32TEXT('DATE RECEIVED FROM ISSUER')
     A                                      DSPATR(HI)
     A N20                                  DSPATR(UL)
     A  20                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                  8 47'Cheque Book Expiry Date:'
     A            SDEXP          6   B  8 72TEXT('CHEQUE BOOK EXPIRY DATE')
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                  9  3'Cheque Book Sort Code     :'
     A            SBSRT          6   B  9 32TEXT('CHEQUE BOOK SORT CODE')
     A                                      DSPATR(HI UL)
     A                                      OVRDTA
      *
     A          R RE4260F5                  TEXT('AMEND SCREEN')
     A                                      OVERLAY
     A                                      PUTOVR
      ** Start of change BUG17964
     A**********                            CLRL(*NO)
      ** End of change BUG17964
     A                                      CF03
     A                                      CF05
     A                                      CF12
      *
      *
     A                                  6  3'Starting Cheque No.       :'
     A            SSTCN         12   O  6 32TEXT('STARTING CHEQUE NO.')
     A                                      DSPATR(HI)
     A                                  6 47'Ending Cheque No.:'
     A            SETCN         12   O  6 66TEXT('ENDING CHEQUE NO.')
     A                                      DSPATR(HI)
     A                                  7  3'No. of Cheques In Book    :'
     A            SNOTC          2   O  7 32TEXT('NO. OF CHEQUES IN BOOK')
     A                                      DSPATR(HI)
     A                                  7 47'No. of Sequential Books:'
     A            SNOSB          4   O  7 72TEXT('NO. OF SEQUENTIAL BOOKS')
     A                                      DSPATR(HI)
     A                                  8  3'Date Received from Issuer :'
     A            SDTIN          6   O  8 32TEXT('DATE RECEIVED FROM ISSUER')
     A                                      DSPATR(HI)
     A                                  8 47'Cheque Book Expiry Date:'
     A            SDEXP          6   B  8 72TEXT('CHEQUE BOOK EXPIRY DATE')
     A                                      DSPATR(HI)
     A N21                                  DSPATR(UL)
     A  21                                  DSPATR(RI PC)
     A                                      OVRDTA
     A                                  9  3'Cheque Book Sort Code     :'
     A            SBSRT          6   O  9 32TEXT('CHEQUE BOOK SORT CODE')
     A                                      DSPATR(HI)
     A                                      OVRDTA
      *
     A          R RE4260F6                  TEXT('FOOTER FORMAT')
     A                                      OVERLAY
     A                                      PUTOVR
      ** Start of change BUG17964
     A**********                            CLRL(*NO)
      ** End of change BUG17964
      *
     A            SFTR          78   O 23  2TEXT('COMMAND KEYS')
     A                                      OVRDTA
      *
     A          R RE4260F7                  TEXT('DUMMY FORMAT')
      *
     A            DUMMYF         1A  O  1 80DSPATR(ND)
      *
     A          R RE4260S0                  TEXT('SUBFILE RECORD')
     A                                      SFL
     A  69
      *** Start 140770 ***
     A*N69*71                               SFLNXTCHG
     AON69 71N25                            SFLNXTCHG
     A            SELF           1A  H
      *** End 140770 ***
      *
     A            SSELC          1   B 12  3TEXT('SELECTION FLAG')
     A                                      DSPATR(HI)
     A N16                                  DSPATR(UL)
     A  16                                  DSPATR(RI PC)
      *** Start 140770 ***
     A  25                                  DSPATR(ND PR)
      *** End 140770 ***
     A            SFBSTS         1   O 12  9TEXT('STATUS')
     A            SFSTCN        12   O 12 20TEXT('STARTING CHEQUE NO.')
     A            SFETCN        12   O 12 50TEXT('ENDING CHEQUE NO.')
      *
     A          R RE4260C0                  TEXT('SUBFILE CONTROL RECORD')
     A                                      CF03
     A                                      CF05
     A                                      CF10
     A                                      CF12
     A                                      SFLCTL(RE4260S0)
     A                                      SFLPAG(10) SFLSIZ(11)
     A  70                                  SFLCLR
     A N70N72                               SFLDSP
     A N70                                  SFLDSPCTL
     A                                      OVERLAY
      *
      *
     A                                 11  2'Sel'
     A                                      DSPATR(HI)
     A                                 11  9'St'
     A                                      DSPATR(HI)
     A                                 11 15'Starting Cheque Number'
     A                                      DSPATR(HI)
     A                                 11 45'Ending Cheque Number'
     A                                      DSPATR(HI)
      *
     A          R #MSGRCD                   TEXT('MESSAGE SUBFILE')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SMGKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
     A          R #MSGCTL                   TEXT('MESSAGE CONTROL RECORD')
      *
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLSIZ(10)
     A                                      SFLPAG(1)
     A                                      SFLDSP
     A                                      SFLINZ
     A  10
     AON10                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
