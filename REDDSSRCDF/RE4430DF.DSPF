      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE CI Monitor (Screen 1)')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  DSPF/RE4430DF - CI Monitor                                   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CRT005             Date 09Aug01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CRT003  *CREATE    Date 22Jul96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRT005 - Cashier Enhancement for Cash In/Cash Out            *
      *           Recompiled                                          *
      *  CRT003 - Cashier Interface.                                  *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CF03
     A                                      CF05
     A                                      CF12
      *
     A          R RE4430F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4430DF'
     A                                  2 30'Cashier interface monitor'
     A            BJMRDT         7A     1 72
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 71TIME
      *
     A                                  4 37'N u m b e r   o f'
     A                                      DSPATR(HI)
     A                                  4 57'a c t i v e   t a s k s'
     A                                      DSPATR(HI)
     A                                  5  5'Function'
     A                                      DSPATR(HI)
     A                                  5 40'Current'
     A                                      DSPATR(HI)
     A                                  5 50'Default'
     A                                      DSPATR(HI)
     A                                  5 60'Minimum'
     A                                      DSPATR(HI)
     A                                  5 70'Maximum'
     A                                      DSPATR(HI)
      *
     A          R RE4430F1                  TEXT('NO DETAILS FORMAT')
      *
     A                                      OVERLAY
      *
     A                                 10 25'*** No details to display ***'
      *
     A          R RE4430F2                  TEXT('FOOTER FORMAT')
      *
     A                                      OVERLAY
     A                                      CLRL(*NO)
      *
     A                                 23  3'F3=Exit  F5=Refresh  F12=Previous'
     A                                 23 70'1-Select'
     A  17                                  DSPATR(ND)
     A            SERRL         75   O 24  3TEXT('ERROR LINE')
     A                                      DSPATR(HI)
      *
     A          R RE4430S0                  TEXT('SUBFILE FORMAT')
     A                                      SFL
     A  13
     AON13 12                               SFLNXTCHG
      *
     A            SSLCT          1   B  7  2TEXT('SELECTION FLAG')
     A N13                                  DSPATR(HI)
     A  13                                  DSPATR(RI PC)
      *
     A            SSFLN         30   O  7  5TEXT('SUBFILE NAME')
     A            SCRNT          2  0O  7 43TEXT('CURRENT NO. OF ACTIVE TASKS')
     A            SDFLT          2  0O  7 53TEXT('DEFAULT NO. OF ACTIVE TASKS')
     A            SMINM          2  0O  7 63TEXT('MINIMUM NO. OF ACTIVE TASKS')
     A            SMAXM          2  0O  7 73TEXT('MAXIMUM NO. OF ACTIVE TASKS')
      *
     A          R RE4430C0                  TEXT('SUBFILE CONTROL RECORD')
      *
     A                                      SFLCTL(RE4430S0)
     A                                      SFLPAG(15) SFLSIZ(16)
     A N17N15                               ROLLUP(01)
     A  15                                  SFLEND
     A  16                                  SFLCLR
     A N16N17                               SFLDSP
     A N16                                  SFLDSPCTL
     A                                      OVERLAY
     A            SRRN           4S 0H      SFLRCDNBR
      *
