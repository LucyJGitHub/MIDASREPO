      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE CI Monitor (Screen 2)')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  DSPF/RE4431DF - CI Monitor (Screen 2)                        *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CRT005             Date 09Aug01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CRT003  *CREATE    Date 22Jul96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRT005 - Cashier Enhancement for Cash In/Cash Out            *
      *           Recompiled                                          *
      *  CRT003 - Cashier Interface.                                  *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CF03
     A                                      CF05
     A                                      CF12
      *
     A          R RE4431F0                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4431DF'
     A                                  2 30'Cashier interface monitor'
     A            BJMRDT         7A     1 72
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 71TIME
      *
     A                                  4 26'Function:'
     A            SFNCN         30   O  4 37
      *
     A                                  6 19'Number of active tasks'
     A                                      DSPATR(HI)
     A                                  6 42'with this function:'
     A                                      DSPATR(HI)
      *
     A            SCRNT          2  0O  8 20
     A                                  8 23'- current'
      *
     A            SREQD          2   B  8 35
     A N13                                  DSPATR(HI)
     A  13                                  DSPATR(RI PC)
     A                                  8 38'- required'
      *
     A            SDFLT          2  0O  9 20
     A                                  9 23'- default'
      *
     A            SMINM          2  0O  9 35
     A                                  9 38'- minimum'
      *
     A            SMAXM          2  0O  9 50
     A                                  9 53'- maximum'
      *
     A          R RE4431F1                  TEXT('NO DETAILS FORMAT')
      *
     A                                 12 21'*** No active jobs'
     A                                 12 40'for this function ***'
      *
     A          R RE4431F2                  TEXT('FOOTER FORMAT')
      *
     A                                      OVERLAY
     A                                      CLRL(*NO)
      *
     A                                 23  3'F3=Exit  F5=Refresh  F12=Previous'
      *
     A          R RE4431S0                  TEXT('SUBFILE FORMAT')
     A                                      SFL
     A  13
     AON13 11                               SFLNXTCHG
      *
     A            SJBNM         10   O 12 17TEXT('JOB NAME')
     A            SUSER         10   O 12 28TEXT('USER')
     A            SNMBR          6   O 12 39TEXT('NUMBER')
     A            SSTTS         15   O 12 50TEXT('STATUS')
      *
     A          R RE4431C0                  TEXT('SUBFILE CONTROL RECORD')
      *
     A                                      SFLCTL(RE4431S0)
     A                                      SFLPAG(10) SFLSIZ(11)
     A                                      OVERLAY
     A  15
     AO 18                                  SFLEND
     A  16                                  SFLCLR
     A N16N17                               SFLDSP
     A N16                                  SFLDSPCTL
     A            SRRN           4S 0H      SFLRCDNBR
      *
     A                                 11 17'Job Name'
     A                                      DSPATR(HI)
     A                                 11 28'User'
     A                                      DSPATR(HI)
     A                                 11 39'Number'
     A                                      DSPATR(HI)
     A                                 11 50'----Status----'
     A                                      DSPATR(HI)
      *
     A          R RE4431S1                  TEXT('MESSAGE SUBFILE')
      *
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            SMGKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
     A          R RE4431C1                  TEXT('MESSAGE CONTROL RECORD')
      *
     A                                      SFLCTL(RE4431S1)
     A                                      SFLSIZ(10)
     A                                      SFLPAG(1)
     A                                      OVERLAY
     A                                      SFLDSP
     A                                      SFLINZ
     A  10
     AON10                                  SFLEND
     A            SPGMQ                     SFLPGMQ
      *
