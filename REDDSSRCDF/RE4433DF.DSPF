      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Retail branch status')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  DSPF/RE4433DF - Retail Branch Status                         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *  Last Amend No. CRT003  *CREATE    Date 01Feb00               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CRT003 - Cashier Interface.                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      CF03
     A                                      CF05
     A                                      CF12
      *=========================================================================
      *
     A          R RE4433F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4433DF'
     A                                  2 25'Retail Connection Branch Status'
     A            BJMRDT         7A     1 72
      *
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 71TIME
      *
     A  18                           O  4  4'Sel'
     A                                      DSPATR(HI)
     A                               O  4  9'Code'
     A                                      DSPATR(HI)
     A                               O  4 15'Branch Name'
     A                                      DSPATR(HI)
     A                               O  4 48'Branch Status'
     A                                      DSPATR(HI)
     A                               O  5  9'___'
     A                                      DSPATR(HI)
     A                               O  5 15'______________________________'
     A                                      DSPATR(HI)
     A                               O  5 48'_________________________'
     A                                      DSPATR(HI)
      *
     A          R RE4433F1                  TEXT('NO DETAILS FORMAT')
      *
     A                                      OVERLAY
      *
     A                                 10 25'*** No details to display ***'
      *
     A          R RE4433F2                  TEXT('FOOTER FORMAT')
      *
     A                                      OVERLAY
     A                                      CLRL(*NO)
      *
     A  18                             22  3'F-Open Off-Line'
     A  17                                  DSPATR(ND)
     A  18                             22 20'C-Force Branch Closure'
     A  17                                  DSPATR(ND)
     A                                 23  3'F3=Exit  F5=Refresh  F12=Previous'
     A            SERRL         75   O 24  3TEXT('ERROR LINE')
     A                                      DSPATR(HI)
      *
     A          R RE4433S0                  SFL
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A  13                                  SFLNXTCHG
      *
     A            SSLCT          1A  B  6  5TEXT('SELECTION FLAG')
     A N13                                  DSPATR(UL)
     A  13                                  DSPATR(RI HI PC)
     A N18                                  DSPATR(PR ND)
     A            SBRCD          3A  O  6  9TEXT('BRANCH CODE')
     A            SBRNM         30A  O  6 15TEXT('BRANCH NAME')
     A            SDESC         25A  O  6 48TEXT('BRANCH STATUS')
     A            SBRST          1A  H      TEXT('BRANCH STATUS')
      *
     A          R RE4433C0                  TEXT('SUBFILE CONTROL')
     A                                      SFLCTL(RE4433S0)
     A                                      SFLSIZ(16)
     A                                      SFLPAG(15)
     A N17N15                               ROLLUP(01)
     A  15                                  SFLEND
     A  16                                  SFLCLR
     A N16N17                               SFLDSP
     A N16                                  SFLDSPCTL
     A                                      OVERLAY
     A            SRRN           4S 0H      SFLRCDNBR
      *
