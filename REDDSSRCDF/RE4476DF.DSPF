     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE CI Transaction Analysis Report - Prompt')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  DSPF/RE4476DF - Transaction Analysis Report                  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  Last Amend No. CRT003 *CREATE     Date 22Jul96               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CRT003 - Branch Automation Interface.                        *
     A*           New Display File.                                   *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A          R RE4476F0
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      CA03(03 'Exit')
     A                                      CA05(05 'Refresh')
     A                                      CA12(12 'Previous')
     A                                      OVERLAY
     A            USER      R        O  1  2REFFLD(USER)
     A            BJMRDT    R        O  1 73REFFLD(RUNA)
     A            WSID      R        O  2  2REFFLD(JOJOB)
     A            SOPT           1A  B  7 53
     A  30                                  DSPATR(RI)
     A  30                                  DSPATR(PC)
     A*                                     VALUES('1' '2' '3' '4' '5' '6' '7')
     A                                  1 13'RE4476DF'
     A                                  1 26'TRANSACTION ANALYSIS REPORT'
     A                                      DSPATR(HI)
     A                                  1 67'DATE '
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  7 25'Type of Report Requested :'
     A                                  9 14'(1)  By Branch, Teller ID, BAI Tra-
     A                                      nsaction Type'
     A                                 10 14'(2)  By Branch, BAI Transaction Ty-
     A                                      pe, Teller ID'
     A                                 11 14'(3)  By BAI Transaction Type, Bran-
     A                                      ch, Teller ID'
     A                                 12 14'(4)  By Branch, Teller ID, Time Po-
     A                                      sted'
     A                                 13 14'(5)  By Branch, Time Posted, Telle-
     A                                      r ID'
     A                                 14 14'(6)  By Time Posted, Branch, Telle-
     A                                      r ID'
     A                                 15 14'(7)  By Time Received, Branch, Tel-
     A                                      ler ID'
     A                                 23  2'F3=Exit   F5=Refresh   F12=Previou-
     A                                      s'
     A*=========================================================================
     A          R #MSGRCD                   TEXT('Program messages')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            ##PGM                     SFLPGMQ
     A*=========================================================================
     A          R #MSGCTL                   TEXT('Program messages')
     A                                      SFLCTL(#MSGRCD)
     A                                      SFLPAG(01) SFLSIZ(03)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  25
     AON25                                  SFLEND
     A            ##PGM                     SFLPGMQ
