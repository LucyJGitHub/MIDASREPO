      *****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE CI IP address maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  DSPF/RE4600DF - Maintain RECITCPD                            *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Last Amend No. CRT007  *CREATE    Date 14Jan02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRT007 - Cashier Branch Status Checking                      *
      *                                                               *
      *****************************************************************
      *
     A                                      CA03
     A                                      CA12
     A                                      INDARA
     A                                      PRINT(MPRINT)
     A                                      MSGLOC(24)
      *
      ** Prompt for Branch
      *
     A          R RE4600F0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      OVERLAY
     A                                      CLRL(*NO)
      *
     A                                  1  2USER
     A                                  1 13'RE4600'
     A                                  1 34'Maintain RECITCPD'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 28'CI Ports against IP Addresses'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 67'Date'
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 67'Time'
     A                                  2 73TIME
      *
     A          R RE4600F1
     A                                      OVERLAY
     A                                  5  8'Enter Branch'
     A                                      DSPATR(HI)
     A            SBRCA          3A  B  5 22
     A                                      DSPATR(HI)
     A  70                             21  3'Invalid Branch Code'
     A                                      DSPATR(HI)
      *
     A          R RE4600F2
     A                                      OVERLAY
     A                                  6  8'Branch name'
     A                                      DSPATR(HI)
     A            A8BRCD         3A  O  6 22
     A                                      DSPATR(HI)
     A       N82  A8TCPP         4S 0O  8 22
     A                                      DSPATR(HI)
     A       N82                        8  8'TCP/IP Port'
     A                                      DSPATR(HI)
     A        82                       12 26'NOT A CASHIER TCP/IP BRANCH'
     A                                      DSPATR(RI)
     A       N82  TCIP1          3A  B 10 22
     A  75                                  DSPATR(RI)
     A       N82                       10  9'IP Address'
     A                                      DSPATR(HI)
     A       N82                       10 26'.'
     A       N82  TCIP2          3A  B 10 28
     A  76                                  DSPATR(RI)
     A       N82                       10 32'.'
     A       N82  TCIP3          3A  B 10 34
     A  77                                  DSPATR(RI)
     A       N82                       10 38'.'
     A       N82  TCIP4          3A  B 10 40
     A  78                                  DSPATR(RI)
     A  79   N82                       19  3'IP Address is Invalid'
     A                                      DSPATR(HI)
     A          R RE4600F3
     A                                      OVERLAY
     A                                 23  3'F3=End'
     A  71                             23 15'F12=Change Branch'
