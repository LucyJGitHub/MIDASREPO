     A*****************************************************************
/*STD *  DSPFBASE                                                     *
/*EXI *  TEXT('Midas RE Teller/Batch Totals Enquiry Display')
      *****************************************************************
      *                                                               *
      *  Midas - Standing Data Module                                 *
      *                                                               *
      *  RE7431DF - Midas RE Teller/Batch Totals Enquiry Display      *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 241342             Date 30Aug06               *
      *                 BUG7792  *CREATE   Date 21Jul05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  241342 - Increase display field length                       *
      *  BUG7792 - Error on retail teller total enquiry / print       *
      *                                                               *
      *****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/MFRFL)
     A                                      CHGINPDFT
     A                                      MSGLOC(24)
     A                                      PRINT(*LIBL/MPRINT)
     A                                      INDARA
     A                                      CA03
     A  27                                  CF07
     A  28                                  CF08
     A  20                                  CF12
     A  20                                  CF13
      *
     A          R RE7431F0
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
     A                                      OVERLAY
      *
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 13'RE4125'
     A                                  1 26'TELLER/BATCH TOTALS ENQUIRY'
     A                                      DSPATR(UL)
     A                                  1 67'DATE '
     A            SRUNA     R        O  1 73REFFLD(RUNA)
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  4  6'ENTER'
     A            SPMPT         10A  O  4 12TEXT('Screen Prompt')
     A            SENQY          3A  B  4 25TEXT('Teller/Batch No')
     A                                      DSPATR(HI)
     A N61                                  DSPATR(UL)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A                                 23  2'F3=Exit'
      *
      ** Subfile Record Format
      *
     A          R RE7431S0                  SFL
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2005')
     A            CCYR           3A  O  9  2
     A            NDRA           5Y 0O  9  6EDTCDE(L)
     A            VDRA          14A  O  9 13
     A            NCRA           5Y 0O  9 28EDTCDE(L)
     A            VCRA          14A  O  9 35
     A            VCAA          16A  O  9 50
     A**********  VCKA          11A  O  9 67                                                  241342
     A            VCKA          14A  O  9 67                                                  241342
      *
      ** Subfile Control
      *
     A          R RE7431C0                  SFLCTL(RE7431S0)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      TEXT('Subfile control.')
     A N81                                  ROLLUP
     A                                      OVERLAY
     A  86                                  PUTOVR
     A N80                                  SFLDSP
     A N80                                  SFLDSPCTL
     A  80                                  SFLCLR
     A N80 81                               SFLEND
     A            RRN            4S 0H      SFLRCDNBR
     A            SUSER     R        O  1  2REFFLD(USER)
     A                                  1 26'TELLER/BATCH TOTALS ENQUIRY'
     A                                      DSPATR(UL)
     A                                  1 67'DATE'
     A            BJMRDT         7A  O  1 74OVRDTA
     A            SWSID     R        O  2  2REFFLD(JOJOB)
     A                                  2 67'TIME '
     A                                  2 73TIME
     A                                  6  2'Ccy --------Debits------- -------C-
     A                                      redits-------'
     A                                  1 13'RE4125'
     A                                  6 62'Cash'
     A**********                        6 69'Clearings'                                       241342
     A                                  6 72'Clearings'                                       241342
     A                                  7  9'No.'
     A                                  7 22'Value'
     A                                  7 30'No.'
     A                                  7 44'Value'
     A                                  7 61'Value'
     A**********                        7 73'Value'                                           241342
     A                                  7 76'Value'                                           241342
     A            SPMPT         10A  O  4 12TEXT('Screen Prompt')
     A            SENQY          3A  O  4 25TEXT('Teller/Batch No')
     A                                  4 31'Transaction nos.'
     A            FRMTRN         5S 0O  4 48TEXT('transactions from')
     A                                  4 54'-'
     A N55        TOTRN          5S 0O  4 56TEXT('transactions to')
     A  55                              4 56'NONE '
     A                                  4  6'ENTER'
      *
      ** Command Line
      *
     A          R RE7431F1
     A                                      TEXT('SELECT SCREEN FOOTER')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                 23  2'F3=Exit'
     A                                 23 11'F12=Previous'
     A                                 23 25'F13=Print'
     A  31                             23 36'F7=Roll down'
     A  32                             23 36'F8=Roll up'
      *
      ** Message Record Format
      *
     A          R RE7431S1                  TEXT('Message Subfile')
     A                                      SFL SFLMSGRCD(24)
     A            SMGKY                     SFLMSGKEY
     A            SPGMQ                     SFLPGMQ
      *
      ** Message Control
      *
     A          R RE7431C1                  TEXT('Control Record')
     A                                      SFLCTL(RE7431S1)
     A                                      SFLPAG(01) SFLSIZ(20)
     A                                      OVERLAY PUTOVR
     A                                      SFLINZ SFLDSP SFLDSPCTL
     A  10                                  SFLEND
     A            SPGMQ                     SFLPGMQ
