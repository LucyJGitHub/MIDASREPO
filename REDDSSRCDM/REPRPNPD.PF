      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas RE T&N Accounts - Periods and Penalties')        *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PF/REPRPNPD - Periods and Penalties                          *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CRE073             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 237973             Date 08Apr10               *
      *                 CRE015  *CREATE    Date 18Dec09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE073 - Interest Rate Rounding                              *
      *  237973 - Prevent duplicate records in REPRPNPD               *
      *  CRE015 - Retail Term and Notice Accounts                     *
      *                                                               *
      *****************************************************************
     A                                      UNIQUE                                            237973
     A          R REPRPND0                  TEXT('(c) Finastra International +
     A                                           Limited 2009')
      *
     A            E2BRCA         3A         TEXT('Branch Code')
     A                                      COLHDG('Branch' 'Code')
     A            E2CNUM         6A         TEXT('Customer number')
     A                                      COLHDG('Customer' +
     A                                             'Number')
     A            E2CCY          3A         TEXT('Currency Code')
     A                                      COLHDG('Curr' 'Code')
     A            E2ACOD        10S 0       TEXT('Account Code')
     A                                      COLHDG('Account Code')
     A            E2ACSQ         2S 0       TEXT('Account Sequence No.')
     A                                      COLHDG('A/C Seq')
     A            E2EVNT         1A         TEXT('Event type')
     A                                      COLHDG('Evt' 'Typ')
     A            E2ITEM         5P 0       TEXT('Item No.')
     A                                      COLHDG('Item' 'No.')
     A            E2VDAT         5P 0       TEXT('Value date')
     A                                      COLHDG('Value' 'Date')
     A            E2MDAT         5P 0       TEXT('Maturity Date')
     A                                      COLHDG('Maturity' 'Date')
     A            E2CEDE         5S 0       TEXT('End date')
     A                                      COLHDG('End' 'Date')
     A            E2FREQ         1A         TEXT('Frequency Code')
     A                                      COLHDG('Freq')
     A            E2DAYM         2S 0       TEXT('Day of the month')
     A                                      COLHDG('Day of' +
     A                                             'Month')
     A            E2NDPD         3S 0       TEXT('No. of days in period')
     A                                      COLHDG('Days' +
     A                                             'in Period')
     A            E2NMPD         2S 0       TEXT('No. of months in period')
     A                                      COLHDG('No. of' 'Months')
     A            E2IRLI         1A         TEXT('Interest rollover indicator')
     A                                      COLHDG('Int' 'Rol' +
     A                                             'Ind')
     A            E2TPRD         1A         TEXT('Type of period')
     A                                      COLHDG('Typ' 'of' 'Prd')
     A            E2DRIB         2S 0       TEXT('DR Interest base type')
     A                                      COLHDG('DR Interest' +
     A                                             'Base Type')
     A            E2DRIS        11P 7       TEXT('DR Interest rate spread')
     A                                      COLHDG('DR Interest' +
     A                                             'Rate Spread')
     A            E2DICT         2S 0       TEXT('DR Interest calculation type')
     A                                      COLHDG('DR Interest' +
     A                                             'Calc. Type')
     A            E2DCST         5S 0       TEXT('DR Interest calc subtype')
     A                                      COLHDG('DR Interest' +
     A                                             'Calc. Subtype')
     A            E2DRIC         1S 0       TEXT('DR Interest calculation +
     A                                           basis')
     A                                      COLHDG('DR Int.' +
     A                                             'Calc. Basis')
     A            E2DNIR         1A         TEXT('DR Interest rate fixing +
     A                                           indicator')
     A                                      COLHDG('DR Int.' +
     A                                             'Rate Fix' +
     A                                             'Ind.')
     A            E2DRIF         1A         TEXT('DR Interest frequency')
     A                                      COLHDG('DR Int.' +
     A                                             'Freq.')
     A            E2DRDY         4S 0       TEXT('Year of the month')
     A                                      COLHDG('Year of' +
     A                                             'the Month')
     A            E2NDID         5S 0       TEXT('Next Dr interest capt date')
     A                                      COLHDG('Next Dr int' +
     A                                             'Cap. Date')
     A            E2DRBR         3A         TEXT('DR A/C Branch')
     A                                      COLHDG('DR A/C Branch')
     A            E2DACP        24A         TEXT('A/c to post Dr interest')
     A                                      COLHDG('A/c to Post Dr Int.')
     A            E2CRIB         2S 0       TEXT('CR interest base type')
     A                                      COLHDG('CR Interest' +
     A                                             'Base Type')
     A            E2CRIS        11P 7       TEXT('CR Interest rate spread')
     A                                      COLHDG('CR Interest' +
     A                                             'Rate Spread')
     A            E2CICT         2S 0       TEXT('CR Interest calc. type')
     A                                      COLHDG('CR Interest' +
     A                                             'Calc. Type')
     A            E2CCST         5S 0       TEXT('CR Interest calc. subtype')
     A                                      COLHDG('CR Interest' +
     A                                             'Calc. Subtype')
     A            E2CRIC         1S 0       TEXT('CR Interest calculation +
     A                                           basis')
     A                                      COLHDG('CR Interest' +
     A                                             'Calc.' +
     A                                             'Basis')
     A            E2CNIR         1A         TEXT('New Interest rate fix +
     A                                           indicator')
     A                                      COLHDG('New Interest' +
     A                                             'Rate Fix' +
     A                                             'Ind.')
     A            E2CRIF         1A         TEXT('CR Interest +
     A                                           frequency')
     A                                      COLHDG('CR Int.' +
     A                                             'Freq.')
     A            E2CRDY         4S 0       TEXT('Day of the month')
     A                                      COLHDG('Day of the' +
     A                                             'Month')
     A            E2NCID         5S 0       TEXT('Next Cr int. capt. date')
     A                                      COLHDG('Next CR' +
     A                                             'Int Cap.' +
     A                                             'Date')
     A            E2CRBR         3A         TEXT('Branch Code')
     A                                      COLHDG('Branch' 'Code')
     A            E2CACP        24A         TEXT('A/c to post credit interest')
     A                                      COLHDG('A/c to post' +
     A                                             'CR Int.')
     A            E2RTTD         5A         TEXT('Retail transaction type')
     A                                      COLHDG('Retail' 'Trans' +
     A                                             'Type')
     A            E2DYBD         1A         TEXT('Day basis')
     A                                      COLHDG('Day' 'Basis')
     A            E2DRST         1A         TEXT('DR Restrict')
     A                                      COLHDG('DR' 'Restrict')
     A            E2RTTC         5A         TEXT('Retail transaction type')
     A                                      COLHDG('Retail' 'Trans' +
     A                                             'Type')
     A            E2DYBC         1A         TEXT('Day basis SC')
     A                                      COLHDG('Day' 'Basis' 'SC')
     A            E2CRST         1A         TEXT('CR Restrict')
     A                                      COLHDG('CR' 'Restrict')
     A            E2DTWD         5P         TEXT('Date of next working day')
     A                                      COLHDG('Next Working' +
     A                                             'Day')
     A            E2BALN        15S 0       TEXT('Initial Balance')
     A                                      COLHDG('Initial Balance')
     A            E2KEY1        24A         TEXT('Account Key 1')
     A                                      COLHDG('Account Key1')
     A            E2NARR        55A         TEXT('Narrative')
     A                                      COLHDG('Narrative')
      **Start of change CRE073
     A            E2DCSP        11P 7       TEXT('Debit Contractual Spread')
     A                                      COLHDG('DR Contractual' +
     A                                             'Spread')
     A            E2DCSG         1A         TEXT('Debit Contractual Spread +
     A                                             Sign')
     A                                      COLHDG('DR Contractual' +
     A                                             'Spread Sign')
     A            E2DRMT         7A         TEXT('Debit Rounding Method')
     A                                      COLHDG('DR Rounding' +
     A                                             'Method')
     A            E2DRFD         4A         TEXT('Debit Rounding +
     A                                             Fraction/Decimal')
     A                                      COLHDG('DR Rounding' +
     A                                             'Fraction/Decimal')
     A            E2CCSP        11P 7       TEXT('Credit Contractual Spread')
     A                                      COLHDG('CR Contractual' +
     A                                             'Spread')
     A            E2CCSG         1A         TEXT('Credit Contractual Spread +
     A                                             Sign')
     A                                      COLHDG('CR Contractual' +
     A                                             'Spread Sign')
     A            E2CRMT         7A         TEXT('Credit Rounding Method')
     A                                      COLHDG('CR Rounding' +
     A                                             'Method')
     A            E2CRFD         4A         TEXT('Credit Rounding +
     A                                             Fraction/Decimal')
     A                                      COLHDG('CR Rounding' +
     A                                             'Fraction/Decimal')
      **End of change CRE073
      *
     A          K E2BRCA
     A          K E2CNUM
     A          K E2CCY
     A          K E2ACOD
     A          K E2ACSQ
     A          K E2EVNT
     A          K E2ITEM
