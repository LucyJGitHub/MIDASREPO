      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('Midas RE Sweep details file')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PF/RESWEPPD - Sweeps Deteails File                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CRE401             Date 26Nov09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 05Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CEU022             Date 20Jan98               *
      *                 CRE004  *CREATE    Date 20Jan97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE401 - 4-Eyes Checking Gaps - Sweeps                       *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CEU022 - New Field CCYC added for EMU Retail utilities       *
      *           module                                              *
      *  CRE004 - Sweep Accounting                                    *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R RESWEPD0                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A            RECI      R               TEXT('Record Id')
     A                                      COLHDG('Rec Id')
     A            SWEP      R               REFFLD(ASOC)
     A                                      TEXT('Sweep Reference')
     A                                      COLHDG('Sweep Ref')
     A            DRBR      R               REFFLD(BBRC)
     A                                      TEXT('DR A/C Branch')
     A                                      COLHDG('DR A/C Brch')
      * Begin /CSD027/
     A**********  DCUS      R               REFFLD(ASOC)                                      CSD027
     A            DCUS           6A
      * End /CSD027/
     A                                      TEXT('Debit Customer Number')
     A                                      COLHDG('DR Cust No.')
     A**Start of change CGL029
     A**********  DACD      R               REFFLD(ACDE)                                      CGL029
     A**********                            TEXT('DR Account Code')                           CGL029
     A**********                            COLHDG('DR A/c Code')                             CGL029
     A            DACDQQ    R               REFFLD(ACODQQ)
     A**End of change CGL029
     A            DACS      R               REFFLD(ACSQ0)
     A                                      TEXT('Debit Account Sequence No.')
     A                                      COLHDG('DR A/c Seq.')
     A            DRAN      R               REFFLD(CHRN)
     A                                      TEXT('Debit Retail A/C Number')
     A                                      COLHDG('DR Retl A/c No.')
     A            CBRC      R               REFFLD(BBRC)
     A                                      TEXT('CR A/C Branch')
     A                                      COLHDG('CR A/C Brch')
      * Begin /CSD027/
     A**********  CCUS      R               REFFLD(ASOC)                                      CSD027
     A            CCUS           6A
      * End /CSD027/
     A                                      TEXT('Credit Customer Number')
     A                                      COLHDG('CR  Cust No.')
     A**Start of change CGL029
     A**********  CSCA      R               REFFLD(ACDE)                                      CGL029
     A**********                            TEXT('CR Account Code')                           CGL029
     A**********                            COLHDG('CR A/c Code')                             CGL029
     A            CSCAQQ    R               REFFLD(ACODQQ)
     A**End of change CGL029
     A            CACD      R               REFFLD(ACSQ0)
     A                                      TEXT('Credit Account Sequence No.')
     A                                      COLHDG('CR A/c Seq No')
     A            CRAN      R               REFFLD(CHRN)
     A                                      TEXT('Credit Retail A/C Number')
     A                                      COLHDG('CR Retl A/C No')
     A            BRCA      R               REFFLD(BBRC)
     A                                      TEXT('Transaction Branch')
     A                                      COLHDG('Trans Brch')
     A            MBSI      R               REFFLD(MBFC)
     A                                      TEXT('Multi-branched Sweep')
     A                                      COLHDG('Multi-brch' +
     A                                             'Sweep')
     A            CCY       R               REFFLD(ACCY)
      *                                              Begin /CEU022/
     A************                          TEXT('Currency Code')
     A************                          COLHDG('Ccy Code')
     A                                      TEXT('Debit Currency Code')
     A                                      COLHDG('Debit Ccy Code')
      *                                              End   /CEU022/
     A            NXTP      R               REFFLD(CRDA)
     A                                      TEXT('Sweep Date')
     A                                      COLHDG('Sweep Date')
     A            DLPY      R               REFFLD(CRDA)
     A                                      TEXT('Final Date')
     A                                      COLHDG('Final Date')
     A            PAYF      R               REFFLD(CHTP)
     A                                      TEXT('Frequency')
     A                                      COLHDG('Freq')
     A            DYTP      R               REFFLD(ASEQ)
     A                                      TEXT('Day in month for sweep')
     A                                      COLHDG('Day in mth' +
     A                                             'for sweep')
     A            PNCD      R               REFFLD(BOKC)
     A                                      TEXT('Posting Narrative Code')
     A                                      COLHDG('Posting' +
     A                                             'Narr Code')
     A            PNAR      R               REFFLD(NRTD)
     A                                      TEXT('Narrative')
     A                                      COLHDG('Narrative')
     A            PRFC      R               REFFLD(DPPC)
     A                                      TEXT('Profit Centre')
     A                                      COLHDG('Profit' +
     A                                             'Centre')
     A            TRAT      R               TEXT('Transaction Type')
     A                                      COLHDG('Trans' +
     A                                             'Type')
     A            STHR      R               REFFLD(AFPY)
     A                                      TEXT('Sweep Threshold')
     A                                      COLHDG('Sweep' +
     A                                             'Threshold')
     A            SWTP      R               REFFLD(CHTP)
     A                                      TEXT('Sweep Type')
     A                                      COLHDG('Sweep' +
     A                                             'Type')
     A            LCD       R               REFFLD(LCND)
     A                                      TEXT('Last Change Date')
     A                                      COLHDG('Last Chg' +
     A                                             'Date')
     A            CHTP      R               TEXT('Last Change Type')
     A                                      COLHDG('Last Chg' +
     A                                             'Type')
     A            TNLU      R               TEXT('Trans. No. of last update')
     A                                      COLHDG('Trans No' +
     A                                             'of last' +
     A                                             'update')
      *                                              Begin /CEU022/
     A            CCYC      R               REFFLD(ACCY)
     A                                      TEXT('Credit Currency Code')
     A                                      COLHDG('Credit Ccy Code')
      *                                              End   /CEU022/
     A**Start of change CGL029
     A            DACD      R               REFFLD(ACOD)
     A                                      TEXT('DR Account Code')
     A            CSCA      R               REFFLD(ACOD)
     A                                      TEXT('CR Account Code')
     A**End of change CGL029
     A**Start of change CRE401
     A            AUTH           1          TEXT('Unauthorised/Authorised')
     A                                      COLHDG('Unauthorised/' +
     A                                             'Authorised')
     A            LUSR          10          TEXT('Last User')
     A                                      COLHDG('Last User')
     A            AUSR          10          TEXT('Authorising User')
     A                                      COLHDG('Authorising' +
     A                                      'User')
     A**End of change CRE401
