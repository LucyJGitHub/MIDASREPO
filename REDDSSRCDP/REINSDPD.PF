      *****************************************************************
/*STD *  PFBASE                                                       *
/*EXS *  PF_STD                                                       *
/*EXI *  TEXT('RE Retail Interest Summary Detail file')               *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PF/REINSDPD - RE Retail Interest Summary Detail file         *
      *                                                               *
      *  (c) Finastra International Limited 2008                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR807782           Date 28Jul11               *
      *                 CER059             Date 19Jul10               *
      *                 CER039  *CREATE    Date 19May08               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR807782 - Increase account field from 15 to 21              *
      *             (Child:AR807783)                                  *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER039 - Retail Interest Summary Correspondence              *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R REINSDD0                  TEXT('(c) Finastra +
     A                                      Internatioal Banking +
     A                                      Limited 2008')
      *
     A            FBRCA     R               REFFLD(BRCA)
      *
     A            FCNUM          6A         TEXT('Customer Number')
     A                                      COLHDG('Customer Number')
      *
     A            FCCY      R               REFFLD(CCY)
      *
     A            FACOD     R               REFFLD(ACOD)
      *
     A            FACSQ     R               REFFLD(ACSQ)
      *
     A            FKEYS1    R               REFFLD(ZZ002)
     A                                      TEXT('Key Sequence +
     A                                      Field #1')
      *
     A            FRECT     R               REFFLD(AMSQ)
     A                                      TEXT('Record Type')
      *
     A            FKEYS2    R               REFFLD(TNLI)
     A                                      TEXT('Key Sequence +
     A                                            Field #2')
      *
     A            FCNA1     R               REFFLD(ZZ035)
     A                                      TEXT('Customer Name & +
     A                                            Address 1')
      *
     A            FCNA2     R               REFFLD(ZZ035)
     A                                      TEXT('Customer Name +
     A                                            & Address 2')
      *
     A            FCNA3     R               REFFLD(ZZ035)
     A                                      TEXT('Customer Name +
     A                                            & Address 3')
      *
     A            FCNA4     R               REFFLD(ZZ035)
     A                                      TEXT('Customer Name +
     A                                            & Address 4')
      *
     A            FDICI     R               REFFLD(DRIF)
     A                                      TEXT('DR Capitalisation +
     A                                            Type')
      *
     A            FDRIF     R               REFFLD(DRIF)
     A                                      TEXT('DR Capitalisation +
     A                                            Frequency')
      *
     A            FCICI     R               REFFLD(DRIF)
     A                                      TEXT('CR Capitalisation +
     A                                            Type')
      *
     A            FCRIF     R               REFFLD(DRIF)
     A                                      TEXT('CR Capitalisation +
     A                                            Frequency')
      *
     A            FDCPD     R               REFFLD(TNLI)
     A                                      TEXT('DR Capitalisation +
     A                                            Date')
      *
     A            FCCPD     R               REFFLD(TNLI)
     A                                      TEXT('CR Capitalisation +
     A                                            Date')
      *
     A            FACNO     R               REFFLD(ACNO)
      *
     A            FANAM     R               REFFLD(ANAM)
      *
     A            FDCPV     R               REFFLD(TNLI)
     A                                      TEXT('DR Capitalisation +
     A                                            Value Date')
      *
     A            FCCPV     R               REFFLD(TNLI)
     A                                      TEXT('CR Capitalisation +
     A                                            Value Date')
      *
     A            FDRTU     R               REFFLD(DR1)
     A                                      TEXT('Total DR Turnover +
     A                                            in 100s')
      *
     A            FCRTU     R               REFFLD(CR1)
     A                                      TEXT('Total CR Turnover +
     A                                            in 100s')
      *
     A            FDRBR     R               REFFLD(DRBR)
     A                                      TEXT('DR Alternate A/C +
     A                                            Branch Code')
      *
     A            FDACP     R               REFFLD(DACP)
      *
     A            FDSPT     R               REFFLD(BSPR)
     A                                      TEXT('DR Spot Rate')
      *
     A            FCRBR     R               REFFLD(DRBR)
     A                                      TEXT('CR Alternate A/C +
     A                                            Branch Code')
      *
     A            FCACP     R               REFFLD(DACP)
      *
     A            FCSPT     R               REFFLD(BSPR)
     A                                      TEXT('CR Spot Rate')
      *
     A            FCHBR     R               REFFLD(DRBR)
     A                                      TEXT('Charges Alternate +
     A                                            A/C Branch Code')
      *
     A            FCHCP     R               REFFLD(DACP)
      *
     A            FCHSP     R               REFFLD(BSPR)
     A                                      TEXT('Charges Spot Rate')
      *
     A            FNPOC     R               REFFLD(TNLI)
     A                                      TEXT('No. Of Postings +
     A                                            Chargeable')
      *
     A            FTOTC     R               REFFLD(CHAM)
     A                                      TEXT('Total Transaction +
     A                                            Charges')
      *
     A            FCHOD     R               REFFLD(CHAM)
     A                                      TEXT('Highest O/D +
     A                                            Commission Amt')
      *
     A            FCTUR     R               REFFLD(CHAM)
     A                                      TEXT('Turnover +
     A                                            Commission Amount')
      *
     A            FCDRI     R               REFFLD(CHAM)
     A                                      TEXT('DR Int. +
     A                                            Commission Amount')
      *
     A            FCINA     R               REFFLD(CHAM)
     A                                      TEXT('Commission on +
     A                                            Inactive A/C')
      *
     A            FCMCB     R               REFFLD(CHAM)
     A                                      TEXT('Minimum CR Balance +
     A                                            Com Amt')
      *
     A            FCFIX     R               REFFLD(CHAM)
     A                                      TEXT('Fixed Commission +
     A                                            Amount')
      *
     A            FCHFX     R               REFFLD(CHAM)
     A                                      TEXT('Fixed Charge +
     A                                            Amount')
      *
     A            FCEDS     R               REFFLD(CHAM)
     A                                      TEXT('Extra Dly Stat +
     A                                            Com. Amount')
      *
     A            FTEDS     R               REFFLD(TNLI)
     A                                      TEXT('No. of Ex Dly +
     A                                            Stat Charged')
      *
     A            FSTCH     R               REFFLD(CHAM)
     A                                      TEXT('Total Stat +
     A                                            Charges Amount')
      *
     A            FTSTC     R               REFFLD(TNLI)
     A                                      TEXT('Total Statements +
     A                                            Charged Amt')
      *
     A            FGADI     R               REFFLD(CHAM)
     A                                      TEXT('Generated Adj. +
     A                                            to DR Int')
      *
     A            FGACI     R               REFFLD(CHAM)
     A                                      TEXT('Generated Adj. +
     A                                            to CR Int')
      *
     A            FDRRT     R               REFFLD(DRIR)
      *
     A            FDICT     R               REFFLD(DICT)
      *
     A            FDCST     R               REFFLD(DCST)
      *
     A            FDRIV     R               REFFLD(TNLI)
     A                                      TEXT('DR Int Rate +
     A                                            Change Val Date')
      *
     A            FDRTD     R               REFFLD(TNLI)
     A                                      TEXT('DR Interest "To" +
     A                                            Value Date')
      *
     A            FDRIN     R               REFFLD(CHAM)
     A                                      TEXT('DR Interest Numbers')
      *
     A            FDRIA     R               REFFLD(DR1)
     A                                      TEXT('DR Interest Amount')
      *
     A            FAODR     R               REFFLD(DRIR)
     A                                      TEXT('Additional O/D +
     A                                            Int Rate')
      *
     A            FAODV     R               REFFLD(TNLI)
     A                                      TEXT('Add. O/D +
     A                                            Value Date')
      *
     A            FAOTD     R               REFFLD(TNLI)
     A                                      TEXT('Add. O/D "To" +
     A                                            Value Date')
      *
     A            FODIN     R               REFFLD(CHAM)
     A                                      TEXT('Overdraft +
     A                                            Interest Numbers')
      *
     A            FODIA     R               REFFLD(DR1)
     A                                      TEXT('Overdraft +
     A                                            Interest Amount')
      *
     A            FCRRT     R               REFFLD(CRIR)
      *
     A            FCICT     R               REFFLD(CICT)
      *
     A            FCCST     R               REFFLD(CCST)
      *
     A            FCRIV     R               REFFLD(TNLI)
     A                                      TEXT('CR Int Rate +
     A                                            Change Val Date')
      *
     A            FCRTD     R               REFFLD(TNLI)
     A                                      TEXT('CR Interest "To" +
     A                                            Value Date')
      *
     A            FCRIN     R               REFFLD(CHAM)
     A                                      TEXT('CR Interest Numbers')
      *
     A            FCRIA     R               REFFLD(CR1)
     A                                      TEXT('CR Interest Amount')
      *
     A            FTDRI     R               REFFLD(DR1)
     A                                      TEXT('Total DR Interest')
      *
     A            FTCRI     R               REFFLD(CR1)
     A                                      TEXT('Total CR Interest')
      *
     A            FTNTI     R               REFFLD(CR1)
     A                                      TEXT('Total Net Interest')
      *
     A            FTCHA     R               REFFLD(CR1)
     A                                      TEXT('Total Charge Amount')
      *
     A            FTCOA     R               REFFLD(CR1)
     A                                      TEXT('Total +
     A                                            Commission Amount')
      *
      ** Start of change AR807782
     A            FDAC1         21A         TEXT('A/c to post debit +
     A                                           interest')
     A                                      COLHDG('A/c debit')
     A            FCAC1         21A         TEXT('A/c to post credit +
     A                                           interest')
     A                                      COLHDG('A/c credit')
      *
     A            FCHC1         21A         TEXT('A/c to post +
     A                                           charges')
     A                                      COLHDG('A/c charges')
      *
      ** End of change AR807782
      *
      ** Key Fields
      *
     A          K FBRCA
     A          K FCNUM
     A          K FCCY
     A          K FACOD
     A          K FACSQ
     A          K FKEYS1
     A          K FRECT
     A          K FKEYS2
      *
