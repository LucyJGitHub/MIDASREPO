      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas RE GEN ENTRIES - DETAIL')                        *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module
      *                                                               *
      *  LF/REEODL1 - RETAIL TODAY'S POSTINGS                         *
      *                                                               *
      *  (c) Finastra International Limited 2020                      *
      *                                                               *
      *  Last Amend No. MD035795           Date 12Mar20               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD035795 - Account Closure does not generate a turnover stmt *
      *             charge when indicated. Ensure that extra daily chg*
      *             will be posted if account has non-charge posting  *
      *****************************************************************
     A          R REEODPFF                  PFILE(REEODPF)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2020')
     A            RECI
     A            CNUM
     A            CCY
     A            ACOD
     A            ACSQ
     A            ACNO
     A            PSTD
     A            VALD
     A            TRAT
     A            PNAR
     A            PSTA
     A            DRCR
     A            SPOS
     A            BRCA
      *
     A          K BRCA
     A          K CNUM
     A          K CCY
     A          K ACOD
     A          K ACSQ
     A          K PSTD
      *
     A          S TRAT                      COMP(NE 91050)
