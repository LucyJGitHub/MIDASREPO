      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas RET.HIST.-CREDIT RATE CHG RECORDS')
     A*****************************************************************
     A*                                                               *
     A*  Midas - RETAIL II MODULE
     A*                                                               *
     A*  JLF/REHRCJ - RETAIL HISTORY FILE                             *
     A*               CREDIT RATE CHANGES RECORDS                     *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 05Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CRE001             Date 18Feb97               *
      *                 S01413  *CREATE    Date 13Apr93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *           (Recompile)                                         *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
     A*  CRE001 - Overdraft Interest Calculations                     *
     A*         - Add Additional Overdraft Interest Rate field        *
     A*  S01413 - Retail 3 Incorporation                              *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      JDFTVAL
     A                                      DYNSLT
     A          R REHRCJF                   JFILE(REHPOSR1 REHPOSR2 REHPOSR3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A          J                           JOIN(1 2)
     A                                      JFLD(BRCA BRCA)
     A                                      JFLD(CNUM CNUM)
     A                                      JFLD(CCY CCY)
     A                                      JFLD(ACOD ACOD)
     A                                      JFLD(ACSQ ACSQ)
     A                                      JFLD(VALD VALD)
     A                                      JFLD(DRCR DRCR)
     A          J                           JOIN(1 3)
     A                                      JFLD(BRCA BRCA)
     A                                      JFLD(CNUM CNUM)
     A                                      JFLD(CCY CCY)
     A                                      JFLD(ACOD ACOD)
     A                                      JFLD(ACSQ ACSQ)
     A                                      JFLD(VALD VALD)
     A                                      JFLD(DRCR DRCR)
     A            RECI                      JREF(1)
     A            CNUM                      JREF(1)
     A            CCY                       JREF(1)
     A            ACOD                      JREF(1)
     A            ACSQ                      JREF(1)
     A            RSEQ                      JREF(1)
     A            CONI                      JREF(1)
     A            ACNO
     A            PSTD
     A            VALD                      JREF(1)
     A            TRAT
     A            PNAR
     A            INTP
     A            INST
     A            DRCR                      JREF(1)
     A            TIME                      JREF(1)
     A            HISD                      JREF(1)
     A            SPOS
     A            BRCA                      JREF(1)
     A            NEIN
     A            RVLD
     A            PRIN
     A            LRCD
     A            INRT
     A            BRT
     A            RTSP
     A            INTH
     A            RAT2
     A            BAL2
     A            RAT3
     A            BAL3
      *                                              Begin /CRE001/
     A            AODR                      JREF(1)
      *                                              End   /CRE001/
     A          K BRCA
     A          K CNUM
     A          K CCY
     A          K ACOD
     A          K ACSQ
     A          K VALD
     A          S DRCR                      COMP(EQ 1)
