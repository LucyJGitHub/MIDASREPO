      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas RE Hist - credit rate chg records via HISD')     *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  JLF/REHRCJH - Retail History File                            *
      *                Credit Rate Changes Records                    *
      *                                                               *
      *  (C) Copyright Finastra International Limited 2011            *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR836982 *CREATE   Date 16May14               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR836982 - REC62N failed due to inserted account with back-  *
      *             valued rate change date. To avoid REC62N failure, *
      *             use HISD as key to search for rate change record. *
      *             Applied fix 244181. (Child: AR836983)             *
      *           - Applied for MD021848.                             *
      *                                                               *
      *****************************************************************
     A                                      JDFTVAL
     A                                      DYNSLT
     A          R REHRCJFH                  JFILE(REHPOSR1 REHPOSR2 REHPOSR3)
     A                                      TEXT('(C) Copyright +
     A                                      Finastra International Limited +
     A                                       2011')
     A          J                           JOIN(1 2)
     A                                      JFLD(BRCA BRCA)
     A                                      JFLD(CNUM CNUM)
     A                                      JFLD(CCY CCY)
     A                                      JFLD(ACOD ACOD)
     A                                      JFLD(ACSQ ACSQ)
     A                                      JFLD(HISD HISD)
     A                                      JFLD(DRCR DRCR)
     A          J                           JOIN(1 3)
     A                                      JFLD(BRCA BRCA)
     A                                      JFLD(CNUM CNUM)
     A                                      JFLD(CCY CCY)
     A                                      JFLD(ACOD ACOD)
     A                                      JFLD(ACSQ ACSQ)
     A                                      JFLD(HISD HISD)
     A                                      JFLD(DRCR DRCR)
     A            RECI                      JREF(1)
     A            CNUM                      JREF(1)
     A            CCY                       JREF(1)
     A            ACOD                      JREF(1)
     A            ACSQ                      JREF(1)
     A            RSEQ                      JREF(1)
     A            CONI                      JREF(1)
     A            ACNO
     A            PSTD
     A            VALD                      JREF(1)
     A            TRAT
     A            PNAR
     A            INTP
     A            INST
     A            DRCR                      JREF(1)
     A            TIME                      JREF(1)
     A            HISD                      JREF(1)
     A            SPOS
     A            BRCA                      JREF(1)
     A            NEIN
     A            RVLD
     A            PRIN
     A            LRCD
     A            INRT
     A            BRT
     A            RTSP
     A            INTH
     A            RAT2
     A            BAL2
     A            RAT3
     A            BAL3
     A            AODR                      JREF(1)
     A          K BRCA
     A          K CNUM
     A          K CCY
     A          K ACOD
     A          K ACSQ
     A          K HISD
     A          S DRCR                      COMP(EQ 1)
