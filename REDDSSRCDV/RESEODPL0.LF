      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*EXI *  TEXT('Midas RE Gen. Entries for RE Keyed on Acct & ClrDt')   *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  LF/RESEODPL0- Midas RE Gen. Entries for RE Keyed on          *
      *                Account and Cleared Balance Date               *
      *                                                               *
      *  (c) Finastra International Limited 2023                      *
      *                                                               *
      *  Last Amend No. MD021383A *CREATE  Date 16Jun23               *
      *  Prev Amend No. xxxxxxxx           Date ddMmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD021383A - Credt Lines (CLE025) Shadow Posting Correction.  *
      *            - MD061267 - Credit Lines (CLE025) fixes           *
      *                                                               *
      *****************************************************************
     A          R RESEDPL1                  PFILE(RESEODP1)
      *
      * Back/Current Valued Posting
      *
     A            BRCA      R
     A            CNUM      R
     A            CCY       R
     A            ACOD      R
     A            ACSQ      R
     A            ACNO      R
     A            PSTD      R
     A            VALD      R
     A            PSTA      R
     A            DRCR      R
      *
     A          K BRCA
     A          K CNUM
     A          K CCY
     A          K ACOD
     A          K ACSQ
     A          K PSTD
      *
     A          S VALD                      CMP(LE PSTD)
      *
     A          R RESEDPL2                  PFILE(RESEODP1)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited 2023')
      *
      * Forward Valued Posting
      *
     A            BRCA      R
     A            CNUM      R
     A            CCY       R
     A            ACOD      R
     A            ACSQ      R
     A            ACNO      R
     A            PSTD                      RENAME(VALD)
     A                                      TEXT('Posting Date')
     A                                      COLHDG('Posting Date')
     A            VALD                      RENAME(PSTD)
     A                                      TEXT('Value Date')
     A                                      COLHDG('Value Date')
     A            PSTA      R
     A            DRCR      R
      *
     A          K BRCA
     A          K CNUM
     A          K CCY
     A          K ACOD
     A          K ACSQ
     A          K PSTD
      *
     A          S PSTD                      CMP(GT VALD)
      *
