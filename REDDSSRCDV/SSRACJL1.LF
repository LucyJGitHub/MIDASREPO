      *****************************************************************
/*STD *  LFBASE                                                       *
/*EXS *  LF_IM                                                        *
/*E*I ***TEXT('Midas*RE*Account*balances*sorted*(all*RECI's)')*********                    MD041443
/*EXI *  TEXT('Midas RE Account balances sorted (all RECIs)')         *                    MD041443
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  JLF/SSRACJL1- RETAIL ACCOUNT BALANCES SORTED  (ALL RECI'S)   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 MD041443           Date 19Sep16               *
      *                 CRE073             Date 06Dec10               *
      *                 CRE075             Date 06Dec10               *
      *                 CER059             Date 19Jul10               *
      *                 BUG23967           Date 13May09               *
      *                 CER047             Date 19May08               *
      *                 CER020             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 241041 *CREATE     Date 13Jul06               *
      *                 XXXXXX             Date ddMMMyy               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *  MD046248 - Finastra Rebranding                               *
      *  MD041443 - CBC022001 failed due to invalid character in file *
      *             text description.                                 *
      *  CRE073 - Interest Rate Rounding (Recompile)                  *
      *  CRE075 - Effective Date for Retail Accounts (Recompile)      *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  BUG23967 - Add Local Industry Code Field. (Recompile)        *
      *  CER047 - German Features LF037-00 Reporting §24c KWG         *
      *           (Recompile)                                         *
      *  CER020 - Enhanced Rollover Window: Upgrade FGE089 to         *
      *           Midas Plus (Recompile)                              *
      *  241041 - Allow account activity report to include details    *
      *           of closed files                                     *
      *                                                               *
      *****************************************************************
     A                                      JDFTVAL
     A                                      DYNSLT
     A*
     A          R SSRACJF1                  JFILE(ACCNTAB CLINTCB)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A          J                           JOIN(1 2)
     A                                      JFLD(CNUM CNUM)
     A*
     A            RECI                      JREF(ACCNTAB)
     A            BRCA                      JREF(ACCNTAB)
     A            ATYP                      JREF(ACCNTAB)
     A            STYP                      JREF(ACCNTAB)
     A            CNUM                      JREF(ACCNTAB)
     A            CCY                       JREF(ACCNTAB)
     A            ACOD                      JREF(ACCNTAB)
     A            ACSQ                      JREF(ACCNTAB)
     A            ACNO                      JREF(ACCNTAB)
     A            CASK                      JREF(CLINTCB)
     A            CRNM                      JREF(CLINTCB)
     A            ACOC                      JREF(CLINTCB)
     A            EFAC                      JREF(ACCNTAB)
     A            RETB                      JREF(ACCNTAB)
     A            LRCR                      JREF(ACCNTAB)
     A            GRBL                      JREF(ACCNTAB)
     A                                      RENAME(LDBL)
     A            NETBL                     JREF(ACCNTAB)
     A                                      RENAME(CLBL)
     A            HELD                      JREF(ACCNTAB)
     A            MINB                      JREF(ACCNTAB)
     A            ODLN                      JREF(ACCNTAB)
     A            ODED                      JREF(ACCNTAB)
     A            DLIM                      JREF(ACCNTAB)
     A*
     A            EPGB                      JREF(ACCNTAB)
     A                                      RENAME(EPLB)
     A            EPNB                      JREF(ACCNTAB)
     A                                      RENAME(EPCB)
     A            RTLB                      JREF(ACCNTAB)
     A            RTCB                      JREF(ACCNTAB)
     A            STFQ                      JREF(ACCNTAB)
     A            DACO                      JREF(ACCNTAB)
     A            DACC                      JREF(ACCNTAB)
     A*
     A          K BRCA
     A          K CNUM
     A          K CCY
     A          K ACOD
     A          K ACSQ
     A*
     A          S ATYP                      VALUES('R')
