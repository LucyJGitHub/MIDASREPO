      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Account Balance Check Warning -Report')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE000012 - Account Balance Check Warning Report         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. BUG4596            Date 04Nov04               *
      *  Prev Amend No. CRE010  *CREATE    Date 06Sep04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  BUG4596 - RABC checking did not take into consideration      *
      *            overdraft and minimum balance                      *
      *  CRE010 - Retail Account Balance Check                        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header Record Format
      *
     A          R RE000012R0                TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE000012P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'ACCOUNT BALANCE CHECK +
     A                                      WARNING REPORT'
     A                                      SPACEA(1)
     A                                    46'-----------------------
     A                                      ---------------'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    40'O/D'
     A                                    97'Blocked'
     A                                      SPACEA(1)
      **Start of change BUG4596
     A**********                           3'Account'                                        BUG4596
     A**********                          13'Ccy'                                            BUG4596
     A**********                          20'Cleared Balance'                                BUG4596
     A                                     2'Account'
     A                                    12'Ccy'
     A                                    19'Cleared Balance'
      **End of change BUG4596
     A                                    39'Expiry'
     A                                    48'Overdraft Amount'
     A                                    73'Held Amount'
     A                                    92'Collateral Amount'
     A                                   114'Available Amount'
     A                                      SPACEA(1)
      **Start of change BUG4596
     A**********                           3'-------'                                        BUG4596
     A**********                          13'---'                                            BUG4596
     A**********                          20'---------------'                                BUG4596
     A                                     2'-------'
     A                                    12'---'
     A                                    19'---------------'
      **End of change BUG4596
     A                                    39'------'
     A                                    48'----------------'
     A                                    73'-----------'
     A                                    92'-----------------'
     A                                   114'----------------'
     A                                      SPACEA(1)
      *
      ** Account Detail Record Format
      *
     A          R RE000012R1
      *
      **Start of change BUG4596
     A**********  R1ACNO        10         2                                                 BUG4596
     A**********  R1CURR         3        13                                                 BUG4596
     A**********  R1CLBL        21        17                                                 BUG4596
     A            R1ACNO        10         1
     A            R1CURR         3        12
     A            R1CLBLX       22        16
      **End of change BUG4596
     A            R1OVEX         6        39
     A            R1OVAM        21        46
     A            R1HELD        21        68
     A            R1BCOA        21        90
     A            R1AVBL        21       112
     A                                      SPACEA(1)
      **Start of change BUG4596
     A                                    14'Minimum Balance Required: '
     A            R1MINB        21        41
     A                                      SPACEA(1)
      **End of change BUG4596
      *
      ** Transaction Header Record Format
      *
     A          R RE000012R2
      *
     A                                    12'Type'
     A                                    39'Reference'
     A                                    59'Posting Amount'
     A                                    83'Narrative'
     A                                   110'Projected Avail. Amount'
     A                                      SPACEA(1)
     A                                    12'----'
     A                                    39'---------'
     A                                    59'--------------'
     A                                    83'---------'
     A                                   110'-----------------------'
     A                                      SPACEA(1)
      *
      ** Transaction Detail Record Format
      *
     A          R RE000012R3
      *
     A            R1TYPE        30         2
     A            R1REFR        20        34
     A            R1PSTA        21        56
     A            R1NARR        30        79
     A            R1PROJ        21       111
     A                                      SPACEA(1)
      *
      ** Report Trailer
      *
     A          R RE000012R4
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH '
     A                                      SPACEB(3)
     A  37        A8BRCD         3        76
     A  37                                81'***'
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
