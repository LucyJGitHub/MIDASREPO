      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Unathorised Sweeps List report')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Modulae                                       *
      *                                                               *
      *  PRTF/RE000334P1 - Midas RE Unathorised Sweeps List report    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Last Amend No. CRE401  *CREATE    Date 26Nov09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE401 - 4-Eyes Checking Gaps - Sweeps                       *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY                    TEXT('(c) Misys +
     A                                      International Banking +
     A                                      Systems Ltd. 2009')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE000334P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    52'UNAUTHORISED SWEEP LIST'
     A                                      SPACEA(1)
     A                                    52'-----------------------'
     A                                    89'Branch -'
     A            RBRCA          3A       98
     A            RDESC         30A      102
     A                                      SPACEA(2)
      *
     A                                     2'SWEEP REF'
     A                                    18'START DATE'
     A                                    35'BRANCH'
     A                                    43'CREDIT A/C OF ORIGIN'
     A                                    72'BRANCH'
     A                                    80'CREDIT SWEEP A/C'
     A                                   109'THRESHOLD AMOUNT'
     A                                      SPACEA(1)
      *
     A                                     8'SWEEP TYPE'
     A                                    45'LAST CHANGE DATE/TYPE'
     A                                    82'LAST USER'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL                    SPACEA(1)
     A            RSWEP          6A        2
     A            RSDAT          7A       19
     A            RDRBR          3A       36
     A            RCRAO         18A       44
     A            RCBRC          3A       73
     A            RCRSA         18A       80
     A            RSTHR         22A      106
     A                                      SPACEB(1)
     A            RSWTP         26A        8
     A            RLCD           7A       47
     A            RCHTP          6A       55
     A            RLUSR         10A       82
      *
      *-------------------*
      ***  REPORT TRAILER *
      *-------------------*
      *
     A          R TRAILPY                   SPACEB(1)
     A N37                                52'***  END OF REPORT  ***'
     A                                      SPACEB(3)
     A  37                                46'***  END OF REPORT FOR BRANCH '
     A                                      SPACEB(3)
     A  37        RBRCA          3A       77
     A  37                                82'***'
      *
