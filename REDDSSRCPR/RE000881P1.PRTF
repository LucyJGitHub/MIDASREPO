      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Debit/Credit RE Transaction Advices')         *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE000881P1 - Debit/Credit Retail Transaction Advices    *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER030             Date 09Jul08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CRE020  *CREATE    Date 20Jan04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER030 - Multicash German Feature                            *
      *  CRE020 - Midas Plus Online Printing of Advices (GE7)         *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
     A                                      INDARA
      *
     A          R ADVICE                    TEXT('(c) Finastra International -
     A                                      Limited 2004')
      *
     A            RPCHTP        12A       15TEXT('Change Type')
     A                                      SKIPB(2)
     A                                    52'RETAIL TRANSACTION-
     A                                       ADVICE'
     A                                      SPACEA(1)
     A                                    52'__________________-
     A                                      _______'
     A                                      SPACEA(3)
     A            RPCNA1        35A       15TEXT('Cust. Name & Address 1')
     A                                      SPACEA(1)
     A            RPCNA2        35A       15TEXT('Cust. Name & Address 2')
     A                                      SPACEA(1)
     A            RPCNA3        35A       15TEXT('Cust. Name & Address 3')
     A                                      SPACEA(1)
     A            RPCNA4        35A       15TEXT('Cust. Name & Address 4')
     A                                      SPACEA(2)
      *
     A            RPTRTD        35A       48TEXT('Transaction Type Description')
     A                                    91'Date'
     A            BJMRDT    R             96REFFLD(MRDT)
     A                                      SPACEA(2)
      *
     A                                    20'Ref'
     A            RPTREF        15A       25
     A                                      SPACEA(2)
      *
     A                                     1'We have today'
     A            RPDRCR         8A       15
     A                                    24'your Account No.:'
     A            RPACCT        10A       42
     A                                    56'Value'
     A            RPVDAT         7A       62
     A                                      SPACEA(1)
     A                                     1'In respect of'
     A                                      SPACEA(3)
      *
     A            RPTNAR        35A       48TEXT('Transaction Narrative')
     A                                      SPACEA(4)
      *
     A            RPTCCY         3A       91TEXT('Currency')
     A            RPPAMT        16A       96TEXT('Posting Amount')
      **Start of change CER030
     A          R P1HEDRD
     A                                      SPACEB(2)
     A                                     1'Extended Narrative+
     A                                      (Debit):'
      *
     A          R P1EXTNARD
     A                                      SPACEB(1)
     A            EXTNARD       70        10
      *
     A          R P1HEDRC
     A                                      SPACEB(2)
     A                                     1'Extended Narrative+
     A                                      (Credit):'
      *
     A          R P1EXTNARC
     A                                      SPACEB(1)
     A            EXTNARC       70        10
      **End of change CER030
