      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RE Stopped Cheques Accounting Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE004211AU - Stopped Cheques Accounting Audit           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CRE019  *CREATE    Date 12Jan04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE019 - Cheque Processing and Maintenance                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
 
      *--------------------------------*
      **   Report Header Record Format *
      *--------------------------------*
 
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
 
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE004211AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
 
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
 
     A N21                                50'STOPPED CHEQUES ACCOUNTING +
     A                                       AUDIT'
     A N21                                  SPACEA(1)
     A  21                                40'STOPPED CHEQUES ACCOUNTING +
     A                                       AUDIT (AFTER END OF PERIOD)'
     A  21                                  SPACEA(1)
 
     A N21                                50'---------------------------+
     A                                       -----'
     A N21                                  SPACEA(3)
     A  21                                40'---------------------------+
     A                                       ---------------------------'
     A  21                                  SPACEA(3)
 
      *-----------------*
      **   File Control *
      *-----------------*
 
     A          R CONTROL
     A                                    30'Number of Records with +
     A                                      Cheques'
     A                                    73'-'
     A            RCNT1          5        87
     A                                      SPACEA(1)
 
     A                                    30'Number of Records +
     A                                      Updated'
     A                                    73'-'
     A            RCNT2          5        87
     A                                      SPACEA(1)
 
     A                                    30'Total value of +
     A                                      Stopped Cheques'
     A                                    73'-'
     A            RCNT3         17        75
     A                                      SPACEA(2)
 
      *-------------------*
      **   Database Error *
      *-------------------*
 
     A          R DBERROR
     A                                    52'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    38'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    38'*'
     A                                      SPACEB(1)
     A                                    91'*'
     A                                    38'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       57
     A                                    65''''
     A                                    91'*'
     A                                    38'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        57
     A                                    86 '''    *'
     A                                    38'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       57
     A                                    67''''
     A                                    91'*'
     A                                    38'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     57
     A                                    60''''
     A                                    91'*'
     A                                    38'*'
     A                                      SPACEB(1)
     A                                    91'*'
     A                                    38'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
 
