      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Stopped Cheques Accounting Exception Rpt')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE004211P1 - Stopped Cheques Accounting Exeption Report *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CRE019  *CREATE    Date 12Jan04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE019 - Cheque Processing and Maintenance                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
 
      *--------------------------------*
      **   Report Header Record Format *
      *--------------------------------*
 
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
 
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE004211P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
 
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
 
     A N21                                50'STOPPED CHEQUES ACCOUNTING +
     A                                       EXCEPTION'
     A  21                                30'STOPPED CHEQUES ACCOUNTING +
     A                                       EXCEPTION (AFTER END OF PERIOD)'
 
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
 
     A                                   132' '
     A                                      SPACEA(1)
     A N21                                50'---------------------------+
     A                                       ---------'
     A N21                                  SPACEA(3)
     A  21                                30'---------------------------+
     A                                       -------------------------------'
     A  21                                  SPACEA(3)
 
      *--------------------*
      **   Column Heading  *
      *--------------------*
 
     A                                      SPACEB(3)
     A                                     2' ORIGINAL ACCOUNT REFERENCE'
     A                                    32'    NEW ACCOUNT REFERENCE'
     A                                    62' CHEQUE'
     A                                    73'  POSTING AMOUNT'
     A                                    91'D/C'
     A                                    96'          NARRATIVE'
     A                                      SPACEA(1)
 
     A                                     2'----------------------------'
     A                                    32'----------------------------'
     A                                    62'--------'
     A                                    73'-----------------'
     A                                    91'---'
     A                                    96'------------------------------'
     A                                      SPACEA(2)
 
      *-------------------------*
      **   Detail Record Format *
      *-------------------------*
 
     A          R DETAIL1
     A            ROACRF        28         2
     A            RNACRF        28        32
     A            RCHQUE         8        62
     A            RPAMNT        17        73
     A            RDRCR          1        92
     A            RNARR         30        96
     A                                      SPACEA(1)
 
      *-------------------*
      ***  Report Trailer *
      *-------------------*
 
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
 
