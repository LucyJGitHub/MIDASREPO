      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Cheque Books Issued Today')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE004240P1 - Cheque Books Issued Today                  *
      *                                                               *
      *  (c) Finastra International Limited 2004                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG9011            Date 19Oct05               *
      *                 CRE019  *CREATE    Date 12Jan04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG9011- Increase field size of No. of Cheques to avoid      *
      *           truncation.                                         *
      *  CRE019 - Cheque Processing and Maintenance                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADERP1                  TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE004240P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(3)
      *
     A                                    52'CHEQUE BOOKS ISSUED TODAY'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    52'-------------------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings
      ***  -------------------
      *
     A                                     6'RETAIL A/C     CHQ/'
     A                                    29'START/END             NO. OF'
     A                                    63'DATE'
     A                                    84'CHARGE AMOUNT'
     A                                   102'CHARGE A/C'
     A                                   117'CHARGE'
     A                                      SPACEA(1)
     A                                    21'GIRO    CHEQUE NUMBER'
     A                                    51'CHEQUES     COLLECTED'
     A                                   117'TRANS. TYPE'
     A                                      SPACEA(1)
     A                                     6'----------     ----'
     A                                    29'-----------------     -------'
     A                                    63'---------'
     A                                    84'-------------'
     A                                   102'----------'
     A                                   117'-----------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAILP1
      *
     A                                      SPACEA(1)
     A            RACNO         10  0      6TEXT('Retail A/C No')
     A            RGCQI          1        22TEXT('Cheque/Giro Ind')
     A            RSCQN          8  0     29TEXT('Start Cheque No')
     A                                    37'-'
     A            RECQN          8  0     38TEXT('End Cheque No')
      *
      ** Start of BUG9011
     A**********  RNOCQ          2  0     53TEXT('No of Cheques')
     A            RNOCQ          3  0     53TEXT('No of Cheques')
      ** End of BUG9011
     A                                      EDTCDE(3)
     A            RDCOL          7        63TEXT('Collection Date')
     A            RCHAM         20        78TEXT('Charge Amount')
     A            RCHAC         10       102TEXT('Charge Account')
     A            RCHTT          5       120TEXT('Charge Trans. Type')
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILERP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
