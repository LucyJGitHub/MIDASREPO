      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Cheque Books Cancelled/Reversed Today')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE004240P2 - Cheque Books Cancelled/Reversed Today      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2004            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CRE019  *CREATE    Date 12Jan04               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE019 - Cheque Processing and Maintenance                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADERP2                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2004')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE004240P2'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(3)
      *
     A                                    45'CHEQUE BOOKS CANCELLED+
     A                                      /REVERSED TODAY'
      *
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    45'----------------------+
     A                                      ---------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings
      ***  -------------------
      *
     A                                     6'RETAIL A/C     CHQ/'
     A                                    29'START/END'
     A                                    52'DATE ISSUED/'
     A                                    82'CHARGE AMOUNT'
     A                                   101'CHARGE A/C'
     A                                   117'CHARGE'
     A                                      SPACEA(1)
     A                                    21'GIRO    CHEQUE NUMBER'
     A                                    52'DATE COLLECTED'
     A                                   117'TRANS. TYPE'
     A                                      SPACEA(1)
     A                                     6'----------     ----'
     A                                    29'-----------------'
     A                                    52'---------------'
     A                                    82'-------------'
     A                                   101'----------'
     A                                   117'-----------'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAILP2
      *
     A                                      SPACEA(1)
     A            RACNO         10  0      6TEXT('Retail A/C No')
     A            RGCQI          1        22TEXT('Cheque/Giro Ind')
     A            RSCQN          8  0     29TEXT('Start Cheque No')
     A                                    37'-'
     A            RECQN          8  0     38TEXT('End Cheque No')
     A            RDISS          7        52TEXT('Collection Date')
     A                                    59'/'
     A            RDCOL          7        60TEXT('Collection Date')
     A            RCHAM         20        76TEXT('Charge Amount')
     A            RCHAC         10       101TEXT('Charge Account')
     A            RCHTT          5       120TEXT('Charge Trans. Type')
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILERP2
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
