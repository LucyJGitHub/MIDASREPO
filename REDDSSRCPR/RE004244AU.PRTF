      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RE Projected Insufficient Funds Audit Report')   *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE004244AU - Projected Insufficient Funds Audit Report  *
      *                                                               *
      *  (c) Finastra International Limited 2011                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR1005246          Date 30Jul12               *
      *                 CAP207  *CREATE    Date 10Feb11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR1005246 - Program RE004244AU receiver too small to hold    *
      *              result.                                          *
      *  CAP207 - Account Balance Check extended to other APIs        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU
     A                                      TEXT('(c) Finastra International -
     A                                      Limited 2011')
      *
     A                                      SKIPB(003)
     A                                      SPACEA(003)
     A                                     6
     A                                      'REFERENCE RE004244AU'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            BJMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                    39
     A                                      'LIST OF PROJECTED INSUFFICIENT FUN-
     A                                      D REPORT - AUDIT'
     A                                      SPACEB(001)
      *
     A                                    39
     A                                      '-----------------------------------
     A                                      ----------------'
     A                                      SPACEB(001)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                      SPACEA(002)
     A                                    58
     A                                      'CALCULATED'
     A                                    58
     A                                      '----------'
     A                                      SPACEB(001)
      *
     A                                     6
     A                                      'NEGATIVE BALANCE'
     A                                      SPACEB(003)
      *
     A**********  RNGCNT         5  0     60                                               AR1005246
     A            RNGCNT        10  0     60                                               AR1005246
     A                                      EDTCDE(3)
      *
     A                                     6
     A                                      'REFERRED'
     A                                      SPACEB(001)
      *
     A            RRFCNT         5  0     60
     A                                      EDTCDE(3)
      *
     A                                     6
     A                                      'BLOCKED'
     A                                      SPACEB(001)
      *
     A            RBKCNT         5  0     60
     A                                      EDTCDE(3)
      *
     A                                     6
     A                                      'INACTIVE'
     A                                      SPACEB(001)
      *
     A            RINCNT         5  0     60
     A                                      EDTCDE(3)
      *
     A                                     6
     A                                      'LIQUIDATED'
     A                                      SPACEB(001)
      *
     A            RLQCNT         5  0     60
     A                                      EDTCDE(3)
      *
     A                                     6
     A                                      'BAD DEBT'
     A                                      SPACEB(001)
      *
     A            RBDCNT         5  0     60
     A                                      EDTCDE(3)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25
     A                                      '**** DATABASE ERROR ****'
     A                                      SPACEB(002)
     A                                    11
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(002)
     A                                    11
     A                                      '*'
     A                                      SPACEB(001)
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    FILE NAME    '''
     A                                      SPACEB(001)
     A            DBFILE         8A       30
     A                                    38
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    KEY          '''
     A                                      SPACEB(001)
     A            DBKEY         29        30
     A                                    59
     A                                      '''    *'
     A                                    11
     A                                      '*    PROGRAM NAME '''
     A                                      SPACEB(001)
     A            DBPGM         10A       30
     A                                    40
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*    AT           '''
     A                                      SPACEB(001)
     A            DBASE          3  0     30
     A                                    33
     A                                      ''''
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '*'
     A                                      SPACEB(001)
     A                                    64
     A                                      '*'
     A                                    11
     A                                      '**********************************-
     A                                      ********************'
     A                                      SPACEB(001)
