      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Projected Insufficient Funds Report')         *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE004244P1 - Projected Insufficient Funds Report        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2011            *
      *                                                               *
      *  Last Amend No. CAP207  *CREATE    Date 10Feb11               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CAP207 - Account Balance Check extended to other APIs        *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Report Header
      *
     A          R RE004244H1
     A                                      TEXT('(c) Misys International Banki-
     A                                      ng Systems Ltd. 2011')
      *
     A                                      SKIPB(003)
     A                                     1
     A                                      'REFERENCE RE004244P1'
     A            BJURPT    R             37
     A                                      REFFLD(URPT)
     A                                   105
     A                                      'DATE'
     A            BJMRDT    R            110
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                    44
     A                                      'Projected Insufficient Funds Repor-
     A                                      t'
     A                                      SPACEB(001)
     A                                    44
     A                                      '-----------------------------------
     A                                      -'
     A                                      SPACEB(001)
      *
      ** Report Sub-header 1
      *
     A          R RE004244H2
     A                                     1
     A                                      'Account:'
     A                                      SPACEB(002)
     A            R1ACCT        28A  O    10
     A            R1RESN        30A  O    47
     A                                    +9
     A                                      'Account Balance Check Required:'
     A            R1ABCR         3A  O    +1
      *
      ** Report Sub-header 2
      *
     A          R RE004244H3
     A                                      SPACEB(001)
     A            R2RESN        30A  O    47
      *
      ** Report Sub-header 3
      *
     A          R RE004244H4
     A                                     1
     A                                      'Ledger Balance:'
     A                                      SPACEB(002)
     A            R3LDBL        21A  O   +10
     A                                    +8
     A                                      'Cleared Balance:'
     A            R3CLBL        21A  O   +11
     A                                     1
     A                                      'Overdraft Line:'
     A                                      SPACEB(001)
     A            R3ODLN        21A  O    26
     A                                    +8
     A                                      'Overdraft Tolerance:'
     A            R3TOLR        21A  O    +7
     A                                     1
     A                                      'Total Reservations:'
     A                                      SPACEB(001)
     A            R3RESV        21A  O    26
     A                                    +8
     A                                      'Total Blocked Collateral:'
     A            R3TOBC        21A  O    +2
     A                                     1
     A                                      'Total Hard Held Items:'
     A                                      SPACEB(001)
     A            R3TOHH        21A  O    26
     A                                    +8
     A                                      'Total Soft Held Items:'
     A            R3TOSH        21A  O    +5
     A                                     1
     A                                      'Available Balances:'
     A                                      SPACEB(002)
     A            R3DAT1         7A  O     1
     A                                      SPACEB(001)
     A            R3AVA1        21A  O   +18
     A            R3DAT2         7A  O    +8
     A            R3AVA2        21A  O   +20
     A            R3DAT3         7A  O     1
     A                                      SPACEB(001)
     A            R3AVA3        21A  O   +18
     A            R3DAT4         7A  O    +8
     A            R3AVA4        21A  O   +20
     A            R3DAT5         7A  O     1
     A                                      SPACEB(001)
     A            R3AVA5        21A  O   +18
     A            R3DAT6         7A  O    +8
     A            R3AVA6        21A  O   +20
     A            R3DAT7         7A  O     1
     A                                      SPACEB(001)
     A            R3AVA7        21A  O   +18
     A            R3DAT8         7A  O    +8
     A            R3AVA8        21A  O   +20
     A            R3DAT9         7A  O     1
     A                                      SPACEB(001)
     A            R3AVA9        21A  O   +18
     A                                     1
     A                                      'Postings:'
     A                                      SPACEB(002)
     A                                     1
     A                                      'Value Date'
     A                                      SPACEB(001)
     A                                   +10
     A                                      'Posting Date'
     A                                   +18
     A                                      'Amount'
     A                                   +11
     A                                      'Posting Narrative'
      *
      ** Report Detail
      *
     A          R RE004244D1
     A                                      SPACEB(001)
     A            R4VUDT         7A  O     1
     A            R4PTDT         7A  O   +13
     A            R4MVAM        21A  O   +11
     A            R4NRTD        30A  O    +9
      *
      ** Report Footer
      *
     A          R RE004244F1
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
