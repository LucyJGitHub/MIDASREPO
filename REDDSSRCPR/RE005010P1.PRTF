      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD66                                                     *
/*EXI *  TEXT('Midas RE Daily Withholding Tax Report PRTF')           *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE005010P1 - Daily Withholding Tax Report               *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2015            *
      *                                                               *
      *  Last Amend No. CGL165  *CREATE    Date 23Feb15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL165 - Dual Withholding Tax                                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Page header - 1
      *
     A          R HEAD1
     A                                      TEXT('PAGE HEADER 1')
     A                                      SKIPB(003)
     A                                      SPACEA(002)
     A                                     7
     A                                      'REFERENCE RE005010P1'
     A            TITL          53A  O    41
     A                                   101
     A                                      'DATE'
     A            RUNA           7A  O   107
     A                                   119
     A                                      'PAGE'
     A            PAGE           4S 0O   124
     A                                      EDTCDE(Z)
      *
     A                                    51
     A                                      'RETAIL DAILY WITHHOLDING TAX REPOR-
     A                                      T'
     A                                      SPACEB(002)
      *
     A                                    51
     A                                      '-----------------------------------
     A                                      -'
     A                                      SPACEB(001)
      *
     A                                     1
     A                                      'CURRENCY :'
     A                                      SPACEB(002)
     A            CCY            3A  O    12
     A                                    29
     A                                      '----------- MAIN TAX ------------'
     A                                      SPACEB(002)
     A                                    64
     A                                      '--------- SECONDARY TAX ---------'
     A                                     1
     A                                      'ACCOUNT NO/'
     A                                      SPACEB(001)
     A                                    29
     A                                      'CODE      RATE  AMT(IN DEAL CCY)/'
     A                                    64
     A                                      'CODE      RATE  AMT(IN DEAL CCY)/'
     A                                   103
     A                                      'INTEREST'
     A                                   112
     A                                      'DUE'
     A                                   120
     A                                      'INTEREST'
     A                                   129
     A                                      'PAID'
     A                                     1
     A                                      'CUSTOMER NAME'
     A                                      SPACEB(001)
     A                                    45
     A                                      'AMT(IN LOCAL CCY)'
     A                                   +18
     A                                      'AMT(IN LOCAL CCY)'
      *
      ** Detail record
      *
     A          R DETAIL
     A                                      TEXT('DETAIL RECORD')
     A                                      SPACEB(002)
     A            CNUM           6A  O     1
     A                                    +0
     A                                      '-'
     A            CCY            3A  O     8
     A                                    +0
     A                                      '-'
     A            ACOD          10A  O    12
     A                                    22
     A                                      '-'
     A            ASEQ           2S  O    23
     A            NTRC           2S  O    29
     A            NTRT           4S 2O    38
     A                                      EDTWRD(' 0.  ')
     A            NTDLAT        17A  O    45
     A            LTRC           2S  O    64
     A            LTRT           4S 2O    73
     A                                      EDTWRD(' 0.  ')
     A            LTDLAT        17A  O    80
     A            INTDUE        17A  O    98
     A            INTPAD        17A  O   116
     A            CNA1          35A  O     1
     A                                      SPACEB(001)
     A            NTBSAT        17A  O    45
     A            LTBSAT        17A  O    80
      *
      ** Totals record
      *
     A          R TOTPRT
     A                                      TEXT('TOTALS RECORD')
     A N24                                  SKIPB(003)
     A N24                                  SPACEA(002)
     A N24                                 7
     A                                      'REFERENCE RE005010P1'
     A N24        TITL          53A  O    41
     A N24                               101
     A                                      'DATE'
     A N24        RUNA           7A  O   107
     A N24                               119
     A                                      'PAGE'
     A N24        PAGE           4S 0O   124
     A                                      EDTCDE(Z)
      *
     A N24                                44
     A                                      'RETAIL DAILY WITHHOLDING TAX REPOR-
     A                                      T - '
     A                                      SPACEB(002)
     A N24                                82
     A                                      'FILE CONTROL'
      *
     A N24                                44
     A                                      '-----------------------------------
     A                                      ----------------'
     A                                      SPACEB(001)
     A N24                                49
     A                                      'TOTAL NO OF RECORDS PROCESSED ='
     A                                      SPACEB(005)
     A N24        CNOR           4  0O    83
     A                                      EDTCDE(3)
     A  24                                54
     A                                      '*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(002)
     A                                    57
     A                                      '*** END OF REPORT ***'
     A                                      SKIPB(054)
      *
      ** Database error print
      *
     A          R DBERRP
     A                                      TEXT('DATABASE ERROR')
     A                                      SPACEB(002)
      *
     A                                     7
     A                                      'DATABASE ERROR IN '
     A                                    26
     A                                      'FILE '
     A            DBFILE         8   O    31
     A                                    39
     A                                      ','
     A                                    41
     A                                      'KEY '
     A            DBKEY         26   O    45
     A                                    71
     A                                      ','
     A                                    73
     A                                      'IN '
     A            DBPGM          8   O    76
     A                                    84
     A                                      ','
     A                                    86
     A                                      'AT '
     A            DBASE          2   O    89
