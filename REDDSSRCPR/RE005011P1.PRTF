      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD66                                                     *
/*EXI *  TEXT('Midas DL Monthly/Yearly W/h Tax Report')               *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE005011P1 - Midas RE Monthly Withholding Tax Report    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2015            *
      *                                                               *
      *  Last Amend No. CGL165  *CREATE    Date 23Feb15               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL165 - Dual Withholding Tax                                *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      ** Page header
      *
     A          R HEADER
     A                                      TEXT('PAGE HEADER')
     A                                      SKIPB(003)
     A                                     7'REFERENCE  RE005011P1'
     A            TITL          53A  O    41
     A                                   101
     A                                      DFT('DATE')
     A            RUNA           7A  O   107
     A                                   119
     A                                      DFT('PAGE')
     A            PAGE           4S 0O   124
     A                                      EDTCDE(Z)
      *
     A                                    53
     A                                      DFT('MONTHLY WITHHOLDING TAX REPORT-
     A                                      ')
     A                                      SPACEB(002)
      *
     A                                    53
     A                                      DFT('-------------------------------
     A                                      ')
     A                                      SPACEB(001)
     A                                    16
     A                                      SPACEB(002)
     A                                      DFT('TAX')
     A                                    94
     A                                      DFT('MTD W/H TAX')
     A                                    +8
     A                                      DFT('YTD W/H TAX')
      *
     A                                     6
     A                                      DFT('CURRENCY')
     A                                      SPACEB(001)
     A                                    15
     A                                      DFT('CODE')
     A                                    21
     A                                      DFT('DESCRIPTION')
     A                                    55
     A                                      DFT('MTD W/H TAX')
     A                                    74
     A                                      DFT('YTD W/H TAX')
     A                                    93
     A                                      DFT('(IN LOCAL CCY)')
     A                                   112
     A                                      DFT('(IN LOCAL CCY)')
      *
      ** Detail record
      *
     A          R DETAIL
     A                                      TEXT('DETAIL RECORD')
     A                                      SPACEB(002)
     A            CCY            3A  O     8
     A            STXRC          2   O    16
     A            STXDS         30   O    20
     A            SMTXA         17A  O    52
     A            SYTXA         17A  O    71
     A            SMTXB         17A  O    91
     A            SYTXB         17A  O   110
      *
      ** Totals record
      *
     A          R TOTPRT
     A                                      TEXT('TOTALS RECORD')
     A                                      SKIPB(051)
     A                                    11
     A                                      DFT('TOTAL  NO  OF  RECORDS')
     A            CNOR           4  0O    43
     A                                      EDTCDE(3)
     A                                    57
     A                                      DFT('*** END OF REPORT ***')
     A                                      SKIPB(055)
      *
      ** Database error print
      *
     A          R DBERRP
     A                                      TEXT('DATABASE ERROR')
     A                                      SPACEB(002)
      *
     A                                     7
     A                                      DFT('DATABASE ERROR IN ')
     A                                    26
     A                                      DFT('FILE ')
     A            DBFILE         8   O    31
     A                                    39
     A                                      DFT(',')
     A                                    41
     A                                      DFT('KEY ')
     A            DBKEY         26   O    45
     A                                    71
     A                                      DFT(',')
     A                                    73
     A                                      DFT('IN ')
     A            DBPGM          8   O    76
     A                                    84
     A                                      DFT(',')
     A                                    86
     A                                      DFT('AT ')
     A            DBASE          2   O    89
      *
      ** Format for empty file.
      *
     A          R NODET
     A                                      TEXT('NO DETAILS')
     A                                      SKIPB(017)
     A                                    53
     A                                      DFT('*** NO DETAILS TO REPORT ***')
     A                                    57
     A                                      DFT('*** END OF REPORT ***')
     A                                      SKIPB(054)
