      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RE Sweeps list audit printer file')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE0331AU - Sweeps List Audit Printer File               *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRE004 *CREATE     Date 20Jan97               *
      *                 000000             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE004 - Sweep Accounting.                                   *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE RE0331AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(001)
      *
     A  50                                42'PARTIAL INTERIM SWEEP LIST - AUDIT'
     A                                      SPACEB(002)
     A  51                                42'INTERIM SWEEP LIST - AUDIT'
     A                                      SPACEB(002)
     A  52                                42'PARTIAL SWEEP LIST - AUDIT'
     A                                      SPACEB(002)
     A  50                                42'--------------------------'
     A                                      SPACEB(001)
     A  51                                42'------------------'
     A                                      SPACEB(001)
     A  52                                42'------------------'
     A                                      SPACEB(001)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R CONTROL
     A                                    31'CONTROL TOTALS'
     A                                      SPACEB(002)
     A                                    31'--------------'
     A                                      SPACEB(001)
     A                                    31'NO. OF SWEEPS INSERTED'
     A                                      SPACEB(003)
     A            RINS           6A  O   +25
     A                                    31'NO. OF SWEEPS DELETED'
     A                                      SPACEB(003)
     A            RDEL           6A  O   +26
     A                                    31'NO. OF SWEEPS AMENDED'
     A                                      SPACEB(003)
     A            RAMD           6A  O   +26
     A                                    31'TOTAL NUMBER OF SWEEPS ON FILE'
     A                                      SPACEB(003)
     A            RTREC          6A  O    +7
     A                                    31'TOTAL NUMBER OF SWEEPS PROCESSED'
     A                                      SPACEB(003)
     A            TPROC          6A  O    68
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(003)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(002)
     A                                    11'**********************************-
     A                                      ********************'
     A                                      SPACEB(002)
     A                                    11'*'
     A                                      SPACEB(001)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(001)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(001)
     A            DBKEY         29        30
     A                                    59'''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(001)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(001)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(001)
     A                                    64'*'
     A                                    11'**********************************-
     A                                      ********************'
     A                                      SPACEB(001)
      *
