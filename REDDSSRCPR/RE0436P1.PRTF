      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas CI Bulk Cheque Deposit Repair Queue List   6')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE0436P1 - CI Bulk Cheque Deposit Repair Queue List     *
      *                                                               *
      *  (c) Finastra International Limited 2003                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. DDB026  *CREATE    Date 25Dec03               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  DDB026 - Bulk Cheque Deposit                                 *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *
     A          R HEAD                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2003')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE0436P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    49'Bulk Cheque Deposit Requi+
     A                                      ring Repair'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    49'-------------------------+
     A                                      -----------'
     A                                      SPACEA(2)
      *
     A          R HEAD1
     A  52                               110'*** REVERSAL ***'
     A                                      SPACEA(1)
     A                                      HIGHLIGHT
     A                                     3'Cashier Ref: '
     A            PCREF         12A       16
     A                                    33'Batch Ref: '
     A            PBREF          3A       44
     A                                    53'Branch: '
     A            PBRCA          3A       61
     A                                    69'Teller ID: '
     A            PTLID          3A       80
     A*
     A                                     3'Deposit A/C: '
     A                                      SPACEB(1)
     A            PACCT         13A       16
     A            PANAM         20A       31
     A                                    56'A/C ccy: '
     A            PACCY          3A       65
     A                                    73'Value Date: '
     A            PVLDT          6A       85
     A                                     3'Cheque ccy: '
     A                                      SPACEB(1)
     A            PCCCY          3A       15
     A                                    21'Cheque amount: '
     A            PAMNT         22A       36
     A                                    61'No Cheques: '
     A            PNCHQ          2A       73
     A                                    78'Charge ccy: '
     A            PCHCY          3A       90
     A                                    96'Charge amount: '
     A            PCHAM         22A      111
     A                                     3'Reference: '
     A                                      SPACEB(1)
     A            PREF          15A       14
     A                                    34'Narrative: '
     A            PNARR         35A       45
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A                                     3'Item Type'
     A                                      SPACEB(3)
     A                                    18'Account'
     A                                    36'Issuing Bank'
     A                                    62'Cheque Amount'
     A                                    85'Equivalent Amount'
     A                                   107'Serial Number'
      *
      *
     A          R DETAIL1
     A            PITEM          1A        7
     A                                      SPACEB(2)
     A            PTRET         13A       18
     A            PIBNK         11A       36
     A            PTAMT         22A       53
     A            PEQIV         22A       80
     A            PSNUM         16A      107
      *
     A          R NODETL
     A                                    46'***  NO DETAILS TO +
     A                                      REPORT  ***'
     A                                      SPACEB(3)
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
