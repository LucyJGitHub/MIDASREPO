      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Shadow Post Audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module
     A*                                                               *
     A*  PRTF/RE0672AU - Shadow Post Audit report                     *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CCB002             Date 04Sep96               *
      *                 041693             Date 05Jan93               *
     A*                 045014             DATE 28NOV92               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CCB002 - COB Performance Enhancements                        *
     A*           Access now via access object                        *
     A*           MEMOS can no longer be 'Full'.                      *
     A*  041693 - Delivered as part of fix.                           *
     A*  045014 - New program to Generate Shadow Post Movements       *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R F1HDR1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  3  6'REFERENCE'
     A            ##PGM         10      3 16
     A            BJURPT    R           3 37REFFLD(URPT)
     A                                  3100'DATE'
     A            BJMRDT    R           3105REFFLD(RUNA)
     A                                  3119'PAGE'
     A            PAGE1          4  0   3124EDTCDE(Z)
     A                                  5 57'Shadow Post Audit Report'
     A                                      UNDERLINE
      *
      *  Audit Information
      *
     A          R F1DET1                    TEXT('Audit Information')
     A                                      SPACEB(2)
     A                                    30'Accounts Master Information'
     A                                      SPACEB(2)
     A                                    30'Records read......:'
     A                                      SPACEB(1)
     A            F1AREC        10  0     50EDTCDE(J)
     A                                    30'Retail Accounts...:'
     A                                      SPACEB(1)
     A            F1RREC        10  0     50EDTCDE(J)
     A                                    30'Memos Information'
     A                                      SPACEB(2)
     A                                    30'Records Updated...:'
     A                                      SPACEB(1)
     A            F1MREC        10  0     50EDTCDE(J)
      * Start CCB002
     A**51********                        80'*** Memos File Full ***'
     A************                          HIGHLIGHT
      * End CCB002
     A                                    30'RSACMTPD Information'
     A                                      SPACEB(2)
     A                                    30'Records Written...:'
     A                                      SPACEB(1)
     A            F1SREC        10  0     50EDTCDE(J)
      *
      *  Database Error Detail
      *
     A          R F1DBERR                   TEXT('Database Error')
     A                                      SPACEB(1)
     A                                    30'Database Error'
     A                                      SPACEB(2)
     A                                    30'Error in Program..:'
     A                                      SPACEB(2)
     A            DBPGM         10        50
     A                                    30'File..............:'
     A                                      SPACEB(2)
     A            DBFILE        10        50
     A                                    30'Key...............:'
     A                                      SPACEB(2)
     A            DBKEY         30        50
     A                                    30'Subroutine........:'
     A                                      SPACEB(2)
     A            DBSTK         10        50
     A                                    30'Error number......:'
     A                                      SPACEB(2)
     A            DBERNB         5  0     50EDTCDE(J)
     A                                    30'Narrative.........:'
     A                                      SPACEB(2)
     A            DBNARR        80        50
      *
      *  End of Report
      *
     A          R F1TRL1                    TEXT('End of Report')
     A                                      SPACEB(2)
     A                                    65'*** End of Report ***'
