      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Cash Management Hierarchy List')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE100501P1 - Cash Management Hierarchy List             *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. 214360             Date 03Feb03               *
      *                 CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  214360 - Cash Management Deferred Posting                    *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-----------------------------------*
      ***  Report Header Record Format  ***
      *-----------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE100501P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    51'CASH MANAGEMENT HIERARCHY +
     A                                      LIST'
      *
     A  37                                82'-'
     A  37        CDBRCA    R             84REFFLD(BRCA)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    51'--------------------------+
     A                                      ----'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Hierarchy Details  ***
      *-------------------------*
      *
     A          R CMHD
      *
     A                                     2'Hierarchy'
     A            CDHISN        10A       14
     A                                    26'Hierarchy Type'
     A            CDHTYP         2A       42
     A                                    47'Hierarchy ID'
     A            CDHIER         9S 0     61
     A                                      SPACEA(1)
      *
     A                                     2'Description'
     A            CDDESC        50A       14
     A                                      SPACEA(1)
      *
     A                                     2'Main Branch'
     A            CDBRCA         3A       16
     A                                    27'Date Effective From'
     A            DDDEFR         7A       47
     A                                    57'Review Date'
     A            DDRVDT         7A       74
     A                                      SPACEA(1)
      *
     A                                     2'Input By User'
     A            CDINBU        10A       16
     A                                    27'Authorized By User'
     A            CDAUBU        10A       46
     A                                    57'Last Change Date'
     A            DDLCDT         7A       74
     A                                      SPACEA(1)
      *
      *-----------------------------
      ***  Group Account Details  **
      *-----------------------------
      *
     A          R GAHD
     A                                     2'Currency'
     A            GDCCY          3A       13
     A                                      SPACEA(1)
      *
     A                                     2'Top on Balance Sheet'
     A            DDTOPB         3A       23
     A                                      SPACEA(1)
      *
     A                                     2'Link Effective At Last Capitalizat-
     A                                      ion Date'
     A            DDLELC         3A       47
     A                                      SPACEA(1)
      *
     A                                     2'Unlink Effective At Last Capitaliz-
     A                                      ation Date'
     A            DDUELC         3A       47
     A                                      SPACEA(2)
      *
      *---------------------------------------*
      ***  Zero Balancing/Sweeping Details  ***
      *---------------------------------------*
      *
     A          R ZSHD
      *
     A                                     2'Holidays'
     A            DDSHOC         7A       16
     A                                    27'Check For Cover on Top'
     A            DDCHKC         3A       50
     A                                      SPACEA(1)
      *
     A                                     2'Transaction Type - Top'
     A            ZDTYPT         5A       25
     A                                    32'Transaction Type - Sweep'
     A            ZDTYPS         5A       57
     A                                      SPACEA(1)
      *
     A                                     2'Input Cycle Sweeping Frequency'
     A            ZDICSF        48A       33
     A                                      SPACEA(1)
      *
     A                                     2'Input Cycle Sweeping/Topping'
     A            DDICSI        48A       33
     A                                      SPACEA(1)
      *
     A          R ZSHDX
      *
     A                                     2'External'
     A            DDEXTL         3A       11
     A                                    15'Master Account'
     A            DDMAST         3A       30
     A                                    34'Narrative'
     A            ZDENAR        30A       44
     A                                      SPACEA(1)
      *
     A                                     2'External Forward Value Sweeping'
     A            DDEFVS         3A       34
     A                                    38'Forward Value Days'
     A            ZDEFVD         3A       57
     A                                    61'Back Value Days'
     A            ZDEBVD         3A       77
     A                                      SPACEA(1)
      *
     A                                     2'Destination'
     A            ZDDEST        18A       14
     A                                    33'Type'
     A            ZDDSTT         1A       38
     A                                    41'Senders Correspondent'
     A            ZDSNDC         2A       63
     A                                      SPACEA(1)
      *
     A                                     2'Receivers Correspondent'
     A            ZDRCVC        11A       26
     A                                    38'Cover Message Required'
     A            DDMCMR         3A       61
     A                                      SPACEA(1)
      *
     A                                     2'External Account'
      *                                                           /*214360*/
     A************ZDEXA1********35A*******19
     A**************************************SPACEA(1)
     A************ZDEXA2********35A*******19
     A**************************************SPACEA(1)
     A************ZDEXA3********35A*******19
     A**************************************SPACEA(1)
     A************ZDEXA4********35A*******19
     A**************************************SPACEA(1)
     A************ZDEXA5********35A*******19
     A**************************************SPACEA(1)
     A            DDEXA11       25A       19
     A            ZDEXA2        35A       45
     A                                      SPACEA(1)
     A            DDEXA12       10A       19
     A            ZDEXA3        35A       45
     A                                      SPACEA(1)
     A            ZDEXA4        35A       45
     A                                      SPACEA(1)
     A            ZDEXA5        35A       45
     A                                      SPACEA(1)
      *                                                           /*214360*/
      *
     A                                     2'103'
     A                                     6'Code'
     A            ZDICD3         4A       11
     A                                    16'Charges'
     A            ZDCHG3         3A       24
     A**Start of change CGL029
     A**********                          28'Account'                                         CGL029
     A**********  ZDCAC3        18A       36                                                  CGL029
     A**********                          55'Amount'                                          CGL029
     A                                    28'A/c'
     A            ZDCAC3        24A       32
     A                                    57'Amt.'
     A**End of change CGL029
     A            DDCAM3        17A       62
     A                                      SPACEA(1)
      *
     A                                     6'Value Date Offset'
     A            ZDMVDO         1A       24
     A                                    26'Bank Operation Code'
     A            ZDBOCD         4A       46
     A                                      SPACEA(1)
      *
     A                                     2'101'
     A                                     6'Code'
     A            ZDICD1         4A       11
     A                                    16'Charges'
     A            ZDCHG1         3A       24
     A**Start of change CGL029
     A**********                          28'Account'                                         CGL029
     A**********  ZDCAC1        18A       36                                                  CGL029
     A**********                          55'Amount'                                          CGL029
     A                                    28'A/c'
     A            ZDCAC1        24A       32
     A                                    57'Amt.'
     A**End of change CGL029
     A            DDCAM1        17A       62
      *                                                           /*214360*/
     A                                      SPACEA(1)
     A                                     2'Deferred Posting'
     A            DDDEFP         3A       19
     A                                    23'Deferred Posting Zero Narrative'
     A            DDDEFZ         3A       55
      *                                                           /*214360*/
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Hierarchy Links    ***
      *-------------------------*
      *
     A          R CMHL
      *
     A            RDLINK       120         6
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Continuation Line  ***
      *-------------------------*
      *
     A          R CMHDCONT
     A                                     2'Hierarchy:'
     A            CDHISN        10        14
     A                                    26'Hierarchy Type'
     A            CDHTYP         2A       42
     A                                    47'Hierarchy ID'
     A            CDHIER         9S 0     61
     A                                      SPACEA(1)
      *
     A                                     2'Description'
     A            CDDESC        50A       14
     A                                      SPACEA(1)
      *
     A                                    20'Continued.........'
     A                                      SPACEA(2)
      *
      *--------------------------*
      ***  Group Account Links ***
      *--------------------------*
      *
     A          R GAHL
      *
     A                                    52'Balance Available for -
     A                                      Intercompany Overdraft'
     A            DDBAIC         3A       97
     A                                      SPACEA(1)
      *
     A                                    52 'Intercompany Threshold Balance'
     A            DDICTB        22A       97
     A                                      SPACEA(1)
      *
     A                                    52'Transaction Account'
     A**Start of change CGL029
     A**********  DDTACC        22A       72                                                  CGL029
     A**********  DDTRAN        10A       95                                                  CGL029
     A            DDTACC        28A       72
     A            DDTRAN        10A      101
     A**End of change CGL029
     A                                      SPACEA(1)
      *
     A                                    52'Income Account'
     A**Start of change CGL029
     A**********  DDIACC        22A       72                                                  CGL029
     A**********  DDIRAN        10A       95                                                  CGL029
     A            DDIACC        28A       72
     A            DDIRAN        10A      101
     A**End of change CGL029
     A                                      SPACEA(1)
      *
     A                                    52'Expense Account'
     A**Start of change CGL029
     A**********  DDEACC        22A       72                                                  CGL029
     A**********  DDERAN        10A       95                                                  CGL029
     A            DDEACC        28A       72
     A            DDERAN        10A      101
     A**End of change CGL029
     A                                      SPACEA(1)
      *
      *--------------------------*
      ***  Zero Balancing Link ***
      *--------------------------*
      *
     A          R ZSHLZ
      *
     A                                    52'Child Narrative'
     A            ZLCNAR        30A       70
     A                                      SPACEA(1)
      *
     A                                    52'Parent Narrative'
     A            ZLPNAR        30A       70
     A                                      SPACEA(1)
      *
     A                                    52'Sweep Type'
     A            DDSTYP        12A       70
     A                                      SPACEA(1)
      *
     A                                    52'Sweep Capitalized Interest'
     A            DDSCPI         3A       79
     A                                      SPACEA(1)
      *
     A                                    52'Reverse Sweep'
     A            DDRVSW         3A       66
     A                                      SPACEA(1)
      *
     A                                    52'Zero Balance - Net or Gross'
     A            DDZBNG         5A       80
     A                                      SPACEA(1)
      *
     A                                    52'Sweep Cleared Balance on -
     A                                      Association Date'
     A            DDSCBD         3A       94
     A                                      SPACEA(1)
      *
     A                                    52'Target Balance'
     A            DDTGBL        22A       67
     A                                      SPACEA(1)
      *
     A                                    52'Shadow Account'
     A**Start of change CGL029
     A**********  DDSACC        22A       72                                                  CGL029
     A**********  DDSRAN        10A       95                                                  CGL029
     A            DDSACC        28A       72
     A            DDSRAN        10A      101
     A**End of change CGL029
     A                                      SPACEA(1)
      *
     A                                    52'Income Account'
     A**Start of change CGL029
     A**********  DDINAC        22A       72                                                  CGL029
     A**********  DDINRN        10A       95                                                  CGL029
     A            DDINAC        28A       72
     A            DDINRN        10A      101
     A**End of change CGL029
     A                                      SPACEA(1)
      *
     A                                    52'Expense Account'
     A**Start of change CGL029
     A**********  DDEXAC        22A       72                                                  CGL029
     A**********  DDEXRN        10A       95                                                  CGL029
     A            DDEXAC        28A       72
     A            DDEXRN        10A      101
     A**End of change CGL029
     A                                      SPACEA(1)
      *
     A                                    52'Floor Limit for Top'
     A            DDFLFT        22A       81
     A                                      SPACEA(1)
      *
     A                                    52'Floor Limit for Sweep'
     A            DDFLFS        22A       81
     A                                      SPACEA(1)
      *
     A                                    52'Maximum Debit Value in a Day'
     A            DDMDVD        22A       81
     A                                      SPACEA(1)
      *
     A                                    52'Maximum Debit Count in a Day'
     A            DDMDCD         5A       98
     A                                      SPACEA(1)
      *
      *--------------------*
      ***  Sweeping Link ***
      *--------------------*
      *
     A          R ZSHLS
      *
     A                                    52'Child Narrative'
     A            ZLCNAR        30A       70
     A                                      SPACEA(1)
      *
     A                                    52'Parent Narrative'
     A            ZLPNAR        30A       70
     A                                      SPACEA(1)
      *
     A                                    52'Sweep Type'
     A            DDSTYP        12A       70
     A                                      SPACEA(1)
      *
     A                                    52'Sweeping Frequency'
     A            DDSFRQ        30A       71
     A                                      SPACEA(1)
      *
     A                                    52'Sweeping Day'
     A            DDSDAY         2A       71
     A                                      SPACEA(1)
      *
     A                                    52'Next Sweep Date'
     A            DDNSDT         7A       71
     A                                      SPACEA(1)
      *
     A                                    52'Last Sweep Date'
     A            DDLSDT         7A       71
     A                                      SPACEA(1)
      *
     A                                    52'Sweep Capitalized Interest'
     A            DDSCPI         3A       79
     A                                      SPACEA(1)
      *
     A                                    52'Reverse Sweep'
     A            DDRVSW         3A       66
     A                                      SPACEA(1)
      *
     A                                    52'MAXIMUM Balance'
     A            DDMAXB        22A       68
     A                                    91'Level'
     A            DDMAXL        22A       97
     A                                      SPACEA(1)
      *
     A                                    52'MINIMUM Balance'
     A            DDMINB        22A       68
     A                                    91'Level'
     A            DDMINL        22A       97
     A                                      SPACEA(1)
      *
     A                                    52'Floor Limit for Top'
     A            DDFLFT        22A       74
     A                                      SPACEA(1)
      *
     A                                    52'Floor Limit for Sweep'
     A            DDFLFS        22A       74
     A                                      SPACEA(1)
      *
     A                                    52'Maximum Debit Value in a Day'
     A            DDMDVD        22A       81
     A                                      SPACEA(1)
      *
     A                                    52'Maximum Debit Count in a Day'
     A            DDMDCD         5A       98
     A                                      SPACEA(1)
      *
      *
      *---------------------*
      ***  Report Trailer ***
      *---------------------*
      *
     A          R TRAILP1
     A  37                                47'***  END OF REPORT FOR +
     A                                      BRANCH -'
     A                                      SPACEB(3)
     A  37        CDBRCA    R             79REFFLD(BRCA)
     A  37                                84'***'
      *
     A N37                                55'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
