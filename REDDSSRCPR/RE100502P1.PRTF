      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Zero-Balancing Stops List')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE100502P1 - Zero-Balancing Stops List                  *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. 212367             Date 10Dec02               *
      *                 CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  212367 - Various reporting errors                            *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R PAGEHEAD                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE100502P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    51'ZERO-BALANCING STOPS LIST'
     A                                      SPACEA(1)
     A                                    51'-------------------------'
     A                                      SPACEA(2)
      *
     A          R DETAIL                    SPACEB(1)
     A                                     4'Hierarchy:'
     A            RHISN         10A       20
     A            RDESC         50A       31
     A                                      SPACEA(1)
     A                                     4'Child Account:'
     A**Start of change CGL029
     A**********  RCACNT        22A       20                                                  CGL029
     A**********  RCRANO        10A       45                                                  CGL029
     A**********                          58'Link ID:'                                        CGL029
     A**********  RLINK          9A       67                                                  CGL029
     A            RCACNT        28A       20
     A            RCRANO        10A       50
     A                                    62'Link ID:'
     A            RLINK          9A       73
     A**End of change CGL029
     A                                      SPACEA(1)
     A                                     4'Parent Account:'
     A**Start of change CGL029
     A**********  RPACNT        22A       20                                                  CGL029
     A**********  RPRANO        10A       45                                                  CGL029
     A            RPACNT        28A       20
     A            RPRANO        10A       50
     A**End of change CGL029
     A                                      SPACEA(1)
     A                                     4'Processing Date:'
     A            RPRCD          7A       21
     A                                    32'Stop Number:'
     A            RSTPN          2A       44
     A                                    50'Released:'
     A            RRELD          3A       60
     A                                    65'Deleted:'
     A            RDELD          3A       74
     A                                      SPACEA(1)
     A                                     4'Maximum DR Value in a Day'
     A            RMDVD         21A       31
     A                                    55'Maximum DR Count in a Day'
     A            RMDCD          3S 0     81EDTCDE(1)
     A                                      SPACEA(1)
     A                                     4'Total DR Value on'
     A            RPRCD1         7A       22
     A            RTVDR         21A       31
     A                                    55'Total DR Count on'
     A            RPRCD2         7A       73
     A            RTCDR          3S 0     81EDTCDE(1)
     A                                      SPACEA(1)
      *
      *------------------*
      *** ITEM HEADER  ***
      *------------------*
      *
     A          R ITEMHDR
     A                                     7'STOPPED ENTRIES'
     A                                      SPACEB(1)
     A                                     7'Value Date'
     A                                      SPACEB(1)
     A                                    25'Narrative'
     A                                    67'Amount'
     A                                    85'Account'
     A                                      SPACEA(1)
     A                                     7'-----------------------------------
     A                                      ------------------------------------
     A                                      -------------------------------'
     A                                      SPACEA(1)
      *
      *----------*
      *** ITEM ***
      *----------*
      *
     A          R ITEM
     A            RVALD          7A        7
     A            RNARR         30A       25
     A            RAMNT         21A       57
     A            RDRCR          2A       79
     A**Start of change CGL029
     A**********  RACCNT        22A       85                                                  CGL029
     A            RACCNT        28A       85
     A**End of change CGL029
     A*                                                      /*212367*/
     A**Start of change CGL029
     A**********  RRACNO        10A      108                                                  CGL029
     A            RRACNO        10A      114
     A**End of change CGL029
     A*                                                      /*212367*/
     A                                      SPACEA(1)
      *
      *---------------------------------*
      *** ORIGINATING POSTING HEADER  ***
      *---------------------------------*
      *
     A          R OPSTHDR
     A                                     7'ORIGINATING ENTRIES'
     A                                      SPACEB(1)
     A                                     7'Value Date'
     A                                      SPACEB(1)
     A                                    25'Narrative'
     A                                    67'Amount'
     A                                    85'Account'
     A                                      SPACEA(1)
     A                                     7'-----------------------------------
     A                                      ------------------------------------
     A                                      -------------------------------'
     A                                      SPACEA(1)
      *
      *-------------------------*
      *** ORIGINATING POSTING ***
      *-------------------------*
      *
     A          R OPST
     A            RVALD          7A        7
     A            RNARR         30A       25
     A            RAMNT         21A       57
     A            RDRCR          2A       79
     A**Start of change CGL029
     A**********  RACCNT        22A       85                                                  CGL029
     A            RACCNT        28A       85
     A**End of change CGL029
     A*                                                      /*212367*/
     A**Start of change CGL029
     A**********  RRACNO        10A      108                                                  CGL029
     A            RRACNO        10A      114
     A**End of change CGL029
     A*                                                      /*212367*/
     A                                      SPACEA(1)
      *
     A          R SEPLINE                   SPACEB(1)
     A                                     5'==================================+
     A                                      ===================================+
     A                                      ===================================+
     A                                      =================='
     A                                      SPACEA(1)
      *
      *------------------*
      *** Report Trailer *
      *------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
