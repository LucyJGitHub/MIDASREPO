      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Rev Sweeping/Target Balancing List')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE100503P1 - Reverse Sweeping/Target Balancing List     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. 212367             Date 10Dec02               *
      *                 CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  212367 - Various reporting errors                            *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE100503P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    47'REVERSE SWEEPING/TARGET +
     A                                      BALANCING LIST'
     A                                      SPACEA(1)
     A                                    47'------------------------+
     A                                      --------------'
     A                                      SPACEA(2)
      *
     A          R HEADP2                    SPACEB(2)
     A                                     4'Hierarchy:'
     A            RHISN         10A       21
     A            RDESC         50A       33
     A                                      SPACEA(1)
     A                                     4'Child Account:'
     A**Start of change CGL029
     A**********  RCACNT        22A       21                                                  CGL029
     A**********  RCRANO        10A       46                                                  CGL029
     A**********                          58'Link ID:'                                        CGL029
     A**********  RLINK          9A       67                                                  CGL029
     A            RCACNT        28A       21
     A            RCRANO        10A       52
     A                                    64'Link ID:'
     A            RLINK          9A       73
     A**End of change CGL029
     A                                      SPACEA(1)
     A                                     4'Parent Account:'
     A**Start of change CGL029
     A**********  RPACNT        22A       21                                                  CGL029
     A**********  RPRANO        10A       46                                                  CGL029
     A            RPACNT        28A       21
     A            RPRANO        10A       52
     A**End of change CGL029
     A                                      SPACEA(1)
     A                                     4'Processing Date:'
     A            RPDATE         7A       21
     A                                    35'Sequence Number:'
     A            RSEQNM         2A       53
     A                                      SPACEA(2)
      *
      *--------------*
      *** Details  ***
      *--------------*
      *
     A          R SUBHDR
     A                                     7'Value Date'
     A                                    25'Narrative'
     A                                    57'Amount'
     A                                    80'DR/CR'
     A                                    90'Account'
     A                                      SPACEA(1)
     A                                     7'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------------------------------'
     A                                      SPACEA(002)
     A          R DETAIL1
     A            RVALD          7A        7
     A            RNARR         30A       25
     A            RAMNT         21A       57
     A            RDRCR          2A       81
     A*                                                      /*212367*/
     A************RACCNT        18A       90
     A**Start of change CGL029
     A**********  RACCNT        22A       90                                                  CGL029
     A**********  RRACNO        10A      113                                                  CGL029
     A            RACCNT        28A       90
     A            RRACNO        10A      119
     A**End of change CGL029
     A*                                                      /*212367*/
     A                                      SPACEA(1)
      *
     A          R SEPLINE                   SPACEB(1)
     A                                     5'==================================+
     A                                      ===================================+
     A                                      ===================================+
     A                                      =================='
     A**                                    SPACEA(1)
      *
      *------------------*
      *** Report Trailer *
      *------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
