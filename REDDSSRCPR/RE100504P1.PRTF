      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE I/C Zero Balancing/Sweeping List')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE100504P1 - Input Cycle Zero Balancing/Sweeping List   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE100504P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    47'INPUT CYCLE ZERO BALANCI+
     A                                      NG/SWEEPING LIST'
     A                                      SPACEA(1)
     A                                    47'------------------------+
     A                                      ----------------'
     A                                      SPACEA(2)
      *
     A          R HEADP2                    SPACEB(1)
     A                                     4'Hierarchy:'
     A            RHISN         10A       21
     A            RDESC         50A       32
     A                                      SPACEA(1)
     A                                     4'Child Account:'
     A**Start of change CGL029
     A**********  RCACNT        22A       21                                                  CGL029
     A**********  RCRANO        10A       46                                                  CGL029
     A**********                          59'Link ID:'                                        CGL029
     A**********  RLINK          9A       68                                                  CGL029
     A            RCACNT        28A       21
     A            RCRANO        10A       51
     A                                    63'Link ID:'
     A            RLINK          9A       74
     A**End of change CGL029
     A                                      SPACEA(1)
     A                                     4'Parent Account:'
     A**Start of change CGL029
     A**********  RPACNT        22A       21                                                  CGL029
     A**********  RPRANO        10A       46                                                  CGL029
     A            RPACNT        28A       21
     A            RPRANO        10A       51
     A**End of change CGL029
     A                                      SPACEA(1)
     A                                     4'Processing Date:'
     A            RPDATE         7A       21
     A                                    35'Sequence No.:'
     A            RSEQN          2A       53
     A                                    59'Deleted:'
     A            RDELD          3A       68
     A                                      SPACEA(1)
     A                                     4'Sweep Type:'
     A            RSTYP         14A       17
     A                                    35'Value Date:'
     A            RVALD          7A       48
     A                                      SPACEA(2)
      *
     A          R HEADP3
     A                                     4'Destination:'
     A            RDEST         18A       17
     A                                    40'Type:'
     A            RDSTT          1A       46
     A                                      SPACEA(1)
     A                                     4'Senders Correspondent:'
     A            RSNDC          2A       30
     A                                    40'MT202 Cover Message Required:'
     A            RMCMR          3A       70
     A                                      SPACEA(1)
     A                                     4'Receivers Corespondent:'
     A            RRCVC         11A       30
     A                                      SPACEA(1)
     A                                     4'Swift Amount:'
     A            RSWFA         22A       30
     A            RTPSW          5A       55
     A                                      SPACEA(1)
     A                                     4'Message Produced:'
     A            RMSGP          3A       23
     A                                    30'Message Time:'
     A            RMSGT          8A       44
     A                                    59'Matched:'
     A            RMTYN          3A       68
     A                                      SPACEA(2)
      *
      *--------------*
      *** Details  ***
      *--------------*
      *
     A          R SUBHDR
     A                                     7'Value Date'
     A                                    25'Narrative'
     A                                    57'Amount'
     A                                    80'DR/CR'
     A                                    90'Account'
     A                                      SPACEA(1)
     A                                     7'-----------------------------------
     A                                      ------------------------------------
     A                                      -----------------------------------'
     A                                      SPACEA(002)
     A          R DETAIL1
     A            RVALD          7A        7
     A            RNARR         30A       25
     A            RAMNT         21A       57
     A            RDRCR          2A       81
     A**Start of change CGL029
     A**********  RACCNT        18A       90                                                  CGL029
     A            RACCNT        24A       90
     A**End of change CGL029
     A                                      SPACEA(1)
      *
     A          R SEPLINE                   SPACEB(1)
     A                                     5'==================================+
     A                                      ===================================+
     A                                      ===================================+
     A                                      =================='
     A                                      SPACEA(1)
      *
      *------------------*
      *** Report Trailer *
      *------------------*
      *
     A          R TRAILP1
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
