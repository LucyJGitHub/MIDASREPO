      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Group Account Hierarchy Imbalance Exc Rpt')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE100509P1 - Group Account Hierarchy Imbalance          *
      *                    Exception Report                           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *-----------------------------------*
      ***  Report Header Record Format  ***
      *-----------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE100509P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    39'GROUP ACCOUNT HIERARCHY +
     A                                      IMBALANCE EXCEPTION REPORT'
      *
     A  37                                90'-'
     A  37        CDBRCA    R             92REFFLD(BRCA)
     A                                   132' '
     A                                      SPACEA(1)
     A                                    39'------------------------+
     A                                      --------------------------'
     A                                      SPACEA(3)
      *
      *-------------------------*
      ***  Hierarchy Details  ***
      *-------------------------*
      *
     A          R CMHD
      *
     A                                     2'Hierarchy'
     A            CDHISN        10A       14
     A                                    26'Hierarchy Type'
     A            CDHTYP         2A       42
     A                                    47'Hierarchy ID'
     A            CDHIER         9S 0     61
     A                                      SPACEA(1)
      *
     A                                     2'Description'
     A            CDDESC        50A       14
     A                                      SPACEA(1)
      *
     A                                     2'Main Branch'
     A            CDBRCA         3A       16
     A                                    27'Date Effective From'
     A            DDDEFR         7A       47
     A                                    57'Review Date'
     A            DDRVDT         7A       74
     A                                      SPACEA(1)
      *
     A                                     2'Input By User'
     A            CDINBU        10A       16
     A                                    27'Authorized By User'
     A            CDAUBU        10A       46
     A                                    57'Last Change Date'
     A            DDLCDT         7A       74
     A                                      SPACEA(1)
      *
      *-----------------------------
      ***  Group Account Details  **
      *-----------------------------
      *
     A          R GAHD
     A                                     2'Currency'
     A            GDCCY          3A       13
     A                                      SPACEA(1)
      *
     A                                     2'Top on Balance Sheet'
     A            DDTOPB         3A       23
     A                                      SPACEA(1)
      *
     A                                     2'Link Effective At Last Capitalizat-
     A                                      ion Date'
     A            DDLELC         3A       47
     A                                      SPACEA(1)
      *
     A                                     2'Unlink Effective At Last Capitaliz-
     A                                      ation Date'
     A            DDUELC         3A       47
     A                                      SPACEA(2)
      *
     A                                    97'DIFFERENCE'
     A                                      SPACEA(1)
     A                                    10'Accounts'
     A                                    54'Customer'
     A                                    83'Ledger Balance'
     A                                   107'Cleared Balance'
     A                                      SPACEA(1)
     A                                    10'--------'
     A                                    54'--------'
     A                                    83'--------------'
     A                                   107'---------------'
     A                                      SPACEA(2)
      *
      *
      *-------------------------*
      ***  Imbalance Details  ***
      *-------------------------*
      *
     A          R IDTL
      *
     A            CLCCAT         1A        8
      *
     A            RDACCS       125A       10
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Continuation Line  ***
      *-------------------------*
      *
     A          R CMHDCONT
     A                                     2'Hierarchy:'
     A            CDHISN        10        14
     A                                    26'Hierarchy Type'
     A            CDHTYP         2A       42
     A                                    47'Hierarchy ID'
     A            CDHIER         9S 0     61
     A                                      SPACEA(1)
      *
     A                                     2'Description'
     A            CDDESC        50A       14
     A                                      SPACEA(1)
      *
     A                                    20'Continued.........'
     A                                      SPACEA(2)
      *
      *
      *---------------------*
      ***  Report Trailer ***
      *---------------------*
      *
     A          R TRAILP1
     A  37                                47'***  END OF REPORT FOR +
     A                                      BRANCH -'
     A                                      SPACEB(3)
     A  37        CDBRCA    R             79REFFLD(BRCA)
     A  37                                84'***'
      *
     A N37                                55'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
