      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RE Zero Balancing Stops Entry merge')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE100701AU - Zero-Balancing Stops Entry merge           *
      *                    Audit                                      *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CRE008  *CREATE    Date 05Mar02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE100701AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    47'ZERO BALANCING STOPS +
     A                                       ENTRY MERGE - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    47'------------------------+
     A                                      ----------------'
     A                                      SPACEA(3)
      *-----------*
      *** Details *
      *-----------*
      *
     A          R DETAILAU
     A                                      SPACEB(3)
      *
     A                                    47'NUMBER OF RELEASED STOPS READ:'
     A            SRCOUNT        9S 0     79
     A                                      EDTCDE(1)
     A                                      SPACEA(1)
     A                                    47'NUMBER OF DELETED STOPS READ:'
     A            SDCOUNT        9S 0     79
     A                                      EDTCDE(1)
     A                                      SPACEA(1)
     A                                    47'NUMBER OF POSTINGS EXTRACTED:'
     A            PCOUNT         9S 0     79
     A                                      EDTCDE(1)
      *
     A                                    54'* END OF REPORT *'
     A                                       SPACEB(2)
      *
