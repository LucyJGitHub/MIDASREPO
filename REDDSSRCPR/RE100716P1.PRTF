      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Cash Management Generated Entries Report')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE100716P1 - Cash Management Generated Entries Report   *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      *                 CGL029             Date 01Sep03               *
      *                 CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R PAGEHEAD                  SKIPB(1)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
     A                                     1 'REF.RE100716'
     A            BJURPT        53        37
     A                                   101 'DATE'
     A            BJMRDT         7       106
     A                                   119 'PAGE'
     A            PAGNUM         3  0    125 EDTCDE(Z) SPACEA(1)
     A                                    45'CASH MANAGEMENT GENERATED ENTRIES -
                                            REPORT'
     A                                    89'BRANCH'
     A            A8BRCD         3A       98
     A            A8BRNM        30A      102
     A                                       SPACEA(1)
     A                                    45'-----------------------------------
                                            ------'
     A                                    89'CURRENCY'
     A            A6CYCD         3A       98
     A            A6CYNM        14A      102
     A                                       SPACEA(1)
     A                                     1'<------ Account ---------->'
     A                                    46'Posting Date'
     A                                    59'Value Date'
     A                                    73'Posting Amount'
     A                                    91'Narrative'
     A                                   124'Reference'
      *
     A          R SPACE2                     SPACEB(1)
     A                                     1 '----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------- '
     A**Start of change CGL029
     A**********  A5ACCD         4A        1 SPACEB(1)                                        CGL029
     A**********  A5ACCN        25A        6                                                  CGL029
     A            A5ACCD        10A        1 SPACEB(1)
     A            A5ACCN        25A       12
     A**End of change CGL029
      *
     A          R ENTRY                      SPACEB(1)
     A**Start of change CGL029
     A**********  ACOD           4S 0      1                                                  CGL029
     A**********  CNUM           6S 0      6                                                  CGL029
     A**********  ACSQ           2S 0     13                                                  CGL029
     A**36******                          16 '-'                                              CGL029
     A**36******  ACNO          10S 0     18                                                  CGL029
     A            ACOD          10S 0      1
      * Begin /CSD027/
     A**********  CNUM           6S 0     12                                                  CSD027
     A            CNUM           6A       12
      * End /CSD027/
     A            ACSQ           2S 0     19
     A  36                                22 '-'
     A  36        ACNO          10S 0     24
     A**End of change CGL029
     A            R_PSTD         7A       49
     A            R_VALD         7A       60
     A            R_PSTA        15A       72
     A            R_DRCR         2A       88
     A            PNAR          30A       91
     A            R_XRFI         3A      122
     A            R_XRFN         7A      126
      *
     A          R CCYIMBAL                   SPACEB(1)
     A                                     1 '----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      ---------------------------- '
     A                                    46'* CURRENCY IMBALANCE *'
     A                                      SPACEB(1)
     A            R_POSTTOT     17A       70
     A            R_TDRCR        2A       88
      *
     A          R ENDOFREP                   SPACEB(1)
     A                                    54'* END OF REPORT *'
     A                                       SPACEB(2)
