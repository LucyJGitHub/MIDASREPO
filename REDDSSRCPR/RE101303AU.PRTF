      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RE IC Zero Bal/Sweep Msg Extract-Audit')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE101303AU - Input Cycle Zero Balancing/Sweeping Msg    *
      *                    Generation - Audit                         *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE101303AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    34'INPUT CYCLE ZERO +
     A                                       BALANCING/SWEEPING +
     A                                       MESSAGE GENERATION - AUDIT'
     A                                      SPACEA(1)
      *
     A                                    34'------------------------+
     A                                      -------------------------+
     A                                      --------------'
     A                                      SPACEA(3)
      *---------------*
      *** Run Details *
      *---------------*
      *
     A          R HASRUN
     A                                    34'INPUT CYCLE ZERO +
     A                                       BALANCING/SWEEPING +
     A                                       MESSAGE GENERATION COMPLETE'
     A                                      SPACEA(3)
     A                                    42'MESSAGES GENERATED :'
     A                                      SPACEA(1)
     A                                    50'MT101       -'
     A            COUNT101       5S 0     64
     A                                      EDTCDE(1)
     A                                      SPACEA(1)
     A                                    50'MT103       -'
     A            COUNT103       5S 0     64
     A                                      EDTCDE(1)
     A                                      SPACEA(1)
     A                                    50'COVER MT202 -'
     A            COUNT202       5S 0     64
     A                                      EDTCDE(1)
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
