      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE MT101/MT103 Generation by Sweep Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE102007P1 - MT101/MT103 Generation by Sweep Report     *
      *                    (Detail)                                   *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2002            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      *  Prev Amend No. CRE008  *Create    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE102007P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    47'MT101/MT103 GENERATION +
     A                                      BY SWEEP'
     A                                      SPACEA(1)
      *
     A                                    47'-----------------------+
     A                                      --------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A  04                                 6'Branch:'
     A  04        RBRCA          3        15
     A  04                                  SPACEA(1)
     A  05                                 6'Creation Date Range'
     A  05                                26'from'
     A  05        FMDAT          6        32
     A  05                                39'to'
     A  05        TODAT          6        43
     A                                      SPACEA(2)
      *
     A                                     5'T.R.N.'
     A                                    23'MSG TYPE'
     A                                    36'DESTINATION'
     A                                    54'GENERATION DATE/TIME'
     A                                    78'MSG STATUS'
     A                                      SPACEA(1)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            RTRNO         16         5
     A            RMTYP          5        24
     A            RDEST         20        32
     A            RGEND         16        56
     A            RMGST         12        78
     A                                      SPACEA(1)
      *
     A                                     5'TOP A/C    :'
     A**Start of change CGL029
     A**********  RACNT         22        18                                                  CGL029
     A**********                          43'AMOUNT:'                                         CGL029
     A**********  RAMTA         20        51                                                  CGL029
     A**********                          75'VALUE DATE:'                                     CGL029
     A**********  RVDAT          7        89                                                  CGL029
     A            RACNT         28        18
     A                                    48'AMOUNT:'
     A            RAMTA         20        56
     A                                    78'VALUE DATE:'
     A            RVDAT          7        91
     A**End of change CGL029
     A                                      SPACEA(1)
     A                                     5'SETTLE A/C.:'
     A            RSETA         10        18
     A                                    29'EXTERNAL A/C:'
     A            REXAC         35        43
     A                                      SPACEA(1)
      *
      *-----------------------------*
      ***  Report Trailer - BRANCH  *
      *-----------------------------*
      *
     A          R TRAILP1
     A                                    44'***  END OF REPORT FOR BRANCH'
     A                                      SPACEB(3)
     A            RBRCA          3        74
     A                                    79'***'
      *
      *--------------------*
      ***  Report Trailer  *
      *--------------------*
      *
     A          R TRAILP2
     A                                    51'***  END OF REPORT ***'
     A                                      SPACEB(3)
      *
