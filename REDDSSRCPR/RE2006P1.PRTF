      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Outward Clearing Trans.Types Report')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE2006P1 - Outward Clearing Transaction Types Report         *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CRE023  *CREATE    Date 29Jul05               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  CRE023 - 2 O'clock Flag Upgrade to MidasPlus                 *
      *                                                               *
      *****************************************************************
     A                                      INDARA
      *================================================================
     A          R P1HDR1
     A                                      TEXT('Standard report header   ')
     A                                     2
     A                                      'Reference'
     A                                      SKIPB(001)
     A                                      SPACEB(002)
     A            ##PGM         10   O    12
     A                                      TEXT('*PROGRAM')
     A            ##URPT        53   O    45
     A                                      TEXT('User Report Title')
     A                                   111
     A                                      'Date'
     A            ##MRDT         7   O   116
     A                                      TEXT('midas rundate')
     A                                   124
     A                                      'Page'
     A                                   129
     A                                      PAGNBR
     A                                      EDTCDE(3)
     A                                   111
     A                                      'Time'
     A                                      SPACEB(001)
     A                                   116
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    45
     A                                      SPACEB(001)
     A                                      'Outward Clearing Transaction Types-
     A                                       Listing -'
     A            REPTYP         5A  O    +1
     A                                    45
     A                                      SPACEB(001)
     A                                      '-----------------------------------
     A                                      ----------------'
      *================================================================*********
     A          R P1CHD1
     A                                      SPACEB(001)
     A                                     2
     A                                      'Transaction'
     A                                    17
     A                                      'Transaction'
     A                                   +22
     A                                      'O/ward'
     A                                    62
     A                                      'Reverse (Today''s)'
     A                                    83
     A                                      'Reverse (Previous)'
     A                                   105
     A                                      'Last'
     A                                   120
     A                                      'Last'
     A                                     2
     A                                      SPACEB(001)
     A                                      'Type'
     A                                    17
     A                                      'Name'
     A                                   +29
     A                                      'Clearing'
     A                                    62
     A                                      'O/ward Clearing'
     A                                    83
     A                                      'O/ward Clearing'
     A                                   105
     A                                      'Change type'
     A                                   120
     A                                      'Change Date'
     A                                     2
     A                                      SPACEB(001)
     A                                      '-----------'
     A                                    17
     A                                      '------------------------------'
     A                                    +3
     A                                      '--------'
     A                                    62
     A                                      '-----------------'
     A                                    83
     A                                      '------------------'
     A                                   105
     A                                      '-----------'
     A                                   120
     A                                      '-----------'
      *================================================================*********
     A          R P1DET1
     A                                      SPACEB(001)
     A            PTTYP          5A  O     2
     A            PTNAM         30A  O    17
     A            POCLR          1A  O    +6
     A            PRCLT          1A  O    70
     A            PRCLP          1A  O    91
     A            PLTYP          6A  O   107
     A            PLCD           6S 0O   121
     A                                      EDTCDE(Y)
      *================================================================
     A          R P1FTR1
     A                                      SPACEB(001)
     A                                    50
     A                                      '**** End of Report ****'
     A                                      SPACEB(001)
      *
