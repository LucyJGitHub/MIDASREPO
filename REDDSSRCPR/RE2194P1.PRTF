      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas BRT Interest History Payment List')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Retail Module                               *
     A*                                                                  *
     A*      PRTF/RE2194P1 - BRT INTEREST PAYMENT HISTORY LIST           *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 072064             Date 08Sep94               *
      *                 S01383             Date 27Apr92               *
     A*                                                                  *
     A*------------------------------------------------------------------*
     A*                                                                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*       072064 - Date is incorrect                                 *
     A*       S01383 - BASIC RATE TAX INCORPORATION                      *
     A*                NEW PROGRAM                                       *
     A*                                                                  *
     A********************************************************************
      *
     A                                      REF(MFRFL)
      *
      ** REPORT HEADING
      *
     A          R HEADER
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE2194'
     A            @TITL         53        37
     A                                   105'DATE'
      *                                            Start   /072064/
     A************@DATE          6  0    111EDTWRD('  /  /  ')
     A            @DATE          7       111
      *                                            End     /072064/
     A                                   121'PAGE'
     A            PAGE      R            126
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                     5'Branch'
     A            @BRAN          3        13
      *
     A                                    45'INTEREST HISTORY PAYMENT LIST'
     A                                      UNDERLINE
     A                                      SPACEA(1)
      *
      ** REPORT DETAILS
      *
     A          R DETAIL                    SPACEB(3)
      *
     A                                     5'Customer'
     A                                    15'Name'
     A                                    38'Deal No.'
     A                                    48'Account'
     A**Start of change CGL029
     A**********                          63'BRT Cat.'                                        CGL029
     A**********                          74'S352 produced ?'                                 CGL029
     A                                    69'BRT Cat.'
     A                                    80'S352 produced ?'
     A**End of change CGL029
     A                                      SPACEA(1)
     A            @CNUM          6         5
     A            @CRNM         20        15
     A            @DLNO          6        38
     A            @CCY           3        48
     A**Start of change CGL029
     A**********  @ACOD          4        52                                                  CGL029
     A**********  @ACSQ          2        57                                                  CGL029
     A**********  @BRTCA         1        66                                                  CGL029
     A**********  @S352R         1        81                                                  CGL029
     A            @ACOD         10        52
     A            @ACSQ          2        63
     A            @BRTCA         1        72
     A            @S352R         1        87
     A**End of change CGL029
     A                                       SPACEA(2)
     A                                    15'Gross Interest :'
     A            @STGGR        13  0     32EDTWRD('  ,   ,   ,  $0.  ')
     A                                       SPACEA(1)
     A                                    15'BRT deducted   :'
     A            @STGBR        13  0     32EDTWRD('  ,   ,   ,  $0.  ')
     A                                       SPACEA(1)
     A                                    15'Net Int. paid  :'
     A            @STGNT        13  0     32EDTWRD('  ,   ,   ,  $0.  ')
     A                                    57'Int. Pay. Date'
     A            @PAYD          6  0     80EDTWRD('  /  /  ')
      *
      ** END OF REPORT FORMAT
      *
     A          R ENDFMT                    SPACEB(4)
     A                                    51'*** END OF REPORT ***'
      *
      ** NO DETAILS FORMAT
      *
     A          R NODET                     SPACEB(4)
     A                                    48'*** NO DETAILS TO REPORT ***'
      *
      ** DATABASE ERROR MESSAGE
      *
     A          R DBERR                     TEXT('DATABASE ERROR MESSAGE')
      *
     A                                      SPACEB(3)
     A                                    39'******************************'
     A                                    69'****************'
     A                                      SPACEA(1)
     A                                    39'*'
     A                                    85'*'
     A                                      SPACEA(1)
     A                                    39'* DATABASE ERROR IN :'
     A            DBPGM         10        60
     A                                    85'*'
     A                                      SPACEA(1)
     A                                    39'* AT :'
     A            DBASE          3  0     60
     A                                    85'*'
     A                                      SPACEA(1)
     A                                    39'* FILE :'
     A            DBFILE         8        60
     A                                    85'*'
     A                                      SPACEA(1)
     A                                    39'* KEY  :'
     A            DBKEY         29        47
     A                                    85'*'
     A                                      SPACEA(1)
     A                                    39'******************************'
     A                                    69'****************'
