      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas S352 Statement')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Retail Module                               *
     A*                                                                  *
     A*      PRTF/RE2195P1 - S352 STATEMENT                              *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01383             Date 27Apr92               *
     A*                                                                  *
     A*------------------------------------------------------------------*
     A*                                                                  *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*       S01383 - BASIC RATE TAX INCORPORATION                      *
     A*                NEW PROGRAM                                       *
     A*                                                                  *
     A********************************************************************
      *
     A                                      REF(MFRFL)
     A          R RE2195F1                   SKIPB(10)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      * Bank name and address
     A            BCNA1         35        36
     A                                       SPACEA(1)
     A            BCNA2         35        36
     A                                       SPACEA(1)
     A            BCNA3         35        36
     A                                       SPACEA(1)
     A            BCNA4         35        36
     A                                       SPACEA(3)
      * Customer name and address
     A            CCNA1         35        10
     A                                       SPACEA(1)
     A            CCNA2         35        10
     A                                       SPACEA(1)
     A            CCNA3         35        10
     A                                       SPACEA(1)
     A            CCNA4         35        10
     A                                       SPACEA(5)
      * Title
     A                                    20'CERTIFICATE OF DEDUCTION '
     A                                    45'OF INCOME TAX'
     A                                       SPACEA(1)
     A                                    20'-------------------------'
     A                                    45'-------------'
     A                                       SPACEA(2)
      * Tax year
     A                                    10'YEAR TO 5 APRIL'
     A            TAXYR          4        26
     A                                       SPACEA(2)
      * Account or deal number
     A                                    10'ACCOUNT NUMBER :'
     A**Start of change CGL029
     A**********  PACNO         18        27                                                  CGL029
     A            PACNO         24        27
     A**End of change CGL029
     A                                       SPACEA(2)
      * Party names
     A                                    10'NAMES OF PARTIES TO '
     A                                    30'ACCOUNTS :'
     A                                       SPACEA(2)
     A  30        H1TITL         4        10
     A  30        H1NAME        23        15
     A  30        H1INIT         3        39
     A  31        H2TITL         4        44
     A  31        H2NAME        23        49
     A  31        H2INIT         3        73
     A                                       SPACEA(1)
     A  23 24                             10'LIABLE PARTY'
     A  23N24                             10'NON-LIABLE PARTY'
     A  23 25                             44'LIABLE PARTY'
     A  23N25                             44'NON-LIABLE PARTY'
     A                                       SPACEA(1)
     A  32        H3TITL         4        10 SPACEB(1)
     A  32        H3NAME        23        15
     A  32        H3INIT         3        39
     A  33        H4TITL         4        44
     A  33        H4NAME        23        49
     A  33        H4INIT         3        73
     A                                       SPACEA(2)
     A  34        H5TITL         4        10
     A  34        H5NAME        23        15
     A  34        H5INIT         3        39
     A                                       SPACEA(3)
      *
     A                                    10'I certify that on paying '
     A                                       SKIPB(40)
     A                                    35'to the above customer(s)'
     A                                       SPACEA(2)
      * Amounts
     A                                    10'Gross Interest of'
     A            GRISUM        13  0     54EDTWRD('  ,   ,   , $0 .  ')
     A                                       SPACEA(2)
     A                                    10'I deducted income tax of'
     A            BRTSUM        13  0     54EDTWRD('  ,   ,   , $0 .  ')
     A                                       SPACEA(2)
     A                                    10'The net payment to the '
     A                                    33'customer being'
     A            NETSUM        13  0     54EDTWRD('  ,   ,   , $0 .  ')
     A                                       SPACEA(4)
      *
     A                                    10'AUTHORISED SIGNATORY'
     A                                       SPACEA(3)
      * Date of issue
     A                                    10'Date of Issue :'
     A            ISSDAT         7        26
     A                                       SPACEA(2)
      *
     A                                     9'_________________________'
     A                                    33'_________________________'
     A                                    57'____________'
     A                                      SPACEA(1)
     A                                     8'¦ This certificate is '
     A                                    30'issued for the purposes '
     A                                    54'of Section 352 ¦'
     A                                      SPACEA(1)
     A                                     8'¦ ICTA 1988. PLEASE KEEP '
     A                                    33'IT AS IT WILL BE NEEDED '
     A                                    57'IF A CLAIM  ¦'
     A                                      SPACEA(1)
     A                                     8'¦ FOR REPAYMENT OF INCOME'
     A                                    33' TAX IS MADE.           '
     A                                    57'            ¦'
     A                                     8'¦________________________'
     A                                    33'________________________'
     A                                    57'____________¦'
      **
