      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE BRT S17 Report')
     A********************************************************************
     A*                                                                  *
     A*  Midas - Retail Module                               *
     A*                                                                  *
     A*      PRTF/RE2196P1 - BRT S17 REPORT                              *
     A*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                                  *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *  Last Amend No. CRE051             Date 14Jan09               *
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CRE021             Date 28Jan05               *  *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 CRE007             Date 01Mar01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01383             Date 27Apr92               *
     A*                                                                  *
     A*------------------------------------------------------------------*
     A*                                                                  *
     A*       CRE051 - BRT S17 Changes for 2009                          *
     A*       CRE021 - S17 Return changes.                               *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*       CRE007 - Base Rate Tax 2001.                               *
     A*       S01383 - BASIC RATE TAX INCORPORATION                      *
     A*                NEW PROGRAM                                       *
     A*                                                                  *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A*
     A** REPORT HEADING
     A*
     A          R RE2196HH
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                      SKIPB(3)
     A                                     3'REFERENCE RE2196'
     A            TITL      R             39
     A                                   100'DATE'
     A            RUNA      R            105
     A                                   119'PAGE'
     A            PAGE      R            124
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A                                   100'TIME'
     A                                   105TIME
     A                                      SPACEA(2)
     A*
     A  10                                 3'(INTERIM REPORT)'
     A*
     A                                    47'SECTION 17 REPORT - Y/E 5TH APRIL '
     A                                      UNDERLINE
     A            CCYY           4        81
     A                                      UNDERLINE
     A  19                                90'(INDIVIDUALS)'
     A  20                                90'(CORPORATES)'
     A  21                                90'(BANKS)'
     A*
     A**--------------------------------------------------------------------
     A** FORM TYPE 1
     A*
     A          R RE2196F1                  TEXT('FORM TYPE 1')
     A                                      SPACEB(3)
     A*
     A                                     4'FORM TYPE - 1'
     A                                      SPACEA(2)
     A*
     A                                     4'INLAND REVENUE REFERENCE :'
     A            P1IRRN         8        33
     A                                      SPACEA(2)
     A                                     4'INSTITUTION NAME         :'
     A            P1INST        50        33
     A                                      SPACEA(2)
     A                                     4'BRANCH NAME              :'
     A            P1BRNM        50        33
     A                                      SPACEA(2)
     A                                     4'SORT CODE                :'
     A            P1SORT         6        33
     A                                      SPACEA(2)
     A                                     4'TAX YEAR                 :'
     A            P1CCYY         4        33
     A                                      SPACEA(2)
     A                                     4'STERLING RETURN SIGNAL   :'
     A                                    33'Y'
     A                                      SPACEA(2)
     A                                     4'A/C TITLE LENGTH         :'
     A                                    33'35'
     A                                      SPACEA(2)
     A                                     4'A/C NAME LENGTH          :'
     A                                    33'0'
     A                                      SPACEA(2)
     A                                     4'A/C NAME COUNT           :'
     A                                    33'0'
     A                                      SPACEA(2)
     A                                     4'A/C ADDRESS LINE LENGTH  :'
     A                                    33'35'
     A                                      SPACEA(2)
     A                                     4'A/C ADDRESS LINE COUNT   :'
     A                                    33'3'
     A                                      SPACEA(2)
     A                                     4'PARTICIPATOR NAME LENGTH :'
     A** Start of Change CRE051
     A**********                          33'0'                                               CRE051
     A                                    33'35'
     A** End of Change CRE051
     A                                      SPACEA(2)
     A                                     4'PARTICIPATOR TITLE LENGTH:'
     A                                    33'4'
     A                                      SPACEA(2)
     A                                     4'PART. INITIALS LENGTH    :'
     A                                    33'5'
     A                                      SPACEA(2)
     A                                     4'PART. SURNAME LENGTH     :'
     A                                    33'23'
     A                                      SPACEA(2)
     A                                     4'PART. ADD. LINE LENGTH   :'
     A                                    33'28'
     A                                      SPACEA(2)
     A                                     4'PART. ADD. LINE COUNT    :'
     A                                    33'3'
     A*
     A**--------------------------------------------------------------------
     A** FORM TYPE 2
     A*
     A          R RE2196F2                  TEXT('FORM TYPE 2')
     A                                      SPACEB(3)
     A                                     4'FORM TYPE - 2'
     A                                      SPACEA(2)
     A**Start of change CGL029
     A**********                           5'ACCOUNT REFERENCE'                               CGL029
     A**********                          33'ACCOUNT NAME & ADDRESS'                          CGL029
     A**********                          65'GROSS REG.?'                                     CGL029
     A**********                          79'NEW A/C?'                                        CGL029
     A**********                          90'GROSS INTEREST'                                  CGL029
     A**********                         111'TAX DEDUCTED'                                    CGL029
     A                                    37'ACCOUNT NAME & ADDRESS'
     A                                    69'GROSS REG.?'
     A                                    83'NEW A/C?'
     A                                    94'GROSS INTEREST'
     A                                   115'TAX DEDUCTED'
     A**End of change CGL029
     A                                      SPACEA(2)
     A**Start of change CGL029
     A**********  P2ACNU        20         4                                                  CGL029
     A**********  P2CNA1        35        27                                                  CGL029
     A**********  P2GRGI         1        70                                                  CGL029
     A**********  P2NAC          1        82                                                  CGL029
     A**********  P2SGRI        13        90                                                  CGL029
     A**********  P2SBRT        13       110                                                  CGL029
     A            P2ACNU        24         4
     A            P2CNA1        35        31
     A            P2GRGI         1        74
     A            P2NAC          1        86
     A            P2SGRI        13        94
     A            P2SBRT        13       114
     A**End of change CGL029
     A                                      SPACEA(1)
     A**Start of change CGL029
     A**********  P2CNA2        35        27                                                  CGL029
     A            P2CNA2        35        31
     A**End of change CGL029
     A                                      SPACEA(1)
     A**Start of change CGL029
     A**********  P2CNA3        35        27                                                  CGL029
     A            P2CNA3        35        31
     A**End of change CGL029
     A                                      SPACEA(1)
     A**Start of change CGL029
     A**********  P2CNA4        35        27                                                  CGL029
     A            P2CNA4        35        31
     A**End of change CGL029
     A                                      SPACEA(2)
     A                                     4'CURRENCY IDENTIFIER - '
     A                                    26'GBP'
     A                                      SPACEA(2)
     A                                     4'NO OF PARTICIPATORS - '
      ** Begin  CRE021
     A*********** P2NOOP         3  0     26SPACEA(2)
     A            P2NOOP         4  0     26SPACEA(2)
      ** End of CRE021
     A*
     A                                     4'NO OF PARTICIPATORS RECORDS - '
      ** Begin  CRE021
     A*********** P2NPCU         3  0     34SPACEA(2)
     A            P2NPCU         4  0     34SPACEA(2)
      ** End of CRE021
      ** Begin  CRE007
     A  89                                 4'POST 5/4/01 R105 - '
     A  89        P2PSTR         1        23SPACEA(2)
      ** End of CRE007
     A*
     A**--------------------------------------------------------------------
     A** FORM TYPE 3
     A*
     A          R RE2196H3                  TEXT('FORM TYPE 3')
     A                                      SPACEB(1)
     A*
     A                                     4'FORM TYPE - 3'
     A                                      SPACEA(2)
     A                                     4'PARTICIPATOR NAME & ADDRESS'
     A                                    42'NI NUMBER'
     A                                    56'DATE OF BIRTH'
     A                                    72'GROSS REG.'
     A                                      SPACEA(1)
     A*
     A          R RE2196F3                  TEXT('DETAILS FORM TYPE 3')
     A                                      SPACEB(1)
     A            P3TITL         4         4
     A            P3PNAM        23         9
     A            P3PINI         5        33
     A            P3NINO         9        42
     A            P3DOB          8        58
     A            P3GRGI         1        76
     A                                      SPACEA(1)
     A
     A            P3ADD1        28         4
     A                                      SPACEA(1)
     A            P3ADD2        28         4
     A                                      SPACEA(1)
     A            P3ADD3        28         4
     A                                      SPACEA(1)
     A            P3PCD1         4         4
     A            P3PCD2         3         9
     A                                      SPACEA(1)
     A*
     A**--------------------------------------------------------------------
     A** FORM TYPE 4
     A*
     A          R RE2196F4                  TEXT('FORM TYPE 4')
     A                                      SPACEB(3)
     A                                     4'FORM TYPE - 4'
     A                                      SPACEA(2)
     A                                     4'ACCOUNT COUNT       :'
     A            P4ACTC        11  0     26EDTCDE(Z)
     A*
     A**--------------------------------------------------------------------
     A** END OF REPORT FORMAT
     A*
     A          R RE2196ZZ                  SPACEB(3)
     A                                    51'*** END OF REPORT ***'
     A*
     A**--------------------------------------------------------------------
     A** DATABASE ERROR MESSAGE
     A*
     A          R RE2196ER                  TEXT('DATABASE ERROR MESSAGE')
     A*
     A                                      SPACEB(3)
     A                                    39'******************************'
     A                                    69'****************'
     A                                      SPACEA(1)
     A                                    39'*'
     A                                    84'*'
     A                                      SPACEA(1)
     A                                    39'* DATABASE ERROR IN :'
     A            DBPGM         10        60
     A                                    84'*'
     A                                      SPACEA(1)
     A                                    39'* AT :'
     A            DBASE          3  0     60
     A                                    84'*'
     A                                      SPACEA(1)
     A                                    39'* FILE :'
     A            DBFILE         8        60
     A                                    84'*'
     A                                      SPACEA(1)
     A                                    39'* KEY  :'
     A            DBKEY         29        47
     A                                    84'*'
     A                                      SPACEA(1)
     A                                    39'*'
     A                                    84'*'
     A                                      SPACEA(1)
     A                                    39'******************************'
     A                                    69'****************'
