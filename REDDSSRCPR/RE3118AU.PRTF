      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas Retail History Update Audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module
     A*                                                               *
     A*  PRTF/RE3118AU - RETAIL HISTORY UPDATE AUDIT                  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01413  *CREATE    Date 13Apr93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  S01413  -  Retail 3 Incorporation                            *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R HEADAU                    SKIPB(3)
     A                                      TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                     7'REFERENCE'
     A                                    17'RE3118AU'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE         004S 0    124EDTCDE(3) SPACEA(2)
     A                                    52'RETAIL HISTORY UPDATE '
     A                                    74'FILE CONTROL'
     A                                      SPACEA(1)
     A                                    52'----------------------'
     A                                    74'------------'
     A                                      SPACEA(2)
     A                                     9'FILE'
     A                                    57'NUMBER OF RECORDS'
     A                                    86'NUMBER OF RECORDS'
     A                                      SPACEA(1)
     A                                    62'ON FILE'
     A                                    90'CALCULATED'
     A          R TOTALS                    SKIPB(10)
     A                                     9'RETAIL RATE CHANGES'
     A                                      SPACEB(2)
     A            RNORET         5  0     63
     A            WNORER         5  0     93
     A  82                               105'*** DIFFERENCE ***'
     A                                     9'RETAIL MANUALS ADJUSTMENTS'
     A                                      SPACEB(2)
     A            MNORE          5  0     63
     A            WNOREM         5  0     93
     A  81                               105'*** DIFFERENCE ***'
     A                                     9'RETAIL POSTINGS'
     A                                      SPACEB(2)
     A            WNOREP         5  0     93
     A                                    19'- PROCESSED'
     A                                      SPACEB(1)
     A            WPPOS          5  0     33
     A                                    19'- FUTURE'
     A                                      SPACEB(1)
     A            WFPOS          5  0     33
     A          R ERRAU                     TEXT('AUDIT DTABASE ERROR')
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8        30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10        30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3S 0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*    CNUM         '''
     A                                      SPACEB(1)
      * Begin /CSD027/
     A**********  CNUM           6S 0     30                                                  CSD027
     A            CNUM           6A       30
      * End /CSD027/
     A                                    36''''
     A                                    64'*'
     A                                    11'*    CCY          '''
     A                                      SPACEB(1)
     A            CCY            3        30
     A                                    33''''
     A                                    64'*'
     A                                    11'*    ACOD         '''
     A                                      SPACEB(1)
     A**Start of change CGL029
     A**********  ACOD           4S 0     30                                                  CGL029
     A            ACOD          10S 0     30
     A**End of change CGL029
     A                                    34''''
     A                                    64'*'
     A                                    11'*    ACSQ         '''
     A                                      SPACEB(1)
     A            ACSQ           2S 0     30
     A                                    32''''
     A                                    64'*'
     A  88                                11'*    STATUS       '''
     A  88                                  SPACEB(1)
     A  88        DBSTAT         5        30
     A  88                                35''''
     A  88                                64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
