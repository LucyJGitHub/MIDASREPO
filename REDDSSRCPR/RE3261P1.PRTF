      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Retail Linked Account Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module
     A*                                                               *
     A*  PRTF/RE3261P1 - RETAIL LINKED ACCOUNTS REPORT                *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01413  *CREATE    Date 13Apr93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  S01413  -  Retail 3 Incorporation                            *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *************
     A**   REPORT HEADER
     A**   *************
     A**
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'RE3261P1'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE           5  0    124EDTCDE(Z) SPACEA(2)
     A                                    55'LINKED ACCOUNTS REPORT'
     A                                    91'BRANCH'
     A            BRCA#     R             98REFFLD(BRCA)
     A            BRNM      R            103
     A                                      SPACEA(1)
     A                                    55'----------------------'
     A*/COPY WNCPYSRC,RE3261P101
     A                                      SPACEA(1)
     A                                     4'Account Number'
     A                                    80'---------------'
     A                                    95'Amount'
     A                                   101'---------------'
     A                                      SPACEA(1)
     A                                    80'Debit'
     A                                   110'Credit'
     A*/COPY WNCPYSRC,RE3261P102
     A                                      SPACEA(2)
     A*/COPY WNCPYSRC,RE3261P103
     A*
     A**
     A**   *************
     A**   REPORT DETAIL
     A**   *************
     A**
     A          R DETP1                     SPACEB(1)
     A            BRCA      R              4
     A                                     7'-'
      * Begin /CSD027/
     A**********  CNUM      R              8                                                  CSD027
     A            CNUM           6A        8TEXT('Customer number')
      * End /CSD027/
     A                                    14'-'
     A            CCY       R             15
     A                                    18'-'
     A            ACOD      R             19
     A**Start of change CGL029
     A**********                          23'-'                                               CGL029
     A**********  ACSQ      R             24                                                  CGL029
     A**********  NARR      R             29                                                  CGL029
     A                                    29'-'
     A            ACSQ      R             30
     A**End of change CGL029
     A                                    60'INCOME/EXP'
     A            DIIE      R             80EDTCDE(A)
     A            CIIE      R            110EDTCDE(A)
     A                                      SPACEA(1)
     A**Start of change CGL029
     A            NARR      R              4
     A**End of change CGL029
     A                                    60'ACCRUED INTEREST'
     A            DAIC      R             78EDTCDE(A)
     A            CAIC      R            108EDTCDE(A)
     A                                      SPACEA(1)
     A                                    60'MAN. ADJUST.'
     A            MADI      R             80EDTCDE(A)
     A            MACI      R            110EDTCDE(A)
     A                                      SPACEA(1)
     A                                    60'GENERATED ADJ.'
     A            GADI      R             80EDTCDE(A)
     A            GACI      R            110EDTCDE(A)
     A                                      SPACEA(2)
     A*/COPY WNCPYSRC,RE3261P104
     A**
     A**   **********
     A**   NO RECORDS
     A**   **********
     A**
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A**
     A**   **********
     A**   REPORT END
     A**   **********
     A**
     A          R ENDP1
     A  79                                46'*** END OF REPORT FOR'
     A  79                                67' BRANCH '
     A  79        BRCA#     R             75REFFLD(BRCA)
     A  79                                78' ***'
     A N79                                56'*** END OF REPORT ***'
     A                                      SPACEB(3)
     A**
     A**   *********************
     A**   REPORT DATABASE ERROR
     A**   *********************
     A**
     A          R ERRP1
     A                                      SPACEB(02)
     A                                     9'REFERENCE RE3261'
     A                                    45'PRINT TERMINATED, '
     A                                    64'DATABASE IN ERROR'
