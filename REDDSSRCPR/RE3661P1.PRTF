      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Retail History Update Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas Retail Module
     A*                                                               *
     A*  PRTF/RE3661P1 - RETAIL HISTORY UPDATE REPORT                 *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*   Note: This file is used in Data Migration and when core     *
     A*         changes to it are signed off, it must be copied to    *
     A*         CORESRC in library MIGRATE and the members that refer *
     A*         to it in the various Migration Data Libraries         *
     A*         must be recompiled.                                   *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01413 *C *CREATE  Date 13Apr93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  S01413 - Retail 3 Incorporation.                             *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *************
     A**   REPORT HEADER
     A**   *************
     A**
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'RE3661P1'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE      R            124EDTCDE(3) SPACEA(2)
     A*
     A                                    55'UPDATED BACK-VALUED '
     A                                    75'POSTINGS REPORT'
     A                                      SPACEA(1)
     A*
     A                                    55'--------------------'
     A                                    75'---------------'
     A                                      SPACEA(2)
     A*
     A  10                                 2'BRANCH '
     A  10        BRCA#     R             +1REFFLD(BRCA)
     A  10        BRNM      R             +1
     A  10                                  SPACEA(1)
     A*
     A                                    79'ORIGINAL     UPDATED'
     A                                      SPACEA(1)
     A*
     A                                    79'HISTORY      HISTORY'
     A                                      SPACEA(1)
     A*
     A                                    10'TYPE'
     A                                    29'CUSTOMER'
     A                                    39'CCY'
     A                                    47'A/C'
     A                                    54'SEQ'
     A                                    79'DATE         DATE'
     A                                      SPACEA(1)
     A**
     A**   *************
     A**   REPORT DETAIL
     A**   *************
     A**
     A          R DETP1                     SPACEB(1)
     A  41                                 8'POSTING'
     A  42                                 8'MAN. ADJ.'
     A  43                                 8'RATE CHG.'
      * Begin /CSD027/
     A**********  CNUM      R             30                                                  CSD027
     A            CNUM           6A       30TEXT('Customer number')
      * End /CSD027/
     A            CCY       R             39
     A            ACOD      R             46
     A            ACSQ      R             55
     A            ORDATE    R             79REFFLD(RUNA)
     A            UPDATE    R             92REFFLD(RUNA)
     A**
     A**   **********
     A**   NO RECORDS
     A**   **********
     A**
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A**
     A**   **********
     A**   REPORT END
     A**   **********
     A**
     A          R ENDP1                     SPACEB(3)
     A N10                                56'*** END OF REPORT ***'
     A  10                                48'*** END OF REPORT FOR BRANCH'
     A  10        BRCA#     R             +1REFFLD(BRCA)
     A  10                                +1'***'
     A**
     A**   *********************
     A**   REPORT DATABASE ERROR
     A**   *********************
     A**
     A          R ERRP1
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(5)
