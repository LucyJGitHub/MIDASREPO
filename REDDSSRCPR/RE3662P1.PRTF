      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Retl Int. & Chrg Capitalisation Rept')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module
     A*                                                               *
     A*  PRTF/RE3662P1 - RETAIL GENERATED ADJUSTMENTS TODAY'S REPORT  *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
     A*   Note: This file is used in Data Migration and when core     *
     A*         changes to it are signed off, it must be copied to    *
     A*         CORESRC in library MIGRATE and the members that refer *
     A*         to it in the various Migration Data Libraries         *
     A*         must be recompiled.                                   *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01413 *C *CREATE  Date 13Apr93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  S01413 - Retail 3 Incorporation                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A*
     A**   REPORT HEADER
     A*
     A          R HEADPR                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'RE3662P1'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGP         004S 0    124EDTCDE(3) SPACEA(2)
     A*
     A                                    45'RETAIL GENERATED ADJUSTM'
     A                                    69'ENTS TODAY    '
     A  30                                93'BRANCH'
     A  30        BBRCA     R            100REFFLD(BRCA)
     A  30                               104'-'
     A  30        BBRNM     R            106REFFLD(BRNM)
     A                                      SPACEA(1)
     A*
     A                                    45'------------------------'
     A                                    69'---------     '
     A                                      SPACEA(3)
     A*
     A                                     1'CUST   CUSTOMER   ACCT'
     A                                    38'INTEREST'
     A                                    59'A C C R U E D'
     A                                    75'I N T E R E S T'
     A                                    93'A D J U S T M E N T S'
     A                                   119'CAPITALISATION'
     A                                      SPACEA(1)
     A*
     A                                     1'NUMBER NAME'
     A                                    19'CODE    CCY'
     A                                    40'SIDE'
     A                                   119'DATE'
     A                                      SPACEA(3)
     A*
     A**   DEBIT DETAIL
     A*
     A          R DETDR                     SPACEA(1)
      * Begin /CSD027/
     A*N43******  BCNUM          6  0      1                                                  CSD027
     A N43        BCNUM          6A        1
      * End /CSD027/
     A N43        CSNM      R              8
     A N43        BACOD          4  0     19
     A N43                                23'-'
     A N43        BACSQ          2  0     24
     A N43        BCCY           3        27
     A                                    41'DR'
     A            LASTDR         7       120
     A  80        GADI0         15  0     89EDTCDE(C)
     A  81        GADI1         15  1     89EDTCDE(C)
     A  82        GADI2         15  2     89EDTCDE(C)
     A  83        GADI3         15  3     89EDTCDE(C)
     A*
     A**   CREDIT DETAIL
     A*
     A          R DETCR                     SPACEA(1)
      * Begin /CSD027/
     A*N43******  BCNUM          6  0      1                                                  CSD027
     A N43        BCNUM          6A        1
      * End /CSD027/
     A N43        CSNM      R              8
     A N43        BACOD          4  0     19
     A N43                                23'-'
     A N43        BACSQ          2  0     24
     A N43        BCCY           3        27
     A                                    41'CR'
     A            LASTCR         7       120
     A  80        GACI0         15  0     89EDTCDE(C)
     A  81        GACI1         15  1     89EDTCDE(C)
     A  82        GACI2         15  2     89EDTCDE(C)
     A  83        GACI3         15  3     89EDTCDE(C)
     A*
     A**   END OF REPORT
     A*
     A          R ENDP1
     A                                      SPACEB(3)
     A  30                                49'*** END OF REPORT FOR'
     A  30                                70' BRANCH '
     A  30        XBRCA     R             78REFFLD(BRCA)
     A  30                                81' ***'
     A N30                                56'*** END OF REPORT ***'
     A*
     A**  NO DETAILS
     A*
     A          R NODET
     A                                      SPACEB(2)
     A                                    53 '*** NO DETAILS '
     A                                    68 'TO REPORT ***'
     A*
     A** DATABASE ERROR
     A*
     A          R ERRP1
     A                                      SPACEB(2)
     A                                    55 '*** DATABASE ERROR ***'
