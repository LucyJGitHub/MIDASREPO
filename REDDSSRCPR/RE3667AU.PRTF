      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RE Retail hist upd for o'd line audit rept')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE3667AU - Retail History Update for Overdraft Line     *
      *                  Audit Report                                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRE001 *CREATE     Date 18Feb97               *
      *                                                               *
      *****************************************************************
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE001 - Overdraft Interest Calculations                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     7'REFERENCE RE3667AU'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE      R            124EDTCDE(3) SPACEA(2)
      *
     A                                    40'Retail History '
     A                                    +0'Overdraft File Controls'
     A                                      SPACEA(1)
      *
     A                                    40'---------------'
     A                                    +0'-----------------------'
     A                                      SPACEA(2)
      *
     A                                     7'File'
     A                                    55'Number of Records'
     A                                      SPACEA(1)
      *
     A                                    60'Calculated'
      *
      *-------------------*
      ***  Audit Details  *
      *------------------ *
      *
     A          R TOTALS                    SKIPB(10)
      *
     A                                     1'Overdraft Records  - Read'
     A            TOTREC        15  0     55EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    20'- Updated'
     A            TOTUPD        15  0     55EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    20'- Processed'
     A            TOTPRC        15  0     55EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    20'- Future'
     A            TOTFUT        15  0     55EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    20'- Deleted'
     A            TOTDEL        15  0     55EDTCDE(Z)
     A                                      SPACEA(2)
      *
     A                                    20'- Expired'
     A            TOTEXP        15  0     55EDTCDE(Z)
     A                                      SPACEA(2)
      *
      *---------------*
      ***  Audit End  *
      *---------------*
      *
     A          R ENDRAU
     A                                    56'*** END OF AUDIT ***'
     A                                      SPACEB(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R DBERAU
     A                                      SPACEB(2)
     A                                    19'**********************'
     A                                      SPACEA(1)
      *
     A                                    19'*   DATABASE ERROR   *'
     A                                      SPACEA(1)
      *
     A                                    19'**********************'
     A                                      SPACEA(2)
      *
     A                                    19'File  . . . . . :'
     A            DBFILE    R             37
     A                                      SPACEA(2)
      *
     A                                    19'Key . . . . . . :'
     A            DBKEY     R             37
     A                                      SPACEA(2)
      *
     A                                    19'DB Error Number :'
     A            DBASE          3  0     37
     A                                      SPACEA(2)
      *
     A  97                                19'DB Status . . . :'
     A  97        DBSTAT        20        37
      *
