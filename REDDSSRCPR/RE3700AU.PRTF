      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RE Retail int rate expiry date audit rept')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE3700AU - Retail Expired Interest Review Date Audit Rpt*
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CRE001 *CREATE     Date 18Feb97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE001 - Overdraft Interest Calculations                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADAU                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE3700AU'
     A            STITL     R             39REFFLD(TITL)
     A                                   101'DATE'
     A            SRUNA     R            107REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    32'Retail Expired '
     A                                    +0'Interest Review '
     A                                    +0'Date Audit Report '
     A  50                                81'(By Branch)'
     A  50                                  SPACEA(1)
     A N50                                81'(Consolidated)'
     A N50                                  SPACEA(1)
      *
     A                                    32'_______________'
     A                                    +0'________________'
     A                                    +0'__________________'
     A  50                                81'___________'
     A  50                                  SPACEA(2)
     A N50                                81'______________'
     A N50                                  SPACEA(2)
      *
      *-----------------*
      ***  File Control *
      *-----------------*
      *
     A          R DETLAU
     A                                     6'Retail Account Details +
     A                                      File'
     A                                    58'Number of Records'
     A                                      SPACEA(1)
     A                                    61'Calculated'
     A                                      SPACEA(2)
     A                                     6'Records Processed'
     A            TOTCNT         5  0     60EDTCDE(3)
     A                                      SPACEA(2)
     A                                     6'Expired Interest +
     A                                      Review Dates'
     A            EXPCNT         5  0     60EDTCDE(3)
      *
      *-------------------*
      ***  Database Error *
      *-------------------*
      *
     A          R ERRAU
     A                                      SPACEB(2)
     A                                    19'**********************'
     A                                      SPACEA(1)
      *
     A                                    19'*   DATABASE ERROR   *'
     A                                      SPACEA(1)
      *
     A                                    19'**********************'
     A                                      SPACEA(2)
     A                                    19'File  . . . . . :'
     A            DBFILE         8        36
     A                                      SPACEA(1)
     A                                    19'Key   . . . . . :'
     A            DBKEY         29        36
     A                                      SPACEA(1)
     A                                    19'DB Error Number :'
     A            DBASE          3  0     36
