      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Retail interest rate expiry date report')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE3700P1 - Retail Expired Interest Review Date Report   *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CRE001  *CREATE    Date 18Feb97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CRE001 - Overdraft Interest Calculations                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE RE3700P1'
     A            STITL     R        O    39REFFLD(TITL)
     A                                   101'DATE'
     A            SRUNA     R        O   107REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    39'Retail Expired '
     A                                    54'Interest Review '
     A                                    70'Date Report'
     A  50                                88'Branch'
     A  50        BRCA#     R        O    95REFFLD(BRCA)
     A  50        BRNM      R        O   100
     A                                      SPACEA(1)
     A N50                                88'Consolidated'
     A                                      SPACEA(1)
      *
     A                                    39'_______________'
     A                                    +0'________________'
     A                                    +0'___________'
     A                                      SPACEA(2)
      *
     A                                     2'Branch'
     A                                    10'Retail Account Number'
     A                                    37'Report Name'
     A                                    55'Overdraft Line'
     A                                    71'Interest Rate'
     A                                    86'Review Date'
     A                                   106'Account Officer'
     A                                      SPACEA(1)
      *
     A                                     2'______'
     A                                    10'_____________________'
     A                                    33'____________________'
     A                                    55'______________'
     A                                    71'_____________'
     A                                    86'___________'
     A                                   100'______________________________'
     A                                      SPACEA(002)
      *
      *-------------------*
      ***  Report Details *
      *-------------------*
      *
     A          R DETP1
     A                                      SPACEB(001)
      *
     A            BRCA#          3   O     2
      * Begin /CSD027/
     A*N51******  ATCNUM         6  0O    11                                                  CSD027
     A N51        ATCNUM         6A  O    11
      * End /CSD027/
     A N51                                17'-'
     A N51        ATCCY          3   O    18
     A N51                                21'-'
     A**Start of change CGL029
     A*N51******  ATACOD         4  0O    22                                                  CGL029
     A*N51******                          26'-'                                               CGL029
     A*N51******  ATACSQ         2  0O    27                                                  CGL029
     A N51        ATACOD        10  0O    22
     A N51                                32'-'
     A N51        ATACSQ         2  0O    33
     A**End of change CGL029
     A  51        ACNO          10  0O    11
      *
     A**Start of change CGL029
     A**********  BBCRNM        20   O    33                                                  CGL029
      **********                                                                              CGL029
     A**********  ODLN          11  0O    55                                                  CGL029
     A**********                            EDTWRD('  .   .   . 0 ')                          CGL029
     A**********  ATAODR        11  7O    71                                                  CGL029
     A**********                            EDTWRD('  0 ,       %')                           CGL029
     A**52******                          84'-'                                               CGL029
     A**********  OTITRD         7   O    88                                                  CGL029
     A**53******                         100'*NONE*'                                          CGL029
     A*N53******  AZACON        30   O   100                                                  CGL029
      *
     A            BBCRNM        20   O    39
      *
     A            ODLN          11  0O    61
     A                                      EDTWRD('  .   .   . 0 ')
     A            ATAODR        11  7O    77
     A                                      EDTWRD('  0 ,       %')
     A  52                                90'-'
     A            OTITRD         7   O    94
     A  53                               106'*NONE*'
     A N53        AZACON        30   O   106
     A**End of change CGL029
      *
      *-------------------*
      ***  Report End     *
      *-------------------*
      *
     A          R ENDP1
     A                                      SPACEB(003)
     A                                    46'*** END OF REPORT'
     A  50                                +1'FOR BRANCH'
     A  50        BRCA#     R        O    +1REFFLD(BRCA)
     A  50                                +1' ***'
     A N50                                63' ***'
      *
      *--------------------------*
      ***  Report Database Error *
      *--------------------------*
      *
     A          R ERRP1
     A                                      SPACEB(002)
     A                                    19'**********************'
     A                                      SPACEA(001)
      *
     A                                    19'*   DATABASE ERROR   *'
     A                                      SPACEA(001)
      *
     A                                    19'**********************'
