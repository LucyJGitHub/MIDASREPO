      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Overdraft line maturity report')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE3710P1 - Retail Overdraft Line Maturity Report        *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CRE001 *CREATE     Date 18Feb97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CRE001 - Overdraft Interest Calculations                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
      *
     A                                      SKIPB(003)
     A                                     6'REFERENCE RE3710P1'
     A            STITL     R        O    39REFFLD(TITL)
     A                                   101'DATE'
     A            SRUNA     R        O   107REFFLD(RUNA)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(2)
      *
     A                                    41'Retail Overdraft '
     A                                    58'Line Maturity '
     A                                    72'Report'
     A  50                                88'Branch'
     A  50        BRCA#     R        O    95REFFLD(BRCA)
     A  50        BRNM      R        O   100
     A                                      SPACEA(001)
     A N50                                88'Consolidated'
     A                                      SPACEA(001)
      *
     A                                    41'_________________'
     A                                    +0'______________'
     A                                    +0'______'
     A                                      SPACEA(002)
      *
     A                                     3'Branch'
     A                                    12'Retail Account Number'
     A                                    40'Report Name'
     A                                    59'Overdraft Line'
     A                                    76'OD Expiry Date'
     A                                   100'Account Officer'
     A                                      SPACEA(001)
      *
     A                                     3
     A                                      '______'
     A                                    12
     A**Start of change CGL029
     A**********                            '_____________________'                           CGL029
     A**********                          36                                                  CGL029
     A                                      '_________________________'
     A                                    38
     A**End of change CGL029
     A                                      '____________________'
     A                                    59
     A                                      '______________'
     A                                    76
     A                                      '______________'
     A                                    93
     A                                      '______________________________'
     A                                      SPACEA(002)
      *
      *-------------------*
      ***  Report Details *
      *-------------------*
      *
     A          R DETP1
     A                                      SPACEB(001)
      *
     A            BRCA#          3   O     4
      * Begin /CSD027/
     A*N51******  ATCNUM         6  0O    13                                                  CSD027
     A N51        ATCNUM         6A  O    13
      * End /CSD027/
     A N51                                19
     A                                      '-'
     A N51        ATCCY          3   O    20
     A N51                                23
     A                                      '-'
     A**Start of change CGL029
     A*N51******  ATACOD         4  0O    24                                                  CGL029
     A*N51******                          28                                                  CGL029
     A**********                            '-'                                               CGL029
     A*N51******  ATACSQ         2  0O    29                                                  CGL029
     A N51        ATACOD        10  0O    24
     A N51                                34
     A                                      '-'
     A N51        ATACSQ         2  0O    35
     A**End of change CGL029
     A  51        ACNO          10  0O    13
      *
     A**Start of change CGL029
     A**********  BBCRNM        20   O    36                                                  CGL029
      **********                                                                              CGL029
     A**********  ODLN          11  0O    59                                                  CGL029
     A**********                            EDTWRD('  .   .   . 0 ')                          CGL029
     A**********  OODED          7   O    79                                                  CGL029
     A**53******                          93'*NONE'                                           CGL029
     A*N53******  AZACON        30   O    93                                                  CGL029
      *
     A            BBCRNM        20   O    38
      *
     A            ODLN          11  0O    59
     A                                      EDTWRD('  .   .   . 0 ')
     A            OODED          7   O    79
     A  53                                93'*NONE'
     A N53        AZACON        30   O    93
     A**End of change CGL029
      *
      *-------------------*
      ***  Report End     *
      *-------------------*
      *
     A          R ENDP1
     A                                      SPACEB(003)
     A                                    46
     A                                      '*** END OF REPORT'
     A  50                                +1
     A                                      'FOR BRANCH'
     A  50        BRCA#     R        O    +1
     A                                      REFFLD(BRCA)
     A  50                                +1
     A                                      ' ***'
     A N50                                63
     A                                      ' ***'
      *
      *--------------------------*
      ***  Report Database Error *
      *--------------------------*
      *
     A          R ERRP1
     A                                      SPACEB(002)
     A                                    19
     A                                      '**********************'
     A                                      SPACEA(001)
      *
     A                                    19
     A                                      '*   DATABASE ERROR   *'
     A                                      SPACEA(001)
      *
     A                                    19
     A                                      '**********************'
