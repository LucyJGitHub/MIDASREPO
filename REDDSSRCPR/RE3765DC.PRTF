      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STMT60                                                    *
/*EXI *  TEXT('Midas RE Int capitalisation report Dr and Cr')         *
/*ADD *  CHGPRTF FILE(RE3765DC) FORMTYPE('CPT-INT')                   *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module
     A*                                                               *
     A*  PRTF/RE3765DC - INTEREST CAPITALISATION REPORT DR & CR       *
     A*                  PRODUCE ON CHANGE OF A/C OR CHANGE OF PAGE   *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. CER050             Date 16Jun19               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 MD037149           Date 11May16               *
      *                 CER059             Date 19Jul10               *
      *                 CER048             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD053             Date 01Jun06               *
      *                 CSD027             Date 30Nov05               *
      *                 CGL031             Date 05Jul04               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 130468             Date 27Apr99               *
      *                 CRE001             Date 18Feb97               *
     A*                 115460             Date 18Feb97               *
     A*                 094657             Date 25Nov95               *
     A*                 S01413 *CREATE     Date 13Apr93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CER050 - Annualised Percentage Rate                          *
      *  MD046248 - Finastra Rebranding                               *
      *  MD037149 - ORF Queries not running                           *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER048 - German Features - Taxes                             *
      *  CSD053 - Correspondence Manager Multilanguage Upgrade        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     A*  CGL031 - Taxation of Savings Income                          *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  130468 - BRT not deducted from CR interest adjustments.      *
     A*  CRE001 - Overdraft Interest Calculations                     *
     F*         - Include O/D Line and O/D Interest Rate Changes to   *
     F*           Debit Capitalisation Report.                        *
     A*  115460 - Prevent Heading Overprinting                        *
     A*  094657 - Suppress customer address details if specified.     *
     A*  S01413 - Retail 3 Incorporation                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A          R HEADDC                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(10)
     A            RUNA      R              4
     A  70                                30'*** MANAGEMENT CAPITALISA+
     A                                      TION ***'
     A            PAGF           4S 0     77EDTCDE(3)
     A*
     A*                                            /*Begin 094657*/
     A************CNA1      R             41SPACEB(4)
     A************CNA2      R             41SPACEB(1)
     A************CNA3      R             41SPACEB(1)
     A**Start of change CSD053
     A**********  CNA1      R             41                                                  CSD053
      *
      ** Start of change MD037149
     A**23******  CNA1          35O       41                                                  MD0371
     A  23        CNA1      R             41
      ** End of change MD037149
      *
     A**End of change CSD053
     A                                      SPACEB(4)
     A**Start of change CSD053
     A**********  CNA2      R             41                                                  CSD053
      *
      ** Start of change MD037149
     A**23******  CNA2          35O       41                                                  MD0371
     A  23        CNA2      R             41
      ** End of change MD037149
      *
     A**End of change CSD053
     A                                      SPACEB(1)
     A**Start of change CSD053
     A**********  CNA3      R             41                                                  CSD053
      *
      ** Start of change MD037149
     A**23******  CNA3          35O       41                                                  MD0371
     A  23        CNA3      R             41
      ** End of change MD037149
      *
     A**End of change CSD053
     A                                      SPACEB(1)
     A*                                            /*End   094657*/
     A                                     4'ACCOUNT NO : '
     A            ACNO      R             17
     A*/COPY WNCPYSRC,RE3765DC02
     A*                                            /*Begin 094657*/
     A************CNA4      R             41SPACEB(1)
     A**Start of change CSD053
     A**********  CNA4      R             41                                                  CSD053
      *
      ** Start of change MD037149
     A**23******  CNA4          35O       41                                                  MD0371
     A  23        CNA4      R             41
      ** End of change MD037149
      *
     A**End of change CSD053
     A                                      SPACEB(1)
     A*                                            /*End   094657*/
     A                                     4'CURRENCY   : '
      *                                              Begin /115460/
     A                                      SPACEB(1)
      *                                              End   /115460/
     A            CCCY      R             17
     A                                     4'REFERENCE : '
     A                                      SPACEB(2)
     A            CBRCA2    R             17REFFLD(BRCA)
      * Begin /CSD027/
     A**********  CCNUM     R             21REFFLD(CNUM)                                      CSD027
     A            CCNUM          6A       21TEXT('Customer number')
      * End /CSD027/
     A            CACOD     R             28REFFLD(ACOD)
     A**Start of change CGL029
     A**********  CACSQ     R             33REFFLD(ACSQ)                                      CGL029
     A            CACSQ     R             39REFFLD(ACSQ)
     A**End of change CGL029
     A                                      SKIPA(24)
     A*
     A          R POSTDC                    SPACEA(1)
     A
     A            PSTDA     R              1REFFLD(CNUMA)
     A            VALDA     R              8REFFLD(CNUMA)
     A            NARR      R             20REFFLD(PNAR)
     A  80        PSTD0         15  0     56EDTCDE(L)
     A  81        PSTD1         15  1     56EDTCDE(L)
     A  82        PSTD2         15  2     56EDTCDE(L)
     A  83        PSTD3         15  3     56EDTCDE(L)
     A  33                                73'*'
     A*
     A          R RATEDC                    SPACEA(1)
     A    N03     PSTDA     R              1REFFLD(CNUMA)
     A            VALDA     R              8REFFLD(CNUMA)
     A            RNARR     R             20REFFLD(PNAR)
     A            RATE          11  7     50EDTWRD('   0.       %')
     A*/COPY WNCPYSRC,RE3765DC13
     A*
      *                                              Begin /CRE001/
     A          R ODLNDC                    SPACEA(1)
     A            ONARR         30        21
     A            OODRL         11  0     51EDTCDE(C)
      *
     A          R AODRDC                    SPACEA(1)
     A            ONARR         30        21
     A            OAODR         11  7     51EDTWRD('   0.       -%')
      *                                              End   /CRE001/
     A*/COPY WNCPYSRC,RE3765DC16
     A          R STOTDC                    SPACEA(1)
     A     03     VALDA     R              8REFFLD(CNUMA)
     A            DAYS           4  0     15EDTCDE(C)
     A            NUMB0         15  0     32EDTCDE(L)
     A*
     A  80        HISB0         15  0     50EDTCDE(L)
     A  81        HISB1         15  1     50EDTCDE(L)
     A  82        HISB2         15  2     50EDTCDE(L)
     A  83        HISB3         15  3     50EDTCDE(L)
     A*
     A*/COPY WNCPYSRC,RE3765DC03
     A  80        INT0          12  0     67EDTCDE(L)
     A  81        INT1          12  1     67EDTCDE(L)
     A  82        INT2          12  2     67EDTCDE(L)
     A  83        INT3          12  3     67EDTCDE(L)
     A*/COPY WNCPYSRC,RE3765DC04
     A*
     A          R TITLEDC                   SPACEA(1)
     A            VALDA     R              8REFFLD(CNUMA)
     A            TNARR     R             20REFFLD(PNAR)
     A  80        HISB0         15  0     50EDTCDE(L)
     A  81        HISB1         15  1     50EDTCDE(L)
     A  82        HISB2         15  2     50EDTCDE(L)
     A  83        HISB3         15  3     50EDTCDE(L)
     A*
     A          R TOTALDC                   SPACEA(1)
     A            DAYS           4  0     15EDTCDE(C)
     A    N22     XNARR     R             20REFFLD(PNAR)
     A*/COPY WNCPYSRC,RE3765DC05
     A  80N22     TINT0         12  0     67EDTCDE(L)
     A  81N22     TINT1         12  1     67EDTCDE(L)
     A  82N22     TINT2         12  2     67EDTCDE(L)
     A  83N22     TINT3         12  3     67EDTCDE(L)
     A*/COPY WNCPYSRC,RE3765DC06
     A*
     A     22                             21'TOTAL GROSS INTEREST'
     A  80 22     TGINT0        15  0     63EDTCDE(C)
     A  81 22     TGINT1        15  1     63EDTCDE(C)
     A  82 22     TGINT2        15  2     63EDTCDE(C)
     A  83 22     TGINT3        15  3     63EDTCDE(C)
     A*
      *                                            /*Begin 130468*/
     A          R TOTBRT                    SPACEA(1)
     A*****22     SPACE     R            131SPACEA(1)
      *                                            /*End   130468*/
     A     22                             21'LESS BRT AT'
     A     22     WKBRT          7  5     33EDTWRD(' 0.     %')
     A  80 22     BRTOT0        15  0     63EDTCDE(C)
     A  81 22     BRTOT1        15  1     63EDTCDE(C)
     A  82 22     BRTOT2        15  2     63EDTCDE(C)
     A  83 22     BRTOT3        15  3     63EDTCDE(C)
     A*/COPY WNCPYSRC,RE3765DC15
     A*
      **Start of Change CGL031
     A          R EUWTAX                    SPACEA(1)
     A     21                             21'LESS EUROPEAN WITHOLDING-
     A                                       TAX'
     A  80 21     EUTAX0        15  0     63EDTCDE(C)
     A  81 21     EUTAX1        15  1     63EDTCDE(C)
     A  82 21     EUTAX2        15  2     63EDTCDE(C)
     A  83 21     EUTAX3        15  3     63EDTCDE(C)
      **End of Change CGL031
      *
      **Start of change CER048
     A          R SECTAX                    SPACEA(1)
     A                                    21'LESS SECONDARY TAX'
     A  80        STAX0         15  0     63EDTCDE(C)
     A  81        STAX1         15  1     63EDTCDE(C)
     A  82        STAX2         15  2     63EDTCDE(C)
     A  83        STAX3         15  3     63EDTCDE(C)
      **End of change CER048
     A          R TOTDDC                    SPACEA(1)
     A            DNARR     R             20REFFLD(PNAR)
     A*/COPY WNCPYSRC,RE3765DC07
     A  80        TINT0         12  0     67EDTCDE(L)
     A  81        TINT1         12  1     67EDTCDE(L)
     A  82        TINT2         12  2     67EDTCDE(L)
     A  83        TINT3         12  3     67EDTCDE(L)
     A*/COPY WNCPYSRC,RE3765DC08
     A*
     A          R RAT1DC                    SPACEA(1)
     A*
     A            PSTDA     R              1REFFLD(CNUMA)
     A            VALDA     R              8REFFLD(CNUMA)
     A            RNARR     R             20REFFLD(PNAR)
     A*
     A          R RAT2DC                    SPACEA(1)
     A*
     A            BNARR     R             20REFFLD(PNAR)
     A  80        HISB0         15  0     50EDTCDE(L)
     A  81        HISB1         15  1     50EDTCDE(L)
     A  82        HISB2         15  2     50EDTCDE(L)
     A  83        HISB3         15  3     50EDTCDE(L)
     A*
     A          R RAT3DC                    SPACEA(1)
     A*
     A            ORAT1         11  7     23EDTWRD('   0.       %')
     A N84        ORAT2         11  7     43EDTWRD('   0.       %')
     A N85        ORAT3         11  7     63EDTWRD('   0.       %')
     A*/COPY WNCPYSRC,RE3765DC14
     A*
     A          R RAT4DC                    SPACEA(1)
     A*
     A  80        OBAL01        15  0     23EDTCDE(L)
     A  81        OBAL11        15  1     23EDTCDE(L)
     A  82        OBAL21        15  2     23EDTCDE(L)
     A  83        OBAL31        15  3     23EDTCDE(L)
     A  80N84     OBAL02        15  0     43EDTCDE(L)
     A  81N84     OBAL12        15  1     43EDTCDE(L)
     A  82N84     OBAL22        15  2     43EDTCDE(L)
     A  83N84     OBAL32        15  3     43EDTCDE(L)
     A  80N85     OBAL03        15  0     63EDTCDE(L)
     A  81N85     OBAL13        15  1     63EDTCDE(L)
     A  82N85     OBAL23        15  2     63EDTCDE(L)
     A  83N85     OBAL33        15  3     63EDTCDE(L)
     A*
     A          R ODAYDC                    SPACEA(1)
     A  03        VALDA     R              8REFFLD(CNUMA)
     A  80        HISB0         15  0     50EDTCDE(L)
     A  81        HISB1         15  1     50EDTCDE(L)
     A  82        HISB2         15  2     50EDTCDE(L)
     A  83        HISB3         15  3     50EDTCDE(L)
     A*
     A          R STODDC                    SPACEA(1)
     A            VALDA     R              8REFFLD(CNUMA)
     A            DAYS           4  0     15EDTCDE(C)
     A            NUMB0         15  0     32EDTCDE(L)
     A*
     A*/COPY WNCPYSRC,RE3765DC09
     A  80        INT0          12  0     67EDTCDE(L)
     A  81        INT1          12  1     67EDTCDE(L)
     A  82        INT2          12  2     67EDTCDE(L)
     A  83        INT3          12  3     67EDTCDE(L)
     A*/COPY WNCPYSRC,RE3765DC10
     A*
     A          R TOTMAN                    SPACEB(2)
     A                                    20'*** MANAGEMENT INFO'
     A                                    39'RMATION ONLY - ENTRIES'
     A                                    62'NOT GENERATED ***'
     A*
     A          R MADJDC                    SPACEA(1)
     A            NARR      R             20REFFLD(PNAR)
     A*
     A  80        HISB0         15  0     50EDTCDE(L)
     A  81        HISB1         15  1     50EDTCDE(L)
     A  82        HISB2         15  2     50EDTCDE(L)
     A  83        HISB3         15  3     50EDTCDE(L)
     A*
     A*/COPY WNCPYSRC,RE3765DC11
     A  80        INT0          12  0     67EDTCDE(L)
     A  81        INT1          12  1     67EDTCDE(L)
     A  82        INT2          12  2     67EDTCDE(L)
     A  83        INT3          12  3     67EDTCDE(L)
     A*/COPY WNCPYSRC,RE3765DC12
     A*
     A          R TOTND                     SPACEB(1)
     A            NARRD         21        11
     A            TNUMD         15  0     32EDTCDE(L)
     A*
     A          R TOTNC                     SPACEA(1)
     A            NARRC         21        11
     A            TNUMC         15  0     32EDTCDE(L)
     A*
     A          R TOTNB                     SPACEA(1)
     A            BLAN           1        10
     A*
     A          R ALTNDC                    SPACEA(1)
     A            NARR      R             20REFFLD(PNAR)
     A            MIDK          15        51
     A            RATE          11  7     50EDTWRD('   0.       %')
     A*
     A          R TURRAT                    SPACEA(1)
     A            QNARR         29        11
     A            RATET         11  7     63EDTWRD('   0.       %')
     A*
     A          R FUTVAL                    SPACEB(1)
     A*
     A            RNARR     R             20REFFLD(PNAR)
     A                                      SPACEA(1)
     A*
     A*/COPY WNCPYSRC,RE3765DC01
      ** Start of change CER050
     A          R APRDC                     SPACEB(2)
      *
     A            TANAR     R             21REFFLD(PNAR)
     A            IAPRR          6  2     52EDTWRD('   0.  -%')
      ** End of change CER050
