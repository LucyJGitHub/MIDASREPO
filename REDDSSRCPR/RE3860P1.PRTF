      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas Account Activity Extract Print')
     A*****************************************************************
     A*                                                               *
     A*  Midas Retail Module
     A*                                                               *
     A*  PRTF/RE3860P1 - ACCOUNT ACTIVITY EXTRACT PRINT               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 241041             DATE 13Jul06               *
      *  Prev Amend No. CSD027             Date 30Nov05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 103413             Date 18Feb98               *
      *                 103710             Date 22Jan98               *
     A*                 S01413 *CREATE     DATE 13APR93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  241041  -  RE3860 changed to include activity of closed      *
     A*             accounts, RE3860P1 file changed to display account*
     A*             closed date.                                      *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  103413  -  SKIP INSTEAD OF SPACE TO AVOID OVERFLOW PROBLEM   *
     A*  103710  -  Add retail a/c number and date account opened.    *
     A*  S01413  -  Retail 3 Incorporation                            *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *************
     A*
     A**   PRINT  HEADER
     A*
     A**   *************
     A**
     A*
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A*
     A                                     7'REFERENCE'
     A                                    17'RE3860P1'
     A            TITL      R             37
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A*
     A            PAGE      R            124EDTCDE(3) SPACEA(2)
     A*
     A*
     A                                    47'ACCOUNT ACTIVITY '
     A                                    64'EXTRACT FILE '
     A*
     A                                    77'PRINT'
     A*
     A                                      SPACEA(1)
     A*
     A                                    47'-----------------'
     A                                    64'-------------'
     A*
     A                                    77'-----'
     A                                      SPACEA(2)
     A*
     A                                     1'BRANCH'
     A            BRCA      R              8
     A            BRNM      R             12
     A                                      SPACEA(2)
     A*
     A                                     5'END OF LAST ACTIVITY ACCRUAL'
     A                                   + 1'PERIOD'
     A            ELACN          6A      + 1
     A                                   + 1'END OF CURRENT ACTIVITY ACCRUAL '
     A                                   + 1'PERIOD'
     A            ECACN          6A      + 1
     A                                      SPACEA(1)
     A                                     5'Postings reported if valued within'
     A                                   + 1'current activity'
     A                                      SPACEA(1)
     A                                     5'accrual period or a previous'
     A                                   + 1'period.'
     A                                      SPACEA(1)
     A                                     5'''*''  - Posting date is greater'
     A                                   + 1'than end of last activity'
     A                                      SPACEA(1)
     A                                     5'accrual period.'
     A                                      SPACEA(2)
     A**
     A**   *************
     A**   PRINT  DETAIL
     A**   *************
     A**
     A          R DETP0                     SPACEA(1)
     A            BRCA      R              5
      * Begin /CSD027/
     A**********  CNUM      R              9                                                  CSD027
     A            CNUM           6A        9TEXT('Customer number')
      * End /CSD027/
     A            CCY       R             16
     A            ACOD      R             20
     A**Start of change CGL029
     A**********  ACSQ      R             25                                                  CGL029
     A*                                                               /*103710*/
     A**********                          28'-'                                               CGL029
     A**********  ACNO      R             30                                                  CGL029
     A**********                          44'Date account opened'                             CGL029
     A**********  DACON          6A       64                                                  CGL029
     A            ACSQ      R             31
     A                                    34'-'
     A            ACNO      R             36
     A***********                         50'Date account opened'                             241041
     A*********** DACON          6A       70                                                  241041
     A N40                                50'Date account opened'                             241041
     A N40        DACON          6A       70                                                  241041
     A  40                                50'Date account closed'                             241041
     A  40        DACCN          6A       70                                                  241041
     A**End of change CGL029
     A*                                                               /*103710*/
     A          R DETP1                     SPACEA(1)
     A            PSTDN          6A        2
     A            VALDN          6A        9
     A            TRATN          5A       17
     A            NARRN         30A       25
     A  70        PSTA0         15  0     57EDTCDE(C)
     A  71        PSTA1         15  1     57EDTCDE(C)
     A  72        PSTA2         15  2     57EDTCDE(C)
     A  73        PSTA3         15  3     57EDTCDE(C)
     A            DRCRN          1A       74
     A  81                                76'*'
     A          R DETP2                     SPACEA(1)
     A            TIRET        132A        1
     A*
     A**
     A**   **********
     A*
     A**   PRINT  END
     A*
     A**   **********
     A**
     A*
      *                                     Start of change /103413/
     A**********R ENDP1                     SPACEB(3)
     A          R ENDP1
     A                                      SKIPB(59)
      *                                       End of change /103413/
     A                                    50'*** END OF REPORT FOR'
     A                                    +1'BRANCH'
     A            BRCA#     R             80REFFLD(BRCA)
     A                                    +1'***'
     A*
     A          R ERRP1                     SPACEB(3)
     A                                    52'*** DATABASE ERROR ***'
