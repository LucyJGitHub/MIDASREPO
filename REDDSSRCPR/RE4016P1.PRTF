/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE T&N rate changes report')                     *
      *****************************************************************
      *                                                               *
      *  Midas - Retail T&N Accounts                                  *
      *                                                               *
      *  PRTF/RE4016P1 - Midas RE T&N rate changes report             *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. AR851810           Date 17Nov11               *
      *                 CRE073             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CRE015 *CREATE     Date 18Dec09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  AR851810 - Account and Value date fields had invalid values  *
      *             after the 2nd COB                                 *
      *  CRE073 - Interest Rate Change                                *
      *  CRE015 - Retail Term and Notice Accounts                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **  Audit header
      *
     A          R AUHEAD                    TEXT('(C) Finastra International +
     A                                      Limited +
     A                                      2009')
     A                                      SKIPB(3) SPACEA(2)
     A                                     2'Reference'
     A                                    15'RE4016P1'
     A            BJURPT    R             39REFFLD(URPT)
     A                                   109'Date'
     A            BJMRDT    R            114REFFLD(MRDT)
     A                                   123'Page'
     A                                   128PAGNBR
     A                                      EDTCDE(4)
     A                                      SPACEA(2)
     A                                    53'Term and Notice Rate Changes'
     A                                      SPACEA(1)
     A                                    53'---------------------------'
     A                                      SPACEA(1)
     A                                     3'Account'
     A                                    15'Value'
     A                                    +3'Maturity'
     A                                    +2'Rate Change'
      *---------------------------------------------------------------*
      *
      **  No details
      *
     A          R NODETL
     A                                      SPACEA(1)
     A                                    51'*** NO DETAILS TO'
     A                                    +1'REPORT ***'
      *---------------------------------------------------------------*
      *
      **  Details reworked
      *
     A          R AUDETS
     A                                      SPACEA(1)
     A** Start of change CRE073
     A**********  R1NARR       112         2                                                  CRE073
     A** Start of change AR851810
     A**********  R1NARR       130         2                                                AR851810
     A            R1NARR       112         2
     A** End of change AR851810
     A** End of change CRE073
     A            R1INCY        17        +1
      *---------------------------------------------------------------*
      *
      **  End of report
      *
     A          R AUEOR
     A                                      SPACEA(1)
     A                                     3'TOTAL='
     A            W0NBRU         8  0     +1EDTCDE(3)
     A                                      SPACEA(1)
     A                                    51'*** END OF'
     A                                    +1'REPORT ***'
      *---------------------------------------------------------------*
      *
      **  Database Error
      *
     A          R DBERROR
     A                                    53'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    38'************************'
     A                                      SPACEB(2)
     A                                    62'************************'
     A                                    86'******'
     A                                    38'*'
     A                                      SPACEB(1)
     A                                    91'*'
     A                                    38'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE    R             57
     A                                    65''''
     A                                    91'*'
     A                                    38'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY     R             57
     A                                    86 '''    *'
     A                                    38'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM     R             57
     A                                    67''''
     A                                    91'*'
     A                                    38'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3S 0     57
     A                                    60''''
     A                                    91'*'
     A                                    38'*'
     A                                      SPACEB(1)
     A                                    91'*'
     A                                    38'************************'
     A                                      SPACEB(1)
     A                                    62'************************'
     A                                    86'******'
