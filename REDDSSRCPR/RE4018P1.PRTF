/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE T&N Accounts - Account code check')
      *****************************************************************
      *                                                               *
      *  Midas - Retail T&N Accounts                                  *
      *                                                               *
      *  PRTF/RE4018P1 Term and Notice Account Code Check             *
      *                                                               *
      *  (c) Finastra International Limited 2009                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. CRE015 *CREATE     Date 18Dec09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRE015 - Retail Term and Notice Accounts                     *
      *                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
      *****************************************************************
      *
      **  Audit header
      *
     A          R AUHEAD
     A                                      TEXT('(c) Finastra +
     A                                      Int Banking Sys 2009')
     A                                      SKIPB(3) SPACEA(2)
     A                                     2'Reference'
     A                                    15'RE4018P1'
     A            BJURPT    R             39REFFLD(URPT)
     A                                   109'Date'
     A            BJMRDT    R            114REFFLD(MRDT)
     A                                   123'Page'
     A                                   128PAGNBR
     A                                      EDTCDE(4)
     A                                      SPACEA(2)
     A                                    10'Term and Notice Accounts - '
     A                                    +1'Account Code Check'
     A                                      SPACEA(1)
     A                                    10'Period and Penalty Details'
     A                                    +1'which are missing and should'
     A                                    +1'be created or are existing'
     A                                    +1'but should be deleted'
     A                                      SPACEA(1)
     A                                    10'This is being run in'
     A            MODE           7A       +1
     A                                    +1'mode'
     A                                      SPACEA(1)
     A                                    10'---------------------------'
     A                                      SPACEA(1)
     A                                     2'Account'
     A                                    46'Action to be taken'
      *
      *****************************************************************
      *
      **  No details
      *
     A          R NODETL
     A                                      SPACEA(1)
     A                                    51'*** NO DETAILS TO'
     A                                    +1'REPORT ***'
      *
      *****************************************************************
      *
      **  Details reworked
      *
     A          R AUDETS
     A                                      SPACEA(1)
     A            RBRCA          3         2
     A            RCNUM          6        +1
     A            RCCY           3        +1
     A            RACOD         10        +1
     A            RACSQ          4        +1
     A            RACNO         10        +1
     A            ACTION         7        +8
      *
      *****************************************************************
      *
      **  End of report
     A          R AUEOR
     A                                      SPACEA(1)
     A                                     3'TOTAL='
     A            W0NBRU         8  0     +1EDTCDE(3)
     A                                      SPACEA(1)
     A                                    51'*** END OF'
     A                                    +1'REPORT ***'
      *
      *****************************************************************
      *
      **  Database Error
      *
     A          R DBERROR
     A                                    53'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    38'************************'
     A                                      SPACEB(2)
     A                                    62'************************'
     A                                    86'******'
     A                                    38'*'
     A                                      SPACEB(1)
     A                                    91'*'
     A                                    38'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE    R             57
     A                                    65''''
     A                                    91'*'
     A                                    38'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY     R             57
     A                                    86 '''    *'
     A                                    38'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM     R             57
     A                                    67''''
     A                                    91'*'
     A                                    38'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3S 0     57
     A                                    60''''
     A                                    91'*'
     A                                    38'*'
     A                                      SPACEB(1)
     A                                    91'*'
     A                                    38'************************'
     A                                      SPACEB(1)
     A                                    62'************************'
     A                                    86'******'
