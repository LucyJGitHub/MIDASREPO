      *****************************************************************
/********PRTFBASE                                                     *                      BUG8085
/*STD *  PRTFPASSTL                                                   *                      BUG8085
/*EXI *  TEXT('Midas RE Cash/Cheque deposit A/C CCY')
/********PR_STD60                                                     *                      BUG7932
/********PR_STD68                                                     *              BUG7932 BUG8085
/********CHGPRTF FILE(RE4114P1  ) PAGESIZE(36 83) OVRFLW(32)          *                       JMI115
/*XAI *  CHGPRTF FILE(RE4114P1  ) PAGESIZE(66 83) OVRFLW(66)          *                       JMI115
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  PRTF/RE4114P1 - VALIDATION PRINTS                            *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. JMI115             Date ddmmmyy               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. BUG8798 (          Date 20Oct05               *
      *                 BUG8798            Date 10Oct05               *
      *                 BUG8067            Date 05Sep05               *
      *                 BUG8085            Date 26Aug05               *
      *                 BUG7932            Date 30Jul05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      *                 134432             Date 20Oct99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 114474             Date 24Sep97               *
     A*                 CRT002             Date  1DEC95               *
     A*                 S01408             Date 26FEB96               *
     A*                 093545             DATE  8SEP95               *
     A*                 CRT001  *CREATE    DATE 28FEB95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  JMI115   - RTS Advice Changes                                *
      *           - /COPY WNCPYSRC,RE4114P101                         *
      *           - /COPY WNCPYSRC,RE4114P102                         *
      *  MD046248 - Finastra Rebranding                               *
      *  BUG8798 - (Reopen) Wrong heading show on advice              *
      *  BUG8798 - Wrong heading show on advice                       *
      *  BUG8067 - Add Transaction Time                               *
      *  BUG8085 - Print to teller printer, change Standard Parameter *
      *  BUG7932 - Change Standard Parameter used                     *
     A*  134432 - Add in 'Charge' line.                               *
     A*  114474 - Different NARR used on Cash Deposit than Cash Advice*
     A*  CRT002 - Retail Branch Access.                               *
     A*  S01408 - Addition of core hook RE4114P101
     A*           Addition of core hook RE4114P102
     A*  093545 - Standardize pagesize of confirmation slips.  Use    *
     A*           36 lines by 83 columns.                             *
     A*  CRT001 - Retail Teller System.                               *
     A*                                                               *
     A*****************************************************************
     A*                                                                   S01408
     A*/COPY WNCPYSRC,RE4114P101                                          S01408
     A*                                                                   S01408
     A                                      REF(MFRFL)
     A*/COPY WNCPYSRC,RE4114P104
      **
      ** TELLER & ACCOUNT DETAILS
      *
     A          R DETAIL1                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      * date/teller id
     A                                      SKIPB(4)
      **Start of Change BUG8067
     A                                      SPACEA(002)
     A                                    50'Date:'
     A            BJMRDT         7        59
     A**********                            SPACEA(1)
     A                                    50
     A                                      'Time:'
     A                                      SPACEB(001)
     A                                    +4
     A                                      TIME
     A                                    50'Reference:'
     A                                      SPACEB(001)
     A**********  STTLU          5  0     61SPACEA(1)
     A            STTLU          5  0     61
     A            PTLID          3        63
     A**********                            SPACEA(1)
     A                                      SPACEB(001)
      *
      * heading
      **Start of change BUG8798
     A**********                          28'Cash Transaction'                               BUG8798
      **Start of change BUG8798
     A*N24N25***                          28'Cash Transaction'                               BUG8798
     A*N24N25***                            SPACEB(1)                                        BUG8798
     A*N24N25***                            SPACEA(1)                                        BUG8798
     A  23                                28'Combined Cash/Cheque Deposit'
     A  23                                  SPACEB(1)
     A  23                                  SPACEA(1)
      **End of change BUG8798
     A  24                                28'Clearing Cheque Deposit'
     A  24                                  SPACEB(1)
     A  24                                  SPACEA(1)
     A  25                                28'Cash Deposit'
     A  25                                  SPACEB(1)
     A  25                                  SPACEA(1)
      **Start of change BUG8798
     A  43                                28'Cash and House Cheque Deposit'
     A  43                                  SPACEB(1)
     A  43                                  SPACEA(1)
     A  44                                28'House Cheque Deposit'
     A  44                                  SPACEB(1)
     A  44                                  SPACEA(1)
     A  50                                28'Travellers Cheque Deposit'
     A  50                                  SPACEB(1)
     A  50                                  SPACEA(1)
      **End of change BUG8798
     A**********                            UNDERLINE                                        BUG8798
      **End of change BUG8798
     A**********                            SPACEA(2)
      **End of Change BUG8067
     A  71                                 8'Operating Instructions:'
     A  71        NARRO         20        33
     A                                      SPACEA(2)
      *
     A*/COPY WNCPYSRC,RE4114P103
      * a/c number & name
     A            ACNO          10S 0      8
     A            OCUST         35        23
     A                                      SPACEA(2)
      *
      *
      *  TRANSACTION DETAILS
      *
     A          R DET00N                    TEXT('CASH DEPOSIT ONLY - 00N')
      * transaction type name
     A            @NRTD1        30         8
     A*                                              Begin /114474/
     A            OCCY           3        42
     A            OAMT          22        48
     A*                                              End   /114474/
     A                                      SPACEA(2)
      *
      *
      *
     A          R DETCASH                   TEXT('TRANS 00H & 00P ONLY')
      * buy cash details
     A                                     8'Payments In:'
     A            OCCY           3        42
     A            OAMT          22        48
     A                                      SPACEA(2)
      *
      *
      *
     A          R DETCHEQ                   TEXT('CLG CHEQUE BUY - 00H&00K')
      * buy clearing cheque details
     A**********                                     Begin /CRT002/
     A**********                           8'Clearing Cheque Payments In:'
     A N50                                 8'Clearing Cheque Payments In:'
     A  50                                 8'Travellers Cheque Payments In:'
     A*                                              End   /CRT002/
     A            OCCY           3        42
     A            OAMT          22        48
     A                                      SPACEA(1)
      * cheque number
     A                                     8'Cheque No. :'
     A            OCQNF          8        29
     A                                      SPACEA(2)
     A          R DETHSCQ                   TEXT('HSE CHEQUE BUY - 00P&00Q')
      * buy house cheque details
     A                                     8'House Cheque Payments In:'
     A            OCCY           3        42
     A            OAMT          22        48
     A                                      SPACEA(1)
      * cheque number
     A                                     8'Cheque No. :'
     A            OCQNF          8        29
     A                                      SPACEA(2)
      *
      *
     A          R CHARGE                    TEXT('TRANSACTION CHARGE')
     A
     A*                                              Begin /134432/
     A                                     8'Charge:'
     A*                                              End   /134432/
     A  14                                29'@'
     A  14        SCPER          5        31
     A  14                                36'%'
     A            OCCY           3        42
     A            OAMT          22        48
     A                                      SPACEA(2)
      *
      *
     A          R TOTAL                     TEXT('TOTAL DEPOSIT')
      * transaction, currency, amount
     A                                      SKIPB(25)
     A                                     8'Credit Total'
     A            OCCY           3        42
     A            OAMT          22        48
      *
      *
      *
     A          R DETSIGN
      * signature etc lines
     A                                      SKIPB(29)
     A                                     8'               '
     A                                      UNDERLINE
     A                                    36'               '
     A                                      UNDERLINE
     A                                      SPACEA(1)
      *
     A                                     8'    Teller     '
     A                                    36'   Customer    '
     A*                                                                   S01408
     A*/COPY WNCPYSRC,RE4114P102                                          S01408
     A*                                                                   S01408
