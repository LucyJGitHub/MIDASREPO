      *****************************************************************
/********PRTFBASE                                                     *                       237730
/*STD *  PRTFPASSTL                                                   *                       237730
/*EXI *  TEXT('Midas RE Foreign Exchange Trans Print')
/********PR_STD60                                                     *                       237730
/*XAI *  CHGPRTF FILE(RE4120P1  ) PAGESIZE(36 83) OVRFLW(32)          *
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  PRTF/RE4120P1 - Validation Prints                            *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. 237730             Date 12Jun06               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CRT002             Date 1DEc95                *
     A*                 S01408             DATE 12FEB96               *
     A*                 093545             DATE  8SEP95               *
     A*                 CRT001  *CREATE    DATE 28FEB95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  237730 - Print to corresponding teller printer.              *
     A*  CRT002 - Retail Branch Access.                               *
     A*  S01408 - Addition of  core hook RE4120P101
     A*           Addition of  core hook RE4120P102
     A*  093545 - Standardize pagesize of confirmation slips.  Use    *
     A*           36 lines by 83 columns.                             *
     A*  CRT001 - Retail Teller System                                *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      ** Detail print
     A*
     A*/COPY WNCPYSRC,RE4120P101
     A*
     A          R HEADER                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(5)
     A            A8BRNM        30        46
     A                                      SPACEA(4)
     A**********                                     Begin /CRT002/
     A  37                                35'F O R E I G N   E X C H A N G E'
     A                                      SPACEA(3)
     A  38                                35'BUYING TRAVELLERS CHEQUE'
     A                                      SPACEA(3)
     A  39                                35'SELLING TRAVELLERS CHEQUE'
     A*                                              End   /CRT002/
     A                                      SPACEA(3)
     A                                    25'Date:'
     A            BJMRDT         7        31
     A                                    62'Time:'
     A            TIME           6  0     68
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(2)
     A                                    15'Transaction no:'
     A            @TTRNO         5  0     31
     A                                    59'Cashier:'
     A            PTLID          3        68
     A                                      SPACEA(5)
      *
     A N41        SNARR         30        15
     A N41                                  SPACEA(2)
      *
     A*/COPY WNCPYSRC,RE4120P103
     A          R SELCOM                    TEXT('COMMISSION IN SELL CCY')
     A                                    26'BUY CURRENCY:'
     A            SBCCY          3        43
     A                                    51'AMOUNT:'
     A            OBAMT         17        59
     A                                      SPACEA(2)
      *
     A                                    25'EXCHANGE RATE:'
     A            OEXRT         17        39
     A                                      SPACEA(2)
      *
     A                                    25'SELL CURRENCY:'
     A            SSCCY          3        43
     A                                    51'AMOUNT:'
     A            OEXAMT        17        59
     A                                      SPACEA(2)
      *
     A                                    23'LESS COMMISSION:'
     A                                    48'@'
     A  32        SCPER          5        50
     A  32                                55'%'
     A            OCAMT         17        59
     A                                      SPACEA(1)
     A                                    59'-----------------'
     A                                      SPACEA(1)
      *
     A                                    28'TOTAL CASH:'
     A            @SSCCY         3        43
     A            OSAMT         17        59
      *
      *
     A          R BUYCOM                    TEXT('COMMISSION IN BUY CCY')
     A                                    26'BUY CURRENCY:'
     A            SBCCY          3        43
     A                                    51'AMOUNT:'
     A            OBAMT         17        59
     A                                      SPACEA(2)
      *
     A                                    23'LESS COMMISSION:'
     A                                    48'@'
     A  32        SCPER          5        50
     A  32                                55'%'
     A            OCAMT         17        59
     A                                      SPACEA(1)
     A                                    59'-----------------'
     A                                      SPACEA(1)
      *
     A            OEXAMT        17        59
     A                                      SPACEA(2)
      *
     A                                    25'EXCHANGE RATE:'
     A            OEXRT         17        39
     A                                      SPACEA(2)
      *
     A                                    25'SELL CURRENCY:'
     A            SSCCY          3        43
     A                                    51'AMOUNT:'
     A            OSAMT         17        59
      *
      *
     A          R NOCOM                     TEXT('NO COMMISSION')
     A                                    26'BUY CURRENCY:'
     A            SBCCY          3        43
     A                                    51'AMOUNT:'
     A            OBAMT         17        59
     A                                      SPACEA(2)
      *
     A                                    25'EXCHANGE RATE:'
     A            OEXRT         17        40
     A                                      SPACEA(2)
      *
     A                                    26'SELL CURRENCY:'
     A            SSCCY          3        43
     A                                    51'AMOUNT:'
     A            OSAMT         17        59
     A                                      SPACEA(2)
      *
     A*
     A*/COPY WNCPYSRC,RE4120P102
     A*
