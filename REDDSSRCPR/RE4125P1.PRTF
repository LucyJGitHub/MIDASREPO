      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD66J                                                    *
/*EXI *  TEXT('Midas RE Teller/Batch Totals Enquiry Print')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                    *
     A*                                                               *
     A*  PRTF/RE4125P1 - Teller Totals Slip                           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT001  *CREATE    Date 28Feb95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CRT001 - Retail Teller System                                *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      ** Heading print
      *
     A          R HEADNG                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                  3  6'REFERENCE RE4125P1'
     A            BJURPT    R           3 37REFFLD(URPT)
     A                                  3101'DATE'
     A            BJMRDT    R           3106REFFLD(MRDT)
     A                                  3119'PAGE'
     A                                  3124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  4101'TIME'
     A                                  4106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A            HD1            8      6 49
     A            HD2            8      6 57
     A            HD3            2      6 65
     A                                  7 49'------------------'
      *
     A            DT1            8      9 10
     A            DT2            7      9 18
     A                                  9 25':'
     A            SENQY          3A     9 28TEXT('Teller/Batch No')
     A                                  9 40'TRANSACTION NOS:'
     A            FRMTRN         5  0O  9 58TEXT('transactions from')
     A                                  9 64'-'
     A N55        TOTRN          5  0O  9 66TEXT('transactions to')
     A  55                              9 66'NONE '
      *
     A                                 13 10'Currency'
     A                                 13 21'-------Debits-------'
     A                                 13 47'------Credits-------'
     A                                 13 84'Cash'
     A                                 13101'Clearings'
      *
     A                                 14 21'No.'
     A                                 14 37'Value'
     A                                 14 47'No.'
     A                                 14 63'Value'
     A                                 14 83'Value'
     A                                 14105'Value'
      *
     A          R DETAIL1                   TEXT('QUERY PRINT')
     A                                      SPACEB(2)
     A            SCCYR          3        12TEXT('CURRENCY')
     A            SNDRA          5  0     18TEXT('DEBIT TRNS')
     A                                      EDTCDE(L)
     A            PVDRA         17        25TEXT('DEBIT VALUE')
     A            SNCRA          5  0     44TEXT('CREDIT TRNS')
     A                                      EDTCDE(L)
     A            PVCRA         17        51TEXT('CREDIT VALUE')
     A            PVCAA         19        72TEXT('CASH VALUE')
     A            PVCKA         17        93TEXT('CLEARING VALUE')
      *
     A          R LAST                      TEXT('LAST LINE')
     A                                    51'*** END OF REPORT ***'
     A                                      SPACEB(2)
      *
      ** Database error reporting line
     A          R ERRLINE                   TEXT('ERROR PRINT LINE')
     A                                    47'*** REFERENCE RE4125 +
     A                                      DATABASE ERROR ***'
     A                                      SPACEB(2)
      *
