      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RE Teller Trans Entry Generation')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Teller System                             *
     A*                                                               *
     A*  PRTF/RE4201AU - Teller Trans. Generated Entries - Audit      *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT001  *CREATE    Date 28Feb95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CRT001 - Retail Teller System                                *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
**CRTA*:*SRCMBR(*FILE)*DEV(*JOB ) DEVTYPE(*SCS) PAGESIZE(72 132)    : *
**CRTA*:*LPI(6)*CPI(10)*OVRFLW(66* FOLD(*NO) RPLUNPRT(*YES ' ')     : *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
      *
      ** Page header - 1  (AUDIT RECORD)
     A          R AUDREC                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(03)
     A                                      SPACEA(2)
     A                                     6'REFERENCE RE4201AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A N50                                40'TELLER TRANSACTIONS GENERATED '
     A N50                                70'ENTRIES - AUDIT REPORT'
     A N50                                  SPACEA(1)
     A  50                                42'TELLER TRANSACTIONS BALANCE CH'
     A  50                                72'ECK - AUDIT REPORT'
     A                                      SPACEA(1)
     A N50                                40'------------------------------'
     A N50                                70'----------------------'
     A N50                                  SPACEA(6)
     A  50                                42'------------------------------'
     A  50                                72'------------------'
     A                                      SPACEA(6)
      *
     A                                    10'NUMBER OF RECORDS'
     A                                    28'ON FILE      :'
     A            @NRECF         5  0     55EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                    10'NUMBER OF RECORDS'
     A                                    28'PROCESSED    :'
     A            @TPCNT         5  0     55EDTCDE(3)
     A N68                                  SPACEA(2)
     A  68                                75'*** DIFFERENCE ***'
     A                                      SPACEA(4)
      *
     A N50                                10'NUMBER OF RECORDS'
     A N50                                28'GENERATED    :'
     A N50        @GECNT         5  0     55EDTCDE(3)
     A N50                                  SPACEA(2)
      *
     A N50                                10'GENERATED VALUE OF'
     A N50                                29'DEBITS      :'
     A  50                                10'VALUE OF DEBITS'
     A  50                                29'            :'
     A            @PGETD        13  0     55EDTCDE(3)
     A            @PGEDD         3  0     68
     A                                      SPACEA(2)
      *
     A N50                                10'GENERATED VALUE OF'
     A N50                                29'CREDITS     :'
     A  50                                10'VALUE OF CREDITS  '
     A  50                                29'            :'
     A            @PGETC        13  0     55EDTCDE(3)
     A            @PGECD         3  0     68
     A N67                                  SPACEA(2)
     A  67                                75'*** DIFFERENCE ***'
     A                                      SPACEA(4)
      *
     A                                    10'NUMBER OF INTERBRANCH'
     A                                    32'TRANSACTIONS :'
     A            @TICNT         5  0     55EDTCDE(3)
     A                                      SPACEA(2)
      *
     A                                    10'VALUE OF INTERBRANCH'
     A                                    31'TRANSACTIONS :'
     A            @PTTIB        13  0     55EDTCDE(3)
     A            @PTIDD         3  0     68
      *
     A                                    57'*** END OF REPORT ***'
     A                                      SKIPB(54)
     A*
     A*----------------------------------------------------------------
     A**  Database Error
     A*----------------------------------------------------------------
     A*
     A          R DBERROR
     A                                    54'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    39'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    39'*'
     A                                      SPACEB(1)
     A                                    92'*'
     A                                    39'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       58
     A                                    66''''
     A                                    92'*'
     A                                    39'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        58
     A                                    87 '''    *'
     A                                    39'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       58
     A                                    68''''
     A                                    92'*'
     A                                    39'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     58
     A                                    61''''
     A                                    92'*'
     A                                    39'*'
     A                                      SPACEB(1)
     A                                    92'*'
     A                                    39'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
