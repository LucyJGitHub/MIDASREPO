      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RE Passbook Upd Control File Audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                    *
     A*                                                               *
     A*  PRTF/RE4204AU - Passbook Update Control File Audit           *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT001  *CREATE    Date 28Feb95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CRT001 - Retail Teller System                                *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *******************
     A**   REPORT HEADING
     A**   *******************
     A**
     A          R HEADAU                    SKIPB(3)
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                     6'REFERENCE RE4204AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A*
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    34'PASSBOOK UPDATE CONTROL FILE'
     A                                    63'REORGANISATION - AUDIT CONTROL'
     A                                      SPACEA(1)
     A                                    34'-----------------------------'
     A                                    63'--------------'
      *
     A          R CONTROL                   SPACEB(13)
     A                                    77'INPUT'
     A                                    95'CALCULATED'
     A                                      SPACEA(1)
     A                                    77'-----'
     A                                    95'----------'
     A                                      SPACEA(2)
     A                                    11'NUMBER OF RECORDS ON PASSBOOK'
     A                                    41'UPDATE CONTROL FILE'
     A            PNORE          5  0     77EDTWRD('   0 ')
     A            PNORD          5  0     97EDTWRD('   0 ')
     A  15                               108'* DIFFERENCE *'
     A                                     1' ' SPACEB(3)
     A                                    11'NUMBER OF RECORDS DELETED'
     A            PNDEL          5  0     97EDTWRD('   0 ')
     A                                     1' ' SPACEB(2)
     A                                    11'NUMBER OF RECORDS ON REORGANISED'
     A                                    44'CONTROL FILE'
     A            PNOREA         5  0     97EDTWRD('   0 ')
     A*
     A*---------------*
     A***  No Details *
     A*---------------*
     A*
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A*
     A*-------------------*
     A***  Database Error *
     A*-------------------*
     A*
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *
