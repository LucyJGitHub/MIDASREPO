      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE List of Unpresented Cheques')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                    *
     A*                                                               *
     A*  PRTF/RE4213P1 - List of Unpresented Cheques - Report         *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT001  *CREATE    Date 28Feb95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CRT001 - Retail Teller System.                               *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
      *****************************************************************
      *
      *
     A                                      REF(MFRFL)
      *
      ** Heading print
     A          R HEADER                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                  3  7'REFERENCE RE4213P1'
     A            BJURPT        53      3 36
     A                                  3100'DATE'
     A            BJMRDT         7      3105
     A                                  3120'PAGE'
     A            PAGE           4  0   3125EDTCDE(Z)
      *
     A            TIME           6  0   4105
     A                                      EDTWRD('0 :  :  ')
      *
      *
     A                                  5 53'LIST OF UNPRESENTED CHEQUES'
      *
     A                                  6 53'---------------------------'
     A  38                              6 89'CONSOLIDATED'
      *
     A  39 37                          10  4'BRANCH  :'
     A  39 37     PBRCA          3     10 15TEXT('BRANCH CODE')
     A  39 37                          10 19'-'
     A  39 37     PBRNM         30     10 21TEXT('BRANCH NAME')
     A                                 12  4'RETAIL   CHEQUE/  START'
     A                                 12 29'NO OF   ---- DATE ----'
     A                                 12 71'LAST FOUR DIGITS OF'
     A                                 12 91'UNPRESENTED CHEQUES'
     A                                 13  4'A/C NO   GIRO IND CHQ NO'
     A                                 13 29'CHEQUES ISSUE  COLLECT'
     A                                 13 71'--------------------'
     A                                 13 91'-------------------'
      *
     A          R DETAIL                    TEXT('DETAIL LINE')
     A                                      SPACEB(2)
     A            PACNO         10  0      1TEXT('retail a/c no')
     A            PGCIN          1        16TEXT('Cheque/Giro ind')
     A            PSCQN          8  0     21TEXT('Start Cheque no')
     A            PNOCQ          3  0     32TEXT('No of Cheques')
     A                                      EDTCDE(3)
     A            PDISS          7        36TEXT('Issue date')
     A            PDCOL          7        44TEXT('Collection date')
     A            PUPCQ         80        53TEXT('Unpresented cheques')
      *
     A          R REST                      TEXT('DETAIL LINE')
     A                                      SPACEB(1)
     A            PUPCQ         80        53TEXT('Unpresented cheques')
      *
     A          R LAST                      TEXT('LAST LINE')
     A                                      SPACEB(3)
     A  38                                51'*** END OF REPORT ***'
     A*
     A  39 37                             46'*** END OF REPORT FOR BRANCH ***'
      *
      ** Database error reporting line
     A          R ERRLINE                   TEXT('ERROR PRINT LINE')
     A                                      SPACEB(2)
     A                                     7'DATABASE ERROR IN '
     A                                    26'FILE '
     A            DBFILE         8        31
     A                                    40'KEY '
     A            DBKEY         29        44
     A                                    70'IN '
     A            @PGM          10        73
     A                                    84'AT '
     A            DBASE          3  0     87
