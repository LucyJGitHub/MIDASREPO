      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RE Daily Posting By Dept Extract')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                    *
     A*                                                               *
     A*  PRTF/RE4214AU - Daily Posting by Dept Extract - Audit        *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT001  *CREATE    Date 28Feb95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*   CRT001 - Retail Teller System                               *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *******************
     A**   CONTROL REPORT
     A**   *******************
     A**
     A          R CONTROL                   TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                  3  6'REFERENCE RE4214AU'
     A            BJURPT        53      3 39
     A                                  3100'DATE'
     A            BJMRDT         7      3105
     A                                  3119'PAGE'
     A            PAGE1        004S 0   3125EDTCDE(Z)
     A                                  5 39'DAILY POSTING BY DEPT EXTRACT'
     A                                  5 68' - FILE CONTROL REPORT'
     A                                  6 39'-----------------------------'
     A                                  6 68'----------------------'
     A*
     A                                  8  7'FILE CONTROL REPORT'
     A*
     A                                 10  7'INPUT FILE :-'
     A                                 10 55'NUMBER'
     A                                 10 86'VALUE'
     A*
     A                                 12  7'PF/GETT - TELLER TRANS'
     A                                 12 30'GENERATED ENTRIES'
     A*
     A                                 14  9'RECORDS PROCESSED'
0432 A            GETREC         5  0  14 57EDTWRD('   0 ')
0433 A            GETHTI        15  0  14 72EDTCDE(3)
0434 A            GETHTD         3  0  14 87
     A*
     A                                 16  9'RECORDS ON FILE'
0432 A            NORE1     R          16 57EDTWRD('   0 ')
0433 A            HRWN      R          16 72EDTCDE(3)
0434 A            HRDC      R          16 87
0440 A  33                             16 93'***  DIFFERENCE  ***'
     A*
     A                                 28  7'OUTPUT FILE :-'
     A*
     A                                 30  7'PF/POSTD - DAILY POSTING BY DEPT'
     A                                 31  7'RECORDS OUTPUT'
     A*
0432 A            PSTREC         5  0  31 57EDTWRD('   0 ')
0433 A            PSTHTI        15  0  31 72EDTCDE(3)
0434 A            PSTHTD         3  0  31 87
     A**
     A**   *******************
     A**   REPORT DATABASE ERROR
     A**   *******************
     A**
     A          R ERROR
     A                                      SPACEB(2)
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8        30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10        30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
     A**
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A          R TRAILAU
     A                                    56'*** END OF REPORT ***'
     A                                      SPACEB(3)
