      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Presented Cheques Maint Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                    *
     A*                                                               *
     A*  PRTF/RE4217P1 - Cheques Presented Maintenance Report         *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT001  *CREATE    Date 28Feb95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CRT001 - Retail Teller System.                               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A*
     A*----------------------------------------------------------------
     A**  Report Header Record Format
     A*----------------------------------------------------------------
     A*
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE4217P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A*
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*
     A                                    43'PRESENTED CHEQUES MAINTENANCE +
     A                                      REPORT'
     A                                      SPACEA(1)
     A                                    43'------------------------------+
     A                                      ------'
     A                                      SPACEA(5)
     A*
     A*----------------------------------------------------------------
     A**  Sub-Heading:
     A*----------------------------------------------------------------
     A*
     A                                     2'BRANCH'
     A                                    10'RETAIL'
     A                                    19'CHEQUE/'
     A                                    29'START'
     A                                    40'ENDING'
     A                                      SPACEA(1)
     A*
     A                                     3'CODE'
     A                                    10'A/C NO'
     A                                    19'GIRO IND'
     A                                    29'CHQ NO'
     A                                    40'CHQ NO'
     A                                    51'STATUS'
     A                                    78'CHEQUES TO BE INVESTIGATED'
     A                                      SPACEA(1)
     A*
     A*----------------------------------------------------------------
     A**  Detail Record Format
     A*----------------------------------------------------------------
     A*
     A          R DETAIL                    SPACEB(2)
     A*
     A            PBRCA          3         3TEXT('Branch code')
     A            PACNO         10  0      8TEXT('retail a/c no')
     A            PGCIN          1        22TEXT('Cheque/Giro ind')
     A            PSCQN          8  0     28TEXT('Start Cheque no')
     A            PECQN          8  0     39TEXT('Ending Cheque no')
     A  15                                49'PRESENTED '
     A N15                                49'REINSTATED'
     A            PUPCQ         70        62TEXT('4 Digit cheque no')
     A*
     A*----------------------------------------------------------------
     A**  Cheque Number line
     A*----------------------------------------------------------------
     A*
     A          R REST                      SPACEB(1)
     A*
     A            PUPCQ         70        62
     A*
     A*----------------------------------------------------------------
     A**  Report Trailer
     A*----------------------------------------------------------------
     A*
     A          R TRAILPY                   SPACEB(4)
     A*
     A                                    51'***  END OF REPORT  ***'
     A*
     A*----------------------------------------------------------------
     A**  Database Error message
     A*----------------------------------------------------------------
     A*
     A          R DBERROR                   SPACEB(3)
     A*
     A                                    45'*** REFERENCE RE4217'
     A                                    66'DATABASE ERROR ***'
     A*
