      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Outward Clearing Cheques List')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  PRTF/RE4218P1 - List of Outward Clearing Cheques - Report    *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. 091241             Date 17Jul95               *
      *                 CRT001  *CREATE    Date 28Feb95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  091241 - Bank Code field extended from 7A to 11A.            *
     A*  CRT001 - Retail Teller System                                *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
      *
      ** Report Heading print
     A          R HEADNG                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
      *
     A                                  3  6'REFERENCE RE4218P1'
     A            BJURPT    R           3 37REFFLD(URPT)
     A                                  3101'DATE'
     A            BJMRDT    R           3106REFFLD(MRDT)
     A                                  3119'PAGE'
     A                                  3124PAGNBR
     A                                      EDTCDE(Z)
      *
     A                                  4101'TIME'
     A                                  4106TIME
     A                                      EDTWRD('  :  :  ')
      *
     A                                  5 47'LIST OF OUTWARD CLEAR'
     A                                  5 68'ING CHEQUES'
      *
     A  33N34                           6 89'CONSOLIDATED'
     A  33 34                           6 89'BRANCH -'
     A  33 34     RBRCA     R           6 98REFFLD(BRCA)
     A  33 34     RBRNM     R           6102REFFLD(BRNM)
      *
     A                                  6 47'---------------------'
     A                                  6 68'-----------'
      *
     A                                  8  3'ISSUING BANK :'
     A*                                              Begin /091241/
     A*********** PRBCD          7  0   8 17TEXT('BANK CODE')
     A            PRBCD         11  0   8 17TEXT('BANK CODE')
     A                                      EDTCDE(Z)
     A*********** PRBNM         30      8 26
     A            PRBNM         30      8 30
     A*                                              End   /091241/
     A                                 10  3'CURRENCY     :'
     A            PTNCY          3     10 17
      *
      ** Detail Heading print
     A          R DETHED                    TEXT('DETAIL HEADING')
      *
     A                                 12  6'CHEQUE NO.'
     A                                 12 52'AMOUNT'
     A                                 12 66'TYPE'
     A                                 13  1'           '
      *
     A          R DETAIL                    TEXT('DETAIL LINE')
     A                                      SPACEB(1)
     A            PCQNF          8  0      7TEXT('CHEQUE NO')
     A            PCQAM         12        46TEXT('CHEQUE AMOUNT')
     A            PTTYP         17        65TEXT('TXN TYPE')
      *
     A          R TOTLIN                    TEXT('TOTAL LINE')
     A                                      SPACEB(2)
     A                                     4'NO. OF RECORDS ='
     A                                    29'TOTAL ='
     A            PNOCQ          5  0     21TEXT('TOTAL NO.OF CHEQUES')
     A                                      EDTCDE(3)
     A            PVOCQ         20        38TEXT('TOTAL CHQ VALUE')
      *
      *
     A          R LAST                      TEXT('LAST LINE')
     A                                      SPACEB(4)
     A N34
     AON33                                55'*** END OF REPORT ***'
     A  33 34                             53'*** END OF BRANCH REPORT ***'
      *
      ** Database error reporting line
     A          R ERRLINE                   TEXT('ERROR PRINT LINE')
     A                                    47'*** REFERENCE RE4218 +
     A                                      DATABASE ERROR ***'
     A                                      SPACEB(2)
