      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RE Cheque Book Reorganisation Audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                    *
     A*                                                               *
     A*  PRTF/RE4219AU - RTS Cheque Book Reorganisation - Audit       *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT001  *CREATE    Date 28Feb95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CRT001 - Retail Teller System.                               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A*
     A*----------------------------------------------------------------
     A**  Report Header Record Format
     A*----------------------------------------------------------------
     A*
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE4219AU'
     A            BJURPT    R             36REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A*
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*
     A                                    36'CHEQUE BOOK REORGANISATION +
     A                                      FILE CONTROL TOTALS REPORT'
     A                                      SPACEA(1)
     A                                    36'---------------------------+
     A                                      --------------------------'
     A                                      SPACEA(3)
     A*
     A*----------------------------------------------------------------
     A**  File Controls
     A*----------------------------------------------------------------
     A*
     A          R CONTROL
     A*
     A                                 12 36'NUMBER OF RECORDS'
     A                                 12 54'PROCESSED'
     A            CNOR           5  0  12 81EDTCDE(3)
     A*
     A                                 14 36'NUMBER OF RECORDS'
     A                                 14 54'ON FILE'
     A            NORE      R          14 81EDTCDE(3)
     A  47                             14 92'*** DIFFERENCE ***'
     A*
     A                                 16 36'NUMBER OF DELETED RECORDS'
     A            NDEL           5  0  16 81EDTCDE(3)
     A*
     A                                 18 36'NUMBER OF RECORDS ON FILE'
     A                                 18 62'AFTER UPDATE'
     A            NRAU           5  0  18 81EDTCDE(3)
     A*
     A*----------------------------------------------------------------
     A**  Database Error
     A*----------------------------------------------------------------
     A*
     A          R DBERROR
     A                                    51'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    36'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    36'*'
     A                                      SPACEB(1)
     A                                    89'*'
     A                                    36'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       55
     A                                    63''''
     A                                    89'*'
     A                                    36'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        55
     A                                    84 '''    *'
     A                                    36'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       55
     A                                    65''''
     A                                    89'*'
     A                                    36'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     55
     A                                    58''''
     A                                    89'*'
     A                                    36'*'
     A                                      SPACEB(1)
     A                                    89'*'
     A                                    36'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
