      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RE Interbranch Transaction Audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                    *
     A*                                                               *
     A*  PRTF/RE4226AU - Interbranch Transaction Audit Layout         *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT001  *CREATE    Date 28Feb95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CRT001 - Retail Teller System                                *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A*
     A*------------------*
     A**  REPORT HEADING *
     A*------------------*
     A*
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                     5'REFERENCE RE4226AU'
     A            BJURPT    R             39REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*
     A                                    35'INTERBRANCH TRANSACTIONS FILE'
     A                                    65'CONTROL TOTALS REPORT'
     A                                      SPACEA(1)
     A                                    35'------------------------------'
     A                                    65'---------------------'
     A                                      SPACEA(2)
     A*
     A          R CONTROL                   SPACEA(9)
     A                                    11'RUN CONTROLS'
     A                                    48'INPUT'
     A                                    76'CALCULATED'
     A                                      SPACEA(5)
     A                                    11'VALUE OF POSTINGS'
     A                                    34'STIB1'
     A            HRWN1         15  0     42EDTCDE(3)
     A            HRDC1          3  0     57
     A            TOTI1         15  0     72EDTCDE(3)
     A            TOTD1          3  0     87
     A                                      SPACEA(1)
     A  40
     AO 41                               101'*** DIFFERENCE ***'
     A*
     A                                    34'STIB2'
     A            HRWN2         15  0     42EDTCDE(3)
     A            HRDC2          3  0     57
     A            TOTI2         15  0     72EDTCDE(3)
     A            TOTD2          3  0     87
     A                                      SPACEA(3)
     A*
     A                                    11'NO. OF RECORDS'
     A                                    34'STIB1'
     A            NORE1          5  0     48EDTWRD('   0 ')
     A            COUNT1         5  0     79EDTWRD('   0 ')
     A                                      SPACEA(1)
     A  42                               101'*** DIFFERENCE ***'
     A*
     A                                    34'STIB2'
     A            NORE2          5  0     48EDTWRD('   0 ')
     A            COUNT2         5  0     79EDTWRD('   0 ')
     A                                      SPACEA(1)
      *
      *---------------*
      ***  No Details *
      *---------------*
      *
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A*
     A*------------------*
     A** Database Error  *
     A*------------------*
     A*
     A          R ERROR
     A                                      TEXT('DATABASE ERROR')
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'************************'
     A                                      SPACEB(2)
     A                                    35'************************'
     A                                    59'******'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8        30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10        30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'************************'
     A                                      SPACEB(1)
     A                                    35'************************'
     A                                    59'******'
