      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Interbranch Transaction Report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                    *
     A*                                                               *
     A*  PRTF/RE4226P1 - Interbranch Transaction Report               *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT001  *CREATE    Date 28Feb95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CRT001 - Retail Teller System                                *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *******************
     A**   PAGE HEADINGS
     A**   *******************
     A*
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     5'REFERENCE RE4226'
     A            BJURPT    R             39REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    43'TELLER - INTERBRANCH '
     A                                    64'TRANSACTIONS REPORT'
     A                                      SPACEA(1)
     A                                    43'---------------------'
     A                                    64'-------------------'
     A                                      SPACEA(2)
     A*
     A  50                                 7'SECTION 1 - FINANCIAL TRANSACTIONS'
     A  50                                42'ENTERED IN THIS BRANCH FOR'
     A  50                                69'ACCOUNTS IN OTHER BRANCHES'
     A  50                                  SPACEA(1)
     A*
     A  51                                 7'SECTION 2 - FINANCIAL TRANSACTIONS'
     A  51                                42'ENTERED BY OTHER BRANCHES'
     A  51                                68'FOR ACCOUNTS IN THIS BRANCH'
     A  51                                  SPACEA(1)
     A*
     A  31                                 4'BRANCH  :'
     A                                      SPACEB(1)
     A  31 50     ORBR      R             15
     A  31 51     BRCA1          3        15
     A  31                                19'-'
     A  31        PBRNM         30        21TEXT('BRANCH NAME')
     A                                      SPACEA(2)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A*
     A                                     4'TELLER ID/  BRANCH'
     A                                    25'ACCOUNT    CCY   ACCOUNT'
     A                                    51'DR  TRANS       ACCOUNT'
     A                                    86'OTHER      ORIG'
     A                                   109'POSTING        VALUE'
     A                                      SPACEA(1)
     A*
     A                                     4'TRANS. NO.   CODE'
     A                                    25'AFFECTED   CODE  SUBTYPE'
     A                                    51'CR   TYPE         NAME'
     A                                    86'ACCOUNT    BRANCH'
     A                                   109'AMOUNT         DATE'
     A                                      SPACEA(2)
     A**
     A**   *******************
     A**   DETAIL
     A**   *******************
     A**
     A          R DETAIL                    TEXT('REPORT DETAIL')
     A                                      SPACEA(2)
     A            TELI      R              4
     A                                     7'/'
     A            TREFN     R              8
     A  50        BRCA1          3        17
     A            ACNO      R             24
     A            CCY       R             36
     A            STYP      R             44
     A            DCIND          1        52
     A            TRAT      R             55
     A            CRNM      R             62
     A  15        ORAN          10  0     84
     A  51        ORBR      R             97
     A  20        PSTA0         13  0    104EDTWRD(' ,   ,   ,   , 0 ')
     A  21        PSTA1         13  0    104EDTWRD('   ,   ,   , 0 . ')
     A  22        PSTA2         13  0    104EDTWRD('  ,   ,   , 0 .  ')
     A  23        PSTA3         13  0    104EDTWRD(' ,   ,   , 0 .   ')
     A            VALDAT         7       123
     A**
     A**   *******************
     A**   DEBIT TOTALS
     A**   *******************
     A**
     A          R DRTOTL                    TEXT('DEBIT TOTAL')
     A                                      SPACEA(2)
     A                                    65'DEBIT TOTAL FOR'
     A            CCNM      R             81
     A  20        DRTOT0        13  0     98EDTWRD(' ,   ,   ,   , 0 ')
     A  21        DRTOT1        13  0     98EDTWRD('   ,   ,   , 0 . ')
     A  22        DRTOT2        13  0     98EDTWRD('  ,   ,   , 0 .  ')
     A  23        DRTOT3        13  0     98EDTWRD(' ,   ,   , 0 .   ')
     A**
     A**   *******************
     A**   CREDIT TOTALS
     A**   *******************
     A**
     A          R CRTOTL                    TEXT('CREDIT TOTAL')
     A                                      SPACEA(3)
     A                                    65'CREDIT TOTAL FOR'
     A            CCNM      R             82
     A  20        CRTOT0        13  0     98EDTWRD(' ,   ,   ,   , 0 ')
     A  21        CRTOT1        13  0     98EDTWRD('   ,   ,   , 0 . ')
     A  22        CRTOT2        13  0     98EDTWRD('  ,   ,   , 0 .  ')
     A  23        CRTOT3        13  0     98EDTWRD(' ,   ,   , 0 .   ')
      *
     A          R LAST                      TEXT('LAST LINE')
     A                                      SPACEB(3)
     A N31                                51'*** END OF REPORT ***'
     A*
     A  31                                46'*** END OF REPORT FOR BRANCH ***'
      *
      ** Database error reporting line
     A          R ERRLINE                   TEXT('ERROR PRINT LINE')
     A                                      SPACEB(2)
     A                                     7'DATABASE ERROR IN '
     A                                    26'FILE '
     A            DBFILE         8        31
     A                                    40'KEY '
     A            DBKEY         29        44
     A                                    70'IN '
     A            DBPGM         10        73
     A                                    84'AT '
     A            DBASE          3  0     87
