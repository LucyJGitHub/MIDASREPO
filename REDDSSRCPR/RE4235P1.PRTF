      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD56J                                                    *
/*EXI *  TEXT('Midas RE Teller cross-in cross-out report')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  RE4235P1 - Teller Cross-In / Cross-Out Trailer               *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
     A*  Last Amend No. 200412             Date 27Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
     A*  Prev Amend No. 143082             DATE 04OCT98               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*                 093527             DATE 21SEP95               *
     A*                 CRT001  *CREATE    DATE 28FEB95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  143082 - The teller cross in ID relation should be reversed .*
     A*  093527 - Add recipient teller id to the report.              *
     A*  CRT001 - Retail Teller System.                               *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A*
     A*----------------------------------------------------------------
     A**  Report Header Record Format
     A*----------------------------------------------------------------
     A*
     A          R RE4235H1                  TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A*
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE4235P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A*
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*
     A                                    43'AUDIT TRAIL OF TELLER CRO+
     A                                       SS-IN CROSS-OUT'
     A                                      SPACEA(1)
     A                                    43'-------------------------+
     A                                       ---------------'
     A*
     A  35                                89'BRANCH -'
     A  35        A8BRCD    R             98REFFLD(BRCA)
     A  35        A8BRNM    R            102REFFLD(BRNM)
     A*
     A                                   132' '
     A                                      SPACEA(2)
     A*
     A*----------------------------------------------------------------
     A**  Report Sub-Heading
     A*----------------------------------------------------------------
     A*
     A          R RE4235H2
     A                                      SPACEB(1)
     A                                    26'CROSS - OUT '
     A                                    93'CROSS - IN '
     A                                      SPACEA(1)
     A                                     6'------------------------+
     A                                       ------------------------+
     A                                       ------'
     A                                    74'------------------------+
     A                                       ------------------------+
     A                                       ------'
     A                                      SPACEA(1)
     A*                                              Begin /093527/
     A**********                           6'TELLER ID'
     A**********                          19'CURRENCY'
     A                                     8'FROM'
     A                                    17'TO'
     A                                    22'CURRENCY'
     A*                                              End   /093527/
     A                                    37'AMOUNT'
     A                                    52'TR. TIME'
     A*                                              Begin /093527/
     A**********                          74'TELLER ID'
     A**********                          86'CURRENCY'
     A*                                              Begin /143082/
     A**********                          76'FROM'
     A**********                          85'TO'
     A                                    76'TO'
     A                                    85'FROM'
     A*                                              End   /143082/
     A                                    90'CURRENCY'
     A*                                              End   /093527/
     A                                   105'AMOUNT'
     A                                   120'TR. TIME'
     A*                                              Begin /093527/
     A                                      SPACEA(1)
     A                                     6'TELLER'
     A                                    15'TELLER'
     A                                    74'TELLER'
     A                                    83'TELLER'
     A*                                              End   /093527/
     A                                      SPACEA(2)
     A*
     A*----------------------------------------------------------------
     A**  Report Details
     A*----------------------------------------------------------------
     A*
     A          R RE4235D1
     A            OTBIDO         3A        9TEXT('TELLER ID OUT')
     A*                                              Begin /093527/
     A            ORTLIO         3A       17TEXT('RELATED TELLER')
     A**********  OCCYO          3A       21TEXT('CURRENCY OUT')
     A            OCCYO          3A       24TEXT('CURRENCY OUT')
     A*                                              End   /093527/
     A            OPAMTO        17A       31TEXT('AMOUNT OUT')
     A  21        OTIMEO         6  0     52TEXT('TRANS. TIME')
     A                                      EDTWRD('0 :  :  ')
     A            OTBIDI         3A       77TEXT('TELLER ID OUT')
     A*                                              Begin /093527/
     A            ORTLII         3A       85TEXT('RELATED TELLER')
     A**********  OCCYI          3A       88TEXT('CURRENCY OUT')
     A            OCCYI          3A       92TEXT('CURRENCY OUT')
     A*                                              End   /093527/
     A            OPAMTI        17A       99TEXT('AMOUNT OUT')
     A  22        OTIMEI         6  0    120TEXT('TRANS. TIME')
     A                                      EDTWRD('0 :  :  ')
     A                                   130' '
     A                                      SPACEA(2)
     A*
     A*----------------------------------------------------------------
     A**  Report Trailer
     A*----------------------------------------------------------------
     A*
     A          R RE4235L1
     A                                      SPACEB(3)
     A  35                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A N35                                48'***  END OF REPORT  ***'
     A*
     A*----------------------------------------------------------------
     A**  Database Error message
     A*----------------------------------------------------------------
     A*
     A          R RE4235E2                  SPACEB(3)
     A                                    45'*** REFERENCE RE4235'
     A                                    66'DATABASE ERROR ***'
     A*
     A*----------------------------------------------------------------
     A**  Missing Transaction error details
     A*----------------------------------------------------------------
     A*
     A          R RE4235E3
     A                                      SPACEB(2)
     A                                    30'*********************************-
     A                                      ******************************'
     A                                      SPACEB(1)
     A                                    45'MISSING TRANSACTION ERROR DETAILS'
     A                                      SPACEB(1)
     A            ERROUT         3  0     31EDTCDE(J) SPACEB(1)
     A                                    35'TELLER IN TRANSACTIONS WITHOUT -
     A                                      TELLER OUT TRANSACTIONS'
     A            ERRIN          3  0     31EDTCDE(J) SPACEB(1)
     A                                    35'TELLER OUT TRANSACTIONS WITHOUT -
     A                                      TELLER IN TRANSACTIONS'
     A*                                              Begin /200412/
     A  34        COUNT          3  0     31EDTCDE(J) SPACEB(1)                  /200412/
     A  34                                35'MISSING TELLER IN/OUTS MAY -        /200412/
     A                                      BALANCE, CHECK DETAILS'              /200412/
     A*                                              End   /200412/
     A                                    30'*********************************-
     A                                      ******************************'
     A                                      SPACEB(1)
