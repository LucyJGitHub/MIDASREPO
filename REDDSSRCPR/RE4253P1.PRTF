      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Bureau de Change Exchange Rate Table')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                    *
     A*                                                               *
     A*  PRTF/RE4253P1 - Bureau de Change Exchange Rate Table Print   *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     A*  LAST AMEND NO. CRT001  *CREATE    DATE 28FEB95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
     A*  CRT001 - Retail Teller System.                               *
     A*                                                               *
     A*****************************************************************
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
      *****************************************************************
      *
     A                                      REF(MFRFL)
      *
     A          R #HEAD                     TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE4253P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A            TIME           6  0    106
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(2)
     A                                    55'B U R E A U   D E -
     A                                        C H A N G E'
     A                                      SPACEA(1)
     A                                    55'------------------'
     A                                    73'-------------'
     A                                      SPACEA(1)
     A                                    44'        E X C H A N -
     A                                      G E   R A T E   T A B-
     A                                       L E  '
     A                                      SPACEA(1)
     A                                    52'-----------'
     A                                    63'---------------------'
     A                                    84'-----'
     A                                      SPACEA(3)
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------'
     A                                      SPACEA(1)
     A                                     1'¦'
     A                                    29'¦'
     A                                    44'C H E Q U E'
     A                                    72'¦'
     A                                    91'C A S H'
     A                                   118'¦'
     A                                      SPACEA(1)
     A                                     1'¦'
     A                                     4'Currency'
     A                                    17'Amount'
     A                                    29'¦'
     A                                    34'Buy'
     A                                    48'Sell'
     A                                    62'Average'
     A                                    72'¦'
     A                                    78'Buy'
     A                                    93'Sell'
     A                                   107'Average'
     A                                   118'¦'
     A                                      SPACEA(1)
     A                                     1'¦'
     A                                    29'¦'
     A                                    34'(DN)'
     A                                    48'(DP)'
     A                                    63'(DS)'
     A                                    72'¦'
     A                                    78'(VN)'
     A                                    93'(VP)'
     A                                   108'(VS)'
     A                                   118'¦'
     A                                      SPACEA(1)
     A                                     1'¦'
     A                                     1' ----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -------------'
     A*                                   72'¦'
     A                                   118'¦'
     A                                      SPACEA(1)
      ****************************************************************
      *
     A          R #TABLE
     A
     A                                     1'¦'
     A            FECYCD         3A        7TEXT('Currency Code')
     A            FEAMNT         9  0     14TEXT('Amount')
     A                                      EDTCDE(L)
     A                                    29'¦'
     A            FEDNRT         8  3     30TEXT('FX buying rate')
     A                                      EDTCDE(J)
     A            FEDPRT         8  3     43TEXT('FX selling rate')
     A                                      EDTCDE(J)
     A            FEDSRT         8  3     58TEXT('FX average rate')
     A                                      EDTCDE(J)
     A                                    72'¦'
     A            FEVNRT         8  3     73TEXT('FX cash buying rate')
     A                                      EDTCDE(J)
     A            FEVPRT         8  3     88TEXT('FX cash selling rate')
     A                                      EDTCDE(J)
     A            FEVSRT         8  3    103TEXT('FX cash average rate')
     A                                      EDTCDE(J)
     A                                   118'¦'
     A                                      SPACEA(1)
     A          R #TRAIL                    TEXT('Trailer')
     A                                     1'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      --------------'
     A                                      SPACEA(1)
     A*
     A          R ENDOFR                    TEXT('END OF REPORT')
     A                                    51'*** END OF REPORT ***'
     A                                      SPACEB(3)
