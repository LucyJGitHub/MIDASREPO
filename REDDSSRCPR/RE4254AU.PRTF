      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_AU60                                                      *
/*EXI *  TEXT('Midas RE Bureau de Change Exchange Rate Upload')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                    *
     A*                                                               *
     A*  PRTF/RE4254P1 - Bureau de Change Exchange Rate Uploading     *
     A*                  Report                                       *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT001  *CREATE    Date 28Feb95               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CRT001 - Retail Teller System.                               *
     A*                                                               *
     A*****************************************************************
      *
     A                                      REF(MFRFL)
     A          R #HEAD                     TEXT('(c) Finastra +
     A                                      International Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE4254AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A            TIME           6  0    106
     A                                      EDTWRD('0 :  :  ')
     A                                      SPACEA(2)
     A                                    59'BUREAU DE CHANGE'
     A                                      SPACEA(1)
     A                                    59'----------------'
     A                                      SPACEA(1)
     A                                    52'EXCHANGE RATE UPLOADING REPORT'
     A                                      SPACEA(1)
     A                                    52'------------------------------'
     A                                      SPACEA(2)
      *=========================================================================
     A          R #TITLE                    TEXT('Line titles')
     A                                     4'Currency'
     A                                   +41'R A T E S'
     A                                      SPACEA(1)
     A                                     6'code'
     A                                    17'Amount'
     A                                    26'Cash Buy'
     A                                    40'Cash Sell'
     A                                    53'Cash Average'
     A                                    69'Cheque Buy'
     A                                    83'Cheque Sell'
     A                                    99'Cheque Average'
     A                                      SPACEA(1)
     A                                     2'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -----------'
     A                                      SPACEA(1)
      *=========================================================================
     A          R #LINE                     TEXT('Ccy line')
     A            R#CYCD         3A  O     6
     A                                      TEXT('Currency code')
     A            R#AMNT         9S 0O    +5
     A                                      EDTCDE(Z)
     A                                      TEXT('Currency amount')
     A            R#VNRT         8S 3O    +3
     A                                      EDTCDE(4)
     A                                      TEXT('Cash buy rate')
     A            R#VPRT         8S 3O    +5
     A                                      EDTCDE(4)
     A                                      TEXT('Cash sell rate')
     A            R#VSRT         8S 3O    +8
     A                                      EDTCDE(4)
     A                                      TEXT('Cash average rate')
     A            R#DNRT         8S 3O    +5
     A                                      EDTCDE(4)
     A                                      TEXT('Cheque buy rate')
     A            R#DPRT         8S 3O    +5
     A                                      EDTCDE(4)
     A                                      TEXT('Cheque sell rate')
     A            R#DSRT         8S 3O    +8
     A                                      EDTCDE(4)
     A                                      TEXT('Cheque average rate')
     A                                      SPACEA(1)
      *=========================================================================
     A          R #INVDT
     A            R#CYCD         3A  O     6
     A                                      TEXT('Currency code')
     A                                    +5
     A                                      '- INPUT DATA INVALID'
     A                                      SPACEA(1)
      *=========================================================================
     A          R #NOTFD
     A            R#CYCD         3A  O     6
     A                                      TEXT('Currency code')
     A                                    +5
     A                                      '- INPUT DATA FOR THIS CURRENCY NOT-
     A                                       FOUND'
     A                                      SPACEA(1)
      *=========================================================================
     A          R #END
     A                                      TEXT('End of Report')
     A                                      SPACEB(1)
     A                                    55'***  END OF REPORT  ***'
      *=========================================================================
     A          R DBERROR
     A                                    25'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       30
     A                                    38''''
     A                                    64'*'
     A                                    11'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        30
     A                                    59 '''    *'
     A                                    11'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       30
     A                                    40''''
     A                                    64'*'
     A                                    11'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     30
     A                                    33''''
     A                                    64'*'
     A                                    11'*'
     A                                      SPACEB(1)
     A                                    64'*'
     A                                    11'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
      *=========================================================================
     A          R NODTLS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
