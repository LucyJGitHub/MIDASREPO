      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE CI Forwarding Exceptions Report - Detail')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE4300P1 - CI Forwarding Exceptions Report              *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CRT008  *CREATE    Date 22Oct02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRT008 - Forwarding Enhancements                             *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2002')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE4300P1'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'CASHIER FORWARDED +
     A                                      EXECPTIONS'
      *
     A N38                                75'- COB'
     A  38                                75'- INTERIM'
     A  37                                89'BRANCH -'
     A  37        A8BRCD    R             98REFFLD(BRCA)
     A  37        A8BRNM    R            102REFFLD(BRNM)
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'------------------------+
     A                                      ---------------'
     A                                      SPACEA(2)
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A          R DTLHDR                    TEXT('Detail Header')
     A                                      SPACEB(2)
     A                                    01'Branch'
     A                                    08'Tel'
     A                                    12'Tran'
     A                                    18'Description'
     A                                    50'Account No'
     A                                    62'Ccy'
     A                                    66'Exchange Rate'
     A                                    83'Tran Amount/'
     A                                   106'Charge'
     A                                   115'Value'
     A                                   122'Reversal/'
     A                                      SPACEA(1)
     A                                    01'Number'
     A                                    08'ID'
     A                                    12'Num'
     A                                    83'Equiv Amount'
     A                                   106'Amount'
     A                                   115'Date'
     A                                   122'Prft Ctr'
     A                                      SPACEA(2)
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
      *
     A            PBRCA          3         1TEXT('BRANCH')
     A            PTELD          3         8TEXT('TELLER ID')
     A            PTRNO          5  0     12TEXT('TRANS NO')
     A            PDESC         30        18TEXT('DESCRIPTION')
     A            PACNO         10A       50TEXT('ACCOUNT NUMBER')
     A            PCCY1          3        62TEXT('CCY')
     A            PEXRT         12A       66TEXT('EXCH RATE')
     A            PAMT1         16        79TEXT('AMOUNT')
     A            PCHAM         16        97TEXT('CHARGE AMOUNT')
     A            PVLDT          6       114TEXT('VALUE DATE')
     A            PCRTN          5       122TEXT('REVERSAL NO')
      *
     A                                      SPACEA(1)
      *
     A            PDSC2          2A       46TEXT('CR/DR')
     A            PACN2         10A       50TEXT('A/C NUMBER2')
     A            PCCY2          3        62TEXT('A/C CCY')
     A            PCVAM         16        79TEXT('EQUIV AMT')
     A            PCHA2         16        97TEXT('CHARGE AMT2')
     A            PPRFC          4       122TEXT('PROFIT CENTRE')
      *
     A                                      SPACEA(1)
     A  36                               101'** MIDAS GENERATED AUTO +
     A                                      REV **'
     A  39                               101'** TRANSACTION NOT +
     A                                      WRITTEN **'
     A  40                               101'** POSTED TO SUSPENSE +
     A                                      A/C  **'
     A                                     1'Message Details :'
     A            PHSMS         81A       19TEXT('Host Message Data')
     A                                      SPACEA(2)
      *
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46'***  END OF REPORT FOR +
     A                                      BRANCH  ***'
     A                                      SPACEB(3)
     A N37                                51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
