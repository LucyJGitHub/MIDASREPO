      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE CS Store and Forward Transaction Details')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE4403P1 - CS Store and Forward Transaction Details     *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Last Amend No. CRT003 *CREATE     Date 22Jul96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRT003 - Cashier Interface New File                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
      *
     A                                     4'REFERENCE RE4403P1'
     A            BJURPT    R             38REFFLD(URPT)
     A                                   102'DATE'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   120'PAGE'
     A                                   125PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   102'TIME'
     A                                   107TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    55'CS STORE AND FORWARD +
     A                                      TRANSACTION DETAILS'
     A                                      SPACEA(1)
     A                                    55'---------------------+
     A                                      -------------------'
     A                                      SPACEA(2)
      *
     A          R DTLHDR                    TEXT('Detail Header')
     A                                      SPACEB(2)
      *
     A                                     7'Comms'
     A                                      SPACEA(1)
     A                                     7'Message'
     A                                    20'Branch'
     A                                    34'Teller'
     A                                    51'Account'
     A                                    69'Transaction'
     A                                   115'Host Message'
     A                                      SPACEA(1)
     A                                     7'Seq No.'
     A                                    21'Code'
     A                                    32'Identifier'
     A                                    51'Number'
     A                                    71'Amount'
     A                                    88'Currency'
     A                                   102'SP Code'
     A                                   117'Severity'
     A                                      SPACEA(2)
      *
     A          R DETAIL                    TEXT('Detail Format')
      *
     A            PCMSQ          6A        7TEXT('Communications Sequence No.')
     A            PBRCA          3A       21TEXT('Branch Name')
     A            PTELD          3A       35TEXT('Teller Identifier')
     A            PACNO         13A       48TEXT('Account Number')
     A            PAMT1         15A       67TEXT('Transaction Amount')
     A            PCCY1          3A       90TEXT('Currency')
     A            PSPCD          5A      103TEXT('SP Code')
     A            PHMSS          1A      120TEXT('Host Message Severity')
     A                                      SPACEA(1)
      *
     A          R DETLS2                    TEXT('Message Details')
      *
     A                                     7'Message Details :'
     A            PHSMS         78A       32TEXT('Host Message Data')
     A                                      SPACEA(1)
      *
     A*         R SUBTOT                    TEXT('Trans Type Messages Subtotal')
     A*                                     SPACEB(1)
      *
     A*                                    7'Number of '
     A*           PITXTP        10A       17TEXT('Transaction Type')
     A*                                   28'Transaction Type Messages '
     A*                                   54'Received : '
     A*           PISUBT         5  0     65TEXT('Subtotal')
     A*                                     EDTCDE(3)
      *
     A          R TRAILP1                   TEXT('End of Report')
     A                                      SPACEB(1)
      *
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
