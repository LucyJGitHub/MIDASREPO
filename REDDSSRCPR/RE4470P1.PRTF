      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE CS Transaction Log Details Report')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE4470P1 - CI Transaction Log Report                    *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT003 *CREATE     Date 22Jul96               *
      *                 000000             Date ddmmmyy               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRT003 - Cashier Interface New File                          *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *=========================================================================
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
      *.........................................................................
     A                                     4'REFERENCE RE4470P1'
     A            BJURPT    R             38REFFLD(URPT)
     A                                   102'DATE'
     A            BJMRDT    R            107REFFLD(MRDT)
     A                                   120'PAGE'
     A                                   125PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   102'TIME'
     A                                   107TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    55'CI TRANSACTION LOG REPORT '
     A                                      SPACEA(1)
     A                                    55'------------------------- '
     A                                      SPACEA(2)
      *=========================================================================
     A          R SUBHDR                    TEXT('Transaction Type')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     7'For CI Transaction Type      '
     A            PITXTP        10A       36TEXT('Transaction Type')
     A            PITXDS        30A       51TEXT('CI Transaction Type  Desc')
      *                                     SPACEA(1)
      *=========================================================================
     A          R DTLHDR                    TEXT('Details Subheader')
     A                                      SPACEB(2)
      *.........................................................................
     A                                     7'Comms'
     A                                      SPACEA(1)
     A                                     7'Message  '
     A                                    16'Branch    '
     A                                    26'Device         '
     A                                    41'Teller         '
     A                                    56'Date         '
     A                                    69'Time        '
     A                                    81'Successful     '
     A                                    96'Completion               '
     A                                   121'Status'
     A                                      SPACEA(1)
     A                                     7'Seq No.  '
     A                                    16'Code      '
     A                                    26'Name           '
     A                                    41'Identifier     '
     A                                    56'Received     '
     A                                    69'Received    '
     A                                    81'Completion     '
     A                                    96'Time           '
     A                                   111'Status    '
     A                                   121'Time'
     A                                      SPACEA(2)
      *========================================================================
     A          R DETAIL                    TEXT('Transaction Log Details')
      *........................................................................
     A            PICMSQ         6A        7TEXT('Communications Sequence No.')
     A            PIBRCA         3A       16TEXT('Branch Name')
     A            PIRMDN        10A       26TEXT('Remote Device Name')
     A            PITELD        10A       41TEXT('Teller Identifier')
     A            PIRCDT         7A  O    56TEXT('Received Date Stamp')
     A            PIRCTM         8A  O    69TEXT('Received Time Stamp')
     A            PISUCM         1A       85TEXT('Successful Completion')
     A            PITMPS         8A  O    96TEXT('Time Posted')
     A            PISTAT         2A      113TEXT('Status')
     A            PISTTM         8A  O   121TEXT('Status Time Stamp')
     A                                      SPACEA(1)
      *========================================================================
     A          R DETLS2                    TEXT('Message Details')
     A                                      SPACEB(1)
      *........................................................................
     A                                     7'Message Details :'
     A            PICMS1       100A       17TEXT('Comms Message Data')
     A                                      SPACEB(1)
     A            PICMS2       100A       17TEXT('Comms Message Data')
     A                                      SPACEB(1)
     A            PICMS3       100A       17TEXT('Comms Message Data')
     A                                      SPACEB(1)
     A            PICMS4       100A       17TEXT('Comms Message Data')
     A                                      SPACEB(1)
     A            PICMS5       100A       17TEXT('Comms Message Data')
     A                                      SPACEB(1)
     A            PICMS6       100A       17TEXT('Comms Message Data')
     A                                      SPACEB(1)
     A            PICMS7       100A       17TEXT('Comms Message Data')
     A                                      SPACEB(1)
     A            PICMS8       100A       17TEXT('Comms Message Data')
     A                                      SPACEB(1)
     A            PICMS9       100A       17TEXT('Comms Message Data')
     A                                      SPACEB(1)
      *========================================================================
     A          R SUBTOT                    TEXT('Trans Type Messages Subtotal')
     A                                      SPACEB(1)
      *........................................................................
     A                                     7'Number of '
     A            PITXTP        10A       17TEXT('Transaction Type')
     A                                    28'Transaction Type Messages '
     A                                    54'Received : '
     A            PISUBT         5  0     65TEXT('Subtotal')
     A                                      EDTCDE(3)
      *========================================================================
     A          R TRAILP1                   TEXT('End of Report')
     A                                      SPACEB(1)
      *........................................................................
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
