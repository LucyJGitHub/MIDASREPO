      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE CI Transaction Analysis Report')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE4475P1 - Transaction Analysis Report                  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT003 *CREATE     Date 22Jul96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRT003 - Branch Automation Interface.                        *
      *           New Report Layout.                                  *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADP1
     A                                      TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(001)
     A                                     2
     A                                      'Reference RE4475P1'
     A                                      SPACEB(002)
     A            PIURPT    R             37
     A                                      REFFLD(URPT)
     A                                   101
     A                                      'DATE'
     A            PIMRDT    R            106
     A                                      REFFLD(MRDT)
     A                                   119
     A                                      'PAGE'
     A                                   124
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                   101
     A                                      'TIME'
     A                                      SPACEB(001)
     A                                   106
     A                                      TIME
     A                                      EDTWRD('  :  :  ')
     A                                    51
     A                                      'CI TRANSACTION ANALYSIS REPORT'
     A                                      SPACEB(001)
     A  37                                89
     A                                      'BRANCH -'
     A  37        PIBRCA    R             98
     A                                      REFFLD(BRCA)
     A  37        PIBRNM    R            102
     A                                      REFFLD(BRNM)
     A                                    51
     A                                      '------------------------------'
     A                                      SPACEB(001)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A          R SUBHDR1
     A                                      SPACEB(001)
     A                                     5
     A                                      'For Teller Id.'
     A                                      SPACEB(001)
     A            PITELD         3A  O    21
     A            PITLNM        30A  O    30
      *
     A          R SUBHDR2
     A                                      SPACEB(001)
     A                                     5
     A                                      'For BAI Transaction Type'
     A                                      SPACEB(001)
     A            PITXTP        10A  O    30
     A            PITXDS        30A  O    45
      *
      ***  Detail Sub-Headings:
      ***  --------------------
      *
     A          R DTLSUBHDR
     A                                      SPACEB(001)
     A                                     5
     A                                      'Comms'
     A                                      SPACEB(001)
     A                                     5
     A                                      'Message'
     A                                      SPACEB(001)
     A                                    15
     A                                      'Branch'
     A                                    24
     A                                      'Device'
     A                                    37
     A                                      'Teller'
     A                                    50
     A                                      'Transaction'
     A                                    64
     A                                      'Date'
     A                                    75
     A                                      'Time'
     A                                    86
     A                                      'Successful'
     A                                    99
     A                                      'Completion'
     A                                   121
     A                                      'Status'
     A                                     5
     A                                      'Seq No'
     A                                      SPACEB(001)
     A                                    15
     A                                      'Code'
     A                                    24
     A                                      'Name'
     A                                    37
     A                                      'Identifier'
     A                                    50
     A                                      'Type'
     A                                    64
     A                                      'Received'
     A                                    75
     A                                      'Received'
     A                                    86
     A                                      'Completion'
     A                                    99
     A                                      'Time'
     A                                   112
     A                                      'Status'
     A                                   121
     A                                      'Time'
      *
      *-------------------------*
      ***  Detail Record Format *
      *-------------------------*
      *
     A          R DETAIL
     A                                      SPACEB(001)
     A            PICMSQ         6A  O     5
     A                                      SPACEB(001)
     A            PIBRCA         3A  O    15
     A            PIRMDN        10A  O    24
     A            PITELD         3A  O    37
     A            PITXTP        10A  O    50
     A            PIRCDT         7A  O    64
     A            PIRCTM         8A  O    75
     A            PISUCM         1A  O    90
     A            PITMPS         8A  O    99
     A            PISTAT         2A  O   114
     A            PISTTM         8A  O   121
      *
     A          R DETLS2
     A                                      SPACEB(001)
     A                                     5
     A                                      'Message Details :'
     A                                      SPACEB(001)
     A            PMSG1        100A  O    16
     A                                      SPACEB(001)
     A            PMSG2        100A  O    16
     A                                      SPACEB(001)
     A            PMSG3        100A  O    16
     A                                      SPACEB(001)
     A            PMSG4        100A  O    16
     A                                      SPACEB(001)
     A            PMSG5        100A  O    16
     A                                      SPACEB(001)
     A            PMSG6        100A  O    16
     A                                      SPACEB(001)
     A            PMSG7        100A  O    16
     A                                      SPACEB(001)
     A            PMSG8        100A  O    16
     A                                      SPACEB(001)
     A            PMSG9        100A  O    16
     A                                      SPACEB(001)
      *
     A          R SUBTOT1
     A                                      SPACEB(001)
     A                                     5
     A                                      'Number Of Messages Received from T-
     A                                      eller Id.'
     A                                      SPACEB(001)
     A            PIXTEL         3A  O    50
     A                                    65
     A                                      ':'
     A            PTELST         5S 0O    70
     A                                      EDTCDE(4)
      *
     A          R SUBTOT2
     A                                      SPACEB(001)
     A                                     5
     A                                      'Number Of'
     A                                      SPACEB(001)
     A            PIXTXT        10A  O    15
     A                                    26
     A                                      'Transaction Type Messages Received'
     A                                    65
     A                                      ':'
     A            PTRNST         5S 0O    70
     A                                      EDTCDE(4)
      *
     A          R SUBTOT3
     A                                      SPACEB(001)
     A                                     5
     A                                      'Number Of Messages Received from B-
     A                                      ranch'
     A                                      SPACEB(001)
     A            PIXBRC         3A  O    46
     A                                    65
     A                                      ':'
     A            PBRCST         5S 0O    70
     A                                      EDTCDE(4)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILP1
     A  37                                46
     A                                      '***  END OF REPORT FOR BRANCH  ***'
     A                                      SPACEB(003)
     A                                    51
     A                                      '***  END OF REPORT  ***'
     A                                      SPACEB(003)
