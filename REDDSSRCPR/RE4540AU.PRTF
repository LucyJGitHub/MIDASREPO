      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Cashier TCP/IP socket debugging audit')
      *****************************************************************
      *                                                               *
      *  Midas - Cashier Interface Module                             *
      *                                                               *
      *  PRTF/RE4540AU - Midas Cashier TCP/IP Socket Debugging Audit  *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.04 -----------------------------------------------*
      *  Prev Amend No. CRT004             Date 12May00               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CRT004 - Cashier Midas TCP/IP interface.                     *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      *--------------------------------*
      ***  Report Header Record Format *
      *--------------------------------*
      *
     A          R HEADPY                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2000')
      *
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE4540AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
      *
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
      *
     A                                    46'Midas Cashier TCP/IP Soc+
     A                                      kets Debugging functions'
      *
     A                                   132' '
     A                                      SPACEA(1)
     A                                    46'------------------------+
     A                                      ------------------------'
     A                                      SPACEA(2)
      *
      ***  Sub-Heading:
      ***  ------------
      *
     A                                     5'Job References'
     A                                    38'TCP Functions and details'
     A                                      SPACEA(1)
     A*
     A                                     5'--------------'
     A                                    38'-------------------------'
     A                                      SPACEA(2)
     A                                     5'*...+....1'
     A                                    15'....+....2'
     A                                    25'....+....3'
     A                                    35'....+....4'
     A                                    45'....+....5'
     A                                    55'....+....6'
     A                                    65'....+....7'
     A                                    75'....+....8'
     A                                    85'....+....9'
     A                                    95'....+....0'
     A                                      SPACEA(1)
      *
      *---------------------------*
      ***  Detail Record Format 1 *
      *---------------------------*
      *
     A          R DETAIL1
     A            XRJNM         10         5
                                          15'/'
     A            XRJUS         10        16
                                          26'/'
     A            XRJNB         10        27
     A            XRTCPF        10        38
     A            XRDAT          8        50
     A            XRTIM          6        60
     A                                      SPACEA(1)
      *
      *---------------------------*
      ***  Detail Record Format 2 *
      *---------------------------*
      *
     A          R DETAIL2
     A            XRTCPD       100         5
     A                                      SPACEA(1)
      *
      *-------------------*
      ***  Report Trailer *
      *-------------------*
      *
     A          R TRAILPY
     A                                    51'***  END OF REPORT  ***'
     A                                      SPACEB(3)
      *
      ***  Example /COPY.
     A*/COPY WNCPYSRC,RE4540AU01
      *
