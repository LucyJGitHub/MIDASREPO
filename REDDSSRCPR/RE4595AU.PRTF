      *****************************************************************
/*STD *  PRTFAU                                                       *
/*EXI *  TEXT('Midas RE Re-Set Branch status - Audit')
     A*****************************************************************
     A*                                                               *
     A*  Midas - Retail Module                                        *
     A*                                                               *
     A*  PRTF/RE4595AU - CI Re-Set Branch status - Audit              *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4.01 -------------------------------------------*
      *  Prev Amend No. CRT007  *CREATE    Date 14Jan02               *
     A*                                                               *
     A*---------------------------------------------------------------*
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
     A*  CRT007 - Update Branch Status after Comms Failure.           *
     A*                                                               *
     A*****************************************************************
     A*
     A                                      REF(MFRFL)
     A*
     A*----------------------------------------------------------------
     A**  Report Header Record Format
     A*----------------------------------------------------------------
     A*
     A          R HEADAU                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A*
     A                                      SKIPB(3)
     A                                     6'REFERENCE RE4595AU'
     A            BJURPT    R             37REFFLD(URPT)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(MRDT)
     A                                   119'PAGE'
     A                                   124PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(1)
     A*
     A                                   101'TIME'
     A                                   106TIME
     A                                      EDTWRD('  :  :  ')
     A                                      SPACEA(1)
     A*
     A                                    43'CI RE-SET BRANCH +
     A                                      STATUS - AUDIT'
     A                                      SPACEA(1)
     A                                    43'----------------------+
     A                                      ----------------------'
     A                                      SPACEA(3)
     A*
     A*----------------------------------------------------------------
     A**  Database Error
     A*----------------------------------------------------------------
     A*
     A          R DBERROR
     A                                    54'**** DATABASE ERROR ****'
     A                                      SPACEB(2)
     A                                    39'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    39'*'
     A                                      SPACEB(1)
     A                                    92'*'
     A                                    39'*    FILE NAME    '''
     A                                      SPACEB(1)
     A            DBFILE         8A       58
     A                                    66''''
     A                                    92'*'
     A                                    39'*    KEY          '''
     A                                      SPACEB(1)
     A            DBKEY         29        58
     A                                    87 '''    *'
     A                                    39'*    PROGRAM NAME '''
     A                                      SPACEB(1)
     A            DBPGM         10A       58
     A                                    68''''
     A                                    92'*'
     A                                    39'*    AT           '''
     A                                      SPACEB(1)
     A            DBASE          3  0     58
     A                                    61''''
     A                                    92'*'
     A                                    39'*'
     A                                      SPACEB(1)
     A                                    92'*'
     A                                    39'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
     A*----------------------------------------------------------------
     A**  Connection Lost
     A*----------------------------------------------------------------
     A*
     A          R CONNECT
     A                                    54'**** CONNECTION LOST ****'
     A                                      SPACEB(2)
     A                                    39'***************************+
     A                                      ***************************'
     A                                      SPACEB(2)
     A                                    39'*'
     A                                      SPACEB(1)
     A                                    92'*'
     A                                    39'*    BRANCH CODE'
     A                                      SPACEB(1)
     A            WBRCA          3A       64
     A                                    92'*'
     A                                    39'*    MIDAS RUNDATE'
     A                                      SPACEB(1)
     A            BJMRDT    R             64REFFLD(MRDT)
     A                                    92'*'
     A                                    39'*    SYSTEM TIME'
     A                                      SPACEB(1)
     A                                    64TIME
     A                                    92'*'
     A                                    39'*    SYSTEM DATE'
     A                                      SPACEB(1)
     A                                    64DATE
     A                                    92'*'
     A                                    39'*    BRANCH IP ADDRESS'
     A                                      SPACEB(1)
     A            WRMTAD        17A       64
     A                                    92'*'
     A                                    39'***************************+
     A                                      ***************************'
     A                                      SPACEB(1)
