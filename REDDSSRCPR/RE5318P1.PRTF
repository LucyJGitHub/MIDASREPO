      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas A/C not subject to fees report')
     A*****************************************************************
     A*                                                               *
     A*  Midas Retail Module
     A*                                                               *
     A*  PRTF/RE5318P1 - ACCOUNTS NOT SUBJECT TO FEES REPORT          *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01413 *CREATE     Date 13Apr93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  S01413 - Retail 3 Incorporation.                             *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
     A*  C R E A T I O N     P A R A M E T E R S                      *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A**
     A**   *******
     A**   HEADER
     A**   *******
     A**
     A          R HEADLI                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'RE5318P1'
     A            BJURPT    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE      R            124
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A*
     A                                    37'ACCOUNTS NOT SUBJECT TO FEE+
     A                                      S REPORT'
     A                                      SPACEA(1)
     A                                    37'-----------------------+
     A                                      ------------'
     A                                      SPACEA(2)
     A*
     A          R HEADPN
     A                                     2'BRANCH'
     A            BRCA      R              9
     A            A8BRNM    R             13REFFLD(BRNM)
     A                                      SPACEA(2)
     A*
     A                                     2'BRANCH'
     A                                    16'CUSTOMER'
     A                                    27'CURRENCY'
     A                                    38'ACCOUNT'
     A                                    48'ACCOUNT'
     A                                    79'STATEMENT'
     A                                    91'NEXT STATEMENT'
     A                                      SPACEA(1)
     A*
     A                                     2'CODE'
     A                                    16'NUMBER'
     A                                    27'CODE'
     A                                    38'CODE'
     A                                    48'SEQ.'
     A                                    65'ACCOUNT NAME'
     A                                    79'FREQUENCY'
     A                                    91'DATE'
     A                                   114'LEDGER BALANCE'
     A                                      SPACEA(1)
     A*
     A                                     2'-----'
     A                                    16'--------'
     A                                    27'--------'
     A                                    38'-------'
     A                                    48'-------'
     A                                    65'------------'
     A                                    79'---------'
     A                                    91'--------------'
     A                                   114'--------------'
     A                                      SPACEA(1)
     A**
     A**   *******
     A**   DETAIL
     A**   *******
     A**
     A          R DETAIL
     A            BRCA      R              6
      * Begin /CSD027/
     A**********  CNUM      R             17                                                  CSD027
     A            CNUM           6A       17TEXT('Customer number')
      * End /CSD027/
     A            CCY       R             29
     A**Start of change CGL029
     A**********  ACOD      R             40                                                  CGL029
     A            ACOD      R             37
     A**End of change CGL029
     A            ACSQ      R             50
     A            ANAM      R             59
     A            STFQ      R             83
     A            PPNSTD         7        95
     A            PPLDBL        15       110
     A                                      SPACEA(1)
     A**
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A          R TRAILP1
     A                                      SPACEB(3)
     A                                    48'*** END OF REPORT FOR BRANCH'
     A            BRCA#     R             +1REFFLD(BRCA)
     A                                    +1'***'
     A**
     A**   **********
     A**   NO DETAILS
     A**   **********
     A**
     A          R NORECS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A**
     A**   **********************
     A**   REPORT DATABASE ERROR
     A**   **********************
     A**
     A          R ERRP1
     A                                    56'**** DATABASE ERROR ****'
     A                                      SPACEB(3)
