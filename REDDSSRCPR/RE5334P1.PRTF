      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas A/C not subject to charge report')
     A*****************************************************************
     A*                                                               *
     A*  Midas  Retail Module
     A*                                                               *
     A*  PRT/RE5334P1 - ACCOUNTS NOT SUBJECT TO FIXED CHARGES REPORT  *
     A*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     A*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. 239284             Date 13Apr06               *
      *  Prev Amend No. CSD027             Date 30Nov05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01413 *Create     Date 13Apr93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  239284 - Applied fix 238920.                                 *
      *  238920 - Make sure correct branch is output to report trail. *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  S01413 - Retail 3 Incorporation                              *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A*
     A**---------------------------------------------------------------
     A**   FORMAT FOR DOCUMENT HEADER
     A**---------------------------------------------------------------
     A*
     A          R HEADLI                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE RE5334P1'
     A            BJURPT    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A            PAGE           4  0    124EDTCDE(Z)
     A                                      SPACEA(2)
     A*
     A                                    46'ACCOUNTS NOT SUBJECT TO FIXED -
     A                                      CHARGE REPORT'
     A                                      SPACEA(1)
     A                                    46'------------------------------+
     A                                      -------------'
     A          R BRCHNM
     A  88                                 5'BRANCH'
     A  88                                  SPACEB(2)
     A  88        BRCA      R             12
     A  88                                16'-'
     A  88        BRNM      R             18
     A                                     5'BRANCH'
     A                                      SPACEB(2)
     A                                    15'CUSTOMER'
     A                                    27'CURRENCY'
     A                                    39'ACCOUNT'
     A                                    50'ACCOUNT'
     A                                     6'CODE'
     A                                      SPACEB(1)
     A                                    16'NUMBER'
     A                                    29'CODE'
     A                                    40'CODE'
     A                                    50'SEQUENCE'
     A                                    63'ACCOUNT NAME'
     A                                    89'LEDGER BALANCE'
     A                                     5'------'
     A                                      SPACEB(1)
     A                                    15'--------'
     A                                    27'--------'
     A                                    39'-------'
     A                                    50'--------'
     A                                    63'------------'
     A                                    89'--------------'
     A                                      SPACEA(1)
     A          R DETAIL
     A            BRCA      R              6
     A                                      SPACEB(1)
      * Begin /CSD027/
     A**********  CNUM      R             16                                                  CSD027
     A            CNUM           6A       16TEXT('Customer number')
      * End /CSD027/
     A            CCY       R             29
     A**Start of change CGL029
     A**********  ACOD      R             40                                                  CGL029
     A            ACOD      R             38
     A**End of change CGL029
     A            ACSQ      R             53
     A            ANAM      R             61
     A            PPLDBL        21        84
     A
     A**
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A          R TRAILAU
     A N88                                56'*** END OF REPORT ***'
     A N88                                  SPACEB(3)
     A  88                                50'*** END OF REPORT BRANCH '
     A  88                                  SPACEB(3)
     A**Start of change 238920            75
     A**88******  BRCA      R             75                                                  238920
     A  88        TBRCA          3        75
     A**End of change 238920
     A  88                                79' ***'
     A**
     A**   **********
     A**   NO RECORDS
     A**   **********
     A**
     A          R NONE
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
