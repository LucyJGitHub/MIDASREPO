      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas A/C with individual charges report')
     A*****************************************************************
     A*                                                               *
     A*  Midas Retail Module
     A*                                                               *
     A*  PRTF/RE5336P1- ACCOUNTS WITH INDIVIDUAL FIXED CHARGES REPORT *
     A*                                                               *
      *  (c) Finastra International Limited 2001                      *
     A*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 S01413 *CREATE     Date 13Apr93               *
     A*                                                               *
     A*****************************************************************
     A*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     A*  S01413 - Retail 3 Incorporation.                             *
     A*                                                               *
     A*****************************************************************
     A                                      REF(MFRFL)
     A*
     A**   *******
     A**   HEADER
     A**   *******
     A*
     A          R HEADLI                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'RE5336P1'
     A            BJURPT    R             37REFFLD(TITL)
     A                                   101'DATE'
     A            BJMRDT    R            106REFFLD(RUNA)
     A                                   119'PAGE'
     A            PAGE      R            124
     A                                      EDTCDE(Z)
     A                                      SPACEA(2)
     A                                    45'ACCOUNTS WITH INDIVIDUAL F+
     A                                      IXED CHARGES REPORT'
     A                                      SPACEA(1)
     A                                    45'--------------------------+
     A                                      -------------------'
     A                                      SPACEA(2)
     A*
     A          R HEADPN
     A                                     5'BRANCH'
     A            BRCA      R             12
     A            A8BRNM    R             16REFFLD(BRNM)
     A                                      SPACEA(2)
     A*
     A                                     5'BRANCH'
     A                                    15'CUSTOMER'
     A                                    27'CURRENCY'
     A                                    39'ACCOUNT'
     A                                    50'ACCOUNT'
     A                                   109'A/C CHARGE'
     A                                      SPACEA(1)
     A                                     6'CODE'
     A                                    16'NUMBER'
     A                                    29'CODE'
     A                                    40'CODE'
     A                                    50'SEQUENCE'
     A                                    63'ACCOUNT NAME'
     A                                    89'LEDGER BALANCE'
     A                                   111'AMOUNT'
     A                                      SPACEA(1)
     A                                     5'------'
     A                                    15'--------'
     A                                    27'--------'
     A                                    39'-------'
     A                                    50'--------'
     A                                    63'------------'
     A                                    89'--------------'
     A                                   109'----------'
     A                                      SPACEA(1)
     A          R DETAIL                    SPACEB(1)
     A            BRCA      R              6
      * Begin /CSD027/
     A**********  CNUM      R             16                                                  CSD027
     A            CNUM           6A       16TEXT('Customer number')
      * End /CSD027/
     A            CCY       R             29
     A**Start of change CGL029
     A**********  ACOD      R             40                                                  CGL029
     A            ACOD      R             37
     A**End of change CGL029
     A            ACSQ      R             53
     A            ANAM      R             61
     A            PPLDBL        21        84
     A            PPCHAM        10       109
     A
     A**
     A**   *******************
     A**   REPORT TRAILER
     A**   *******************
     A**
     A          R TRAILP1
     A                                      SPACEB(3)
     A                                    48'*** END OF REPORT FOR BRANCH'
     A            BRCA#     R             +1REFFLD(BRCA)
     A                                    +1'***'
     A**
     A**   **********
     A**   NO DETAILS
     A**   **********
     A**
     A          R NORECS
     A                                    52'*** NO DETAILS TO REPORT ***'
     A                                      SPACEB(3)
     A**
     A**   **********************
     A**   REPORT DATABASE ERROR
     A**   **********************
     A**
     A          R ERRP1
     A                                    56'**** DATABASE ERROR ****'
     A                                      SPACEB(3)
