      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Retail History Update Report')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE5400P1 - RETAIL HISTORY UPDATE REPORT                 *
      *                                                               *
      *  (c) Finastra International Limited 2001                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 30Nov05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CCB003 *Create     Date 09Oct96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CCB003 - COB Performance Enhancements - Task Split           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **********************
      * MAIN REPORT HEADER
      **********************
     A          R HEADP1                    TEXT('(c) Finastra International +
     A                                      Limited +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'RE5400P1'
     A                                    33'***'
     A            TITL      R             36
     A                                    89'***'
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE      R            124EDTCDE(3) SPACEA(2)
      *
     A                                    45'UPDATED BACK-VALUED '
     A                                    65'POSTINGS REPORT'
     A                                      SPACEA(1)
      *
     A                                    45'--------------------'
     A                                    65'---------------'
     A                                      SPACEA(2)
      *
      ******************************
      * MULTI BRANCH REPORT HEADER
      ******************************
     A          R HEADP2
     A                                     7'BRANCH '
     A            BRCA      R             15
     A                                    19'-'
     A            BRNM      R             21
     A                                      SPACEA(1)
      *
      *********************************
      * COLUMN HEADINGS REPORT HEADER
      *********************************
     A          R HEADP3
     A                                    79'ORIGINAL     UPDATED'
     A                                      SPACEA(1)
      *
     A                                    79'HISTORY      HISTORY'
     A                                      SPACEA(1)
      *
     A                                    10'TYPE'
     A                                    29'CUSTOMER'
     A                                    39'CCY'
     A                                    47'A/C'
     A**Start of change CGL029
     A**********                          54'SEQ'                                             CGL029
     A                                    60'SEQ'
     A**End of change CGL029
     A                                    79'DATE         DATE'
     A                                      SPACEA(1)
      *
      *****************
      * REPORT DETAIL
      *****************
     A          R DETP1
     A            PTYPE          8         8
      * Begin /CSD027/
     A**********  CNUM      R             30                                                  CSD027
     A            CNUM           6A       30TEXT('Customer number')
      * End /CSD027/
     A            CCY       R             39
     A            ACOD      R             46
     A**Start of change CGL029
     A**********  ACSQ      R             55                                                  CGL029
     A            ACSQ      R             61
     A**End of change CGL029
     A            ORDATE    R             79REFFLD(RUNA)
     A            UPDATE    R             92REFFLD(RUNA)
     A                                      SPACEA(1)
      *
      **************
      * NO RECORDS
      **************
     A          R NONE                      SPACEB(3)
     A                                    52'*** NO DETAILS TO REPORT ***'
      *
      **************
      * REPORT END
      **************
     A          R ENDP1                     SPACEB(1)
     A                                    56'*** END OF REPORT ***'
      *
      ****************************
      * END OF REPORT FOR BRANCH
      ****************************
     A          R ENDP2                     SPACEB(3)
     A                                    48'*** END OF REPORT FOR BRANCH'
     A            BRCOLD    R             77REFFLD(BRCA)
     A                                    81'***'
      *
      *************************
      * REPORT DATABASE ERROR
      *************************
     A          R ERRP1
     A                                    54'**** DATABASE ERROR ****'
     A                                      SPACEB(5)
