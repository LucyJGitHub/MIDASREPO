      *****************************************************************
/*STD *  PRTFBASE                                                     *
/*EXS *  PR_STD60                                                     *
/*EXI *  TEXT('Midas RE Accr Int Adjustments Today Rpt')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  PRTF/RE5420P1 - Accrued Interest Adjustments Today Report    *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027             Date 30Nov05               *
      *  Prev Amend No. CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 138266             Date 21Nov00               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 130467             Date 26May98               *
      *                 CCB003 *Create     Date 10Oct96               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  138266 - Add RCDTM Field for Credit in Manual Adj. Amount and*
      *           RCDTG Field for Credit in Generated Int. Adj  Amount*
      *  130467 - Amounts not being formatted correctly.              *
      *  CCB003 - COB Perfermance Enhancements - Task Split           *
      *                                                               *
      *****************************************************************
     A                                      REF(MFRFL)
      *
      **********************
      * MAIN REPORT HEADER
      **********************
     A          R HEADP1                    TEXT('(c) Misys International +
     A                                      Banking Systems Ltd. +
     A                                      2001')
     A                                      SKIPB(3)
     A                                     7'REFERENCE'
     A                                    17'RE5420P1'
     A                                    33'***'
     A            TITL      R             36
     A                                    89'***'
     A                                   101'DATE'
     A            RUNA      R            106
     A                                   119'PAGE'
     A            PAGE      R            124EDTCDE(3) SPACEA(2)
      *
     A                                    45'RETAIL ACCRUED INTEREST'
     A                                    69'- POSTED TODAY'
     A                                      SPACEA(1)
      *
     A                                    45'------------------------+
     A                                       --------------'
     A                                      SPACEA(2)
      *
      ******************************
      * MULTI BRANCH REPORT HEADER
      ******************************
     A          R HEADP2
     A                                     7'BRANCH '
     A            BRCA      R             15
     A                                    19'-'
     A            BRNM      R             21
     A                                      SPACEA(3)
      *
      *********************************
      * COLUMN HEADINGS REPORT HEADER
      *********************************
     A          R HEADP3
     A                                     1'CUST'
     A                                     8'CUSTOMER'
     A                                    19'ACCT'
     A**Start of change CGL029
     A**********                          31'RETAIL'                                          CGL029
     A                                    37'RETAIL'
     A**End of change CGL029
     A                                    46'SYSTEM'
     A                                    57'CURRENT'
     A                                    69'A C C R U E D'
     A                                    85'I N T E R E S T'
     A                                   103'A D J U S T M E N T S'
     A                                   127'POSTED'
      *
     A                                      SPACEA(1)
      *
     A                                     1'NUMBER'
     A                                     8'NAME'
     A                                    19'CODE'
     A**Start of change CGL029
     A**********                          27'CCY'                                             CGL029
     A**********                          31'A/C NO'                                          CGL029
     A                                    33'CCY'
     A                                    37'A/C NO'
     A**End of change CGL029
     A                                    45'CAP DATE'
     A                                    55'RATE OR TYPE'
     A                                    77'MANUAL'
     A                                    94'GENERATED'
     A                                   116'ACCRUED'
     A                                   128'TODAY'
     A                                      SPACEA(1)
      *
      *****************
      * REPORT DETAIL
      *****************
     A          R DETLP1
      * Begin /CSD027/
     A**********  CNUM      R              1                                                  CSD027
     A            CNUM           6A        1TEXT('Customer number')
      * End /CSD027/
     A            BBCSSN        10A        8
     A            ACOD      R             19
     A**Start of change CGL029
     A**********                          23'-'                                               CGL029
     A**********  ACSQ      R             24                                                  CGL029
     A**********  CCY       R             27                                                  CGL029
     A**********  ACNO      R             31                                                  CGL029
     A                                    29'-'
     A            ACSQ      R             30
     A            CCY       R             33
     A            ACNO      R             37
     A                                      SPACEA(1)
     A**End of change CGL029
      *
      *********************************************
      * REPORT DETAIL - INTEREST ADJUSTMENT DETAILS
      *********************************************
     A          R DETLP2
     A            POSTP          3A       42
     A            CPDAP          7A       46
      *                                              Begin /130467/
     A**53********INTRP         13A       54
     A  53        INTR          11  7     54EDTCDE(C)
      *                                              End   /130467/
     A N53        TYPEP          8A       57
      *                                              Begin /130467/
     A************MADJP         16        69
     A************GADTP         16        89
     A            MADJP         16  2     69EDTCDE(C)
     A**Start of change 138266
     A            RCDTM          2A       86
     A**End of change 138266
     A            GADTP         16  2     89EDTCDE(C)
      *                                              End   /130467/
     A**Start of change 138266
     A            RCDTG          2A      106
     A**End of change 138266
     A            AIAPP         16       109
     A            CREDT          2A      125
     A            PTPRP          3A      130
     A                                      SPACEA(1)
      *
      **************
      * NO RECORDS
      **************
     A          R NONE                      SPACEB(3)
     A                                    52'*** NO DETAILS TO REPORT ***'
      *
      **************
      * REPORT END
      **************
     A          R ENDP1                     SPACEB(1)
     A                                    56'*** END OF REPORT ***'
      *
      ****************************
      * END OF REPORT FOR BRANCH
      ****************************
     A          R ENDP2                     SPACEB(3)
     A                                    48'*** END OF REPORT FOR BRANCH'
     A            BRCOLD    R             77REFFLD(BRCA)
     A                                    81'***'
      *
      *************************
      * REPORT DATABASE ERROR
      *************************
     A          R ERRP1
     A                                    54'**** DATABASE ERROR ****'
     A                                      SPACEB(5)
