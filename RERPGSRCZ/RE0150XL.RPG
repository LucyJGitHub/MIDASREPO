     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas IBLC 2002 Set Up Customer Exchange EXT. File')
      *****************************************************************
      *                                                               *
      *  Midas - IBLC 2002 Module                                     *
      *                                                               *
      *  RPG/RE0150XL  - Set up IBLC Customer Exchange Ext. File      *
      *                                                               *
      *  Function:  This program set up IBLC Customer Echange Ext.    *
      *             file with default values.                         *
      *                                                               *
      *  (phase(s)) Installation                                      *
      *                                                               *
      *  Called By: ILC0000 - IBLC MODULE INSTALLATION                *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2005            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD027A            Date 10May06               *
      *  Prev Amend No. CER001             Date 25Apr05               *
      *                 ULX043             Date 06Jul01               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD027A - Conversion Of Customer Number to Alpha (recompile) *
      *  CER001 - LUX Upgrade to MidasPlus                            *
      *  ULX043 - IBLC 2002 Reporting                                 *
      *                                                               *
      *****************************************************************
      *
     FSDBRCHL0IF  E           K        DISK
      ***  Standing data - branch code                     Prefix A8
      *
     F***SDACODYLIF  E           K        DISK                                                CER001
     FSDACX1L0IF  E           K        DISK                                                   CER001
      ***  Extended Account codes                          Prefix AC
      *
     F***SDCUSTYLIF  E           K        DISK                                                CER001
     FSDCUX1L0IF  E           K        DISK                                                   CER001
      ***  Extended Customer file                          Prefix CU
      *
     FCUSEXCE IF  E                    DISK
      ***  Customer exchange           Master file         Prefix none
      *
     F***CUSEXYL UF  E           K        DISK                      A                         CER001
     FRECEX1L0UF  E           K        DISK                      A                            CER001
      ***  Extended Customer Exchange                      Prefix CE
      *
      /EJECT
      *-------------------------------------------------------------------------
     E                    CPY@    1   1 80               COPYRIGHT  ARRAY
      /EJECT
      *-------------------------------------------------------------------------
     IDLDA        DS                            256
      * Data structure for data-base error processing
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
      /EJECT
      *-------------------------------------------------------------------------
      * Main processing
      *-------------------------------------------------------------------------
      *
      ***  Redefine data-base error fields for program
      *
     C           *LIKE     DEFN DBFILE    WWBFIL
     C           *LIKE     DEFN DBKEY     WWBKEY
     C           *LIKE     DEFN DBASE     WWBASE
      *
      ***  Key list
      *
     C           BATKEY    KLIST
     C                     KFLD           IPDN
      *
      ** Set up copyright parameter
      *
     C                     MOVEACPY@      CPY2@  80
      *
      ***  First read
      *
     C                     READ CUSEXCE                  LR
      *
      ***  Process until LR indicator not on
      *
     C           *INLR     DOWEQ*OFF
      *
      ***  Check if record exists in the extension file
      *
     C           RECI      IFEQ 'D'
     C           BATKEY    CHAINCUSEXDL              89
      *
      ***  If record not in the extension file : FIELDS SET UP & WRITE
      *
     C           *IN89     IFEQ '1'
      *
      ***  Check if it must be inserted in Extension file :
      ***  Cond : ONE and ONLY ONE party must be Resident .
      *
      ***  First check : Is the Debit Customer accepted ?
      *
     C                     MOVE DCUS      WWDCUS  6
     C********** WWDCUS    CHAINSDCUSTYL             77                                       CER001
     C           WWDCUS    CHAINSDCUX1L0             77                                       CER001
     C           *IN77     IFEQ *ON
     C**********           MOVEL'SDCUSTYL'WWBFIL           ***************                    CER001
     C                     MOVEL'SDCUX1L0'WWBFIL           ***************                    CER001
     C                     MOVELWWDCUS    WWBKEY           * DBERROR 001 *
     C                     Z-ADD1         WWBASE           ***************
     C                     EXSR SRDBER
     C                     ELSE
     C                     MOVE CUTYPE    XXTYPE  2
     C                     MOVEL' '       XXTYPE
     C           XXTYPE    IFNE ' R'
     C                     Z-ADD0         WHICH   10       Not Resident
     C                     ELSE
     C**********           MOVE DCOD      WWDCOD  4                                           CER001
     C                     MOVE DCOD      WWDCOD 10                                           CER001
     C********** WWDCOD    CHAINSDACODYL             78                                       CER001
     C           WWDCOD    CHAINSDACX1L0             78                                       CER001
     C           *IN78     IFEQ *ON
     C**********           MOVEL'SDACODYL'WWBFIL           ***************                    CER001
     C                     MOVEL'SDACX1L0'WWBFIL           ***************                    CER001
     C                     MOVELWWDCOD    WWBKEY           * DBERROR 002 *
     C                     Z-ADD2         WWBASE           ***************
     C                     EXSR SRDBER
     C                     ELSE
     C           ACDRPT    IFNE 'Y'
     C                     Z-ADD0         WHICH            Not Reported
     C                     ELSE
     C                     MOVELBRCA      WWBRCD  3
     C           WWBRCD    CHAINSDBRCHL0             79
     C           *IN79     IFEQ *ON
     C                     MOVEL'SDBRCHL0'WWBFIL           ***************
     C                     MOVELWWBRCD    WWBKEY           * DBERROR 003 *
     C                     Z-ADD3         WWBASE           ***************
     C                     EXSR SRDBER
     C                     ELSE
     C           WWDCUS    IFEQ A8BICN
     C                     Z-ADD0         WHICH   10       Customer not Accepted
     C                     ELSE
     C                     Z-ADD1         WHICH   10       Customer Accepted
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      ***  Second check : Is the Credit Customer accepted ?
      *
     C                     MOVE CCUS      WWCCUS  6
     C********** WWCCUS    CHAINSDCUSTYL             77                                       CER001
     C           WWCCUS    CHAINSDCUX1L0             77                                       CER001
     C           *IN77     IFEQ *ON
     C**********           MOVEL'SDCUSTYL'WWBFIL           ***************                    CER001
     C                     MOVEL'SDCUX1L0'WWBFIL           ***************                    CER001
     C                     MOVELWWCCUS    WWBKEY           * DBERROR 004 *
     C                     Z-ADD4         WWBASE           ***************
     C                     EXSR SRDBER
     C                     ELSE
     C                     MOVE CUTYPE    XXTYPE  2
     C                     MOVEL' '       XXTYPE
     C           XXTYPE    IFNE ' R'
     C                     ADD  0         WHICH            Not Resident
     C                     ELSE
     C**********           MOVE CCOD      WWCCOD  4                                           CER001
     C                     MOVE CCOD      WWCCOD 10                                           CER001
     C********** WWCCOD    CHAINSDACODYL             78                                       CER001
     C           WWCCOD    CHAINSDACX1L0             78                                       CER001
     C           *IN78     IFEQ *ON
     C**********           MOVEL'SDACODYL'WWBFIL           ***************                    CER001
     C                     MOVEL'SDACX1L0'WWBFIL           ***************                    CER001
     C                     MOVELWWCCOD    WWBKEY           * DBERROR 005 *
     C                     Z-ADD5         WWBASE           ***************
     C                     EXSR SRDBER
     C                     ELSE
     C           ACDRPT    IFNE 'Y'
     C                     ADD  0         WHICH            Not Reported
     C                     ELSE
     C                     MOVELBRCA      WWBRCD  3
     C           WWBRCD    CHAINSDBRCHL0             79
     C           *IN79     IFEQ *ON
     C                     MOVEL'SDBRCHL0'WWBFIL           ***************
     C                     MOVELWWBRCD    WWBKEY           * DBERROR 003 *
     C                     Z-ADD3         WWBASE           ***************
     C                     EXSR SRDBER
     C                     ELSE
     C           WWCCUS    IFEQ A8BICN
     C                     ADD  0         WHICH   10       Customer not Accepted
     C                     ELSE
     C                     ADD  2         WHICH   10       Customer Accepted
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
      *
      *** Third Check : Which customer is accepted ?
      ***            1   Customer 1  ----> display
      ***            2            2  ----> display
      *
     C           WHICH     IFEQ 1
     C           WHICH     OREQ 2
     C                     MOVE IPDN      CEIPDN
     C                     MOVE *BLANKS   CEOPCO
     C                     MOVE *BLANKS   CECOCO
     C                     MOVE *BLANKS   CEIDCO
     C                     MOVE *BLANKS   CEIDEN
     C                     WRITECUSEXDL
     C                     ENDIF
      *
     C                     ENDIF
     C                     ENDIF
      *
      ***  Main file read
      *
     C                     READ CUSEXCE                  LR
      *
     C                     ENDDO
      *-------------------------------------------------------------------------
      * End of processing
      *-------------------------------------------------------------------------
     C                     RETRN
      *-------------------------------------------------------------------------
      * SRDBER - Routine to handle database errors
      *-------------------------------------------------------------------------
      *
     C           SRDBER    BEGSR
      *
      ***  Update data area LDA
      *
     C           *NAMVAR   DEFN LDA       DLDA
     C           *LOCK     IN   DLDA
     C                     MOVEL'RE0150XL'DBPGM
     C                     MOVE WWBFIL    DBFILE
     C                     MOVE WWBKEY    DBKEY
     C                     MOVE WWBASE    DBASE
     C                     OUT  DLDA
      *
      ***  Set on data-base error indicators
      *
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *IN69
      *
      ***  Dump program
      *
     C                     DUMP
      *
      ***  Call standard DB error handler
      *
     C                     CALL 'DBERRCTL'             81
      *
     C                     ENDSR
      /EJECT
** CPY@  COPYRIGHT
(c) Misys International Banking Systems Ltd. 2005
