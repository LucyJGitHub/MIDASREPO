0001 H        1                                                           S01117
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Trans Chges- Log/Master File Update')         *
0001 H***********                                                         S01117
0002 F*****************************************************************
0003 F*                                                               *
     F*  Midas - Retail Module
0005 F*                                                               *
0006 F*  RE0229 - TRANSACTION  CHARGES  INPUT - S/RETRG               *
 007 F*           MASTER FILE UPDATE                                  *
0008 F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*      1990                                                     *
0010 F*                                                               *
      *  Last Amend No. CER049             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.02 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
 011 F*                 S01413             Date 13Apr93               *
 011 F*                 S01117                  31JAN90               *
0012 F*                                                               *
     F*****************************************************************
     F*                                                               *
      *  CER049 - Stamp Tax                                           *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
0010 F*  S01413  -  Retail 3 Incorporation                            *
0010 F*  S01117  -  MULTIBRANCHING                                    *
     F*                                                               *
0013 F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F/COPY WNCPYSRC,RE0229F002
0016 F***RE0220FMCF  F     271            WORKSTN                                             CER049
     FRE0220FMCF  F     272            WORKSTN                                                CER049
     F/COPY WNCPYSRC,RE0229F003
0017 F*
0019 F***RETRG   UF  F     128 12AI     2 DISK                                                CER049
     FRETRG   UF  F     129 12AI     2 DISK                                                   CER049
0017 F                                              KCOMIT
0019 F***RETRGD  O   F     128            DISK                      A                         CER049
     FRETRGD  O   F     129            DISK                      A                            CER049
0017 F                                              KCOMIT
0017 F                                              KINFDS INFDS
0017 F                                              KINFSR INFSR
     F/COPY WNCPYSRC,RE0229F001
0023 F*
0024 F*       FUNCTION OF INDICATORS
0025 F*       STANDARD INPUT PROGRAM INDICATORS
0027 F*       U7      DATABASE ERROR
0031 F*       89      BLINK ACTION CODE LABEL
0032 F*       88      NON-DISPLAY ERROR MESSAGE/CODES
0033 F*       87      NON-DISPLAY CHANGE DETAILS
0034 F*       86      NON-DISPLAY USEFUL DATA
0035 F*       85      OUTPUT ENQUIRE - SE - SCREEN
0036 F*       84      OUTPUT DELETE - SD - SCREEN
0037 F*       83      OUTPUT AMEND - SA - SCREEN
0038 F*       82      OUTPUT INSERT - SI - SCREEN
0039 F*       81      OUTPUT INITIAL - SF - SCREEN
0040 F*       80      BLINK DELETE CHECK FIELD LABEL
0041 F*       79      SYSTEM SET-UP FLAG ON
0042 F*       78      SI SCREEN READ IN
0043 F*       77      SA SCREEN READ IN
0044 F*       76      SD SCREEN READ IN
0045 F*       RECORD ID INDICATORS
0046 F*       75      MASTER FILE - HEADER
0047 F*       74      MASTER FILE - DETAIL RECORD
0048 F*       73      MASTER FILE - TRAILER
0049 F*       72      MASTER FILE - NO RECORD FOUND
0050 F*       71      SPARE
0054 F*       67      WTRAN - SI RECORD
0055 F*       66      WTRAN - SA RECORD
0056 F*       65      WTRAN - SD RECORD
0057 F*       64      RECORD UPDATED BY ANOTHER WORKSTATION
0058 F*       FILE UPDATE INDICATORS
 060 F*       54      UPDATE MASTER TRAILER
 062 F*       51      UPDATE MASTER DETAIL RECORD
0065 F*       SUNDRY INDS
0066 F*       49      DELETE INCLUDES SETTING RECI TO *, OTHERWISE LEAVE
0015 F*       02      SET ON IF ERROR DURING READ FROM THE WORK STATION *
0067 E*    STANDARD TABLES/ARRAYS
     E                    ERCD       19  4
0069 E                    ERCM    1   1 79               ERROR MESSAGES
0070 E                    NR          5  5 0
0071 E                    WN          5 15 0
0072 E                    DC          5  3 0
     E/COPY WNCPYSRC,RE0229E001
     E                    CPY@    1   1 80
     E/EJECT
0073 I*    STANDARD FILES
0074 I*
0075 I*    WORKSTATION FILE
0076 I*
0077 IRE0220FMNS
0078 I*
0079 I*    MASTER FILE - HEADER RECORD
0080 I*
0081 IRETRG   NS  75   1 CA
0082 I*
0083 I*    MASTER FILE - TRAILER
0084 I*
0085 I        NS  73   1 CZ
0086 I                                        1   1 RECI
0087 I                                    P  31  330TNLU
0088 I                                       34  34 IRRI
0089 I                                    P  35  49 NR
0090 I                                    P  50  89 WN
0091 I                                    P  90  99 DC
0092 I**********                              1 128 OLD3                                      CER049
     I                                        1 129 OLD3                                      CER049
0093 I*
0094 I*    MASTER FILE - DETAIL RECORD
0095 I*
0096 I        NS  74
0097 I*
0098 I*    STANDARD MASTER FILE TNLU AND OLD HASH AMOUNT
0099 I                                    P  31  330MTNLU
0100 I                                    P  41  470OLDH
0101 I*    OLD MASTER FILE CHARACTER STRINGS
0102 I**********                              1 128 OLD1                                      CER049
     I                                        1 129 OLD1                                      CER049
     I/COPY WNCPYSRC,RE0229I001
0134 I*
0157 I*    STANDARD LOCAL DATA AREA FIELDS
0158 I*
0159 ILDA        UDS                            256
0160 I                                        1   8 LPROC
0161 I***********                           134 138 LFILE                 S01117
0162 I***********                           139 167 LKEY                  S01117
0163 I***********                           168 175 LPROG                 S01117
0164 I***********                           176 1770LDBNO                 S01117
0183 I                                      134 141 LFILE                 S01117
0183 I                                      142 170 LKEY                  S01117
0183 I                                      171 180 LPROG                 S01117
0183 I                                      181 1830LDBNO                 S01117
0183 I                                      134 141 DBFILE                S01117
0183 I                                      142 170 DBKEY                 S01117
0183 I                                      171 180 DBPGM                 S01117
0183 I                                      181 1830DBASE                 S01117
0165 I***********                           181 1850LRUND                 S01117
0166 I***********                           186 192 LRUNA                 S01117
0167 I***********                           193 193 LDATF                 S01117
0168 I***********                           194 1940LSFLG                 S01117
0169 I***********                           195 1960LKEYL                 S01117
0170 I***********                           197 202 LISP                  S01117
0171 I***********                           203 203 LTWOL                 S01117
0172 I***********                           204 2040LLDLP                 S01117
0173 I***********                           205 2090LTNLU                 S01117
0174 I***********                           210 210 LDELM                 S01117
0175 I***********                           211 211 LDUD                  S01117
0190 I                                      187 1910LRUND                 S01117
0191 I                                      192 198 LRUNA                 S01117
0192 I                                      199 199 LDATF                 S01117
0193 I**********                            200 2000LSFLG           S01117S01194
0193 I                                      200 200 LSFLG                 S01194
0194 I                                      201 2020LKEYL                 S01117
0195 I                                      203 208 LISP                  S01117
0196 I                                      209 209 LTWOL                 S01117
0197 I                                      210 2100LLDLP                 S01117
0198 I                                      211 2150LTNLU                 S01117
0199 I                                      216 216 LDELM                 S01117
0200 I                                      217 217 LDUD                  S01117
0176 I*    NON-STANDARD LDA FIELD
0177 I*
0178 I                                      104 133 LTRAN
0157 I*    WORKSTATION TRANSACTIONF FILE DATA STRUCTURE
0158 I*
0159 IWTRAN      UDS                            860
0140 I*    MASTER FILE KEY
0141 I                                        2  13 MASTKY
     I/COPY WNCPYSRC,RE0229I002
0142 I*    STANDARD NEW HASH AMOUNT
0143 I                                    P  41  470NEWH
     I/COPY WNCPYSRC,RE0229I003
0144 I*    NEW MASTER FILE CHARACTER STRINGS
0145 I**********                              1 128 NEW1                                      CER049
     I                                        1 129 NEW1                                      CER049
0146 I                                        1 200 NEW200
0149 I*    STANDARD SCREEN FIELDS
0150 I                                      461 521 SCN60
0151 I                                      461 716 SCN256
     I*    SCREEN IDENTIFICATION FIELD
     I                                      468 469 SID
     I                                      469 469 ACTCD
     I/COPY WNCPYSRC,RE0229I005
     I*
     I*  RETRIEVE WSID FROM PROGRAM STATUS DATA STRUCTURE
     I*
     IPSDS       SDS
     I                                      244 246 WSID
     I                                      245 246 WSID2
     I                                      254 263 USRID
     I**
     I**  DATA STRUCTURE  TO UPDATE LAST TRANSACTION NUMBER DTAARA
     I**
     IDNATN       DS                              5
     I                                        1   50FNATN
     I**
     I**  DATA STRUCTURE FOR ERROR HANDLING SUBROUTINE
     I**
     IINFDS       DS
     I                                     *STATUS  STATUS
     I/COPY WNCPYSRC,RE0229I004
0255 I**
     I**  DATA STRUCTURE TO PROVIDE TEXT ON COMIT ENTRY IN JOURNAL
     ICMTTXT      DS
     I                                        1   50NATN
     I                                        6   7 MDID
     I                                        8  15 PGMN
     I                                       16  18 WSIDX
     I                                       19  240MTIME
     I                                       25  25 ACTCDX
     I                                       26  35 USRIDX
     I                                       51 306 SC256X
     I/EJECT
     C                     MOVEACPY@      BIS@   80
     C/COPY WNCPYSRC,RE0229C021
     C**
     C**   FROM ERROR HANDLING SUBROUTINE RETURN TO EXIT POINT
     C           STATUS    IFEQ 01021
     C/COPY WNCPYSRC,RE0229C022
     C                     EXSR UDLOG
     C                     GOTO END
     C                     END
0179 C*  SET-UP STANDARD INPUT PROGRAM FIELDS  -  WPROG IS THIS PROGRAM
0189 C*
     C           *NAMVAR   DEFN           LDA
     C           *NAMVAR   DEFN           WTRAN
     C           *LOCK     IN   LDA
     C           *LOCK     IN   WTRAN
     C/COPY WNCPYSRC,RE0229C001
0180 C*
0181 C   13                READ RE0220FM                 02
0183 C                     SETOF                     02
0184 C                     MOVELLISP      WPROG   8
0185 C                     MOVE '9  '     WPROG
0186 C                     MOVELWPROG     SPID    6
0187 C*  SET-UP STANDARD LDA FIELDS
0188 C                     MOVE WPROG     LPROG
0189 C                     Z-ADD9         LLDLP
0190 C*  SET-UP STANDARD BLANK FIELDS
0191 C                     MOVE ' '       BLK200200
     C*  INITIALISE FIELD TO BLANK OUT KEY/ACTION CODE
     C/COPY WNCPYSRC,RE0229C002
     C                     MOVE ' '       BLK09   9
     C/COPY WNCPYSRC,RE0229C003
0192 C*  INITIALISE STANDARD INDICATORS
0193 C                     SETON                     88    * NON-DISPLAY
0194 C           LDATF     COMP 'M'                      98* DATE FORMAT
0195 C********** LSFLG     COMP 1                        79* SSF          S01194
0195 C           LSFLG     COMP 'Y'                      79* SSF          S01194
0196 C           LDELM     COMP '*'                      49* DELETE METHOD
     C*
     C*  DETERMINE TYPE OF WTRAN RECORD SI, SA OR SD?
     C           SID       COMP 'SI'                     67
     C           SID       COMP 'SA'                     66
     C           SID       COMP 'SD'                     65
0197 C*
0198 C*  CLEAR ERROR CODE ARRAY, RESET INDEX
0199 C*
0200 C                     MOVE BLK200    ERMSG  79
0202 C                     Z-ADD0         S       20
0203 C*
0208 C                     Z-ADD0         ZERO05  50
0209 C*
0210 C*  SET-UP KEY FOR MASTER FILE TRAILER
0211 C                     MOVEL'99999999'TRLRKY 12
0212 C                     MOVE '999D'    TRLRKY
0213 C*
0225 C*    EXECUTE UPDATE PROCEDURE                                       S01030
0226 C*
0227 C                     EXSR UDLOG
0228 C*
0229 C           END       TAG                             ** END TAG
     C                     OUT  LDA
     C                     OUT  WTRAN
0228 C*
     C   U7                SETON                     LR
     C   LR U7             EXSR *PSSR                                     S01117
     C                     EXCPT
     C/COPY WNCPYSRC,RE0229C023
     C                     RETRN
0230 C*
0231 C********************************************************************
0232 C**
0233 C**   SUBR. - TO SET UP INITIAL SCREEN OUTPUT
0234 C**
0235 C           SUBSF     BEGSR                           ***  SUBSF  ***
0236 C                     SETON                     81
0237 C                     MOVE '1  '     LPROC
0238 C                     MOVE 'SF'      SID     2
0239 C                     ENDSR
0240 C********************************************************************
0241 C**
0242 C**   SUBR. - TO CHAIN TO MASTER FILE
0243 C**
0244 C           MASTCH    BEGSR                           *** MASTCH  ***
     C/COPY WNCPYSRC,RE0229C004
0245 C                     SETOF                     727374
0246 C                     SETOF                     75
0247 C           MASTKY    CHAINRETRG                72
     C/COPY WNCPYSRC,RE0229C005
0248 C                     ENDSR
     C/COPY WNCPYSRC,RE0229C006
0249 C********************************************************************
0259 C**
0260 C**   SUBR. - TO PERFORM EXCEPTION OUTPUT
0261 C**
0262 C           EXEPT     BEGSR                           ***  EXEPT  ***
0263 C                     EXCPT
     C/COPY WNCPYSRC,RE0229C007
0264 C                     SETOF                     515481
0267 C                     ENDSR
0268 C********************************************************************
0269 C**
0270 C**   SUBR. - TO PERFORM UPDATES
0271 C**
0272 C           UDLOG     BEGSR                           ***  UDLOG  ***
     C**   FROM ERROR HANDLING SUBROUTINE RETURN TO EXIT POINT
     C           STATUS    IFEQ 01021
     C/COPY WNCPYSRC,RE0229C024
     C                     Z-ADD0         STATUS  50
     C                     GOTO INFTAG
     C                     END
0273 C*
0280 C*
0281 C                     TIME           MTIME   60
0282 C*
0283 C*  ZEROISE MASTER FILE TNLU
     C/COPY WNCPYSRC,RE0229C008
0284 C                     Z-ADD0         MTNLU
     C/COPY WNCPYSRC,RE0229C025
0285 C*
0286 C*  UPDATE CHAIN TO THE MASTER FILE DETAIL RECORD
0287 C                     EXSR MASTCH
     C/COPY WNCPYSRC,RE0229C026
0288 C*
0289 C*  CHECK IF STATUS CHANGED
0290 C           MTNLU     COMP LTNLU                6464
     C/COPY WNCPYSRC,RE0229C009
0290 C           INFTAG    TAG                             *** INFTAG ***
0291 C*
0292 C*  OUTPUT INITIAL SCREEN WITH MSG IF STATUS CHANGED
0293 C  N64                GOTO UDLOG1
0294 C                     EXSR SUBSF
0295 C                     SETOF                     88
0296 C           S         ADD  1         S
0298 C                     MOVE ERCM,1    ERMSG
0299 C                     EXSR EXEPT
0300 C                     GOTO UDEND
0301 C*
0302 C           UDLOG1    TAG                             ** UDLOG1 TAG
0303 C*
0304 C*  MOVE OLD MASTER RECORD INTO OLD200
0305 C*  TO SIMPLIFY UPDATE PROCEDURE                                     S01030
0306 C*
     C/COPY WNCPYSRC,RE0229C010
0307 C                     MOVELOLD1      OLD200200
     C/COPY WNCPYSRC,RE0229C011
0308 C*
     C**
     C**  OBTAIN LAST TRANSACTION NUMBER  AND SET UP STANDARD OUTPUT
     C**  INFORMATION
     C                     EXSR ZTNLU1
     C                     MOVE 'RE'      MDID    2
     C                     MOVEL'RE0229'  PGMN    8
     C                     MOVE ACTCD     ACTCDX  1
     C                     MOVE WSID      WSIDX   3
     C                     MOVE USRID     USRIDX 10
     C                     MOVE SCN256    SC256X256
 331 C*    OUTPUT NEW INITIAL SCREEN AND CONTINUE PROCESSING
0332 C                     EXSR SUBSF
0333 C                     EXSR EXEPT
0334 C*
0353 C**   UPDATE MASTER FILE DETAIL RECORD
     C/COPY WNCPYSRC,RE0229C027
0353 C                     SETON                     51
0354 C                     EXSR EXEPT
0368 C*  UPDATE CHAIN TO THE MASTER FILE TRAILER
0369 C                     MOVE TRLRKY    MASTKY
0370 C                     EXSR MASTCH
     C/COPY WNCPYSRC,RE0229C012
0371 C  N73                SETON                     U7
0372 C  N73                MOVELWPROG     LFILE
0373 C  N73                MOVE BLK200    LKEY
0374 C  N73                MOVELMASTKY    LKEY
0375 C  N73                MOVE '97'      LDBNO            ** DB ERROR 97
0376 C  N73                GOTO UDEND
0377 C*
0378 C*  MOVE OLD MASTER TRAILER INTO OLD200
 379 C*  TO SIMPLIFY UPDATE PROCEDURE
0380 C*
     C/COPY WNCPYSRC,RE0229C013
0381 C                     MOVELOLD3      OLD200
     C/COPY WNCPYSRC,RE0229C014
0382 C*
0383 C*  IF THERE IS NO HASH TOTAL FIELD ON THE MASTER FILE DETAIL RECORD,
0384 C*  THEN ZEROISE OLDH/NEWH AND CONTINUE AS IF THERE IS.
0385 C*
0386 C                     Z-ADD0         OLDH   130
0387 C                     Z-ADD0         NEWH   130
0388 C*
0389 C*  UPDATE FILE CONTROLS
0390 C*
0391 C*  UPDATE NUMBER AND HASH OF RECORDS ON FILE
0392 C*
0393 C*  NOT DONE FOR DELETE IF RECI NOT SET TO *
0394 C*
0395 C   65N49             GOTO UDLOG2
0396 C*
0397 C                     SETOF                     96
0398 C*
0399 C*  INSERT
0400 C   67                Z-ADD1         H          97    *  ADD
0401 C   67                MOVE NEWH      ZZAMT
0402 C*  AMEND
0403 C   66                Z-ADD1         H          97    *  ADD
0404 C   66      NEWH      SUB  OLDH      WRK15  150
0405 C   66                MOVE WRK15     ZZAMT
0406 C*  SUPPRESS UPDATE OF NR,1 FOR AMENDMENT
0407 C   66                SETON                     96
0408 C*  DELETE - IF * PUT IN RECORD ID
0409 C   65                Z-ADD1         H            97  *  SUBTRACT
0410 C   65                MOVE OLDH      ZZAMT
0411 C*
0412 C                     EXSR NPZHSH
0413 C*
0414 C           UDLOG2    TAG                             ** UDLOG2 TAG
0415 C*
0416 C*  UPDATE NUMBER AND HASH OF RECORDS INSERTED/AMENDED/DELETED TODAY
0417 C*
0418 C                     SETOF                     96
0419 C*
0420 C*  INSERT
0421 C   67                Z-ADD3         H          97    *  ADD
0422 C   67                MOVE NEWH      ZZAMT
0423 C*  AMEND
0424 C   66                Z-ADD4         H          97    *  ADD
0425 C   66      NEWH      SUB  OLDH      WRK15
0426 C   66                MOVE WRK15     ZZAMT
0427 C*  DELETE - WHETHER RECORD ID SET TO * OR NOT
0428 C   65                Z-ADD5         H          97    *  ADD
0429 C   65                MOVE OLDH      ZZAMT
0430 C*
0431 C                     EXSR NPZHSH
0432 C*
 433 C*    UPDATE MASTER FILE TRAILER
     C/COPY WNCPYSRC,RE0229C028
0434 C                     SETON                     54
0435 C                     EXSR EXEPT
     C/COPY WNCPYSRC,RE0229C029
     C**
     C**   END COMMITMENT CONTROL
     C           CMTTXT    COMIT
     C**
0456 C           UDEND     ENDSR                           ** UDEND TAG
0457 C*
0458 C********************************************************************
0459 C**
0460 C**   SUBR. - SUBROUTINE TO ADD  -97 ON-  OR SUBTRACT  -97 OFF-  1
0461 C**           TO/FROM NR,H AND TO ADD/SUBTRACT ZZAMT TO/FROM WN/DC,H.
0462 C**           96 ON SUPPRESSES THE UPDATE OF NR,H - USED FOR AMEND
0463 C**
0464 C           NPZHSH    BEGSR                           ***  NPZHSH ***
0465 C                     Z-ADDH         H       10
0466 C*
     C/COPY WNCPYSRC,RE0229C015
0467 C   97N96   NR,H      ADD  1         NR,H
0468 C  N97N96   NR,H      SUB  1         NR,H
0469 C*
0470 C                     Z-ADDWN,H      ZZTOTI
0471 C                     Z-ADDDC,H      ZZTOTD
     C/COPY WNCPYSRC,RE0229C016
0472 C   97                EXSR GLZADD
0473 C  N97                EXSR GLZSUB
     C/COPY WNCPYSRC,RE0229C017
0474 C                     Z-ADDZZTOTI    WN,H
0475 C                     Z-ADDZZTOTD    DC,H
     C/COPY WNCPYSRC,RE0229C018
0476 C*
0477 C                     ENDSR
0478 C********************************************************************
0479 C* SUBROUTINE TO ADD AN AMOUNT (ZZAMT) TO THE TOTAL (ZZTOTI,ZZTOTD)
0480 C*   IND '99' IS SET BY AN OVERFLOW ERROR
0481 C*
0482 CSR         GLZADD    BEGSR                           *** GLZADD ****
0483 C*
0484 CSR                   Z-ADDZZAMT     ZZAMT  153     91-DEFINE ZZAMT
0485 CSR 91                GOTO ZZAEND                     AMT = 0.BYPASS
0486 C*
0487 C* SPLIT ZZAMT INTO INTEGER AND DECIMAL FIELDS
0488 CSR                   Z-ADDZZAMT     ZZAMTI 150       INTEGER FIELD
0489 CSR                   MOVE ZZAMT     ZZAMTD  30       DECIMAL FIELD
0490 C* BOTH ZZAMTI AND ZZAMTD CONTAIN A 'SIGN' ZONE NOW
0491 C*
0492 CSR                   EXSR GLZSUM
0493 CSR         ZZAEND    ENDSR                           ** ZZAEND TAG *
0494 C*
0495 C********************************************************************
0496 C* SUBROUTINE TO SUBTRACT AN AMOUNT (ZZAMT) FROM THE TOTAL
0497 C*                                                   (ZZTOTI,ZZTOTD)
0498 C*  IND '99' IS SET BY AN OVERFLOW ERROR
0499 CSR         GLZSUB    BEGSR                           *** GLZSUB ****
0500 C* PROCESSING. JUST REVERSE THE 'SIGN' AND ADD
0501 CSR                   Z-SUBZZAMT     ZZAMT  153       REVERSE 'SIGN'
0502 CSR                   EXSR GLZADD                       AND 'ADD'
0503 CSR                   Z-SUBZZAMT     ZZAMT            RESTORE 'SIGN'
0504 CSR                   ENDSR
0505 C********************************************************************
0506 C* SUBROUTINE TO CARRY OUT THE ADDITION FOR SUBROUTINES -
0507 C*                        GLZADD, GLZSUB, GLZCMP.
0508 C*          PARAMETERS PASSED -
0509 C*                     I/P ZZAMTI ZZAMTD
0510 C*                     O/P ZZTOTI ZZTOTD ZZNEGD IND 96 IND 99.
0511 C*
0512 CSR         GLZSUM    BEGSR                           *** GLZSUM ****
0513 C*
0514 CSR                   Z-ADDZZTOTI    ZZTOTI 150       DEFINE ZZTOTI
0515 CSR                   Z-ADDZZTOTD    ZZTOTD  30       DEFINE ZZTOTD
0516 CSR                   SETOF                     919293
0517 CSR                   SETOF                     949599
0518 C*
0519 C*    DETERMINE SIGN OF ZZAMTI & D,   92 IF NEG
0520 CSR         ZZAMTI    COMP 0                      9293
0521 CSR 93      ZZAMTD    COMP 0                      9293
0522 CSR 93                GOTO ZZSEND                     ZERO BYPASS
0523 C*
0524 C*    DETERMINE SIGN OF ZZTOTI & D, 91 IF NEG.
0525 CSR         ZZTOTI    COMP 0                      9193
0526 CSR 93      ZZTOTD    COMP 0                      9193
0527 C*
0528 C*    IF ZZTOTAL IS ZERO, RETURN ZZAMOUNT.
0529 CSR 93                Z-ADDZZAMTI    ZZTOTI
0530 CSR 93                Z-ADDZZAMTD    ZZTOTD
0531 CSR 93                GOTO ZZSEND                     ZERO BYPASS
0532 C*    IF SIGNS DIFFER BYPASS OVERFLOW CHECKS.
0533 CSR 91N92
0534 CORN91 92             GOTO ZZOFPS
0535 C*
0536 CSR         ZZAMTD    ADD  ZZTOTD    ZZWK2   40
0537 CSR         ZZWK2     COMP 999                  93    '93' = CARRY
0538 CSRN93      ZZWK2     COMP -999                   93    INTO INTEGER.
0539 C*
0540 CSR 93N92   ZZAMTI    ADD  1         ZZWK3  150       ADD 'CARRY' +VE
0541 CSR    93 92ZZAMTI    SUB  1         ZZWK3            SUB 'CARRY' -VE
0542 CSR 93      ZZTOTI    ADD  ZZWK3     ZZWK3
0543 CSRN93      ZZTOTI    ADD  ZZAMTI    ZZWK3
0544 C*
0545 C* IF THE MODULUS OF ZZWK3 IS LT MOD. ZZTOTI THEN O/F HAS OCCURED
0546 CSRN92      ZZWK3     COMP ZZTOTI                 99  -92 MEANS NOS.
0547 CSR 92      ZZWK3     COMP ZZTOTI               99     NEGATIVE
0548 CSRN99                Z-ADDZZWK2     ZZTOTD
0549 CSRN99                Z-ADDZZWK3     ZZTOTI
0550 C*
0551 C* IF O/F ZEROIZE ZZAMT , IND '99' SET AND ZZTOT FIELDS LEFT INTACT.
0552 CSR 99                Z-ADD0         ZZAMT  153
0553 CSR                   GOTO ZZSEND
0554 C*
0555 C* THE 'SIGNS' ARE DIFFERENT
0556 CSR         ZZOFPS    TAG                             ** ZZOFPS TAG**
0557 C* IF ZZAMT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZTOT
0558 C*
0559 CSR 92                Z-SUBZZAMTI    ZZAMTI 150
0560 CSR 92                Z-SUBZZAMTD    ZZAMTD  30
0561 C*
0562 C* IF ZZTOT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZAMT.
0563 C*
0564 CSR 91                Z-SUBZZTOTI    ZZTOTI
0565 CSR 91                Z-SUBZZTOTD    ZZTOTD
0566 C* BOTH ZZAMT AND ZZTOT ARE NOW POSITIVE.
0567 C*
0568 CSR         ZZTOTI    COMP ZZAMTI               93  95
0569 CSR 95      ZZTOTD    COMP ZZAMTD               93  95
0570 C*
0571 C* IF ZZTOT EQ. ZZAMT RETURN ZERO.
0572 CSR 95                Z-ADD0         ZZTOTI
0573 CSR 95                Z-ADD0         ZZTOTD
0574 CSR 95                GOTO ZZSEND
0575 C*
0576 C* IF ZZTOT GT. ZZAMT.
0577 CSR 93      ZZAMTD    COMP ZZTOTD               94
0578 CSR 93 94   ZZTOTI    SUB  1         ZZTOTI
0579 CSR 93 94   ZZTOTD    ADD  1000      ZZWK2
0580 CSR 93      ZZTOTI    SUB  ZZAMTI    ZZTOTI
0581 CSR 93 94   ZZWK2     SUB  ZZAMTD    ZZTOTD
0582 CSR 93N94   ZZTOTD    SUB  ZZAMTD    ZZTOTD
0583 C*
0584 C*
0585 C* IF ZZAMT GT. ZZTOT.
0586 CSRN93      ZZTOTD    COMP ZZAMTD               94
0587 CSRN93 94   ZZAMTI    SUB  1         ZZWK3  150
0588 CSRN93 94   ZZAMTD    ADD  1000      ZZWK2
0589 CSRN93 94   ZZWK3     SUB  ZZTOTI    ZZTOTI
0590 CSRN93N94   ZZAMTI    SUB  ZZTOTI    ZZTOTI
0591 CSRN93 94   ZZWK2     SUB  ZZTOTD    ZZTOTD
0592 CSRN93N94   ZZAMTD    SUB  ZZTOTD    ZZTOTD
0593 C*
0594 C* REVERSE SIGN OF ZZTOT IF LARGER I/P FIELDS WERE NEGATIVE
0595 CSR                   SETOF                     94
0596 CSR 93 91
0597 CORN93 92             SETON                     94
0598 CSR 94                Z-SUBZZTOTI    ZZTOTI
0599 CSR 94                Z-SUBZZTOTD    ZZTOTD
0600 C**
0601 C**   RESTORE SIGN OF ZZAMTI & ZZAMTD IF IT WAS REVERSED.
0602 CSR 92                Z-SUBZZAMTI    ZZAMTI
0603 CSR 92                Z-SUBZZAMTD    ZZAMTD
0604 CSR         ZZSEND    TAG                             ** ZZSEND TAG *
0605 C**
0606 C**   IF ZZTOTD IS ZERO, AND ZZTOTI IS NEG. SET UP ZZNEGD.
0607 CSR                   SETOF                     96
0608 CSR         ZZTOTD    COMP 0                        91
0609 CSR 91      ZZTOTI    COMP 0                      96
0610 CSR 96                MOVE '.000-'   ZZNEGD  5
0611 CSR                   ENDSR                             ** ENDSR **
0612 C********************************************************************
     C*
     C*   ZTNLU1 SR. - TO LOCK THE TRANSACTION NUMBER DATA AREA AND
     C*                THEN UPDATE AND RELEASE THE DATA AREA.
     C*
     C           ZTNLU1    BEGSR
     C           *NAMVAR   DEFN MNATN     DNATN
     C           *LOCK     IN   DNATN
     C                     MOVE FNATN     NATN    50
     C                     MOVE FNATN     ZZWK05  50
     C                     ADD  1         ZZWK05
     C                     MOVE ZZWK05    FNATN
     C                     OUT  DNATN
     C                     ENDSR
 131 C********************************************************************
     C**   FILE EXCEPTION / ERROR HANDLING SUBROUTINE
     C           INFSR     BEGSR
     C**   CHECK ERROR TYPE
     C           STATUS    IFEQ 01021
     C**   CHAIN FILE TO GET CURRENT DETAILS
     C/COPY WNCPYSRC,RE0229C019
     C           MASTKY    CHAINRETRG                72
     C/COPY WNCPYSRC,RE0229C020
     C**
     C           *IN72     IFEQ '0'
     C**   ROLLBACK UPDATES ALREADY DONE
     C                     ROLBK
     C**   SET ON RECORD UPDATED BY ANOTHER WORKSTATION INDICATOR
     C                     SETON                     64
     C**   RESET OTHER INDICATORS
     C                     SETOF                     515481
     C**
     C                     MOVE '*DETC '  EXTTAG  6
     C**
     C                     ELSE
     C**
     C                     MOVE '*CANCL'  EXTTAG
     C                     END
     C**
     C                     ELSE
     C**
     C                     MOVE '*CANCL'  EXTTAG
     C                     END
     C**
     C                     ENDSREXTTAG
     C********************************************************************
     C*                                                                   S01117
     C* *PSSR  --- SUBROUTINE TO HANDLE ABNORMAL ERROR CONDITIONS         S01117
     C*                                                                   S01117
     C* CALLED FROM: AFTER ABNORNAL OPERATION OCCURS                      S01117
     C*                                                                   S01117
     C* CALLS: NOTHING                                                    S01117
     C*                                                                   S01117
     C********************************************************************S01117
     C**                                                                  S01117
     C*                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C*                                                                   S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C                     ENDSR                           ** *PSSR **    S01117
     C********************************************************************S01117
     C/EJECT
0613 O*  WORKSTATION - INITIAL SCREEN OUTPUT
0614 O*
0615 ORE0220FME        81
0616 O                                   K8 'RE0220F1'
0617 O                         SCN60     61
0618 O                         SID        9
0619 O                         SPID      15
0620 O                         MTIME     23 '  .  .  '
     O/COPY WNCPYSRC,RE0229O001
     O                         BLK09     35
     O/COPY WNCPYSRC,RE0229O009
0621 O*  ERROR FIELDS
     O                         ERCD     111
0623 O                         ERMSG    193
     O/COPY WNCPYSRC,RE0229O002
0624 O*
0655 O*  MASTER FILE - DETAIL RECORD UPDATE, FOR INSERT
0656 O*  WHEN NO RECORD ON FILE
0657 O*
     O/COPY WNCPYSRC,RE0229O003
0658 ORETRGD  EADD     51 72
0659 O**********               NEW1     128                                                   CER049
     O                         NEW1     129                                                   CER049
     O/COPY WNCPYSRC,RE0229O004
0660 O                         NATN      33P
0661 O*
0662 O*  MASTER FILE - DETAIL RECORD UPDATE, FOR INSERT OVER DELETED/
0663 O*  BACKED OUT RECORD AND AMEND/DELETE
0664 O*
     O/COPY WNCPYSRC,RE0229O005
0665 ORETRG   E        51N72
0666 O**********               NEW1     128                                                   CER049
     O                         NEW1     129                                                   CER049
     O/COPY WNCPYSRC,RE0229O006
0667 O                         NATN      33P
0686 O*
0687 O*  MASTER FILE - TRAILER UPDATE
0688 O*
     O/COPY WNCPYSRC,RE0229O007
0689 O        E        54
0690 O**********               OLD3     128                                                   CER049
     O                         OLD3     129                                                   CER049
     O/COPY WNCPYSRC,RE0229O008
0691 O                         NATN      33P
0692 O                                   34 'Y'
0693 O                         NR        49P
0694 O                         WN        89P
0695 O                         DC        99P
**   ERROR MESSAGES
THIS RECORD HAS BEEN UPDATED BY ANOTHER WORKSTATION
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
