0001 H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Retl Charges Maint-Log/Master File U')
0001 H***********
0002 F*****************************************************************
0003 F*                                                               *
     F*  Midas - Retail Module
0005 F*                                                               *
0006 F*  RE0239 - RETAIL CHARGES MAINTENANCE - S/RECRG                *
 007 F*           MASTER FILE UPDATE                                  *
0008 F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*      1990, 1993                                               *
0010 F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
0011  *                 S01413             Date 13Apr93               *
0011  *                 S01117                  06Nov89               *
0012 F*                                                               *
0012 F*****************************************************************
0012 F*                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
     F*  S01413 - Retail 3 Incorporation                              *
0012 F*  S01117 - MULTIBRANCHING                                      *
0012 F*                                                               *
0013 F*****************************************************************
0012 F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
0016 FRE0230FMCF  F     318            WORKSTN
0019 F***RECRG** UF  F     160 12AI     2 DISK                                                CGL029
     FRECRG   UF  F     170 12AI     2 DISK                                                   CGL029
     F                                              KCOMIT
0019 F***RECRGD* O   F     160            DISK                      A                         CGL029
     FRECRGD  O   F     170            DISK                      A                            CGL029
     F                                              KCOMIT
     F                                              KINFDS INFDS
     F                                              KINFSR INFSR
     F/COPY WNCPYSRC,RE0239F001
0023 F*
0024 F*       FUNCTION OF INDICATORS
0025 F*       STANDARD INPUT PROGRAM INDICATORS
0027 F*       U7      DATABASE ERROR
0031 F*       89      BLINK ACTION CODE LABEL
0032 F*       88      NON-DISPLAY ERROR MESSAGE/CODES
0033 F*       87      NON-DISPLAY CHANGE DETAILS
0034 F*       86      NON-DISPLAY USEFUL DATA
0035 F*       85      OUTPUT ENQUIRE - SE - SCREEN
0036 F*       84      OUTPUT DELETE - SD - SCREEN
0037 F*       83      OUTPUT AMEND - SA - SCREEN
0038 F*       82      OUTPUT INSERT - SI - SCREEN
0039 F*       81      OUTPUT INITIAL - SF - SCREEN
0040 F*       80      BLINK DELETE CHECK FIELD LABEL
0041 F*       79      SYSTEM SET-UP FLAG ON
0042 F*       78      SI SCREEN READ IN
0043 F*       77      SA SCREEN READ IN
0044 F*       76      SD SCREEN READ IN
0045 F*       RECORD ID INDICATORS
0046 F*       75      MASTER FILE - HEADER
0047 F*       74      MASTER FILE - DETAIL RECORD
0048 F*       73      MASTER FILE - TRAILER
0049 F*       72      MASTER FILE - NO RECORD FOUND
0050 F*       71      SPARE
     F*       68      INSERT OVER DELETED RECORD
0054 F*       67      WTRN - SI RECORD
0055 F*       66      WTRN - SA RECORD
0056 F*       65      WTRN - SD RECORD
0057 F*       64      RECORD UPDATED BY ANOTHER WORKSTATION
0058 F*       FILE UPDATE INDICATORS
     F*       54      UPDATE MASTER FILE TRAILER
     F*       51      UPDATE MASTER FILE DETAIL
0065 F*       SUNDRY INDS
0066 F*       49      DELETE INCLUDES SETTING RECI TO *, OTHERWISE LEAVE
0015 F*       02      SET ON IF ERROR DURING READ FROM THE WORK STATION *
     F*       03      DUMMY RECORD INDICATOR
0067 E*    STANDARD TABLES/ARRAYS
0068 E                    ERCD       19  4               ERROR CODES
0069 E                    ERCM    1   1 79               ERROR MESSAGES
0070 E                    NR          5  5 0
0071 E                    WN          5 15 0
     E                    CPY@    1   1 80
0072 E                    DC          5  3 0
0073 I*    STANDARD FILES
0074 I*
0075 I*    WORKSTATION FILE
0076 I*
0077 IRE0230FMNS
0078 I*
0079 I*    MASTER FILE - HEADER RECORD
0080 I*
0081 IRECRG   NS  75   1 CA
0082 I*
0083 I*    MASTER FILE - TRAILER
0084 I*
0085 IRECRG   NS  73   1 CZ
0086 I                                        1   1 RECI
0087 I                                    P  31  330TNLU
0088 I                                       34  34 IRRI
0089 I                                    P  35  49 NR
0090 I                                    P  50  89 WN
0091 I                                    P  90  99 DC
0092 I                                        1 160 OLD3
0093 I*
0094 I*    MASTER FILE - DETAIL RECORD
0095 I*
0096 I        NS  74
0097 I*
     I                                        1   1 RECI
0098 I*    STANDARD MASTER FILE TNLU AND OLD HASH AMOUNT
0099 I                                    P  31  330MTNLU
0100 I*    OLD MASTER FILE CHARACTER STRINGS
0101 I**********                              1 160 OLD1                                      CGL029
0101 I                                        1 170 OLD1                                      CGL029
0133 I*
0134 I*    WORKSTATION TRANSACTION DATA STRUCTURE
0135 I*
0136 IWTRN       UDS                            860
0139 I*    MASTER FILE KEY
0140 I                                        2  13 MASTKY
     I/COPY WNCPYSRC,RE0239I001
0141 I*    STANDARD NEW HASH AMOUNT
0142 I                                    P  41  470NEWH
0143 I*    NEW MASTER FILE CHARACTER STRINGS
0144 I**********                              1 160 NEW1                                      CGL029
     I                                        1 170 NEW1                                      CGL029
0145 I                                        1 200 NEW200
     I                                      401 401 TYPED
0148 I*    STANDARD SCREEN FIELDS
0149 I                                      461 520 SCN60
0150 I                                      461 716 SCN256
0151 I*
0154 I                                      468 469 SID
0155 I*
0156 I*    STANDARD LOCAL DATA AREA FIELDS
0157 I*
0158 ILDA        UDS                            256
0159 I                                        1   8 LPROC
0160 I***********                           134 138 LFILE                 S01117
0161 I***********                           139 167 LKEY                  S01117
0162 I***********                           168 175 LPROG                 S01117
0163 I***********                           176 1770LDBNO                 S01117
0164 I***********                           181 1850LRUND                 S01117
0165 I***********                           186 192 LRUNA                 S01117
0166 I***********                           193 193 LDATF                 S01117
0167 I***********                           194 1940LSFLG                 S01117
0168 I***********                           195 1960LKEYL                 S01117
0169 I***********                           197 202 LISP                  S01117
0170 I***********                           203 203 LTWOL                 S01117
0171 I***********                           204 2040LLDLP                 S01117
0172 I***********                           205 2090LTNLU                 S01117
0173 I***********                           210 210 LDELM                 S01117
0174 I***********                           211 211 LDUD                  S01117
     I                                      134 141 LFILE                 S01117
     I                                      142 170 LKEY                  S01117
     I                                      171 180 LPROG                 S01117
     I                                      181 1830LDBNO                 S01117
     I                                      134 141 DBFILE                S01117
     I                                      142 170 DBKEY                 S01117
     I                                      171 180 DBPGM                 S01117
     I                                      181 1830DBASE                 S01117
     I                                      187 1910LRUND                 S01117
     I                                      192 198 LRUNA                 S01117
     I                                      199 199 LDATF                 S01117
     I**********                            200 2000LSFLG           S01117S01194
     I                                      200 200 LSFLG                 S01194
     I                                      201 2020LKEYL                 S01117
     I                                      203 208 LISP                  S01117
     I                                      209 209 LTWOL                 S01117
     I                                      210 2100LLDLP                 S01117
     I                                      211 2150LTNLU                 S01117
     I                                      216 216 LDELM                 S01117
     I                                      217 217 LDUD                  S01117
     I**
     I**   RETRIEVE WSID FROM PROGRAM STATUS DATA STRUCTURE
     IPSDS       SDS
     I                                      244 246 WSID
     I                                      245 246 WSID2
     I                                      254 263 USRID
     I**
     I**  FILE EXCEPTION/ERROR HANDLING DATA STRUCTURE
     IINFDS       DS
     I                                     *STATUS  STATUS
     I**
     I**  DATA STRUCTURE TO UPDATE LAST TRANSACTION NUMBER DTAARA
     I**
     IDNATN       DS                              5
     I                                        1   50FNATN
     I**
     I**  DATA STRUCTURE TO PROVIDE TEXT ON COMIT ENTRY IN JOURNAL
     I**
     ICMTTXT      DS                            306
     I                                        1   50NATN
     I                                        6   7 MDID
     I                                        8  15 PGMN
     I                                       16  18 WSIDX
     I                                       19  240MTIME
     I                                       25  25 ACTCD
     I                                       26  35 USRIDX
     I                                       51 306 SCNX
     I**
     C                     MOVEACPY@      BIS@   80
     C*    FILE EXCEPTION/ERROR HANDLING FOR RECRGD
     C           STATUS    IFEQ 01021
     C                     EXSR UDLOG
     C                     END
     C*
0175 C*  SET-UP STANDARD INPUT PROGRAM FIELDS  -  WPROG IS THIS PROGRAM
0176 C*
0177 C* DEFINE DATA STRUCTURES
0178 C           *NAMVAR   DEFN           LDA
0179 C           *NAMVAR   DEFN           WTRN
     C           *LOCK     IN   LDA
     C           *LOCK     IN   WTRN
     C*
0176 C*
0180 C                     MOVELLISP      WPROG   8
0181 C                     MOVE '9  '     WPROG
0182 C                     MOVELWPROG     SPID    6
0183 C*  SET-UP STANDARD LDA FIELDS
0184 C                     MOVE WPROG     LPROG
0185 C                     Z-ADD9         LLDLP
0186 C*  SET-UP STANDARD BLANK FIELDS
0187 C                     MOVE ' '       BLK200200
0188 C*  INITIALISE STANDARD INDICATORS
0189 C                     SETON                     88    * NON-DISPLAY
0190 C           LDATF     COMP 'M'                      98* DATE FORMAT
0191 C********** LSFLG     COMP 1                        79* SSF          S01194
0191 C           LSFLG     COMP 'Y'                      79* SSF          S01194
0192 C           LDELM     COMP '*'                      49* DELETE METHOD
     C/COPY WNCPYSRC,RE0239C001
0193 C*
0194 C*  CLEAR ERROR CODE ARRAY, RESET INDEX
0195 C*
0196 C                     MOVE BLK200    ERMSG  79
0197 C                     MOVE '    '    ERCD
0198 C                     Z-ADD0         S       20
0199 C*
0200 C*  PROCESSING IN ONE CYCLE
0201 C*
0204 C                     Z-ADD0         ZERO05  50
0205 C*
0206 C*  SET-UP KEY FOR MASTER FILE TRAILER
0207 C                     MOVEL'99999999'TRLRKY 12
0208 C                     MOVE '999D'    TRLRKY
0209 C*
 210 C*  ACCESS DATA FOR UPDATING FROM DATA STRUCTURE WTRN
0211 C*
     C                     MOVE *BLANKS   TRANTP  3
     C                     MOVELTYPED     TRANTP
     C                     MOVE SID       TRANTP
     C           TRANTP    COMP 'DSI'                    67
     C           TRANTP    COMP 'DSA'                    66
     C           TRANTP    COMP 'DSD'                    65
0213 C  N67N66N65          SETON                     U7
0214 C   U7                MOVE 'WTRN '   LFILE
0215 C   U7                MOVE BLK200    LKEY
0216 C   U7                MOVEL'1'       LKEY
0217 C   U7                MOVE SID       LKEY
0218 C   U7                MOVE '91'      LDBNO            ** DB ERROR 91
0219 C   U7                GOTO END
0220 C*
 221 C*  EXECUTE UPDATE PROCEDURE
0222 C*
0223 C                     EXSR UDLOG
0224 C*
0225 C           END       TAG                             ** END TAG
     C   U7                SETON                     LR
     C   U7                EXSR *PSSR                                     S01117
0326 C*
0327 C*   OUTPUT NEW INITIAL SCREEN
0328 C  NLR                EXSR SUBSF
0329 C  NLR                EXSR EXEPT
     C                     MOVE '0'       *IN
     C                     OUT  LDA
     C                     OUT  WTRN
     C                     RETRN
     C*
     C* DUMMY READ FOR WORKSTATION FILE
     C                     READ RE0230FM                 02
     C   02                SETOF                         02
     C**
     C*
0226 C*
0227 C********************************************************************
0228 C**
0229 C**   SUBR. - TO SET UP INITIAL SCREEN OUTPUT
0230 C**
0231 C           SUBSF     BEGSR                           ***  SUBSF  ***
0232 C                     SETON                     81
0233 C                     MOVE '1  '     LPROC
0234 C                     MOVE 'SF'      SID     2
0235 C                     ENDSR
0236 C********************************************************************
0237 C**
0238 C**   SUBR. - TO CHAIN TO MASTER FILE
0239 C**
0240 C           MASTCH    BEGSR                           *** MASTCH  ***
0241 C                     SETOF                     727374
0242 C                     SETOF                     75
0243 C           MASTKY    CHAINRECRG                72
0244 C                     ENDSR
0254 C********************************************************************
0255 C**
0256 C**   SUBR. - TO PERFORM EXCEPTION OUTPUT
0257 C**
0258 C           EXEPT     BEGSR                           ***  EXEPT  ***
     C                     EXCPT
0260 C                     SETOF                     515481
0263 C                     ENDSR
0264 C********************************************************************
0265 C**
0266 C**   SUBR. - TO PERFORM UPDATING
0267 C**
0268 C           UDLOG     BEGSR                           ***  UDLOG  ***
     C**
     C**   FILE EXCEPTION/ERROR HANDLING FOR RECRGD
     C           STATUS    IFEQ 01021
     C                     Z-ADD0         STATUS  50
     C                     GOTO INFTAG
     C                     END
0269 C*
0277 C                     TIME           MTIME   60
0278 C*
0279 C*  ZEROISE MASTER FILE TNLU
0280 C                     Z-ADD0         MTNLU
0281 C*
0282 C*  UPDATE CHAIN TO THE MASTER FILE DETAIL RECORD
0283 C                     EXSR MASTCH
     C   67      RECI      COMP '*'                      68
     C                     SETOF                     64
0284 C*
0285 C*  CHECK IF STATUS CHANGED
0286 C           MTNLU     COMP LTNLU                6464
     C           INFTAG    TAG                             ** INFTAG TAG
0287 C*
0288 C*  OUTPUT INITIAL SCREEN WITH MSG IF STATUS CHANGED
0289 C  N64                GOTO UDLOG1
0290 C                     EXSR SUBSF
0291 C                     SETOF                     88
0292 C           S         ADD  1         S
0294 C                     MOVE ERCM,1    ERMSG
0295 C                     EXSR EXEPT
0296 C                     GOTO UDEND
0297 C*
0298 C           UDLOG1    TAG                             ** UDLOG1 TAG
0299 C*
     C**  OBTAIN LAST TRANSACTION NUMBER, AND SET UP
     C**  STANDARD OUTPUT INFORMATION FOR CMTTXT.
     C                     EXSR ZTNLU1
     C                     MOVE 'RE'      MDID
     C                     MOVEL'RE0239'  PGMN
     C                     TIME           MTIME
     C                     MOVE WSID      WSIDX
     C                     MOVE TRANTP    ACTCD
     C                     MOVE USRID     USRIDX
     C                     MOVE SCN256    SCNX
     C**
 348 C*  UPDATE MASTER FILE DETAIL RECORD
0349 C                     SETON                     51
0350 C                     EXSR EXEPT
     C/COPY WNCPYSRC,RE0239C002
0351 C*
0363 C*
0364 C*  UPDATE CHAIN TO THE MASTER FILE TRAILER
0365 C                     MOVE TRLRKY    MASTKY
0366 C                     EXSR MASTCH
0367 C  N73                SETON                     U7
0368 C  N73                MOVELWPROG     LFILE
0369 C  N73                MOVE BLK200    LKEY
0370 C  N73                MOVELMASTKY    LKEY
0371 C  N73                MOVE '97'      LDBNO            ** DB ERROR 97
0372 C  N73                GOTO UDEND
0373 C*
0379 C*  IF THERE IS NO HASH TOTAL FIELD ON THE MASTER FILE DETAIL RECORD,
0380 C*  THEN ZEROISE OLDH/NEWH AND CONTINUE AS IF THERE IS.
0381 C*
0382 C                     Z-ADD0         OLDH   130
0383 C                     Z-ADD0         NEWH   130
0384 C*
0385 C*  UPDATE FILE CONTROLS
0386 C*
0387 C*  UPDATE NUMBER AND HASH OF RECORDS ON FILE
0388 C*
0389 C*  NOT DONE FOR DELETE IF RECI NOT SET TO *
0390 C*
0391 C   65N49             GOTO UDLOG2
0392 C*
0393 C                     SETOF                     96
0394 C*
0395 C*  INSERT
0396 C   67                Z-ADD1         H          97    *  ADD
0397 C   67                MOVE NEWH      ZZAMT
0398 C*  AMEND
0399 C   66                Z-ADD1         H          97    *  ADD
0400 C   66      NEWH      SUB  OLDH      WRK15  150
0401 C   66                MOVE WRK15     ZZAMT
0402 C*  SUPPRESS UPDATE OF NR,1 FOR AMENDMENT
0403 C   66                SETON                     96
0404 C*  DELETE - IF * PUT IN RECORD ID
0405 C   65                Z-ADD1         H            97  *  SUBTRACT
0406 C   65                MOVE OLDH      ZZAMT
0407 C*
0408 C                     EXSR NPZHSH
0409 C*
0410 C           UDLOG2    TAG                             ** UDLOG2 TAG
0411 C*
0412 C*  UPDATE NUMBER AND HASH OF RECORDS INSERTED/AMENDED/DELETED TODAY
0413 C*
0414 C                     SETOF                     96
0415 C*
0416 C*  INSERT
0417 C   67                Z-ADD3         H          97    *  ADD
0418 C   67                MOVE NEWH      ZZAMT
0419 C*  AMEND
0420 C   66                Z-ADD4         H          97    *  ADD
0421 C   66      NEWH      SUB  OLDH      WRK15
0422 C   66                MOVE WRK15     ZZAMT
0423 C*  DELETE - WHETHER RECORD ID SET TO * OR NOT
0424 C   65                Z-ADD5         H          97    *  ADD
0425 C   65                MOVE OLDH      ZZAMT
0426 C*
0427 C                     EXSR NPZHSH
0428 C*
 429 C*  UPDATE MASTER FILE TRAILER
0430 C                     SETON                     54
0431 C                     EXSR EXEPT
0432 C*
0451 C*
     C*  END COMMITMENT CYCLE
     C           CMTTXT    COMIT
     C*
0452 C           UDEND     ENDSR                           ** UDEND TAG
0453 C*
0454 C********************************************************************
0455 C**
0456 C**   SUBR. - SUBROUTINE TO ADD  -97 ON-  OR SUBTRACT  -97 OFF-  1
0457 C**           TO/FROM NR,H AND TO ADD/SUBTRACT ZZAMT TO/FROM WN/DC,H.
0458 C**           96 ON SUPPRESSES THE UPDATE OF NR,H - USED FOR AMEND
0459 C**
0460 C           NPZHSH    BEGSR                           ***  NPZHSH ***
0461 C                     Z-ADDH         H       10
0462 C*
0463 C   97N96   NR,H      ADD  1         NR,H
0464 C  N97N96   NR,H      SUB  1         NR,H
0465 C*
0466 C                     Z-ADDWN,H      ZZTOTI
0467 C                     Z-ADDDC,H      ZZTOTD
0468 C   97                EXSR GLZADD
0469 C  N97                EXSR GLZSUB
0470 C                     Z-ADDZZTOTI    WN,H
0471 C                     Z-ADDZZTOTD    DC,H
0472 C*
0473 C                     ENDSR
0474 C********************************************************************
0475 C* SUBROUTINE TO ADD AN AMOUNT (ZZAMT) TO THE TOTAL (ZZTOTI,ZZTOTD)
0476 C*   IND '99' IS SET BY AN OVERFLOW ERROR
0477 C*
0478 CSR         GLZADD    BEGSR                           *** GLZADD ****
0479 C*
0480 CSR                   Z-ADDZZAMT     ZZAMT  153     91-DEFINE ZZAMT
0481 CSR 91                GOTO ZZAEND                     AMT = 0.BYPASS
0482 C*
0483 C* SPLIT ZZAMT INTO INTEGER AND DECIMAL FIELDS
0484 CSR                   Z-ADDZZAMT     ZZAMTI 150       INTEGER FIELD
0485 CSR                   MOVE ZZAMT     ZZAMTD  30       DECIMAL FIELD
0486 C* BOTH ZZAMTI AND ZZAMTD CONTAIN A 'SIGN' ZONE NOW
0487 C*
0488 CSR                   EXSR GLZSUM
0489 CSR         ZZAEND    ENDSR                           ** ZZAEND TAG *
0490 C*
0491 C********************************************************************
0492 C* SUBROUTINE TO SUBTRACT AN AMOUNT (ZZAMT) FROM THE TOTAL
0493 C*                                                   (ZZTOTI,ZZTOTD)
0494 C*  IND '99' IS SET BY AN OVERFLOW ERROR
0495 CSR         GLZSUB    BEGSR                           *** GLZSUB ****
0496 C* PROCESSING. JUST REVERSE THE 'SIGN' AND ADD
0497 CSR                   Z-SUBZZAMT     ZZAMT  153       REVERSE 'SIGN'
0498 CSR                   EXSR GLZADD                       AND 'ADD'
0499 CSR                   Z-SUBZZAMT     ZZAMT            RESTORE 'SIGN'
0500 CSR                   ENDSR
0501 C********************************************************************
0502 C* SUBROUTINE TO CARRY OUT THE ADDITION FOR SUBROUTINES -
0503 C*                        GLZADD, GLZSUB, GLZCMP.
0504 C*          PARAMETERS PASSED -
0505 C*                     I/P ZZAMTI ZZAMTD
0506 C*                     O/P ZZTOTI ZZTOTD ZZNEGD IND 96 IND 99.
0507 C*
0508 CSR         GLZSUM    BEGSR                           *** GLZSUM ****
0509 C*
0510 CSR                   Z-ADDZZTOTI    ZZTOTI 150       DEFINE ZZTOTI
0511 CSR                   Z-ADDZZTOTD    ZZTOTD  30       DEFINE ZZTOTD
0512 CSR                   SETOF                     919293
0513 CSR                   SETOF                     949599
0514 C*
0515 C*    DETERMINE SIGN OF ZZAMTI & D,   92 IF NEG
0516 CSR         ZZAMTI    COMP 0                      9293
0517 CSR 93      ZZAMTD    COMP 0                      9293
0518 CSR 93                GOTO ZZSEND                     ZERO BYPASS
0519 C*
0520 C*    DETERMINE SIGN OF ZZTOTI & D, 91 IF NEG.
0521 CSR         ZZTOTI    COMP 0                      9193
0522 CSR 93      ZZTOTD    COMP 0                      9193
0523 C*
0524 C*    IF ZZTOTAL IS ZERO, RETURN ZZAMOUNT.
0525 CSR 93                Z-ADDZZAMTI    ZZTOTI
0526 CSR 93                Z-ADDZZAMTD    ZZTOTD
0527 CSR 93                GOTO ZZSEND                     ZERO BYPASS
0528 C*    IF SIGNS DIFFER BYPASS OVERFLOW CHECKS.
0529 CSR 91N92
0530 CORN91 92             GOTO ZZOFPS
0531 C*
0532 CSR         ZZAMTD    ADD  ZZTOTD    ZZWK2   40
0533 CSR         ZZWK2     COMP 999                  93    '93' = CARRY
0534 CSRN93      ZZWK2     COMP -999                   93    INTO INTEGER.
0535 C*
0536 CSR 93N92   ZZAMTI    ADD  1         ZZWK3  150       ADD 'CARRY' +VE
0537 CSR    93 92ZZAMTI    SUB  1         ZZWK3            SUB 'CARRY' -VE
0538 CSR 93      ZZTOTI    ADD  ZZWK3     ZZWK3
0539 CSRN93      ZZTOTI    ADD  ZZAMTI    ZZWK3
0540 C*
0541 C* IF THE MODULUS OF ZZWK3 IS LT MOD. ZZTOTI THEN O/F HAS OCCURED
0542 CSRN92      ZZWK3     COMP ZZTOTI                 99  -92 MEANS NOS.
0543 CSR 92      ZZWK3     COMP ZZTOTI               99     NEGATIVE
0544 CSRN99                Z-ADDZZWK2     ZZTOTD
0545 CSRN99                Z-ADDZZWK3     ZZTOTI
0546 C*
0547 C* IF O/F ZEROIZE ZZAMT , IND '99' SET AND ZZTOT FIELDS LEFT INTACT.
0548 CSR 99                Z-ADD0         ZZAMT  153
0549 CSR                   GOTO ZZSEND
0550 C*
0551 C* THE 'SIGNS' ARE DIFFERENT
0552 CSR         ZZOFPS    TAG                             ** ZZOFPS TAG**
0553 C* IF ZZAMT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZTOT
0554 C*
0555 CSR 92                Z-SUBZZAMTI    ZZAMTI 150
0556 CSR 92                Z-SUBZZAMTD    ZZAMTD  30
0557 C*
0558 C* IF ZZTOT WAS NEGATIVE, MAKE IT POS. TO COMP WITH ZZAMT.
0559 C*
0560 CSR 91                Z-SUBZZTOTI    ZZTOTI
0561 CSR 91                Z-SUBZZTOTD    ZZTOTD
0562 C* BOTH ZZAMT AND ZZTOT ARE NOW POSITIVE.
0563 C*
0564 CSR         ZZTOTI    COMP ZZAMTI               93  95
0565 CSR 95      ZZTOTD    COMP ZZAMTD               93  95
0566 C*
0567 C* IF ZZTOT EQ. ZZAMT RETURN ZERO.
0568 CSR 95                Z-ADD0         ZZTOTI
0569 CSR 95                Z-ADD0         ZZTOTD
0570 CSR 95                GOTO ZZSEND
0571 C*
0572 C* IF ZZTOT GT. ZZAMT.
0573 CSR 93      ZZAMTD    COMP ZZTOTD               94
0574 CSR 93 94   ZZTOTI    SUB  1         ZZTOTI
0575 CSR 93 94   ZZTOTD    ADD  1000      ZZWK2
0576 CSR 93      ZZTOTI    SUB  ZZAMTI    ZZTOTI
0577 CSR 93 94   ZZWK2     SUB  ZZAMTD    ZZTOTD
0578 CSR 93N94   ZZTOTD    SUB  ZZAMTD    ZZTOTD
0579 C*
0580 C*
0581 C* IF ZZAMT GT. ZZTOT.
0582 CSRN93      ZZTOTD    COMP ZZAMTD               94
0583 CSRN93 94   ZZAMTI    SUB  1         ZZWK3  150
0584 CSRN93 94   ZZAMTD    ADD  1000      ZZWK2
0585 CSRN93 94   ZZWK3     SUB  ZZTOTI    ZZTOTI
0586 CSRN93N94   ZZAMTI    SUB  ZZTOTI    ZZTOTI
0587 CSRN93 94   ZZWK2     SUB  ZZTOTD    ZZTOTD
0588 CSRN93N94   ZZAMTD    SUB  ZZTOTD    ZZTOTD
0589 C*
0590 C* REVERSE SIGN OF ZZTOT IF LARGER I/P FIELDS WERE NEGATIVE
0591 CSR                   SETOF                     94
0592 CSR 93 91
0593 CORN93 92             SETON                     94
0594 CSR 94                Z-SUBZZTOTI    ZZTOTI
0595 CSR 94                Z-SUBZZTOTD    ZZTOTD
0596 C**
0597 C**   RESTORE SIGN OF ZZAMTI & ZZAMTD IF IT WAS REVERSED.
0598 CSR 92                Z-SUBZZAMTI    ZZAMTI
0599 CSR 92                Z-SUBZZAMTD    ZZAMTD
0600 CSR         ZZSEND    TAG                             ** ZZSEND TAG *
0601 C**
0602 C**   IF ZZTOTD IS ZERO, AND ZZTOTI IS NEG. SET UP ZZNEGD.
0603 CSR                   SETOF                     96
0604 CSR         ZZTOTD    COMP 0                        91
0605 CSR 91      ZZTOTI    COMP 0                      96
0606 CSR 96                MOVE '.000-'   ZZNEGD  5
0607 CSR                   ENDSR                             ** ENDSR **
0608 C********************************************************************
     C**
     C**   INFSR SR. - FILE EXCEPTION/ERROR HANDLING SUBROUTINE.
     C**
     C           INFSR     BEGSR
     C*    CHECK ERROR TYPE.
     C           STATUS    IFEQ 01021
     C*    CHAIN TO FILE TO GET CURRENT DETAILS.
     C                     EXSR MASTCH
     C*
     C           *IN72     IFEQ '0'
     C*    ROLLBACK UPDATES ALREADY DONE.
     C                     ROLBK
     C*    SET ON 'RECORD UPDATED BY ANOTHER WORKSTATION' INDICATOR.
     C                     SETON                     64
     C*    RESET OTHER INDICATORS.
     C                     SETOF                     515481
     C*
     C                     MOVEL'*DETC'   EXTTAG  6
     C*
     C                     ELSE
     C*
     C                     MOVE '*CANCL'  EXTTAG
     C                     END
     C*
     C                     ELSE
     C*
     C                     MOVE '*CANCL'  EXTTAG
     C                     END
     C*
     C                     ENDSREXTTAG
     C********************************************************************
     C**
     C**   ZTNLU1 SR. - TO LOCK THE TRANSACTION NUMBER DATA AREA AND
     C**                THEN UPDATE AND RELEASE THE DATA AREA.
     C**
     C           ZTNLU1    BEGSR
     C           *NAMVAR   DEFN MNATN     DNATN
     C           *LOCK     IN   DNATN
     C                     MOVE FNATN     NATN    50
     C                     MOVE FNATN     ZZWK05  50
     C                     ADD  1         ZZWK05
     C                     MOVE ZZWK05    FNATN
     C                     OUT  DNATN
     C                     ENDSR
     C*****************************************************************
     C*                                                                   S01117
     C* *PSSR  --- SUBROUTINE TO HANDLE ABNORMAL ERROR CONDITIONS         S01117
     C*                                                                   S01117
     C* CALLED FROM: AFTER ABNORNAL OPERATION OCCURS                      S01117
     C*                                                                   S01117
     C* CALLS: NOTHING                                                    S01117
     C*                                                                   S01117
     C********************************************************************S01117
     C**                                                                  S01117
     C*                                                                   S01117
     C           *PSSR     BEGSR                           ** *PSSR **    S01117
     C*                                                                   S01117
     C           @RUN      IFEQ *BLANKS                                   S01117
     C                     MOVE 'Y'       @RUN    1                       S01117
     C                     DUMP                                           S01117
     C                     END                                            S01117
     C*                                                                   S01117
     C                     ENDSR                           ** *PSSR **    S01117
     C********************************************************************S01117
0609 O*  WORKSTATION - INITIAL SCREEN OUTPUT
0610 O*
0611 ORE0230FME        81
0612 O                                   K8 'RE0230F1'
0613 O                 64      SCN60     60
0614 O                         SID        9
0615 O                         SPID      15
0616 O                         MTIME     23 '  .  .  '
     O                         WSID      26
0617 O*  ERROR FIELDS
0618 O                         ERMSG    114
0619 O                         ERCD     193
0650 O*
0651 O*  MASTER FILE - DETAIL RECORD UPDATE, FOR INSERT
0652 O*  WHEN NO RECORD ON FILE
0653 O*
0654 ORECRGD  EADD     51 67N68
0655 O**********               NEW1     160                                                   CGL029
     O                         NEW1     170                                                   CGL029
0656 O                         NATN      33P
0657 O*
0658 O*  MASTER FILE - DETAIL RECORD UPDATE, FOR INSERT OVER DELETED/
0659 O*  BACKED OUT RECORD AND AMEND/DELETE
0660 O*
0661 ORECRG   E        51N67
0661 O       OR        51 67 68
0662 O**********               NEW1     160                                                   CGL029
     O                         NEW1     170                                                   CGL029
0663 O                         NATN      33P
0682 O*
0683 O*  MASTER FILE - TRAILER UPDATE
0684 O*
0685 ORECRG   E        54
0686 O                         OLD3     160
0687 O                         NATN      33P
0688 O                                   34 'Y'
0689 O                         NR        49P
0690 O                         WN        89P
0691 O                         DC        99P
**   ERROR MESSAGES
THIS RECORD HAS BEEN UPDATED BY ANOTHER WORKSTATION
** CPY@   **      OBJECT COPYRIGHT
(c) Finastra International Limited 2001
