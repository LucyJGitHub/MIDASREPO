     H DEBUG
      *****************************************************************
/*STD *  RPGBASEMOD                                                   *
/*EXI *  TEXT('Midas RE Cash Management Generated Entries Report')    *
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE100716 - Cash Management Generated Entries Report          *
      *                                                               *
      *  Function:  This program lists entries generated by           *
      *             cash management. (It also updates the             *
      *             trailer with calculated values.)                  *
      *                                                               *
      *  (c) Finastra International Limited 2002                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 CGL029             Date 01Sep03               *
      *                 CRE008  *CREATE    Date 19Feb02               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CRE008 - Cash Management                                     *
      *                                                               *
      *****************************************************************
 
     FGECXL3    IP   E           K DISK    INFSR(*PSSR)
     F                                     RENAME(APOSTPDF:GECXF)
     FGECXZZ    UF A E             DISK    INFSR(*PSSR)
     FRE100716P1O    E             PRINTER INFSR(*PSSR)  USROPN
     F                                     INFDS(SPOOL1)
 
      *****************************************************************
      * Standard D Specifications
     D/COPY ZACPYSRC,RECMSTDD
      * Program Status Data Structure
     D/COPY ZACPYSRC,PSDS
      *****************************************************************
 
 
0076 D INT             S             15  0 DIM(1)
0077 D DEC             S              3  0 DIM(1)
 
 
     D SDBANK        E DS                  EXTNAME(SDBANKPD)
      ** External DS for BANK details
     D SDBRCH        E DS                  EXTNAME(SDBRCHPD)
      ** Branch Details
     D SDCURR        E DS                  EXTNAME(SDCURRPD)
      ** Currency Details
     D SDACOD        E DS                  EXTNAME(SDACODPD)
      ** A/C Code Details
     D DSFDY         E DS                  EXTNAME(DSFDY)
      ** First DS for access programs
 
 
      ** File Information Data Structure for RE100716P1
     D SPOOL1          DS
     D  PSFile1               83     92
     D  PSFNum1              123    124B 0
 
 
      ** Parameters for ZSFILE
     D PSeq            S              5
     D PLevel          S              1
     D PEnty           S              3
     D ZSnum           S              6  0
     D ZSerr           S              1
 
 
     IGECXF         01
     I                                          BRCA          L4
     I                                          CCY           L3
     I                                          ACOD          L2
     I                                          CNUM          L1
     I                                          ACSQ          L1
 
      * On change of branch
      *   Access branch
      *   Produce 'end of report' and close printer file if previously open
      *   Open printer file
      *   Register P1 printer file with RCF
 
     C   L4              EXSR      ACS_BRCH
     C   L4P1Open        IFEQ      'Y'
     C                   EXSR      END_OF_REP
     C                   CLOSE     RE100716P1
     C                   ENDIF
     C   L4              OPEN      RE100716P1
     C   L4              EXSR      RCFP1
     C   L4              MOVE      'Y'           P1Open            1
 
      * On change of currency
      *   Access currency
      *   Trigger page change
 
     C   L3              EXSR      ACS_CURR
     C   L3              Z-ADD     99            LINENO
 
      * On change of a/c code
      *   Access a/c code
 
     C   L2              MOVEL     ACOD          ACCD
     C   L2              EXSR      ACS_ACOD
 
      * Report entry
      * ------------
 
     C                   EXSR      REP_ENTRY
 
      * Accumulate posting total (by branch and currency)
 
     C     DRCR          IFEQ      0
     C                   ADD       PSTA          POST_TOT
     C                   ELSE
     C                   SUB       PSTA          POST_TOT
     C                   ENDIF
 
      * Increment count of records
 
     C                   ADD       1             CountRecs         5 0
 
      * Hash total an amount = the posting amount
 
     C     PSTA          DIV       1000          ZZAMT
     C                   EXSR      HASH
 
      * If posting total not zero
      *    Report currency imbalance
 
     CL3   POST_TOT      IFNE      0
     CL3                 EXSR      REP_CCYIMBAL
     CL3                 SETON                                        U7U8
     CL3                 ENDIF
     CL3                 Z-ADD     0             POST_TOT         17 0
 
 
      * At end of data:
      * If printer file not open
      *   Open printer file
      *   Register P1 printer file with RCF
      * Produce end of report
      * Close printer file
      * Update trailer record
 
     CLR   P1Open        IFNE      'Y'
     CLR                 OPEN      RE100716P1
     CLR                 EXSR      RCFP1
     CLR                 ENDIF
     CLR                 EXSR      END_OF_REP
     CLR                 CLOSE     RE100716P1
     CLR   1             CHAIN     APOSTZZF                           60
     CLR                 MOVEL     'T'           RECI
     CLR   CountRecs     ADD       2             NORE1
     CLR                 Z-ADD     INT(1)        HRWN
     CLR                 Z-ADD     DEC(1)        HRDC
     CLR 60              WRITE     APOSTZZF
     CLRN60              UPDATE    APOSTZZF
      /SPACE 5
      ********************************************************************
      * Report entry
      ********************************************************************
     C     REP_ENTRY     BEGSR
 
      * Space
     C     *INL1         IFEQ      '1'
     C                   Z-ADD     2             CHLINE            3 0
     C                   EXSR      PAG
     C                   WRITE     SPACE2
     C                   ENDIF
 
     C     ACNO          COMP      0                                  3636      *
 
      * Edit posting date of entry
 
     C                   Z-ADD     PSTD          ZDAYNO
     C                   EXSR      ZDATE2
     C                   MOVEL     ZADATE        R_PSTD
 
      * Edit value date of entry
 
     C                   Z-ADD     VALD          ZDAYNO
     C                   EXSR      ZDATE2
     C                   MOVEL     ZADATE        R_VALD
 
      * Edit posting amount of entry
 
     C                   MOVE      *ZERO         ZFIELD
     C                   MOVE      PSTA          ZFIELD
     C                   EXSR      ZEDIT
     C                   MOVE      ZFIELD        R_PSTA
 
      * Edit posting dr/cr indicator
 
     C     DRCR          IFEQ      0
     C                   MOVEL     'DR'          R_DRCR
     C                   ELSE
     C                   MOVEL     'CR'          R_DRCR
     C                   ENDIF
 
      * Edit Reference ID
 
     C                   MOVE      XRFI          R_XRFI
 
      * Edit Reference Number
 
     C     XRFN          IFNE      *ZERO
     C                   MOVE      XRFN          R_XRFN
     C                   ELSE
     C                   MOVE      *BLANK        R_XRFN
     C                   ENDIF
 
     C                   Z-ADD     1             CHLINE            3 0
     C                   EXSR      PAG
     C                   WRITE     ENTRY
 
     C                   ENDSR
      *******************************************************************
     C/SPACE 5
      ********************************************************************
      * Report currency imbalance
      ********************************************************************
     C     REP_CCYIMBAL  BEGSR
 
      * Edit posting total
      * and  posting total dr/cr indicator
 
     C     POST_TOT      IFGT      0
     C                   Z-ADD     POST_TOT      w_AMT            17 0
     C                   MOVEL     'DR'          R_TDRCR
     C                   ELSE
     C                   Z-SUB     POST_TOT      w_AMT
     C                   MOVEL     'CR'          R_TDRCR
     C                   ENDIF
 
     C                   MOVE      *ZERO         ZFIELD
     C                   MOVE      w_AMT         ZFIELD
     C                   EXSR      ZEDIT
     C                   MOVE      ZFIELD        R_POSTTOT
 
     C                   Z-ADD     2             CHLINE
     C                   EXSR      PAG
     C                   WRITE     CCYIMBAL
 
     C                   ENDSR
      *******************************************************************
     C/SPACE 5
      ********************************************************************
      * End of Report
      ********************************************************************
     C     END_OF_REP    BEGSR
 
      * Output count of errors and 'end of report'
 
     C                   Z-ADD     3             CHLINE
     C                   EXSR      PAG
     C                   WRITE     ENDOFREP
 
     C                   ENDSR
      *******************************************************************
      /SPACE 5
      ********************************************************************
      * Throw page headings (if required)
      ********************************************************************
     C     PAG           BEGSR
 
     C                   ADD       CHLINE        LINENO
     C     LINENO        IFGT      59
     C                   ADD       1             PAGNUM
     C                   WRITE     PAGEHEAD
     C     6             ADD       CHLINE        LINENO            3 0
     C                   ENDIF
 
     C                   ENDSR
     C********************************************************************
      /SPACE 5
      ********************************************************************
      * Access a branch
      ********************************************************************
     C     ACS_BRCH      BEGSR
 
     C                   CALLB     'ZAACSBRCH'
     C                   PARM                    BRCA
     C                   PARM                    SDBRCH
 
      * Database error if branch not found
 
     C     A8BRCD        IFEQ      *BLANK
     C                   EVAL      X_ERMS = 'BAD BRANCH CODE:' + BRCA
     C                   EXSR      *PSSR
     C                   ENDIF
 
     C                   ENDSR
      *********************************************************************
      /SPACE 5
      ********************************************************************
      * Access a currency
      ********************************************************************
     C     ACS_CURR      BEGSR
 
     C                   CALLB     'ZAACSCURR'
     C                   PARM                    CCY
     C                   PARM                    SDCURR
 
      * Database error if currency not found
 
     C     A6CYCD        IFEQ      *BLANK
     C                   EVAL      X_ERMS = 'BAD CCY CODE:' + CCY
     C                   EXSR      *PSSR
     C                   ENDIF
 
     C                   ENDSR
      *********************************************************************
      /SPACE 5
      ********************************************************************
      * Access A/c code
      ********************************************************************
     C     ACS_ACOD      BEGSR
 
     C                   CALLB     'ZAACSACOD'
     C**********         PARM                    ACCD              4                          CGL029
     C                   PARM                    ACCD             10                          CGL029
     C                   PARM                    SDACOD
 
      * Database error if a/c code not found
 
     C     A5ACCD        IFEQ      *BLANK
     C                   EVAL      X_ERMS = 'BAD A/C CODE:' + ACCD
     C                   EXSR      *PSSR
     C                   ENDIF
 
     C                   ENDSR
      *******************************************************************
      /SPACE 5
      ********************************************************************
      * Convert a day number into a date
      ********************************************************************
     C     ZDATE2        BEGSR
     C                   CALLB     'ZDATE2'
     C                   PARM                    ZDAYNO            5 0
     C                   PARM      'M'           ZDFIN             1
     C                   PARM      *ZERO         ZDATEN            6 0
     C                   PARM      *BLANK        ZADATE            7
     C                   ENDSR
      *********************************************************************
      /SPACE 5
      ********************************************************************
      * Edit an amount
      ********************************************************************
     C     ZEDIT         BEGSR
     C                   CALLB     'ZEDIT'
     C                   PARM                    ZFIELD           16
     C                   PARM      A6NBDP        ZADEC             1 0
     C                   ENDSR
      *********************************************************************
      /SPACE 5
      ********************************************************************
      * Hash total an amount
      ********************************************************************
     C     HASH          BEGSR
 
     C                   Z-ADD     INT(1)        ZZTOTI           15 0
     C                   Z-ADD     DEC(1)        ZZTOTD            3 0
 
     C     ZZAMT         IFLT      0
     C                   Z-SUB     ZZAMT         ZZAMT            15 3
     C                   END
 
     C                   Z-ADD     ZZAMT         ZZAMTI           15 0
     C                   MOVE      ZZAMT         ZZAMTD            3 0
     C                   ADD       ZZAMTD        ZZTOTD
     C     ZZTOTD        IFLT      ZZAMTD
     C                   ADD       1             ZZTOTI
     C                   END
     C                   ADD       ZZAMTI        ZZTOTI
     C                   Z-ADD     ZZTOTI        INT(1)
     C                   Z-ADD     ZZTOTD        DEC(1)
 
     C                   ENDSR
      *********************************************************************
      /SPACE 5
      ********************************************************************
      * Register P1 printer file with RCF
      ********************************************************************
     C     RCFP1         BEGSR
 
     C                   EVAL      ZSnum = PSFNum1
 
     C                   CALL      'ZSFILE'
     C                   PARM                    PSeq
     C                   PARM      BRCA          PEnty
     C                   PARM                    PSFile1
     C                   PARM                    ZSnum
     C                   PARM      *Blank        ZSErr
 
      ** If Error occurs during ZSFILE processing, then end abnormally
 
     C                   IF        ZSErr = 'Y'
     C                   EVAL      X_ERMS = 'Call to ZSFILE in error'
     C                   EXSR      *PSSR
     C                   ENDIF
 
     C                   ENDSR
      *********************************************************************
      /SPACE 5
      ********************************************************************
      * *INZSR --- INITIAL PROCESSING
      ********************************************************************
     C     *INZSR        BEGSR
 
      *  Access Bank Details
 
     C                   CALL      'AOBANKR0'
     C                   PARM      '*DBERR  '    @RTCD             7
     C                   PARM      '*FIRST  '    @OPTN             7
     C     SDBANK        PARM      SDBANK        DSFDY
 
      * Throw a page
 
     C                   Z-ADD     99            LINENO
 
     C                   ENDSR
      *******************************************************************
      /SPACE 5
      ********************************************************************
      * * P S S R  --- A B N O R M A L   E R R O R   C O N D I T I O N S
      /COPY ZACPYSRC,RECMPSSR
      *****************************************************************
