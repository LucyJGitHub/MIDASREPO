     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Password file maintenance')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE2002 - Password File Maintenance                           *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CRE023  *CREATE    Date 29Jul05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CRE023 - 2 O'clock Flag Upgrade to MidasPlus                 *
      *                                                               *
      *****************************************************************
      /EJECT
     F*****************************************************************
     FRE2002DFCF  E                    WORKSTN
     FREOSECL0UF  E           K        DISK         KCOMIT       A
      **
      ** INDICATORS FUNCTION
      **
      **      77    Master ID already set
      **      88    Error indicator
      *****************************************************
     E                    NCHR   37  37  1
     E                    ECOD   41  82  1
     E                    AMSG    1  12 30
     E                    CHEK        8  1
     E                    APAS        8  1
      *
      ** Data structure to update last transaction number DTAARA
     IDNATN       DS                              5
     I                                        1   50FNATN
      *
      ** Program status data structure
     IPSDS       SDS
     I                                      244 246 PWSID
     I                                      247 248 WSID
     I                                      254 263 USRID
      *
      ** Data structure to provide text on COMIT entry in journal
     ICMTTXT      DS
     I                                        1   50NATN
     I                                        6   7 MDID
     I                                        8  15 PGMN
     I                                       16  18 WSIDX
     I                                       19  240MTIME
     I                                       25  25 SCHTP
     I                                       26  35 USRIDX
     I                                       51 306 SLOT1
     I                                      307 365 SLOT2X
     I                                      366 371 SLNBRX
     I                                      372 3770SLNBR
     I                                      378 378 SACTCD
      *                                                                   SAR005
      *****************************************************************
      *                                                               *
      *  Start process                                                *
      *                                                               *
      *****************************************************************
     C                     MOVEL'99999999'TRAIL   8
     C                     EXSR P100
     C                     SETON                     LR
     C                     RETRN
      *****************************************************************
      *                                                               *
      *  P100 - Screen-1 (Master ID and Master password)              *
      *                                                               *
      *****************************************************************
     C           P100      BEGSR
     C           *INKC     DOWEQ'0'
     C                     MOVEL*BLANKS   MPSWD
     C                     EXFMTRE2002S1
     C                     MOVE '0'       *IN88
     C                     MOVE *BLANKS   ERRMSG
     C                     SELEC
     C           *INKC     WHEQ '1'
     C                     OTHER
     C                     EXSR V100
     C  N88                EXSR P200
     C                     ENDSL
     C                     ENDDO
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  V100 - Screen-1 Validation                                   *
      *                                                               *
      *****************************************************************
     C           V100      BEGSR
     C           SMSID     CHAINREOSECL0            N99
     C           *IN99     IFEQ '0'
     C           GRECI     ANDEQ'H'
     C                     MOVEAMPSWD     CHEK
     C                     EXSR ENCR
     C                     MOVEAAPAS      WPSWD   8
     C           WPSWD     IFNE GMPAS
     C                     MOVE '1'       *IN88
     C                     MOVELAMSG,2    ERRMSG
     C                     ENDIF
     C                     ELSE
     C                     MOVE '1'       *IN88
     C                     MOVELAMSG,1    ERRMSG
     C                     ENDIF
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  P200 - Screen-2 (Get Action code)                            *
      *                                                               *
      *****************************************************************
     C           P200      BEGSR
     C           *INKC     DOWEQ'0'
     C                     EXFMTRE2002S2
     C                     MOVE '0'       *IN88
     C                     MOVE *BLANKS   ERRMSG
     C                     SELEC
     C           *INKC     WHEQ '1'
     C           ACODE     WHEQ 'I'
     C                     EXSR V210
     C  N88                EXSR P600
     C  N88                MOVE *BLANKS   ACODE
     C  N88                MOVE *BLANKS   USID
     C           ACODE     WHEQ 'D'
     C                     EXSR V220
     C  N88                EXSR P700
     C  N88                MOVE *BLANKS   ACODE
     C  N88                MOVE *BLANKS   USID
     C           ACODE     WHEQ 'M'
     C                     EXSR V205
     C  N88                EXSR P500
     C                     MOVE *BLANKS   ACODE
     C                     MOVE *BLANKS   USID
     C                     OTHER
     C                     MOVE '1'       *IN88
     C                     MOVE AMSG,3    ERRMSG
     C                     ENDSL
     C                     ENDDO
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  V205 - Screen-2 Validation  (Master ID)                      *
      *                                                               *
      *****************************************************************
     C           V205      BEGSR
     C           USID      CHAINREOSECL0            N99
     C           *IN99     IFEQ '1'
     C           GRECI     ORNE 'H'
     C                     MOVE '1'       *IN88
     C                     MOVELAMSG,1    ERRMSG
     C                     ENDIF
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  V210 - Screen-2 Validation (Insert Action)                   *
      *                                                               *
      *****************************************************************
     C           V210      BEGSR
     C                     MOVEAUSID      CHEK
     C                     EXSR UIDCHK
     C           *IN88     IFEQ '0'
     C           USID      CHAINREOSECL0            N99
     C           *IN99     IFEQ '0'
     C           GRECI     IFEQ 'D'
     C           GRECI     OREQ 'H'
     C           GRECI     OREQ 'T'
     C                     MOVE '1'       *IN88
     C                     MOVELAMSG,12   ERRMSG
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  V220 - Screen-2 Validation (Delete Action)                   *
      *                                                               *
      *****************************************************************
     C           V220      BEGSR
     C           USID      CHAINREOSECL0            N99
     C           *IN99     IFEQ '1'
     C           GRECI     ORNE 'D'
     C                     MOVE '1'       *IN88
     C                     MOVELAMSG,4    ERRMSG
     C                     ENDIF
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  P500 - Screen-5 (Master Password change)                     *
      *                                                               *
      *****************************************************************
     C           P500      BEGSR
     C           *INKC     DOWEQ'0'
     C           *INKL     ANDEQ'0'
     C                     MOVE *BLANKS   MPSWD
     C                     MOVE *BLANKS   NMPSWD
     C                     EXFMTRE2002S5
     C                     MOVE '0'       *IN88
     C                     MOVE *BLANKS   ERRMSG
     C                     SELEC
     C           *INKC     WHEQ '1'
     C           *INKL     WHEQ '1'
     C                     OTHER
     C                     EXSR V500
     C  N88                EXSR P501
     C  N88                LEAVE
     C                     ENDSL
     C                     ENDDO
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  V500 - Screen-5 Validation(MASTER PASS CHANGE)               *
      *                                                               *
      *****************************************************************
     C           V500      BEGSR
     C           USID      CHAINREOSECL0            N99
     C           *IN99     IFEQ '0'
     C           GRECI     ANDEQ'H'
     C                     MOVEAMPSWD     CHEK
     C                     EXSR ENCR
     C                     MOVEAAPAS      WPSWD   8
     C           WPSWD     IFNE GMPAS
     C                     MOVE '1'       *IN88
     C                     MOVELAMSG,9    ERRMSG
     C                     ENDIF
     C                     ELSE
     C                     MOVE '1'       *IN88
     C                     MOVELAMSG,11   ERRMSG
     C                     ENDIF
     C           *IN88     IFEQ '0'
     C                     MOVEANMPSWD    CHEK
     C                     EXSR PASCHK
     C   88                MOVELAMSG,10   ERRMSG
     C                     ENDIF
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  P600 - Input Screen 3                                        *
      *                                                               *
      *****************************************************************
     C           P600      BEGSR
     C                     MOVE *BLANKS   PSWD
     C                     MOVE *BLANKS   SCTYP
     C                     EXFMTRE2002S3
     C                     MOVE '0'       *IN88
     C                     MOVE *BLANKS   ERRMSG
     C                     SELEC
     C           *INKC     WHEQ '1'
     C           *INKL     WHEQ '1'
     C                     OTHER
     C                     EXSR V600
     C  N88                EXSR V601
     C  N88                EXSR P602
     C                     ENDSL
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  V600 - Validate screen 3 - Password                          *
      *                                                               *
      *****************************************************************
     C           V600      BEGSR
     C                     MOVEAPSWD      CHEK
     C                     EXSR PASCHK
     C   88                MOVELAMSG,7    ERRMSG
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  V601 - Validate screen 4 - class type                        *
      *                                                               *
      *****************************************************************
     C           V601      BEGSR
     C           SCTYP     IFEQ *BLANKS
     C                     MOVEL'A'       SCTYP
     C                     ENDIF
     C           SCTYP     IFLT 'A'
     C           SCTYP     ORGT 'Z'
     C                     MOVELAMSG,8    ERRMSG
     C                     MOVE '1'       *IN88
     C                     ENDIF
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  P700 - Screen-4 (Delete screen)                              *
      *                                                               *
      *****************************************************************
     C           P700      BEGSR
     C           *INKC     DOWEQ'0'
     C           *INKJ     ANDEQ'0'
     C           *INKL     ANDEQ'0'
     C                     EXFMTRE2002S4
     C                     MOVE '0'       *IN88
     C                     MOVE *BLANKS   ERRMSG
     C                     SELEC
     C           *INKC     WHEQ '1'
     C           *INKJ     WHEQ '1'
     C                     EXSR P701
     C           *INKL     WHEQ '1'
     C                     OTHER
     C                     MOVELAMSG,6    ERRMSG
     C                     MOVE '1'       *IN88
     C                     ENDSL
     C                     ENDDO
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  P701 - DELETE A USER                                         *
      *                                                               *
      *****************************************************************
     C           P701      BEGSR
     C                     MOVE '1'       *IN87
     C           USID      CHAINREOSECL0             99
     C           *IN99     IFEQ '0'
     C           GRECI     ANDNE'*'
     C                     MOVEL'*'       GRECI
     C                     UPDATREOSECD0               87
     C           TRAIL     CHAINREOSECL0             99
     C           *IN99     IFEQ '0'
     C           GRECI     ANDEQ'T'
     C                     SUB  1         GNORE
     C  N87                UPDATREOSECT0               87
     C                     ENDIF
     C                     ELSE
     C                     MOVELAMSG,11   ERRMSG
     C                     MOVE '1'       *IN88
     C                     ENDIF
     C  N87                EXSR CMTUPD
     C  N87      CMTTXT    COMIT
     C   87                ROLBK
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  P602 - Insert User-Name, Passw , GCTYP - New                 *
      *                                                               *
      *****************************************************************
     C           P602      BEGSR
     C                     MOVE '1'       *IN87
     C                     MOVEAPSWD      CHEK
     C                     EXSR ENCR
     C           USID      CHAINREOSECL0             99
     C           *IN99     IFEQ '1'
     C                     MOVEAAPAS      GPSWD
     C                     MOVEL'D'       GRECI
     C                     MOVELUSID      GUSID
     C                     MOVELSCTYP     GCTYP
     C                     WRITEREOSECD0               87
     C           TRAIL     CHAINREOSECL0             99
     C           *IN99     IFEQ '0'
     C           GRECI     ANDEQ'T'
     C                     ADD  1         GNORE
     C  N87                UPDATREOSECT0               87
     C                     ELSE
     C                     MOVE '1'       *IN87
     C                     ENDIF
     C                     ELSE
     C           GRECI     IFEQ '*'
     C                     MOVEAAPAS      GPSWD
     C                     MOVEL'D'       GRECI
     C                     MOVELUSID      GUSID
     C                     MOVELSCTYP     GCTYP
     C                     UPDATREOSECD0               87
     C           TRAIL     CHAINREOSECL0             99
     C           *IN99     IFEQ '0'
     C           GRECI     ANDEQ'T'
     C                     ADD  1         GNORE
     C  N87                UPDATREOSECT0               87
     C                     ELSE
     C                     MOVE '1'       *IN87
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C  N87                EXSR CMTUPD
     C  N87      CMTTXT    COMIT
     C   87                ROLBK
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  P501 - Save Master Password - New                            *
      *                                                               *
      *****************************************************************
     C           P501      BEGSR
     C                     MOVE '1'       *IN87
     C                     MOVEANMPSWD    CHEK
     C                     EXSR ENCR
     C           USID      CHAINREOSECL0             99
     C           *IN99     IFEQ '0'
     C           GRECI     ANDEQ'H'
     C                     MOVEAAPAS      GMPAS
     C                     UPDATREOSECA0               87
     C                     ENDIF
     C  N87                EXSR CMTUPD
     C  N87      CMTTXT    COMIT
     C   87                ROLBK
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  UIDCHK - Check user ID                                       *
      *                                                               *
      *****************************************************************
     C           UIDCHK    BEGSR
     C           CHEK,1    IFEQ ' '
     C           CHEK,1    OREQ '0'
     C                     MOVE '1'       *IN88
     C                     ENDIF
     C                     Z-ADD0         II      20
     C                     Z-ADD0         JJ      20
     C           II        DOWLT8
     C                     ADD  1         II
     C           CHEK,II   LOKUPNCHR                     99
     C           *IN99     IFEQ '0'
     C                     MOVE '1'       *IN88
     C                     ENDIF
     C           CHEK,II   IFEQ '9'
     C                     ADD  1         JJ
     C                     ENDIF
     C   88                LEAVE
     C                     ENDDO
     C           JJ        IFGE 8
     C                     MOVE '1'       *IN88
     C                     ENDIF
     C           *IN88     IFEQ '1'
     C                     MOVELAMSG,4    ERRMSG
     C                     ENDIF
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  PASCHK - Check user password                                *
      *                                                               *
      *****************************************************************
     C           PASCHK    BEGSR
     C           CHEK,1    IFEQ ' '
     C           CHEK,1    OREQ '0'
     C                     MOVE '1'       *IN88
     C                     ENDIF
     C                     Z-ADD0         II      20
     C           II        DOWLT8
     C                     ADD  1         II
     C           CHEK,II   LOKUPNCHR                     99
     C           *IN99     IFEQ '0'
     C                     MOVE '1'       *IN88
     C                     ENDIF
     C   88                LEAVE
     C                     ENDDO
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  ENCR - PASSWORD ENCRYPTION                                   *
      *                                                               *
      *****************************************************************
     C           ENCR      BEGSR
      *
     C                     SETOF                     30
      *
     C                     Z-ADD1         P       20
     C           CHEK,8    LOKUPNCHR,P                   30
     C                     Z-ADD1         N       20
      *
     C           ENCD      TAG
      *
     C                     Z-ADD1         S       20
     C           CHEK,N    LOKUPNCHR,S                   30
     C           P         ADD  N         P
     C           P         ADD  S         P
     C                     MOVEAECOD,P    APAS,N
     C                     Z-ADDS         P
     C           N         ADD  1         N
     C           N         COMP 8                      3030
      *
     C           *IN30     IFEQ '1'
     C                     GOTO ENCD
     C                     ENDIF
      *
     C                     ENDSR
      *
      ** Obtain last transaction number and set up standard output
      ** information
      *
     C           CMTUPD    BEGSR
     C                     EXSR ZTNLU1
     C                     MOVE 'RE'      MDID    2
     C                     MOVEL'RE2002'  PGMN    8
     C                     MOVE WSID      WSIDX   3
     C                     MOVE USRID     USRIDX 10
     C                     TIME           MTIME
     C                     ENDSR
     C/TITLE SR/ZTNLU1
     C/COPY ZSRSRC,ZTNLU1Z2
     C**
      *****************************************************
**  Alphanumer characters.
ABCDEFGHIJKLMNOPQRSTUVWXYZ 0123456789
**  Encryption code.
SDKJFIENVQPWOCMUXZ293847501LRBAGYHT6ZSDKJ
FIENVQPWOCMUXZ293847501LRBAGYHT6ZSDKJFIEN
**  Error messages.
INVALID MASTER USER ID
INVALID MASTER PASSWORD
INVALID ACTION CODE
INVALID USER ID
INVALID CMD KEY
PRESS CMD/10 TO DELETE
INVALID PASSWORD
INVALID CLASS TYPE
OLD MASTER PASSWORD NOT MATCH
NEW MASTER PASSWORD NOT MATCH
RECORD NOT FOUND
RECORD ALREADY EXISTS
