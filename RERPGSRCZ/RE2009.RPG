     H        1   Y
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Outward Clearing Trans.Types Enquiry')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Banking                                       *
      *                                                               *
      *  RE2009 - Outward Clearing Transaction Types Enquiry          *
      *                                                               *
      *  (c) Finastra International Limited 2005                      *
      *                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      *  Prev Amend No. MD035554           Date 25May16               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD027             Date 09Dec05               *
      *                 CRE023  *CREATE    Date 29Jul05               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  MD046248 - Finastra Rebranding                               *
      *  MD035554 - 2o'clock/ABC compatibility (Recompile)            *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CRE023 - 2 O'clock Flag Upgrade to MidasPlus                 *
      *                                                               *
     ******************************************************************
      **
      **   INDICATORS FUNCTION
      **
      **      21    INPUT #POS
      **      25    Sfldsp
      **      26    Sfldspctl
      **      27    Sflnxtchg
      **      30    Sflend
      **      88    Error
      **      99    General
      **
     FRE2009DFCF  E                    WORKSTN
     F                                        RRN01 KSFILE SFL01
     F                                              KINFDS INFDS#
      *
     FRETTYPL0IF  E           K        DISK
      *
     E                    FTR     1   1 78               FOOTER
      *
      ** Data structures:
     IPGMDS     ESDSY2PGDSP
      *
      ** Program data structure
     IJBDTTM      DS
      *
      ** Job date/time
     I                                        1   60##JDT
     I                                        1   20##JYY
     I                                        3   40##JMM
     I                                        5   60##JDD
     I                                        7  120##JTM
     I                                        7   80##JHH
     I                                        9  100##JNN
     I                                       11  120##JSS
     IINFDS#    E DSY2I#DSP
      *
      ** Display file information data structure
     IRUNDAT      DS
     I                                        1   7 MRDT
     I                                    P   8  100RDNB
     I                                       11  11 SUC
     I                                       12  12 DFF
     I                                       13  13 MBIN
      *****************************************************************
      *                                                               *
      * START PROCESS                                                 *
      *                                                               *
      *****************************************************************
     C                     EXSR INIT
     C                     EXSR POSSR
     C           *INKC     DOWEQ'0'
     C           RCD01     IFGT 0
     C                     MOVE '1'       *IN25
     C                     ELSE
     C                     MOVE '0'       *IN25
     C                     CALL 'ZA0350'
     C                     PARM 'REUSRMSG'MSGFL  10
     C                     PARM 'SGX0025 'MSGID  10
     C                     PARM *BLANKS   MSDTA  45
     C                     MOVE '1'       *IN88
     C                     ENDIF
     C                     WRITEFTR01
     C                     EXSR WRTMSG
     C                     EXFMTSFC01
     C                     EXSR CLRMSG
     C                     SELEC
     C           *INKC     WHEQ '1'
     C           *IN08     WHEQ '1'
     C                     EXSR BLD10
     C           *INKE     WHEQ '1'
     C                     EXSR REFSR
     C           RCD01     WHEQ *ZEROS
     C           *IN21     WHEQ '1'
     C                     EXSR POSSR
     C                     OTHER
     C                     EXSR SCRPOS
     C                     ENDSL
     C                     ENDDO
     C                     SETON                     LR
     C                     RETRN
      *****************************************************************
      *                                                               *
      *  CLR10 - Clear subfile                                        *
      *                                                               *
      *****************************************************************
     C           CLR10     BEGSR
     C                     MOVE '1'       *IN29
     C                     WRITESFC01
     C                     MOVE '0'       *IN29
     C                     Z-ADD*ZEROS    RCD01
     C                     Z-ADD*ZEROS    RRN01   50
     C                     Z-ADD*ZEROS    RRN51   50
     C                     MOVE *BLANKS   BLDPOS  5
     C                     MOVE '0'       *IN30
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  BLD10 - Build subfile                                        *
      *                                                               *
      *****************************************************************
     C           BLD10     BEGSR
     C           BLDPOS    IFNE *BLANKS
     C           BLDPOS    SETGTRETTYPL0
     C                     ENDIF
     C                     Z-ADDRRN51     RRN01
     C                     DO   14
     C           *IN30     DOUEQ'1'
     C           GRECI     OREQ 'D'
     C                     READ RETTYPL0                 30
     C                     ENDDO
     C           *IN30     IFEQ '0'
     C                     ADD  1         RRN01
     C                     MOVELGTTYP     BLDPOS
     C                     EXSR FILSUB
     C                     WRITESFL01
     C                     ENDIF
     C                     ENDDO
     C                     Z-ADDRRN01     RRN51
     C                     EXSR SETRCD
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  FILSUB - Move file variables to subfile                      *
      *                                                               *
      *****************************************************************
     C           FILSUB    BEGSR
     C                     MOVELGTTYP     #GTTYP
     C                     MOVELGOCLR     #GOCLR
     C                     MOVELGRCLT     #GRCLT
     C                     MOVELGRCLP     #GRCLP
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  POSSR - Position to transaction                              *
      *                                                               *
      *****************************************************************
     C           POSSR     BEGSR
     C                     MOVEL#POS      SPOSN   5
     C           SPOSN     CHAINRETTYPL0             99
     C           *IN99     IFEQ '0'
     C           SPOSN     SETLLRETTYPL0
     C                     ELSE
     C           SPOSN     SETGTRETTYPL0
     C                     READPRETTYPL0                 99
     C           *IN99     IFEQ '0'
     C                     MOVELGTTYP     SPOSN
     C           SPOSN     SETLLRETTYPL0
     C                     ELSE
     C           *LOVAL    SETLLRETTYPL0
     C                     READ RETTYPL0                 99
     C           *IN99     IFEQ '0'
     C                     MOVELGTTYP     SPOSN
     C           *LOVAL    SETLLRETTYPL0
     C                     ENDIF
     C                     ENDIF
     C                     ENDIF
     C                     EXSR CLR10
     C                     EXSR BLD10
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SETRCD - Set subfile record                                  *
      *                                                               *
      *****************************************************************
     C           SETRCD    BEGSR
     C           RRN01     IFGT 0
     C           RRN01     DIV  14        RCD01
     C                     MVR            REMDR   40
     C           REMDR     IFNE 0
     C                     ADD  1         RCD01
     C                     ENDIF
     C           RCD01     MULT 14        RCD01
     C                     SUB  13        RCD01
     C                     ENDIF
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  SCRPOS - Screen position                                     *
      *                                                               *
      *****************************************************************
     C           SCRPOS    BEGSR
     C           @#SFRC    IFGT 0
     C                     Z-ADD@#SFRC    RCD01
     C                     ENDIF
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  REFSR - Refresh                                              *
      *                                                               *
      *****************************************************************
     C           REFSR     BEGSR
     C                     EXSR POSSR
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  WRTMSG - Write message                                       *
      *                                                               *
      *****************************************************************
     C           WRTMSG    BEGSR
     C           *IN88     IFEQ '1'
     C                     WRITE#MSGCTL
     C                     ENDIF
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  CLRMSG - Clear message                                       *
      *                                                               *
      *****************************************************************
     C           CLRMSG    BEGSR
     C           *IN88     IFEQ '1'
     C                     MOVE '0'       *IN88
     C                     CALL 'ZA0250'
     C                     WRITE#MSGCTL
     C                     ENDIF
     C                     MOVE *BLANKS   MSGID
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  INIT - Initialise                                            *
      *                                                               *
      *****************************************************************
     C           INIT      BEGSR
     C                     MOVE '1'       *IN26
     C                     MOVELFTR,1     #FTR01
      *
      ** Setup job date/time
      *
     C                     Z-ADDUDATE     ##JDT
     C                     TIME           ##JTM
      *
      ** Get Rundate - Rundate  *
      *
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C                     MOVELMRDT      ##MRDT           RUN DATE
     C                     Z-ADDRDNB      WRDNB   50
     C                     ENDSR
**
F3=Main Menu   F5=Refresh
