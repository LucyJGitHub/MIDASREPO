     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas Manual Adjustment to Int.Pay.History')           *
     F********************************************************************
     F*                                                                  *
     F*  Midas - Retail Module
     F*                                                                  *
     F*    RE2192 - MANUAL ADJUSTMENT TO INTEREST PAYMENT HISTORY        *
     F*                                                                  *
     F*    Function:  This program is designed to refund BRT when        *
     F*               either a gross interest registration form or a     *
     F*               non-resident declaration form is received from     *
     F*               an account holder.                                 *
     F*                                                                  *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 05Feb18               *
      *  Prev Amend No. CDL099             Date 06Oct17               *
      *                 CGL165             Date 15Feb17               *
      *                 CDL096             Date 22Sep14               *
      *                 CDL094             Date 11Jun14               *
      *                 CER059             Date 19Jul10               *
      *                 CER043             Date 19May08               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CDL049             Date 11May06               *
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 237063             Date 20Nov05               *
      *                 CDL038             Date 10May05               *
      *                 BUG7411            Date 01Jun05               *
      *                 CDL028             Date 07Feb05               *
      *                 CSW037A            Date 02May05               *
      *                 229544             Date 15Sep04               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CDL020             Date 03Feb04               *
      *                 CDL019             Date 03Feb04               *
      *                 CDL018             Date 03Feb04               *
      *                 CDL017             Date 03Feb04               *
      *                 CDL016             Date 03Feb04               *
      *                 CGL029             Date 01Sep03               *
      *                 222727             Date 05Nov03               *
      *                 CDL010             Date 02Aug02               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.05 -----------------------------------------------*
      *                 192085             Date 07Mar01               *
      *                 CRE007             Date 01Mar01               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD005             Date 02Mar99               *
      *                 CTI002             DATE 09Sep98               *
     F*                          096730              DATE 22JAN98        *
     F*                          S01383              DATE 27APR92        *
     F*                                                                  *
     F*------------------------------------------------------------------*
     F*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *  CDL099 - Split Value Date (Recompile)                        *
      *  CGL165 - Dual Withholding Tax (Recompile)                    *
      *  CDL096 - Business Day Conventions on MM Deals                *
      *           (Recompile)                                         *
      *  CDL094 - Enhance  Receive Settlement Instructions            *
      *           (Recompiled)                                        *
      *  CER059 - German Feature Upgrade to Delhi                     *
      *  CER043 - German Features LF041-00 New Fields and Defaulting  *
      *           (Recompile)                                         *
      *  CDL049 - Addition of a Reference Rate field (recompile)      *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  237063 - EU Tax fixes upgrade to MP build 103 (Recompile).   *
      *  CDL038 - Extended Deal Sub Type (Recompile)                  *
      *  BUG7411- CDL028 fields missing from DLGHISPD (Recompile)     *
      *  CDL028 - Automatic Rate Interpolations (Recompile)           *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  229544 - Recompiled due to inserted fields in DLCHISPD.      *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CDL020 - Apply Base Rate at Value Date (Recompile)           *
      *  CDL019 - Allow Base Rate Changes on Fixed Deposits/Loans     *
      *           (Recompile)                                         *
      *  CDL018 - Multiple Settlement Accounts on Loans/Deposits      *
      *           (Recompile)                                         *
      *  CDL017 - Penalty Fee & Early Maturity of Fixed Term Loan/Dep *
      *           (Recompile)                                         *
      *  CDL016 - Automatic rollover of Fixed Term Loans/Deposits     *
      *           (Recompile)                                         *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  222727 - Release 5.0 errors  (Recompile)                     *
     F*  CDL010 - Prevent last user that actioned the trade from      *
     F*           authorising it.Recompile due to changes in FXDEALPP *
     F*           MMDELDPP,MMDENBPP and MMDENSPP.                        *
      *  192085 - When validating the deal number need to chain to the*  *
      *           deals history file aswell as the current deals file.*  *
      *           Use DEALSALL instead of DEALS so can access DLCHISPD*  *
     F*  CRE007 - Base Rate Tax 2001.                                    *
      *  CSD005 - Standing Data Account Narrative.  Add new field     *
      *           to SDACODPD.  Recompilation of programs.            *
      *  CTI002 - Recompiled : File SDACODPD changed                  *
     F*       096730 - Allow adjustment to be entered for a/c holder     *
     F*                type 'B' (Bank) or 'C' (Corporate), as may have   *
     F*                been initially entered as 'I' (Individual) or     *
     F*                'J' (Joint Account) and been changed, and so may  *
     F*                require adjustment of Base Rate Tax - this is     *
     F*                already allowed for 'O' (Overseas bank/corp).     *
     F*       S01383 - BASIC RATE TAX INCORPORATION                      *
     F*                NEW PROGRAM                                       *
     F*                                                                  *
     F********************************************************************
     FTABTG10 IF  E                    DISK
     FSDCBRTL1IF  E           K        DISK
     FACCNT   IF  E           K        DISK
     F            ACCNTAAF                          KIGNORE
     F            ACCNTACF                          KIGNORE
     F***********DEALS   IF  E           K        DISK                                     192085
     FDEALSALLIF  E           K        DISK                                                192085
     F*********** DEALSDAF                          KIGNORE                                192085
     F            DEALSDBF                          KIGNORE
     F            DEALSDDF                          KIGNORE
     F            DEALSDEF                          KIGNORE
     F*********** DEALSDFF                          KIGNORE                                192085
     F            DEALSDGF                          KIGNORE                                192085
     F            MMDELDP0                          KIGNORE                                192085
     F            MMDENBP0                          KIGNORE                                192085
     F            MMDENSP0                          KIGNORE                                192085
     F            FXDEALP0                          KIGNORE                                192085
     F            DLBHISD0                          KIGNORE                                192085
     F            DLDHISD0                          KIGNORE                                192085
     F            DLEHISD0                          KIGNORE                                192085
     F            DLGHISD0                          KIGNORE                                192085
     FINTPSL26UF  E           K        DISK         KCOMIT
     F            INTPSDDF                          KRENAMEINTPS26
     F            COINTDDF                          KRENAMECOINT26
     FINTPSL27UF  E           K        DISK         KCOMIT
     F            INTPSDDF                          KRENAMEINTPS27
     F            COINTDDF                          KRENAMECOINT27
     FINTPSL28UF  E           K        DISK         KCOMIT
     F            INTPSDDF                          KRENAMEINTPS28
     F            COINTDDF                          KRENAMECOINT28
     FINTPSL29UF  E           K        DISK         KCOMIT
     F            INTPSDDF                          KRENAMEINTPS29
     F            COINTDDF                          KRENAMECOINT29
     FINTPSDD O   E                    DISK         KCOMIT
      * Display File
     FRE2192FMCF  E                    WORKSTN
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *    F U N C T I O N    O F   I N D I C A T O R S               *
      *    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~               *
      *         01         CMD/1 PRESSED                              *
      *         12         CMD/12 PRESSED                             *
      *         30         ERROR FOUND                                *
      *         31         CUSTOMER NUMBER IN ERROR                   *
      *         32         DEAL NUMBER IN ERROR                       *
      *         33         DEAL CURRENCY IN ERROR                     *
      *         34         ACCOUNT CURRENCY IN ERROR                  *
      *         35         ACCOUNT CODE IN ERROR                      *
      *         36         ACCOUNT SEQUENCE IN ERROR                  *
      *         37         CREDITED BRT (STERLING) IN ERROR           *
      *         38         CREDITED BRT (ORIG. CCY) IN ERROR          *
      *         39         'CONFIRM' FIELD IN ERROR                   *
      *         40         ORIGINAL CCY BRT DEDUCTED                  *
      *         41         BASE CCY BRT DEDUCTED                      *
      *         42         'CONFIRM Y/N' LINE                         *
      *         43         BRANCH IN ERROR                            *
      *         44         SYSTEM IS MULTI-BRANCHING                  *
      *         50         'CONFIRM' SCREEN TO APPEAR ON SCREEN       *
      *         80         FAILED CHAIN INTPSL27/INTPSL29             *
      *         81         FAILED CHAIN INTPSL26/INTPSL28             *
      *         85         CHAIN FAIL INDICATOR                       *
      *         90 - 99    USED IN STANDARD SUBROUTINES               *
      *         U7U8       DATABASE ERROR                             *
      *         LR         END OF PROGRAM                             *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *          I N D E X   O F   S U B R O U T I N E S              *
      *          ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~              *
      *          INIT   -  INITIAL PROCESSING                         *
      *          INPUT  -  MAIN INPUT PROCESSING                      *
      *          VALID1 -  INPUT VALIDATION                           *
      *          VALID2 -  CONFIRMATION VALIDATION                    *
      *          CLEAR  -  CLEAR INPUT SCREEN                         *
      *          WRITE  -  WRITE RECORD TO INTPSDD                    *
      *          WRITNO -  WRITE RECORD FOR ACCOUNT HOLDER TYPES N&O  *
      *                                                               *
      *****************************************************************
      /EJECT
      *****************************************************************
     E/COPY ZSRSRC,ZALIGNZ1
     E*
     E                    ERNO       38  4               ERROR CODES
     E*
     E** Arrays to hold details from TABTG10
     E*
     E                    CCYD      150  3               CURRENCY CODES
     E                    CDPD      150  1 0             DECIMAL PLACES
     E*
     E*****************************************************************
     I*
     I* Data structure for program status data area
     I*
     IPSDS       SDS
     I                                      244 253 WID
     I                                      254 263 USRID
     I*
     I** Data structure for error messages
     I*
     ISCRMSG      DS
     I                                        1 152 ERNO
     I                                        1  76 ERCD
     I                                       77 152 ERMSG
     I**
     I**   DATA AREA RUNDAT
     I**
     IRUNDAT      DS
     I                                       13  13 MBIN
     I**
     I** DATA AREA OF MENU USER
     I**
     IZMUSER      DS                             17
     I                                       11  13 USRBCH
     I*
     ILDA        UDS
     I                                        1 256 DBLDA
     I                                      134 183 DBLOT
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     I*
     I*  Data structure to update last transaction no. DTAARA
     I*
     IDNATN       DS                              5
     I                                        1   50FNATN
     I*
     I** Data structure to provide text on COMIT entry in journal
     I*
     ICMTTXT      DS
     I                                        1   50NATN
     I                                        6   7 MDID
     I                                        8  15 PGMN
     I                                       16  18 WSIDX
     I                                       19  240MTIME
     I                                       25  34 USIDX
     I*
     I** Data structures to hold details of keylists ACCKEY/ACKEY2
     I*
     IKEY1        DS
     I**********                              1   60CUST                                      CSD027
     I                                        1   6 CUST                                      CSD027
     I                                        7   9 CNCY
     I**********                             10  130CODE                                      CGL029
     I**********                             14  150SEQ                                       CGL029
     I**********                             16  18 BRCH                                      CGL029
     I**********                              1  18 ACKEY                                     CGL029
     I                                       10  190CODE                                      CGL029
     I                                       20  210SEQ                                       CGL029
     I                                       22  24 BRCH                                      CGL029
     I                                        1  24 ACKEY                                     CGL029
     I*
     IKEY2        DS
     I**********                              1   60CST                                       CSD027
     I                                        1   6 CST                                       CSD027
     I                                        7   9 CUR
     I**********                             10  130COD                                       CGL029
     I**********                             14  150SQ                                        CGL029
     I**********                             16  18 BR                                        CGL029
     I                                       10  190COD                                       CGL029
     I                                       20  210SQ                                        CGL029
     I                                       22  24 BR                                        CGL029
     I*
     I@BANK     E DSSDBANKPD
     I** DUMMY RECORD FORMATS FOR ACCESS TO BANK DETAILS
     I@TRAD     E DSSDTRADPD
     I** DUMMY RECORD FORMATS FOR ACCESS TO TRADING DATA DETAILS
     ISDCUST    E DSSDCUSTPD
     I** EXTERNAL DS FOR CUSTOMER DETAILS
     ISDCURR    E DSSDCURRPD
     I**  EXTERNAL DS FOR CURRENCY CODES
     ISDACOD    E DSSDACODPD
     I              QQACCD                          QQACC1                                    CGL029
     I**  EXTERNAL DS FOR ACCOUNT CODES
     ISDBRCH    E DSSDBRCHPD
     I              QQDFAC                          QQDFA1                                    CGL029
     I**  EXTERNAL DS FOR BRANCH CODES
     IDSFDY     E DSDSFDY
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
     IDSSDY     E DSDSSDY
     I* SECOND DS FOR ACCESS PROGRAMS, LONG DATA STRUCTURE
      *****************************************************************
      /EJECT
      *****************************************************************
     C*
     C**   MAIN PROCESSING
     C**   ~~~~~~~~~~~~~~~
     C*
     C** Set up key lists to access files ACCNT, INTPSL*
     C*
     C           ACCKEY    KLIST
     C**********           KFLD           CUST    60                                          CSD027
     C                     KFLD           CUST    6                                           CSD027
     C                     KFLD           CNCY    3
     C**********           KFLD           CODE    40                                          CGL029
     C                     KFLD           CODE                                                CGL029
     C                     KFLD           SEQ     20
     C                     KFLD           BRCH    3
     C*
     C           ACKEY2    KLIST
     C**********           KFLD           CST     60                                          CSD027
     C                     KFLD           CST     6                                           CSD027
     C                     KFLD           CUR     3
     C**********           KFLD           COD     40                                          CGL029
     C                     KFLD           COD                                                 CGL029
     C                     KFLD           SQ      20
     C                     KFLD           BR      3
     C*
     C                     EXSR INIT
     C*
     C           *INU7     IFEQ '0'
     C                     EXSR INPUT
     C                     END
     C*
     C                     SETON                     LR
     C*
     C****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C**  INIT       :  INITIAL PROCESSING                            *
     C*   CALLED BY  :  MAIN PROCESS                                  *
     C*   CALLS      :                                                *
     C*                                                               *
     C*****************************************************************
     C*
     C           INIT      BEGSR
     C*
     C**  Prepare COMIT text
     C*
     C                     MOVEL'RE'      MDID
     C                     MOVEL'RE2192'  PGMN
     C                     MOVEL'RE2192'  DBPGM
     C                     MOVELWID       WSIDX
     C                     MOVELUSRID     USIDX
     C*
     C** Access TABTG10 to load arrays for currency & decimal places
     C*
     C                     Z-ADD1         Y       30
     C                     READ TABTG10                  89*
     C*
     C           *IN89     DOWEQ'0'
     C*
     C                     MOVE CCY       CCYD,Y
     C                     MOVE CDP       CDPD,Y
     C*
     C                     ADD  1         Y
     C                     READ TABTG10                  89*
     C*
     C                     END
     C*
     C** OBTAIN BANK DETAILS VIA ACCESS PROGRAM
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           @BANK     PARM @BANK     DSFDY
     C*
     C           BJDFIN    COMP 'M'                      98*
     C*
     C** CALL ACCESS PROGRAM FOR TRADING DATA  DETAILS
     C*
     C                     CALL 'AOTRADR0'
     C                     PARM '*DBERR  '@RTCD
     C                     PARM '*FIRST  '@OPTN
     C           @TRAD     PARM @TRAD     DSFDY
     C*
     C** Compare base currency to sterling code
     C*
     C           BJCYCD    IFNE BLCYCD
     C                     SETON                     41
     C                     END
     C*
     C* CHECK IF SYSTEM IS MULTI-BRANCHING
     C*
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C           MBIN      IFEQ 'Y'
     C                     MOVE '1'       *IN44
     C                     END
     C*
     C* GET DEFAULT BRANCH
     C*
     C           *NAMVAR   DEFN           ZMUSER
     C                     IN   ZMUSER
     C*
     C** Display input screen
     C*
     C                     EXFMTRE2192F1
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C**  INPUT      :  INPUT PROCESSING                              *
     C*   CALLED BY  :  MAIN PROCESS                                  *
     C*   CALLS      :  VALID1                                        *
     C*                 VALID2                                        *
     C*                 HELP                                          *
     C*                 CLEAR                                         *
     C*                 WRITE                                         *
     C*                                                               *
     C*****************************************************************
     C*
     C           INPUT     BEGSR
     C*
     C** Process while Cmd/3 not pressed and no database error
     C*
     C           *IN03     DOWEQ'0'                        B0001
     C           *INU7     ANDEQ'0'
     C*
     C** Perform initial validation of data input from screen
     C*
     C                     EXSR VALID1
     C*
     C** Process while there are errors (*IN30 is on) and neither
     C** Cmd/3 nor Cmd/12 are pressed and no database error
     C*
     C           *IN30     DOWEQ'1'                        B0002
     C           *IN03     ANDEQ'0'
     C           *IN12     ANDEQ'0'
     C           *INU7     ANDEQ'0'
     C*
     C                     EXFMTRE2192F1
     C*
     C** If neither Cmd/3 nor Cmd/12 are pressed
     C*
     C           *IN03     IFEQ '0'
     C           *IN12     ANDEQ'0'
     C                     EXSR VALID1
     C                     END
     C*
     C                     END                             E0002
     C*
     C** Continue processing only if no database error
     C*
     C           *INU7     IFEQ '0'                        B0002
     C*
     C** If neither Cmd/3 nor Cmd/12 are pressed
     C*
     C           *IN03     IFEQ '0'                        B0003
     C           *IN12     ANDEQ'0'
     C*
     C** Set on *IN50 & *IN42 to display confirmation screen
     C** and validate input into field 'CONF'
     C*
     C                     SETON                     5042
     C                     MOVE ' '       CONF
     C                     EXFMTRE2192F1
     C                     EXSR VALID2
     C*
     C** If screen is confirmed, write refund record & clear input
     C** fields for next input
     C*
     C           CONF      IFEQ 'Y'                        B0004
     C*
     C           ACHLRT    IFEQ 'I'                        B00005
     C           ACHLRT    OREQ 'J'
     C           ACHLRT    OREQ 'B'                                       096730
     C           ACHLRT    OREQ 'C'                                       096730
     C           ACHLRT    OREQ 'R'                                       CRE007
     C                     EXSR WRITE
     C                     ELSE                            X00005
     C                     EXSR WRITNO
     C                     END                             E00005
     C                     EXSR CLEAR
     C                     EXFMTRE2192F1
     C*
     C                     ELSE                            X0004
     C*
     C                     SETOF                     5042
     C                     EXFMTRE2192F1
     C*
     C                     END                             E0004
     C*
     C                     END                             E0003
     C*
     C** If Cmd/12 is pressed, clear input fields
     C*
     C           *IN12     IFEQ '1'                        B0003
     C*
     C                     EXSR CLEAR
     C                     EXFMTRE2192F1
     C*
     C                     END                             E0003
     C*
     C                     END                             E0002
     C*
     C                     END                             E0001
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C**  VALID1     :  INPUT VALIDATION                              *
     C*   CALLED BY  :  INPUT                                         *
     C*   CALLS      :  VALIDA                                        *
     C*                                                               *
     C*****************************************************************
     C*
     C           VALID1    BEGSR
     C*
     C** Initialise data structure  ERNO
     C*
     C                     MOVE *BLANKS   ERNO
     C*
     C** Set off all error indicators
     C*
     C                     SETOF                     303132
     C                     SETOF                     333435
     C                     SETOF                     363738
     C                     SETOF                     394043
     C                     SETOF                     50
     C*
     C** Initialise array index X
     C*
     C                     Z-ADD0         X       20
     C*
     C** Initialise total accumulators
     C*
     C                     Z-ADD0         TOTSTG 150
     C                     Z-ADD0         TOTCCY 150
     C                     Z-ADD0         TOTBSE 150
     C*
     C** Check that customer number (mandatory field) is not blank
     C*
     C           CUSNO     IFEQ ' '                        B0001
     C*
     C                     ADD  1         X
     C                     MOVE '001'     ERNO,X
     C                     SETON                     3031
     C*
     C                     ELSE                            X0001
     C*
     C** Using input customer number as key, access SDCUSTPD
     C*
     C                     CALL 'AOCUSTR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM CUSNO     @KEY1  10
     C                     PARM *BLANKS   @KYST   7
     C           SDCUST    PARM SDCUST    DSSDY
     C*
     C* If record not found
     C*
     C           @RTCD     IFNE *BLANKS                    B0002
     C           @KYST     OREQ '*ERROR'
     C*
     C                     ADD  1         X
     C                     MOVE '002'     ERNO,X
     C                     SETON                     3031
     C*
     C                     ELSE                            X0002
     C                     MOVEL*BLANK    CUSNO
     C                     MOVELBBCUST    CUSNO
     C                     MOVELBBCUST    CUST
     C*
     C** Use field CUST to access PF/SDCBRTL1
     C*
     C           CUST      CHAINSDCBRTL1             85
     C*
     C           *IN85     IFEQ '1'                        B0003
     C                     MOVELCUST      DBKEY            *************
     C                     MOVEL'SDCBRTPD'DBFILE           * DBERR 001 *
     C                     MOVEL'001'     DBASE            *************
     C                     SETON                     U7U8LR
     C                     DUMP
     C*
     C                     ELSE                            X0003
     C*
     C** Store BRT tax indicator
     C*
     C                     MOVE TAXIND    TAX     1
     C*
     C***Check*account*holder*type                                        096730
     C*
     C***********ACHLRT    IFNE 'I'                        B0004          096730
     C***********ACHLRT    ANDNE'J'                                       096730
     C***********ACHLRT    ANDNE'N'                                       096730
     C***********ACHLRT    ANDNE'O'                                       096730
     C*
     C***********          ADD  1         X                               096730
     C***********          MOVE '003'     ERNO,X                          096730
     C***********          SETON                     3031                 096730
     C*
     C***********          ELSE                            X0004          096730
     C*
     C           ACHLRT    IFEQ 'I'                        B0005
     C*
     C** Check all holders
     C*
     C           H1BRTL    IFEQ 'Y'                        B0006
     C           H2BRTL    OREQ 'Y'
     C           H3BRTL    OREQ 'Y'
     C           H4BRTL    OREQ 'Y'
     C           H5BRTL    OREQ 'Y'
     C*
     C                     ADD  1         X
     C                     MOVE '004'     ERNO,X
     C                     SETON                     3031
     C*
     C                     ELSE                            X0006
     C                     EXSR VALIDA
     C*
     C                     END                             E0006
     C*
     C                     ELSE                            X0005
     C*
     C** If account holder type is 'J', check first 2 holders
     C*
     C           ACHLRT    IFEQ 'J'                                       096730
     C***********H1BRTL    IFEQ 'Y'                        B0006          096730
     C           H1BRTL    ANDEQ'Y'                                       096730
     C           H2BRTL    ANDEQ'Y'
     C*
     C                     ADD  1         X
     C                     MOVE '005'     ERNO,X
     C                     SETON                     3031
     C*
     C                     ELSE                            X0006
     C                     EXSR VALIDA
     C*
     C                     END                             E0006
     C*
     C                     END                             E0005
     C*
     C***********          END                             E0004          096730
     C*
     C                     END                             E0003
     C*
     C                     END                             E0002
     C*
     C                     END                             E0001
     C*
     C                     ENDSR
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C**  VALIDA     :  INPUT VALIDATION CONT.                        *
     C*   CALLED BY  :  VALID1                                        *
     C*   CALLS      :  ZALIGN                                        *
     C*                 ZEDIT                                         *
     C*                 TOTAL1                                        *
     C*                 TOTAL2                                        *
     C*                                                               *
     C*****************************************************************
     C*
     C           VALIDA    BEGSR
     C*
     C** If the Account Holder Type is 'N' or 'O' the BRT credit
     C** fields must be blank.
     C*
     C           ACHLRT    IFEQ 'N'                        B0001
     C           ACHLRT    OREQ 'O'
     C*
     C           BRTSTG    IFNE ' '                        B0002
     C           BRTCCY    ORNE ' '
     C*
     C                     ADD  1         X
     C                     MOVE '021'     ERNO,X
     C                     SETON                     303738
     C*
     C                     END                             E0002
     C*
     C                     END                             E0001
     C*
     C***If*the*Account*Holder*Type*is*'I'*or*'J'                         096730
     C***If*the*Account*Holder*Type*is*'I',*'J',*'B'*or*'C'*amount**096730CRE007
     C** If the Account Holder Type is 'I', 'J', 'B', 'C' or 'R' amount   CRE007
     C** must be entered.                                                 096730
     C*
     C           ACHLRT    IFEQ 'I'                        B0001
     C           ACHLRT    OREQ 'J'
     C           ACHLRT    OREQ 'B'                                       096730
     C           ACHLRT    OREQ 'C'                                       096730
     C           ACHLRT    OREQ 'R'                                       CRE007
     C*
     C** Check that ONE of the BRT credit amount fields is entered
     C*
     C           BRTSTG    IFEQ ' '                        B0002
     C           BRTCCY    ANDEQ' '
     C*
     C                     ADD  1         X
     C                     MOVE '006'     ERNO,X
     C                     SETON                     303738
     C*
     C                     END                             E0002
     C*
     C           BRTSTG    IFNE ' '                        B0002
     C           BRTCCY    ANDNE' '
     C*
     C                     ADD  1         X
     C                     MOVE '006'     ERNO,X
     C                     SETON                     303738
     C*
     C                     END                             E0002
     C*
     C** Perform ZALIGN & ZEDIT (to re-display amounts BRTSTG and
     C** BRTCCY) ONLY IF there are no other errors (*IN30 is off)
     C*
     C           *IN30     IFEQ '0'                        B0002
     C*
     C** If sterling entered:
     C*
     C           BRTSTG    IFNE ' '                        B0003
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE BRTSTG    ZFIELD
     C*
     C** Store input amount in case of need to re-display
     C*
     C                     MOVE BRTSTG    WKFLD  16
     C*
     C                     Z-ADD2         ZADEC
     C                     Z-ADD13        ZADIG
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    STGCR  150
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    BRTSTG
     C*
     C                     ELSE                            X0003
     C*
     C** If original currency:
     C*
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE BRTCCY    ZFIELD
     C*
     C** Store input amount in case of need to re-display
     C*
     C                     MOVE BRTCCY    WKFLD2 16
     C*
     C** Look up arrays to get decimal places for original currency
     C*
     C                     Z-ADD1         Y
     C           DLCUR     IFNE ' '                        B0004
     C*
     C           DLCUR     LOKUPCCYD,Y                   09
     C                     ELSE                            X0004
     C           CURR      LOKUPCCYD,Y                   09
     C*
     C                     END                             E0004
     C*
     C                     MOVE CDPD,Y    ZADEC
     C           15        SUB  ZADEC     ZADIG
     C*
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    CCYCR  150
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    BRTCCY
     C                     END                             E0003
     C*
     C                     END                             E0002
     C*
     C                     END                             E0001
     C*
     C** Validate for account details
     C*
     C           DEAL      IFEQ ' '                        B0001
     C*
     C*  DEFAULT USER BRANCH IF BRANCH NOT SPECIFIED
     C*
     C           BRANCH    IFEQ ' '
     C                     MOVE USRBCH    BRANCH
     C                     END
     C*
     C** Deal currency must be blank
     C*
     C           DLCUR     IFNE ' '                        B0002
     C*
     C                     ADD  1         X
     C                     MOVE '007'     ERNO,X
     C                     SETON                     3033
     C*
     C                     END                             E0002
     C*
     C*  VALIDATE CURRENCY
     C*
     C           CURR      IFNE ' '                        B0002
     C                     CALL 'AOCURRR0'
     C                     PARM *BLANK    @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM CURR      @AJCD   3
     C           SDCURR    PARM SDCURR    DSSDY
     C           @RTCD     IFNE *BLANK
     C                     ADD  1         X
     C                     MOVE '022'     ERNO,X
     C                     SETON                     3034
     C                     ELSE
     C                     MOVELA6CYCD    CURR
     C                     END
     C                     END                             E0002
     C*
     C*  VALIDATE ACCOUNT CODE
     C*
     C           ACODE     IFNE ' '                        B0002
     C                     CALL 'AOACODR0'
     C                     PARM *BLANK    @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C**********           PARM ACODE     @AFNB   4                                           CGL029
     C********** SDACOD    PARM SDACOD    DSFDY                                               CGL029
     C                     PARM ACODE     @AFNB  10                                           CGL029
     C           SDACOD    PARM SDACOD    DSSDY                                               CGL029
     C           @RTCD     IFNE *BLANK
     C                     ADD  1         X
     C                     MOVE '023'     ERNO,X
     C                     SETON                     3035
     C                     ELSE
     C                     MOVELA5ACCD    ACODE
     C                     END
     C                     END                             E0002
     C*
     C*  IF SYSTEM IS MULTI-BRANCHING
     C*  VALIDATE BRANCH
     C*
     C           *IN44     IFEQ '1'                        B0002
     C           BRANCH    ANDNE' '
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM *BLANK    @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM BRANCH    @DSNB   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
     C           @RTCD     IFNE *BLANKS                    B0003
     C                     ADD  1         X
     C                     MOVE '024'     ERNO,X
     C                     SETON                     3043
     C                     ELSE
     C                     MOVELA8BRCD    BRANCH
     C                     END                             E0003
     C*
     C* IF NO ERRORS, CHECK AUTHORITY TO BRANCH
     C*
     C           *IN43     IFNE '1'                        B0003
     C                     CALL 'ZVBBU'
     C                     PARM BRANCH    @BRCX   3
     C                     PARM           @OTCD   10
     C           @OTCD     IFNE 0                          B0004
     C                     ADD  1         X
     C                     MOVE '025'     ERNO,X
     C                     SETON                     3043
     C                     END                             E0004
     C                     END                             E0003
     C                     END                             E0002
     C*
     C* CHECK AUTHORITY TO ACTION CODE ('INSERT')
     C*
     C* - IF MULTI-BRANCHING
     C*
     C                     Z-ADD*ZERO     @OTCD
     C           *IN44     IFEQ '1'                        B0002
     C           BRANCH    IFNE ' '
     C           *IN43     ANDNE'1'
     C                     CALL 'ZVACTBU'
     C                     PARM 'I'       @ACTN   1
     C                     PARM BRANCH    @BRCX   3
     C                     PARM           @OTCD
     C                     END
     C                     ELSE
     C*
     C* - IF NOT MULTI-BRANCHING
     C*
     C                     CALL 'ZVACTU'
     C                     PARM 'I'       @ACTN   1
     C                     PARM           @OTCD
     C                     END                             E0002
     C*
     C* NO AUTHORITY TO DO THIS TRANSACTION
     C*
     C           @OTCD     IFNE 0                          B0002
     C                     ADD  1         X
     C                     MOVE '026'     ERNO,X
     C                     SETON                     303132
     C                     SETON                     333435
     C                     SETON                     3641
     C                     END                             E0002
     C*
     C** Set up keylist ACCKEY to chain to LF/ACCNT
     C*
     C                     MOVE CURR      CNCY
     C                     MOVE ACODE     CODE
     C                     MOVE ACSEQ     SEQ
     C                     MOVE BRANCH    BRCH
     C*
     C           ACCKEY    CHAINACCNT                85
     C*
     C           *IN85     IFEQ '1'                        B0002
     C*
     C                     ADD  1         X
     C                     MOVE '008'     ERNO,X
     C                     SETON                     303134
     C                     SETON                     353643
     C*
     C                     ELSE                            X0002
     C*
     C** Store branch code
     C*
     C                     MOVE BRANCH    BRCAX   3
     C*
     C** Check account type
     C*
     C           ATYP      IFNE 'R'                        B0003
     C*
     C                     ADD  1         X
     C                     MOVE '009'     ERNO,X
     C                     SETON                     303134
     C                     SETON                     353643
     C*
     C                     ELSE                            X0003
     C*
     C** Check BRT category
     C*
     C           BRTCA     IFEQ 'L'                        B0004
     C*
     C                     ADD  1         X
     C                     MOVE '010'     ERNO,X
     C                     SETON                     303134
     C                     SETON                     353643
     C*
     C                     ELSE                            X0004
     C*
     C** Calculate total interest payments to date
     C*
     C           *IN30     IFEQ '0'                        B0005
     C                     EXSR TOTAL1
     C                     END                             E0005
     C*
     C                     END                             E0004
     C*
     C                     END                             E0003
     C*
     C                     END                             E0002
     C*
     C** Validate for deal details
     C*
     C                     ELSE                            X0001
     C*
     C** Deal currency must not be blank
     C*
     C           DLCUR     IFEQ ' '                        B0002
     C*
     C                     ADD  1         X
     C                     MOVE '011'     ERNO,X
     C                     SETON                     3033
     C*
     C                     END                             E0002
     C*
     C***Move*field DEAL into numeric field to access LF/DEALS                          192085
     C** Move field DEAL into numeric field to access LF/DEALSALL                       192085
     C*
     C                     MOVE DEAL      DLNUM   60
     C***********DLNUM     CHAINDEALS                85                                 192085
     C           DLNUM     CHAINDEALSALL             85                                 192085
     C*
     C           *IN85     IFEQ '1'                        B0002
     C*
     C                     ADD  1         X
     C                     MOVE '012'     ERNO,X
     C                     SETON                     3032
     C*
     C                     ELSE                            X0002
     C*
     C** Store branch code
     C*
     C                     MOVE BRCA      BRCAX   3
     C*
     C** Check deal type
     C*
     C           DTYP      IFNE 'IT'                       B0003
     C           DTYP      ANDNE'TD'
     C           DTYP      ANDNE'CD'
     C*
     C                     ADD  1         X
     C                     MOVE '013'     ERNO,X
     C                     SETON                     3032
     C*
     C                     END                             E0003
     C*
     C** Check input customer no. against customer no. of deal
     C*
     C           CUST      IFNE CNUM                       B0003
     C*
     C                     ADD  1         X
     C                     MOVE '014'     ERNO,X
     C                     SETON                     3031
     C*
     C                     END                             E0003
     C*
     C** Check input currency against currency of deal
     C*
     C           DLCUR     IFNE CCY                        B0003
     C*
     C                     ADD  1         X
     C                     MOVE '015'     ERNO,X
     C                     SETON                     3033
     C*
     C                     END                             E0003
     C*
     C** Check tax indicator from PF/SDCBRTL1
     C*
     C           TAX       IFEQ 'L'                        B0003
     C*
     C                     ADD  1         X
     C                     MOVE '016'     ERNO,X
     C                     SETON                     3031
     C*
     C                     ELSE                            X0003
     C*
     C** Calculate total interest payments to date
     C*
     C           *IN30     IFEQ '0'                        B0004
     C                     EXSR TOTAL2
     C                     END                             E0004
     C*
     C                     END                             E0003
     C*
     C                     END                             E0002
     C*
     C                     END                             E0001
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C**  VALID2     :  CONFIRMATION VALIDATION                       *
     C*   CALLED BY  :  INPUT                                         *
     C*   CALLS      :  HELP
     C*                                                               *
     C*****************************************************************
     C*
     C           VALID2    BEGSR
     C*
     C** Check that field CONF is either Y or N
     C*
     C           CONF      IFNE 'Y'                        B0001
     C           CONF      ANDNE'N'
     C*
     C                     SETON                     3039
     C*
     C           *IN39     DOWEQ'1'                        B0002
     C*
     C                     Z-ADD1         X
     C                     MOVE '020'     ERNO,X
     C*
     C                     EXFMTRE2192F1
     C*
     C           CONF      IFEQ 'Y'
     C           CONF      OREQ 'N'
     C                     SETOF                     3039
     C                     END
     C*
     C                     END                             E0002
     C*
     C                     END                             E0001
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C**  TOTAL1     :  CALCULATE TOTAL INTEREST PAID TO DATE FOR     *
     C**                ACCOUNTS
     C**  CALLED BY  :  VALID1                                        *
     C**  CALLS      :  ZEDIT
     C*
     C*****************************************************************
     C*
     C           TOTAL1    BEGSR
     C*
     C** Set of  end-of file indicators
     C*
     C                     SETOF                     808182
     C*
     C** Access LF/INTPSL26 using ACCKEY
     C*
     C           ACCKEY    CHAININTPSL26             81
     C*
     C           *IN81     IFEQ '1'                        B0001
     C*
     C                     ADD  1         X
     C                     MOVE '017'     ERNO,X
     C                     SETON                     303134
     C                     SETON                     3536
     C*
     C                     ELSE                            X0001
     C*
     C** Store ACCKEY into ACKEY2 to enable DOWHILE loop
     C*
     C                     MOVE CUST      CST
     C                     MOVE CNCY      CUR
     C                     MOVE CODE      COD
     C                     MOVE SEQ       SQ
     C                     MOVE BRCH      BR
     C*
     C** Do while record with same key is found and no error (and not
     C** end of file)
     C*
     C           KEY1      DOWEQKEY2                       B0002
     C           *IN30     ANDEQ'0'
     C           *IN81     ANDEQ'0'
     C*
     C** Check S352 indicator
     C*
     C           S352RI    IFEQ 'Y'                        B0003
     C*
     C                     ADD  1         X
     C                     MOVE '018'     ERNO,X
     C                     SETON                     303134
     C                     SETON                     353643
     C                     SETON                     82
     C                     Z-ADD0         TOTSTG
     C                     Z-ADD0         TOTCCY
     C                     Z-ADD0         TOTBSE
     C*
     C                     ELSE                            X0003
     C*
     C** Accumulate BRT deducted amounts
     C*
     C                     ADD  STGBRT    TOTSTG
     C                     ADD  CCYBRT    TOTCCY
     C                     ADD  BCEBRT    TOTBSE
     C*
     C** Store interest payment date
     C*
     C                     MOVE IPDT      IPDTX   50
     C*
     C** Read next record
     C*
     C                     READ INTPSL26                 81
     C*
     C** IGNORE REST OF PROCESSING IF E.O.F
     C*
     C           *IN81     IFEQ '0'
     C*
     C** Set up ACCKEY with new information
     C*
     C                     MOVE CNUM      CUST
     C                     MOVE CCY       CNCY
     C                     MOVE ACOD      CODE
     C                     MOVE ACSQ      SEQ
     C                     MOVE BRCA      BRCH
     C*
     C           RTYP      IFEQ 'A'
     C           RTYP      OREQ 'R'                                       CRE007
     C                     EXCPTRELI26
     C                     ELSE
     C                     EXCPTRELC26
     C                     END
     C                     END
     C*
     C                     END                             E0003
     C*
     C                     END                             E0002
     C           *IN82     IFEQ '0'
     C*
     C** Use stored account number to access LF/INTPSL27
     C*
     C           ACKEY2    CHAININTPSL27             80
     C*
     C** Do while record with same key is found (load keylist ACCKEY
     C** with values from ACKEY2) and not end of file
     C*
     C                     MOVE CST       CUST
     C                     MOVE CUR       CNCY
     C                     MOVE COD       CODE
     C                     MOVE SQ        SEQ
     C                     MOVE BR        BRCH
     C*
     C           KEY2      DOWEQKEY1                       B0002
     C           *IN80     ANDEQ'0'
     C*
     C** Subtract amounts from accumulated totals
     C*
     C           TOTSTG    SUB  CRBRTS    TOTSTG
     C           TOTCCY    SUB  CRBRTC    TOTCCY
     C           TOTBSE    SUB  CRBRTE    TOTBSE
     C*
     C** Read next record
     C*
     C                     READ INTPSL27                 80
     C*
     C** IGNORE REST OF PROCESSING IF E.O.F
     C*
     C           *IN80     IFEQ '0'
     C*
     C** Set up ACKEY2 with new information
     C*
     C                     MOVE CNUM      CST
     C                     MOVE CCY       CUR
     C                     MOVE ACOD      COD
     C                     MOVE ACSQ      SQ
     C                     MOVE BRCA      BR
     C*
     C           RTYP      IFEQ 'A'
     C           RTYP      OREQ 'R'                                       CRE007
     C                     EXCPTRELI27
     C                     ELSE
     C                     EXCPTRELC27
     C                     END
     C                     END
     C*
     C                     END                             E0002
     C*
     C** Check BRT category (from ACCNT)
     C*
     C           BRTCA     IFEQ 'P'
     C*
     C           TOTSTG    MULT 0.5       TOTSTG
     C           TOTCCY    MULT 0.5       TOTCCY
     C           TOTBSE    MULT 0.5       TOTBSE
     C*
     C                     END
     C                     END                             E0002
     C*
     C** output BRT deducted for
     C** original currency as well as sterling
     C*
     C** Perform ZEDIT to display total amounts
     C*
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE TOTSTG    ZFIELD
     C                     Z-ADD2         ZADEC
     C                     Z-ADD13        ZADIG
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    PRVSTG
     C*
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE TOTCCY    ZFIELD
     C*
     C** Look up arrays to get decimal places for original currency
     C*
     C                     Z-ADD1         Y
     C           CURR      LOKUPCCYD,Y                   09
     C                     MOVE CDPD,Y    ZADEC
     C           15        SUB  ZADEC     ZADIG
     C*
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    PRVCCY
     C*
     C** If base currency is not sterling, output BRT deducted for
     C** base currency
     C*
     C           *IN41     IFEQ '1'
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE TOTBSE    ZFIELD
     C*
     C** Look up arrays to get decimal places for base currency
     C*
     C                     Z-ADD1         Y
     C           BJCYCD    LOKUPCCYD,Y                   09
     C                     MOVE CDPD,Y    ZADEC
     C           15        SUB  ZADEC     ZADIG
     C*
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    PRVBSE
     C                     END
     C*
     C** Indicate errors if input amounts do not equal accumulated
     C** totals
     C*
     C           ACHLRT    IFEQ 'I'
     C           ACHLRT    OREQ 'J'
     C           ACHLRT    OREQ 'B'                                       096730
     C           ACHLRT    OREQ 'C'                                       096730
     C           ACHLRT    OREQ 'R'                                       CRE007
     C           *IN82     IFEQ '0'                        B0002
     C           BRTSTG    IFNE ' '                        B0002
     C*
     C           STGCR     IFNE TOTSTG
     C*
     C                     ADD  1         X
     C                     MOVE '019'     ERNO,X
     C                     MOVE WKFLD     BRTSTG
     C                     SETON                     3037
     C*
     C                     END
     C*
     C                     ELSE                            X0002
     C*
     C           CCYCR     IFNE TOTCCY
     C*
     C                     ADD  1         X
     C                     MOVE '019'     ERNO,X
     C                     MOVE WKFLD2    BRTCCY
     C                     SETON                     3038
     C*
     C                     END
     C*
     C                     END                             E0002
     C                     END                             E0002
     C                     END
     C*
     C                     END                             E0001
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C**  TOTAL2     :  CALCULATE TOTAL INTEREST PAID TO DATE FOR     *
     C**                DEALS
     C**  CALLED BY  :  VALID1                                        *
     C**  CALLS      :  ZEDIT
     C*                                                               *
     C*****************************************************************
     C*
     C           TOTAL2    BEGSR
     C*
     C** Set off end-of file indicators
     C*
     C                     SETOF                     808182
     C*
     C** Access LF/INTPSL28 using deal number
     C*
     C           DLNUM     CHAININTPSL28             81
     C*
     C           *IN81     IFEQ '1'                        B0001
     C*
     C                     ADD  1         X
     C                     MOVE '017'     ERNO,X
     C                     SETON                     3032
     C*
     C                     ELSE                            X0001
     C*
     C** Store deal number to enable DOWHILE loop
     C*
     C                     MOVE DLNUM     DLNOX   60
     C*
     C** Do while record with same key is found and no error (and not
     C** end of file)
     C*
     C           DLNUM     DOWEQDLNOX                      B0002
     C           *IN30     ANDEQ'0'
     C           *IN81     ANDEQ'0'
     C*
     C** Check S352 indicator
     C*
     C           S352RI    IFEQ 'Y'                        B0003
     C*
     C                     ADD  1         X
     C                     MOVE '018'     ERNO,X
     C                     SETON                     303282
     C                     Z-ADD0         TOTSTG
     C                     Z-ADD0         TOTCCY
     C                     Z-ADD0         TOTBSE
     C*
     C                     ELSE                            X0003
     C*
     C** Accumulate BRT deducted amounts
     C*
     C                     ADD  STGBRT    TOTSTG
     C                     ADD  CCYBRT    TOTCCY
     C                     ADD  BCEBRT    TOTBSE
     C*
     C** Store interest payment date
     C*
     C                     MOVE IPDT      IPDTX
     C*
     C** Read next record
     C*
     C                     READ INTPSL28                 81
     C*
     C* IF CHAIN FAIL, IGNORE REST OF PROCESSING
     C           *IN81     IFEQ '0'                        B0FIX
     C*
     C** Set up DLNUM with new deal number
     C*
     C                     MOVE DLNO      DLNUM
     C*
     C           RTYP      IFEQ 'A'
     C           RTYP      OREQ 'R'                                       CRE007
     C                     EXCPTRELI28
     C                     ELSE
     C                     EXCPTRELC28
     C                     END
     C*
     C                     END                             E0FIX
     C*
     C                     END                             E0003
     C*
     C                     END                             E0002
     C           *IN82     IFEQ '0'                        B0002
     C*
     C** Use stored deal number to access LF/INTPSL29
     C*
     C           DLNOX     CHAININTPSL29             80
     C*
     C** Do while record with same key is found and not end of file
     C*
     C                     MOVE DLNOX     DLNUM
     C           DLNOX     DOWEQDLNUM                      B0002
     C           *IN80     ANDEQ'0'
     C*
     C** Subtract amounts from accumulated totals
     C*
     C           TOTSTG    SUB  CRBRTS    TOTSTG
     C           TOTCCY    SUB  CRBRTC    TOTCCY
     C           TOTBSE    SUB  CRBRTE    TOTBSE
     C*
     C** Read next record
     C*
     C                     READ INTPSL29                 80
     C*
     C** IGNORE REST OF PROCESSING IF E.O.F
     C*
     C           *IN80     IFEQ '0'
     C*
     C** Set up DLNOX with new deal number
     C*
     C                     MOVE DLNO      DLNOX
     C*
     C           RTYP      IFEQ 'A'
     C           RTYP      OREQ 'R'                                       CRE007
     C                     EXCPTRELI29
     C                     ELSE
     C                     EXCPTRELC29
     C                     END
     C                     END
     C*
     C                     END                             E0002
     C*
     C** Check tax indicator (from SDCBRTL1)
     C*
     C           TAX       IFEQ 'P'
     C*
     C           TOTSTG    MULT 0.5       TOTSTG
     C           TOTCCY    MULT 0.5       TOTCCY
     C           TOTBSE    MULT 0.5       TOTBSE
     C*
     C                     END
     C                     END                             E0002
     C*
     C** output BRT deducted for
     C** original currency as well as sterling
     C*
     C** Perform ZEDIT to display total amounts
     C*
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE TOTSTG    ZFIELD
     C                     Z-ADD2         ZADEC
     C                     Z-ADD13        ZADIG
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    PRVSTG
     C*
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE TOTCCY    ZFIELD
     C*
     C** Look up arrays to get decimal places for original currency
     C*
     C                     Z-ADD1         Y
     C           DLCUR     LOKUPCCYD,Y                   09
     C                     MOVE CDPD,Y    ZADEC
     C           15        SUB  ZADEC     ZADIG
     C*
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    PRVCCY
     C*
     C** If base currency is not sterling, output BRT deducted for
     C** base currency
     C*
     C           *IN41     IFEQ '1'
     C                     MOVE *BLANKS   ZFIELD
     C                     MOVE TOTBSE    ZFIELD
     C*
     C** Look up arrays to get decimal places for base currency
     C*
     C                     Z-ADD1         Y
     C           BJCYCD    LOKUPCCYD,Y                   09
     C                     MOVE CDPD,Y    ZADEC
     C           15        SUB  ZADEC     ZADIG
     C*
     C                     EXSR ZEDIT
     C                     MOVE ZFIELD    PRVBSE
     C                     END
     C*
     C** Indicate errors if input amounts do not equal accumulated
     C** totals
     C*
     C           ACHLRT    IFEQ 'I'
     C           ACHLRT    OREQ 'J'
     C           ACHLRT    OREQ 'B'                                       096730
     C           ACHLRT    OREQ 'C'                                       096730
     C           ACHLRT    OREQ 'R'                                       CRE007
     C           *IN82     IFEQ '0'                        B0002
     C           BRTSTG    IFNE ' '                        B0002
     C*
     C           STGCR     IFNE TOTSTG
     C*
     C                     ADD  1         X
     C                     MOVE '019'     ERNO,X
     C                     MOVE WKFLD     BRTSTG
     C                     SETON                     3037
     C*
     C                     END
     C*
     C                     ELSE                            X0002
     C*
     C           CCYCR     IFNE TOTCCY
     C*
     C                     ADD  1         X
     C                     MOVE '019'     ERNO,X
     C                     MOVE WKFLD2    BRTCCY
     C                     SETON                     3038
     C*
     C                     END
     C*
     C                     END                             E0002
     C                     END                             E0002
     C                     END
     C*
     C                     END                             E0001
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C**  WRITE      :  WRITE INTEREST HISTORY DETAIL                 *
     C**  CALLED BY  :  INPUT                                         *
     C**  CALLS      :                                                *
     C*                                                               *
     C*****************************************************************
     C*
     C           WRITE     BEGSR
     C*
     C**  Obtain last transaction No. and prepare standard
     C**  information for CMTTXT Data Structure
     C*
     C                     EXSR ZTNLU1
     C                     TIME           MTIME   60
     C*
     C                     MOVE 'D'       RECI
     C                     MOVELBBCRNM    CRNM
     C*
     C** Move fields into relevant fields for output
     C*
     C                     MOVE DEAL      DLNO
     C                     MOVELCUSNO     CNUM
     C*
     C           DEAL      IFEQ ' '
     C                     MOVE CURR      CCY
     C                     ELSE
     C                     MOVE DLCUR     CCY
     C                     END
     C*
     C                     MOVE ACODE     ACOD
     C                     MOVE ACSEQ     ACSQ
     C                     MOVE IPDTX     IPDT
     C*
     C** If account, use BRT category from ACCNT
     C*
     C           DEAL      IFEQ ' '
     C                     MOVE BRTCA     BRTCAT
     C                     ELSE
     C                     MOVE TAX       BRTCAT
     C                     END
     C*
     C                     Z-ADD0         LIAPTY
     C                     MOVE ' '       NACSG
     C                     MOVE 'N'       S352RI
     C*
     C                     MOVE TOTSTG    CRBRTS
     C                     MOVE TOTCCY    CRBRTC
     C                     MOVE TOTBSE    CRBRTE
     C*
     C                     Z-ADD0         STGGRI
     C                     Z-ADD0         STGBRT
     C                     Z-ADD0         STGNTI
     C                     Z-ADD0         CCYGRI
     C                     Z-ADD0         CCYBRT
     C                     Z-ADD0         CCYNTI
     C                     Z-ADD0         BCEGRI
     C                     Z-ADD0         BCEBRT
     C                     Z-ADD0         BCENTI
     C*
     C                     Z-ADD0         EXRATE
     C*
     C                     MOVE BRCAX     BRCA
     C*
     C*  Set Record Type to 'A'.
     C*
     C                     MOVE 'A'       RTYP
     C                     MOVE PSTR      PSTR1                           CRE007
     C*
     C** Write details to history file
     C*
     C                     WRITEINTPSDDF
     C*
     C** End of commitment cycle
     C*
     C           CMTTXT    COMIT
     C*
     C                     ENDSR
     C*
     C*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C**  WRITNO     :  WRITE INTEREST HISTORY DETAIL FOR 'N' AND 'O' *
     C*                 ACCOUNT HOLDER TYPES.                         *
     C**  CALLED BY  :  INPUT                                         *
     C**  CALLS      :                                                *
     C*                                                               *
     C*****************************************************************
     C*
     C           WRITNO    BEGSR
     C*
     C* IF THE DEAL NUMBER IS BLANK DELETE RECORDS RELATING TO ACCOUNT
     C*
     C           DEAL      IFEQ *BLANK
     C*
     C* STORE KEY VALUES INTO KEY2
     C*
     C                     MOVE KEY1      KEY2
     C*
     C           ACCKEY    CHAININTPSL26             79
     C*
     C* DO WHILE SAME ACCOUNT AND NOT END OF FILE
     C*
     C           *IN79     DOWEQ'0'
     C           KEY1      ANDEQKEY2
     C                     MOVE '*'       RECI
     C*
     C* UPDATE THE CORRECT FORMAT DEPENDING ON WHETHER CUSTOMER IS AN
     C* INDIVIDUAL OR NOT
     C*
     C           RTYP      IFEQ 'A'
     C           RTYP      OREQ 'R'                                       CRE007
     C                     UPDATINTPS26
     C                     ELSE
     C                     UPDATCOINT26
     C                     END
     C*
     C* READ THE NEXT RECORD
     C*
     C                     READ INTPSL26                 79
     C*
     C* STORE NEW KEY FIELDS TO KEY1 FOR COMPARE TO KEY2
     C*
     C                     MOVE CNUM      CUST
     C                     MOVE CCY       CNCY
     C                     MOVE ACOD      CODE
     C                     MOVE ACSQ      SEQ
     C                     MOVE BRCA      BRCH
     C                     END
     C*
     C           ACKEY2    CHAININTPSL27             78
     C*
     C* RESTORE KEY2 VALUES TO KEY1
     C*
     C                     MOVE KEY2      KEY1
     C*
     C* DO WHILE SAME ACCOUNT AND NOT END OF FILE
     C*
     C           *IN78     DOWEQ'0'
     C           KEY2      ANDEQKEY1
     C                     MOVE '*'       RECI
     C*
     C* UPDATE THE CORRECT FORMAT DEPENDING ON WHETHER CUSTOMER IS AN
     C* INDIVIDUAL OR NOT
     C*
     C           RTYP      IFEQ 'A'
     C           RTYP      OREQ 'R'                                       CRE007
     C                     UPDATINTPS27
     C                     ELSE
     C                     UPDATCOINT27
     C                     END
     C*
     C* READ THE NEXT RECORD
     C*
     C                     READ INTPSL27                 78
     C*
     C* STORE NEW KEY FIELDS TO KEY1 FOR COMPARE TO KEY2
     C*
     C                     MOVE CNUM      CST
     C                     MOVE CCY       CUR
     C                     MOVE ACOD      COD
     C                     MOVE ACSQ      SQ
     C                     MOVE BRCA      BR
     C                     END
     C*
     C*
     C* IF DEAL NUMBER IS NOT BLANK DELETE RECORDS REFERRING TO DEAL
     C*
     C                     ELSE
     C*
     C* STORE DEAL NUMBER INTO DLNOX
     C*
     C                     MOVE DLNUM     DLNOX
     C*
     C           DLNUM     CHAININTPSL28             79
     C*
     C* DO WHILE SAME DEAL AND NOT END OF FILE
     C*
     C           *IN79     DOWEQ'0'
     C           DLNUM     ANDEQDLNOX
     C                     MOVE '*'       RECI
     C*
     C* UPDATE THE CORRECT FORMAT DEPENDING ON WHETHER CUSTOMER IS AN
     C* INDIVIDUAL OR NOT
     C*
     C           RTYP      IFEQ 'A'
     C           RTYP      OREQ 'R'                                       CRE007
     C                     UPDATINTPS28
     C                     ELSE
     C                     UPDATCOINT28
     C                     END
     C*
     C* READ THE NEXT RECORD
     C*
     C                     READ INTPSL28                 79
     C*
     C* SET UP DLNUM WITH THE NEW DEAL NUMBER
     C*
     C                     MOVE DLNO      DLNUM
     C                     END
     C*
     C           DLNOX     CHAININTPSL29             78
     C*
     C* STORE DLNOX IN DLNUM FOR COMPARE
     C*
     C                     MOVE DLNOX     DLNUM
     C*
     C           *IN78     DOWEQ'0'
     C           DLNUM     ANDEQDLNOX
     C                     MOVE '*'       RECI
     C*
     C* UPDATE THE CORRECT FORMAT DEPENDING ON WHETHER CUSTOMER IS AN
     C* INDIVIDUAL OR NOT
     C*
     C           RTYP      IFEQ 'A'
     C           RTYP      OREQ 'R'                                       CRE007
     C                     UPDATINTPS29
     C                     ELSE
     C                     UPDATCOINT29
     C                     END
     C*
     C* READTHE NEXT RECORD
     C*
     C                     READ INTPSL29                 78
     C*
     C* MOVE THE NEW DEAL NUMBER INTO DLNUM FOR COMPARE TO DLNOX
     C*
     C                     MOVE DLNO      DLNUM
     C                     END
     C*
     C                     END
     C*
     C** End of commitment cycle
     C*
     C           CMTTXT    COMIT
     C*
     C*
     C                     ENDSR
     C*
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C**  CLEAR      :  CLEAR INPUT SCREEN                            *
     C**  CALLED BY  :  INPUT                                         *
     C**  CALLS      :                                                *
     C*                                                               *
     C*****************************************************************
     C*
     C           CLEAR     BEGSR
     C*
     C** Set off all error/conditioning indicators
     C*
     C                     SETOF                     303132
     C                     SETOF                     333435
     C                     SETOF                     363738
     C                     SETOF                     394042
     C                     SETOF                     4350
     C*
     C** Initialise all input fields
     C*
     C                     MOVE *BLANKS   CUSNO
     C                     MOVE *BLANKS   DEAL
     C                     MOVE *BLANKS   DLCUR
     C                     MOVE *BLANKS   CURR
     C                     MOVE *BLANKS   ACODE
     C                     MOVE *BLANKS   ACSEQ
     C                     MOVE *BLANKS   BRANCH
     C                     MOVE *BLANKS   BRTSTG
     C                     MOVE *BLANKS   BRTCCY
     C                     MOVE *BLANKS   PRVSTG
     C                     MOVE *BLANKS   PRVCCY
     C                     MOVE *BLANKS   PRVBSE
     C                     MOVE ' '       CONF
     C*
     C                     ENDSR
     C*
     C*****************************************************************
      /EJECT
     C********************************************************************
     C**
     C**   ZTNLU1 SR: OBTAIN LAST TRANSACTION NUMBER
     C**
     C********************************************************************
     C           ZTNLU1    BEGSR
     C*
     C* GO TO DATA AREA FOR NEXT AVAILABLE TRANSACTION NUMBER
     C*
     C           *NAMVAR   DEFN MNATN     DNATN
     C           *LOCK     IN   DNATN
     C                     MOVE FNATN     NATN    50
     C                     MOVE FNATN     ZZWK05  50
     C                     ADD  1         ZZWK05
     C                     MOVE ZZWK05    FNATN
     C                     OUT  DNATN
     C                     ENDSR
     C****************************************************************
      /EJECT
     C****************************************************************
     C/COPY ZSRSRC,ZALIGNZ2
     C/COPY ZSRSRC,ZEDITZ2
     C****************************************************************
      /EJECT
     O****************************************************************
     OINTPS26 E                RELI26
     OINTPS27 E                RELI27
     OINTPS28 E                RELI28
     OINTPS29 E                RELI29
     OCOINT26 E                RELC26
     OCOINT27 E                RELC27
     OCOINT28 E                RELC28
     OCOINT29 E                RELC29
