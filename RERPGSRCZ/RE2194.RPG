     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Interest payment history list')
     F********************************************************************
     F*                                                                  *
     F*  Midas - Retail Module
     F*                                                                  *
     F*    RE2194 - INTEREST PAYMENT HISTORY LIST                        *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Prev Amend No. CSD027             Date 09Dec05               *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 072064             Date 08Sep94               *
     F*                 S01383             Date 27APR92               *
     F*                                                                  *
     F********************************************************************
     F*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
     F*  072064 - Recompiled over amended printer file                   *
     F*       S01383 - BASIC RATE TAX INCORPORATION                      *
     F*                NEW PROGRAM                                       *
     F********************************************************************
     F*                                                                  *
     F*     Purpose of Program :                                         *
     F*     This program produces a list of all records held on          *
     F*     the Interest Payments History File in order of Branch        *
     F*     Customer number, Deal No. and Account Key.                   *
     F*                                                                  *
     F********************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      /TITLE S U B R O U T I N E   I N D E X .
      ********************************************************************
      *
      *     Subroutine Index
      *     ================
      *
      *     1. #MAIN   -   Main Processing
      *     2. #INIT   -   Initial Processing
      *     2. #DETL   -   Detail Processing
      *    10. *PSSR   -   Database Error Handling
      *
      *
     F********************************************************************
     F* ID F  C  H  L    FUNCTION OF INDICATORS                       *
     F*                                                               *
     F*       01      READ FAIL                                       *
     F*       02      END OF FILE INTPSL24                            *
     F*                                                               *
     F*****************************************************************
     FINTPSL24IF  E           K        DISK         KINFSR *PSSR
     FRE2194P1O   E                    PRINTER
     F                                              KINFDS SPOOL
     F*****************************************************************
     E/COPY ZSRSRC,ZDATE2Z1
     E*
     E                    CPY@    1   1 80               COPYRIGHT
     E*
     I*****************************************************************
     I*
      * Data structure for data-base error processing.
      *
     ILDA         DS                            256
     I                                        1 256 DBLDA
     I                                      134 183 DBLOT
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     ISPOOL       DS
     I*
     I**  DS FOR P1 SPOOL FILE NAME
     I*
     I                                       83  92 SFILE
     I                                    B 123 1240SFNUM
     I                                    B 188 1890@OVRF
     I                                    B 367 3680@LINE
     I*
      * Data structure for copyright message.
      *
     ICPYR@       DS
     I                                        1  80 CPY@
     ISDBANK    E DSSDBANKPD
     I* BANK DETAILS ACCESSED VIA ACCESS PROGRAM
     IDSFDY     E DSDSFDY
     I* FIRST DS FOR ACCESS PROGRAMS, SHORT DATA STRUCTURE
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                              *
      * #MAIN - Main processing                                      *
      *         CALLS :#INIT                                         *
      *                #DETL                                         *
      *                                                              *
      *****************************************************************
      *                                                              *
      * Initial processing.
      *
     C                     EXSR #INIT
      *
      * Detail processing.
      *
     C                     EXSR #DETL
      *
      * Termination processing.
      *
     C                     MOVE '1'       *INLR
      *
      *****************************************************************
      /EJECT
     C****************************************************************
      *                                                              *
      * #INIT - Read ICD                                             *
      *   CALLED FROM : #MAIN                                        *
      *         CALLS : *PSSR                                        *
      *                                                              *
      *                                                              *
      ****************************************************************
     C           #INIT     BEGSR
      *
      * Initialise LDA
      *
     C                     MOVE 'RE2194'  #PGM
     C*
     C** READ IN PARAMETER
     C*
     C           *ENTRY    PLIST
     C                     PARM           PSEQ    5
     C*
     C** RCF Processing for Detail Printer File
     C*
     C                     EXSR RCFP1
     C*
     C* CALL ACCESS PROGRAM FOR BANK DETAILS
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C           @RTCD     IFNE *BLANK
     C                     MOVEL'SDBANKPD'#FILE            *************
     C                     MOVEL'001'     #BASE            * DBERR 001 *
     C                     MOVEL'FIRST  ' #KEY             *************
     C                     WRITEDBERR
     C                     EXSR *PSSR
     C                     END
      *
      * Set up Header Details and date format.
      *
     C           BJDFIN    IFEQ 'M'
     C                     MOVE '1'       *IN98
     C                     END
      *
     C                     MOVELBJURPT    @TITL
     C                     MOVELBJMRDT    @DATE
      *
      * Set up page end
      *
     C           @OVRF     SUB  6         @LIMIT  20
      *
     C                     ENDSR
      *
      ****************************************************************
      /EJECT
      ****************************************************************
      *                                                              *
      * #DETL - Print Details                                        *
      *   CALLED FROM : #MAIN                                        *
      *         CALLS : NO SUBROUTINES                               *
      *                                                              *
      ****************************************************************
     C           #DETL     BEGSR
      *
     C* Read Interest Payment History File.
      *
     C           *LOVAL    SETLLINTPSL24
     C                     READ INTPSL24                 02
     C*
     C* Write Header
     C*
     C                     MOVE BRCA      @BRAN
     C                     WRITEHEADER
     C*
     C           *IN02     IFEQ '0'
     C           *IN02     DOWEQ'0'
     C*
     C                     MOVE CNUM      @CNUM
     C                     MOVE CRNM      @CRNM
     C                     MOVE DLNO      @DLNO
     C                     MOVE CCY       @CCY
     C                     MOVE ACOD      @ACOD
     C                     MOVE ACSQ      @ACSQ
     C                     MOVE BRTCAT    @BRTCA
     C                     MOVE S352RI    @S352R
     C                     MOVE STGGRI    @STGGR
     C                     MOVE STGBRT    @STGBR
     C                     MOVE STGNTI    @STGNT
     C*
     C                     MOVE IPDT      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     @PAYD
      *
      * If overflow or new branch code - new page
      *
     C           @LINE     IFGE @LIMIT
     C           BRCA      ORNE @BRAN
     C                     WRITEHEADER
     C                     MOVE BRCA      @BRAN
     C                     END
     C*
     C                     WRITEDETAIL
     C                     READ INTPSL24                 02
     C                     END
     C*
     C* If no records write no details
     C*
     C                     ELSE
     C                     WRITENODET
     C                     END
     C*
     C* Write End of Report
     C*
     C                     WRITEENDFMT
     C                     ENDSR
     C*
      ****************************************************************
      /EJECT
      ****************************************************************
      *                                                              *
      * *PSSR - DATABASE ERROR PROCESSING                            *
      *         CALLS : NO SUBROUTINES                               *
      *                                                              *
      ****************************************************************
      *
     C           *PSSR     BEGSR
      *
      * Set on U7, U8 and LR
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     MOVE '1'       *INLR
      **
     C           *LIKE     DEFN DBPGM     #PGM
     C           *LIKE     DEFN DBKEY     #KEY
     C           *LIKE     DEFN DBFILE    #FILE
     C           *LIKE     DEFN DBASE     #BASE
      **
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL#PGM      DBPGM
     C                     MOVEL#KEY      DBKEY
     C                     MOVEL#FILE     DBFILE
     C                     MOVEL#BASE     DBASE
     C                     OUT  LDA
      *
      * Produce program dump.
     C                     DUMP
      *
      * Return
     C                     RETRN
      *
     C                     ENDSR
      *
     C********************************************************************
      /EJECT
     C********************************************************************
     C*
     C*  RCFP1 -- SUBROUTINE TO REGISTER P1 PRINTER FILE VIA RCF
     C*
     C********************************************************************
     C*
     C           RCFP1     BEGSR                            ** RCFP1 **
     C*
     C**      ENSURE REPORT SPOOL FILE RECORDED BY RCF
     C*
     C                     Z-ADDSFNUM     ZSNUM   60
     C**
     C                     CALL 'ZSFILE'
     C                     PARM           PSEQ    5
     C                     PARM *BLANKS   SENTY   3
     C                     PARM           SFILE
     C                     PARM           ZSNUM
     C                     PARM *BLANKS   ZSERR   1
     C*
     C           ZSERR     IFEQ 'Y'
     C*
     C**  ERROR DURING ZSFILE PROCESSING, RETURN TO CALLING PROGRAM
     C*
     C                     SETON                     U7U8LR
     C                     RETRN
     C                     END
     C*
     C                     ENDSR
     C*
     C********************************************************************
     C/EJECT
      ****************************************************************
      /COPY ZSRSRC,ZDATE2Z2
      ****************************************************************
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
**   CPY@
(c) Finastra International Limited 2001
