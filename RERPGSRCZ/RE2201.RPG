     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas S17 Report Request')
     F********************************************************************
     F*                                                                  *
     F*  Midas - Retail Module
     F*                                                                  *
     F*    RE2201 - S17 REPORT REQUEST                                   *
     F*                                                                  *
      *  (c) Finastra International Limited 2001                      *
     F*                                                                  *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. S01383             Date 27Apr92               *
     F*                                                                  *
      *  MD046248 - Finastra Rebranding                               *
     F*       S01383 - BASIC RATE TAX INCORPORATION                      *
     F*                NEW PROGRAM                                       *
     F*                                                                  *
     F********************************************************************
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FRE2201DFCF  E                    WORKSTN
     FSDBRTDL1IF  E           K        DISK
      /EJECT
      ***************************************************************** **
      **                                                                **
      **    F U N C T I O N   O F   I N D I C A T O R S                 **
      **    ~~~~~~~~~~~~~~~   ~~~   ~~~~~~~~~~~~~~~~~~~                 **
      **                                                                **
      **        02          READ FAIL, FILE SDBRTDPD                    **
      **        03          CMD/3 PRESSED                               **
      **        09          PROTECT INPUT FIELDS IF END OF TAX YEAR     **
      **        10          DISPLAY ERROR MESSAGE                       **
      **        11          NON DISPLAY BANKS                           **
      **        12          INDIVIDUALS FIELD IN ERROR                  **
      **        13          CORPORATES FIELD IN ERROR                   **
      **        14          BANKS FIELD IN ERROR                        **
      **        15          DATAFILE FIELD IN ERROR                     **
      **        LR          END OF PROGRAM                              **
      **                                                                **
      ***************************************************************** **
      /EJECT
     E                    ERR     1   3 77
     I            DS
     I                                        1   2 PASSBK
     I                                        1   1 MODE
     I                                        2   2 DATAF
     IPSDS       SDS
     I                                      244 253 WID
     I                                      254 263 USRID
     IRUNDAT      DS                             13
     I                                        1   7 RUNA
     I** Rundate Data Area
     I*
     C***************************************************************
      *                                                             *
      *   MAIN   PROCESSING                                         *
      *                                                             *
      *   CALLS  INITL  -  INITIAL PROCESSING                       *
      *          DETAIL -  DETAIL PROCESSING                        *
      *          TERM   -  TERMINATION PROCESSING                   *
      *                                                             *
      ***************************************************************
      *
      *  PERFORM INITIAL PROCESSING
      *
     C                     EXSR INITL
      *
      *  PERFORM DETAIL PROCESSING
      *
     C                     EXSR DETAIL
      *
      *  PERFORM TERMINATION PROCESSING
      *
     C                     EXSR TERM
     C*
      /EJECT
     C***************************************************************
      *                                                             *
      *   INITIAL PROCESSING - INITL                                *
      *                                                             *
      *   CALLS  NONE                                               *
      *                                                             *
      ***************************************************************
     C           INITL     BEGSR
     C*
     C*  SET UP PARAMETER LIST
     C*
     C           *ENTRY    PLIST
     C                     PARM           SEQ     5
     C                     PARM           LEV     1
     C                     PARM           ENT     3
     C                     PARM           PARM    2
     C                     PARM           ACT     1
     C                     PARM           CMD     1
     C*
     C** Define Rundat Data Area
     C*
     C           *NAMVAR   DEFN           RUNDAT
     C                     IN   RUNDAT
     C*
     C*  CALL RE2189 TO DETERMINE IF TODAY IS END OF TAX YEAR
     C*
     C                     CALL 'RE2189'
     C                     PARM *BLANKS   EOTY    1
     C                     PARM *BLANKS   YY      1
     C*
     C*  IF END OF TAX YEAR DISPLAY ERROR MESSAGE AND PROTECT INPUT
     C*
     C           EOTY      IFEQ 'Y'
     C                     MOVEAERR,3     ERMSG
     C                     MOVE '1'       *IN09
     C                     MOVE '1'       *IN10
     C                     ELSE
     C*
     C*  IF NOT END OF TAX YEAR
     C*  ACCESS PF/SDBRTDPD FOR THE 'BANKS INCLUDED THIS YEAR' FIELD
     C*
     C                     MOVE 'D'       KEY     1
     C           KEY       CHAINSDBRTDD0             02
     C*
     C*  IF 'BANKS INCLUDED THIS YEAR' IS 'N' DO NOT DISPLAY BANK
     C*  OPTION ON MENU
     C*
     C           BTHY      IFEQ 'N'
     C                     MOVE '1'       *IN11
     C                     MOVE 'N'       BANK
     C                     END
     C*
     C                     MOVE *BLANKS   ERMSG
     C*
     C                     END
     C*
     C                     ENDSR
     C*
      /EJECT
     C***************************************************************
      *                                                             *
      *   DETAIL   PROCESSING - DETAIL                              *
      *                                                             *
      *   CALLS  VALID - VALIDATE SCREEN VALUES
      *                                                             *
      ***************************************************************
     C           DETAIL    BEGSR
     C*
     C* DISPLAY SCREEN UNTIL THERE ARE NO ERRORS OR CMD/3 PRESSED
     C*
     C           *INKC     DOUEQ'1'                        B1
     C           *IN09     OREQ '1'                        B1
     C           *IN10     OREQ '0'                        B1
     C*
     C                     EXFMTS17MNUF
     C*
     C* IF CMD/3 IS PRESSED SET RETURN CODE AND EXIT
     C*
     C           *INKC     IFEQ '1'                        B*2
     C                     MOVE 'E'       CMD
     C                     ELSE                            X*2
     C*
     C* VALIDATE VALUES ENTERED AND CONTINUE IF NO ERROR
     C*
     C                     EXSR VALID
     C           *IN10     IFEQ '0'                        B**3
     C*
     C* IF INDIVIDUALS REQUESTED
     C*
     C           INDV      IFEQ 'Y'                        B***4
     C*
     C* IF CORPORATES REQUESTED
     C*
     C           CORP      IFEQ 'Y'                        B****5
     C*
     C* IF BANKS REQUESTED
     C*
     C           BANK      IFEQ 'Y'                        B*****6
     C*
     C* ALL OPTIONS REQUSTED MODE = 1
     C*
     C                     MOVE '1'       MODE
     C*
     C* IF BANKS NOT REQUESTED (INDIVIDUALS & CORPORATES ONLY)
     C* MODE = 2
     C*
     C                     ELSE                            X*****6
     C                     MOVE '2'       MODE
     C                     END                             E*****6
     C*
     C* IF CORPORATES NOT REQUESTED
     C*
     C                     ELSE                            X****5
     C*
     C* IF INDIVIDUALS AND BANKS ONLY
     C*
     C           BANK      IFEQ 'Y'                        B*****6
     C                     MOVE '3'       MODE
     C*
     C* IF INDIVIDUALS ONLY
     C*
     C                     ELSE                            X*****6
     C                     MOVE '4'       MODE
     C                     END                             E*****6
     C*
     C                     END                             E****5
     C*
     C* IF INDIVIDUALS NOT REQUSTED
     C*
     C                     ELSE                            X***4
     C*
     C* IF CORPORATES REQUESTED
     C*
     C           CORP      IFEQ 'Y'                        B****5
     C*
     C* IF CORPORATES AND BANKS ONLY
     C*
     C           BANK      IFEQ 'Y'                        B*****6
     C                     MOVE '5'       MODE
     C*
     C* IF CORPORATES ONLY
     C*
     C                     ELSE                            X*****6
     C                     MOVE '6'       MODE
     C                     END                             E*****6
     C*
     C* IF CORPORATES NOT REQUESTED
     C*
     C                     ELSE                            X****5
     C*
     C* IF BANKS ONLY REQUESTED
     C*
     C           BANK      IFEQ 'Y'                        B*****6
     C                     MOVE '7'       MODE
     C*
     C* IF NO OPTIONS REQUESTED A MESSAGE IS SENT TO THE USER
     C*
     C                     ELSE                            X*****6
     C                     MOVE ERR,2     ERMSG
     C                     MOVE '1'       *IN10
     C                     END                             E*****6
     C*
     C                     END                             E****5
     C*
     C                     END                             E***4
     C*
     C* IF STILL NO ERRORS
     C*
     C           *IN10     IFEQ '0'                        B***4
     C*
     C* SET UP DATAFILE PARAMETER WITH SCREEN VALUE
     C*
     C                     MOVE DATF      DATAF
     C*
     C* AS CMD/3 OR CMD/12 IS NOT TAKEN, SET PARAMETER PASSED BACK
     C*
     C                     MOVELPASSBK    PARM
     C                     END                             E***4
     C*
     C                     END                             E**3
     C*
     C                     END                             E*2
     C*
     C                     END                             E1
     C*
     C                     ENDSR
     C*
      /EJECT
     C***************************************************************
      *                                                             *
      *   VALIDATION PROCESSING - VALID                             *
      *                                                             *
      *   CALLS  NONE
      *                                                             *
      ***************************************************************
     C           VALID     BEGSR
     C*
     C* INITIALISE ALL ERROR INDICATORS
     C*
     C                     MOVE '0'       *IN10
     C                     MOVEA'0000'    *IN,12
     C*
     C* CHECK THAT EACH FIELD EITHER IS 'Y' OR 'N'
     C*
     C           INDV      IFNE 'Y'
     C           INDV      ANDNE'N'
     C                     MOVE '1'       *IN12
     C                     MOVE '1'       *IN10
     C                     MOVE ERR,1     ERMSG
     C                     END
     C*
     C*
     C           CORP      IFNE 'Y'
     C           CORP      ANDNE'N'
     C                     MOVE '1'       *IN13
     C                     MOVE '1'       *IN10
     C                     MOVE ERR,1     ERMSG
     C                     END
     C*
     C           BANK      IFNE 'Y'
     C           BANK      ANDNE'N'
     C                     MOVE '1'       *IN14
     C                     MOVE '1'       *IN10
     C                     MOVE ERR,1     ERMSG
     C                     END
     C*
     C           DATF      IFNE 'Y'
     C           DATF      ANDNE'N'
     C                     MOVE '1'       *IN15
     C                     MOVE '1'       *IN10
     C                     MOVE ERR,1     ERMSG
     C                     END
     C*
     C                     ENDSR
     C*
      /EJECT
     C***************************************************************
      *                                                             *
      *   TERMINATION PROCESSING - TERM                             *
      *                                                             *
      *   CALLS  NONE                                               *
      *                                                             *
      ***************************************************************
     C           TERM      BEGSR
     C*
     C* SET ON LR AND RETURN TO CALLING PROGRAM
     C*
     C                     MOVE '1'       *INLR
     C                     RETRN
     C*
     C                     ENDSR
     C***************************************************************
** ERR
INVALID SELECTION - MUST BE 'Y' OR 'N'
NO SELECTION MADE - PRESS CMD/3 TO EXIT
FULL S17 REPORT WILL BE AUTOMATICALLY PRODUCED - SELECTION NOT REQUIRED
