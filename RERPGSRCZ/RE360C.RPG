     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Transfer bet. Group Jobs-AME enq')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Retail Module
     F*                                                               *
     F*  RE360C  - ENQUIRY UTILITY PROGRAM TO TRANSFER BETWEEN        *
     F*            DIFFERENT ENQUIRIES                                *
     F*                                                               *
     F*  (c) Finastra International Limited 2001                      *
     F*                                                               *
      *  Last Amend No. MD046248           Date 27Oct17               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
     F*  PREV AMEND NO. S01413 *CREATE     DATE 13APR93               *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  MD046248 - Finastra Rebranding                               *
     F*  S01413 - Retail 3 Incorporation                              *
     F*                                                               *
     F*****************************************************************
     F*                                                               *
     F*  C R E A T I O N     P A R A M E T E R S                      *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     F*
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*       U1       Program error - *PSSR executed                 *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E                    CPY@    1   1 80               COPYRIGHT
     E                    XCMD    1   5 80
     E*
     E** Table holding CL commands to be processed by QCAEXEC
     E*
     C/EJECT
     C****************************************************************
     C*                                                              *
     C* MAINLINE Check the parameter passed to this program is a     *
     C*          valid enquiry program name,  if not send a message  *
     C*          to the job log.                                     *
     C*                                                              *
     C*          Call REC360A to transfer *LDA to *GDA, if an error  *
     C*          occurs during the call end this program.            *
     C*                                                              *
     C*          Transfer to a group job, on return to this job call *
     C*          REC360A to transfer *GDA to *LDA.  Return to the    *
     C*          enquiry leaving this transfer program active.       *
     C*                                                              *
     C****************************************************************
     C*
     C** Copyright Insertion
     C*
     C                     MOVEACPY@      BIS@   80
     C*
     C           *ENTRY    PLIST
     C                     PARM           WPARM1  7
     C*
     C           WPARM1    IFEQ 'RE3360 '                  - B1
     C                     Z-ADD1         X       30
     C                     ELSE                            - X1
     C           WPARM1    IFEQ 'RE3361 '                  - B2
     C                     Z-ADD2         X
     C                     ELSE                            - X2
     C           WPARM1    IFEQ 'RE3362 '                  - B3
     C                     Z-ADD3         X
     C                     ELSE                            - X3
     C           WPARM1    IFEQ 'RE3363 '                  - B4
     C                     Z-ADD4         X
     C                     ELSE                            - X4
     C                     Z-ADD5         X
     C                     END                             - E4
     C                     END                             - E3
     C                     END                             - E2
     C                     END                             - E1
     C*
     C                     MOVE 'O'       WPARM2  1
     C                     CALL 'REC360A'
     C                     PARM           WPARM2
     C*
     C           *INU1     IFEQ '1'                        - B1
     C                     EXSR *PSSR
     C                     END                             - E1
     C*
     C                     Z-ADD80        XLEN   155
     C                     CALL 'QCAEXEC'
     C                     PARM           XCMD,X
     C                     PARM           XLEN
     C*
     C                     MOVE 'I'       WPARM2
     C                     CALL 'REC360A'
     C                     PARM           WPARM2
     C*
     C           *INU1     IFEQ '1'                        - B1
     C                     EXSR *PSSR
     C                     END                             - E1
     C*
     C                     RETRN
     C*
     C/EJECT
     C****************************************************************
     C*                                                              *
     C*  *PSSR  Program error trap subroutine.                       *
     C*         *INU1 will be seton if a loop occurs within the SR   *
     C*         or was seton after a call to REC360A ended in error. *
     C*                                                              *
     C****************************************************************
     C*
     C           *PSSR     BEGSR
     C           *INU1     IFEQ '1'                        - B1
     C                     GOTO ENDIT
     C                     END                             - E1
     C*
     C                     SETON                     U1
     C                     DUMP
     C           ENDIT     ENDSR'*CANCL'
     C/EJECT
** CPY@ OBJECT COPYRIGHT
(c) Finastra International Limited 2001
**  XCMD
TFRGRPJOB GRPJOB(RECJOB1) INLGRPPGM(REC361) TEXT('Retail Link Enquiry')
TFRGRPJOB GRPJOB(RECJOB2) INLGRPPGM(REC3361) TEXT('History Position')
TFRGRPJOB GRPJOB(RECJOB3) INLGRPPGM(REC3362) TEXT('History Position')
TFRGRPJOB GRPJOB(RECJOB4) INLGRPPGM(REC3363) TEXT('Interest Details')
SNDPGMMSG *NONE CPF9898 QCPFMSG 'Invalid Parameter Passed' *PRV MSGTYPE(*STATUS)
