     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Expired interest review date report')
      *****************************************************************
      *                                                               *
      *  Midas - Retail Module                                        *
      *                                                               *
      *  RE3700 - Expired Interest Rate Review Dates Report           *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
      *                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. CSD031             Date 10Apr06               *
      *  Prev Amend No. CSD027             Date 09Dec05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 CLE025             Date 20Oct03               *
      *                 CGL029             Date 01Sep03               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CDE002             Date 21Dec99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CRE001  *CREATE    Date 18Feb97               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CGL029 - Increase Account Code to 10 digits                  *
      *  CDE002 - Revenue Analysis - Recompiled due to changes in     *
      *           PF/REACCJL2.                                        *
      *  CRE001 - Overdraft Interest Calculations                     *
      *                                                               *
      *****************************************************************
     FREACCJL2IF  E           K        DISK
      *
     FRE3700P1O   E             70     PRINTER      KINFDS SPOOL      UC
     FRE3700AUO   E                    PRINTER      KINFDS SPOOLU     UC
      *
      *****************************************************************
      *                                                               *
      *  F U N C T I O N   O F  I N D I C A T O R S                   *
      *                                                               *
      *       50      Branch level report.                            *
      *       51      Retail account numbers required.                *
      *       52      Negative Interest rate.                         *
      *       60      General Indicator.                              *
      *       61      General Indicator.                              *
      *       70      RE3700P1 printer file overflow indicator.       *
      *                                                               *
      *       LR      End of program                                  *
      *                                                               *
      *       U7      Database error                                  *
      *       U8      Database error                                  *
      *                                                               *
      *****************************************************************
      /SPACE 3
      *****************************************************************
      *                                                               *
      *  S U B R O U T I N E   I N D E X                              *
      *                                                               *
      *  #FIRST - First Cycle Processing                              *
      *  #MAIN  - Main Processing                                     *
      *  #RTVBR - Retrieve Branch Details                             *
      *                                                               *
      *  *PSSR  - Program Error Processing Subroutine                 *
      *  RCFP1  - RCF Processing for P1 Report                        *
      *  RCFAU  - RCF Processing for Audit Report                     *
      *                                                               *
      *  ZACCH  - Access Record for Holiday File                      *
      *  ZFWDT  - Find next working from a given date                 *
      *  ZDATE2 - Convert Midas Day Number into Date (DDMMYY/MMDDYY)  *
      *                                                               *
      *****************************************************************
     E                    CPY@    1   1 80
     E/COPY ZSRSRC,ZHOLE
     E/COPY ZSRSRC,ZDATE2Z1
      *
      **  File information data structure
      *
     ISPOOL       DS
     I                                       83  92 SFILE
     I                                    B 123 1240SFNUM
     I                                    B 188 1890OFLLN
     I                                    B 367 3680PRTLN1
      *
     ISPOOLU      DS
     I                                       83  92 SFILEU
     I                                    B 123 1240SFNUMU
      *
     ILDA        UDS                            256
     I                                      134 141 DBFILE
     I                                      142 170 DBKEY
     I                                      171 180 DBPGM
     I                                      181 1830DBASE
     IRUNDT       DS
      ** Data area RUNDAT
      *
     I                                        1   7 RUNA
     I                                    P   8  100RUND
     I                                       11  11 SSF
     I                                       12  12 DATF
     I                                       13  13 MBIN
      *
     I/COPY ZSRSRC,ZHOLI
     I/COPY ZSRSRC,ZHOLDS
      *
      ** Data structures for use with access programs
      *
     ISDBANK    E DSSDBANKPD
     I              BJRDNB                          SRUND
     I              BJDFIN                          SDATF
     I              BJMRDT                          SRUNA
     I              BJURPT                          STITL
      ** Bank Details
      *
     ISDBRCH    E DSSDBRCHPD
     I              A8BRCD                          BBRCA
     I              A8BRNM                          BRNM
     I              A8BICN                          BICN
     I              A8DTAC                          NBACB
      ** Branch Codes
      *
     ISDRETL    E DSSDRETLPD
     I              BMLDAI                          LDAC
     I              BMCBAI                          CBAI
     I              BMNDFV                          NDFV
     I              BMNDBI                          NDBI
     I              BMRCPI                          RCPT
     I              BMRANR                          RACN
      ** Retail Installation Control Details
      *
     IDSFDY     E DSDSFDY
      ** First DS for access programs, Short data structure
      *
     C/EJECT
      *================================================================
      *  M A I N   P R O C E S S I N G
      *================================================================
     C                     MOVEACPY@      CPY2@  80
      *
     C           *ENTRY    PLIST
     C                     PARM           @LVL    1
     C                     PARM           SEQ     5
      ** LDA
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBFILE
     C                     MOVE *BLANKS   DBKEY
     C                     MOVE 'RE3700  'DBPGM
     C                     OUT  LDA
      *
      ** RUNDAT
     C           *NAMVAR   DEFN RUNDAT    RUNDT
     C                     IN   RUNDT
      *
      *
      ** Audit report.
     C                     OPEN RE3700AU
      ** Recorded by RCF.
     C                     EXSR RCFAU
      *
     C                     EXSR #FIRST
      *
     C                     EXSR #MAIN
      *
      ** Write Audit rec.
     C                     WRITEDETLAU
     C                     SETON                     LR
     C                     RETRN
      *================================================================
     C/EJECT
      *****************************************************************
      *                                                               *
      *  #FIRST - Initial Processing                                  *
      *                                                               *
      *  CALLED BY:   Main                                            *
      *                                                               *
      *  CALLS    :   ZACCH - Access Record for Holiday File          *
      *               ZFWDT - Find next working day from given date   *
      *               *PSSR - Program Error Processing Subroutine     *
      *                                                               *
      *****************************************************************
     C           #FIRST    BEGSR
      *
      ** Initialise counts/work-fields.
     C                     Z-ADD0         TOTCNT  50
     C                     Z-ADD0         EXPCNT  50
     C                     Z-ADD0         RPTCNT  50
     C                     Z-ADD0         PAGE
      *
     C                     MOVE *BLANKS   BRCA#
      *
      ** Set Branch indicator from *Entry parameter
     C           @LVL      IFEQ 'B'
     C                     MOVE '1'       *IN50
     C                     END
      *
      ** Access Bank Details.
     C                     CALL 'AOBANKR0'
     C                     PARM '*MSG   ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE           ***************
     C                     MOVEL@OPTN     DBKEY            * DBERROR 001 *
     C                     Z-ADD001       DBASE            ***************
     C                     OUT  LDA
     C                     WRITEERRAU
     C                     EXSR *PSSR
     C                     END
      *
      ** Write Audit Header.
     C                     WRITEHEADAU
      *
      ** Set up Advance Control Date.
      ** (ie. Next Working day + 5)
     C                     Z-ADDBJDNWD    ZDAYNO
     C                     MOVELBJCYCD    ZCCY
     C                     MOVE *BLANK    ZLOC
     C                     Z-ADD5         ZNRDYS
     C                     Z-ADD0         ZDYNBR
      *
     C                     EXSR ZACCH
      *
     C                     EXSR ZFWDT
      *
     C                     Z-ADDZDYNBR    ACDATE  50
      *
      ** Access Retail ICD
     C                     CALL 'AORETLR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDRETL    PARM SDRETL    DSFDY
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDRETLPD'DBFILE           ***************
     C                     MOVEL@OPTN     DBKEY            * DBERROR 002 *
     C                     Z-ADD002       DBASE            ***************
     C                     WRITEERRAU
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
      ** Check if Retail A/c Numbers are supported.
     C           RACN      IFEQ 'Y'
     C                     MOVE '1'       *IN51
     C                     END
      *
     C                     ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *  #MAIN - Main Routine                                         *
      *                                                               *
      *  CALLED BY:   Main                                            *
      *                                                               *
      *  CALLS    :   RCFP1 - RCF Processing for P1 Report            *
      *               #RTVBR - Retrieve Branch Details                *
      *               ZDATE2 - Convert Midas Day Number into Date     *
      *                                                               *
      *****************************************************************
     C           #MAIN     BEGSR
      *
     C           *LOVAL    SETLLREACCJL2
     C                     READ REACCJL2                 60
      *
      *
     C           *IN60     DOWEQ'0'
      *
     C                     ADD  1         TOTCNT
      *
      ** Process if Review date
      ** is within the next four
      ** working days.
     C           ATITRD    IFGT 0
     C           ATITRD    ANDLTACDATE
     C                     ADD  1         EXPCNT
     C                     ADD  1         RPTCNT
      *
      ** Change in the
      ** Branch code.
     C           BRCA      IFNE BRCA#
      *
      ** Branch level
      ** report.
     C           @LVL      IFEQ 'B'
      *
     C           *IN61     IFEQ '1'
     C                     WRITEENDP1
     C                     CLOSERE3700P1
     C                     ELSE
     C                     MOVE '1'       *IN61
     C                     END
      *
     C                     OPEN RE3700P1
     C                     EXSR RCFP1
      ** Get Branch details.
     C                     EXSR #RTVBR
     C                     Z-ADD0         PAGE
     C                     Z-ADD0         RPTCNT
      *
     C                     ELSE
      ** Consolidated
      ** report.
     C           *IN61     IFEQ '0'
     C                     OPEN RE3700P1
     C                     EXSR RCFP1
     C                     MOVE '1'       *IN61
     C                     END
      *
     C                     END
      ** Save Branch Code.
     C                     MOVE BRCA      BRCA#
     C                     WRITEHEADP1
     C                     END
      ** Format the
      ** Interest Rate.
     C           ATAODR    IFLT 0
     C                     MOVE '1'       *IN52
     C                     ELSE
     C                     MOVE '0'       *IN52
     C                     END
      ** Format the
      ** Review Date.
     C                     Z-ADDATITRD    ZDAYNO
     C           DATF      IFEQ 'D'
     C                     MOVE '0'       *IN98
     C                     ELSE
     C                     MOVE '1'       *IN98
     C                     ENDIF
     C                     EXSR ZDATE2
     C                     MOVE ZADATE    OTITRD
      ** Account Officer
     C           AZACON    IFEQ *BLANKS
     C                     MOVE '1'       *IN53
     C                     ELSE
     C                     MOVE '0'       *IN53
     C                     END
      *
      ** Check overflow.
     C           *IN70     IFEQ '1'
     C                     WRITEHEADP1
     C                     MOVE '0'       *IN70
     C                     END
      ** Write details.
     C                     WRITEDETP1
     C                     END
      *
     C                     READ REACCJL2                 60
     C                     ENDDO
      *
      ** Close file.
     C           *IN61     IFEQ '1'
     C                     WRITEENDP1
     C                     CLOSERE3700P1
     C                     END
      *
     C                     ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *  RCFP1 - RCF Processing for P1 Report                         *
      *                                                               *
      *  CALLED BY:   #MAIN - Main Routine                            *
      *                                                               *
      *  CALLS    :   *PSSR - Program Error Processing Subroutine     *
      *                                                               *
      *****************************************************************
     C           RCFP1     BEGSR
      *
     C                     Z-ADDSFNUM     SFNUM1  60
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ     5
     C                     PARM BRCA      SENTY   3
     C                     PARM           SFILE
     C                     PARM           SFNUM1
     C                     PARM           ZSERR   1
      *
      **  Error during ZSFILE processing, return to calling program
      *
     C           ZSERR     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     MOVEL'ZSFILE  'DBFILE           ***************
     C                     MOVELSFILE     DBKEY            * DBERROR 004 *
     C                     Z-ADD004       DBASE            ***************
     C                     WRITEERRAU
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
     C                     ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *  RCFAU - RCF Processing for AU Report                         *
      *                                                               *
      *  CALLED BY:   Main                                            *
      *                                                               *
      *  CALLS    :   *PSSR - Program Error Processing Subroutine     *
      *                                                               *
      *****************************************************************
     C           RCFAU     BEGSR
      *
     C                     Z-ADDSFNUMU    SFNUM2  60
     C                     CALL 'ZSFILE'
     C                     PARM           SEQ
     C                     PARM *BLANKS   @PARM   3
     C                     PARM           SFILEU
     C                     PARM           SFNUM2
     C                     PARM           ZSERR
      *
      ** Error during ZSFILE processing,
      ** return to calling program
     C           ZSERR     IFEQ 'Y'
     C           *LOCK     IN   LDA
     C                     MOVEL'ZSFILE  'DBFILE           ***************
     C                     MOVELSFILEU    DBKEY            * DBERROR 005 *
     C                     Z-ADD005       DBASE            ***************
     C                     WRITEERRAU
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
     C                     ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *  #RTVBR - Retrieve Branch Details                             *
      *                                                               *
      *  CALLED BY:   #MAIN - Main Routine                            *
      *                                                               *
      *  CALLS    :   *PSSR - Program Error Processing Subroutine     *
      *                                                               *
      *****************************************************************
     C           #RTVBR    BEGSR
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*MSG   ' @RTCD
     C                     PARM '*KEY   ' @OPTN
     C                     PARM BRCA      @BRCA   3
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBRCHPD'DBFILE           ***************
     C                     MOVELBRCA      DBKEY            * DB ERROR 004*
     C                     Z-ADD004       DBASE            ***************
     C                     WRITEERRAU
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     END
      *
     C                     ENDSR
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *  *PSSR -  Subroutine to handle abnormal error conditions      *
      *                                                               *
      *  CALLED BY:   After abnormal operation occurs                 *
      *                                                               *
      *  CALLS    :   Nothing                                         *
      *                                                               *
      *****************************************************************
      *
     C           *PSSR     BEGSR                            ** PSSR **
     C                     MOVE '1'       *INLR
     C                     MOVE '1'       *INU7
     C                     MOVE '1'       *INU8
     C                     DUMP
     C                     RETRN
      *
     C                     ENDSR                            ** PSSR **
      *****************************************************************
     C/EJECT
     C/COPY ZSRSRC,ZDATE2Z2
     C/COPY ZSRSRC,ZACCH
     C/COPY ZSRSRC,ZFWDT
      *****************************************************************
**  CPY@
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
