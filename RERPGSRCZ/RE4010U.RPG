     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE T&N Accounts - Period Rtv/Upd Routine')       *
      *****************************************************************
      *                                                               *
      *  Midas     - Retail T&N Accounts                              *
      *                                                               *
      *  RPG/RE4010U - Period & Penalties Update Routine              *
      *                                                               *
      *  (c) Misys International Banking Systems Ltd. 2009            *
      *                                                               *
      *  Last Amend No. CRE073             Date 06Dec10               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *  Prev Amend No. 227316             Date 18Dec09               *
      *                 223607             Date 18Dec09               *
      *                 CRE015 *CREATE     Date 18Dec09               *
      *                                                               *
      *---------------------------------------------------------------*
      *                                                               *
      *  CRE073 - Interest Rate Rounding                              *
      *  227316 - Prevent update/delete without prior read/chain      *
      *  223607 - Pass on interest calc basis to ACCNTAB              *
      *  CRE015 - Retail Term and Notice Accounts                     *
      *                                                               *
      *****************************************************************
      *
     FREPRPNPDUF  E           K        DISK         KCOMIT       A
      *
      ** Convert counter to (binary) bit settings
      *
     IDSCVT       DS
     I                                    B   1   20WEIGHT
     I                                        2   2 BYTE
      *
      ** Parameters
      *
     IP@FMT     E DSDSSDY
      *
      ** Bank details
      *
     IBKFMT     E DSSDBANKPD
      *
      ** Period and Penalty details
      *
     IREPFMT    E DSREPRPNPD
      *
      ** Local data area for database error details
      ** *LOCK IN LDA immediately before and OUT LDA immediately
      ** after each database error handling.
      **
      **                                    134 141 DBFILE
      **       Defines:                     142 170 DBKEY
      **                                    171 180 DBPGM
      **                                    181 1830DBASE
      *
     ILDA       E DSLDA                         256
     I              SPARE                           W24
      *
      ** Program Status Data Structure - gives Job/Workstation name and
      ** User ID
      *
     IPSDS       SDS
     I                                     *PROGRAM ##PGM
     I                                      244 253 JOB
     I                                      244 246 WSID
     I                                      254 263 USER
      *
     I***W##FMT*     DS                            268                                        CRE073
     IW##FMT      DS                            304                                           CRE073
      *
      *****************************************************************
      *
      ** Initialise
      *
     C                     EXSR INIT
      *
      ** UPDATE MODE
      *
     C           PMODE     IFEQ '*UPD'
     C           PMODE     OREQ '*UPDIND'
     C                     EXSR UPD
     C                     END
      *
      ** DELETE MODE
      *
     C           PMODE     IFEQ '*DELT'
     C                     EXSR DELT
     C                     END
      *
      ** FREE MODE
      *
     C           PMODE     IFEQ '*FREE'
     C                     CALL 'ZFRQB5'
     C                     PARM *BLANKS   ZFREQ   1
     C                     PARM           ZDAYNO  50
     C                     PARM           ZMDAY   20
     C                     PARM           ZCCY    3
     C                     PARM           ZLOC    3
     C                     PARM           ZDIREC  1
     C                     PARM *BLANKS   ZRTRN   7
     C                     FREE 'ZFRQB5'
     C                     SETON                     LR
     C                     RETRN
     C                     END
      *
     C                     RETRN
      *****************************************************************
      *                                                               *
      *  DELT   - Delete                                              *
      *                                                               *
      *****************************************************************
     C           DELT      BEGSR
      *
     C                     MOVELPBRCA     E2BRCA
     C                     MOVE PCNUM     E2CNUM
     C                     MOVELPCCY      E2CCY
     C                     MOVE PACOD     E2ACOD
     C                     MOVE PACSQ     E2ACSQ
     C                     MOVELPEVNT     E2EVNT
     C                     MOVE PITEM     E2ITEM
     C                     MOVELPBRCA     W2BRCA  3
     C                     MOVE PCNUM     W2CNUM  6
     C                     MOVELPCCY      W2CCY   3
     C                     MOVE PACOD     W2ACOD 100
     C                     MOVE PACSQ     W2ACSQ  20
     C                     MOVEL' '       WSTOP   1
     C           KPRPN3    SETLLREPRPNPD
     C           WSTOP     DOUEQ'Y'
     C                     READ REPRPNPD                 81
     C           W2BRCA    IFEQ E2BRCA
     C           W2CNUM    ANDEQE2CNUM
     C           W2CCY     ANDEQE2CCY
     C           W2ACOD    ANDEQE2ACOD
     C           W2ACSQ    ANDEQE2ACSQ
     C           *IN81     ANDEQ'0'                                                           227316
     C                     DELETREPRPND0
     C                     ELSE
     C                     MOVEL'Y'       WSTOP
     C                     END
     C                     END
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  UPD    - Update                                              *
      *                                                               *
      *****************************************************************
     C           UPD       BEGSR
      *
      ** Chain to record to set pointer
      *
     C                     MOVELPBRCA     E2BRCA
     C                     MOVE PCNUM     E2CNUM
     C                     MOVELPCCY      E2CCY
     C                     MOVE PACOD     E2ACOD
     C                     MOVE PACSQ     E2ACSQ
     C                     MOVELPEVNT     E2EVNT
     C                     Z-ADD0         E2ITEM
     C           KPRPN2    CHAINREPRPNPD             81
      *
      ** Save values of rates
      *
     C                     MOVE E2DRIB    RDRIB   2
     C                     MOVE E2DRIS    RDRIS  117
     C                     MOVE E2DICT    RDICT   2
     C                     MOVE E2DCST    RDCST   50
     C                     MOVE E2DRIC    RDRIC   10                                          223607
     C                     MOVE E2CRIB    RCRIB   2
     C                     MOVE E2CRIS    RCRIS  117
     C                     MOVE E2CICT    RCICT   2
     C                     MOVE E2CCST    RCCST   50
     C                     MOVE E2CRIC    RCRIC   10                                          223607
     C                     MOVE E2DCSP    RDCSP  117                                          CRE073
     C                     MOVE E2DCSG    RDCSG   1                                           CRE073
     C                     MOVE E2DRMT    RDRMT   7                                           CRE073
     C                     MOVE E2DRFD    RDRFD   4                                           CRE073
     C                     MOVE E2CCSP    RCCSP  117                                          CRE073
     C                     MOVE E2CCSG    RCCSG   1                                           CRE073
     C                     MOVE E2CRMT    RCRMT   7                                           CRE073
     C                     MOVE E2CRFD    RCRFD   4                                           CRE073
      *
      ** Move in new values
      *
     C                     EXSR MOVPE2
      *
      ** Calculate value, maturity, end dates
      *
     C                     EXSR CALDAT
      *
      ** If independent from accounts maintenance, and active term,
      **  i.e. run date lies within value and maturity dates
      **  and if rates have changed
      **  or  interest calculation basis has changed
      **  then calculate and apply rates
      **  and update other details to accounts file
      *
     C           PMODE     IFEQ '*UPDIND'
     C           E2VDAT    ANDLEBJRDNB
     C           E2MDAT    ANDGEBJRDNB
     C           RDRIB     IFNE PDRIB
     C           RDRIS     ORNE PDRIS
     C           RDICT     ORNE PDICT
     C           RDCST     ORNE PDCST
     C           RDRIC     ORNE PDRIC                                                         223607
     C           RCRIB     ORNE PCRIB
     C           RCRIS     ORNE PCRIS
     C           RCICT     ORNE PCICT
     C           RCCST     ORNE PCCST
     C           RCRIC     ORNE PCRIC                                                         223607
     C           RDCSP     ORNE PDCSP                                                         CRE073
     C           RDCSG     ORNE PDCSG                                                         CRE073
     C           RDRMT     ORNE PDRMT                                                         CRE073
     C           RDRFD     ORNE PDRFD                                                         CRE073
     C           RCCSP     ORNE PCCSP                                                         CRE073
     C           RCCSG     ORNE PCCSG                                                         CRE073
     C           RCRMT     ORNE PCRMT                                                         CRE073
     C           RCRFD     ORNE PCRFD                                                         CRE073
      *
     C                     EXSR CALRAT
      *
     C                     ENDIF
     C                     ENDIF
      *
      ** Do interest notifications
      *
     C                     EXSR DONOTI
      *
      ** Update P+P file
      *
     C                     EXSR UPDFIL
      *
      ** Move values into output parameters
      *
     C                     EXSR MOVE2P
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  INIT   - Program Initialization                              *
      *                                                               *
      *****************************************************************
     C           INIT      BEGSR
      *
     C           *ENTRY    PLIST
     C                     PARM           PMODE   7
     C                     PARM           PBRCA   3
     C                     PARM           PCNUM   6
     C                     PARM           PCCY    3
     C                     PARM           PACOD  10
     C                     PARM           PACSQ   2
     C                     PARM           PDATE   50
     C                     PARM           PEVNT   1
     C                     PARM           PITEM   50
     C                     PARM           PVDAT   50
     C                     PARM           PMDAT   50
     C                     PARM           PCEDE   50
     C                     PARM           PFREQ   1
     C                     PARM           PNDPD   30
     C                     PARM           PNMPD   20
     C                     PARM           PIRLI   1
     C                     PARM           PTPRD   1
     C                     PARM           PDRIB   2
     C                     PARM           PDRIS  117
     C                     PARM           PDICT   2
     C                     PARM           PDCST   50
     C                     PARM           PDRIC   10
     C                     PARM           PDNIR   1
     C                     PARM           PDRIF   1
     C                     PARM           PDACP  24
     C                     PARM           PNDID   50
     C                     PARM           PDRDY   40
     C                     PARM           PCRIB   2
     C                     PARM           PCRIS  117
     C                     PARM           PCICT   2
     C                     PARM           PCCST   50
     C                     PARM           PCRIC   10
     C                     PARM           PCNIR   1
     C                     PARM           PCRIF   1
     C                     PARM           PCACP  24
     C                     PARM           PNCID   50
     C                     PARM           PCRDY   40
     C                     PARM           PRTTD   5
     C                     PARM           PDYBD   1
     C                     PARM           PDRST   1
     C                     PARM           PRTTC   5
     C                     PARM           PDYBC   1
     C                     PARM           PCRST   1
     C                     PARM           PDAYM   20
     C                     PARM           PKEY1  24
     C                     PARM           PBALN  150
     C                     PARM           PRTCD   7
     C                     PARM           PSTAT   1
     C                     PARM           MAINT   7
     C                     PARM           PDCSP  117                                          CRE073
     C                     PARM           PDCSG   1                                           CRE073
     C                     PARM           PDRMT   7                                           CRE073
     C                     PARM           PDRFD   4                                           CRE073
     C                     PARM           PCCSP  117                                          CRE073
     C                     PARM           PCCSG   1                                           CRE073
     C                     PARM           PCRMT   7                                           CRE073
     C                     PARM           PCRFD   4                                           CRE073
      *
      **   Define the LDA for error handling
      *
     C           *NAMVAR   DEFN           LDA
      *
      ** Bank details
      *
     C           BKFMT     IFEQ *BLANKS
     C                     CALL 'AOBANKR0'
     C                     PARM *BLANKS   P@RTCD  7
     C                     PARM '*FIRST'  P@OPTN  7
     C           BKFMT     PARM *BLANKS   P@FMT
     C                     END
      *
     C           KPRPN1    KLIST
     C                     KFLD           E2BRCA
     C                     KFLD           E2CNUM
     C                     KFLD           E2CCY
     C                     KFLD           E2ACOD
     C                     KFLD           E2ACSQ
     C                     KFLD           E2EVNT
     C                     KFLD           E2ITEM
      *
     C           KPRPN2    KLIST
     C                     KFLD           E2BRCA
     C                     KFLD           E2CNUM
     C                     KFLD           E2CCY
     C                     KFLD           E2ACOD
     C                     KFLD           E2ACSQ
     C                     KFLD           E2EVNT
      *
     C           KPRPN3    KLIST
     C                     KFLD           E2BRCA
     C                     KFLD           E2CNUM
     C                     KFLD           E2CCY
     C                     KFLD           E2ACOD
     C                     KFLD           E2ACSQ
      *
      ** Read record of REPRPNPD to avoid problem of write
      ** without prior read/chain
      *
     C           *LOVAL    SETLLREPRPNPD
     C                     READ REPRPNPD                 80
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  CALDAT - Date Calculations                                   *
      *                                                               *
      *****************************************************************
     C           CALDAT    BEGSR
      *
     C                     MOVE BJRDNB    RUNCHG
     C                     CALL 'RETNDAT1'
     C                     PARM           REPFMT
     C                     PARM           MAINT
     C                     PARM           RUNCHG  5
     C                     PARM           PDATE   50
     C                     PARM           PRTCD   7
      *
     C           PRTCD     IFEQ '*ERROR'
     C           *INU7     OREQ '1'
     C           *INU8     OREQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD1         DBASE
     C                     MOVEL##PGM     DBPGM
     C                     MOVEL'RETNDAT1'DBFILE
     C                     MOVEL'*CALL'   DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  CALRAT - Rate Calculations                                   *
      *                                                               *
      *****************************************************************
     C           CALRAT    BEGSR
      *
      ** Calculate and apply rates
      *
     C                     MOVE 'RERCH'   P#MODE
     C                     CALL 'RE4017'
      *
      ** Record format of REPRPNPD
      *
     C                     PARM           REPFMT
      *
      ** Return Code
      *
     C                     PARM           PRTCD   7
     C                     PARM           P#MODE  5
      *
      ** Effective debit rates
      *
     C                     PARM 0         QDRIB   20
     C                     PARM 0         QDRIS  117
     C                     PARM 0         QDICT   20
     C                     PARM 0         QDCST   50
      *
      ** Effective credit rates
      *
     C                     PARM 0         QCRIB   20
     C                     PARM 0         QCRIS  117
     C                     PARM 0         QCICT   20
     C                     PARM 0         QCCST   50
      *
     C           PRTCD     IFEQ '*ERROR'
     C           *INU7     OREQ '1'
     C           *INU8     OREQ '1'
     C           *LOCK     IN   LDA
     C                     Z-ADD2         DBASE
     C                     MOVEL##PGM     DBPGM
     C                     MOVEL'RE4017  'DBFILE
     C                     MOVEL'*CALL'   DBKEY
     C                     OUT  LDA
     C                     EXSR *PSSR
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  DONOTI - A change in Termination period start date or        *
      *           interest notif type requires a change in            *
      *           P+P notifications (termination and interest).       *
      *                                                               *
      *****************************************************************
     C           DONOTI    BEGSR
      *
     C           E2EVNT    IFEQ 'Z'
      *
     C           MAINT     IFEQ '*EXIST'
     C           MAINT     OREQ '*NEW  '
      *
      **  Update termination notification
      *
     C                     CALL 'RE4012'
     C                     PARM '*TERMIN' PWMODE  7
     C                     PARM E2KEY1    PKEY1  24
     C                     PARM *BLANKS   PTRR1   8
     C                     PARM -1        PREAM  150
     C                     PARM *BLANKS   PDC01   1
     C                     PARM -1        PCAVL  150
     C                     PARM *BLANKS   PDC02   1
     C                     PARM E2VDAT    PDEFF   50
     C                     PARM BJRDNB    PRECD   50
     C                     PARM -1        PHISB  150
     C                     PARM *BLANKS   PDC03   1
     C                     PARM -2        PRTDE   20
      *
      **  Update interest notification
      *
     C           E2IRLI    IFEQ 'F'
     C                     CALL 'RE4012'
     C                     PARM '*RMVINT' PWMODE
     C                     PARM E2KEY1    PKEY1
     C                     PARM *BLANKS   PTRR1
     C                     PARM -1        PREAM
     C                     PARM *BLANKS   PDC01
     C                     PARM -1        PCAVL
     C                     PARM *BLANKS   PDC02
     C                     PARM 0         PDEFF
     C                     PARM 0         PRECD
     C                     PARM -1        PHISB
     C                     PARM *BLANKS   PDC03
     C                     PARM -2        PRTDE   20
     C                     END
      *
     C           E2IRLI    IFEQ 'P'
     C                     CALL 'RE4012'
     C                     PARM '*ADDINT' PWMODE
     C                     PARM E2KEY1    PKEY1
     C                     PARM *BLANKS   PTRR1
     C                     PARM 0         PREAM
     C                     PARM 'D'       PDC01
     C                     PARM 0         PCAVL
     C                     PARM 'D'       PDC02
     C                     PARM 0         PDEFF
     C                     PARM 0         PRECD
     C                     PARM 0         PHISB
     C                     PARM 'D'       PDC03
     C                     PARM -2        PRTDE   20
     C                     END
     C                     END
      *
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  UPDFIL - Update Files                                        *
      *                                                               *
      *****************************************************************
     C           UPDFIL    BEGSR
      *
     C                     MOVELREPFMT    W##FMT
     C           KPRPN2    CHAINREPRPNPD             81
     C                     MOVELW##FMT    REPFMT
      *
      ** If no record exists, then write
      *
     C           *IN81     IFEQ '1'
     C                     WRITEREPRPND0
      *
      ** else update record
      *
     C                     ELSE
     C                     UPDATREPRPND0
     C                     ENDIF
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  MOVE2P                                                       *
      *                                                               *
      *****************************************************************
     C           MOVE2P    BEGSR
      *
     C                     MOVELE2EVNT    PEVNT
     C                     Z-ADDE2ITEM    PITEM
     C                     Z-ADDE2VDAT    PVDAT
     C                     Z-ADDE2MDAT    PMDAT
     C                     Z-ADDE2CEDE    PCEDE
     C                     Z-ADDE2NDPD    PNDPD
     C                     Z-ADDE2NMPD    PNMPD
     C                     MOVELE2FREQ    PFREQ
     C                     MOVELE2IRLI    PIRLI
     C                     MOVELE2TPRD    PTPRD
     C                     MOVELE2DRIB    PDRIB
     C                     MOVELE2DRIS    PDRIS
     C                     MOVELE2DICT    PDICT
     C                     MOVELE2DCST    PDCST
     C                     MOVELE2DRIC    PDRIC
     C                     MOVELE2DNIR    PDNIR
     C                     MOVELE2DRIF    PDRIF
     C                     MOVELE2DACP    PDACP
     C                     MOVELE2CRIB    PCRIB
     C                     MOVELE2CRIS    PCRIS
     C                     MOVELE2CICT    PCICT
     C                     MOVELE2CCST    PCCST
     C                     MOVELE2CRIC    PCRIC
     C                     MOVELE2CNIR    PCNIR
     C                     MOVELE2CRIF    PCRIF
     C                     MOVELE2CACP    PCACP
     C                     MOVELE2RTTD    PRTTD
     C                     MOVELE2DYBD    PDYBD
     C                     MOVELE2DRST    PDRST
     C                     MOVELE2RTTC    PRTTC
     C                     MOVELE2DYBC    PDYBC
     C                     MOVELE2CRST    PCRST
     C                     Z-ADDE2DAYM    PDAYM
     C                     Z-ADDE2NDID    PNDID
     C                     Z-ADDE2DRDY    PDRDY
     C                     Z-ADDE2NCID    PNCID
     C                     Z-ADDE2CRDY    PCRDY
     C                     Z-ADDE2BALN    PBALN
     C                     MOVELE2KEY1    PKEY1
     C                     MOVELE2DCSP    PDCSP                                               CRE073
     C                     MOVELE2DCSG    PDCSG                                               CRE073
     C                     MOVELE2DRMT    PDRMT                                               CRE073
     C                     MOVELE2DRFD    PDRFD                                               CRE073
     C                     MOVELE2CCSP    PCCSP                                               CRE073
     C                     MOVELE2CCSG    PCCSG                                               CRE073
     C                     MOVELE2CRMT    PCRMT                                               CRE073
     C                     MOVELE2CRFD    PCRFD                                               CRE073
      *
     C                     ENDSR
      *****************************************************************
      *                                                               *
      *  MOVPE2                                                       *
      *                                                               *
      *****************************************************************
     C           MOVPE2    BEGSR
      *
     C                     MOVELPEVNT     E2EVNT
     C                     Z-ADDPITEM     E2ITEM
     C                     Z-ADDPVDAT     E2VDAT
     C                     Z-ADDPMDAT     E2MDAT
     C                     Z-ADDPCEDE     E2CEDE
     C                     Z-ADDPNDPD     E2NDPD
     C                     Z-ADDPNMPD     E2NMPD
     C                     MOVELPFREQ     E2FREQ
     C                     MOVELPIRLI     E2IRLI
     C                     MOVELPTPRD     E2TPRD
     C                     MOVELPDRIB     E2DRIB
     C                     MOVELPDRIS     E2DRIS
     C                     MOVELPDICT     E2DICT
     C                     MOVELPDCST     E2DCST
     C                     MOVELPDRIC     E2DRIC
     C                     MOVELPDNIR     E2DNIR
     C                     MOVELPDRIF     E2DRIF
     C                     MOVELPDACP     E2DACP
     C                     MOVELPCRIB     E2CRIB
     C                     MOVELPCRIS     E2CRIS
     C                     MOVELPCICT     E2CICT
     C                     MOVELPCCST     E2CCST
     C                     MOVELPCRIC     E2CRIC
     C                     MOVELPCNIR     E2CNIR
     C                     MOVELPCRIF     E2CRIF
     C                     MOVELPCACP     E2CACP
     C                     MOVELPRTTD     E2RTTD
     C                     MOVELPDYBD     E2DYBD
     C                     MOVELPDRST     E2DRST
     C                     MOVELPRTTC     E2RTTC
     C                     MOVELPDYBC     E2DYBC
     C                     MOVELPCRST     E2CRST
     C                     Z-ADDPDAYM     E2DAYM
     C                     Z-ADDPNDID     E2NDID
     C                     Z-ADDPDRDY     E2DRDY
     C                     Z-ADDPNCID     E2NCID
     C                     Z-ADDPCRDY     E2CRDY
     C                     Z-ADDPBALN     E2BALN
     C                     MOVELPKEY1     E2KEY1
     C                     MOVELPDCSP     E2DCSP                                              CRE073
     C                     MOVELPDCSG     E2DCSG                                              CRE073
     C                     MOVELPDRMT     E2DRMT                                              CRE073
     C                     MOVELPDRFD     E2DRFD                                              CRE073
     C                     MOVELPCCSP     E2CCSP                                              CRE073
     C                     MOVELPCCSG     E2CCSG                                              CRE073
     C                     MOVELPCRMT     E2CRMT                                              CRE073
     C                     MOVELPCRFD     E2CRFD                                              CRE073
      *
     C                     ENDSR
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR  - Program exception error routine                      *
      *          Called automatically if a program error occurs,      *
      *          or directly by the program code using EXSR.          *
      *          This subroutine DUMPs the program just once.         *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
      *
     C           @RUN      IFEQ *BLANK
     C                     MOVE 'Y'       @RUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
      *
     C                     ENDIF
      *
     C                     ENDSR
