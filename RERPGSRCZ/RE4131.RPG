     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Teller Sign Off')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Retail Module                                    *
     F*                                                               *
     F*  RE4131 - Teller Sign Off                                     *
     F*                                                               *
     F*  Function:  This program allows a teller to sign off one      *
     F*  (I/C)      terminal in order to move to another during       *
     F*             the day.                                          *
     F*                                                               *
     F*  Called By: Retail Teller System Menu                         *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *  Last Amend No. TDA035             Date  22Mar04              *
      * Midas Release 4 --------------- Base -------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *  Prev Amend No. CRT001  *CREATE    DATE  28Feb95              *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  TDA035 - Automatic Teller ID sign on/off                     *
     F*  CRT001 - Retail Teller System                                *
     F*                                                               *
     F*****************************************************************
     F*  C R E A T I O N   P A R A M E T E R S                        *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
     FTTRNT   UF  E           K        DISK         KINFSR *PSSR
      ** Teller controls file
      *
      ****************************************************************
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*    F U N C T I O N   O F   I N D I C A T O R S                *
     F*                                                               *
     F*  70    - General Indicator Used For CHAIN                     *
     F*                                                               *
     F*  U7+U8 - If Database error occurs                             *
     F*                                                               *
     F*                                                               *
     F*****************************************************************
      /SPACE 3
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  Control routines                                             *
     F*                                                               *
     F*  DBERR  - Database error handling                             *
     F*  MAIN   - Control Process                                     *
     F*  *PSSR  - Program error                                       *
     F*                                                               *
     F*  Validation routines                                          *
     F*                                                               *
     F*  RTTWID - Teller workstation conditions check                 *
     F*                                                               *
     F*  External Routines                                            *
     F*                                                               *
     F*  DBERRCTL Program error                                       *
     F*                                                               *
     F*****************************************************************
      /EJECT
      *
     E                    CPY@    1   1 80
      *
     IPSDS       SDS
      **  Program Status Data Structure
      *
      **  Field containing Program name
     I                                        1  10 @PGM
      **  Field containing Workstation ID
     I                                      244 253 @JOB
      **  Field Containing User ID
     I                                      254 263 @USER
      *
      *
     ILDA        UDS                            256
      **  Local data area for data base errors.
     I                                        1   3 ERTLID
     I                                       11  20 ERTWID
     I                                       21  30 ERWSID
      *
      **  Field combining the following fields
     I                                      134 183 DBLOT
      **  Name of database file in error
     I                                      134 141 DBFILE
      **  Key value causing database error
     I                                      142 170 DBKEY
      **  Name of program causing database error
     I                                      171 180 DBPGM
      **  Number of database error
     I                                      181 1830DBASE
      *
      *
     IRTSDTA     UDS                            256
      **  Job dataarea data structure
     I                                        1   3 @TLID
     I                                        4   6 @TLBC
     I                                        7   7 @SPVI
     I                                        8  32 @OVRI
     I                                    P  33  390@CHTL
     I                                    P  40  460@NCTL
     I                                       47  76 @TLNM
     I                                       77  800@TLMG
     I                                       81  840@TLSL
     I                                       85  890@TLTO
      *
      *
     I            DS                              5
     I**  Data structure for key-list name.
     I                                        1   5 @TTRNT
     I                                        1   1 KTBRI
     I                                        2   4 KTBID
     I                                        5   5 KRCTP
     C/EJECT
      *****************************************************************
      *                                                               *
      *            MAIN   - Control Process                           *
      *                                                               *
      * CALLS      RTTWID - Teller workstation conditions check       *
      *            DBERR  - Database error handling                   *
      *                                                               *
      * CALLED BY         -                                           *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
      *
     C                     MOVEACPY@      BIS@   80
      *                                                                                       TDA035
      ** Entry Parameters                                                                     TDA035
      **   (Web user, Display Sign on Screen, Display Error message)                          TDA035
      *                                                                                       TDA035
     C           *ENTRY    PLIST                                                              TDA035
     C                     PARM           P#WEBU  1                                           TDA035
     C                     PARM           P#TLID  3                                           TDA035
      *                                                                                       TDA035
      ** Check whether user is a web browser user, if so overwrite                            TDA035
      ** Call SFC000026                                                                       TDA035
      *                                                                                       TDA035
     C                     MOVEL'SFC0000' SFCPGM  9                                           TDA035
     C                     MOVE '26'      SFCPGM                                              TDA035
      *                                                                                       TDA035
     C                     CALL SFCPGM                                                        TDA035
     C                     PARM *BLANKS   USER   10                                           TDA035
     C                     MOVELUSER      @USER                                               TDA035
      *
      * Prepare LDA
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL*BLANK    DBLOT
     C                     OUT  LDA
      *
      ** Retrieve data area rtsdta and lock it
     C           *NAMVAR   DEFN           RTSDTA
     C           *LOCK     IN   RTSDTA
      *
      * Move fields to key data structure - Teller-id from data area
     C                     MOVEL'T'       KTBRI
     C                     MOVEL@TLID     KTBID
     C                     MOVEL'1'       KRCTP
      *                                                                                       TDA035
     C                     MOVEL@TLID     P#TLID                                              TDA035
      *
      ** Read teller control details
     C           KLTRNT    CHAINTTRNT                70
      *
      ** If record not found - database error
     C           *IN70     IFEQ '1'
     C           *LOCK     IN   LDA                        ***************
     C                     Z-ADD1         DBASE            ** DB ERR 01 **
     C                     MOVEL'TTRNTM2' DBFILE           ***************
     C                     MOVEL@TTRNT    DBKEY
     C                     EXSR DBERR
     C                     END
      *
      ** If U1 is off - perform teller-wsid-conds-check
      ** *INU1 is off for REC4130                                                             TDA035
      *                                                                                       TDA035
     C           *INU1     IFEQ '0'
     C                     MOVEL@TLID     PTLID   3
     C                     EXSR RTTWID
      *
      ** Record not found.
     C           *IN70     IFEQ '1'
     C           *LOCK     IN   LDA                        ***************
     C                     Z-ADD2         DBASE            ** DB ERR 02 **
     C                     MOVEL'TTRNT'   DBFILE           ***************
     C                     MOVEL@TTRNT    DBKEY
     C                     EXSR DBERR
     C                     END
      *
     C                     END
      *
     C                     MOVE *BLANKS   TWID
     C                     UPDATTTRNTM2F
      *
      ** Clear job data area
     C                     MOVE *BLANKS   RTSDTA
     C                     OUT  RTSDTA
      *
      ** Seton LR and exit
     C                     SETON                     LR
     C                     RETRN
      *****************************************************************
      /EJECT
      *************************************************************
      *                                                           *
      *            RTTWID - Teller workstation conditions check   *
      *                                                           *
      * CALLS             -                                       *
      *                                                           *
      * CALLED BY  MAIN   - Main processing                       *
      *                                                           *
      * FLDS USED  Input                                          *
      *            @TLID  - Teller identifier                     *
      *                                                           *
      *************************************************************
      *
     C           RTTWID    BEGSR                           ** RTTWID **
      *
      ** Define parameters
     C                     MOVEL@TLID     @TLID   3
      *
      ** Define key list
     C           KLTRNT    KLIST
     C                     KFLD           KTBRI   1
     C                     KFLD           KTBID   3
     C                     KFLD           KRCTP   1
      *
      * Move fields to key list.
     C                     MOVEL'T'       KTBRI
     C                     MOVEL@TLID     KTBID
     C                     MOVEL'1'       KRCTP
      *
      ** Get Teller Control Details
     C           KLTRNT    CHAINTTRNT                70
      *
      ** If record found
     C           *IN70     IFEQ '0'
     C           TWID      ANDNE*BLANKS
      *
      ** Check for multiple sign on
     C           TWID      IFNE @JOB
     C           *LOCK     IN   LDA
     C                     MOVEL@TLID     ERTLID
     C                     MOVELTWID      ERTWID
     C                     MOVEL@JOB      ERWSID
     C                     OUT  LDA
     C                     SETON                     U6LR
     C                     RETRN
     C                     END
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            *PSSR  - Program error                             *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR
     C*
     C** Check to see that *PSSR has not been called yet
     C*
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
     C*
     C** Call standard database error program
     C*
     C                     CALL 'DBERRCTL'
     C*
     C                     END
     C*
     C** If *PSSR already run, then just end the program here
     C*
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            DBERR  - Database error handling                   *
      *                                                               *
      * CALLS      *PSSR  - Program  error                            *
      *                                                               *
      * CALLED BY  MAIN   - Control Process                      (01) *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           DBERR     BEGSR                           ** DBERR  **
      *
     C                     MOVEL@PGM      DBPGM
     C                     OUT  LDA
      *
     C                     EXSR *PSSR
      *
     C                     ENDSR
      *****************************************************************
      *
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
