     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Clearing/payroll input')
     F*****************************************************************
     F*                                                               *
     F*  Midas - Retail Module                                        *
     F*                                                               *
     F*  RE4209 - Clearing/Payroll Input                              *
     F*                                                               *
      *  (c) Finastra International Limited 2001                      *
     F*                                                               *
     F*  Function:  This program allows the user to debit/credit      *
     F*  (I/C)      a batch of Retail Accounts with one single        *
     F*             general ledger contra postings for the whole      *
     F*             batch.                                            *
     F*                                                               *
     F*  Called By: Retail Teller System Menu                         *
     F*                                                               *
      *  Last Amend No. CSD103             Date 10Aug20               *
      *  Prev Amend No. MD046248           Date 27Oct17               *
      *                 CGL154             Date 13Oct14               *
      *                 218877             Date 26Aug15               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 BUG27045A          Date 13Apr10               *
      *                 BUG27045           Date 18Feb10               *
      *                 CAP205             Date 16Mar10               *
      *                 CAP204             Date 09Feb10               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      *                 BUG17964           Date 28Apr08               *
      * Midas Plus 1.4 Base ------------------------------------------*
      *                 243871             Date 14Dec06               *
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 CSD031             Date 10Apr06               *
      *                 BUG8514            Date 14Jun06               *
      *                 237720             Date 13Jun06               *
      *                 CRT026             Date 23May06               *
      *                 CSD027             Date 09Dec05               *
      *                 BUG7871            Date 14Jul05               *
      *                 BUG3229            Date 23Aug04               *
      *                 CLE025             Date 20Oct03               *
      *                 CRE019             Date 12Jan04               *
      *                 CGL029             Date 01Sep03               *
      *                 CRT012             Date 26Nov02               *
      * Midas Release 4.01 -------------------------------------------*
     F*                 CRT007             Date 14Jan02               *
      *                 CRT005             Date 27Nov01               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 CRT006             Date 06Aug01               *
      *                 194978             Date 28Jun01               *
     F*                 184205             Date 19Apr01               *
     F*                 174972             Date 17Apr01               *
     F*                 159251             Date 17Apr01               *
      * Midas DBA 3.04 -----------------------------------------------*
     F*                 145752             Date 15Aug00               *
      * Midas DBA 3.03 -----------------------------------------------*
      * Midas DBA 3.01 -----------------------------------------------*
      *                 CGL007             Date 26Mar99               *
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CSD005             Date 02Mar99               *
      *                 CTI002             Date 09Sep98               *
     F*                 140802             Date 17Aug98               *
     F*                 104270             Date 17Sep97               *
     F*                 114621             Date 21Feb97               *
     F*                 CRT002             DATE 01DEC95               *
     F*                 098166             DATE 11JAN96               *
     F*                 CCB002             DATE 30AUG96               *
     F*                 S01408             DATE 06SEP95               *
     F*                 091241             DATE 21JUL95               *
     F*                 CRT001  *CREATE    DATE 28FEB95               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CSD103 - LIBOR Deregulation - Common Layer/Standing Data     *
      *           (Recompile)                                         *
      *  MD046248 - Finastra Rebranding                               *
      *  CGL154 - FATCA Reporting (Recompile)                         *
      *  218877 - Recompiled due to changes in RTCCYSC1               *
      *         - Applied for MD-28376                                *
      *  BUG27045A - Override the fix of Bug 27045 (Recompile)        *
      *  BUG27045 - Amend via SOAP removes Document Linkage           *
      *             (Recompile)                                       *
      *  CAP205 - Teller Related APIs - Account Balance Check         *
      *           (Recompile)                                         *
      *  CAP204 - Teller Related APIs - Account Transfer (Recompile)  *
      *  BUG17964 - Screen looks as if text is overwritten            *
      *             (Recompile)                                       *
      *  243871 - Add confirmation message if transaction was inserted*
      *           successfully.                                       *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  BUG8514 - Issue regarding teller overrides.                  *
      *  237720 - N.S.F. error always shown in Clearing/Payroll input *
      *           though balance is sufficient. Applied fix 217779    *
      *  CRT026 - Retail Teller Password Change                       *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG7871 - Issue regarding Clearing Transaction               *
      *  BUG3229 - Process Cheque Books only, not GIRO.               *
      *  CLE025 - Credit Lines (Recompile)                            *
      *  CRE019 - Cheque Processing and Maintenance (Recompile)       *
      *  CGL029 - Increase Account Code to 10 digits (Recompile)      *
      *  CRT012 - Recompile over changed TTRANPD                      *
      *  CRT007 - Branch Offline Checking                             *
     F*  CRT005 - Recompile due to change in RTTWIDC1                 *
      *  CRT006 - Cashier Reserve Transactions. Recompile.            *
      *  194978 - Recompiled due to changes in RTTWIDC1 and RTODCKC1. *
     F*  184205 - Load ACCY for stopped cheques routine changes       *
     F*  174972 - Recompiled due to changes in /COPY RTCHQRC1.        *
     F*  159251 - Recompiled due to changes in RTCHQRC1               *
      *  145752 - Recompile due to change in RTBALCC1.                *
      *  CGL007 - Account Postings Enquiry.  Recompile.               *
      *  CSD005 - Standing Data Account Narrative.  Add new field     *
      *           to SDACODPD.  Recompilation of programs.            *
      *  CTI002 - Recompiled : File SDACODPD changed                  *
     F*  140802 - Recompiled due to change in /COPY RTCCYSC1          *
     F*  104270 - Recompile due to changes to /COPYs RT*.             *
     F*  114621 - Replace blank with 'X' to mean not to validate      *
     F*           condition.                                          *
     F*  CRT002 - Retail Branch Access.                               *
     F*           Recompile due to change in /COPY RTCCYSC1.          *
     F*  098166 - Correct various errors in profit centre.            *
     F*  CCB002 - COB Performance Enhancements                        *
     F*           Access now via access object                        *
     F*           MEMOS is now accessed via GL/RE key rather than old *
     F*           relative record number.                             *
     F*  S01408 - Addition of hook RE4209IIP1.                        *
     F*           Addition of hook RE4209IIP2.                        *
     F*           Addition of hook RE4209CCP1.                        *
     F*           Addition of hook RE4209CCP2.                        *
     F*           Addition of hook RE4209CCP3.                        *
     F*           Addition of hook RE4209CCP4.                        *
     F*           Addition of hook RE4209CCP5.                        *
     F*           Addition of hook RE4209CCP6.                        *
     F*           Addition of hook RE4209CCP7.                        *
     F*           Addition of hook RE4209CCP8.                        *
     F*           Addition of hook RE4209CCP9.                        *
     F*           Addition of hook RE4209CP10.                        *
     F*           Addition of hook RE4209CP11.                        *
     F*  091241 - Bank Code field extended from 7A to 11A.            *
     F*  CRT001 - Retail Teller System                                *
     F*                                                               *
     F*****************************************************************
      *
     FSTOPC   IF  E           K        DISK
      *
      ** Stopped cheques
      *
     FCHQBKPD UF  E           K        DISK         KINFSR *PSSR      UC
     F                                              KCOMIT
      ** Cheque book details
      *
      *
     FRSACMTPDO   E                    DISK         KINFSR *PSSR
     F                                              KCOMIT
      ** Shadow posting file
     F/COPY WNCPYSRC,RE4209F001
      *
     FTTRANPD O   E                    DISK         KINFSR *PSSR      UC
     F                                              KCOMIT
      ** Teller transaction details
      *
     FTTRANPT UF  E                    DISK         KINFSR *PSSR      UC
     F                                              KCOMIT
      ** Teller Transactions Trailer
      *
     FACNUM   IF  E           K        DISK
      *
      ** Account Numbers Cross Reference File
     F            ACCNTABF                          KRENAMEACNUMABF       145752
      *
     F****MEMOS***UF**E****                DISK         KINFSR *PSSR      CCB002
     F*********************                         KCOMIT                CCB002
      ** Retail Shadow Balances
      *
     FTTRNT   UF  E           K        DISK         KINFSR *PSSR A    UC
     F                                              KCOMIT
      ** Teller Controls/Totals
      *
     FRSACMTL2IF  E           K        DISK
     F            APOSTPDF                          KRENAMEAPOSTPD2
     F            RSACMTPO                          KRENAMERSACMTP2
      ** Shadow posting file
      *
     FRE4209DFCF  E                    WORKSTN
     F                                        @SFRRNKSFILE RE4209S1
      *
      ** Display file - Clearing/Payroll input
      *
      /COPY ZSRSRC,RTCCYSF1                                               CRT002
      *                                                                   CRT002
      *****************************************************************
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*    F U N C T I O N   O F   I N D I C A T O R S                *
     F*                                                               *
     F*  09    -  Clear item subfile                                  *
     F*  10    -  Message Subfile End Indicator (SFLEND)              *
     F*                                                               *
     F*  21    -  Display message array 1                             *
     F*  22    -  Display message array 2                             *
     F*  23    -  Clearing input                                      *
     F*  24    -  Payroll  input                                      *
     F*  26    -  Display override prompt                             *
     F*  27    -  Display underline attributes & non-protect          *
     F*  28    -  Profit Centre used                                  *
     F*                                                               *
     F*  30    -  Error in Retail A/C number                          *
     F*  31    -  Error in Action code                                *
     F*  32    -  Error in Cash amount                                *
     F*  33    -  Error in Currency code                              *
     F*  34    -  Error in GL account code                            *
     F*  35    -  Error in Department code                            *
     F*  36    -  Error in Value Date    e                            *
     F*  37    -  Error in Profit Centre                              *
     F*  38    -  Error in Override teller identifier                 *
     F*  39    -  Error in Override passcode                          *
     F*  40    -  Invalid input                                       *
     F*  41    -  Duplicate cheque error                              *
     F*  42    -  Cheque has been presented                           *
     F*  43    -  Invalid cheque reference                            *
     F*  54    -  Display override user id / password                 *                       CRT026
     F*  56    -  Terminal error(s)                                   *
     F*  57    -  Warning  error(s)                                   *
     F*  58    -  Account found in retail account array               *
     F*  59    -  Work indicator used for TESTB operations            *
     F*                                                               *
     F*  66    -  Exit loop indicator                                 *
     F*  68    -  Unsuccessful override                               *
     F*  69    -  Validation error                                    *
     F*                                                               *
     F*  70    -  Record not found                                    *
     F*  79    -  Error found on CALL command                         *
     F*                                                               *
     F*  80-89 -  Standard RTS subroutines                            *
     F*                                                               *
     F*  90-99 -  Standard MIDAS subroutines                          *
     F*                                                               *
     F*  KA    -  Accept transaction with override                    *
     F*  KC    -  Exit program.                                       *
     F*  KE    -  Refresh screen                                      *
     F*  KL    -  Previous                                            *
     F*                                                               *
     F*  U6    -  System error                                        *
     F*  U7+U8 -  Database error occurs                               *
     F*                                                               *
     F*****************************************************************
      /EJECT
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  Control Subroutines                                          *
     F*                                                               *
     F*  DBERR  - Database error handling                             *
     F*  FIRST  - common  Processing                                  *
     F*  LAST   - Final Processing                                    *
     F*  MAIN   - Main Processing                                     *
     F*  SNDMSG - Send program message parameters                     *
     F*  *PSSR  - Program error                                       *
     F*                                                               *
     F*  Screen Handling                                              *
     F*                                                               *
     F*  WRKITM - Work with items details                             *
     F*  DSPITM - Display transaction details                         *
     F*  EXTITM - Exit items details screen                           *
     F*  CRTITM - Create blank item details screen                    *
     F*  ACCITM - Accept items details                                *
     F*  DSPCOM - Display common  screen                              *
     F*  WRKOVR - Work with override                                  *
     F*  DSPOVR - Display overrride screen                            *
     F*  EXTOVR - Exit override screen                                *
     F*  PRVOVR - Return to previous screen                           *
     F*  ACCOVR - Accept override                                     *
     F*                                                               *
     F*  Screen initialisation / Resets                               *
     F*                                                               *
     F*  RFRITM - Refresh details screen                              *
     F*  RFRCOM - Refresh common screen                               *
     F*  RFROVR - Refresh override screen                             *
     F*                                                               *
     F*  Validation Subroutines                                       *
     F*                                                               *
     F*  VSACNO - Validate retail account number                      *
     F*  VSTNAM - Validate transaction amount                         *
     F*  VSRBCD - Validate bank code                                  *
     F*  VSCQNF - Validate cheque reference                           *
     F*  VALITM - Validate items details                              *
     F*  SETARY - Set up account and cleared balance array            *
     F*  VTCOND - Validate teller transaction conditions              *
     F*  VALCOM - Validate common details                             *
     F*  ACCCOM - Accept common details                               *
     F*  CHKCOM - Common details conditions check                     *
     F*  VALOVR - Validate override                                   *
     F*  VOCOND - Validate override conditions                        *
     F*  VSBTNO - Validate batch number                               *
     F*  VSCCY  - Validate currency code                              *
     F*  VSACTN - Validate action code                                *
     F*  VSBRCH - Validate branch code                                *
     F*  VSTTYP - Validate transaction type                           *
     F*  VSDEPC - Validate department code                            *
     F*  VSVLDT - Validate value date                                 *
     F*                                                               *
     F*  Updates Subroutines                                          *
     F*                                                               *
     F*  UPDATE - Update files                                        *
     F*  INSBAT - Insert new batch processing                         *
     F*  WTNTM2 - Write teller controls                               *
     F*  WTNTM3 - Write teller totals                                 *
     F*  INZPDS - Initialize data structure                           *
     F*  WTTRAN - Add teller transaction members                      *
     F*  UPDBAT - Update batch status                                 *
     F*  WTRAND - Write teller transacton details                     *
     F*  UMEMOS - Update memos                                        *
     F*  WRSACM - Write shadow postings                               *
     F*  UCHQBK - Update cheque book                                  *
     F*  UTRANT - Update teller transaction trailer                   *
     F*  UTNTM2 - Update teller controls                              *
     F*  UTNTM1 - Update teller header                                *
     F*  UTNTM3 - Update teller totals                                *
     F*                                                               *
     F*  Standard RTS Subroutines                                     *
     F*                                                               *
     F*  RTABAL - Account available balance check                     *
     F*  RTACLG - Account closing check                               *
     F*  RTBALC - Calculate available balances                        *
     F*  RTBDPT - Bad Debt check                                      *
     F*  RTBKCR - Block credit check                                  *
     F*  RTBKDR - Block debit check                                   *
     F*  RTBKPT - Bankrupt/Liquidation check                          *
     F*  RTRFCR - Refer credit check                                  *
     F*  RTRFDR - Refer debit check                                   *
     F*  RTBRCH - Branch different check                              *
     F*  RTCCYS - Currency setup                                      *
     F*  RTCHQP - Cheque present check                                *
     F*  RTCHQR - Cheque out of range check                           *
     F*  RTLCNV - Local currency conversion                           *
     F*  RTODCK - Exceed OD check                                     *
     F*  RTSTPC - Stopped cheque check                                *
     F*  RTTCCY - Teller currency conditions check                    *
     F*  RTTLMT - Teller limit check                                  *
     F*  RTTWID - Teller workstation conditions check                 *
     F*  RTWDAY - Non-working day check                               *
     F*  RTIACT - Inactive account check                              *
     F*  RTBVIC - Backvalue check                                     *
     F*  RTTOTS - Teller total updates                                *
     F*  RTENCP - Encript passcode                                    *
     F*                                                               *
     F*  Standard MIDAS Subroutines                                   *
     F*                                                               *
     F*  ZALIGN - Validate amount fields                              *
     F*  ZACCH  - Call holiday access object                          *
     F*  ZDATE1 - Validate dates submitted and convert to a           *
     F*           number of days                                      *
     F*  ZDATE2 - Convert date to dayno. to date formats              *
     F*  ZCHKH  - Determine whether a day is a working day            *
     F*  ZFWDT  - Determine whether a day is a working day            *
     F*           & find the next working day if it is not            *
     F*  ZFRPED - Format and edit amount field                        *
     F*  ZTNLU1 - Access dataarea for next transaction no.            *
     F*                                                               *
     F*  External Subroutines                                         *
     F*                                                               *
     F*  SCC0250 - Clear message queue                                *
     F*  SCC0240 - Send program message queue                         *
     F*                                                               *
     F*  DBERRCTL Program error                                       *
     F*                                                               *
     F*****************************************************************
      /EJECT
      *****************************************************************
      *
     E                    CPY@    1   1 80
      *
     E                    @CN     1   3 80
      ** Constants used by QCMDEXC
      *
      /COPY ZSRSRC,ZDATE2Z1
      ** Array for SR.ZDATE1, SR.ZDATE2
      *
      /COPY ZSRSRC,RTXXXXE1
      ** Arrays for standard RTS subroutines
      *
      /COPY ZSRSRC,ZALIGNZ1
      ** Array for both SR.ZALIGN, SR.ZEDIT
      *
      /COPY ZSRSRC,ZFRPEDZ1
      ** Array for SR.ZFRPED
      *
      /COPY ZSRSRC,ZHOLE
      ** Array for SR.ZACCH, SR.ZCHKH
      *
     E                    @CY        24  3
      ** Teller currencies array
      *
     E                    @CA        20  1
      ** Cheque presented status array
      *
     E                    @MS         9  8
      ** Message array
      *
     E/COPY WNCPYSRC,RE4209E001
     E                    @RA         6 10  D
      ** Retail accounts array
      *
     E                    @CB         6 15 0
      ** Cleared balance array
      *
     E                    @TA         6 15 0
      ** Total transaction amount
      *
     E                    @RN         6  5 0
      ** Relative record number
     E                    @RNO        6 10
      ** Retail a/c number
     E                    @CNO        6  8
      ** Cheque number
     E/COPY WNCPYSRC,RE4209E002
      *
      /COPY ZSRSRC,RTCCYSE1
      ** Array for SR.RTCCYS
      *
      /COPY ZSRSRC,RTENCPE1
      ** Array for SR.RTENCP
      *
      *
      /COPY ZSRSRC,RTCHQPE1
      ** Array for SR.RTCHQP
      *
      *****************************************************************
      /EJECT
      *****************************************************************
     ISTOPCSDF
      ** Renamed fields in PF/STOPCSD
     I              ACNO                            SCACNO
      *
     ITTRANPTF
      ** Renamed fields in PF/TTRANPT
     I              RECI                            TTRECI
     I              TBRI                            TTTBRI
     I              TBID                            TTTBID
     I              TBTN                            TTTBTN
     I              NORE                            TTNORE
     I              FNCD                            TTFNCD
      *                                                                   145752
     IACNUMABF                                                            145752
      ** Renamed fields in LF/ACNUM                                       145752
     I              CCY                             ACCY                  145752
      *
     ITTRNTM1F
      ** Renamed fields in PF/TTRNTM1
     I              NORE                            T1NORE
      *
     ITTRNTM2F
      ** Renamed fields in PF/TTRNTM2
     I              RECI                            T2RECI
     I              NATN                            T2NATN
     I              TBIDI                           T2TBID
      *
     ITTRNTM3F
      ** Renamed fields in PF/TTRNTM3
     I              RECI                            T3RECI
      *
     ICHQBKPDF
      ** Renamed fields in PF/CHQBKPD
     I              ACNO                            CQACNO
     I/COPY WNCPYSRC,RE4209I001
      *
     IRTSDTA     UDS                            256
      ** RTS job data area data structure
     I                                        1   3 PTLID
     I                                        4   6 PTLBC
     I                                        7   7 PSPVI
     I                                        8  32 POVIN
     I                                    P  33  390PCHTL
     I                                    P  40  460PNCTL
     I                                       90  92 PDEPC
      *
     ILDA         DS                            256
      **  Local data area for data base errors.
     I                                        1   3 ERTLID
     I                                       11  20 ERTWID
     I                                       21  30 ERWSID
      **  Field combining the following fields
     I                                      134 183 DBLOT
      **  Name of database file in error
     I                                      134 141 DBFILE
      **  Key value causing database error
     I                                      142 170 DBKEY
      **  Name of program causing database error
     I                                      172 180 DBPGM
      **  Number of database error
     I                                      181 1830DBASE
     I*
     IZMUSER      DS                             17
     I** Data area of menu user
     I                                        1  10 USRPRF
      *
      /COPY ZSRSRC,RTXXXXI1
      ** Data structure to positions of validation indicators
      *
      /COPY ZSRSRC,RTXXXXI2
      ** Data structure for 15 character account number
      *
     I            DS                             72
      ** Data structure to initialise teller currencies array
     I                                        1  72 CCYR1
     I                                        1  72 @CY
      *
     I            DS                             72
      ** Data structure to initialise message array
     I                                        1  72 SMSG2
     I                                        1  72 @MS
      *
     I            DS                             80
      ** Data structure hold format of QCMDEXC
     I                                        1  80 @@CMD
     I                                       26  29 @@MBR
      *
     I            DS
      ** Data structure to hold key fields of LF/TTRNT
     I                                        1   5 @TTRNT
     I                                        1   1 KTBRI
     I                                        2   4 KTBID
     I                                        5   5 KRCTP
      *
     IPSDS       SDS
      ** Program Status Data Structure
      *
      ** Field containing program ID
     I                                        1  10 @PGM
      *
      ** Field containing workstation ID
     I                                      244 253 @JOB
      *
      ** Field containing user ID
     I                                      254 263 @USER
      *
      /COPY ZSRSRC,ZTNLU1Z1
      ** Array for SR.ZTNLU1
      *
      /COPY ZSRSRC,ZFRPEDZ2
      ** Data structure for SR.ZFRPED
      *
     I*                                                                   S01408
     I/COPY WNCPYSRC,RE4209IIP1                                           S01408
     I*                                                                   S01408
      /COPY ZSRSRC,ZHOLI
      ** Data structure for SR.ZACCH, SR.ZCHKH, SR.ZFWDT
      *
      /COPY ZSRSRC,RTCHQPI1
      ** Array for SR.ZDATE3
      *
      /COPY ZSRSRC,RTTOTSI1
      ** Multiple occurrence data structures for SR.RTTOTS
      ** - for initialising packed fields
      *
     I/COPY WNCPYSRC,RE4209I002
     ICMTTXT      DS                            256
     I/COPY WNCPYSRC,RE4209I003
      *
      **  Comit text
     I                                        1   50@NATN
     I                                        6   7 MDID
     I                                        8  15 PGMN
     I                                       16  18 WSIDX
     I                                       19  240MTIME
     I                                       25  25 SACTNX
     I                                       26  35 USRIDX
      *
      ** Data structure for common details screen
     I                                       51 100 @COMDS
     I                                       51  53 SBTNO
     I                                       54  54 SACTN
     I                                       55  57 SBRCH
     I                                       58  60 SCCY
     I                                       61  65 STTYP
     I                                       66  68 SDEPC
     I                                       69  74 SVLDT
     I                                       75  78 SPRFC
      *
      ** Data structure for items details screen
     I/COPY WNCPYSRC,RE4209I004
     I                                      101 200 @ITMDS
     I/COPY WNCPYSRC,RE4209I005
     I                                      101 110 SACNO
     I                                      111 130 SANAM
     I                                      131 144 STNAM
     I**************************************145 151 SRBCD                 091241
     I**************************************152 159 SCQNF                 091241
     I                                      145 155 SRBCD                 091241
     I                                      156 163 SCQNF                 091241
     I/COPY WNCPYSRC,RE4209I006
      *
      ** Data structure for override screen
     I                                      201 256 @OVRDS
     I                                      201 203 SOVTI
     I                                      204 209 SOVPC
     I                                      210 219 SOVUR                                     CRT026
     I                                      220 229 SOVPW                                     CRT026
     I/COPY WNCPYSRC,RE4209I007
     I*
     ISCSARD    E DSSCSARDPD                                              CRT002
     I** Switchable SARs                                                  CRT002
     I*                                                                   CRT002
     ISDACOD    E DSSDACODPD
      ** Account Code Details
      *
     ISDRETR    E DSSDRETRPD
      ** Transaction Types Details
     I*
     ISDBANK    E DSSDBANKPD
      ** Bank Details
      *
     ISDBRCH    E DSSDBRCHPD
      ** Branch Code Details
      *
     ISDCURR    E DSSDCURRPD
      ** Currency Details
      *
     ISDDEPT    E DSSDDEPTPD
      ** Department Codes Details
      *
     ISDRETL    E DSSDRETLPD
      ** SD Retail Details
     I              QQACCD                          QQACCX                                    CGL029
     I*
     ISDGELR    E DSSDGELRPD
     I** General Ledger Details
     I              BKPRCU                          PFCU
     I              BKPCDU                          PFCD
     I              QQACCD                          QQACCY                                    CGL029
     I*
     ISDPRFM    E DSSDPRFMPD
     I** Profit Centre Default Matrix Retrieval Index
     I*
     ISDPRFC    E DSSDPRFCPD
     I** Profit Centre Details
     I*
     ISDTELD    E DSSDTELDPD
     I** Teller ID Details
     I*
     ISDFNCD    E DSSDFNCDPD
     I** Function Code  Details
     I*
     ISDBNCD    E DSSDBNCDPD
     I** Bank Code  Details
      *
     IMUSER     E DSMUSERDD                                                                   CRT026
     I** External data structure for User Details                                             CRT026
     I** Renamed fields in PF/MUSERDD                                                         CRT026
     I              RECI                            RECIU                                     CRT026
     I              LCD                             LCDU                                      CRT026
      *                                                                                       CRT026
     IDSMEMS    E DSMEMOS                                                 CCB002
     I** External data structure for MEMOS Details                        CCB002
     I** Renamed fields in PF/MEMOS                                       CCB002
     I              CNUM                            MECNUM                CCB002
     I              CCY                             MECCY                 CCB002
     I              ACOD                            MEACOD                CCB002
     I              ACSQ                            MEACSQ                CCB002
     I*                                                                   CCB002
     IDSFDY     E DSDSFDY
      *First DS for Access Programs, short data structure
      *
     IDSSDY     E DSDSSDY
      *Second DS for Access Programs, long data structure
      *
     I*                                                                   S01408
     I/COPY WNCPYSRC,RE4209IIP2                                           S01408
     I*                                                                   S01408
      *****************************************************************
     C/EJECT
      *****************************************************************
      *                                                               *
      *            MAIN   - Main Control Processing                   *
      *                                                               *
      * CALLS      DSPCOM - Display common  screen                    *
      *            FIRST  - common  Processing                        *
      *            LAST   - Final Processing                          *
      *            RFRCOM - Refresh common  screen                    *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
      *
     C                     MOVEACPY@      BIS@   80
      ** Initial processing
     C                     EXSR FIRST
      *
      ** Main processing
      *
      ** Refresh common details screen
     C                     EXSR RFRCOM
      *
      ** Display common details screen
     C                     EXSR DSPCOM
      *
      ** Do while Cmd/3 is not pressed
     C           *INKC     DOWEQ'0'
      *
      ** If Cmd/5 is pressed, refresh common details screen
     C           *INKE     CASEQ'1'       RFRCOM
      *
      ** If Enter key is pressed, accept common details
     C                     CAS            ACCCOM
      *
     C                     END
      *
      ** Redisplay common details screen
     C                     EXSR DSPCOM
      *
     C                     END
      *
      ** When Cmd/3 is pressed, perform last processing
     C                     EXSR LAST
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            DSPCOM - Display common details screen             *
      *                                                               *
      * CALLS     SCC0250 - Clears message queue                      *
      *            RFRERR - Refresh warning/terminal error screen     *
      *                                                               *
      * CALLED BY  MAIN   - Main Control Processing                   *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           DSPCOM    BEGSR                           ** DSPCOM **
      *
      ** Display program message subfile for any messages
     C                     WRITERE4209C0
      *
      ** Display and accept common screen
     C                     EXFMTRE4209F0
      *
      ** Clear program message queue
     C                     CALL 'SCC0250'
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *           SNDMSG  - Send program message parameters           *
      *                                                               *
      * CALLS     SCC0240 - Send message to program queue             *
      *                                                               *
      * CALLED BY                                                     *
      *                                                               *
      * FLDS USED  @MSGID - Message id                                *
      *            @MSGF  - Message file                              *
      *                                                               *
      *****************************************************************
     C           SNDMSG    BEGSR                           ** SNDMSG **
      *
     C                     SETON                     69
      *
     C                     CALL 'SCC0240'
     C                     PARM           @MSGID
     C                     PARM           @MSGF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            ACCCOM - Accept common details                     *
      *                                                               *
      * CALLS      VALCOM - Validate common details                   *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           ACCCOM    BEGSR                           ** ACCCOM **
      *
      *
     C                     EXSR VALCOM
      *
      ** If no validation error(s)?
     C           *IN69     IFEQ '0'
      *
     C                     MOVE PTLID     @TLID
     C                     EXSR RTTWID
      *
      ** If record not found?
     C           *IN80     IFEQ '1'
     C           *LOCK     IN   LDA                        ***************
     C                     Z-ADD1         DBASE            ** DB ERR 01 **
     C                     MOVE 'TTRNT'   DBFILE           ***************
     C                     MOVEL@TTRNT    DBKEY
     C                     EXSR DBERR
     C                     END
      *
      ** Check for account terminal conditions
     C                     EXSR CHKCOM
      *
      ** If no terminal error(s) exist?
     C           @TI       IFEQ 0
      *
      ** If warning error(s) found
     C           @WI       IFGT 0
      *
      ** Move teller override indicators from DTAARA/RTSDTA
      ** to teller override array
     C                     MOVEAPOVIN     @R
      *
      ** Perform override validate process
     C                     EXSR VOCOND
      *
      ** If sign-on teller has authority to override
      ** Do not display override teller prompt
     C           *IN68     IFEQ '0'
     C                     SETOF                     26
     C                     MOVE *OFF      *IN54                                               CRT026
      *                                                                                       CRT026
      ** If CRT026 is switch ON use user id instead of teller id                              CRT026
      *                                                                                       CRT026
     C                     ELSE
     C           CRT026    IFEQ 'Y'                                                           CRT026
     C                     MOVE *ON       *IN54                                               CRT026
     C                     ELSE                                                               CRT026
     C                     SETON                     26
     C                     ENDIF                                                              CRT026
     C                     END
      *
     C                     MOVE 'COMMON'  @OVR    6
     C                     EXSR WRKOVR
      *
     C                     END
      *
      ** If no override required or a successful override occurs  AND
      ** there are no validation error(s) (i.e. no terminal/warning error(s)?
      ** AND  previous key is Not pressed
     C           *IN68     IFEQ '0'
     C           *IN69     ANDEQ'0'
     C           *INKL     ANDEQ'0'
      *
      ** Perform batch status processing depending on action code
      *
     C           SACTN     CASEQ'I'       INSBAT
     C           SACTN     CASEQ'U'       UPDBAT
     C                     END
      *
      ** If no validation error(s)?
     C           *IN69     IFEQ '0'
      *
      ** Work with items details
     C                     EXSR WRKITM
      *
      ** Refresh common details screen
     C                     EXSR RFRCOM
     C                     SETON                     66
     C                     END
      *
     C                     END
      *
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VALCOM - Validate common details                   *
      *                                                               *
      * CALLS                                                         *
      *            VSVLDT - Validate value date                       *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           VALCOM    BEGSR                           ** VALCOM **
      *
      ** Set off Validation error indicator
     C                     SETOF                     699930
     C                     SETOF                     313233
     C                     SETOF                     343536
      *
      ** Validation of value date reinstated at this point.
      ** Instead of pushing SR.VSVLDT down, SR.VSTTYP is placed above
      ** to setup the clearing/payroll indicator (*IN23) which then
      ** allows SR.VSVLDT to function properly.
      *
      ** Validate transaction type
     C                     EXSR VSTTYP
      *
     C                     EXSR VSVLDT
      *
      ** Validate batch number
     C                     EXSR VSBTNO
      *
      ** Validate action code
     C                     EXSR VSACTN
      *
      ** Validate branch code
     C                     EXSR VSBRCH
      *
      ** Validate currency
     C                     EXSR VSCCY
      *
      ** Validate transaction type
      *
      ** Validate department code
     C                     EXSR VSDEPC
      *
      ** Validate profit centre
     C           PFCU      IFEQ 'Y'
     C                     EXSR VSPRFC
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VOCOND - Validate override conditions              *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  VALCOM - Validate common details                   *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           VOCOND    BEGSR                           ** VOCOND **
      *
     C                     SETOF                     68
     C                     Z-ADD1         @I
      *
      ** Do until end of array reached or unsuccessful override found
     C           @I        DOUGT25
      *
      ** If element of override conditions is yes
      ** and teller override indicators array element is no
     C           @O,@I     IFEQ 'Y'
     C           @R,@I     ANDEQ'N'
      *
      ** Return as unsuccessful override
     C                     SETON                     68
     C                     Z-ADD25        @I
     C                     END
      *
      ** Check next element
     C                     ADD  1         @I
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VSBTNO - Validate batch number                     *
      *                                                               *
      * CALLS      SNDMSG - Send program message parameters           *
      *                                                               *
      * CALLED BY  VALCOM - Validate common details                   *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           VSBTNO    BEGSR                           ** VSBTNO **
      *
      ** Batch number entered?
     C           SBTNO     IFNE *BLANKS
     C           SBTNO     ANDNE*ZEROS
      *
     C                     TESTN          SBTNO      59
      *
      ** Is batch number valid?
     C           *IN59     IFEQ '1'
      *
      * Set up key list
     C                     MOVEL'B'       KTBRI
     C                     MOVELSBTNO     KTBID
     C                     MOVEL'1'       KRCTP
      *
      ** Get Teller Control Details
     C           KLTRNT    CHAINTTRNT                70
      *
      ** For inserts?
      ** If batch already exist?
     C           SACTN     IFEQ 'I'
     C           *IN70     ANDEQ'0'
     C                     MOVE 'USR0092' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     30
     C                     END
      *
      ** For updates?
     C           SACTN     IFEQ 'U'
      *
      ** If batch not found?
     C           *IN70     IFEQ '1'
      *
     C                     MOVE 'USR0093' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     30
      *
     C                     ELSE
      *
      ** Is batch active?
     C           ACTV      IFEQ 'Y'
     C                     MOVE 'USR0094' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     30
     C                     END
      *
      ** Is branch code different?
     C           TLBC      IFNE PTLBC
     C                     MOVE 'USR0078' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     30
     C                     END
      *
      ** Are input details different from batch common details?
     C                     MOVELCCYR1     @CCYR   3
     C                     MOVE BRCAI     @BRCAI  3
     C                     MOVE STTYP     @TTYP   50
     C           SBRCH     IFNE @BRCAI
     C           SCCY      ORNE @CCYR
     C           @TTYP     ORNE TRANI
     C           SDEPC     ORNE DEPTI
     C           ZDATE     ORNE VDATI
     C                     MOVE 'USR0098' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     323334
     C                     SETON                     3536
     C                     END
      *
     C                     END
      *
     C                     END
      *
      ** Else batch number is invalid
     C                     ELSE
     C                     MOVE 'USR0091' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     30
     C                     END
      *
      ** Else batch number is invalid
     C                     ELSE
     C                     MOVE 'USR0091' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     30
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VSACTN - Validate action code                      *
      *                                                               *
      * CALLS      SNDMSG - Send program message parameters           *
      *                                                               *
      * CALLED BY  VALCOM - Validate common details                   *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           VSACTN    BEGSR                           ** VSACTN **
      *
      ** If action code is 'I' or 'U'
     C           SACTN     IFNE 'I'
     C           SACTN     ANDNE'U'
     C                     MOVE 'USR0090' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     31
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VSBRCH - Validate branch code                      *
      *                                                               *
      * CALLS      SNDMSG - Send program message parameters           *
      *                                                               *
      * CALLED BY  VALCOM - Validate common details                   *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           VSBRCH    BEGSR                           ** VSBRCH **
      *
      ** Get branch code details
      ** Use access program AOBRCHR0 to retrieve header details
      *
     C**********           CALL 'AOBRCHR0'                                                    CGL029
     C                     CALL 'AOBRCHR1'                                                    CGL029
     C                     PARM '*BLANKS' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           SBRCH
     C********** SDBRCH    PARM SDBRCH    DSFDY                                               CGL029
     C           SDBRCH    PARM SDBRCH    DSSDY                                               CGL029
      *
      ** If record not found?
     C** If error in access program, exit via DBERR subroutine
     C*
     C           @RTCD     IFNE *BLANK
     C                     MOVE 'USR0095' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     32
     C                     END
      *
      ** If update mode and signed on teller branch is not the same as
      ** the teller who originally input the branch?
     C           SACTN     IFEQ 'U'
     C           PTLBC     ANDNETLBC
     C                     MOVE 'USR0118' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     32
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VSCCY  - Validate currency                         *
      *                                                               *
      * CALLS      SNDMSG - Send program message parameters           *
      *                                                               *
      * CALLED BY  VALCOM - Validate common details                   *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           VSCCY     BEGSR                           ** VSCCY  **
      *
      ** If currency code not entered display error
     C           SCCY      IFEQ *BLANKS
     C                     MOVE 'USR0076' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     3380
     C                     ELSE
      *
      ** Access currency details
     C                     MOVE SCCY      @CCY
     C                     EXSR RTCCYS
     C                     MOVEL@CURR     SCCY
     C                     MOVE A6NBDP    @CYDP
      *
      ** If record not found?
     C           @RTCD     IFNE *BLANK
     C                     MOVE 'USR0049' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     33
     C                     ELSE
     C                     Z-ADDTDP,@I    @TCDP
     C                     Z-ADDTDP,@I    @CYDP
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VSTTYP - Validate transaction type                 *
      *                                                               *
      * CALLS      SNDMSG - Send program message parameters           *
      *                                                               *
      * CALLED BY  VALCOM - Validate common details                   *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           VSTTYP    BEGSR                           ** VSTTYP **
      *
      ** If transaction type entered?
     C           STTYP     IFNE *BLANKS
     C*
     C** Access transaction code details
     C*
     C                     CALL 'AORETRR0'
     C                     PARM '*BLANKS' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C           STTYP     PARM STTYP     @RETR   5
     C           SDRETR    PARM SDRETR    DSFDY
      *
      ** If record found?
     C           @RTCD     IFEQ *BLANK
      *
      ** If DR/CR valid?
     C           A1DCIN    IFEQ 'D'
     C           A1DCIN    OREQ 'C'
      *
      ** If Associated GL code exist?
     C                     MOVE A1ASGL    A5ACCD
     C                     CALL 'AOACODR0'
     C                     PARM '*BLANKS' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C**********           PARM A5ACCD    @ACCD   4                                           CGL029
     C********** SDACOD    PARM SDACOD    DSFDY                                               CGL029
     C                     PARM A5ACCD    @ACCD  10                                           CGL029
     C           SDACOD    PARM SDACOD    DSSDY                                               CGL029
      *
     C           @RTCD     IFEQ *BLANK
      *
      ** If Account type is 'G'?
     C           A5ACTY    IFEQ 'G'
      *
      ** Set up transaction type details
     C                     MOVELA1RVIN    @RVIN   1
     C                     MOVELA1DCIN    @DCIN   1
     C                     MOVELA1CIND    @CSIN   1
     C                     MOVELA1CLIN    @CLIN   1
     C                     MOVELA1PASB    @PASS   2
     C                     MOVELA1RTNM    @NRTD  30
      *
      ** If clearing or payroll input?
     C                     SETOF                     2324
     C           @DCIN     IFEQ 'D'
     C                     SETON                     23
     C                     ELSE
     C                     SETON                     24
     C                     END
      *
     C                     ELSE
     C                     MOVE 'USR0107' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     34
     C                     END
      *
     C                     ELSE
     C                     MOVE 'USR0106' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     34
     C                     END
      *
     C                     ELSE
     C                     MOVE 'USR0087' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     34
     C                     END
      *
     C                     ELSE
     C                     MOVE 'USR0086' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     34
     C                     END
      *
     C                     ELSE
     C                     MOVE 'USR0085' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     34
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VSDEPC - Validate department code                  *
      *                                                               *
      * CALLS      SNDMSG - Send program message parameters           *
      *                                                               *
      * CALLED BY  VALCOM - Validate common details                   *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           VSDEPC    BEGSR                           ** VSDEPC **
      *
      *
      ** If department code is not blank
     C           SDEPC     IFNE *BLANK
      *
      ** Get department code details
     C                     CALL 'AODEPTR0'
     C                     PARM '*BLANKS' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           SDEPC
     C           SDDEPT    PARM SDDEPT    DSFDY
      *
      ** If record not found?
     C           @RTCD     IFNE *BLANK
     C                     MOVE 'USR0054' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     35
     C                     END
      *
      ** Department code is required
     C                     ELSE
     C                     MOVE 'USR0055' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     35
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VSVLDT - Validate value date                       *
      *                     Assign defaults if not entered            *
      *                                                               *
      * CALLS      SNDMSG - Send program message parameters           *
      *            ZDATE1 - Validate dates submitted and convert to a *
      *                     number of days                            *
      *            ZDATE2 - Convert date to dayno. to date formats    *
      *            ZFWDT  - Calculate any no. of working days forward *
      *                     in any currency                           *
      *                                                               *
      * CALLED BY  VALCOM - Validate common details                   *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           VSVLDT    BEGSR                           ** VSVLDT **
      *
      ** If value date not entered?  default to run day number
     C           SVLDT     IFEQ *BLANKS
      *
     C                     Z-ADDBJRDNB    @VLDT
     C                     Z-ADD@VLDT     ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVE ZDATE     SVLDT
      *
      ** Else move screen field to validation field
     C                     ELSE
      *
     C                     MOVE SVLDT     ZDATE
     C                     EXSR ZDATE1
      *
     C                     END
      *
      ** If date is valid
     C           *IN99     IFEQ '0'
      *
     C                     Z-ADDZDAYNO    @VLDT   50
      *
      ** Value date must not be later than no. of days forward
     C                     Z-ADDBMNDFV    ZNRDYS
     C                     MOVE SCCY      ZCCY
     C                     Z-ADDBJRDNB    ZDAYNO
     C                     EXSR ZFWDT
     C           @VLDT     IFGT ZDYNBR
     C                     MOVE 'USR0057' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     36
     C                     END
      *
      ** For clearing input, is value date on or before run date?
     C           *IN23     IFEQ '1'
     C           @VLDT     ANDGT@RUND
     C                     MOVE 'USR0088' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     36
     C                     END
      *
     C                     ELSE
      *
     C                     MOVE 'USR0009' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     36
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VSPRFC - Validate profit centre                    *
      *                                                               *
      * CALLS      SNDMSG - Send program message parameters           *
      *                                                               *
      * CALLED BY  VALCOM - Validate common details                   *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           VSPRFC    BEGSR                           ** VSPRFC **
      *
     C** Validate the input Profit Centre if entered
     C           SPRFC     IFNE *BLANK
     C                     MOVE *BLANKS   @KEY
     C                     CALL 'AOPRFCR0'
     C                     PARM *BLANKS   @RTCD   7
     C                     PARM '*KEY    '@OPTN   7
     C                     PARM SPRFC     @KEY    4
     C           SDPRFC    PARM SDPRFC    DSFDY
     C*
     C           @RTCD     IFNE *BLANK
     C                     MOVE 'USR1199' @MSGID
     C                     EXSR SNDMSG
     C                     MOVE '1'       *IN37   1
     C                     ELSE
     C                     MOVE DSPCCD    SPRFC
     C                     END
     C*
     C                     ELSE
     C*
     C           PFCD      IFEQ 'Y'
     C*
     C                     MOVE @CNUM     @CUST   6
     C                     CALL 'AOPRFMR0'
     C                     PARM *BLANKS   @RTC1   1
     C                     PARM *BLANKS   @BOOK   2
     C                     PARM A1RTTY    @TRPT   5
     C                     PARM *BLANKS   @SUTP   2
     C                     PARM PTLBC     @BRAN   3
     C                     PARM SDEPC     @DEPT   3
     C                     PARM USRPRF    @USRID 10
     C                     PARM *BLANKS   @ACOF   2
     C                     PARM           @CUST
     C                     PARM *BLANKS   @PRFC   4
     C*
     C                     SELEC
     C           @RTC1     WHEQ '0'
     C                     MOVE @PRFC     SPRFC
     C                     MOVE '0'       *IN37                           098166
     C           @RTC1     WHEQ '2'
     C           @RTC1     OREQ '1'                                       098166
     C                     MOVE @PRFC     SPRFC
     C                     MOVE 'USR1199' @MSGID
     C                     EXSR SNDMSG
     C                     MOVE '1'       *IN37
     C                     ENDSL
      *                                                                   098166
      **  Else, if default profit centre usage is off and profit centre   098166
      **  is blank, then error.                                           098166
      *                                                                   098166
     C                     ELSE                                           098166
     C                     MOVE 'USR1199' @MSGID                          098166
     C                     MOVE '1'       *IN37                           098166
     C                     EXSR SNDMSG                                    098166
     C*
     C                     END
     C*
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            RFRCOM - Refresh common details screen             *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           RFRCOM    BEGSR                           ** RFRCOM **
      *
      ** Set off Validation error fields and indicators
     C                     SETOF                     213368
     C                     SETOF                     303132
     C                     SETOF                     343536
     C                     SETON                     27
     C                     MOVE '0'       *IN37
     C                     MOVEL*BLANKS   @COMDS
     C                     MOVEA*BLANKS   @W
     C                     MOVEA*BLANKS   @T
     C                     MOVEA*BLANKS   @O                                                 BUG8514
      *
     C                     Z-ADD0         @TI     20
     C                     Z-ADD0         @WI     20
     C                     Z-ADD0         SWI
     C                     Z-ADD0         @WX
     C                     Z-ADD0         @@WI
      *
      ** Display default department code
     C                     MOVELPDEPC     SDEPC
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      /COPY ZSRSRC,RTTWIDC1
      ** teller workstation-id conditions check
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *            CHKCOM - Common details conditions check           *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           CHKCOM    BEGSR                           ** CHKCOM **
      *
      ** Branch different check
      *                                                                   114621
     C***********@V,@DF    IFNE ' '                                       114621
     C           @V,@DF    IFNE 'X'                                       114621
     C                     MOVELSBRCH     @BRCA
     C                     MOVELPTLBC     @TLBC
     C                     EXSR RTBRCH
     C                     END
      *
      ** Non-working day check
      *                                                                   114621
     C***********@V,@NW    IFNE ' '                                       114621
     C           @V,@NW    IFNE 'X'                                       114621
     C                     Z-ADD@VLDT     ZDAYNO
     C                     MOVELSCCY      ZCCY
     C                     EXSR RTWDAY
     C                     END
      *
      ** If terminal error(s) found
     C           @TI       IFGT 0
     C                     MOVE 'USR0096' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     21
     C                     MOVEA@T,1      SMSG1
      *
     C                     ELSE
      *
      ** If warning error(s) found
     C           @WI       IFGT 0
     C                     MOVE 'USR0097' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     21
     C                     MOVEA@W,1      SMSG1
     C                     END
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            INSBAT - Insert new batch processing               *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           INSBAT    BEGSR                           ** INSBAT **
      *
      * Set up key list
     C                     MOVEL'B'       KTBRI
     C                     MOVELSBTNO     KTBID
     C                     MOVEL'1'       KRCTP
      *
      ** Get Teller Control Details
     C           KLTRNT    CHAINTTRNT                70
      *
      ** If batch already exist?
     C           *IN70     IFEQ '0'
     C                     MOVE 'USR0092' @MSGID
     C                     EXSR SNDMSG
     C                     ELSE
      *
      ** Access and update next available transaction number
     C                     EXSR ZTNLU1
      *
      ** Write teller controls
     C                     EXSR WTNTM2
      *
      ** Write teller totals
     C                     EXSR WTNTM3
      *
      ** Update header record
     C                     EXSR UTNTM1
      *
      ** Add teller transaction members
     C                     EXSR WTTRAN
      *
      ** Update last item used
     C                     Z-ADD0         SBITU
      *
     C                     MOVE 'INSERT'  STATUS
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            WTNTM2 - Write teller controls                     *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           WTNTM2    BEGSR                           ** WTNTM2 **
      *
      ** Set up and write record
     C                     MOVE 'D'       T2RECI
     C                     MOVE 'B'       TBRI
     C                     MOVELSBTNO     TBID
     C                     MOVE '1'       RCTP
     C                     Z-ADD1         T2NATN
     C                     Z-ADDBJRDNB    RUND
     C                     MOVE 'Y'       ACTV
     C                     Z-ADD0         BITU
     C                     MOVE *BLANKS   CCYR1
     C                     MOVELSCCY      CCYR1
     C                     MOVELPTLBC     TLBC
     C                     MOVELSBRCH     BRCAI
     C                     MOVELSTTYP     TRANI
     C                     MOVELSDEPC     DEPTI
     C                     MOVELSVLDT     VDATI
     C                     MOVELSPRFC     PRFC
     C                     Z-ADDBJRDNB    LCD
     C                     MOVEL'I'       CHTP
     C                     Z-ADDNATN      TNLU
     C                     MOVE PTLID     T2TBID
      *
     C                     WRITETTRNTM2F
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            WTNTM3 - Write teller totals                       *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           WTNTM3    BEGSR                           ** WTNTM3 **
      *
      ** Initialise packed data structure
     C                     EXSR INZPDS
      *
      ** Set up and write record
     C                     MOVE 'D'       T3RECI
     C                     MOVE 'B'       TBRI
     C                     MOVELSBTNO     TBID
     C                     MOVE '2'       RCTP
     C                     Z-ADD0         TTLU
     C                     Z-ADDBJRDNB    LCD
     C                     MOVEL'I'       CHTP
     C                     Z-ADDNATN      TNLU
      *
     C                     WRITETTRNTM3F
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            INZPDS - Initialize data structure                 *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY                                                     *
      *                                                               *
      *****************************************************************
     C           INZPDS    BEGSR                           ** INZPDS **
      *
     C                     Z-ADD0         @NC1
     C                     Z-ADD0         @NC2
     C                     Z-ADD0         @NC3
     C                     Z-ADD0         @NC4
     C                     Z-ADD0         @NC5
     C                     Z-ADD0         @NC6
     C                     Z-ADD0         @NC7
     C                     Z-ADD0         @NC8
     C                     Z-ADD0         @NC9
     C                     Z-ADD0         @NC10
     C                     Z-ADD0         @NC11
     C                     Z-ADD0         @NC12
     C                     Z-ADD0         @NC13
     C                     Z-ADD0         @NC14
     C                     Z-ADD0         @NC15
     C                     Z-ADD0         @NC16
     C                     Z-ADD0         @NC17
     C                     Z-ADD0         @NC18
     C                     Z-ADD0         @NC19
     C                     Z-ADD0         @NC20
     C                     Z-ADD0         @NC21
     C                     Z-ADD0         @NC22
     C                     Z-ADD0         @NC23
     C                     Z-ADD0         @NC24
      *
     C                     Z-ADD0         @VC1
     C                     Z-ADD0         @VC2
     C                     Z-ADD0         @VC3
     C                     Z-ADD0         @VC4
     C                     Z-ADD0         @VC5
     C                     Z-ADD0         @VC6
     C                     Z-ADD0         @VC7
     C                     Z-ADD0         @VC8
     C                     Z-ADD0         @VC9
     C                     Z-ADD0         @VC10
     C                     Z-ADD0         @VC11
     C                     Z-ADD0         @VC12
     C                     Z-ADD0         @VC13
     C                     Z-ADD0         @VC14
     C                     Z-ADD0         @VC15
     C                     Z-ADD0         @VC16
     C                     Z-ADD0         @VC17
     C                     Z-ADD0         @VC18
     C                     Z-ADD0         @VC19
     C                     Z-ADD0         @VC20
     C                     Z-ADD0         @VC21
     C                     Z-ADD0         @VC22
     C                     Z-ADD0         @VC23
     C                     Z-ADD0         @VC24
      *
     C                     Z-ADD0         @ND1
     C                     Z-ADD0         @ND2
     C                     Z-ADD0         @ND3
     C                     Z-ADD0         @ND4
     C                     Z-ADD0         @ND5
     C                     Z-ADD0         @ND6
     C                     Z-ADD0         @ND7
     C                     Z-ADD0         @ND8
     C                     Z-ADD0         @ND9
     C                     Z-ADD0         @ND10
     C                     Z-ADD0         @ND11
     C                     Z-ADD0         @ND12
     C                     Z-ADD0         @ND13
     C                     Z-ADD0         @ND14
     C                     Z-ADD0         @ND15
     C                     Z-ADD0         @ND16
     C                     Z-ADD0         @ND17
     C                     Z-ADD0         @ND18
     C                     Z-ADD0         @ND19
     C                     Z-ADD0         @ND20
     C                     Z-ADD0         @ND21
     C                     Z-ADD0         @ND22
     C                     Z-ADD0         @ND23
     C                     Z-ADD0         @ND24
      *
     C                     Z-ADD0         @VD1
     C                     Z-ADD0         @VD2
     C                     Z-ADD0         @VD3
     C                     Z-ADD0         @VD4
     C                     Z-ADD0         @VD5
     C                     Z-ADD0         @VD6
     C                     Z-ADD0         @VD7
     C                     Z-ADD0         @VD8
     C                     Z-ADD0         @VD9
     C                     Z-ADD0         @VD10
     C                     Z-ADD0         @VD11
     C                     Z-ADD0         @VD12
     C                     Z-ADD0         @VD13
     C                     Z-ADD0         @VD14
     C                     Z-ADD0         @VD15
     C                     Z-ADD0         @VD16
     C                     Z-ADD0         @VD17
     C                     Z-ADD0         @VD18
     C                     Z-ADD0         @VD19
     C                     Z-ADD0         @VD20
     C                     Z-ADD0         @VD21
     C                     Z-ADD0         @VD22
     C                     Z-ADD0         @VD23
     C                     Z-ADD0         @VD24
      *
     C                     Z-ADD0         @VS1
     C                     Z-ADD0         @VS2
     C                     Z-ADD0         @VS3
     C                     Z-ADD0         @VS4
     C                     Z-ADD0         @VS5
     C                     Z-ADD0         @VS6
     C                     Z-ADD0         @VS7
     C                     Z-ADD0         @VS8
     C                     Z-ADD0         @VS9
     C                     Z-ADD0         @VS10
     C                     Z-ADD0         @VS11
     C                     Z-ADD0         @VS12
     C                     Z-ADD0         @VS13
     C                     Z-ADD0         @VS14
     C                     Z-ADD0         @VS15
     C                     Z-ADD0         @VS16
     C                     Z-ADD0         @VS17
     C                     Z-ADD0         @VS18
     C                     Z-ADD0         @VS19
     C                     Z-ADD0         @VS20
     C                     Z-ADD0         @VS21
     C                     Z-ADD0         @VS22
     C                     Z-ADD0         @VS23
     C                     Z-ADD0         @VS24
      *
     C                     Z-ADD0         @VQ1
     C                     Z-ADD0         @VQ2
     C                     Z-ADD0         @VQ3
     C                     Z-ADD0         @VQ4
     C                     Z-ADD0         @VQ5
     C                     Z-ADD0         @VQ6
     C                     Z-ADD0         @VQ7
     C                     Z-ADD0         @VQ8
     C                     Z-ADD0         @VQ9
     C                     Z-ADD0         @VQ10
     C                     Z-ADD0         @VQ11
     C                     Z-ADD0         @VQ12
     C                     Z-ADD0         @VQ13
     C                     Z-ADD0         @VQ14
     C                     Z-ADD0         @VQ15
     C                     Z-ADD0         @VQ16
     C                     Z-ADD0         @VQ17
     C                     Z-ADD0         @VQ18
     C                     Z-ADD0         @VQ19
     C                     Z-ADD0         @VQ20
     C                     Z-ADD0         @VQ21
     C                     Z-ADD0         @VQ22
     C                     Z-ADD0         @VQ23
     C                     Z-ADD0         @VQ24
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            WTTRAN - Add teller transaction members            *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           WTTRAN    BEGSR                           ** WTTRAN **
      *
      ** Add file member to teller transaction files
     C                     MOVEL'B'       PTBRI   1
     C                     MOVE *BLANKS   RETCD   2
     C                     CALL 'REC4206'              79
     C                     PARM           PTBRI
     C                     PARM           SBTNO
     C                     PARM           RETCD
      *
      ** If no error(s)? override PF/TTRANPD
     C           *IN79     IFEQ '0'
     C                     MOVEL@CN,1     @@CMD
     C                     MOVELPTBRI     @@MBR
     C                     MOVE SBTNO     @@MBR
     C                     CALL 'QCMDEXC'              79
     C                     PARM           @@CMD
     C                     PARM 80        @@LEN  155
     C                     END
      *
      ** If no error(s)? override PF/TTRANPT
     C           *IN79     IFEQ '0'
     C                     MOVEL@CN,2     @@CMD
     C                     MOVELPTBRI     @@MBR
     C                     MOVE SBTNO     @@MBR
     C                     CALL 'QCMDEXC'              79
     C                     PARM           @@CMD
     C                     PARM 80        @@LEN  155
     C                     END
      *
      ** Initialise Batch transaction no. for PF/TTRANPD
     C                     Z-ADD1         TBTN
      *
      ** If any error in the CALL?  set error return code to '01'
     C           *IN79     IFEQ '1'
     C           RETCD     OREQ '01'
     C                     ROLBK
     C                     EXSR PRVOVR
     C                     MOVE 'USR0099' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            UPDBAT - Update batch status                       *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           UPDBAT    BEGSR                           ** UPDBAT **
      *
      ** Set up key list
     C                     MOVEL'B'       KTBRI
     C                     MOVELSBTNO     KTBID
     C                     MOVEL'1'       KRCTP
      *
      ** Access Teller Control Details
     C           KLTRNT    CHAINTTRNT                70
      *
      ** If record not found?
     C           *IN70     IFEQ '1'
     C           *LOCK     IN   LDA                        ***************
     C                     Z-ADD2         DBASE            ** DB ERR 02 **
     C                     MOVE 'TTRNT   'DBFILE           ***************
     C                     MOVEL@TTRNT    DBKEY
     C                     EXSR DBERR
     C                     END
      *
      ** Initialise Batch transaction no. for PF/TTRANPD
     C                     Z-ADDT2NATN    TBTN
      *
      ** Set batch as active
     C                     MOVE 'Y'       ACTV
     C                     UPDATTTRNTM2F
      *
      ** Obtain batch item no. last used
     C                     Z-ADDBITU      SBITU
      *
      ** Override PF/TTRANPD
     C                     MOVEL'B'       PTBRI
     C                     MOVEL@CN,1     @@CMD
     C                     MOVELPTBRI     @@MBR
     C                     MOVE SBTNO     @@MBR
     C                     CALL 'QCMDEXC'              79
     C                     PARM           @@CMD
     C                     PARM 80        @@LEN  155
      *
      ** If no error(s)? override PF/TTRANPT
     C           *IN79     IFEQ '0'
     C                     MOVEL@CN,2     @@CMD
     C                     MOVELPTBRI     @@MBR
     C                     MOVE SBTNO     @@MBR
     C                     CALL 'QCMDEXC'              79
     C                     PARM           @@CMD
     C                     PARM 80        @@LEN  155
     C                     END
      *
      ** If any error in the CALL?
     C           *IN79     IFEQ '1'
     C                     ROLBK
     C                     EXSR PRVOVR
     C                     MOVE 'USR0099' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
     C                     MOVE 'UPDATE'  STATUS
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      /COPY ZSRSRC,RTBRCHC1
      ** Branch different check
      *
      /EJECT
      /COPY ZSRSRC,RTWDAYC1
      ** Non-working day check
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *            WRKOVR - Work with override                        *
      *                                                               *
      * CALLS      DSPOVR - Display overrride screen                  *
      *            EXTOVR - Exit override screen                      *
      *            LAST   - Final Processing                          *
      *            RFROVR - Refresh override screen                   *
      *            WRKTRN - Work with transaction                     *
      *                                                               *
      * CALLED BY  VALDET - Validate details                          *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           WRKOVR    BEGSR                           ** WRKOVR **
      *
      ** Set off underline attributes
     C                     SETOF                     27
      *
      ** Refresh override screen
     C                     EXSR RFROVR
      *
      ** Initialise loop exit indicator
     C                     SETOF                     66
      *
      ** Do while no exit requested in override loop
     C           *IN66     DOWEQ'0'
      *
      ** Display override screen
     C                     EXSR DSPOVR
      *
      ** If Cmd/1  is pressed, accept override
     C           *INKA     CASEQ'1'       ACCOVR
      *
      ** If Cmd/3  is pressed, exit override
     C           *INKC     CASEQ'1'       EXTOVR
      *
      ** If Cmd/12 is pressed, exit to previous screen
     C           *INKL     CASEQ'1'       PRVOVR
      *
     C                     END
      *
     C                     END
      *
      ** Reset underline attributes
     C                     SETON                     27
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            DSPOVR - Display overrride screen                  *
      *                                                               *
      * CALLS     SCC0250 - Clears message queue                      *
      *                                                               *
      * CALLED BY                                                     *
      *                                                               *
      *****************************************************************
     C           DSPOVR    BEGSR                           ** DSPOVR **
      *
      ** If loop processing to continue?
     C           *IN66     IFEQ '0'
      *
      ** Display program message subfile for any messages
     C                     WRITERE4209C0
      *
      ** If override taken from common or items details screen?
     C           @OVR      IFEQ 'COMMON'
     C                     WRITERE4209F0
     C                     ELSE
     C                     WRITERE4209C1
     C                     END
      *
      ** Display and accept override screen
     C                     EXFMTRE4209F1
      *
      ** Clear program message queue
     C                     CALL 'SCC0250'
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            EXTOVR - Exit override                             *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY                                                     *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           EXTOVR    BEGSR                           ** EXTOVR **
      *
      ** Determine exit taken from common or items details screen
     C           @OVR      IFEQ 'COMMON'
     C                     EXSR LAST
     C                     END
      *
     C           @OVR      IFEQ 'ITEMS '
     C                     SETON                     66
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            PRVOVR - Return to previous screen                 *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY                                                     *
      *                                                               *
      *****************************************************************
     C           PRVOVR    BEGSR                           ** PRVOVR **
      *
      ** If previous key taken from common details screen?
      ** Clear terminal/warning error details
      ** Set previous screen indicator off to return to common details
     C           @OVR      IFEQ 'COMMON'
     C                     SETOF                     21
     C                     MOVE *BLANKS   SMSG1
     C                     Z-ADD0         @TI     20
     C                     Z-ADD0         @WI     20
     C                     MOVEA*BLANKS   @W
     C                     MOVEA*BLANKS   @T
     C                     MOVEA*BLANKS   @O                                                 BUG8514
     C                     END
      *
      ** If previous key taken from items details screen?
      ** Set unsuccessful override ind on to avoid file updates
     C           @OVR      IFEQ 'ITEMS '
     C                     SETON                     68
     C*
     C** Clear Cheque Number Duplicate array
     C                     MOVEA*ZEROS    @CQN
     C                     Z-ADD0         Z       10
     C                     END
      *
     C                     SETON                     66
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            RFROVR - Refresh override screen                   *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      *****************************************************************
     C           RFROVR    BEGSR                           ** RFROVR **
      *
     C                     SETOF                     3839
     C                     MOVE *BLANKS   @OVRDS
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            ACCOVR - Accept override                           *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           ACCOVR    BEGSR                           ** ACCOVR **
      *
     C                     SETOF                       6869
      *
      ** If override teller id is prompted? validate teller override
     C           *IN26     IFEQ '1'
     C           *IN54     OREQ *ON                                                           CRT026
      *
     C                     EXSR VALOVR
      *
      ** If no validation error(s) exist?
     C           *IN69     IFEQ '0'
      *
      ** Move teller override indicators from PF/TELIDPD
      ** to teller override array
     C                     MOVEAFROVRI    @R
      **
      **    O V E R R I D E   F R O M   I T E M S
      **
     C           @OVR      IFEQ 'ITEMS '
      *
      ** Do for each subfile record within subfile page
      ** Perform override validate process
      *
     C                     SETOF                     58
     C                     Z-ADD@FMSFR    @SFI
     C           *IN58     DOUEQ'1'
      *
     C           @SFI      CHAINRE4209S1             70
      *
      ** If empty record found? increment index & search next record
      ** If subfile record is not empty?  do the following
     C           @ITMDS    IFNE *BLANKS
      *
      ** Set up override conditions array
      *
      ** Validate override conditions
     C                     EXSR VOCOND
      *
      ** If override failed?  exit do loop
     C           *IN68     IFEQ '1'
     C                     SETON                     58
     C                     END
      *
     C                     END
      *
     C                     ADD  1         @SFI
      *
      ** If all records within subfile page searched?  exit loop
     C           @SFI      IFGT @FMSFR
     C                     SETON                     58
     C                     END
      *
     C                     END
      *
     C                     END
      **
      **    O V E R R I D E   F R O M   C O M M O N
      **
     C           @OVR      IFEQ 'COMMON'
     C                     EXSR VOCOND
     C                     END
      *
      ** If successful override?  Set on Previous screen indicator
      ** to return to update handling
     C           *IN68     IFEQ '0'
     C                     SETON                     66
     C                     ELSE
     C                     MOVE 'USR0029' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     69
     C                     END
      *
     C                     END
      *
     C                     ELSE
     C                     SETON                     66
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VALOVR - Validate override                         *
      *                                                               *
      * CALLS      SNDMSG - Send program message parameters           *
      *            RTENCP - Encript passcode                          *
      *                                                               *
      * CALLED BY  WRKTRN - Work with transaction                     *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           VALOVR    BEGSR                           ** VALOVR **
      *
      ** Set off validation error indicator
     C                     SETOF                     69
     C                     MOVE *BLANKS   @OVTI   3
      *
      ** If override teller identifier is not blank
      ** If CRT026 is switch ON check user id if not blank                                    CRT026
      *                                                                                       CRT026
     C           SOVTI     IFNE *BLANK
     C           SOVUR     ORNE *BLANK                                                        CRT026
      *                                                                                       CRT026
      ** Access user id details to obtain the teller id attached                              CRT026
      ** to the user id                                                                       CRT026
      *                                                                                       CRT026
     C           CRT026    IFEQ 'Y'                                                           CRT026
     C                     CALL 'AOUSERR0'                                                    CRT026
     C                     PARM *BLANKS   PRTCD                                               CRT026
     C                     PARM '*KEY'    POPTN                                               CRT026
     C                     PARM SOVUR     @USRP  10                                           CRT026
     C           MUSER     PARM MUSER     DSSDY                                               CRT026
      *                                                                                       CRT026
     C           PRTCD     IFNE *BLANKS                                                       CRT026
     C           PRTCD     ANDNE'*NRF   '                                                     CRT026
     C           *LOCK     IN   LDA                                                           CRT026
     C                     MOVEL'MUSERDD' DBFILE           ***************                    CRT026
     C                     Z-ADD39        DBASE            ** DB ERR 39 **                    CRT026
     C                     MOVELSOVUR     DBKEY            ***************                    CRT026
     C                     EXSR DBERR                                                         CRT026
     C                     ENDIF                                                              CRT026
      *                                                                                       CRT026
     C           PRTCD     IFNE *BLANKS                                                       CRT026
     C                     MOVE *ON       *IN38                                               CRT026
     C                     MOVEL'USR1501' @MSGID                                              CRT026
     C                     EXSR SNDMSG                                                        CRT026
      ** If user id is valid use the teller attached to this user                             CRT026
      ** to obtain the teller details                                                         CRT026
     C                     ELSE                                                               CRT026
     C                     MOVE TLID      @TLID                                               CRT026
     C                     ENDIF                                                              CRT026
      ** If CRT026 enhancement is switch OFF use the teller id                                CRT026
      ** entered to obtain the teller details                                                 CRT026
     C                     ELSE                                                               CRT026
     C                     MOVE SOVTI     @TLID                                               CRT026
     C                     ENDIF                                                              CRT026
      *                                                                                       CRT026
      ** If no validation errors exist                                                        CRT026
      *
      ** Get teller identifier details
     C           *IN69     IFEQ *OFF                                                          CRT026
     C                     CALL 'AOTELDR0'
     C                     PARM '*BLANKS' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C**********           PARM           SOVTI                                               CRT026
     C                     PARM           @TLID   3                                           CRT026
     C           SDTELD    PARM SDTELD    DSFDY
      *
      ** If record found?
     C           @RTCD     IFEQ *BLANK
      *
      ***If*record*is live?
     C**********           MOVE SOVTI     @OVTI                                               CRT026
     C                     MOVE @TLID     @OVTI                                               CRT026
      *
      ** Teller Identifier not found
     C                     ELSE
     C           CRT026    IFEQ 'Y'                                                           CRT026
     C                     MOVEL'USR1509' @MSGID                                              CRT026
     C                     ELSE                                                               CRT026
     C                     MOVE 'USR0030' @MSGID
     C                     ENDIF                                                              CRT026
     C                     EXSR SNDMSG
     C                     SETON                     38
     C                     END
     C                     ENDIF                                                              CRT026
      *
      ** Teller identifier is required
      ** If CRT026 is switch ON User identifier is required                                   CRT026
      *                                                                                       CRT026
     C                     ELSE
     C           CRT026    IFEQ 'Y'                                                           CRT026
     C                     MOVE 'USR1500' @MSGID                                              CRT026
     C                     ELSE                                                               CRT026
     C                     MOVE 'USR0032' @MSGID
     C                     ENDIF                                                              CRT026
     C                     EXSR SNDMSG
     C                     SETON                     38
     C                     END
      *
      ** If no validation error(s) exist?
     C           *IN69     IFEQ '0'
      *
      ** If teller passcode is not blank?
      ** If CRT026 is switch ON check user password if not blank                              CRT026
      *                                                                                       CRT026
     C           SOVPC     IFNE *BLANK
     C           SOVPW     ORNE *BLANK                                                        CRT026
      ** If CRT026 is switch ON check the password via REC4170                                CRT026
     C           CRT026    IFEQ 'Y'                                                           CRT026
     C                     CALL 'REC4170'                                                     CRT026
     C                     PARM           SOVUR                                               CRT026
     C                     PARM           SOVPW                                               CRT026
     C                     PARM *BLANKS   @RTCD   7                                           CRT026
      *                                                                                       CRT026
     C           @RTCD     IFNE *BLANKS                                                       CRT026
     C                     SELEC                                                              CRT026
     C           @RTCD     WHEQ 'CPF2204'                                                     CRT026
     C                     MOVE 'USR1501' @MSGID                                              CRT026
     C                     MOVE *ON       *IN38                                               CRT026
     C           @RTCD     WHEQ 'CPF22E3'                                                     CRT026
     C                     MOVE 'USR1506' @MSGID                                              CRT026
     C                     MOVE *ON       *IN38                                               CRT026
     C           @RTCD     WHEQ 'CPF2203'                                                     CRT026
     C                     MOVE 'USR1507' @MSGID                                              CRT026
     C                     MOVE *ON       *IN38                                               CRT026
     C           @RTCD     WHEQ 'CPF2213'                                                     CRT026
     C                     MOVE 'USR1508' @MSGID                                              CRT026
     C                     MOVE *ON       *IN38                                               CRT026
     C           @RTCD     WHEQ 'CPF22E5'                                                     CRT026
     C                     MOVE 'USR1505' @MSGID                                              CRT026
     C           @RTCD     WHEQ 'CPF22E2'                                                     CRT026
     C                     MOVE 'USR1503' @MSGID                                              CRT026
     C           @RTCD     WHEQ 'CPF22E4'                                                     CRT026
     C                     MOVE 'USR1504' @MSGID                                              CRT026
     C           @RTCD     WHEQ 'CPF22E6'                                                     CRT026
     C                     MOVE 'USR1510' @MSGID                                              CRT026
     C           @RTCD     WHEQ 'CPF22E9'                                                     CRT026
     C                     MOVE 'USR1511' @MSGID                                              CRT026
     C           @RTCD     WHEQ 'CPF2225'                                                     CRT026
     C                     MOVE 'USR1512' @MSGID                                              CRT026
     C                     ENDSL                                                              CRT026
     C                     MOVE *ON       *IN39                                               CRT026
     C                     EXSR SNDMSG                                                        CRT026
     C                     ENDIF                                                              CRT026
      *                                                                                       CRT026
     C                     ELSE                                                               CRT026
      *
      ** If teller passcode does not match
      *
      ** Encript the passcode
     C                     MOVEASOVPC     BPWD
     C                     Z-ADD6         X       10
     C                     EXSR RTENCP
     C                     MOVEAEPWD      SOVPC
      *
     C           SOVPC     IFNE FRTLPC
     C                     MOVE *BLANKS   SOVPC
     C                     MOVE 'USR0039' @MSGID
     C                     EXSR SNDMSG
     C                     SETON                     39
     C                     END
     C                     ENDIF                                                              CRT026
      *
     C                     ELSE
     C           CRT026    IFEQ 'Y'                                                           CRT026
     C                     MOVE 'USR1502' @MSGID                                              CRT026
     C                     ELSE                                                               CRT026
     C                     MOVE *BLANKS   SOVPC
     C                     MOVE 'USR0033' @MSGID
     C                     ENDIF                                                              CRT026
     C                     EXSR SNDMSG
     C                     SETON                     39
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            WRKITM - Work with items details                   *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           WRKITM    BEGSR                           ** WRKITM **
      *
     C                     Z-ADD0         @SFRRN
     C                     Z-ADD0         SSFRRN
     C*
     C** Clear Cheque Number Duplicate array
     C                     MOVEA*ZEROS    @CQN
     C                     Z-ADD0         Z       10
      *
      ** Stores previous override
     C                     Z-ADD@WI       @@WI    20
     C                     MOVEA@O        @@O    25
      *
      ** End of commitment cycle
     C           CMTTXT    COMIT
      *
      ** Clear items subfile
     C                     SETON                       09
     C                     WRITERE4209C1
     C                     SETOF                       09
      *
      ** Create empty items screen
     C                     EXSR CRTITM
      *
      ** Display items subfile
     C                     EXSR DSPITM
      *
      ** Do while Cmd/3 is not pressed
     C           *INKC     DOWEQ'0'
      *
      ** If Cmd/5 is pressed, refresh items subfile
     C           *INKE     CASEQ'1'       RFRITM
      *
      ** If Enter key is pressed?
     C                     CAS            ACCITM
      *
     C                     END
      *
      ** Redisplay appropriate screen (items subfile)
     C                     EXSR DSPITM
      *
     C                     END
      *
      ** Return to previous screen
     C                     EXSR EXTITM
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            DSPITM - Display items details                     *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           DSPITM    BEGSR                           ** DSPITM **
      *
      ** If Cmd/3 key has not been pressed
     C           *INKC     IFEQ '0'
      *
      ** Display program message subfile for any messages
     C                     WRITERE4209C0
      *
      ** Display instruction screen
     C                     WRITERE4209F2
      *
      ** Display and accept items subfile
     C                     EXFMTRE4209C1
      *
      ** Clear program message queue
     C                     CALL 'SCC0250'
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            RFRITM - Refresh items details screen              *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           RFRITM    BEGSR                           ** RFRITM **
      *
     C                     SETON                     27
     C                     SETON                     40
     C                     SETOF                     22
     C                     SETOF                     414243
     C*
     C** Clear Cheque Number Duplicate array
     C                     MOVEA*ZEROS    @CQN
     C                     Z-ADD0         Z       10
      *
      ** Do for each subfile record within subfile page
     C           @FMSFR    DO   @TOSFR    @SFI    50
      *
     C           @SFI      CHAINRE4209S1             70
      *
     C           *IN70     IFEQ '0'
     C                     MOVEL*BLANKS   @ITMDS
     C                     MOVEL*BLANKS   SMSG2
     C                     MOVEL*BLANKS   SOV
     C                     MOVEL*BLANKS   SMSG2
     C                     Z-ADD0         S@AMT
     C                     Z-ADD0         SRRNM
     C**********           Z-ADD0         S@CNUM                                              CSD027
     C                     MOVE *BLANKS   S@CNUM                                              CSD027
     C                     MOVE *BLANKS   S@CCY
     C                     Z-ADD0         S@ACOD
     C                     Z-ADD0         S@ACSQ
     C                     UPDATRE4209S1
     C      40             SETOF                     40
     C                     END
      *
     C                     END
      *
     C                     Z-ADD0         @NORE
     C*
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            EXTITM - Exit items details screen                 *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      *****************************************************************
     C           EXTITM    BEGSR                           ** EXTITM **
      *
      ** Reset batch status
     C                     Z-ADD1         @IAFLG  10
     C                     EXSR UTNTM2
      *
      ** End of commitment cycle
     C           CMTTXT    COMIT
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            ACCITM - Accept items details                      *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           ACCITM    BEGSR                           ** ACCITM **
     C*
     C** Clear Cheque Number Duplicate array
     C                     MOVEA*ZEROS    @CQN
     C                     Z-ADD0         Z       10
     C*
      *
      ** Validate items details
     C                     EXSR VALITM
      *
      ** If terminal/warning error(s) found?  Set up error message
     C           *IN56     IFEQ '1'
     C                     MOVE 'USR0096' @MSGID
     C                     EXSR SNDMSG
     C                     END
     C           *IN57     IFEQ '1'
     C           *IN69     ANDEQ'0'
     C                     MOVE 'USR0097' @MSGID
     C                     EXSR SNDMSG
     C                     SETOF                       69
     C                     END
      *
      ** If no validation error(s) or no terminal error(s) exist?
     C           *IN69     IFEQ '0'
      *
      ** If warning error(s) found?
     C           *IN57     IFEQ '1'
      *
      ** Move teller override indicators from DTAARA/RTSDTA
      ** to teller override array
     C                     MOVEAPOVIN     @R
      *
      ** Perform override validate process (for all items)
      ** Note:  @O accumulates all override conditions for all items
     C                     EXSR VOCOND
      *
      ** If sign-on teller has authority to override
      ** Do not display override teller prompt
     C           *IN68     IFEQ '0'
     C                     SETOF                     26
     C                     MOVE *OFF      *IN54                                               CRT026
      *                                                                                       CRT026
      ** If CRT026 is switch ON use user id instead of teller id                              CRT026
      *                                                                                       CRT026
     C                     ELSE
     C           CRT026    IFEQ 'Y'                                                           CRT026
     C                     MOVE *ON       *IN54                                               CRT026
     C                     ELSE                                                               CRT026
     C                     SETON                     26
     C                     ENDIF                                                              CRT026
     C                     END
      *
     C                     MOVE 'ITEMS '  @OVR    6
     C                     EXSR WRKOVR
      *
     C                     END
      *
      ** If no override required or a successful override occurs  AND
      ** there are no validation error(s) (i.e. no terminal/warning error(s)?
      ** AND  previous key is Not pressed
     C           *IN68     IFEQ '0'
     C           *IN69     ANDEQ'0'
     C           *INKL     ANDEQ'0'
     C*                                                                   S01408
     C/COPY WNCPYSRC,RE4209CCP1                                           S01408
     C*                                                                   S01408
      *
      ** Update files
     C                     EXSR UPDATE
     C*                                                                   S01408
     C/COPY WNCPYSRC,RE4209CCP2                                           S01408
     C*                                                                   S01408
      *
      ** Create empty items screen
     C                     EXSR CRTITM
     C*                                                                   S01408
     C/COPY WNCPYSRC,RE4209CCP3                                           S01408
     C*                                                                   S01408
      *
     C                     END
      *
     C                     ELSE
      *
     C** Clear Cheque Number Duplicate array
     C                     MOVEA*ZEROS    @CQN
     C                     Z-ADD0         Z       10
     C*
     C                     MOVE 'USR0096' @MSGID
     C                     EXSR SNDMSG
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            CRTITM - Create blank item details screen          *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           CRTITM    BEGSR                           ** CRTITM **
      *
     C                     SETON                     27
     C                     SETON                     40
     C                     SETOF                     22
     C                     SETOF                     414243
      *
     C                     Z-ADDSSFRRN    @SFRRN
     C           @SFRRN    ADD  1         @FMSFR  50
     C           @SFRRN    ADD  @SFPAG    @TOSFR  50
      *
     C           @FMSFR    DO   @TOSFR
     C                     ADD  1         @SFRRN
     C                     ADD  1         SSFRRN
     C                     MOVEL*BLANKS   @ITMDS
     C                     Z-ADD0         SWI
     C                     MOVEL*BLANKS   SOV
     C                     MOVEL*BLANKS   SMSG2
     C                     Z-ADD0         S@AMT
     C                     Z-ADD0         SRRNM
     C**********           Z-ADD0         S@CNUM                                              CSD027
     C                     MOVE *BLANKS   S@CNUM                                              CSD027
     C                     MOVE *BLANKS   S@CCY
     C                     Z-ADD0         S@ACOD
     C                     Z-ADD0         S@ACSQ
     C/COPY WNCPYSRC,RE4209C007
     C                     WRITERE4209S1
     C      40             SETOF                     40
     C                     END
      *
     C                     MOVE *BLANKS   @RA
     C                     MOVE *ZEROS    @CB
     C                     MOVE *ZEROS    @TA
     C                     MOVE *ZEROS    @RN
     C                     Z-ADD0         @NORE
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VALITM - Validate items details                    *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      *****************************************************************
     C           VALITM    BEGSR                           ** VALITM **
      *
     C                     SETOF                     565769
     C                     SETOF                     68
      *
      ** Initialise override conditions for this retail account
     C                     MOVEA@@O       @O
      *
     C                     MOVE *BLANKS   @RA
     C                     MOVE *ZEROS    @CB
     C                     MOVE *ZEROS    @TA
     C                     MOVE *ZEROS    @RN
      *
      ** Do for each subfile record within subfile page
     C           @FMSFR    DO   @TOSFR    @SFI
      *
     C           @SFI      CHAINRE4209S1             70
      *
     C                     SETOF                     22
     C                     SETON                     27
     C                     MOVEL*BLANKS   SMSG2
     C                     Z-ADD0         @I
     C                     Z-ADD@@WI      @WI
     C                     Z-ADD0         @TI
     C                     Z-ADD0         SWI
     C                     MOVEL*BLANKS   SOV
     C                     Z-ADD0         S@AMT
     C                     Z-ADD0         SRRNM
     C**********           Z-ADD0         S@CNUM                                              CSD027
     C                     MOVE *BLANKS   S@CNUM                                              CSD027
     C                     MOVE *BLANKS   S@CCY
     C                     Z-ADD0         S@ACOD
     C                     Z-ADD0         S@ACSQ
     C                     MOVE *BLANKS   S@UCQB
     C                     MOVE *ZEROS    S@BITN
     C                     MOVE *ZEROS    S@I
      *
      ** Initialise screen account name when account no. is cleared
     C           SACNO     IFEQ *BLANKS
     C                     MOVE *BLANKS   SANAM
     C                     END
      *
      ** If subfile line not empty?  validate input
     C           @ITMDS    IFNE *BLANKS
      *
      ** Validate retail account number
     C                     EXSR VSACNO
      *
      ** Validate transaction amount
     C                     EXSR VSTNAM
      *
     C/COPY WNCPYSRC,RE4209C001
      ** Validate bank code
     C                     EXSR VSRBCD
      *
      ** Validate cheque reference
     C                     EXSR VSCQNF
     C/COPY WNCPYSRC,RE4209C002
      *
      ** For each subfile record, if no validation error(s)?
     C           *IN22     IFEQ '0'
      *
      ** Set up account array
     C                     EXSR SETARY
      *
      ** Validate teller transaction conditions
     C                     EXSR VTCOND
      *
      ** Reinstate the item currency
     C                     MOVE SCCY      @CCY
      *
      ** Save warning index
     C                     Z-ADD@WI       SWI
     C                     MOVEA@O        SOV
     C                     Z-ADD@TNAM     S@AMT
     C                     Z-ADD@RRNM     SRRNM
     C**********           Z-ADD@CNUM     S@CNUM                                              CSD027
     C                     MOVE @CNUM     S@CNUM                                              CSD027
     C                     MOVE @CCY      S@CCY
     C                     Z-ADD@ACOD     S@ACOD
     C                     Z-ADD@ACSQ     S@ACSQ
     C                     MOVE @UCQB     S@UCQB
     C                     MOVE @BITN     S@BITN
     C                     MOVE @I        S@I
      *
     C                     END
      *
     C                     END
      *
      ** Update subfile record
     C                     UPDATRE4209S1
      *
      ** Read next changed subfile record
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VSACNO - Validate retail account number            *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      *****************************************************************
     C           VSACNO    BEGSR                           ** VSACNO **
      *
      ** Set off indicators for invalid input
     C                     SETOF                     40
     C                     SETOF                     70
      *
     C                     MOVE *BLANKS   SACN20 10
      *
      ** Set up key list
     C                     MOVE SACNO     @ACNO  100
     C                     MOVEL@ACNO     SACN20
      *
     C           SACNO     IFNE SACN20
     C           @ACNO     ORLT 0
     C                     SETON                     70
     C                     END
      *
      ** Get Account Details
     C           *IN70     IFEQ '0'
     C           @ACNO     CHAINACNUM                70
     C                     END
      *
      ** If record found?
     C           *IN70     IFEQ '0'
      *
      ** If account status is not closed?
     C           ACST      IFNE 'C'
      *
      ** If value date after date account opened?
     C           @VLDT     IFGE DACO
      *
      ** If branch code same as input branch code?
     C                     MOVE BRCA      @BRCH   3
     C           @BRCH     IFEQ SBRCH
      *
      ** If currency code same as input currency code?
     C********** CCY       IFEQ SCCY                                                         BUG7871
     C           ACCY      IFEQ SCCY                                                         BUG7871
      *
     C                     MOVE ANAM      SANAM
     C                     Z-ADDRRNM      @RRNM
     C**********           Z-ADDCNUM      @CNUM                                               CSD027
     C                     MOVE CNUM      @CNUM                                               CSD027
     C                     MOVE SCCY      @CCY
     C                     Z-ADDACOD      @ACOD
     C                     Z-ADDACSQ      @ACSQ
      *
     C                     ELSE
     C                     ADD  1         @I
     C                     MOVEL'CCYDIFF' @MS,@I
     C                     SETON                     224069
     C                     END
      *
     C                     ELSE
     C                     ADD  1         @I
     C                     MOVEL'BF.DIFF' @MS,@I
     C                     SETON                     224069
     C                     END
      *
     C                     ELSE
     C                     ADD  1         @I
     C                     MOVEL'BF.OPEN' @MS,@I
     C                     SETON                     224069
     C                     END
      *
     C                     ELSE
     C                     ADD  1         @I
     C                     MOVEL'CLOSED'  @MS,@I
     C                     SETON                     224069
     C                     END
      *
     C                     ELSE
     C                     ADD  1         @I
     C                     MOVEL'NO.AC'   @MS,@I
     C                     SETON                     224069
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VSTNAM - Validate transaction amount               *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      *****************************************************************
     C           VSTNAM    BEGSR                           ** VSTNAM **
      *
     C                     SETOF                     41
      *
      ** Check if transaction amount field is entered
     C           STNAM     IFNE *BLANKS
      *
      ** Set up decimal values
     C                     Z-ADD@TCDP     ZADEC
     C           13        SUB  @TCDP     ZADIG
      *
      ** Align amount
     C                     MOVEL*BLANK    ZFIELD
     C                     MOVE STNAM     ZFIELD
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    @TNAM  130                      E14168
      *
      ** If error found?  Set up error message
     C           *IN99     IFEQ '1'
     C           ZFIELD    OREQ *ZERO
     C                     ADD  1         @I
     C                     MOVEL'AMT.ERR' @MS,@I
     C                     SETON                     224169
     C                     END
      *
      ** Amount must not be blank or zero
     C                     ELSE
     C                     ADD  1         @I
     C                     MOVEL'NO.AMT'  @MS,@I
     C                     SETON                     224169
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VSRBCD - Validate bank code                        *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      *****************************************************************
     C           VSRBCD    BEGSR                           ** VSRBCD **
      *
     C                     SETOF                     42
      *
      ** If bank code entered?
     C           SRBCD     IFNE *BLANKS
      *
      ** Access bank code details
     C                     CALL 'AOBNCDR0'
     C                     PARM '*BLANKS' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM           SRBCD
     C           SDBNCD    PARM SDBNCD    DSFDY
      *
      ** If record found?  Set up default bank code
     C           @RTCD     IFEQ *BLANK
     C*********************MOVE SRBCD     @@RBCD  7                       091241
     C                     MOVE SRBCD     @@RBCD 11                       091241
     C                     ELSE
     C                     ADD  1         @I
     C                     MOVEL'BK.CODE' @MS,@I
     C                     SETON                     224269
     C                     END
      *
      ** Bank code must not be blank or zero
     C                     ELSE
      *
      ** If default bank code exist?  Set it up
     C           @@RBCD    IFNE *BLANKS
     C                     MOVE @@RBCD    SRBCD
     C                     ELSE
     C                     ADD  1         @I
     C                     MOVEL'NO.BANK' @MS,@I
     C                     SETON                     224269
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VSCQNF - Validate cheque reference                 *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      *****************************************************************
     C           VSCQNF    BEGSR                           ** VSCQNF **
      *
     C                     SETOF                     35
     C                     MOVELSCQNF     @RCHAR  1
      *
      ** If cheque reference entered?                                  '
     C           SCQNF     IFNE *BLANKS
      *
      ** If cheque reference is numeric?
     C                     TESTN          SCQNF      59
     C           *IN59     IFEQ '1'
      *
     C                     MOVE *BLANKS   @@CQNF  8
      *
      ** If numeric?  then debit account sub-type must be 'C'
      ** If debit account sub-type is 'C' set up default Cheque/Ref no.
     C           STYP      IFEQ 'C'
     C                     MOVE SCQNF     @CQNF   80
     C                     ELSE
     C                     ADD  1         @I
     C                     MOVEL'STYP.ER' @MS,@I
     C                     SETON                     224369
     C                     END
      *
      ** Else if first character is not 'R', cheque reference is invalid
     C                     ELSE
      *
     C           @RCHAR    IFNE 'R'
     C                     ADD  1         @I
     C                     MOVEL'CHQ.ERR' @MS,@I
     C                     SETON                     224369
     C                     END
      *
     C                     MOVE SCQNF     @@CQNF  8
      *
     C                     END
      *
      ** Else if cheque reference is not entered?
     C                     ELSE
      *
      ** If default Cheque/Ref no. exist?  Set it up
     C                     MOVEL@@CQNF    @@CHAR  1
     C           @@CHAR    IFEQ 'R'
     C                     MOVE @@CQNF    SCQNF
     C                     ELSE
     C                     ADD  1         @I
     C                     MOVEL'NO.REF ' @MS,@I
     C                     SETON                     224369
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            SETARY - Set up account and cleared balance array  *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      *****************************************************************
     C           SETARY    BEGSR                           ** SETARY **
      *
     C                     SETOF                       7058
     C                     MOVEL*BLANKS   @@ZZ10 10
      *
      ** Check if record exist on accounts array
     C                     Z-ADD1         @Z      10
     C           SACNO     LOKUP@RA,@Z                   58
      *
      ** If record does not exist on accounts array?
      ** Locate an empty element
     C           *IN58     IFEQ '0'
      *
     C           @@ZZ10    LOKUP@RA,@Z                   58
      *
      ** If an empty record exist?
     C           *IN58     IFEQ '1'
      *
      ** Chain & release PF/MEMOS record for cleared balance
      ** and set up account array
     C***********@RRNM*****CHAINMEMOS                70                   CCB002
     C************IN70*****IFEQ '0'                                       CCB002
     C*********************EXCPT@RLMEM                                    CCB002
      *                                                                   CCB002
      ** Access account details                                           CCB002
     C                     MOVE SACNO     SACNON 100                      CCB002
      *                                                                   CCB002
      ** Chain access object AOMEMSR0                                     CCB002
     C                     CALL 'AOMEMSR0'                                CCB002
     C                     PARM *BLANK    W0RTCD  7        I:Return code  CCB002
     C                     PARM '*KEY   ' W0OPTN  7        O:Option       CCB002
     C                     PARM SACNON    W0RETL 100       O:Retail A/C NoCCB002
     C**********           PARM *ZERO     W0CNUM  60       O:Customer No.              CCB002 CSD027
     C                     PARM *BLANKS   W0CNUM  6        O:Customer No.                     CSD027
     C                     PARM *BLANK    W0CCY   3        O:Currency     CCB002
     C                     PARM *ZERO     W0ACDD  40       O:Account Code CCB002
     C                     PARM *ZERO     W0ACSQ  20       O:Account Seq. CCB002
     C                     PARM *BLANK    W0BRCA  3        O:Branch       CCB002
     C           DSMEMS    PARM DSMEMS    DSFDY                           CCB002
      *                                                                   CCB002
      ** Set up account array details with cleared balance                CCB002
     C           W0RTCD    IFEQ *BLANK                                    CCB002
     C                     Z-ADDCLBLN     @CLBL
     C                     Z-ADD@CLBL     @CB,@Z
     C                     MOVELSACNO     @RA,@Z
     C*********************Z-ADD@RRNM     @RN,@Z                          CCB002
     C                     END
      *
     C                     END
      *
     C                     END
      *
      ** Accumulate total transaction amount array
     C           *IN70     IFEQ '0'
      *
     C                     ADD  @TNAM     @TA,@Z
      *
      ** If clearing input or payroll input? set cleared balance array
     C           *IN23     IFEQ '1'
     C           @CB,@Z    ADD  @TNAM     @CLBL
     C                     Z-ADD@CLBL     @CB,@Z
     C                     END
      *
     C           *IN24     IFEQ '1'
     C           @CB,@Z    SUB  @TNAM     @CLBL
     C                     Z-ADD@CLBL     @CB,@Z
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VTCOND - Validate teller transaction conditions    *
      *                                                               *
      * CALLS      RTIACT - Inactive account check                    *
      *            RTBKPT - Bankrupt/Liquidation check                *
      *            RTBDPT - Bad Debt check                            *
      *            RTACLG - Account closing check                     *
      *            RTBVIC - Backvalue check                           *
      *            RTTLMT - Teller limit check                        *
      *            RTBKCR - Block credit check                        *
      *            RTRFCR - Refer credit check                        *
      *            RTBKDR - Block debit check                         *
      *            RTRFDR - Refer debit check                         *
      *            RTSTPC - Stopped cheque check                      *
      *            RTCHQP - Cheque present check                      *
      *            RTABAL - Account available balance check           *
      *            RTODCK - Exceed OD check                           *
      *                                                               *
      * CALLED BY                                                     *
      *                                                               *
      *****************************************************************
     C           VTCOND    BEGSR                           ** VTCOND **
      *
     C                     MOVE RETB      @RETB
     C                     MOVE '  '      @ACIN   2
     C                     MOVE ' '       @UCQB
     C                     Z-ADD0         @WI
      *
     C                     MOVEA*BLANKS   @W
     C                     MOVEA*BLANKS   @T
      *
      ** Inactive account check?
      *                                                                   114621
     C***********@V,@IA    IFNE ' '                                       114621
     C           @V,@IA    IFNE 'X'                                       114621
     C                     EXSR RTIACT
     C                     END
      *
      ** Bankrupt/Liquidation check?
      *                                                                   114621
     C***********@V,@BL    IFNE ' '                                       114621
     C           @V,@BL    IFNE 'X'                                       114621
     C                     EXSR RTBKPT
     C                     END
      *
      ** Bad Debt check?
      *                                                                   114621
     C***********@V,@DB    IFNE ' '                                       114621
     C           @V,@DB    IFNE 'X'                                       114621
     C                     EXSR RTBDPT
     C                     END
      *
      ** Account closing check?
      *                                                                   114621
     C***********@V,@AC    IFNE ' '                                       114621
     C           @V,@AC    IFNE 'X'                                       114621
     C                     MOVELACST      @ACST
     C                     EXSR RTACLG
     C                     END
      *
      ** Backvaluation/ic check?
      *                                                                   114621
     C***********@V,@BV    IFNE ' '                                       114621
     C           @V,@BV    IFNE 'X'                                       114621
     C                     Z-ADDLDID      @LDID
     C                     MOVE DRIF      @DRIF
     C                     Z-ADDLCID      @LCID
     C                     MOVE CRIF      @CRIF
     C                     MOVE '  '      @ACIN
     C                     EXSR RTBVIC
     C                     END
      *
      ** Teller limit check
      *                                                                   114621
     C***********@V,@TL    IFNE ' '                                       114621
     C           @V,@TL    IFNE 'X'                                       114621
     C                     MOVELSCCY      @TCCY
     C                     MOVELBJLCCY    @LCCY
     C                     Z-ADD@TNAM     @TAM1
     C                     Z-ADD0         @TAM2
     C                     MOVEL@CSIN     @TCI1
     C                     MOVEL' '       @TCI2
     C                     Z-ADDPCHTL     @CHTL
     C                     Z-ADDPNCTL     @NCTL
     C                     EXSR RTTLMT
     C                     END
      *
      ** If payroll input?
     C           *IN24     IFEQ '1'
      *
      ** Block credit check?
      *                                                                   114621
     C***********@V,@BC    IFNE ' '                                       114621
     C           @V,@BC    IFNE 'X'                                       114621
     C                     EXSR RTBKCR
     C                     END
      *
      ** Refer credit check?
      *                                                                   114621
     C***********@V,@RC    IFNE ' '                                       114621
     C           @V,@RC    IFNE 'X'                                       114621
     C                     EXSR RTRFCR
     C                     END
      *
     C                     END
      *
      ** If clearing input?
     C           *IN23     IFEQ '1'
      *
      ** Block debit check?
      *                                                                   114621
     C***********@V,@BD    IFNE ' '                                       114621
     C           @V,@BD    IFNE 'X'                                       114621
     C                     EXSR RTBKDR
     C                     END
      *
      ** Refer debit check?
      *                                                                   114621
     C***********@V,@RD    IFNE ' '                                       114621
     C           @V,@RD    IFNE 'X'                                       114621
     C                     EXSR RTRFDR
     C                     END
      *
      * If Retail Account Sub-type is not a Savings account, OR
      * if cheque no. entered (first character of cheque no. is not 'R')
      * do the cheque validations.
     C           STYP      IFNE 'S'
     C           @RCHAR    ORNE 'R'
      *
     C                     OPEN CHQBKPD
      *
      ** Stopped cheque check
     C/COPY WNCPYSRC,RE4209C003
      *                                                                   114621
     C***********@V,@SC    IFNE ' '                                       114621
     C           @V,@SC    IFNE 'X'                                       114621
     C                     MOVEL@CCY      ACCY    3                       184205
     C                     EXSR RTSTPC
     C                     END
      *
      ** Cheque out of range check
      *                                                                   114621
     C***********@V,@OR    IFNE ' '                                       114621
     C           @V,@OR    IFNE 'X'                                       114621
     C                     EXSR RTCHQR
     C                     END
     C/COPY WNCPYSRC,RE4209C004
     C*
     C* If cheque out of range, check account movements file to see if
     C* cheque has already been presented.
     C*
     C***********@V,@CP    IFNE ' '                                       114621
     C           @V,@CP    IFNE 'X'                                       114621
     C           *IN81     ANDEQ'1'
     C           SCQN      ANDNE0
     C           @ACNO     CHAINACNUM                70
      *
     C           *IN70     IFEQ '0'
     C           KLRSAC    CHAINRSACMTL2             70
     C           *IN70     IFEQ '0'
     C*
     C** If override allowed
     C           @V,@CP    IFEQ 'Y'
     C                     ADD  1         @WI
     C                     MOVEL'CHQPRES' @W,@WI
     C                     MOVE 'Y'       @O,@CP
     C                     END
     C*
     C** If override not allowed
     C           @V,@CP    IFEQ 'N'
     C                     ADD  1         @TI
     C                     MOVEL'CHQPRES' @T,@TI
     C                     END
     C*
     C                     END
     C                     END
     C                     END
      *
      ** Cheque present check and if no cheque out of range error?
      *                                                                   114621
     C***********@V,@CP    IFNE ' '                                       114621
     C           @V,@CP    IFNE 'X'                                       114621
     C           *IN81     ANDEQ'0'
     C           SCQN      ANDNE0
     C                     EXSR RTCHQP
     C                     END
      *
     C                     CLOSECHQBKPD
      *
      ** Check warning message not already displayed for this record
     C           @WI       IFGT 0
     C           @W,@WI    ANDEQ'CHQPRES'
     C                     SETON                     42
     C                     END
     C                     END
      *
      ** Calculate no of record on each sub-file page from the sub-file
      ** index (6 records per page) use N as array pointer.
     C           @SFI      SUB  1         TEMP    20
     C/COPY WNCPYSRC,RE4209C008
     C                     DIV  6         TEMP
     C/COPY WNCPYSRC,RE4209C009
     C                     MVR            TSFI    20
     C                     ADD  1         TSFI
     C                     Z-ADDTSFI      N       20
      *
      ** Store Retail a/c number and Cheque number in array
     C                     MOVE SACNO     @RNO,N
     C                     MOVE SCQNF     @CNO,N
      *
      ** If record is not first in subfile check through previous
      ** array elements to see if cheque has been presented before.
     C           @SFI      IFGT 1
     C                     SUB  1         TSFI
     C           1         DO   TSFI      TSFC    20
     C           SACNO     IFEQ @RNO,TSFC
     C           SCQNF     ANDEQ@CNO,TSFC
     C           *IN42     ANDEQ'0'
      *
      ** If cheque presented to this account before and warning not
      ** already displayed then display warning
     C                     ADD  1         @WI
     C                     MOVE 'CHQPRES '@W,@WI
     C                     SETON                     42
     C                     END
     C                     END
     C                     END
     C                     SETOF                     42
      *
     C                     Z-ADDRRNM      @RRNM
     C                     Z-ADDODLN      @ODLN
     C                     Z-ADDODED      @ODED
     C                     Z-ADDHELD      @HELD
     C                     Z-ADDMINB      @MINB
      *
      ** Save transaction amount
     C                     Z-ADD@TNAM     @@TNAM 130
     C                     Z-ADD@TA,@Z    @TNAM
      *
      ** Insufficient funds check
      ** Exceed OD limit    check
      ** Minimum balance    check
      *                                                                   114621
     C***********@V,@IF    IFNE ' '                                       114621
     C***********@V,@OD    ORNE ' '                                       114621
     C***********@V,@MB    ORNE ' '                                       114621
     C           @V,@IF    IFNE 'X'                                       114621
     C           @V,@OD    ORNE 'X'                                       114621
     C           @V,@MB    ORNE 'X'                                       114621
     C                     MOVE SACNO     @ACNOM 100                                          237720
     C                     EXSR RTABAL
     C                     END
      *
      ** Recover from saved transaction amount
     C                     Z-ADD@@TNAM    @TNAM
      *
     C                     Z-ADDCLBLN     @CLBL
     C                     MOVELACST      @ACST
     C                     MOVELSTYP      @STYP
     C                     EXSR RTODCK
      *
     C                     END
      *
      ** If Terminal/warning error(s) found?
     C           @TI       IFGT 0
     C                     SETON                       2256
     C                     MOVEA@T,1      SMSG2
     C*
     C** Clear Cheque Number Duplicate array
     C                     MOVEA*ZEROS    @CQN
     C                     Z-ADD0         Z       10
     C*
     C                     ELSE
     C           @WI       IFGT 0
     C                     SETON                       2257
     C                     MOVEA@W,1      SMSG2
     C                     END
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      *
      /EJECT
      /COPY ZSRSRC,RTIACTC1
      ** Inactive account check subroutine
      *
      /EJECT
      /COPY ZSRSRC,RTBKPTC1
      ** Bankrupt/Liquidation check
      *
      /EJECT
      /COPY ZSRSRC,RTBDPTC1
      ** Bad Debt check
      *
      /EJECT
      /COPY ZSRSRC,RTACLGC1
      ** Account closing check
      *
      /EJECT
      /COPY ZSRSRC,RTBVICC1
      ** Backvaluation/IC check
      *
      /EJECT
      /COPY ZSRSRC,RTTLMTC1
      ** Teller limit check
      *
      /EJECT
      /COPY ZSRSRC,RTLCNVC1
      ** local currency conversion
      *
      /EJECT
      /COPY ZSRSRC,RTBKDRC1
      ** Block debit check subroutine
      *
      /EJECT
      /COPY ZSRSRC,RTBKCRC1
      ** Block credit check subroutine
      *
      /EJECT
      /COPY ZSRSRC,RTRFDRC1
      ** Refer debit check subroutine
      *
      /EJECT
      /COPY ZSRSRC,RTRFCRC1
      ** Refer credit check subroutine
      *
      /EJECT
      /COPY ZSRSRC,RTSTPCC1
      ** Stopped cheque check
      *
      /EJECT
      /COPY ZSRSRC,RTCHQRC1
      ** Cheque out of range check
      *
      /EJECT
      /COPY ZSRSRC,RTABALC1
      ** Account available balance check for debit transaction
      *
      /EJECT
      /COPY ZSRSRC,RTBALCC1
      ** Calculate available balance
      *
      /EJECT
      /COPY ZSRSRC,RTCHQPC1
      ** Cheque present check
      *
      /EJECT
      /COPY ZSRSRC,RTODCKC1
      ** Overdraft check
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *            UPDATE - Update files                              *
      *                                                               *
      * CALLS                                                         *
      *                                                               *
      * CALLED BY         -                                           *
      *                                                               *
      *****************************************************************
     C           UPDATE    BEGSR                           ** UPDATE **
      *
      ** Access and update next available transaction number
     C                     EXSR ZTNLU1
      *
     C                     TIME           STIME   60
     C                     Z-ADD0         @COUNT
     C                     Z-ADD0         @AMDC  130
      *
     C                     SETOF                     27
     C*                                                                   S01408
     C/COPY WNCPYSRC,RE4209CP10                                           S01408
     C*                                                                   S01408
      *
      ** Do for each subfile record within subfile page
     C           @FMSFR    DO   @TOSFR    @SFI
      *
     C           @SFI      CHAINRE4209S1             70
      *
     C           *IN70     IFEQ '0'
     C           @ITMDS    ANDNE*BLANKS
      *
     C                     ADD  1         SBITU
     C                     ADD  1         @NORE   50
     C                     ADD  1         @COUNT
     C                     ADD  S@AMT     @AMDC
      *
     C                     EXSR WTRAND
      *
     C                     EXSR WRSACM
      *
      ** Increment next available transaction number
     C                     ADD  1         TBTN
     C                     MOVE S@BITN    @BITN
     C                     Z-ADDS@I       @I
      *
      ** Cheque book indicator indicates update? update cheque book
     C           S@UCQB    IFEQ 'Y'
     C           GCQI      ANDEQ*ZERO                                                        BUG3229
     C                     EXSR UCHQBK
     C                     END
      *
     C                     END
      *
     C           SMSG2     IFEQ *BLANKS
     C                     SETOF                     22
     C                     ELSE
     C                     SETON                     22
     C                     END
      *
      ** Update subfile line
     C                     UPDATRE4209S1
      *
     C                     END
      *                                                                                       243871
      * Send confirmation message for successful insert                                       243871
     C           @COUNT    IFGT *ZERO                                                         243871
     C                     MOVE 'USR2411' @MSGID                                              243871
     C                     EXSR SNDMSG                                                        243871
     C                     ENDIF                                                              243871
     C*                                                                   S01408
     C/COPY WNCPYSRC,RE4209CP11                                           S01408
     C*                                                                   S01408
      *
      ** Update PF/MEMOS
     C                     EXSR UMEMOS
      *
      ** Update teller transaction trailer
     C                     EXSR UTRANT
      *
      ** Update teller controls
     C                     Z-ADD0         @IAFLG  10
     C                     EXSR UTNTM2
      *
      ** Update teller totals
     C                     EXSR UTNTM3
     C*                                                                   S01408
     C/COPY WNCPYSRC,RE4209CCP4                                           S01408
     C*                                                                   S01408
      *
      ** End of commitment cycle
     C           CMTTXT    COMIT
     C*                                                                   S01408
     C/COPY WNCPYSRC,RE4209CCP5                                           S01408
     C*                                                                   S01408
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            WTRAND - Write teller transacton details           *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  UPDATE - Update control                            *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           WTRAND    BEGSR                           ** WTRAND **
      *
     C                     OPEN TTRANPD
      *
      ** Format record details
     C                     MOVEL'D'       RECI
     C                     MOVEL'B'       TBRI
     C                     MOVELSBTNO     TBID
     C                     MOVELSACNO     ACNO
     C                     MOVELPFNCD     FNCD
     C                     MOVELSCCY      CCY1
     C                     Z-ADDS@AMT     AMT1
     C                     MOVELSTTYP     TTP1
     C                     MOVELSVLDT     VLDT
     C                     MOVELSDEPC     DEPC
     C                     MOVELSPRFC     PRFC
     C                     MOVELSRBCD     RBCD
     C                     MOVELSCQNF     CQNF
     C/COPY WNCPYSRC,RE4209C010
     C                     MOVELPTLBC     ITLB
     C                     MOVEL@JOB      TWID
     C                     MOVELSTIME     TIME
     C                     MOVEL@OVTI     OVTI
     C                     MOVEL' '       CLOS
     C                     Z-ADDSRRNM     MRR1
     C                     MOVEASMSG2     @W,1
     C           SWI       ADD  1         @WX     20
     C                     MOVEASMSG1     @W,@WX
     C                     MOVEA@W,1      MSG1
     C                     MOVEA@W,10     MSG2
     C                     Z-ADD@VLDT     VLDT
     C                     MOVELSANAM     ANAM
     C                     MOVE PTLID     TBIDI
     C                     MOVE S@UCQB    UCQB
      *
     C                     WRITETTRANPDF
      *
     C                     CLOSETTRANPD
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            UMEMOS - Update memos                              *
      *                                                               *
      * CALLS      DBERR  - Database error handling                   *
      *                                                               *
      * CALLED BY  UPDATE - Update control                            *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           UMEMOS    BEGSR                           ** UMEMOS **
      *
      ** Do for each account
     C/COPY WNCPYSRC,RE4209C011
     C                     DO   6         @Z
     C/COPY WNCPYSRC,RE4209C012
      *
     C           @RA,@Z    IFNE *BLANKS
      *
      ** Retrieve PF/MEMOS details
      *
     C***********@RN,@Z****CHAINMEMOS                70                   CCB002
      *********************                                               CCB002
      ***If record not found?                                             CCB002
     C************IN70*****IFEQ '1'                                       CCB002
     C************LOCK*****IN   LDA                        ***************CCB002
     C*********************Z-ADD3         DBASE            ** DB ERR 03 **CCB002
     C*********************MOVE 'MEMOS  ' DBFILE           ***************CCB002
     C*********************MOVEL@RN,@Z    DBKEY                           CCB002
     C*********************EXSR DBERR                                     CCB002
     C*********************END                                            CCB002
      *********************                                               CCB002
      ***If clearing input?                                               CCB002
     C************IN23*****IFEQ '1'                                       CCB002
      *********************                                               CCB002
      ***If value date less than or equal to today's date?                CCB002
     C***********@VLDT*****IFLE @RUND                                     CCB002
     C*********************ADD  @TA,@Z    CLBLN                           CCB002
     C*********************END                                            CCB002
     C*********************ADD  @TA,@Z    LDBLN                           CCB002
     C*********************END                                            CCB002
      *********************                                               CCB002
      ***If payroll input?                                                CCB002
     C************IN24*****IFEQ '1'                                       CCB002
      *********************                                               CCB002
      ***If value date less than or equal to today's date?                CCB002
     C***********@VLDT*****IFLE @RUND                                     CCB002
     C*********************SUB  @TA,@Z    CLBLN                           CCB002
     C*********************END                                            CCB002
     C*********************SUB  @TA,@Z    LDBLN                           CCB002
     C*********************END                                            CCB002
      *********************                                               CCB002
      ***Update PF/MEMOS record                                           CCB002
     C*********************UPDATMEMOSMDF                                  CCB002
      *********************                                               CCB002
     C*********************END                                            CCB002
      *********************                                               CCB002
     C           *IN23     IFEQ '1'                                       CCB002
     C                     Z-ADD@TA,@Z    @PAAO  150                      CCB002
     C                     ENDIF                                          CCB002
      *                                                                   CCB002
     C           *IN24     IFEQ '1'                                       CCB002
     C                     Z-SUB@TA,@Z    @PAAO  150                      CCB002
     C                     ENDIF                                          CCB002
      *                                                                   CCB002
      **                                                                  CCB002
     C                     MOVE @RA,@Z    @RA@ZN 100                      CCB002
      *                                                                   CCB002
      ** Call Access Object AOMEMSU0...                                   CCB002
      *                                                                   CCB002
     C                     CALL 'AOMEMSU0'                                CCB002
     C                     PARM *BLANKS   W0RTCD  7        I:Return code  CCB002
     C                     PARM @RA@ZN    W0RETL 100       O:Retail A/C NoCCB002
     C**********           PARM *ZERO     W0CNUM  60       O:Customer No.              CCB002 CSD027
     C                     PARM *BLANKS   W0CNUM  6        O:Customer No.                     CSD027
     C                     PARM *BLANK    W0CUCD  3        O:Currency     CCB002
     C**********           PARM *ZERO     W0ACCD  40       O:Account Code              CCB002 CGL029
     C                     PARM *ZERO     W0ACCD 100                                          CGL029
     C                     PARM *ZERO     W0ACSQ  20       O:Account Seq. CCB002
     C                     PARM *BLANK    W0BRCA  3        O:Branch       CCB002
     C                     PARM @RUND     W0PSTD  50       O:Posting date CCB002
     C                     PARM @VLDT     W0VDAT  50       O:Value date   CCB002
     C                     PARM @PAAO     W0AMT  150       O:Movement amt CCB002
     C                     PARM 'N'       W0FTOV  1        O:FT override  CCB002
      *                                                                   CCB002
      ** If not accessed properly...                                      CCB002
     C           W0RTCD    IFNE '       '                                 CCB002
     C           *LOCK     IN   LDA                        ***************CCB002
     C                     Z-ADD28        DBASE            ** DB ERR 28 **CCB002
     C                     MOVE 'AOMEMSU0'DBFILE           ***************CCB002
     C                     MOVEL@RN,@Z    DBKEY                           CCB002
     C                     EXSR DBERR                                     CCB002
     C                     ENDIF                                          CCB002
     C/COPY WNCPYSRC,RE4209C005
     C                     ENDIF                                          CCB002
     C                     ENDDO                                          CCB002
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            WRSACM - Write shadow postings                     *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY                                                     *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           WRSACM    BEGSR                           ** WRSACM **
      *
     C**********           Z-ADDS@CNUM    CUSN                                                CSD027
     C                     MOVE S@CNUM    CUSN                                                CSD027
     C                     MOVELS@CCY     CCYD
     C                     Z-ADDS@ACOD    ACDE
     C                     Z-ADDS@ACSQ    ASNC
     C                     MOVELBRCAI     BRCA
     C                     Z-ADDBJRDNB    PTDT
     C                     Z-ADD@VLDT     VUDT
     C                     MOVEL'TT'      TRYP
     C                     MOVEL@NRTD     NRTD
     C                     MOVE TBTN      TNMR
     C                     MOVELSCQNF     CQNR
     C/COPY WNCPYSRC,RE4209C013
     C                     Z-ADDS@AMT     MVAM
      *
      ** If clearing input?
     C           *IN23     IFEQ '1'
     C                     Z-ADD0         DORC
     C                     ELSE
     C                     Z-ADD1         DORC
     C                     END
      *
     C                     MOVELSBTNO     TBID
     C                     MOVEL'N'       APBI
     C                     MOVEL@PASS     PASS
      *                                                                                       CAP205
     C           CAP205    IFEQ 'Y'                                                           CAP205
     C                     MOVEL'RE'      CMOD                                                CAP205
     C                     MOVEL'S'       MTYP                                                CAP205
     C                     ENDIF                                                              CAP205
      *
     C                     WRITERSACMTPO
     C/COPY WNCPYSRC,RE4209C006
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            UCHQBK - Update cheque book                        *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  UPDATE - Update control                            *
      *                                                               *
      * FLDS USED  @CA    - Cheque presented status array             *
      *            @CAI   - Cheque presented status array element     *
      *            @BITN  - Bit position representing cheque number   *
      *                                                               *
      *****************************************************************
     C           UCHQBK    BEGSR                           ** UCHQBK **
      *
     C                     OPEN CHQBKPD
      *
     C                     MOVE SACNO     KACNO  100
     C                     MOVE SCQNF     KSCQN   80
     C*
     C           KLCQBK    SETGTCHQBKPD
     C                     READPCHQBKPD                  70
     C*
     C           *IN70     IFEQ '0'
     C           KACNO     IFNE ACNO
     C                     SETON                     70
     C                     END
     C           KSCQN     IFLT SCQN
     C           KSCQN     ORGT ECQN
     C                     SETON                     70
     C                     END
     C                     END
     C*
      ** If record not found?
     C           *IN70     IFEQ '1'
     C           *LOCK     IN   LDA                        ***************
     C                     Z-ADD4         DBASE            ** DB ERR 04 **
     C                     MOVE 'CHQBKPD' DBFILE           ***************
     C                     MOVELKACNO     DBKEY
     C                     MOVE KSCQN     DBKEY
     C                     EXSR DBERR
     C                     END
      *
     C           @BITN     IFEQ 0
     C                     BITON'0'       @CA,@I
     C                     END
     C           @BITN     IFEQ 1
     C                     BITON'1'       @CA,@I
     C                     END
     C           @BITN     IFEQ 2
     C                     BITON'2'       @CA,@I
     C                     END
     C           @BITN     IFEQ 3
     C                     BITON'3'       @CA,@I
     C                     END
     C           @BITN     IFEQ 4
     C                     BITON'4'       @CA,@I
     C                     END
     C           @BITN     IFEQ 5
     C                     BITON'5'       @CA,@I
     C                     END
     C           @BITN     IFEQ 6
     C                     BITON'6'       @CA,@I
     C                     END
     C           @BITN     IFEQ 7
     C                     BITON'7'       @CA,@I
     C                     END
      *
     C                     UPDATCHQBKPDF
      *
     C                     CLOSECHQBKPD
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            UTRANT - Update teller transaction trailer         *
      *                                                               *
      * CALLS      DBERR  - Database error handling                   *
      *                                                               *
      * CALLED BY  UPDATE - Update control                            *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           UTRANT    BEGSR                           ** UTRANT **
      *
     C                     OPEN TTRANPT
      *
      ** Retrieve teller transaction trailer
     C           1         CHAINTTRANPTF             70
      *
      ** If record not found?
     C           *IN70     IFEQ '1'
     C           *LOCK     IN   LDA                        ***************
     C                     Z-ADD5         DBASE            ** DB ERR 05 **
     C                     MOVE 'TTRANPT' DBFILE           ***************
     C                     MOVEL'1'       DBKEY
     C                     EXSR DBERR
     C                     END
      *
     C                     MOVE 'B'       TTTBRI
     C                     MOVE SBTNO     TTTBID
     C                     Z-ADD99999     TTTBTN
      *
      ** Increment no. of records
     C                     ADD  @NORE     TTNORE
      *
      ** Update teller transaction trailer
     C                     UPDATTTRANPTF
      *
     C                     CLOSETTRANPT
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            UTNTM2 - Update teller controls                    *
      *                                                               *
      * CALLS      DBERR  - Database error handling                   *
      *                                                               *
      * CALLED BY  UPDATE - Update control                            *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           UTNTM2    BEGSR                           ** UTNTM2 **
      *
      ** Set up key list
     C                     MOVEL'B'       KTBRI
     C                     MOVELSBTNO     KTBID
     C                     MOVEL'1'       KRCTP
      *
      ** Get Teller Control Details
     C           KLTRNT    CHAINTTRNT                70
      *
      ** If record not found?
     C           *IN70     IFEQ '1'
     C           *LOCK     IN   LDA                        ***************
     C                     Z-ADD6         DBASE            ** DB ERR 06 **
     C                     MOVE 'TTRNT'   DBFILE           ***************
     C                     MOVEL@TTRNT    DBKEY
     C                     EXSR DBERR
     C                     END
      *
      ** If not reset batch status?
     C           ACTV      IFEQ 'Y'
      *
      ** If subroutine is called from SR.UPDATE (F3 not activated)?
     C           *INKC     IFEQ '0'
     C                     ADD  @NORE     T2NATN
     C                     END
      *
     C                     Z-ADDSBITU     BITU
     C                     END
      *
      ** If active flag is to be reset
     C           @IAFLG    IFEQ 1
     C                     MOVE ' '       ACTV
     C                     END
      *
      ** Update teller controls
     C                     UPDATTTRNTM2F
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      /COPY ZSRSRC,RTTCCYC1
      ** Teller currency conditions
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *            UTNTM1 - Update teller header                      *
      *                                                               *
      * CALLS      DBERR  - Database error handling                   *
      *                                                               *
      * CALLED BY  UPDATE - Update control                            *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           UTNTM1    BEGSR                           ** UTNTM1 **
      *
      * Move fields to key list.
     C                     MOVEL*BLANKS   @TTRNT
      *
      ** Get Teller Control Details
     C           KLTRNT    CHAINTTRNT                70
      *
      ** If record not found?
     C           *IN70     IFEQ '1'
     C           *LOCK     IN   LDA                        ***************
     C                     Z-ADD7         DBASE            ** DB ERR 07 **
     C                     MOVE 'TTRNT'   DBFILE           ***************
     C                     MOVEL@TTRNT    DBKEY
     C                     EXSR DBERR
     C                     END
      *
      ** Update teller tansaction no. of last update
     C                     ADD  2         T1NORE
      *
      ** Update teller totals
     C                     UPDATTTRNTM1F
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            UTNTM3 - Update teller totals                      *
      *                                                               *
      * CALLS      DBERR  - Database error handling                   *
      *            RTTOTS - Teller total updates                      *
      *                                                               *
      * CALLED BY  UPDATE - Update control                            *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           UTNTM3    BEGSR                           ** UTNTM3 **
      *
      * Move fields to key list.
     C                     MOVEL'B'       KTBRI
     C                     MOVELSBTNO     KTBID
     C                     MOVEL'2'       KRCTP
      *
      ** Get Teller Control Details
     C           KLTRNT    CHAINTTRNT                70
      *
      ** If record not found?
     C           *IN70     IFEQ '1'
     C           *LOCK     IN   LDA                        ***************
     C                     Z-ADD8         DBASE            ** DB ERR 08 **
     C                     MOVE 'TTRNT'   DBFILE           ***************
     C                     MOVEL@TTRNT    DBKEY
     C                     EXSR DBERR
     C                     END
      *
     C                     MOVELSCCY      @CCY
     C                     Z-ADD@AMDC     @TAMT
     C                     MOVEL@RVIN     @RVIN
     C                     MOVEL@DCIN     @DCIN
     C                     MOVEL@CSIN     @CSIN
     C                     MOVEL@CLIN     @CLIN
     C                     EXSR RTTOTS
      *
      ** Update teller tansaction no. of last update
     C                     Z-ADD@NATN     TTLU
      *
      ** Update teller totals
     C                     UPDATTTRNTM3F
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      /COPY ZSRSRC,RTTOTSC1
      ** Calculate teller totals updates
      *
      *****************************************************************
      /EJECT
      ** SR.ZACCH  subroutine
      /COPY ZSRSRC,ZACCH
      *
      *****************************************************************
      /EJECT
      ** SR.ZALIGN subroutine
      /COPY ZSRSRC,ZALIGNZ2
      *
      *****************************************************************
      /EJECT
      ** SR.ZCHKH  subroutine
      /COPY ZSRSRC,ZCHKH
      *
      *****************************************************************
      /EJECT
      ** SR.ZDATE1 subroutine
      /COPY ZSRSRC,ZDATE1Z2
      *
      *****************************************************************
      /EJECT
      ** SR.ZDATE2 subroutine
      /COPY ZSRSRC,ZDATE2Z2
      *
      *****************************************************************
      /EJECT
      ** SR.ZFWDT  Subroutine
      /COPY ZSRSRC,ZFWDT
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      /EJECT
      ** SR.ZTNLU1 Subroutine
      /COPY ZSRSRC,ZTNLU1Z2
      *
      *****************************************************************
      /EJECT
      ** SR.ZFRPED Subroutine
      /COPY ZSRSRC,ZFRPEDZ3
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            FIRST  - Initial processing                        *
      *                                                               *
      * CALLS      DBERR  - Database error handling                   *
      *            RTCCYS - Currency setup                            *
      *                                                               *
      * CALLED BY  MAIN   - Main control processing                   *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           FIRST     BEGSR                           ** FIRST  **
      *
      ** Input parameter(s) :
     C           *ENTRY    PLIST                           Entry parameter list
     C                     PARM           PFNCD   3        Function code
      *
      ** Define Keylist(s)
      *
      ** Currency Details
     C           KLBANK    KLIST
     C                     KFLD           BJBANK
      *
      ** Retail Details
     C           KLRETD    KLIST
     C                     KFLD           BMRETL
      *
      ** Account code details
     C           KLACOD    KLIST
     C                     KFLD           A5ACCD
      *
     C** Account Movements details
     C           KLRSAC    KLIST
     C                     KFLD           BRCA
     C                     KFLD           CNUM
     C                     KFLD           CCY
     C                     KFLD           ACOD
     C                     KFLD           ACSQ
     C                     KFLD           @CQNF
      *
      ** Retrieve RTS job dataarea
     C           *NAMVAR   DEFN           RTSDTA
     C                     IN   RTSDTA
      *
     C                     MOVELPDEPC     SDEPC
      *
      **   Reset LDA
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVE *BLANKS   DBLOT
     C                     OUT  LDA
      *
      ** Get Base Currency - Bank Details
      ** Set up key list
      *
      ** If record not found?
     C** Use access program AOBANKR0 to retrieve header details
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*BLANKS' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C** If error in access program, exit via DBERR subroutine
     C*
     C           @RTCD     IFNE *BLANKS
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE           ***************
     C                     MOVE @OPTN     DBKEY            ** DB ERR 09 **
     C                     Z-ADD9         DBASE            ***************
     C                     EXSR DBERR
     C                     END
      *
      ** Set up local currency
     C                     MOVE BJLCCY    @CCY
     C                     EXSR RTCCYS
      *
      ** If record not found?
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVE 'SDCURRPD'DBFILE
     C                     Z-ADD10        DBASE
     C                     MOVELBJLCCY    DBKEY
     C                     EXSR DBERR
     C                     END
      *
      ** Set up date format indicator
     C           BJDFIN    IFEQ 'D'
     C                     SETOF                     98
     C                     ELSE
     C                     SETON                     98
     C                     END
      *
      ** Get Retail Details
      ** Set up key list
     C*
     C                     CALL 'AORETLR0'
     C                     PARM '*BLANK ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDRETL    PARM SDRETL    DSFDY
      *
      ** If record not found?
      *
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDRETLPD'DBFILE           **************
     C                     MOVEL@OPTN     DBKEY            ** DB ERR 11 **
     C                     Z-ADD11        DBASE            ***************
     C                     EXSR DBERR
     C                     END
      *
      ** Get Function Code Details
     C                     CALL 'AOFNCDR0'
     C                     PARM '*BLANKS' @RTCD   7
     C                     PARM '*KEY   ' @OPTN   7
     C                     PARM PFNCD     @FNCD   3
     C           SDFNCD    PARM SDFNCD    DSFDY
      *
      ** If record not found?
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA                        ***************
     C                     Z-ADD12        DBASE            ** DB ERR 12 **
     C                     MOVE 'SDFNCDPD'DBFILE           ***************
     C                     MOVELPFNCD     DBKEY
     C                     EXSR DBERR
     C                     END
      *
      ** Store validation indicators in an array
     C                     MOVEAFQVWIN    @V
      *
      ** Access PF/SDGELRPD for profit centre details
     C**********           CALL 'AOGELRR0'                                                    CGL029
     C                     CALL 'AOGELRR1'                                                    CGL029
     C                     PARM '*DBERR ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C********** SDGELR    PARM SDGELR    DSFDY                                               CGL029
     C           SDGELR    PARM SDGELR    DSSDY                                               CGL029
     C*
     C** Check if 'Profit Centre used'
     C           PFCU      IFEQ 'Y'
     C                     MOVE '1'       *IN28   1
     C                     END
      *                                                                   CRT007
      ** Determine if SAR CRT003 Cashier Interface is installed           CRT007
      *                                                                   CRT007
     C                     CALL 'AOSARDR0'                                CRT007
     C                     PARM *BLANKS   PRTCD   7                       CRT007
     C                     PARM '*VERIFY 'POPTN   7                       CRT007
     C                     PARM 'CRT003'  PSARD   6                       CRT007
     C           SCSARD    PARM SCSARD    DSFDY                           CRT007
      *                                                                   CRT007
     C           PRTCD     IFNE *BLANKS                                   CRT007
     C           PRTCD     ANDNE'*NRF   '                                 CRT007
     C           *LOCK     IN   LDA                                       CRT007
     C                     MOVEL'SCSARDPD'DBFILE                          CRT007
     C                     Z-ADD8         DBASE                           CRT007
     C                     MOVEL'CRT003'  DBKEY                           CRT007
     C                     EXSR DBERR                                     CRT007
     C                     ENDIF                                          CRT007
      *                                                                   CRT007
     C           PRTCD     IFEQ *BLANKS                                   CRT007
     C                     MOVE 'Y'       CRT003                          CRT007
     C                     ELSE                                           CRT007
     C                     MOVE 'N'       CRT003                          CRT007
     C                     ENDIF                                          CRT007
      *                                                                                       CRT026
      ** Check if CRT026 is switched on                                                       CRT026
      *                                                                                       CRT026
     C                     CALL 'AOSARDR0'                                                    CRT026
     C                     PARM *BLANKS   @RTCD   7                                           CRT026
     C                     PARM '*VERIFY' @OPTN   7                                           CRT026
     C                     PARM 'CRT026'  @SARD   6                                           CRT026
     C           SCSARD    PARM SCSARD    DSFDY                                               CRT026
      *                                                                                       CRT026
     C           @RTCD     IFNE *BLANKS                                                       CRT026
     C           @RTCD     ANDNE'*NRF   '                                                     CRT026
     C           *LOCK     IN   LDA                                                           CRT026
     C                     MOVEL'SCSARDPD'DBFILE           ***************                    CRT026
     C                     Z-ADD13        DBASE            ** DB ERR 13 **                    CRT026
     C                     MOVEL'CRT026'  DBKEY            ***************                    CRT026
     C                     EXSR DBERR                                                         CRT026
     C                     ENDIF                                                              CRT026
      *                                                                                       CRT026
     C           @RTCD     IFEQ *BLANKS                                                       CRT026
     C                     MOVE 'Y'       CRT026  1                                           CRT026
     C                     ELSE                                                               CRT026
     C                     MOVE 'N'       CRT026                                              CRT026
     C                     ENDIF                                                              CRT026
      *                                                                                       CAP205
      ** Check if CAP205 is switched on                                                       CAP205
      *                                                                                       CAP205
     C                     CALL 'AOSARDR0'                                                    CAP205
     C                     PARM *BLANKS   @RTCD                                               CAP205
     C                     PARM '*VERIFY' @OPTN                                               CAP205
     C                     PARM 'CAP205'  @SARD                                               CAP205
     C           SCSARD    PARM SCSARD    DSFDY                                               CAP205
      *                                                                                       CAP205
     C           @RTCD     IFNE *BLANKS                                                       CAP205
     C           @RTCD     ANDNE'*NRF   '                                                     CAP205
     C           *LOCK     IN   LDA                                                           CAP205
     C                     MOVEL'SCSARDPD'DBFILE           ***************                    CAP205
     C                     Z-ADD14        DBASE            ** DB ERR 14 **                    CAP205
     C                     MOVEL'CAP205'  DBKEY            ***************                    CAP205
     C                     EXSR DBERR                                                         CAP205
     C                     ENDIF                                                              CAP205
      *                                                                                       CAP205
     C           @RTCD     IFEQ *BLANKS                                                       CAP205
     C                     MOVE 'Y'       CAP205  1                                           CAP205
     C                     ELSE                                                               CAP205
     C                     MOVE 'N'       CAP205                                              CAP205
     C                     ENDIF                                                              CAP205
     C*                                                                   S01408
     C/COPY WNCPYSRC,RE4209CCP6                                           S01408
     C*                                                                   S01408
      *
      ** If no error(s)? override LF/TTRNT
     C                     CALL 'QCMDEXC'              79
     C                     PARM           @CN,3
     C                     PARM 80        @@LEN  155
      *
     C                     OPEN TTRNT
      *
      ** Define Error Message Fields
     C                     MOVE *BLANKS   @MSGID  7
     C                     MOVEL'RTSMSGF' @MSGF  10
      *
      ** initialise Screen fields
     C                     SETON                     10
     C                     MOVEL@PGM      SPGMQ
     C                     MOVEL@JOB      SWID
     C                     MOVEL@USER     SUSER
     C                     MOVELBJMRDT    SRUNA
      *
      ** initialise work fields and indicators
     C                     Z-ADD0         @TAMT  150
     C                     Z-ADD0         @SFRRN  50
     C/COPY WNCPYSRC,RE4209C014
     C                     Z-ADD6         @SFPAG  50
     C/COPY WNCPYSRC,RE4209C015
     C                     MOVE PASS      @PASS   2
     C                     Z-ADD0         @VLDT
     C                     Z-ADDBJRDNB    @RUND   50
     C                     Z-ADD0         TBTN
     C                     Z-ADD1         @COUNT  10
      *
     C                     ENDSR
      *****************************************************************
     C*                                                                   S01408
     C/COPY WNCPYSRC,RE4209CCP7                                           S01408
     C*                                                                   S01408
     C*                                                                   S01408
     C/COPY WNCPYSRC,RE4209CCP9                                           S01408
     C*                                                                   S01408
     C*                                                                   S01408
     C/COPY WNCPYSRC,RE4209CCP8                                           S01408
     C*                                                                   S01408
      /EJECT
      /COPY ZSRSRC,RTCCYSC1
      ** currency setup
      *
      /EJECT
      *****************************************************************
      *                                                               *
      *            DBERR  - Database Error Handling                   *
      *                                                               *
      * CALLS      *PSSR  - Program  error                            *
      *                                                               *
      * CALLED BY  FIRST  - common  Processing                        *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           DBERR     BEGSR                           ** DBERR  **
      *
     C                     MOVEL@PGM      DBPGM
     C                     OUT  LDA
      *
     C                     EXSR *PSSR
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            LAST   - Final Processing                          *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  MAIN   - Main Control Processing                   *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           LAST      BEGSR                           ** LAST   **
      *
     C                     CLOSE*ALL                   69
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * *PSSR             - Program error                             *
      *                                                               *
      * CALLS      DBERRCTL                                           *
      *                                                               *
      * CALLED BY  DBERR  - Database Error Handling                   *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           ** *PSSR  **
      *
     C*
     C** Check to see that *PSSR has not been called yet
     C*
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
     C*
     C** Call standard database error program
     C*
     C                     CALL 'DBERRCTL'
     C*
     C                     END
     C*
     C** If *PSSR already run, then just end the program here
     C*
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      /COPY ZSRSRC,RTENCPC1
      ** Passcode encription
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      ***Retail Shadow Balances                                           CCB002
     O****MEMOSMDFE************    @RLMEM                                 CCB002
      *
      ** Cheque book details
     OCHQBKPDFE                @RLCQB
      *
      ** Teller controls
     OTTRNTM2FE                @RELTT
      *
      *****************************************************************
**  CPY@  OBJECT COPYRIGHT
(c) Finastra International Limited 2001
** @CN - Constants for QCMDEXC functions
OVRDBF FILE(TTRANPD) MBR(    ) SHARE(*NO) SECURE(*YES) SEQONLY(*NO)
OVRDBF FILE(TTRANPT) MBR(    ) SHARE(*NO) SECURE(*YES) SEQONLY(*NO)
OVRDBF FILE(TTRNT)   SHARE(*NO)
**   ZYDY - Years in days cumulative, four year span
0366073110961461
**   ZMDY - Months in days cumulative through year
000031059090120151181212243273304334365
**   ZMNM - Months short names
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
** - @P   - Array to hold the positions of the validation indicators
01020304050607080910111213141516171819202122232425
**  alphanumeric table
ABCDEFGHIJKLMNOPQRSTUVWXYZ 0123456789
**  encription code table
SDKJFIENVQPWOCMUXZ293847501LRBAGYHT6ZSDKJ
FIENVQPWOCMUXZ293847501LRBAGYHT6ZSDKJFIEN