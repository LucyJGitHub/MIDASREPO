     H        1
      *****************************************************************
/*STD *  RPGBASE                                                      *
/*EXI *  TEXT('Midas RE Cheque book maintenance')                     *
/*OVRF*: OVRDBF SUBF1 SDATPD1                                       : *                     BUG23918
     F*****************************************************************
     F*                                                               *
     F*  Midas - Retail Module                                        *
     F*                                                               *
     F*  RE4212 - Cheque Book Maintenance                             *
     F*                                                               *
     F*  Function:  This program allows the issuing and deletion      *
     F*   (I/C)     of cheque books.                                  *
     F*                                                               *
     F*  Called By: REC4212 - Cheque Book Maintenance                 *
     F*                                                               *
      *  (c) Misys International Banking Systems Ltd. 2001            *
     F*                                                               *
      *  Last Amend No. CGL154             Date 13Oct14               *
      *  Prev Amend No. MD031369           Date 05Jun15               *
      *                 AR1035423          Date 19Nov12               *
      *                 AR876064           Date 05Jan12               *
      *                 AR848491           Date 03Dec11               *
      *                 CSF011             Date 12Sep11               *
      * Bank Fusion Midas 1.4 Base -----------------------------------*
      *                 CSC041             Date 26Nov09               *
      * Midas Plus 1.4 Base 04 ---------------------------------------*
      * Midas Plus 1.4 Base ------------------------------------------*
      * Midas Plus 1.3 ----------- Base ------------------------------*
      *                 240786             Date 06Jul06               *
      *                 CSD031             Date 10Apr06               *
      *                 CSD027             Date 09Dec05               *
      *                 BUG3224            Date 19May05               *
      *                 CSW037A            Date 02May05               *
      *                 CSD025             Date 11Jan05               *
      *                 CSW037             Date 15Dec04               *
      *                 CSW036             Date 15Dec04               *
      *                 BUG2966            Date 04Jun04               *
      *                 BUG3685            Date 16Jul04               *
      *                 BUG3303            Date 23Jun04               *
      *                 BUG2965            Date 15Jun04               *
      *                 BUG3026            Date 08Jun04               *
      *                 BUG3025            Date 02Jun04               *
      *                 BUG3023            Date 02Jun04               *
      *                 BUG2965            Date 30May04               *
      *                 BUG2964            Date 30May04               *
      *                 CRE019             Date 12Jan04               *
      * Midas Release 4 --------------- Base -------------------------*
      *                 174972             Date 17Apr01               *
      * Midas DBA 3.04 -----------------------------------------------*
      * Midas DBA 3.00 ---------------- Base -------------------------*
      *                 CRT001  *CREATE    Date 28Feb95               *
     F*                                                               *
     F*---------------------------------------------------------------*
     F*                                                               *
      *  CGL154 - FATCA Reporting (Recompile)                         *
      *  MD031369 - Error message encountered during Cheque book      *
      *             deletion (Recompile)                              *
      *  AR1035423 - Issues in cheque book maintenance. Recompiled    *
      *              due to changes in DSPF/RE4212DF                  *
      *              (Child: AR1035425)
      *  AR876064 - Upon enquiry in the cheque book maintenance       *
      *             screen erroneous NUMBER OF CHEQUES is shown       *
      *             (Recompile)                                       *
      *  AR848491 - Date issued and Date collected showed             *
      *             erroneous dates (Recompile)                       *
      *  CSF011 - Customer Name on Inputs                             *
      *  CSC041 - 4-Eyes Checking Gaps (Applied BUG23918)             *
      *  BUG23918 - Database error occurs in Cheque Book Maintenance  *
      *             upon pressing F15. Applied GEMS fix 239709A       *
      *  239709A - Session Device occur when Subfile records exceed   *
      *            9999.                                              *
      *  240786 - Add a more appropriate error message for cases      *
      *           wherein the inputted checkbook has a range that     *
      *           overlaps with logically deleted checkbook;          *
      *           add in subroutine VRANGE                            *
      *  CSD031 - Enhanced Centralised Static Data (Recompile)        *
      *  CSD027 - Conversion Of Customer Number to Alpha              *
      *  BUG3224 - Incorrect warning message during Insert            *
      *  CSW037A- Additional Field extended to 40 chars (recompile)   *
      *  CSD025 - Customer De-Activation                              *
      *  CSW037 - Additional Field Data on MT300/MT320                *
      *  CSW036 - Dual SWIFT BIC held on Client Details               *
      *  BUG2966 - Charge book should not require an associated GL    *
      *            code on transaction.                               *
      *  BUG3685 - Allow reinsert deleted checks issued to customer   *
      *            if not yet collected.                              *
      *  BUG3303 - Remove commitment control on batch input of cheque *
      *            books. F12 cancels on screen insert in progress.   *
      *  BUG2965 - Fix reversed/not required. NAD. Also fixed missing *
      *            error messages in browser screen.                  *
      *  BUG3026 - Re-compiled due to changed DSPF/RE4212DF.          *
      *  BUG3025 - Use a different LF for building subfiles.          *
      *  BUG3023 - Webfacing adjustments                              *
      *  BUG2965 - Cheque books should not overlap irregardless of    *
      *            GIRO/Cheque indicator                              *
      *  BUG2964 - Add column to cheque book browser to distinguish   *
      *            GIRO and cheques.                                  *
      *  CRE019 - Cheque Processing and Maintenance                   *
     F*  174972 - CHKB cannot be used simultaneously by many users    *
     F*           Database error encountered. Applied fix 123030.     *
     F*  CRT001 - Retail Teller System.                               *
     F*                                                               *
     F*****************************************************************
     F*
     FACNUM   IF  E           K        DISK
     F**  Retail Account Number
     F*
     FCHQBKL1 UF  E           K        DISK         KINFSR *PSSR A
     F**  Chq Book Logical file
     F**********************************************KCOMIT*************                       CRE019
     F**********                                    KCOMIT                            CRE019 BUG3303
      *                                                                                      BUG3025
     FCHQBKL3 IF  E           K        DISK         KINFSR *PSSR                             BUG3025
     F            CHQBKPHF                          KRENAMECHQBKPH3                          BUG3025
     F            CHQBKPDF                          KRENAMECHQBKPD3                          BUG3025
     F            CHQBKPTF                          KRENAMECHQBKPT3                          BUG3025
     F*
     FRE4212DFCF  E                    WORKSTN      KINFSR *PSSR
     F                                        SFRLRNKSFILE RE4212S1                           CRE019
     F**  Display file - Cheque book maintenance
     F*
     FSUBF1   UF  E           K        DISK                      A                           239709A
     F/EJECT
     F*****************************************************************
     F*                                                               *
     F*  F U N C T I O N   O F   I N D I C A T O R S                  *
     F*                                                               *
     F*       09         To Condition Display Of 'F10=DELETE'         *
     F*       10         Message Subfile End Indicator (SFLEND)       *
     F*       11         Protect Indicator for SACTN,SRACNO,SGCIN     *
     F*                                        SSCQN                  *
     F*       12         Protect Indicator for SECQN,SNOCQ,SISSDT     *
     F*                                        SCLTDT                 *
     F*       13         Protect Indicator for SECQN,SNOCQ,SISSDT     *
     F*                                                               *
     F*       31         Error in field SACTN                         *
     F*       32         Error in field SRACNO                        *
     F*       33         Error in field SGCIN                         *
     F*       34         Error in field SSCQN                         *
     F*                                                               *
     F*       41         Error in field SECQN                         *
     F*       42         Error in field SNOCQ                         *
     F*       43         Error in field SISSDT                        *
     F*       44         Error in field SCLTDT                        *
     F*                                                               *
     F*       51         Validate key Fields if it is OFF             *
     F*       52         Validate non-key Fields if it is ON          *
     F*                                                               *
     F*       61         Any error in key fields validation           *
     F*       62         Any error in non-key fields validation       *
     F*       69         General error indicator                      *
     F*                                                               *
     F*       70         General Indicator Used For CHAIN             *
     F*       73         Error during updation                        *
     F*                                                               *
     F*       KC         Exit Program                                 *
     F*       KE         Refresh Screen                               *
     F*       KJ         Delete Record                                *
     F*                                                               *
     F*     90-99        Indicators used by standard subroutines      *
     F*     U7-U8        If database error occurs                     *
     F*                                                               *
     F*****************************************************************
     F/EJECT
     F*****************************************************************
     F*                                                               *
     F*  S U B R O U T I N E   I N D E X                              *
     F*                                                               *
     F*  Control routines                                             *
     F*                                                               *
     F*  AMDPR  - Amendment processing                                *
     F*  DBERR  - Database error handling                             *
     F*  DELPR  - Deletion processing                                 *
     F*  ENQPR  - Enquiry processing                                  *
     F*  FIRST  - Initial Processing                                  *
     F*  FTOWK  - Move File Fields to Work/Screen                     *
     F*  INSPR  - Insertion processing                                *
     F*  LAST   - Final Processing                                    *
     F*  MAIN   - Main Processing                                     *
     F*  PCMTXT - Prepare comit text CMTTXT                           *
     F*  SBTOFF - Set bit off                                         *
     F*  SNDMSG - Send program message parameters                     *
     F*  WKTOF  - Move Work/Screen Fields To File                     *
     F*  ZA0150 - Get Midas Rundate & Setup Indicator                 *
     F*  *PSSR  - Program error                                       *
     F*                                                               *
     F*  Screen Handling                                              *
     F*                                                               *
     F*  DSPF0  - Display screen format                               *
     F*                                                               *
     F*  Screen Initialisation / Resets                               *
     F*                                                               *
     F*  INT01  - Initialize Indicators for key Fields                *
     F*  INT02  - Initialize Indicators for non-key fields            *
     F*  IFLDS  - Initialize Work/Screen Fields                       *
     F*  RFRESH - Refresh screen                                      *
     F*                                                               *
     F*  Validation routines                                          *
     F*                                                               *
     F*  VALF0  - Validate screen input                               *
     F*  VAL01  - Validate key Fields                                 *
     F*  VAL02  - Validate non-key Fields                             *
     F*  VSNOCQ - Validate number of cheques                          *
     F*  VSECQN - Validate ending cheque no                           *
     F*  VSDISS - Validate date of issue                              *
     F*  VSDCOL - Validate date of collection                         *
     F*                                                               *
     F*  Updates routines                                             *
     F*                                                               *
     F*  UPDAD  - Update procedure for amend/delete                   *
     F*  UPDRTN - Update control routine                              *
     F*  UPHED  - Update Header Record                                *
     F*  UPTRL  - Update Control Trailer Record                       *
     F*  WRCQBK - Write CHKBKPD Record                                *
     F*                                                               *
     F*  Printing routines                                            *
     F*                                                               *
     F*                                                               *
     F*  Standard RTS Subroutines                                     *
     F*                                                               *
     F*                                                               *
     F*  Standard MIDAS Subroutines                                   *
     F*                                                               *
     F*  ZTNLU1 - Access dataarea for next transaction no.            *
     F*  ZDATE1 - Date validation routine                             *
     F*  ZDATE2 - Convert Day number to date                          *
     F*  ZALIGN - Validate amount fields                              *
     F*                                                               *
     F*  External Routines                                            *
     F*                                                               *
     F*  SCC0250 - Clear Message Queue                                *
     F*  SCC0240 - Send Message                                       *
     F*                                                               *
     F*  DBERRCTL Program error                                       *
     F*                                                               *
     F*****************************************************************
     E/EJECT
     E*****************************************************************
     E                    CPY@    1   1 80
     E**  Array containing Copyright statement
     E*
     E                    @CP        20  1
     E**  Cheque presented array
     E*
     E/COPY ZSRSRC,ZALIGNZ1
     E**  Array for both SR.ZALIGN, SR.ZEDIT
     E*
      /COPY ZSRSRC,ZFRPEDZ1                                                                   CRE019
      ** Array for SR.ZFRPED                                                                  CRE019
      *                                                                                       CRE019
     E/COPY ZSRSRC,ZDATE2Z1
     E**  Array for SR.ZDATE2 subroutine
     E*
     E*****************************************************************
     I/EJECT
     I*****************************************************************
     I*
     IACCNTABF
     I**  Renamed fields in LF/ACNUM
     I              BRCA                            ABRCA
     I*
     I@CPDS       DS                             20
     I**  Data structure to redefine cheque presented array
     I                                        1  20 @CP
     I                                        1  20 CQPS
     I            DS                             20
     I**  Data structure to for holding key list of cheque book file
     I                                        1  19 @CHQBK
     I                                        1  100KACNO
     I                                       11  110KGCIN
     I                                       12  190KSCQN
     I*
     I*
     ICMTTXT      DS                             92
     I**  Comit text
     I                                        1   50NATN
     I                                        6   7 MDID
     I                                        8  15 PGMN
     I                                       16  18 WSIDX
     I                                       19  240MTIME
     I                                       25  25 SACTN
     I                                       26  35 USRIDX
     I*
     I                                       51  60 SRACNO
     I                                       61  61 SGCIN
     I                                       62  69 SSCQN
     I                                       70  77 SECQN
     I                                       78  80 SNOCQ
     I                                       81  86 SISSDT
     I                                       87  92 SCLTDT
     I*
     ICSFLDS      DS                             45                                           CRE019
      **  Data structure for current subfile selection fields                                 CRE019
     I                                        1   3 SLBRCA                                    CRE019
     I                                        4   4 SLNYIS                                    CRE019
     I                                        5  14 SLCNUM                                    CRE019
     I                                       15  15 SLNYCL                                    CRE019
     I                                       16  21 SLISFR                                    CRE019
     I                                       22  27 SLISUN                                    CRE019
     I                                       28  37 SLACNO                                    CRE019
     I                                       38  45 SLCQNO                                    CRE019
      *                                                                                       CRE019
     ICSFBAK      DS                             45                                           CRE019
      **  Data structure for back-up subfile selection fields                                 CRE019
     I                                        1   3 BKBRCA                                    CRE019
     I                                        4   4 BKNYIS                                    CRE019
     I                                        5  14 BKCNUM                                    CRE019
     I                                       15  15 BKNYCL                                    CRE019
     I                                       16  21 BKISFR                                    CRE019
     I                                       22  27 BKISUN                                    CRE019
     I                                       28  37 BKACNO                                    CRE019
     I                                       38  45 BKCQNO                                    CRE019
      *                                                                                       CRE019
     I**SFLINE      DS                            108                                 CRE019 BUG2964
     I**SFLINE**    DS                          112                                   BUG2964 CSF011
     ISFLINE      DS                            172                                           CSF011
      **  Data structure for a line of detail in the subfile                                  CRE019
     I                                        1   3 SFBRCA                                    CRE019
     I                                        4   9 SFCNUM                                    CRE019
     I                                       10  10 SFGCIN                                    CRE019
     I                                       11  13 SFNOCQ                                    CRE019
     I                                       14  18 SFCHTT                                    CRE019
     I                                       19  24 SFISDT                                    CRE019
     I                                       25  30 SFCLDT                                    CRE019
     I                                       31  350SFTNLU                                    CRE019
     I                                       36  410SFOEDT                                    CRE019
     I                                       42  42 SFSEL                                     CRE019
     I                                       43  52 SFACNO                                    CRE019
     I                                       53  69 SFSECN                                    CRE019
     I                                       53  60 SFSCQN                                    CRE019
     I                                       62  69 SFECQN                                    CRE019
     I                                       70  84 SFDATE                                    CRE019
     I                                       85  98 SFCHAM                                    CRE019
     I                                       99 108 SFCHAC                                    CRE019
     I                                      109 112 SFGORC                                   BUG2964
     I                                      113 122 SFCSSN                                    CSF011
     I                                      123 142 SFREPN                                    CSF011
     I                                      143 152 SFREPT                                    CSF011
     I                                      153 172 SFACCN                                    CSF011
     I*
     I/COPY ZSRSRC,ZTNLU1Z1
     I**  Data structure to update last transaction number
     I*
      /COPY ZSRSRC,ZFRPEDZ2                                                                   CRE019
      ** Data structure for SR.ZFRPED                                                         CRE019
     I*
     IPSDS       SDS
     I**  Program Status Data Structure
     I                                        1  10 @PGM
     I**  Field containing Program name
     I                                      244 253 @JOB
     I**  Field containing Workstation ID
     I                                      254 263 @USER
     I**  Field Containing User ID
     I*
     I*
     ILDA        UDS                            256
     I**  Local data area for data base errors.
     I*
     I                                      134 183 DBLOT
     I**  Field combining the following fields
     I                                      134 141 DBFILE
     I**  Name of database file in error
     I                                      142 170 DBKEY
     I**  Key value causing database error
     I                                      171 180 DBPGM
     I**  Name of program causing database error
     I                                      181 1830DBASE
     I**  Number of database error
     I*
     I*
     ISDBANK    E DSSDBANKPD
     I**  Bank Details
     I*
     IDSFDY     E DSDSFDY
     I**  Short data structure for access programs (200 long)
     I*
     IDSSDY     E DSDSSDY                                                                     CRE019
      **  Long data structure for access programs (800 long)                                  CRE019
      *                                                                                       CRE019
     ISCSARD    E DSSCSARDPD                                                                  CRE019
      ** Switchable features' description file                                                CRE019
      *                                                                                       CRE019
     ISDBRCH    E DSSDBRCHPD                                                                  CRE019
      ** SD Branch Details                                                                    CRE019
      *                                                                                       CRE019
     ISDCUST    E DSSDCUSTPD                                                                  CRE019
      ** SD Customer Details                                                                  CRE019
     I              QQDFAC                          CQDFAC                                    CRE019
      *                                                                                       CRE019
     ISDCURR    E DSSDCURRPD                                                                  CRE019
      ** SD Currency Details                                                                  CRE019
      *                                                                                       CRE019
     ISDRETR    E DSSDRETRPD                                                                  CRE019
      ** Retail Transaction Types                                                             CRE019
     I*
     I*****************************************************************
     C/EJECT
     C*****************************************************************
     C*                                                               *
     C*            MAIN   - Main Processing                           *
     C*                                                               *
      *****************************************************************                       CRE019
     C                     MOVEACPY@      BIS@   80                                           CRE019
      *                                                                                       CRE019
      ** Initial processing                                                                   CRE019
      *                                                                                       CRE019
     C                     EXSR FIRST                                                         CRE019
      *                                                                                       CRE019
     C           CRE019    IFEQ 'Y'                                                           CRE019
     C                     EXSR PROMPT                                                        CRE019
     C                     ELSE                                                               CRE019
     C                     EXSR DTLSCR                                                        CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     EXSR LAST                                                          CRE019
      *                                                                                       CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      * PROMPT - Prompt for action and control screen processing                              CRE019
      *****************************************************************                       CRE019
     C           PROMPT    BEGSR                                                              CRE019
      *                                                                                       CRE019
     C                     MOVE '001'     SCBIT                                               CRE019
     C                     MOVE *OFF      *IN18                                               CRE019
     C                     MOVE *OFF      *IN19                                               CRE019
     C                     MOVE *ON       *IN20                                               CRE019
     C                     MOVE *ON       *IN22                                               CRE019
     C                     MOVE *OFF      *IN23                                               CRE019
     C                     MOVE *ON       *IN28                                               CRE019
      *                                                                                       CRE019
      ** Do while F3 Exit is not pressed                                                      CRE019
      *                                                                                       CRE019
     C           *INKC     DOWEQ*OFF                                                          CRE019
      *                                                                                       CRE019
      ** Display header and footer format                                                     CRE019
      *                                                                                       CRE019
     C                     WRITERE4212C0                                                     BUG3023
     C                     WRITERE4212F0                                                      CRE019
     C                     WRITERE4212F2                                                      CRE019
     C**********           WRITERE4212C0                                              CRE019 BUG3023
      *                                                                                       CRE019
      ** Only display action code and number of cheques fields for prompt                     CRE019
      *                                                                                       CRE019
     C                     EXFMTRE4212F1                                                      CRE019
      *                                                                                       CRE019
      ** Clear program message queue                                                          CRE019
      *                                                                                       CRE019
     C                     CALL 'SCC0250'                                                     CRE019
      *                                                                                       CRE019
     C                     SELEC                                                              CRE019
      *                                                                                       CRE019
      ** Refresh (F5)                                                                         CRE019
      *                                                                                       CRE019
     C           *INKE     WHEQ *ON                                                           CRE019
     C                     EXSR RFRESH                                                        CRE019
     C                     MOVE *BLANK    SACTN                                               CRE019
     C                     MOVE '001'     SCBIT                                               CRE019
     C                     MOVE *OFF      *IN18                                               CRE019
     C                     MOVE *OFF      *IN19                                               CRE019
     C                     MOVE *ON       *IN22                                               CRE019
     C                     MOVE *OFF      *IN24                                               CRE019
      *                                                                                       CRE019
      ** Select details to process from subfile (F15)                                         CRE019
      *                                                                                       CRE019
     C           *INKP     WHEQ *ON                                                           CRE019
     C                     EXSR RFRESH                                                        CRE019
     C                     MOVE *BLANK    SACTN                                               CRE019
     C                     MOVE *ON       *IN18                                               CRE019
     C                     MOVE *ON       *IN19                                               CRE019
     C                     MOVE *OFF      *IN22                                               CRE019
     C                     MOVE *OFF      *IN24                                               CRE019
     C                     EXSR CQLIST                                                        CRE019
     C                     MOVE *OFF      *IN18                                               CRE019
     C                     MOVE *OFF      *IN19                                               CRE019
     C                     MOVE *ON       *IN20                                               CRE019
     C                     MOVE *ON       *IN22                                               CRE019
      *                                                                                       CRE019
      ** Enter key has been pressed                                                           CRE019
      *                                                                                       CRE019
     C                     OTHER                                                              CRE019
      *                                                                                       CRE019
      ** Ensure correct footer                                                                CRE019
      *                                                                                       CRE019
     C                     MOVE *OFF      *IN25                                               CRE019
      *                                                                                       CRE019
      ** Validate prompt fields                                                               CRE019
      *                                                                                       CRE019
     C                     EXSR VALPMT                                                        CRE019
      *                                                                                       CRE019
     C           *IN31     IFEQ *OFF                                                          CRE019
     C           *IN48     ANDEQ*OFF                                                          CRE019
      *                                                                                       CRE019
     C                     MOVE *OFF      *IN22                                               CRE019
     C                     MOVE *ON       *IN24                                               CRE019
      *                                                                                       CRE019
     C                     MOVE *OFF      *IN28                                               CRE019
     C           SACTN     IFEQ 'I'                                                           CRE019
     C                     MOVE *ON       *IN28                                               CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Insert of group of cheque books                                                      CRE019
      *                                                                                       CRE019
     C           WCBIT     IFGT 1                                                             CRE019
     C           SACTN     ANDEQ'I'                                                           CRE019
     C                     MOVE *ON       *IN23                                               CRE019
     C                     MOVE *ON       *IN19                                               CRE019
     C                     EXSR MULINS                                                        CRE019
     C                     MOVE *ON       *IN22                                               CRE019
     C                     MOVE *OFF      *IN23                                               CRE019
     C                     MOVE *OFF      *IN24                                               CRE019
     C                     MOVE *OFF      *IN19                                               CRE019
      *                                                                                       CRE019
     C                     ELSE                                                               CRE019
      *                                                                                       CRE019
      ** Otherwise, process a single cheque book                                              CRE019
      *                                                                                       CRE019
     C                     MOVE *ON       *IN19                                               CRE019
     C                     MOVE *OFF      *IN20                                               CRE019
     C                     MOVE *ON       *IN14                                               CRE019
     C                     EXSR DTLSCR                                                        CRE019
     C                     MOVE *OFF      *IN19                                               CRE019
     C                     MOVE *ON       *IN20                                               CRE019
     C                     MOVE *ON       *IN22                                               CRE019
     C                     MOVE *OFF      *IN14                                               CRE019
     C                     MOVE *OFF      *IN24                                               CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDSL                                                              CRE019
     C                     ENDDO                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      * CQLIST - Main cheque book list subfile processing                                     CRE019
      *****************************************************************                       CRE019
     C           CQLIST    BEGSR                                                              CRE019
      *                                                                                       CRE019
      ** Initialise selection fields and build subfile                                        CRE019
      *                                                                                       CRE019
     C                     MOVE *BLANKS   CSFLDS                                              CRE019
     C                     MOVE 'N'       SLNYIS                                              CRE019
     C                     MOVE 'N'       SLNYCL                                              CRE019
     C                     MOVE CSFLDS    CSFBAK                                              CRE019
     C                     EXSR BLDSFL                                                        CRE019
      *                                                                                       CRE019
     C           *INKC     DOWEQ*OFF                                                          CRE019
     C           *INKI     ANDEQ*OFF                                                          CRE019
     C           *INKL     ANDEQ*OFF                                                          CRE019
     C                     EXSR PRCSFL                                                        CRE019
     C                     ENDDO                                                              CRE019
      *                                                                                       CRE019
     C           *INKL     IFEQ *ON                                                           CRE019
     C                     MOVE *ON       *IN28                                               CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C           *INKI     IFEQ *ON                                                           CRE019
     C                     MOVE 'I'       SACTN                                               CRE019
     C                     MOVE *ON       *IN24                                               CRE019
     C                     MOVE *ON       *IN28                                               CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      * BLDSFL - Build cheque book list subfile                                               CRE019
      *****************************************************************                       CRE019
     C           BLDSFL    BEGSR                                                              CRE019
      *                                                                                       CRE019
      ** Clear appropriate select fields if F5 is pressed                                     CRE019
      *                                                                                       CRE019
     C           *INKE     IFEQ *ON                                                           CRE019
     C                     MOVE *BLANKS   CSFLDS                                              CRE019
     C                     MOVE 'N'       SLNYIS                                              CRE019
     C                     MOVE 'N'       SLNYCL                                              CRE019
     C                     MOVE CSFLDS    CSFBAK                                              CRE019
     C                     MOVE *OFF      *IN75                                              239709A
     C                     MOVE *OFF      *IN76                                              239709A
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C**********           MOVE *OFF      *IN26                                       CRE019 239709A
     C**********           MOVE *OFF      *IN79                                       CRE019 239709A
     C**********           MOVE *OFF      *IN80                                       CRE019 239709A
     C**********           MOVE *ON       *IN81                                       CRE019 239709A
     C**********           WRITERE4212C1                                              CRE019 239709A
      *                                                                                       CRE019
      ** Read a cheque book records                                                           CRE019
      *                                                                                       CRE019
     C           *IN38     IFNE *ON                                                          239709A
     C           *IN39     ANDNE*ON                                                          239709A
      *                                                                                      239709A
      ** Clear Temporary File                                                                239709A
      *                                                                                      239709A
     C           *LOVAL    SETLLSUBF1                                                        239709A
     C                     READ SUBF1                    70                                  239709A
     C           *IN70     DOWEQ*OFF                                                         239709A
     C                     DELETSUBF1                                                        239709A
     C                     READ SUBF1                    70                                  239709A
     C                     ENDDO                                                             239709A
      *                                                                                      239709A
     C                     Z-ADD0         @FPTR1                                             239709A
     C                     Z-ADD0         #0SFRN                                             239709A
     C                     Z-ADD1         @RRN1                                              239709A
     C                     MOVE *OFF      *IN75                                              239709A
     C                     MOVE *OFF      *IN76                                              239709A
     C                     MOVE *OFF      *IN71                                               CRE019
     C********** *LOVAL    SETLLCHQBKPDF                                              CRE019 BUG3025
     C**********           READ CHQBKPDF                 71                           CRE019 BUG3025
     C           *LOVAL    SETLLCHQBKPD3                                                     BUG3025
     C                     READ CHQBKPD3                 71                                  BUG3025
      *                                                                                       CRE019
      ** If end of file is reached, no records is displayed                                   CRE019
      *                                                                                       CRE019
     C********** *IN71     IFEQ *ON                                                   CRE019 239709A
     C           *IN71     IFEQ *OFF                                                         239709A
     C**********           MOVE *BLANKS   CSFLDS                                      CRE019 239709A
     C**********           MOVEL'USR0178' @MSGID                                      CRE019 239709A
     C**********           EXSR SNDMSG                                                CRE019 239709A
      **********                                                                      CRE019 239709A
     C**********           MOVE *OFF      *IN79                                       CRE019 239709A
     C**********           MOVE *ON       *IN80                                       CRE019 239709A
     C**********           MOVE *OFF      *IN81                                       CRE019 239709A
     C**********           ELSE                                                       CRE019 239709A
      *                                                                                       CRE019
      ** Initialise subfile relative record number and subfile                                CRE019
      ** control indicators.                                                                  CRE019
      *                                                                                       CRE019
     C**********           MOVE *ON       *IN79                                       CRE019 239709A
     C**********           MOVE *ON       *IN80                                       CRE019 239709A
     C**********           MOVE *ON       *IN81                                       CRE019 239709A
     C**********           Z-ADD1         SFRLRN  40                                  CRE019 239709A
     C**********           Z-ADD*ZERO     #0SFRN                                      CRE019 239709A
      *                                                                                       CRE019
      ** For each record read, filter according to selection                                  CRE019
      ** and write to subfile until all records are read.                                     CRE019
      *                                                                                       CRE019
     C           *IN71     DOWEQ*OFF                                                          CRE019
      *                                                                                       CRE019
      ** Store cheque book's transaction number of last update                                CRE019
      *                                                                                       CRE019
     C                     Z-ADDTNLU      WTNLU                                               CRE019
      *                                                                                       CRE019
      ** Retrieve account details of cheque book being read.                                  CRE019
      *                                                                                       CRE019
     C           ACNO      CHAINACNUM                70                                       CRE019
     C           *IN70     IFEQ *OFF                                                          CRE019
     C                     MOVE CNUM      WCCNUM  6                                           CRE019
     C                     MOVE ABRCA     WBRCA   3                                           CRE019
     C                     MOVE CCY       WCCY    3                                           CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE *BLANK    WCCNUM                                              CRE019
     C                     MOVE *BLANKS   WBRCA                                               CRE019
     C                     MOVE *BLANKS   WCCY                                                CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     EXSR FILTER                                                        CRE019
      *                                                                                       CRE019
     C           WFFLAG    IFEQ 'Y'                                                           CRE019
      *                                                                                       CRE019
      ** Format transaction fields for output                                                 CRE019
      *                                                                                       CRE019
     C                     EXSR FMTDTL                                                        CRE019
      *                                                                                       CRE019
      ** Write details to the subfile                                                         CRE019
      *                                                                                       CRE019
     C                     Z-ADD1         #0SFRN                                              CRE019
     C**********           WRITERE4212S1                                              CRE019 239709A
     C                     WRITESUBF1F                                                       239709A
     C**********           ADD  1         SFRLRN                                      CRE019 239709A
     C                     ADD  1         @RRN1                                              239709A
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Read next cheque book record                                                         CRE019
      *                                                                                       CRE019
     C**********           READ CHQBKPDF                 71                           CRE019 BUG3025
     C                     READ CHQBKPD3                 71                                  BUG3025
      *                                                                                       CRE019
     C                     ENDDO                                                              CRE019
     C                     ENDIF                                                             239709A
     C                     ENDIF                                                             239709A
      *                                                                                       CRE019
     C                     MOVE *OFF      *IN26                                              239709A
     C                     MOVE *OFF      *IN79                                              239709A
     C                     MOVE *OFF      *IN80                                              239709A
     C                     MOVE *ON       *IN81                                              239709A
     C                     WRITERE4212C1                                                     239709A
     C                     MOVE '1'       *IN79                                              239709A
     C                     MOVE '1'       *IN80                                              239709A
     C                     MOVE '1'       *IN81                                              239709A
      *                                                                                      239709A
     C                     Z-ADD0         SFRLRN  40                                         239709A
     C           @FPTR1    SETLLSUBF1F                                                       239709A
     C                     READ SUBF1F                   70                                  239709A
     C           SFRLRN    DOWLT9                                                            239709A
     C           *IN70     ANDEQ'0'                                                          239709A
      *                                                                                      239709A
     C                     ADD  1         SFRLRN                                             239709A
     C                     WRITERE4212S1                                                     239709A
     C                     READ SUBF1F                   70                                  239709A
      *                                                                                      239709A
     C                     ENDDO                                                             239709A
      *                                                                                      239709A
     C           #0SFRN    IFEQ *ZERO                                                         CRE019
     C           SFRLRN    OREQ *ZERO                                                        239709A
      *                                                                                       CRE019
      ** If no valid record found                                                             CRE019
      *                                                                                       CRE019
     C                     MOVEL'USR0178' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
     C                     MOVE *OFF      *IN79                                              239709A
     C                     MOVE *ON       *IN80                                              239709A
     C                     MOVE *OFF      *IN81                                              239709A
     C                     WRITERE4212C1                                                     239709A
     C                     MOVE *BLANKS   SFLINE                                              CRE019
     C                     MOVE *ON       *IN26                                               CRE019
     C                     MOVE *ON       *IN75                                              239709A
     C                     MOVE *ON       *IN76                                              239709A
     C                     Z-ADD1         #0SFRN                                              CRE019
     C**********           WRITERE4212S1                                              CRE019 239709A
     C                     WRITERE4212ND                                                     239709A
     C                     ENDIF                                                              CRE019
      **********                                                                      CRE019 239709A
     C**********           ENDIF                                                      CRE019 239709A
      *                                                                                       CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      * PRCSFL - Process cheque book list subfile                                             CRE019
      *****************************************************************                       CRE019
     C           PRCSFL    BEGSR                                                              CRE019
      *                                                                                       CRE019
      ** Display screen header and footer                                                     CRE019
      *                                                                                       CRE019
     C                     WRITERE4212C0                                                     BUG2965
     C                     WRITERE4212F0                                                      CRE019
     C                     WRITERE4212F2                                                      CRE019
      *                                                                                       CRE019
      ** Display screen subfile                                                               CRE019
      *                                                                                       CRE019
     C**********           WRITERE4212C0                                              CRE019 BUG3023
     C                     EXFMTRE4212C1                                                      CRE019
      *                                                                                       CRE019
      ** Clear program message queue and initialise error indicators                          CRE019
      *                                                                                       CRE019
     C                     CALL 'SCC0250'                                                     CRE019
     C                     MOVE *OFF      *IN54                                               CRE019
     C                     MOVE *OFF      *IN55                                               CRE019
     C                     MOVE *OFF      *IN56                                               CRE019
     C                     MOVE *OFF      *IN57                                               CRE019
     C                     MOVE *OFF      *IN58                                               CRE019
     C                     MOVE *OFF      *IN59                                               CRE019
     C                     MOVE *OFF      *IN75                                              239709A
     C                     MOVE *OFF      *IN76                                              239709A
      *                                                                                       CRE019
      ** Select processing type                                                               CRE019
      *                                                                                       CRE019
     C                     SELEC                                                              CRE019
      *                                                                                       CRE019
      ** F3 Exit or F12 Cancel                                                                CRE019
      *                                                                                       CRE019
     C           *INKC     WHEQ *ON                                                           CRE019
     C           *INKL     OREQ *ON                                                           CRE019
     C                     EXSR RFRESH                                                        CRE019
     C                     MOVE '001'     SCBIT                                               CRE019
     C                     MOVE *BLANK    SACTN                                               CRE019
      *                                                                                       CRE019
      ** F5 Refresh                                                                           CRE019
      *                                                                                       CRE019
     C           *INKE     WHEQ *ON                                                           CRE019
     C                     EXSR BLDSFL                                                        CRE019
      *                                                                                      239709A
      ** ROLLUP                                                                              239709A
      *                                                                                      239709A
     C           *IN38     WHEQ *ON                                                          239709A
     C           *IN70     IFEQ '0'                                                          239709A
     C                     ADD  10        @FPTR1                                             239709A
     C                     EXSR BLDSFL                                                       239709A
     C                     ELSE                                                              239709A
     C                     SUB  10        @FPTR1                                             239709A
     C                     SETON                     75                                      239709A
     C                     MOVE '1'       *IN81                                              239709A
     C                     ENDIF                                                             239709A
      *                                                                                      239709A
      ** ROLLDOWN                                                                            239709A
      *                                                                                      239709A
     C           *IN39     WHEQ *ON                                                          239709A
     C                     SELEC                                                             239709A
     C           @FPTR1    WHGT 0                                                            239709A
     C                     SUB  10        @FPTR1                                             239709A
     C                     SETOF                     76                                      239709A
     C                     EXSR BLDSFL                                                       239709A
     C           @FPTR1    WHLT 0                                                            239709A
     C                     ADD  10        @FPTR1                                             239709A
     C                     SETON                     76                                      239709A
     C                     EXSR BLDSFL                                                       239709A
     C           @FPTR1    WHEQ 0                                                            239709A
     C                     ADD  1         @FPTR1                                             239709A
     C                     SETON                     76                                      239709A
     C                     EXSR BLDSFL                                                       239709A
     C                     ENDSL                                                             239709A
      *                                                                                       CRE019
      ** Selection criteria has changed                                                       CRE019
      *                                                                                       CRE019
     C           CSFLDS    WHNE CSFBAK                                                        CRE019
     C           WERCTL    OREQ 'Y'                                                           CRE019
     C                     EXSR VALCTL                                                        CRE019
     C                     MOVE CSFLDS    CSFBAK                                              CRE019
     C                     EXSR BLDSFL                                                        CRE019
      *                                                                                       CRE019
      ** Validate and process user selection input                                            CRE019
      *                                                                                       CRE019
     C                     OTHER                                                              CRE019
     C                     EXSR VALSEL                                                        CRE019
     C                     MOVEL'N'       WNOCHG  1                                           CRE019
     C           WERSEL    IFEQ 'N'                                                           CRE019
     C                     EXSR PRCSEL                                                        CRE019
     C                     Z-ADD1         #0SFRN                                              CRE019
     C                     ELSE                                                               CRE019
     C                     Z-ADDWSFRN     SFRLRN                                              CRE019
     C                     Z-ADDWSFRN     #0SFRN                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Rebuild subfile after processing all selected records.                               CRE019
      *                                                                                       CRE019
     C           WNOCHG    IFEQ 'Y'                                                           CRE019
     C                     EXSR BLDSFL                                                        CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSL                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      * MULINS - Process multiple inserts                                                     CRE019
      *****************************************************************                       CRE019
     C           MULINS    BEGSR                                                              CRE019
      *                                                                                       CRE019
     C                     Z-ADD1         WCNT    30                                          CRE019
     C                     MOVE WCBIT     SCBIT2                                              CRE019
      *                                                                                       CRE019
     C           WCNT      DOWLEWCBIT                                                         CRE019
     C           *INKL     ANDEQ*OFF                                                          CRE019
     C                     MOVE 'I'       SACTN                                               CRE019
     C                     MOVE WCNT      SCBIT                                               CRE019
     C                     EXSR DTLSCR                                                        CRE019
     C                     ADD  1         WCNT                                                CRE019
     C                     ENDDO                                                              CRE019
      *                                                                                       CRE019
      ***If*input*is*cancelled,*rollback*all*updates*******************               CRE019 BUG3303
      ***else*commit*the*group*of*cheques*inserted.********************               CRE019 BUG3303
      *****************************************************************               CRE019 BUG3303
     C********** *INKL     IFEQ *ON                                                   CRE019 BUG3303
     C**********           ROLBK                                                      CRE019 BUG3303
     C**********           MOVE *ON       *IN28                                       CRE019 BUG3303
     C**********           ELSE                                                       CRE019 BUG3303
     C********** CMTTXT    COMIT                                                      CRE019 BUG3303
     C**********           ENDIF                                                      CRE019 BUG3303
      *                                                                                       CRE019
     C                     MOVE '001'     SCBIT                                               CRE019
     C                     MOVE *BLANK    SCBIT2                                              CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      * VALPMT - Validate prompt fields                                                       CRE019
      *****************************************************************                       CRE019
     C           VALPMT    BEGSR                                                              CRE019
      *                                                                                       CRE019
      ** Validate action code. Must be I, A, C, E or D.                                       CRE019
      *                                                                                       CRE019
     C                     MOVE *OFF      *IN31                                               CRE019
     C           SACTN     IFEQ *BLANK                                                        CRE019
     C           SACTN     ORNE 'I'                                                           CRE019
     C           SACTN     ANDNE'A'                                                           CRE019
     C           SACTN     ANDNE'C'                                                           CRE019
     C           SACTN     ANDNE'E'                                                           CRE019
     C           SACTN     ANDNE'D'                                                           CRE019
     C                     MOVE *ON       *IN31                                               CRE019
     C                     MOVEL'USR1243' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Validate cheques books issued together if action                                     CRE019
      ** code is 'I'. Should be numeric.                                                      CRE019
      *                                                                                       CRE019
     C                     Z-ADD*ZERO     WCBIT   30                                          CRE019
     C                     MOVE *OFF      *IN48                                               CRE019
     C           *IN31     IFEQ *OFF                                                          CRE019
      *                                                                                       CRE019
     C                     Z-ADD*ZERO     ZADEC                                               CRE019
     C                     Z-ADD3         ZADIG                                               CRE019
     C                     MOVEL*BLANK    ZFIELD                                              CRE019
     C                     MOVE SCBIT     ZFIELD                                              CRE019
     C                     EXSR ZALIGN                                                        CRE019
      *                                                                                       CRE019
      ** If error found?  Set up error message                                                CRE019
      *                                                                                       CRE019
     C           *IN99     IFEQ *ON                                                           CRE019
     C           ZFIELD    OREQ *ZERO                                                         CRE019
     C                     MOVE *ON       *IN48                                               CRE019
     C                     MOVE 'USR1244' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE ZFIELD    SCBIT                                               CRE019
     C                     MOVE ZFIELD    WCBIT                                               CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Eror if issued together field is greater than 1 and action code                      CRE019
      ** is not 'I'                                                                           CRE019
      *                                                                                       CRE019
     C           WCBIT     IFGT 1                                                             CRE019
     C           SACTN     ANDNE'I'                                                           CRE019
     C           *IN48     ANDEQ*OFF                                                          CRE019
     C                     MOVE *ON       *IN48                                               CRE019
     C                     MOVEL'USR1240' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      * VALCTL - Validate subfile control fields                                              CRE019
      *****************************************************************                       CRE019
     C           VALCTL    BEGSR                                                              CRE019
      *                                                                                       CRE019
      ** Initialise error flag                                                                CRE019
      *                                                                                       CRE019
     C                     MOVE 'N'       WERCTL  1                                           CRE019
      *                                                                                       CRE019
      ** Validate branch                                                                      CRE019
      *                                                                                       CRE019
     C           SLBRCA    IFNE *BLANK                                                        CRE019
     C                     CALL 'AOBRCHR0'                                                    CRE019
     C                     PARM *BLANKS   @RTCD                                               CRE019
     C                     PARM '*KEY    '@OPTN                                               CRE019
     C                     PARM           SLBRCA                                              CRE019
     C           SDBRCH    PARM SDBRCH    DSFDY                                               CRE019
      *                                                                                       CRE019
     C           @RTCD     IFNE *BLANK                                                        CRE019
     C           @RTCD     ANDNE'*NOSEL '                                                     CRE019
     C                     MOVE 'Y'       WERCTL                                              CRE019
     C                     MOVE *ON       *IN55                                               CRE019
     C                     MOVEL'USR0095' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
     C                     ELSE                                                               CRE019
     C           @RTCD     IFNE '*NOSEL '                                                     CRE019
     C                     MOVE A8BRCD    SLBRCA                                              CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE *BLANK    SLBRCA                                              CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Validate customer number                                                             CRE019
      *                                                                                       CRE019
     C           SLCNUM    IFNE *BLANK                                                        CRE019
     C                     CALL 'AOCUSTR0'                                                    CRE019
     C                     PARM *BLANKS   @RTCD                                               CRE019
     C                     PARM '*KEY'    @OPTN                                               CRE019
     C                     PARM           SLCNUM                                              CRE019
     C                     PARM *BLANKS   @KSTAT  7                                           CRE019
     C           SDCUST    PARM SDCUST    DSSDY                                               CRE019
      *                                                                                       CRE019
     C           @RTCD     IFNE *BLANKS                                                       CRE019
     C           @RTCD     ANDNE'*NOSEL '                                                     CRE019
     C                     MOVE 'Y'       WERCTL                                              CRE019
     C                     MOVE *ON       *IN56                                               CRE019
     C                     MOVEL'USR1025' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE *BLANK    SLCNUM                                              CRE019
     C           @RTCD     IFNE '*NOSEL '                                                     CRE019
     C                     MOVE BBCUST    SLCNUM                                              CRE019
     C                     MOVE BBCUST    WKCNUM  6                                           CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE *BLANKS   SLCNUM                                              CRE019
     C                     MOVE *BLANKS   WKCNUM                                              CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDIF                                                              CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE *BLANKS   WKCNUM                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Validate issue dates                                                                 CRE019
      *                                                                                       CRE019
     C                     Z-ADD*ZERO     WKISFR  50                                          CRE019
     C           SLISFR    IFNE *BLANK                                                        CRE019
     C                     MOVE SLISFR    ZDATE                                               CRE019
     C                     EXSR ZDATE1                                                        CRE019
     C           *IN99     IFEQ *ON                                                           CRE019
     C                     MOVE 'Y'       WERCTL                                              CRE019
     C                     MOVE *ON       *IN57                                               CRE019
     C                     MOVEL'USR0009' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE ZDAYNO    WKISFR                                              CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     Z-ADD99999     WKISUN  50                                          CRE019
     C           SLISUN    IFNE *BLANK                                                        CRE019
     C                     MOVE SLISUN    ZDATE                                               CRE019
     C                     EXSR ZDATE1                                                        CRE019
     C           *IN99     IFEQ *ON                                                           CRE019
     C                     MOVE 'Y'       WERCTL                                              CRE019
     C                     MOVE *ON       *IN58                                               CRE019
     C                     MOVEL'USR0009' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE ZDAYNO    WKISUN                                              CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Validate not yet issued indicator                                                    CRE019
      *                                                                                       CRE019
     C           SLNYIS    IFEQ 'Y'                                                           CRE019
     C           *IN57     ANDEQ*OFF                                                          CRE019
     C           *IN58     ANDEQ*OFF                                                          CRE019
     C           SLISFR    IFNE *BLANK                                                        CRE019
     C           SLISUN    ORNE *BLANK                                                        CRE019
     C                     MOVE 'Y'       WERCTL                                              CRE019
     C                     MOVE *ON       *IN57                                               CRE019
     C                     MOVE *ON       *IN58                                               CRE019
     C                     MOVE *ON       *IN59                                               CRE019
     C                     MOVEL'USR1245' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
     C                     ENDIF                                                              CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE 'N'       SLNYIS                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Set not yet collected indicator flag to 'N' as default                               CRE019
      *                                                                                       CRE019
     C           SLNYCL    IFNE 'Y'                                                           CRE019
     C                     MOVE 'N'       SLNYCL                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      * VALSEL - Validate action codes in subfile selection fields                            CRE019
      *****************************************************************                       CRE019
     C           VALSEL    BEGSR                                                              CRE019
      *                                                                                       CRE019
     C                     MOVE 'N'       WERSEL  1                                           CRE019
     C                     MOVEL'Y'       W1STER  1                                           CRE019
     C                     MOVE *OFF      *IN54                                               CRE019
      *                                                                                       CRE019
      ** Read subfile that has changed                                                        CRE019
      *                                                                                       CRE019
     C                     READCRE4212S1                 71                                   CRE019
      *                                                                                       CRE019
     C           *IN71     DOWEQ*OFF                                                          CRE019
      *                                                                                       CRE019
      ** Valid action codes: 'A', 'C', 'E', 'D'                                               CRE019
      *                                                                                       CRE019
     C           SFSEL     IFNE *BLANK                                                        CRE019
     C           SFSEL     ANDNE'A'                                                           CRE019
     C           SFSEL     ANDNE'C'                                                           CRE019
     C           SFSEL     ANDNE'E'                                                           CRE019
     C           SFSEL     ANDNE'D'                                                           CRE019
     C                     MOVE *ON       *IN54                                               CRE019
     C                     MOVE 'Y'       WERSEL                                              CRE019
     C           W1STER    IFEQ 'Y'                                                           CRE019
     C                     Z-ADDSFRLRN    WSFRN   50                                          CRE019
     C                     MOVEL'N'       W1STER                                              CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     MOVE *ON       *IN84                                               CRE019
     C                     UPDATRE4212S1                                                      CRE019
     C                     MOVE *OFF      *IN84                                               CRE019
     C                     MOVE *OFF      *IN54                                               CRE019
      *                                                                                       CRE019
      ** Read subfile again                                                                   CRE019
      *                                                                                       CRE019
     C                     READCRE4212S1                 71                                   CRE019
     C                     ENDDO                                                              CRE019
      *                                                                                       CRE019
      ** Send error message                                                                   CRE019
      *                                                                                       CRE019
     C           WERSEL    IFEQ 'Y'                                                           CRE019
     C                     MOVEL'USR1247' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      * PRCSEL - Process action codes on changed subfile records                              CRE019
      *****************************************************************                       CRE019
     C           PRCSEL    BEGSR                                                              CRE019
      *                                                                                       CRE019
     C                     MOVE 'N'       WINKL   1                                           CRE019
      *                                                                                       CRE019
      ** Read subfile that has changed                                                        CRE019
      *                                                                                       CRE019
     C                     READCRE4212S1                 71                                   CRE019
      *                                                                                       CRE019
     C           *IN71     DOWEQ*OFF                                                          CRE019
     C           *INKC     ANDEQ*OFF                                                          CRE019
     C           WINKL     ANDEQ'N'                                                           CRE019
      *                                                                                       CRE019
     C                     MOVE *OFF      *INKJ                                               CRE019
     C           SFSEL     IFNE *BLANK                                                        CRE019
      *                                                                                       CRE019
      ** Move from subfile field values to detail screen                                      CRE019
      *                                                                                       CRE019
     C                     EXSR RFRESH                                                        CRE019
     C                     MOVE SFSEL     SACTN                                               CRE019
     C                     MOVE SFACNO    SRACNO                                              CRE019
     C                     MOVE SFGCIN    SGCIN                                               CRE019
     C                     MOVE SFSCQN    SSCQN                                               CRE019
     C                     MOVE SFECQN    SECQN                                               CRE019
     C                     MOVE SFNOCQ    SNOCQ                                               CRE019
     C                     MOVE SFISDT    SISSDT                                              CRE019
     C                     MOVE SFCLDT    SCLTDT                                              CRE019
     C                     MOVE SFCHAM    SCHAM                                               CRE019
     C                     MOVE SFCHAC    SCHAC                                               CRE019
     C                     MOVE SFCHTT    SCHTT                                               CRE019
     C                     MOVE SFTNLU    @TNLU                                               CRE019
     C                     MOVELSFCSSN    SCCSSN                                              CSF011
     C                     MOVELSFREPN    SCREPN                                              CSF011
     C                     MOVELSFREPT    SCREPT                                              CSF011
     C                     MOVELSFACCN    SCACCN                                              CSF011
      *                                                                                       CRE019
      ** Set appropriate field protection depending on action code                            CRE019
      *                                                                                       CRE019
     C                     MOVE *ON       *IN11                                               CRE019
     C                     MOVE *ON       *IN12                                               CRE019
     C                     MOVE *OFF      *IN13                                               CRE019
     C                     MOVE *OFF      *IN18                                               CRE019
     C                     MOVE *OFF      *IN28                                               CRE019
      *                                                                                       CRE019
     C                     MOVE *OFF      *IN27                                               CRE019
     C           SFOEDT    IFEQ BJRDNB                                                        CRE019
     C           SACTN     ANDEQ'A'                                                           CRE019
     C           SACTN     OREQ 'I'                                                           CRE019
     C                     MOVE *ON       *IN27                                               CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     MOVE 'N'       WPRMPT                                              CRE019
      *                                                                                       CRE019
     C           *INKC     DOWEQ*OFF                                                          CRE019
     C           *INKL     ANDEQ*OFF                                                          CRE019
     C                     MOVE *ON       *IN25                                               CRE019
     C                     SELEC                                                              CRE019
      *                                                                                       CRE019
      ** Amendment processing                                                                 CRE019
      *                                                                                       CRE019
     C           SACTN     WHEQ 'A'                                                           CRE019
     C                     MOVE *ON       *IN13                                               CRE019
     C                     MOVE *OFF      *IN25                                               CRE019
     C                     MOVE SSCQN     @SCQN                                               CRE019
     C                     EXSR AMDPR                                                         CRE019
      *                                                                                       CRE019
      ** Deletion  processing                                                                 CRE019
      *                                                                                       CRE019
     C           SACTN     WHEQ 'D'                                                           CRE019
     C                     EXSR DELPR                                                         CRE019
      *                                                                                       CRE019
      ** Cancellation processing                                                              CRE019
      *                                                                                       CRE019
     C           SACTN     WHEQ 'C'                                                           CRE019
     C                     EXSR DELPR                                                         CRE019
      *                                                                                       CRE019
      ** Enquiry   processing                                                                 CRE019
      *                                                                                       CRE019
     C           SACTN     WHEQ 'E'                                                           CRE019
     C                     EXSR ENQPR                                                         CRE019
      *                                                                                       CRE019
     C                     ENDSL                                                              CRE019
      *                                                                                       CRE019
      ** Process next subfile record after a completed transaction.                           CRE019
      *                                                                                       CRE019
     C           WPRMPT    IFEQ 'Y'                                                           CRE019
     C                     LEAVE                                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Display detail screen                                                                CRE019
      *                                                                                       CRE019
     C                     WRITERE4212C0                                                     BUG3023
     C                     WRITERE4212F0                                                      CRE019
     C                     WRITERE4212F2                                                      CRE019
     C**********           WRITERE4212C0                                              CRE019 BUG3023
     C                     EXFMTRE4212F1                                                      CRE019
      *                                                                                       CRE019
      ** Refresh screen if necessary                                                          CRE019
      *                                                                                       CRE019
     C           *INKE     IFEQ *ON                                                           CRE019
     C           SACTN     ANDEQ'A'                                                           CRE019
     C                     MOVE *OFF      *IN44                                               CRE019
     C                     MOVE *OFF      *IN45                                               CRE019
     C                     MOVE *OFF      *IN46                                               CRE019
     C                     MOVE *OFF      *IN47                                               CRE019
     C                     MOVE *OFF      *IN52                                               CRE019
     C                     MOVE *BLANKS   SCLTDT                                              CRE019
     C           *IN27     IFEQ *ON                                                           CRE019
     C                     MOVE *BLANKS   SCHAM                                               CRE019
     C                     MOVE *BLANKS   SCHAC                                               CRE019
     C                     MOVE *BLANKS   SCHTT                                               CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Clear program message queue                                                          CRE019
      *                                                                                       CRE019
     C                     CALL 'SCC0250'                                                     CRE019
     C                     ENDDO                                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     MOVE *OFF      *IN09                                               CRE019
     C                     MOVE *ON       *IN18                                               CRE019
     C                     MOVE *OFF      *IN25                                               CRE019
     C                     MOVEL*BLANKS   SFSEL                                               CRE019
     C                     MOVE *ON       *IN84                                               CRE019
     C                     UPDATRE4212S1                                                      CRE019
     C                     MOVE *OFF      *IN84                                               CRE019
      *                                                                                       CRE019
      ** If F12, go back to subfile screen                                                    CRE019
      *                                                                                       CRE019
     C           *INKL     IFEQ *ON                                                           CRE019
     C                     EXSR RFRESH                                                        CRE019
     C                     MOVE *OFF      *INKL                                               CRE019
     C                     MOVE 'Y'       WINKL                                               CRE019
      *                                                                                       CRE019
     C                     ELSE                                                               CRE019
      *                                                                                       CRE019
      ** Read subfile again                                                                   CRE019
      *                                                                                       CRE019
     C                     READCRE4212S1                 71                                   CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDDO                                                              CRE019
      *                                                                                       CRE019
      ** If no more records to process                                                        CRE019
      *                                                                                       CRE019
     C           *IN71     IFEQ *ON                                                           CRE019
     C                     MOVEL'Y'       WNOCHG                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      * FILTER - Check if CHQBKPD record read meets selection criteria                        CRE019
      *****************************************************************                       CRE019
     C           FILTER    BEGSR                                                              CRE019
      *                                                                                       CRE019
     C                     MOVE 'Y'       WFFLAG  1                                           CRE019
      *                                                                                       CRE019
      ** Record ID                                                                            CRE019
     C           RECID     IFEQ '*'                                                           CRE019
     C                     MOVE 'N'       WFFLAG                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Branch                                                                               CRE019
     C           SLBRCA    IFNE BRCA                                                          CRE019
     C           SLBRCA    ANDNE*BLANK                                                        CRE019
     C                     MOVE 'N'       WFFLAG                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Customer Number                                                                      CRE019
     C           WKCNUM    IFNE WCCNUM                                                        CRE019
     C           WKCNUM    ANDNE*BLANK                                                        CRE019
     C                     MOVE 'N'       WFFLAG                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     SELEC                                                              CRE019
      *                                                                                       CRE019
      ** Issued dates between from - until range                                              CRE019
     C           SLISFR    WHNE *BLANK                                                        CRE019
     C           SLISUN    ANDNE*BLANK                                                        CRE019
     C           DISS      IFLT WKISFR                                                        CRE019
     C           DISS      ORGT WKISUN                                                        CRE019
     C                     MOVE 'N'       WFFLAG                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Issued dates from                                                                    CRE019
     C           SLISFR    WHNE *BLANK                                                        CRE019
     C           SLISUN    ANDEQ*BLANK                                                        CRE019
     C           DISS      IFLT WKISFR                                                        CRE019
     C                     MOVE 'N'       WFFLAG                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Issued dates until                                                                   CRE019
     C           SLISFR    WHEQ *BLANK                                                        CRE019
     C           SLISUN    ANDNE*BLANK                                                        CRE019
     C           DISS      IFGT WKISUN                                                        CRE019
     C                     MOVE 'N'       WFFLAG                                              CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDSL                                                              CRE019
      *                                                                                       CRE019
      ** Not yet issued                                                                       CRE019
     C           SLNYIS    IFEQ 'Y'                                                           CRE019
     C           SLISFR    IFNE *BLANK                                                        CRE019
     C           SLISUN    ORNE *BLANK                                                        CRE019
     C           DISS      ORNE *ZERO                                                         CRE019
     C                     MOVE 'N'       WFFLAG                                              CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Not yet collected                                                                    CRE019
     C           SLNYCL    IFEQ 'Y'                                                           CRE019
     C           DCOL      ANDNE*ZERO                                                         CRE019
     C                     MOVE 'N'       WFFLAG                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Retail Number                                                                        CRE019
     C           SLACNO    IFNE *BLANK                                                        CRE019
     C                     MOVE SLACNO    WKACNO 100                                          CRE019
     C           WKACNO    IFNE ACNO                                                          CRE019
     C                     MOVE 'N'       WFFLAG                                              CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Start/End Cheque                                                                     CRE019
     C           SLCQNO    IFNE *BLANK                                                        CRE019
     C                     MOVE SLCQNO    WKCQNO  80                                          CRE019
     C           WKCQNO    IFLT SCQN                                                          CRE019
     C           WKCQNO    ORGT ECQN                                                          CRE019
     C                     MOVE 'N'       WFFLAG                                              CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      * FMTDTL - Format cheque book details for subfile display                               CRE019
      *****************************************************************                       CRE019
     C           FMTDTL    BEGSR                                                              CRE019
      *                                                                                       CRE019
     C                     MOVE *BLANKS   SFLINE                                              CRE019
     C                     MOVE *BLANKS   WKFLD9  9                                           CRE019
     C                     MOVE *BLANKS   WKFLD8  8                                           CRE019
      *                                                                                       CRE019
     C                     MOVE BRCA      SFBRCA                                              CRE019
     C                     MOVE WCCNUM    SFCNUM                                              CRE019
     C                     MOVE ACNO      SFACNO                                              CRE019
     C                     MOVELSCQN      WKFLD9                                              CRE019
     C                     MOVE '-'       WKFLD9                                              CRE019
     C                     MOVELWKFLD9    SFSECN                                              CRE019
     C                     MOVE ECQN      SFSECN                                              CRE019
     C                     Z-ADDWTNLU     SFTNLU                                              CRE019
     C                     Z-ADDOEDT      SFOEDT                                              CRE019
      *                                                                                       CSF011
      ** Get Customer name                                                                    CSF011
      *                                                                                       CSF011
     C                     MOVELANAM      SFACCN                                              CSF011
     C                     MOVELSFCNUM    @CNUM                                               CSF011
     C                     CALL 'AOCUSTR0'                                                    CSF011
     C                     PARM *BLANKS   @RTCD                                               CSF011
     C                     PARM '*KEY'    @OPTN                                               CSF011
     C                     PARM           @CNUM  10                                           CSF011
     C                     PARM *BLANKS   @KSTAT                                              CSF011
     C           SDCUST    PARM SDCUST    DSSDY                                               CSF011
     C           @RTCD     IFEQ *BLANKS                                                       CSF011
     C                     MOVELBBCSSN    SFCSSN                                              CSF011
     C                     MOVELBBCRNM    SFREPN                                              CSF011
     C                     MOVELBBCRTN    SFREPT                                              CSF011
     C                     ENDIF                                                              CSF011
      *                                                                                       CRE019
     C           CHAC      IFNE *ZERO                                                         CRE019
     C                     MOVE CHAC      SFCHAC                                              CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE *BLANKS   SFCHAC                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     MOVE GCQI      SFGCIN                                              CRE019
     C           GCQI      IFEQ *ZERO                                                        BUG2964
     C                     MOVEL'CHQ '    SFGORC                                             BUG2964
     C                     ELSE                                                              BUG2964
     C                     MOVEL'GIRO'    SFGORC                                             BUG2964
     C                     ENDIF                                                             BUG2964
     C                     MOVE NOCQ      SFNOCQ                                              CRE019
     C           CHTT      IFNE *ZERO                                                         CRE019
     C                     MOVE CHTT      SFCHTT                                              CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE *BLANKS   SFCHTT                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C           DISS      IFNE *ZERO                                                         CRE019
     C           DCOL      ANDNE*ZERO                                                         CRE019
     C                     MOVEL'/'       WKFLD8                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C           DISS      IFNE *ZERO                                                         CRE019
     C                     MOVELDISS      ZDAYNO                                              CRE019
     C                     EXSR ZDATE2                                                        CRE019
     C                     MOVELZADATE    SFDATE                                              CRE019
     C                     MOVE ZDATE     SFISDT                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C           DCOL      IFNE *ZERO                                                         CRE019
     C                     MOVELDCOL      ZDAYNO                                              CRE019
     C                     EXSR ZDATE2                                                        CRE019
     C                     MOVE ZADATE    WKFLD8                                              CRE019
     C                     MOVE ZDATE     SFCLDT                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     MOVE WKFLD8    SFDATE                                              CRE019
      *                                                                                       CRE019
      ** if charge account is not blanks, use charge account's currency                       CRE019
      ** else, use retail account's currency.                                                 CRE019
      *                                                                                       CRE019
     C           CHAC      IFNE *ZERO                                                         CRE019
     C           CHAC      CHAINACNUM                70                                       CRE019
     C                     MOVELCCY       WCCY                                                CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Get currency details                                                                 CRE019
      *                                                                                       CRE019
     C                     MOVELWCCY      PCCY                                                CRE019
     C                     EXSR SRCURR                                                        CRE019
      *                                                                                       CRE019
      ** Format charge amount                                                                 CRE019
      *                                                                                       CRE019
     C                     MOVE *BLANKS   ZFLD15                                              CRE019
     C           CHAM      IFNE *ZERO                                                         CRE019
     C                     MOVE CHAM      ZFLD15                                              CRE019
     C                     MOVE A6NBDP    ZDECS                                               CRE019
     C                     MOVE 'L'       ZECODE                                              CRE019
     C                     EXSR ZSEDIT                                                        CRE019
     C                     MOVELZOUT21    WRK20                                               CRE019
     C                     MOVE WRK20     SFCHAM                                              CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE *BLANKS   SFCHAM                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      *                                                               *                       CRE019
      * SRCURR - Get Currency Details                                 *                       CRE019
      *                                                               *                       CRE019
      *****************************************************************                       CRE019
     C           SRCURR    BEGSR                                                              CRE019
      *                                                                                       CRE019
     C                     CALL 'AOCURRR0'                                                    CRE019
     C                     PARM *BLANKS   PRTCD                                               CRE019
     C                     PARM '*KEY   ' POPTN                                               CRE019
     C                     PARM           PCCY    3                                           CRE019
     C           SDCURR    PARM SDCURR    DSSDY                                               CRE019
      *                                                                                       CRE019
     C           PRTCD     IFNE *BLANKS                                                       CRE019
     C           *LOCK     IN   LDA                                                           CRE019
     C                     MOVEL'SDCURRPD'DBFILE                                              CRE019
     C                     Z-ADD2         DBASE                                               CRE019
     C                     MOVELPCCY      DBKEY                                               CRE019
     C                     EXSR DBERR                                                         CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      *                                                               *                       CRE019
      * ZSEDIT - Convert amount from file format to screen format     *                       CRE019
      *                                                               *                       CRE019
      *****************************************************************                       CRE019
     C           ZSEDIT    BEGSR                                                              CRE019
      *                                                                                       CRE019
     C                     CALL 'ZSEDIT'                                                      CRE019
     C                     PARM           ZFLD15 150                                          CRE019
     C                     PARM           ZDECS   10                                          CRE019
     C                     PARM           ZECODE  1                                           CRE019
     C                     PARM           ZOUT21 21                                           CRE019
      *                                                                                       CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      * DTLSCR - Process detail screen                                                        CRE019
      *****************************************************************                       CRE019
      *                                                               *                       CRE019
     C* CALLS      DSPF0  - Display screen format                     *
     C**********   FIRST  - Initial Processing                        *                       CRE019
     C**********   LAST   - Final Processing                          *                       CRE019
     C*            RFRESH - Refresh screen                            *
     C*            VALF0  - Validate screen input                     *
     C*                                                               *
     C* CALLED BY         -                                           *
     C*                                                               *
     C* FLDS USED         -                                           *
     C*                                                               *
     C*****************************************************************
     C           DTLSCR    BEGSR                                                              CRE019
     C*
     C**********           MOVEACPY@      BIS@   80                                           CRE019
     C**********                                                                              CRE019
     C****Initial processing                                                                  CRE019
     C**********                                                                              CRE019
     C**********           EXSR FIRST                                                         CRE019
     C                     MOVE *BLANK    WPRMPT  1                                           CRE019
     C*
     C**  Display initial screen
     C*
     C                     EXSR DSPF0
     C*
     C**  Do while cmd/3 is not pressed
     C           *INKC     DOWEQ'0'
     C*
     C**  Process screen input - depending upon input type.
     C*
     C**  If Cmd/5 is pressed, perform initialisation
     C*
     C                     SELEC                                                              CRE019
     C********** *INKE     CASEQ'1'       RFRESH                                              CRE019
     C           *INKE     WHEQ *ON                                                           CRE019
     C                     EXSR RFRESH                                                        CRE019
      *                                                                                       CRE019
      ** Cancel (F12)                                                                         CRE019
      *                                                                                       CRE019
     C           *INKL     WHEQ *ON                                                           CRE019
     C           CRE019    ANDEQ'Y'                                                           CRE019
     C                     EXSR RFRESH                                                        CRE019
     C                     MOVE 'Y'       WPRMPT                                              CRE019
     C                     MOVE *ON       *IN28                                               CRE019
     C*
     C**  If Enter key is pressed, validate screen input
     C*
     C**********           CAS            VALF0                                               CRE019
     C                     OTHER                                                              CRE019
     C                     EXSR VALF0                                                         CRE019
     C*
     C**********           ENDCS                                                              CRE019
     C                     ENDSL                                                              CRE019
      *                                                                                       CRE019
      ** Return to prompt screen after a completed transaction.                               CRE019
      *                                                                                       CRE019
     C           CRE019    IFEQ 'Y'                                                           CRE019
     C           WPRMPT    ANDEQ'Y'                                                           CRE019
     C                     MOVEL*BLANK    SACTN                                               CRE019
     C                     LEAVE                                                              CRE019
     C                     ENDIF                                                              CRE019
     C*
     C**  Redisplay appropriate screen
     C*
     C                     EXSR DSPF0
     C*
     C                     ENDDO
     C**********                                                                              CRE019
     C****Final*Processing                                                                    CRE019
     C**********                                                                              CRE019
     C**********           EXSR LAST                                                          CRE019
     C*
     C                     ENDSR                                                              CRE019
     C*****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            DSPF0  - Display screen format                     *
      *                                                               *
      * CALLS      SCC0250- clear message queue                       *
      *                                                               *
      * CALLED BY  MAIN   - Control Process                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           DSPF0     BEGSR                           ** DSPF0  **
      *
     C           SACTN     IFEQ 'I'
     C           *IN51     ANDEQ'0'
     C           CRE019    ANDEQ'N'                                                           CRE019
     C           SSCQN     ANDEQ*BLANK                                                       BUG3685
     C           SACTN     OREQ 'I'                                                           CRE019
     C           *IN51     ANDEQ*OFF                                                          CRE019
     C           CRE019    ANDEQ'Y'                                                           CRE019
     C           SSCQN     ANDEQ*BLANK                                                        CRE019
     C                     MOVELNCQN      SSCQN
     C                     END
      *
      ** If Cmd/3 is not pressed
     C           *INKC     IFEQ '0'
      *
      ** Display message subfile control record
     C                     WRITERE4212C0
      *
      ** Display & accept screen format
     C**********           EXFMTRE4212F0                                                      CRE019
     C                     WRITERE4212F0                                                      CRE019
     C                     WRITERE4212F2                                                      CRE019
     C                     EXFMTRE4212F1                                                      CRE019
      *
      ** Clear program message queue
     C                     CALL 'SCC0250'
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            RFRESH - Refresh screen                            *
      *                                                               *
      * CALLS      IFLDS  - Initialize Work/Screen Fields             *
      *            INT01  - Initialize Indicators for key Fields      *
      *            INT02  - Initialize Indicators for non-key fields  *
      *                                                               *
      * CALLED BY  AMDPR  - Amendment processing                      *
      *            DELPR  - Deletion processing                       *
      *            ENQPR  - Enquiry processing                        *
      *            INSPR  - Insertion processing                      *
      *            MAIN   - Main Processing                           *
      *                                                               *
      * FLDS USED         -                                           *
      *                                                               *
      *****************************************************************
     C           RFRESH    BEGSR                           ** RFRESH **
      *
      ** Set off screen disp. & validation control indicators
     C                     SETOF                     095152
      *
      ** Initialize screen/work fields
     C                     EXSR IFLDS
      *
      ** Initialize validation indicators for key fields
     C                     EXSR INT01
      *
      ** Initialize validation indicators for non key fields
     C                     EXSR INT02
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VALF0  - Validate screen input                     *
      *                                                               *
      * CALLS      AMDPR  - Amendment processing                      *
      *            DELPR  - Deletion processing                       *
      *            ENQPR  - Enquiry processing                        *
      *            INSPR  - Insertion processing                      *
      *            VAL01  - Validate key Fields                       *
      *                                                               *
      * CALLED BY  MAIN   - Main Processing                           *
      *                                                               *
      *****************************************************************
     C           VALF0     BEGSR                           ** VALF0  **
      *
      ** First validate key fields if indicator 51 is OFF (*IN51 = '0')
      *
     C           *IN51     IFEQ '0'
     C                     EXSR VAL01
     C                     END
      *
      ** If no errors in validation - branch to resp. processes
      ** 61 is seton if there are any errors in validation
      *
     C           *IN61     IFEQ '0'
     C                     MOVE '1'       *IN51
      *                                                                                       CRE019
      * Seton necessary protect indicator                                                     CRE019
      *                                                                                       CRE019
     C           CRE019    IFEQ 'Y'                                                           CRE019
     C                     MOVE *OFF      *IN27                                               CRE019
     C           OEDT      IFEQ BJRDNB                                                        CRE019
     C           SACTN     ANDEQ'A'                                                           CRE019
     C           SACTN     OREQ 'I'                                                           CRE019
     C                     MOVE *ON       *IN27                                               CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDIF                                                              CRE019
      *
      ** Insertion processing
     C           SACTN     CASEQ'I'       INSPR
      *
      ** Amendment processing
     C           SACTN     CASEQ'A'       AMDPR
      *
      ** Deletion  processing
     C           SACTN     CASEQ'D'       DELPR
      *                                                                                       CRE019
      ** Cancellation processing                                                              CRE019
     C           SACTN     CASEQ'C'       DELPR                                               CRE019
      *
      ** Enquiry   processing
     C           SACTN     CASEQ'E'       ENQPR
      *
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            INSPR  - Insertion processing                      *
      *                                                               *
      * CALLS      RFRESH - Refresh screen                            *
      *            UPDRTN - Update control routine                    *
      *            VAL02  - Validate non-key Fields                   *
      *                                                               *
      * CALLED BY  VALF0  - Validate screen input                     *
      *                                                               *
      *****************************************************************
     C           INSPR     BEGSR                           ** INSPR  **
      *
     C                     EXSR VAL02
      *
      ** If there are no errors-
     C           *IN62     IFEQ '0'
      *
      ** Update the files
     C                     EXSR UPDRTN
      *
      ** If no errors in updation - initialize fields & indicators
     C           *IN73     IFEQ '0'
     C                     EXSR RFRESH
      *                                                                                       CRE019
      ** Set flag to indicate processing is completed                                         CRE019
      *                                                                                       CRE019
     C           CRE019    IFEQ 'Y'                                                           CRE019
     C                     MOVE 'Y'       WPRMPT                                              CRE019
     C                     ENDIF                                                              CRE019
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            AMDPR  - Amendment processing                      *
      *                                                               *
      * CALLS      RFRESH - Refresh screen                            *
      *            UPDRTN - Update control routine                    *
      *            VAL02  - Validate non-key Fields                   *
      *                                                               *
      * CALLED BY  VALF0  - Validate screen input                     *
      *                                                               *
      *****************************************************************
     C           AMDPR     BEGSR                           ** AMDPR  **
      *
      ** Validate other fields if indicator 52 is ON  (*IN52 = '1')
      ** otherwise 52 is set ON for the next cycle
     C           *IN52     IFEQ '1'
     C                     EXSR VAL02
      *
      ** If there are no errors-
     C           *IN62     IFEQ '0'
      *
      ** Update the files
     C                     EXSR UPDRTN
      *
      ** If no errors in updation - initialize fields & indicators
     C           *IN73     IFEQ '0'
     C                     EXSR RFRESH
      *                                                                                       CRE019
      ** Set flag to indicate processing is completed                                         CRE019
      *                                                                                       CRE019
     C           CRE019    IFEQ 'Y'                                                           CRE019
     C                     MOVE 'Y'       WPRMPT                                              CRE019
     C                     ENDIF                                                              CRE019
     C                     END
      *
     C                     END
      *
      ** otherwise seton 52 for the next cycle
     C                     ELSE
      *
     C                     SETON                     52
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            DELPR  - Deletion processing                       *
      *                                                               *
      * CALLS      RFRESH - Refresh screen                            *
      *            UPDRTN - Update control routine                    *
      *                                                               *
      * CALLED BY  VALF0  - Validate screen input                     *
      *                                                               *
      *****************************************************************
     C           DELPR     BEGSR                           ** DELPR  **
      *
      ** If F10 is pressed - then
     C           *INKJ     IFEQ '1'
      *
      ** Update the files
     C                     EXSR UPDRTN
      *
      ** If no errors in updation - initialize fields/indicators
     C           *IN73     IFEQ '0'
     C                     EXSR RFRESH
      *                                                                                       CRE019
      ** Set flag to indicate processing is completed                                         CRE019
      *                                                                                       CRE019
     C           CRE019    IFEQ 'Y'                                                           CRE019
     C                     MOVE 'Y'       WPRMPT                                              CRE019
     C                     ENDIF                                                              CRE019
     C                     END
      *
     C                     ELSE
      *
      ** Seton protect indicators for non key fields
      ** and indicator for display of F10 prompt
     C                     SETON                     09
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            ENQPR  - Enquiry processing                        *
      *                                                               *
      * CALLS      RFRESH - Refresh screen                            *
      *                                                               *
      * CALLED BY  VALF0  - Validate screen input                     *
      *                                                               *
      *****************************************************************
     C           ENQPR     BEGSR                           ** ENQPR  **
      *
      ** Seton protect indicators for non key fields
      ** in the first cycle & refresh the screen in the next cycle
     C           *IN52     IFEQ '0'
     C                     SETON                     52
     C                     ELSE
     C                     EXSR RFRESH
      *                                                                                       CRE019
      ** Set flag to indicate processing is completed                                         CRE019
      *                                                                                       CRE019
     C           CRE019    IFEQ 'Y'                                                           CRE019
     C                     MOVE 'Y'       WPRMPT                                              CRE019
     C                     ENDIF                                                              CRE019
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            INT01  - Initialize Indicators for key Fields      *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  RFRESH - Refresh screen                            *
      *            VAL01  - Validate key Fields                       *
      *                                                               *
      *****************************************************************
     C           INT01     BEGSR                           ** INT01  **
      *
     C                     SETOF                     113132
     C                     SETOF                     3334
     C                     SETOF                     61
      *                                                                                       CRE019
      ** If CRE019 is installed, also set off error indicators for                            CRE019
      ** ending cheque number and number of cheques.                                          CRE019
      *                                                                                       CRE019
     C           CRE019    IFEQ 'Y'                                                           CRE019
     C                     MOVE *OFF      *IN41                                               CRE019
     C                     MOVE *OFF      *IN42                                               CRE019
     C                     ENDIF                                                              CRE019
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            INT02  - Initialize Indicators for non-key fields  *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  RFRESH - Refresh screen                            *
      *            VAL02  - Validate non-key Fields                   *
      *                                                               *
      *****************************************************************
     C           INT02     BEGSR                           ** INT02  **
      *
     C                     SETOF                     414243
     C                     SETOF                     441213
     C                     SETOF                     62
     C                     MOVE *OFF      *IN45                                               CRE019
     C                     MOVE *OFF      *IN46                                               CRE019
     C                     MOVE *OFF      *IN47                                               CRE019
      *                                                                                       CRE019
      ** If CRE019 is installed, also set off error indicators for                            CRE019
      ** start cheque number.                                                                 CRE019
     C           CRE019    IFEQ 'Y'                                                           CRE019
     C                     MOVE *OFF      *IN34                                               CRE019
     C                     ENDIF                                                              CRE019
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VAL01  - Validate key Fields                       *
      *                                                               *
      * CALLS      FTOWK  - Move File Fields to Work/Screen           *
      *            INT01  - Initialize Indicators for key Fields      *
      *            SNDMSG - Send program message parameters           *
      *            ZALIGN - Validate amount fields                    *
      *                                                               *
      * CALLED BY  VALF0  - Validate screen input                     *
      *                                                               *
      *****************************************************************
     C           VAL01     BEGSR                           ** VAL01  **
      *
      ** Initialize validation indicators for key fields
     C                     EXSR INT01
      *
      ** Action code if not I,A,D or E -
      ** message - Action code is invalid. valid values are I,A,D,E.
     C           SACTN     IFNE 'I'
     C           SACTN     ANDNE'A'
     C           SACTN     ANDNE'D'
     C           SACTN     ANDNE'E'
     C           CRE019    ANDNE'Y'                                                           CRE019
     C                     SETON                     3161
     C                     MOVEL'USR0001' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
      ** Set off Chain Indicator and validate retail acct no.
     C                     SETOF                     70
      *
     C                     MOVE *BLANKS   SACN20 10
      *
      ** Check if retail a/c no exists in LF/ACNUM
      * Move fields to key list.
     C                     MOVELSRACNO    KACNO
     C                     MOVELKACNO     SACN20
      *
     C           SRACNO    IFNE SACN20
     C           KACNO     ORLT 0
     C                     SETON                     70
     C                     END
      *
      ** Get Account Details
     C           *IN70     IFEQ '0'
     C           KLACNO    CHAINACNUM                70
     C                     MOVE ABRCA     WBRCA                                               CRE019
     C                     MOVE CCY       WCCY                                                CRE019
     C                     END
      *
      ** Account number must refer to a live record
     C           *IN70     IFEQ '1'
     C                     SETON                     3261
     C                     MOVE 'USR0123' @MSGID
     C                     EXSR SNDMSG
     C                     ELSE
      *
      ** If account is a not a live record
     C           SACTN     IFEQ 'I'
     C           SACTN     OREQ 'A'
     C           RECI      IFNE 'D'
     C                     SETON                     3261
     C                     MOVE 'USR0124' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
      ** If account sub-type is 'S' (Savings account) bypass
     C           STYP      IFEQ 'S'
     C                     SETON                     3261
     C                     MOVE 'USR0116' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
     C                     END
      *
     C                     END
      *                                                                                       CSF011
      ** Get Customer Name details                                                            CSF011
      *                                                                                       CSF011
     C                     MOVE *BLANKS   SCCSSN                                              CSF011
     C                     MOVE *BLANKS   SCREPN                                              CSF011
     C                     MOVE *BLANKS   SCREPT                                              CSF011
     C                     MOVE *BLANKS   SCACCN                                              CSF011
     C           *IN70     IFEQ '0'                                                           CSF011
     C                     MOVELANAM      SCACCN                                              CSF011
     C                     MOVELCNUM      @CNUM                                               CSF011
     C                     CALL 'AOCUSTR0'                                                    CSF011
     C                     PARM *BLANKS   @RTCD                                               CSF011
     C                     PARM '*KEY'    @OPTN                                               CSF011
     C                     PARM           @CNUM  10                                           CSF011
     C                     PARM *BLANKS   @KSTAT                                              CSF011
     C           SDCUST    PARM SDCUST    DSSDY                                               CSF011
     C           @RTCD     IFEQ *BLANKS                                                       CSF011
     C                     MOVELBBCSSN    SCCSSN                                              CSF011
     C                     MOVELBBCRNM    SCREPN                                              CSF011
     C                     MOVELBBCRTN    SCREPT                                              CSF011
     C                     ENDIF                                                              CSF011
     C                     ENDIF                                                              CSF011
      *
      ** if giro/cheque indicator not 0 or 1
     C           SGCIN     IFNE *BLANKS
     C           SGCIN     IFNE '0'
     C           SGCIN     ANDNE'1'
     C                     SETON                     3361
     C                     MOVE 'USR0125' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
     C                     ELSE
      *
      ** if not entered move default '0'
     C                     MOVEL'0'       SGCIN
     C                     END
      *
      ** validation of starting cheque number
      ** must not be blanks or zeroes and must be numeric
      *
      ** Set up decimal values
     C                     Z-ADD0         ZADEC
     C                     Z-ADD8         ZADIG
      *
      ** Align start cheque no
     C                     MOVEL*BLANK    ZFIELD
     C                     MOVE SSCQN     ZFIELD
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    @SCQN   80
      *
      ** If error found?  Set up error message
     C           *IN99     IFEQ '1'
     C           ZFIELD    OREQ *ZERO
     C                     SETON                     3461
     C                     MOVE 'USR0135' @MSGID
     C                     EXSR SNDMSG
     C*
     C                     ELSE
     C                     MOVE ZFIELD    SSCQN
     C                     END
      *
      ** Start cheque no must be a valid next cheque no
     C           SACTN     IFEQ 'I'
      *
      ** If no errors check if record already present
     C           *IN61     IFEQ '0'
     C                     MOVELSRACNO    KACNO
     C                     MOVELSGCIN     KGCIN
     C                     MOVELSSCQN     KSCQN
     C                     MOVE '0'       WREINS  1                                          BUG3685
      *
     C           KCHQBK    CHAINCHQBKPDF             70
     C*
     C           *IN70     IFEQ '0'
     C           DCOL      ANDNE0                                                            BUG3685
     C********** RECID     ANDEQ'D'                                                   BUG3685 240786
     C                     SETON                     323334
     C                     SETON                     61
     C           RECID     IFNE 'D'
     C                     MOVEL'USR0138' @MSGID
     C                     EXSR SNDMSG
     C                     ELSE
     C                     MOVEL'USR0136' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *                                                                                      BUG3685
     C                     ELSE                                                              BUG3685
     C           *IN70     IFEQ '0'                                                          BUG3685
     C           DCOL      ANDEQ0                                                            BUG3685
     C********** RECID     ANDEQ'*'                                                   BUG3685 240786
     C           RECID     IFNE 'D'                                                           240786
     C                     MOVE '1'       WREINS                                             BUG3685
     C                     ELSE                                                               240786
     C                     MOVEL'USR0136' @MSGID                                              240786
     C                     EXSR SNDMSG                                                        240786
     C                     END                                                                240786
     C                     END                                                               BUG3685
      *
     C                     END
      *
     C                     END
      *
      ** Send warning message if not equal to next cheque/giro number                        BUG3224
      *                                                                                      BUG3224
     C                     MOVELSRACNO    KACNO                                              BUG3224
     C                     MOVELSGCIN     KGCIN                                              BUG3224
     C           KCQPAR    SETGTCHQBKPDF                                                     BUG3224
     C           KCQPAR    REDPECHQBKPDF                 70                                  BUG3224
      *                                                                                      BUG3224
     C           *IN70     IFEQ *OFF                                                         BUG3224
     C           ECQN      ADD  1         W#ECQN  80                                         BUG3224
     C           @SCQN     IFNE W#ECQN                                                       BUG3224
      *                                                                                      BUG3224
     C                     MOVEL'USR0126' @MSGID                                             BUG3224
     C                     EXSR SNDMSG                                                       BUG3224
     C                     ENDIF                                                             BUG3224
     C                     ENDIF                                                             BUG3224
      *                                                                                      BUG3224
     C********** SGCIN     IFEQ '0'                                                          BUG3224
      *
      ** Cheque no if not equal to next cheque number
     C********** @SCQN     IFNE NCQN                                                         BUG3224
     C********** WREINS    ANDEQ'1'                                                  BUG3685 BUG3224
     C*                    SETON                     3461
      *
      ** display a warning if no in sequence
     C**********           MOVEL'USR0126' @MSGID                                             BUG3224
     C**********           EXSR SNDMSG                                                       BUG3224
     C**********           END                                                               BUG3224
     C**********           ELSE                                                              BUG3224
      *
      ** Cheque no if not equal to next giro number
     C********** @SCQN     IFNE NGRN                                                         BUG3224
     C*                    SETON                     3461
      *
      ** display a warning if no in sequence
     C**********           MOVEL'USR0126' @MSGID                                             BUG3224
     C**********           EXSR SNDMSG                                                       BUG3224
     C**********           END                                                               BUG3224
      *
     C**********           END                                                               BUG3224
      *
     C                     ELSE
      *
      ** If not insert mode check for existence of the record
     C                     MOVELSRACNO    KACNO
     C                     MOVELSGCIN     KGCIN
     C                     MOVELSSCQN     KSCQN
      *
     C********** KCHQBK    CHAINCHQBKPDF             70                   174972
     C           KCHQBK    CHAINCHQBKPDF             7053                 174972
     C           *IN53     IFEQ '1'                                       174972
      *                                                                   174972
      ** Send message - 'Record has been updated another workstation'     174972
      *                                                                   174972
     C                     MOVEL'USR0005' @MSGID                          174972
     C                     SETON                     323334               174972
     C                     SETON                     61                   174972
     C                     EXSR SNDMSG                                    174972
      *                                                                   174972
     C                     ELSE                                           174972
     C*
     C           *IN70     IFEQ '1'
     C                     SETON                     323334
     C                     SETON                     61
     C                     MOVEL'USR0127' @MSGID
     C                     EXSR SNDMSG
      *
     C                     ELSE
      *
      ** Check if the record is a live record
     C           RECID     IFNE 'D'
     C                     SETON                     323334
     C                     SETON                     61
     C                     MOVEL'USR0127' @MSGID
     C                     EXSR SNDMSG
     C                     END
     C*
     C**  If setup complete flag is on deletion only allowed
     C**  if no cheques have been presented
     C*
     C           SACTN     IFEQ 'D'                        B003
     C           @SCI      ANDEQ'N'
     C*
     C**  scan status array for presented cheques
     C*
     C                     Z-ADD1         K       20
     C           *IN31     DOWEQ'0'                        B004
     C           K         ANDLE20
     C           @CP,K     IFNE X'00'                      B005
     C                     SETON                     31
     C                     END                             E005
     C                     ADD  1         K
     C                     END                             E004
     C*
     C           *IN31     IFEQ '1'                        B004
     C                     SETON                     61
     C                     MOVEL'USR0239' @MSGID
     C                     EXSR SNDMSG
     C                     END                             E004
     C*
     C                     END                             E003
     C*
     C                     END
     C*                                                                   174972
     C                     END                                            174972
     C*
     C                     END
      *                                                                                       CRE019
      ** Validate overlapping cheque ranges for inserts                                       CRE019
      *                                                                                       CRE019
     C           CRE019    IFEQ 'Y'                                                           CRE019
     C           SACTN     ANDEQ'I'                                                           CRE019
     C           *IN61     ANDEQ*OFF                                                          CRE019
      *                                                                                       CRE019
      ** Validate number of cheques and ending cheque number first                            CRE019
      ** since validating range requires valid start and ending                               CRE019
      ** cheque numbers                                                                       CRE019
      *                                                                                       CRE019
     C                     EXSR VSNOCQ                                                        CRE019
     C                     EXSR VSECQN                                                        CRE019
      *                                                                                       CRE019
      ** If errors are found, flag error indicator for key fields                             CRE019
      ** else, validate cheque range for overlapping                                          CRE019
      *                                                                                       CRE019
     C           *IN62     IFEQ *ON                                                           CRE019
     C                     MOVE *ON       *IN61                                               CRE019
     C                     MOVE *OFF      *IN62                                               CRE019
     C                     ELSE                                                               CRE019
     C                     EXSR VRANGE                                                        CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDIF                                                              CRE019
      *
      ** 61 is set on if any error in key fields validation
      ** If no errors -
     C           *IN61     IFEQ '0'
      *
      ** Set on protect indicators for action & Other key feilds
     C                     SETON                     11
      *
      ** Set on protect ind. for non-key feilds except collected date
     C           SACTN     IFEQ 'I'
     C                     SETOF                     1213
     C                     MOVE *OFF      *IN27                                               CRE019
     C                     ELSE
     C           SACTN     IFEQ 'D'
     C           SACTN     OREQ 'E'
     C           SACTN     OREQ 'C'                                                           CRE019
     C           CRE019    ANDEQ'Y'                                                           CRE019
     C                     SETON                     12
     C                     SETOF                     13
     C                     MOVE *OFF      *IN27                                               CRE019
     C                     ELSE
     C           SACTN     IFEQ 'A'
     C                     SETON                     1213
     C                     END
     C                     END
     C                     END
      *
      ** If act.cd is not 'I' - Move fields from file to work/screen
     C           SACTN     IFNE 'I'
     C           WREINS    OREQ '1'                                                          BUG3685
     C                     EXSR FTOWK
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VAL02  - Validate non-key Fields                   *
      *                                                               *
      * CALLS      INT02  - Initialize Indicators for non-key fields  *
      *            SNDMSG - Send program message parameters           *
      *            VSDCOL - Validate date of collection               *
      *            VSDISS - Validate date of issue                    *
      *            VSECQN - Validate ending cheque no                 *
      *            VSNOCQ - Validate number of cheques                *
      *                                                               *
      * CALLED BY  AMDPR  - Amendment processing                      *
      *            INSPR  - Insertion processing                      *
      *                                                               *
      *****************************************************************
     C           VAL02     BEGSR                           ** VAL02  **
      *
      ** Initialize validation indicators for non key fields
     C                     EXSR INT02
      *
      ** 61 is set on if any error in key fields validation
      ** If no errors -
     C           *IN61     IFEQ '0'
      *
      ** Set on protect indicators for action & Other key feilds
     C                     SETON                     11
      *
      ** Set on protect ind. for non-key feilds except collected date
     C           SACTN     IFEQ 'I'
     C                     SETOF                     1213
     C                     MOVE *OFF      *IN27                                               CRE019
     C                     ELSE
     C           SACTN     IFEQ 'D'
     C           SACTN     OREQ 'E'
     C                     SETON                     12
     C                     SETOF                     13
     C                     MOVE *OFF      *IN27                                               CRE019
     C                     ELSE
     C           SACTN     IFEQ 'A'
     C                     SETON                     1213
     C                     END
     C                     END
     C                     END
     C                     END
      *
      ** Validate no of cheque,should not exceed 160
     C                     EXSR VSNOCQ
      *
      ** Validate ending cheque no
     C                     EXSR VSECQN
      *
     C/COPY WNCPYSRC,RE4212C001
      ** Validate date of issue
     C                     EXSR VSDISS
      *
      ** Validate date of collection
     C                     EXSR VSDCOL
      *                                                                                       CRE019
      ** Additional validations for CRE019                                                    CRE019
      *                                                                                       CRE019
     C           CRE019    IFEQ 'Y'                                                           CRE019
      *                                                                                       CRE019
      ** Validate Charge Account                                                              CRE019
      *                                                                                       CRE019
     C                     EXSR VSCHAC                                                        CRE019
      *                                                                                       CRE019
      ** Proceed validations only if charge account is valid.                                 CRE019
      *                                                                                       CRE019
     C           *IN46     IFEQ *OFF                                                          CRE019
     C                     MOVELCCY       WCCY                                                CRE019
      *                                                                                       CRE019
      ** Validate Charge Amount                                                               CRE019
      *                                                                                       CRE019
     C                     EXSR VSCHAM                                                        CRE019
      *                                                                                       CRE019
      ** Validate charge transaction type                                                     CRE019
      *                                                                                       CRE019
     C                     EXSR VSCHTT                                                        CRE019
      *                                                                                       CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDIF                                                              CRE019
      *
      ** Check if command 10 is pressed
      ** message - Function key not allowed
     C           *INKJ     IFEQ '1'
     C                     SETON                     62
     C                     MOVEL'USR0004' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VSNOCQ - Validate number of cheques                *
      *                                                               *
      * CALLS      SNDMSG - Send program message parameters           *
      *            ZALIGN - Validate amount fields                    *
      *                                                               *
      * CALLED BY  VAL02  - Validate non-key Fields                   *
      *                                                               *
      *****************************************************************
     C           VSNOCQ    BEGSR                           ** VSNOCQ **
      *
      ** Set up decimal values
     C                     Z-ADD0         ZADEC
     C                     Z-ADD3         ZADIG
      *
      ** Align amount
     C                     MOVEL*BLANK    ZFIELD
     C                     MOVE SNOCQ     ZFIELD
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    @NOCQ   30                      E14168
      *
      ** If error found?  Set up error message
     C           *IN99     IFEQ '1'
     C           ZFIELD    OREQ *ZERO
     C                     SETON                     4262
     C                     MOVE 'USR0128' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
      ** Validate no of cheque,should not exceed 160
     C           @NOCQ     IFGT 160
     C                     SETON                     4262
     C                     MOVEL'USR0128' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
     C                     ENDSR
      *****************************************************************
     C/COPY WNCPYSRC,RE4212C002
      /EJECT
      *****************************************************************
      *                                                               *
      *            VSECQN - Validate ending cheque no                 *
      *                                                               *
      * CALLS      SNDMSG - Send program message parameters           *
      *            ZALIGN - Validate amount fields                    *
      *                                                               *
      * CALLED BY  VAL02  - Validate non-key Fields                   *
      *                                                               *
      *****************************************************************
     C           VSECQN    BEGSR                           ** VSECQN **
      *
      ** If no errors then check ending cheque no
     C           *IN42     IFEQ '0'
      *
      ** Set up decimal values
     C                     Z-ADD0         ZADEC
     C                     Z-ADD8         ZADIG
      *
      ** Align amount
     C                     MOVEL*BLANK    ZFIELD
     C                     MOVE SECQN     ZFIELD
     C                     EXSR ZALIGN
     C                     MOVE ZFIELD    @ECQN   80                      E14168
      *
      ** If error found?  Set up error message
     C           *IN99     IFEQ '1'
     C           ZFIELD    OREQ *ZERO
     C                     SETON                     4162
     C                     MOVE 'USR0129' @MSGID
     C                     EXSR SNDMSG
     C*
     C                     ELSE
     C                     MOVE ZFIELD    SECQN
     C                     END
      *
     C           *IN62     IFEQ '0'
     C                     Z-ADD@SCQN     @TEMP   80
     C                     ADD  @NOCQ     @TEMP
     C                     SUB  1         @TEMP
     C           @TEMP     IFNE @ECQN
     C                     SETON                     4162
     C                     MOVEL'USR0129' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VSDISS - Validate date of issue                    *
      *                                                               *
      * CALLS      SNDMSG - Send program message parameters           *
      *            ZDATE1 - Date validation routine                   *
      *                                                               *
      * CALLED BY  VAL02  - Validate non-key Fields                   *
      *                                                               *
      *****************************************************************
     C           VSDISS    BEGSR                           ** VSDISS **
      *
      ** to be validated if entered
     C           SISSDT    IFNE *BLANK
     C                     MOVE SISSDT    ZDATE
      *
     C                     EXSR ZDATE1
      *
     C           *IN99     IFNE '1'
     C                     MOVE ZDAYNO    @VDISS  50
      *
      ** must not be later than midas run date
     C           @VDISS    IFGT BJRDNB
     C                     SETON                     4362
     C                     MOVEL'USR0131' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
     C                     ELSE
      *
      ** if invalid date i.e. 99 is on
     C                     SETON                     4362
     C                     MOVEL'USR0130' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
     C                     ELSE
      *
      ** if date not entered should default to run date
     C                     Z-ADDBJRDNB    @VDISS
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            VSDCOL - Validate date of collection               *
      *                                                               *
      * CALLS      SNDMSG - Send program message parameters           *
      *            ZDATE1 - Date validation routine                   *
      *                                                               *
      * CALLED BY  VAL02  - Validate non-key Fields                   *
      *                                                               *
      *****************************************************************
     C           VSDCOL    BEGSR                           ** VSDCOL **
      *
      ** to be validated if entered
      *                                                                                       CRE019
     C                     MOVE *BLANKS   @VDCOL                                              CRE019
     C           SCLTDT    IFNE *BLANK
     C                     MOVE SCLTDT    ZDATE
      *
     C                     EXSR ZDATE1
      *
     C           *IN99     IFNE '1'
     C                     MOVE ZDAYNO    @VDCOL  50
      *
      ** must not be later than midas run date
     C           @VDCOL    IFGT BJRDNB
     C                     SETON                     4462
     C                     MOVEL'USR0133' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
      ** must not be earlier than date of issue
     C           @VDISS    IFGT @VDCOL
     C                     SETON                     4462
     C                     MOVEL'USR0134' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
     C                     ELSE
      *
      ** if invalid date i.e. 99 is on
     C                     SETON                     4462
     C                     MOVEL'USR0132' @MSGID
     C                     EXSR SNDMSG
     C                     END
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      *                                                               *                       CRE019
      *  VSCHAM - Validate charge amount                              *                       CRE019
      *                                                               *                       CRE019
      *****************************************************************                       CRE019
     C           VSCHAM    BEGSR                                                              CRE019
      *                                                                                       CRE019
      ** If charge amount field is entered                                                    CRE019
      *                                                                                       CRE019
     C                     MOVEL*BLANK    WCHAM  14                                           CRE019
     C           SCHAM     IFNE *BLANKS                                                       CRE019
      *                                                                                       CRE019
      ** Get currency details                                                                 CRE019
      *                                                                                       CRE019
     C                     MOVELWCCY      PCCY                                                CRE019
     C                     EXSR SRCURR                                                        CRE019
      *                                                                                       CRE019
      ** Align charge amount                                                                  CRE019
      *                                                                                       CRE019
     C                     Z-ADDA6NBDP    ZADEC                                               CRE019
     C           14        SUB  A6NBDP    ZADIG                                               CRE019
     C                     MOVEL*BLANK    ZFIELD                                              CRE019
     C                     MOVE SCHAM     ZFIELD                                              CRE019
     C                     EXSR ZALIGN                                                        CRE019
      *                                                                                       CRE019
      ** If error found?  Set up error message                                                CRE019
      *                                                                                       CRE019
     C           *IN99     IFEQ *ON                                                           CRE019
     C           ZFIELD    OREQ *ZERO                                                         CRE019
     C                     MOVE *ON       *IN45                                               CRE019
     C                     MOVE *ON       *IN62                                               CRE019
     C                     MOVE 'USR0008' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE ZFIELD    WCHAM                                               CRE019
      *                                                                                       CRE019
      ** Display formated  charge amount                                                      CRE019
      *                                                                                       CRE019
     C                     Z-ADDA6NBDP    ZDECS                                               CRE019
     C                     MOVE 'L'       ZECODE                                              CRE019
     C                     MOVE WCHAM     ZFLD15                                              CRE019
     C                     EXSR ZFRPED                                                        CRE019
     C                     MOVELZOUT22    WRK20  20                                           CRE019
     C                     MOVE WRK20     SCHAM                                               CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      *                                                               *                       CRE019
      *  VSCHAC - Validate charge account                             *                       CRE019
      *                                                               *                       CRE019
      *****************************************************************                       CRE019
     C           VSCHAC    BEGSR                                                              CRE019
      *                                                                                       CRE019
      ** Charge account is entered                                                            CRE019
      *                                                                                       CRE019
     C           SCHAC     IFNE *BLANKS                                                       CRE019
      *                                                                                       CRE019
      ** Charge account must be different from retail account                                 CRE019
      *                                                                                       CRE019
     C           SCHAC     IFEQ SRACNO                                                        CRE019
     C                     MOVE *ON       *IN46                                               CRE019
     C                     MOVE *ON       *IN62                                               CRE019
     C                     MOVE 'USR1241' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
      *                                                                                       CRE019
     C                     ELSE                                                               CRE019
      *                                                                                       CRE019
     C                     MOVELSCHAC     KCHAC  100                                          CRE019
     C           KCHAC     CHAINACNUM                70                                       CRE019
     C           *IN70     IFEQ '1'                                                           CRE019
     C                     MOVE *ON       *IN46                                               CRE019
     C                     MOVE *ON       *IN62                                               CRE019
     C                     MOVE 'USR0123' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
     C                     ELSE                                                               CRE019
      *                                                                                       CRE019
      ** If account is a not a live record                                                    CRE019
      *                                                                                       CRE019
     C           RECI      IFNE 'D'                                                           CRE019
     C                     MOVE *ON       *IN46                                               CRE019
     C                     MOVE *ON       *IN62                                               CRE019
     C                     MOVE 'USR0124' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** If charge account branch is different retail account branch                          CRE019
      *                                                                                       CRE019
     C           ABRCA     IFNE WBRCA                                                         CRE019
     C                     MOVE *ON       *IN46                                               CRE019
     C                     MOVE *ON       *IN62                                               CRE019
     C                     MOVE 'USR1248' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C/COPY WNCPYSRC,REH00359
      ** If account sub-type is 'S' (Savings account) bypass                                  CRE019
      *                                                                                       CRE019
     C           STYP      IFEQ 'S'                                                           CRE019
     C                     MOVE *ON       *IN46                                               CRE019
     C                     MOVE *ON       *IN62                                               CRE019
     C                     MOVE 'USR0116' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C/COPY WNCPYSRC,REH00360
     C                     ENDIF                                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Ensure that correct CCY is being use                                                 CRE019
      *                                                                                       CRE019
     C                     ELSE                                                               CRE019
     C                     MOVELSRACNO    KCHAC                                               CRE019
     C           KCHAC     CHAINACNUM                70                                       CRE019
     C                     MOVELCCY       WCCY                                                CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      *                                                               *                       CRE019
      *  VSCHTT - Validate charge transaction type                    *                       CRE019
      *                                                               *                       CRE019
      *****************************************************************                       CRE019
     C           VSCHTT    BEGSR                                                              CRE019
      *                                                                                       CRE019
     C                     SELEC                                                              CRE019
      *                                                                                       CRE019
      ** Transaction type is mandatory if Charge Amount is entered                            CRE019
      ** else, transaction type must be also blanks.                                          CRE019
      *                                                                                       CRE019
     C           SCHAM     WHNE *BLANKS                                                       CRE019
     C           SCHTT     ANDNE*BLANK                                                        CRE019
      *                                                                                       CRE019
      ** Transaction type must exist                                                          CRE019
      *                                                                                       CRE019
     C                     CALL 'AORETRR0'                                                    CRE019
     C                     PARM '*BLANK ' PRTCD                                               CRE019
     C                     PARM '*KEY   ' POPTN                                               CRE019
     C           SCHTT     PARM SCHTT     PRETR   5                                           CRE019
     C           SDRETR    PARM SDRETR    DSFDY                                               CRE019
      *                                                                                       CRE019
      ** If record does not exist or is deleted -                                             CRE019
      *                                                                                       CRE019
     C           PRTCD     IFNE *BLANK                                                        CRE019
     C                     MOVE *ON       *IN47                                               CRE019
     C                     MOVE *ON       *IN62                                               CRE019
     C                     MOVEL'USR0086' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
      *                                                                                       CRE019
     C                     ELSE                                                               CRE019
      *                                                                                       CRE019
      ** Transaction type must be debit only                                                  CRE019
      *                                                                                       CRE019
     C           A1DCIN    IFNE 'D'                                                           CRE019
     C                     MOVE *ON       *IN47                                               CRE019
     C                     MOVE *ON       *IN62                                               CRE019
     C                     MOVEL'USR0087' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
      *                                                                                       CRE019
     C**********           ELSE                                                       CRE019 BUG2966
      *****************************************************************               CRE019 BUG2966
      ***Associated*GL*code*must*be*set*up*****************************               CRE019 BUG2966
      *****************************************************************               CRE019 BUG2966
     C********** A1ASGL    IFEQ *BLANK                                                CRE019 BUG2966
     C**********           MOVE *ON       *IN47                                       CRE019 BUG2966
     C**********           MOVE *ON       *IN62                                       CRE019 BUG2966
     C**********           MOVEL'USR0106' @MSGID                                      CRE019 BUG2966
     C**********           EXSR SNDMSG                                                CRE019 BUG2966
     C**********           ENDIF                                                      CRE019 BUG2966
      *                                                                                       CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** If only charge amount is entered or only transaction type                            CRE019
      ** is entered                                                                           CRE019
      *                                                                                       CRE019
     C           SCHAM     WHEQ *BLANKS                                                       CRE019
     C           SCHTT     ANDNE*BLANK                                                        CRE019
     C           SCHAM     ORNE *BLANKS                                                       CRE019
     C           SCHTT     ANDEQ*BLANK                                                        CRE019
     C                     MOVE *ON       *IN47                                               CRE019
     C                     MOVE *ON       *IN62                                               CRE019
     C                     MOVEL'USR1242' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
      *                                                                                       CRE019
      ** No error if both Charge amount and tansaction type are both                          CRE019
      ** blanks                                                                               CRE019
      *                                                                                       CRE019
     C           SCHAM     WHEQ *BLANKS                                                       CRE019
     C           SCHTT     ANDEQ*BLANK                                                        CRE019
      *                                                                                       CRE019
     C                     ENDSL                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************                       CRE019
      /EJECT                                                                                  CRE019
      *****************************************************************                       CRE019
      *  VRANGE - Validate cheque number range to prevent overlapping *                       CRE019
      *****************************************************************                       CRE019
     C           VRANGE    BEGSR                                                              CRE019
      *                                                                                       CRE019
      ** else cheque for overlapping ranges                                                   CRE019
      *                                                                                       CRE019
     C                     MOVE 'N'       WOVRLP  1                                           CRE019
     C                     MOVELSSCQN     WSCQN   80                                          CRE019
     C                     MOVELSECQN     WECQN   80                                          CRE019
      *                                                                                       CRE019
     C                     MOVELSRACNO    KACNO                                               CRE019
     C                     MOVELSGCIN     KGCIN                                               CRE019
     C                     MOVELSSCQN     KSCQN                                               CRE019
      *                                                                                       CRE019
     C           KCHQBK    SETLLCHQBKPDF                                                      CRE019
     C           KCQPAR    READECHQBKPDF                 70                                   CRE019
      *                                                                                       CRE019
     C           *IN70     DOWEQ*OFF                                                          CRE019
     C           WOVRLP    ANDEQ'N'                                                           CRE019
      *                                                                                       CRE019
     C           SCQN      IFLE WSCQN                                                         CRE019
     C           WSCQN     ANDLEECQN                                                          CRE019
     C           SCQN      ORLE WECQN                                                         CRE019
     C           WECQN     ANDLEECQN                                                          CRE019
     C           WSCQN     ORLE SCQN                                                          CRE019
     C           SCQN      ANDLEWECQN                                                         CRE019
     C           WSCQN     ORLE ECQN                                                          CRE019
     C           ECQN      ANDLEWECQN                                                         CRE019
     C                     MOVE 'Y'       WOVRLP                                              CRE019
     C                     LEAVE                                                              240786
     C                     ENDIF                                                              CRE019
     C           KCQPAR    READECHQBKPDF                 70                                   CRE019
     C                     ENDDO                                                              CRE019
      *                                                                                       CRE019
     C           WOVRLP    IFEQ 'N'                                                           CRE019
     C           KCHQBK    SETGTCHQBKPDF                                                      CRE019
     C           KCQPAR    REDPECHQBKPDF                 70                                   CRE019
      *                                                                                       CRE019
     C           *IN70     DOWEQ*OFF                                                          CRE019
     C           WOVRLP    ANDEQ'N'                                                           CRE019
      *                                                                                       CRE019
     C           SCQN      IFLE WSCQN                                                         CRE019
     C           WSCQN     ANDLEECQN                                                          CRE019
     C           SCQN      ORLE WECQN                                                         CRE019
     C           WECQN     ANDLEECQN                                                          CRE019
     C           WSCQN     ORLE SCQN                                                          CRE019
     C           SCQN      ANDLEWECQN                                                         CRE019
     C           WSCQN     ORLE ECQN                                                          CRE019
     C           ECQN      ANDLEWECQN                                                         CRE019
     C                     MOVE 'Y'       WOVRLP                                              CRE019
     C                     LEAVE                                                              240786
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C           KCQPAR    REDPECHQBKPDF                 70                                   CRE019
     C                     ENDDO                                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C           WOVRLP    IFEQ 'Y'                                                           CRE019
     C           RECID     IFEQ 'D'                                                           240786
     C                     MOVE *ON       *IN34                                               CRE019
     C                     MOVE *ON       *IN41                                               CRE019
     C                     MOVE *ON       *IN61                                               CRE019
     C                     MOVEL'USR1246' @MSGID                                              CRE019
     C                     EXSR SNDMSG                                                        CRE019
     C                     ELSE                                                               240786
     C           DCOL      IFNE *ZERO                                                         240786
     C                     MOVE '1'       *IN34                                               240786
     C                     MOVE '1'       *IN41                                               240786
     C                     MOVE '1'       *IN61                                               240786
     C                     MOVEL'USR1253' @MSGID                                              240786
     C                     EXSR SNDMSG                                                        240786
     C                     ENDIF                                                              240786
     C                     ENDIF                                                              240786
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR                                                              CRE019
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            FTOWK  - Move File Fields to Work/Screen           *
      *                                                               *
      * CALLS      ZDATE2 - Convert Day number to date                *
      *                                                               *
      * CALLED BY  VAL01  - Validate key Fields                       *
      *                                                               *
      *****************************************************************
     C           FTOWK     BEGSR                           ** FTOWK  **
      *
     C                     MOVELNOCQ      SNOCQ
      *
     C           CRE019    IFNE 'Y'                                                           CRE019
     C           DISS      ORNE *ZERO                                                         CRE019
     C                     MOVELDISS      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZDATE     SISSDT
     C                     ELSE                                                               CRE019
     C                     MOVEL*BLANKS   SISSDT                                              CRE019
     C                     ENDIF                                                              CRE019
      *
     C           CRE019    IFNE 'Y'                                                           CRE019
     C           WREINS    ANDNE'1'                                                          BUG3685
     C           DCOL      ORNE *ZERO                                                         CRE019
     C                     MOVELDCOL      ZDAYNO
     C                     EXSR ZDATE2
     C                     MOVELZDATE     SCLTDT
     C                     ELSE                                                               CRE019
     C                     MOVEL*BLANKS   SCLTDT                                              CRE019
     C                     ENDIF                                                              CRE019
      *
     C                     MOVELECQN      SECQN
     C                     Z-ADDTNLU      @TNLU
      *
     C           CRE019    IFEQ 'Y'                                                           CRE019
      *                                                                                       CRE019
      ** Format charge account                                                                CRE019
      *                                                                                       CRE019
     C           CHAC      IFNE *ZERO                                                         CRE019
     C                     MOVELCHAC      SCHAC                                               CRE019
     C           CHAC      CHAINACNUM                70                                       CRE019
     C                     MOVELCCY       WCCY                                                CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE *BLANKS   SCHAC                                               CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Get currency details                                                                 CRE019
      *                                                                                       CRE019
     C                     MOVELWCCY      PCCY                                                CRE019
     C                     EXSR SRCURR                                                        CRE019
      *                                                                                       CRE019
      ** Format charge amount                                                                 CRE019
      *                                                                                       CRE019
     C                     MOVE *BLANKS   ZFLD15                                              CRE019
     C           CHAM      IFNE *ZERO                                                         CRE019
     C                     MOVE CHAM      ZFLD15                                              CRE019
     C                     MOVE A6NBDP    ZDECS                                               CRE019
     C                     MOVE 'L'       ZECODE                                              CRE019
     C                     EXSR ZSEDIT                                                        CRE019
     C                     MOVELZOUT21    WRK20                                               CRE019
     C                     MOVE WRK20     SCHAM                                               CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE *BLANKS   SCHAM                                               CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      ** Format charge transaction type                                                       CRE019
      *                                                                                       CRE019
     C           CHTT      IFNE *ZERO                                                         CRE019
     C                     MOVELCHTT      SCHTT                                               CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE *BLANKS   SCHTT                                               CRE019
     C                     ENDIF                                                              CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            WKTOF  - Move Work/Screen Fields To File           *
      *                                                               *
      * CALLS      SBTOFF - Set bit off                               *
      *                                                               *
      * CALLED BY  UPDAD  - Update procedure for amend/delete         *
      *            UPDRTN - Update control routine                    *
      *                                                               *
      *****************************************************************
     C           WKTOF     BEGSR                           ** WKTOF  **
      *
     C                     MOVEL'D'       RECID
      *
     C                     MOVELSRACNO    ACNO
     C                     MOVELSGCIN     GCQI
     C                     Z-ADD@SCQN     SCQN
      *
     C                     Z-ADD@NOCQ     NOCQ
     C                     Z-ADD@ECQN     ECQN
     C                     MOVEL@VDISS    DISS
     C                     MOVEL@VDCOL    DCOL
      *
     C                     MOVE BJRDNB    LCD
     C                     MOVELSACTN     CHTP
     C                     Z-ADDNATN      TNLU
      *
     C                     MOVELABRCA     BRCA
     C                     EXSR SBTOFF
      *
     C           CRE019    IFEQ 'Y'                                                           CRE019
     C                     MOVE WCHAM     CHAM                                                CRE019
     C                     MOVELSCHAC     CHAC                                                CRE019
     C                     MOVELSCHTT     CHTT                                                CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            SBTOFF - Set bit off                               *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  WKTOF  - Move Work/Screen Fields To File           *
      *                                                               *
      *****************************************************************
     C           SBTOFF    BEGSR                           ** SBTOFF **
      *
      ** Set bit of for all the 160 cheque presented array
     C                     Z-ADD1         @I      20
      *
     C           @I        DOWLE20
     C                     BITON'01234567'@CP,@I  1
     C                     BITOF'01234567'@CP,@I  1
     C                     ADD  1         @I
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            UPDRTN - Update control routine                    *
      *                                                               *
      * CALLS      PCMTXT - Prepare comit text CMTTXT                 *
      *            SNDMSG - Send program message parameters           *
      *            UPDAD  - Update procedure for amend/delete         *
      *            UPHED  - Update Header Record                      *
      *            UPTRL  - Update Control Trailer Record             *
      *            WKTOF  - Move Work/Screen Fields To File           *
      *            WRCQBK - Write CHKBKPD Record                      *
      *            ZTNLU1 - Access dataarea for next transaction no.  *
      *                                                               *
      * CALLED BY  AMDPR  - Amendment processing                      *
      *            DELPR  - Deletion processing                       *
      *            INSPR  - Insertion processing                      *
      *                                                               *
      *****************************************************************
     C           UPDRTN    BEGSR                           ** UPDRTN **
      *
     C                     MOVE '0'       *IN73
      *
      ** If action code is 'I' - Insertion
     C           SACTN     IFEQ 'I'
      *
      ** Check if record has been inserted by another workstation
      *
     C                     MOVELSRACNO    KACNO
     C                     MOVELSGCIN     KGCIN
     C                     MOVELSSCQN     KSCQN
     C           KCHQBK    CHAINCHQBKPDF             70
     C           *IN70     IFEQ '0'
     C           WREINS    IFNE '1'                                                          BUG3685
     C                     MOVE '1'       *IN73
      *
      ** Rollback all updates
     C*                    ROLBK
      *
      ** Send message - 'Record inserted by another workstation'
     C                     MOVEL'USR0005' @MSGID
     C                     EXSR SNDMSG
     C                     ENDIF                                                             BUG3685
      *
     C                     ELSE
      *
      ** Update next available transaction number
     C                     EXSR ZTNLU1
      *
      ** Prepare commitment text
     C                     EXSR PCMTXT
      *
     C                     EXSR WKTOF
     C                     EXSR WRCQBK
      *
     C                     END
      *
     C                     END
      *
      ** If action code is 'A' - amend or 'D' - delete
     C           SACTN     IFEQ 'A'
     C           SACTN     OREQ 'D'
     C           WREINS    OREQ '1'                                                          BUG3685
      *
     C                     EXSR UPDAD
      *
     C                     END
      *
      ** Update control trailer & end commitment if no errors
     C           *IN73     IFEQ '0'
     C                     EXSR UPTRL
      *
     C                     EXSR UPHED
      *
      ** End of commitment cycle
     C***********CMTTXT****COMIT***************************************                       CRE019
     C           *IN23     IFEQ *OFF                                                          CRE019
     C           CMTTXT    COMIT                                                              CRE019
     C                     ENDIF                                                              CRE019
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            UPDAD  - Update procedure for amend/delete         *
      *                                                               *
      * CALLS      PCMTXT - Prepare comit text CMTTXT                 *
      *            SNDMSG - Send program message parameters           *
      *            WKTOF  - Move Work/Screen Fields To File           *
      *            ZTNLU1 - Access dataarea for next transaction no.  *
      *                                                               *
      * CALLED BY  UPDRTN - Update control routine                    *
      *                                                               *
      *****************************************************************
     C           UPDAD     BEGSR                           ** UPDAD  **
      *
      ** Check whether tran.no.of last update has changed
      *
     C                     MOVELSRACNO    KACNO
     C                     MOVELSGCIN     KGCIN
     C                     MOVELSSCQN     KSCQN
     C           KCHQBK    CHAINCHQBKPDF             70
     C           TNLU      IFNE @TNLU
     C                     MOVE '1'       *IN73
      *
      ** Rollback all updates
     C*                    ROLBK
      *
      ** Send message - 'Record inserted by another workstation'
     C                     MOVEL'USR0005' @MSGID
     C                     EXSR SNDMSG
      *
     C                     END
      *
      ** If no errors -
     C           *IN73     IFEQ '0'
      *
      ** Update next available tran.no.
     C                     EXSR ZTNLU1
      *
      ** Prepare commitment text
     C                     EXSR PCMTXT
      *
     C           SACTN     IFEQ 'A'
     C           WREINS    OREQ '1'                                                          BUG3685
     C                     EXSR WKTOF
     C                     ELSE
     C                     MOVEL'*'       RECID
     C                     END
     C                     MOVE BJRDNB    LCD
     C                     MOVELSACTN     CHTP
     C                     Z-ADDNATN      TNLU
      *                                                                                       CRE019
     C           CRE019    IFEQ 'Y'                                                           CRE019
     C                     MOVE @VDCOL    DCOL                                                CRE019
     C                     MOVE WCHAM     CHAM                                                CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     UPDATCHQBKPDF
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            WRCQBK - Write CHKBKPD Record                      *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  UPDRTN - Update control routine                    *
      *                                                               *
      *****************************************************************
     C           WRCQBK    BEGSR                           ** WRCQBK **
      *
     C                     MOVEL'D'       RECID
     C                     MOVE BJRDNB    LCD
     C                     MOVELSACTN     CHTP
     C                     Z-ADDNATN      TNLU
      *                                                                                       CRE019
      ** intialise new field introduce by CRE019                                              CRE019
      *                                                                                       CRE019
     C                     MOVE *BLANK    OUTR                                                CRE019
     C                     MOVE WCHAM     CHAM                                                CRE019
     C                     MOVE SCHAC     CHAC                                                CRE019
     C                     MOVE SCHTT     CHTT                                                CRE019
     C                     Z-ADDBJRDNB    OEDT                                                CRE019
      *                                                                                       CRE019
     C                     WRITECHQBKPDF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            PCMTXT - Prepare comit text CMTTXT                 *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  UPDAD  - Update procedure for amend/delete         *
      *            UPDRTN - Update control routine                    *
      *                                                               *
      *****************************************************************
     C           PCMTXT    BEGSR                           ** PCMTXT **
      *
      ** Prepare the comit text CMTTXT
     C                     MOVE 'RE'      MDID
     C                     MOVEL@PGM      PGMN
     C                     MOVE SWID      WSIDX
     C                     TIME           MTIME
     C                     MOVE @USER     USRIDX
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            UPHED  - Update Header Record                      *
      *                                                               *
      * CALLS      DBERR  - Database error handling                   *
      *                                                               *
      * CALLED BY  UPDRTN - Update control routine                    *
      *                                                               *
      *****************************************************************
     C           UPHED     BEGSR                           ** UPHED  **
      *
      ** Access cheq book header pf/chqbkph for update
     C                     MOVEL*ZEROS    KACNO
     C                     MOVEL*ZEROS    KGCIN
     C                     MOVEL*ZEROS    KSCQN
     C           KCHQBK    CHAINCHQBKPHF             70
     C           *IN70     IFEQ '1'
      *
     C           *LOCK     IN   LDA
     C                     MOVEL'CHQBKPHF'DBFILE           ***************
     C                     Z-ADD1         DBASE            ** DB ERR 01 **
     C                     MOVEL@CHQBK    DBKEY            ***************
     C                     EXSR DBERR
      *
     C                     END
     C           SGCIN     IFNE '1'
      *
     C           @NOCQ     ADD  @SCQN     NCQN
      *
     C                     ELSE
      *
     C           @NOCQ     ADD  @SCQN     NGRN
      *
     C                     END
      *
      ** In case of an overflow of the next cheque/giro no move 1
     C           NCQN      IFEQ *ZEROS
     C                     Z-ADD1         NCQN
     C                     END
      *
     C           NGRN      IFEQ *ZEROS
     C                     Z-ADD1         NGRN
     C                     END
      *
     C                     UPDATCHQBKPHF
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            UPTRL  - Update Control Trailer Record             *
      *                                                               *
      * CALLS      DBERR  - Database error handling                   *
      *                                                               *
      * CALLED BY  UPDRTN - Update control routine                    *
      *                                                               *
      *****************************************************************
     C           UPTRL     BEGSR                           ** UPTRL  **
     C*
     C                     MOVE '0'       *IN70
     C                     MOVE *ALL'9'   @CHQBK
     C           KCHQBK    CHAINCHQBKPTF             70
     C*
     C**  If no record found, *IN70 is ON
     C*
     C           *IN70     IFEQ '1'
     C           *LOCK     IN   LDA                        ***************
     C                     Z-ADD2         DBASE            ** DB ERR 02 **
     C                     MOVEL'CHQBKPTF'DBFILE           ***************
     C                     MOVEL@CHQBK    DBKEY
     C                     EXSR DBERR
      *
     C                     ELSE
      *
     C           SACTN     IFEQ 'I'
     C                     ADD  1         NORE
     C                     END
      *
     C           SACTN     IFEQ 'D'
     C                     SUB  1         NORE
     C                     END
      *
     C                     MOVE BJRDNB    LCD
     C                     MOVEL'A'       CHTP
     C                     Z-ADDNATN      TNLU
     C                     UPDATCHQBKPTF
      *
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            LAST   - Final Processing                          *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  MAIN   - Main Processing                           *
      *                                                               *
      *****************************************************************
     C           LAST      BEGSR                           ** LAST   **
      *
     C                     SETON                     LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            *PSSR  - Program error                             *
      *                                                               *
      * CALLS      DBERRCTL                                           *
      *                                                               *
      * CALLED BY  DBERR  - Database Error Handling                   *
      *                                                               *
      *****************************************************************
     C           *PSSR     BEGSR                           ** *PSSR  **
     C*
     C** Check to see that *PSSR has not been called yet
     C*
     C           WRUN      IFEQ *BLANK
     C                     MOVE 'Y'       WRUN    1
     C                     SETON                     U7U8LR
     C                     DUMP
     C*
     C** Call standard database error program
     C*
     C                     CALL 'DBERRCTL'
     C*
     C                     END
     C*
     C** If *PSSR already run, then just end the program here
     C*
     C                     SETON                     U7U8LR
     C                     RETRN
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            FIRST  - Initial Processing                        *
      *                                                               *
      * CALLS      DBERR  - Database error handling                   *
      *            IFLDS  - Initialize Work/Screen Fields             *
      *            ZA0150 - Get Midas Rundate & Setup Indicator       *
      *                                                               *
      * CALLED BY  MAIN   - Main Processing                           *
      *                                                               *
      *****************************************************************
     C           FIRST     BEGSR                           ** FIRST  **
      *
     C           *LIKE     DEFN TNLU      @TNLU
     C           *LIKE     DEFN TNLU      WTNLU                                               CRE019
     C           *LIKE     DEFN @RRN1     @FPTR1                                             239709A
      *
      * Prepare LDA
     C           *NAMVAR   DEFN           LDA
     C           *LOCK     IN   LDA
     C                     MOVEL*BLANK    DBLOT
     C                     OUT  LDA
      ** Account Numbers Cross Reference
     C           KLACNO    KLIST
     C                     KFLD           KACNO  100
      ** Cheque book file keylist
     C           KCHQBK    KLIST
     C                     KFLD           KACNO
     C                     KFLD           KGCIN
     C                     KFLD           KSCQN
      *                                                                                       CRE019
      ** Partial cheque book file keylist                                                     CRE019
     C           KCQPAR    KLIST                                                              CRE019
     C                     KFLD           KACNO                                               CRE019
     C                     KFLD           KGCIN                                              BUG2965
     C**********           KFLD           KGCIN                                       CRE019 BUG2965
      *
      ** Define program work fields
     C                     MOVE *BLANKS   @MSGID  7
     C                     MOVEL'RTSMSGF' @MSGF  10
      *
      ** Initialize screen message queue
     C                     MOVEL'*'       SPGMQ
      *
      ** Seton  message subfile end indicator
     C                     MOVE '1'       *IN10
      *
     C                     MOVEL@JOB      SWID   10
     C                     MOVEL@USER     SUSER
     C                     MOVE *BLANKS   SMRDT   7
      *
      * Initialise screen/work fields
     C                     EXSR IFLDS
      *
      ** Call subroutine to get Midas Run Date.
     C                     EXSR ZA0150
     C                     MOVELBJMRDT    SMRDT
      *
      ** Access cheq book header pf/chqbkph for next starting cheq no
     C                     MOVEL*ZEROS    KACNO
     C                     MOVEL*ZEROS    KGCIN
     C                     MOVEL*ZEROS    KSCQN
     C********** KCHQBK    CHAINCHQBKPHF             70                   174972
     C           KCHQBK    CHAINCHQBKPHF            N70                   174972
     C           *IN70     IFEQ '1'
      *
     C           *LOCK     IN   LDA
     C                     MOVEL'CHQBKPHF'DBFILE           ***************
     C                     Z-ADD3         DBASE            ** DB ERR 03 **
     C                     MOVEL@CHQBK    DBKEY            ***************
     C                     EXSR DBERR
      *
     C                     ELSE
      *
      ** Initialise start cheque no. as next cheq.no from header
     C                     MOVELNCQN      SSCQN
      *
     C                     END
      *                                                                                       CRE019
      ** Determine if CRE019 is installed                                                     CRE019
      *                                                                                       CRE019
     C                     CALL 'AOSARDR0'                                                    CRE019
     C                     PARM *BLANKS   PRTCD   7                                           CRE019
     C                     PARM '*VERIFY 'POPTN   7                                           CRE019
     C                     PARM 'CRE019'  PSARD   6                                           CRE019
     C           SCSARD    PARM SCSARD    DSFDY                                               CRE019
      *                                                                                       CRE019
     C           PRTCD     IFNE *BLANKS                                                       CRE019
     C           PRTCD     ANDNE'*NRF   '                                                     CRE019
     C           *LOCK     IN   LDA                                                           CRE019
     C                     MOVEL'SCSARDPD'DBFILE                                              CRE019
     C                     MOVEL'CRE019'  DBKEY                                               CRE019
     C                     Z-ADD05        DBASE                                               CRE019
     C                     EXSR DBERR                                                         CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C           PRTCD     IFEQ *BLANKS                                                       CRE019
     C                     MOVE 'Y'       CRE019  1                                           CRE019
     C                     MOVE *ON       *IN21                                               CRE019
     C                     ELSE                                                               CRE019
     C                     MOVE 'N'       CRE019                                              CRE019
     C                     MOVE *OFF      *IN21                                               CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            IFLDS  - Initialize Work/Screen Fields             *
      *                                                               *
      * CALLS             -                                           *
      *                                                               *
      * CALLED BY  FIRST  - Initial Processing                        *
      *            RFRESH - Refresh screen                            *
      *                                                               *
      *****************************************************************
     C           IFLDS     BEGSR                           ** IFLDS  **
      *
     C           CRE019    IFEQ 'N'                                                           CRE019
     C                     MOVE *BLANKS   SACTN
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     MOVE *BLANKS   SRACNO
     C                     MOVE *BLANKS   SGCIN
     C                     MOVE *BLANKS   SSCQN
     C                     MOVE *BLANKS   SECQN
     C                     MOVE *BLANKS   SNOCQ
     C                     MOVE *BLANKS   SISSDT
     C                     MOVE *BLANKS   SCLTDT
     C                     MOVE *BLANKS   @CPDS
     C                     MOVEL*BLANKS   SCCSSN                                              CSF011
     C                     MOVEL*BLANKS   SCREPN                                              CSF011
     C                     MOVEL*BLANKS   SCREPT                                              CSF011
     C                     MOVEL*BLANKS   SCACCN                                              CSF011
      *
     C           CRE019    IFEQ 'Y'                                                           CRE019
     C                     MOVE *BLANKS   SCHAM                                               CRE019
     C                     MOVE *BLANKS   SCHAC                                               CRE019
     C                     MOVE *BLANKS   SCHTT                                               CRE019
     C                     ENDIF                                                              CRE019
      *                                                                                       CRE019
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * ZA0150 - This subroutine chains to file LF/SDBANKL1           *
      *            to get Midas Rundate & setup-complete-flag         *
      *                                                               *
      *            Indicator 70 is set on if the chain fails          *
      *                                                               *
      *            if indicator 70 is on, details of the              *
      *            attempted access is placed in the LDA              *
      *                                                               *
      * CALLS      DBERR  - Database error handling                   *
      *                                                               *
      * CALLED BY  FIRST  - Initial Processing                        *
      *                                                               *
      *****************************************************************
     C           ZA0150    BEGSR                           ** ZA0150 **
     C*
     C                     CALL 'AOBANKR0'
     C                     PARM '*BLANK ' @RTCD   7
     C                     PARM '*FIRST ' @OPTN   7
     C           SDBANK    PARM SDBANK    DSFDY
     C*
     C           @RTCD     IFNE *BLANK
     C           *LOCK     IN   LDA
     C                     MOVEL'SDBANKPD'DBFILE
     C                     MOVEL@OPTN     DBKEY
     C                     Z-ADD04        DBASE
     C                     EXSR DBERR
     C                     END
      *
     C                     MOVELBJSUC     @SCI    1
      *
      ** Set up date format indicator
     C           BJDFIN    IFEQ 'D'
     C                     SETOF                     98
     C                     ELSE
     C                     SETON                     98
     C                     END
      *
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      * SNDMSG  - Sends parameters of SNDPGMMSG command to be executed*
      *                                                               *
      * CALLS     SCC0240 - Send message to program queue             *
      *                                                               *
      * CALLED BY  UPDAD  - Update procedure for amend/delete         *
      *            UPDRTN - Update control routine                    *
      *            VAL01  - Validate key Fields                       *
      *            VAL02  - Validate non-key Fields                   *
      *            VSDCOL - Validate date of collection               *
      *            VSDISS - Validate date of issue                    *
      *            VSECQN - Validate ending cheque no                 *
      *            VSNOCQ - Validate number of cheques                *
      *                                                               *
      * FLDS USED  @MSGID - Message id                                *
      *            @MSGF  - Message file                              *
      *            @MSDTA - Message data                              *
      *            @MSPRL - Program relationship                      *
      *            @MSPGM - Program name                              *
      *            @MSTYP - Message type                              *
      *                                                               *
      *****************************************************************
     C           SNDMSG    BEGSR                           ** SNDMSG **
     C*
     C                     SETON                     69
     C*
     C                     CALL 'SCC0240'
     C                     PARM           @MSGID
     C                     PARM           @MSGF
     C*
     C                     ENDSR
      *****************************************************************
      /EJECT
      *****************************************************************
      *                                                               *
      *            DBERR  - Database error handling                   *
      *                                                               *
      * CALLS      *PSSR  - Program  error                            *
      *                                                               *
      * CALLED BY  FIRST  - Initial Processing                        *
      *            UPHED  - Update Header Record                      *
      *            UPTRL  - Update Control Trailer Record             *
      *            ZA0150 - Get Midas Rundate & Setup Indicator       *
      *                                                               *
      *****************************************************************
     C           DBERR     BEGSR                           ** DBERR  **
      *
     C                     MOVEL@PGM      DBPGM
     C                     OUT  LDA
      *
     C                     EXSR *PSSR
      *
     C                     ENDSR
      *****************************************************************
      *
      /EJECT
      ** SR.ZDATE1 subroutine
      /COPY ZSRSRC,ZDATE1Z2
      *
      /EJECT
      ** SR.ZDATE2 subroutine
      /COPY ZSRSRC,ZDATE2Z2
      *
      /EJECT
      ** SR.ZTNLU1 subroutine
      /COPY ZSRSRC,ZTNLU1Z2
      /EJECT                                                                                  CRE019
      ** SR.ZFRPED Subroutine                                                                 CRE019
      /COPY ZSRSRC,ZFRPEDZ3                                                                   CRE019
      *
      *****************************************************************
      /EJECT
      *****************************************************************
      *
      /EJECT
      ** SR.ZALIGN subroutine
      /COPY ZSRSRC,ZALIGNZ2
      *
      /EJECT
** CPY@   **      OBJECT COPYRIGHT
(c) Misys International Banking Systems Ltd. 2001
**   ZYDY - YEARS IN DAYS CUMULATIVE, FOUR YEAR SPAN
0366073110961461
**   ZMDY - MONTHS IN DAYS CUMULATIVE THROUGH YEAR
000031059090120151181212243273304334365
**   ZMNM - MONTHS SHORT NAMES
JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC
